SUBROUTINE GetOrdInqInfo
********************************************************************************
*   Program name :- GetOrdInqInfo
*   Created:- 11/13/2002
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE CPYLIB CHAR

ERRMSG = ''

* Open files
CALL RBO_OPEN_FILE('ORDER', ORDER, ERRMSG)
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Get properties
CALL RBO_GET_PROPERTY('ID', ID, ERRMSG)
CALL RBO_GET_PROPERTY('CUST_ORD_NUM', CUST.ORDERS, ERRMSG)
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Get CONO
CONO = ID[1,3]

* Set data
LINES = DCOUNT(CUST.ORDERS ,VM)
COIOrderNumber  = ''
COICustPONumber = ''
COIOrderDate    = ''
COIOrderDueDate = ''
COIOrderStatus  = ''
COIOrderAmount  = ''
COIOrderTotInv  = ''
FOR N =1 TO LINES
  MATREAD ORD.REC FROM ORDER, CONO:CUST.ORDERS<1,N> ELSE
    MAT ORD.REC = ""
  END
  COIOrderNumber<1,N>  = CUST.ORDERS<1,N>
  COICustPONumber<1,N> = ORD.PO
  COIOrderDate<1,N>    = OCONV(ORD.DATE,"D2/")
  COIOrderDueDate<1,N> = OCONV(ORD.DUE,"D2/")
  COIOrderStatus<1,N>  = ORD.STATUS
  COIOrderAmount<1,N>  = OCONV(ORD.TOT.AMT,"MD2,")
  COIOrderTotInv<1,N>  = OCONV(ORD.TOT.INV,"MD2,")
NEXT N

   SWAP "," WITH "" IN COIOrderAmount
   SWAP "," WITH "" IN COIOrderTotInv

* Set properties
CALL RBO_SET_PROPERTY('COIOrderNumber', COIOrderNumber, ERRMSG)
CALL RBO_SET_PROPERTY('COICustPONumber', COICustPONumber, ERRMSG)
CALL RBO_SET_PROPERTY('COIOrderDate', COIOrderDate, ERRMSG)
CALL RBO_SET_PROPERTY('COIOrderDueDate', COIOrderDueDate, ERRMSG)
CALL RBO_SET_PROPERTY('COIOrderStatus', COIOrderStatus, ERRMSG)
CALL RBO_SET_PROPERTY('COIOrderAmount', COIOrderAmount, ERRMSG)
CALL RBO_SET_PROPERTY('COIOrderTotInv', COIOrderTotInv, ERRMSG)
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* End of method code
RETURN

