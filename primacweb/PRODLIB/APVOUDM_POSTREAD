   SUBROUTINE APVOUDM_POSTREAD
*********************************************************************
* REVISION    - [12.0]
*
* SYSTEM      - PRIMAC
*
* PROGRAM     - APVOUDM_POSTREAD
*
* BY          - CHRIS MYKLEBUST
*
* DATE        - 11/11/2002
*
* DESCRIPTION - This program is called after a voucher is entered
*               and some error checking takes place.
*********************************************************************
   $INCLUDE WWINSERT RBO.H
   $INCLUDE APS.CPYLIB OAP
   $INCLUDE APS.CPYLIB SQV
   $INCLUDE APS.CPYLIB CKP
   $INCLUDE APS.CPYLIB HDCH
   $INCLUDE PMC.CPYLIB COMPANY
   $INCLUDE PMC.CPYLIB VEND
   $INCLUDE CPYLIB FILE.VARS
   $INCLUDE CPYLIB CHAR
   DIM SVEND.REC(50)
   EQU SVEND.DESC TO SVEND.REC(1)
   EQU SVEND.ADDR1 TO SVEND.REC(2)
   EQU SVEND.CT.ST TO SVEND.REC(4)
   EQU SVEND.ZIP TO SVEND.REC(5)
   EQU SVEND.TERMS TO SVEND.REC(6)
   EQU SVEND.PD.M.T.D TO SVEND.REC(7)
   EQU SVEND.PD.Y.T.D TO SVEND.REC(8)
   EQU SVEND.DATE.PD TO SVEND.REC(10)
   EQU SVEND.PA.AMT TO SVEND.REC(11)
   EQU SVEND.CK.NO.PD TO SVEND.REC(12)
   EQU SVEND.OP.BAL TO SVEND.REC(13)
*
*---- OPEN FILES
*
   IF FILEINFO(COMPANY,0) = 0 THEN
      OPEN 'COMPANY' TO COMPANY ELSE
         ERRMSG = 'CANNOT OPEN COMPANY FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(CONTROL,0) = 0 THEN
      OPEN '','CONTROL' TO CONTROL ELSE
         ERRMSG='CANNOT OPEN CONTROL FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(OAP,0) = 0 THEN
      OPEN '','OAP' TO OAP ELSE
         ERRMSG='Cannot open OAP'
         GOTO 93000
      END
   END
   IF FILEINFO(CKP,0) = 0 THEN
      OPEN 'CKP' TO CKP ELSE
         ERRMSG = 'CANNOT OPEN CKP FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(SQV,0) = 0 THEN
      OPEN 'SQV' TO SQV ELSE
         ERRMSG = 'CANNOT OPEN SQV FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(HDCH,0) = 0 THEN
      OPEN 'HDCH' TO HDCH ELSE
         ERRMSG = 'CANNOT OPEN HDCH FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(VEND,0) = 0 THEN
      OPEN 'VEND' TO VEND ELSE
         ERRMSG = 'CANNOT OPEN VEND FILE'
         GOTO 93000
      END
   END
*
*---- INITIALIZATION
*
   STATUS = RBO.getProperty('','ID',ID)
   CONO = ID[1,3]
   V.CODE = ID[4,99]
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG = "COMPANY RECORD IS MISSING FOR COMPANY ":CONO
      GOTO 93000
   END
*
*---- MAIN PROCESS
*
   MAT HDCK.REC = ''
   MAT SQV.REC = ''
   HFILE.FLG = 1
   OFILE.FLG = 1
   NEW.DISB.ACCT = ''
   NEW.CHK.NO = ''
   NEW.PAID.DATE = ''
   PP.FLG = 0
   PAY.FLG = 0
   OAP.INV.FLG = ''
   SQV.FLG = ''
    MATREAD OAP.REC FROM OAP, ID ELSE OFILE.FLG = 0 ; MAT OAP.REC = ""
*   MATREADU OAP.REC FROM OAP, ID LOCKED
*     ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
*     GOTO 93000
*   END ELSE
*     OFILE.FLG = 0 ; MAT OAP.REC = ""
*   END
    MATREAD HDCK.REC FROM HDCH, ID ELSE HFILE.FLG = 0
*   MATREADU HDCK.REC FROM HDCH, ID LOCKED
*      ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
*      GOTO 93000
*   END ELSE
*      HFILE.FLG = 0
*   END
   IF HFILE.FLG THEN PAY.FLG = 1
   BEGIN CASE
      CASE OFILE.FLG = 1 AND HFILE.FLG = 1
         ERRMSG = "Must run Handcheck Register first."
         GOTO 93000
      CASE OFILE.FLG = 0 AND HFILE.FLG = 0
         ERRMSG = "Voucher # (":V.CODE:") not on file."
         GOTO 93000
      CASE OFILE.FLG = 0 AND HFILE.FLG = 1
         FOR VV = 1 TO 24
            IF VV # 17 AND VV # 18 AND VV # 19 THEN
               OAP.REC(VV) = HDCK.REC(VV)
            END
         NEXT VV
         OAP.MON = HDCK.MON
      CASE 1
   END CASE
   MATREAD CKP.REC FROM CKP, ID THEN
      ERRMSG = "This voucher is selected to be paid.  Cannot change."
      GOTO 93000
   END
   IF OAP.DUE.DATE = "ADVANCE" THEN
*     MATREADU OAP.REC FROM HDCH, ID ELSE
      MATREAD OAP.REC FROM HDCH, ID ELSE
         ERRMSG = "VOUCHER # (":V.CODE:") not on file."
         GOTO 93000
      END
   END
   IF PAY.FLG = 0 THEN
      MATREAD SQV.REC FROM SQV, ID ELSE SQV.FLG = 1
*      MATREADU SQV.REC FROM SQV, ID LOCKED
*         ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
*      END ELSE
*         SQV.FLG = 1
*      END
   END
   MATREAD VEND.REC FROM VEND, CONO:OAP.VEND<1,1> ELSE
      MAT VEND.REC = "??????????"
   END
   IF DCOUNT(OAP.VEND<1>,VM) # 1 THEN
      VEND.DESC = OAP.VEND<1,2>
      VEND.ADDR1 = OAP.VEND<1,3>
      VEND.CT.ST = OAP.VEND<1,4>
      VEND.ZIP = OAP.VEND<1,5>
   END
   IF OAP.VEND<1,1>[6,1] = "-" THEN
      MATREAD SVEND.REC FROM VEND, CONO:OAP.VEND<1,1>[1,5] ELSE
         MAT SVEND.REC = "??????????"
      END
      SVEND.ID = OAP.VEND<1,1>[1,5]
      SVEND.CT.ST.ZIP = SVEND.CT.ST[1,19]:' ':SVEND.ZIP
   END ELSE
      SVEND.ID = ""
   END
   *** REMAINING AMOUNT DUE ***
   REM.AMT = OAP.GRS.AMT - OAP.DSC.AMT - OAP.PAID.AMT - OAP.DSC.PAID
   IF REM.AMT < 0 AND OAP.GRS.AMT > 0 THEN REM.AMT = 0
   IF REM.AMT > 0 AND OAP.GRS.AMT < 0 THEN REM.AMT = 0
   REM.AMT = OCONV(REM.AMT,'MD2')
   *** NET AMOUNT ***
   NET.AMT = OAP.GRS.AMT - OAP.DSC.AMT
   NET.AMT = OCONV(NET.AMT,'MD2')
   *** PAYMENT INFORMATION FIELDS ***
   NEW.GRS = OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.PAID
   NEW.DSC = OAP.DSC.AMT - OAP.DSC.PAID
   IF NEW.DSC < 0 AND OAP.DSC.AMT >= 0 THEN NEW.DSC = 0
   IF NEW.DSC > 0 AND OAP.DSC.AMT <= 0 THEN NEW.DSC = 0
   NEW.NET = NEW.GRS - NEW.DSC
   NEW.GRS = OCONV(NEW.GRS,'MD2')
   NEW.DSC = OCONV(NEW.DSC,'MD2')
   NEW.NET = OCONV(NEW.NET,'MD2')
*
*---- SET RBO FIELDS
*
   STATUS = RBO.setProperty('','VEND_DESC',VEND.DESC)
   STATUS = RBO.setProperty('','VEND_ADDR1',VEND.ADDR1)
   STATUS = RBO.setProperty('','VEND_CT_ST',VEND.CT.ST'L#19')
   STATUS = RBO.setProperty('','VEND_ZIP',VEND.ZIP)
   IF SVEND.ID # "" THEN
      STATUS = RBO.setProperty('','SVEND_ID',SVEND.ID)
      STATUS = RBO.setProperty('','SVEND_DESC',SVEND.DESC)
      STATUS = RBO.setProperty('','SVEND_ADDR1',SVEND.ADDR1)
      STATUS = RBO.setProperty('','SVEND_CT_ST_ZIP',SVEND.CT.ST.ZIP)
   END
   STATUS = RBO.setProperty('','REM_AMT',REM.AMT)
   STATUS = RBO.setProperty('','OAP_NET_AMT',NET.AMT)
   STATUS = RBO.setProperty('','NEW_GRS',NEW.GRS)
   STATUS = RBO.setProperty('','NEW_DSC',NEW.DSC)
   STATUS = RBO.setProperty('','NEW_NET',NEW.NET)
   STATUS = RBO.setProperty('','OAP_TERMS_NET',OAP.TERMS<1,2>)
   STATUS = RBO.setProperty('','OAP_TERMS_PER',OCONV(OAP.TERMS<1,1>[1,4],'MD2'))
   STATUS = RBO.setProperty('','OAP_TERMS_DAYS',OAP.TERMS<1,1>[5,2])
   RETURN
93000 *
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
   RETURN










