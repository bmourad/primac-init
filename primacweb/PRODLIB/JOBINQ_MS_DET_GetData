SUBROUTINE JOBINQ_MS_DET_GetData
********************************************************************************
*   Program name :- JOBINQ_MS_DET_GetData
*   Created:- 12/17/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'MS Detail' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.MISC
$INCLUDE JCS.CPYLIB OPERATION
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(JOB.MISC,0)=0 THEN
   OPEN '','JOB.MISC' TO JOB.MISC ELSE
      ERRMSG<1,-1>='JOB.MISC FILE IS MISSING'
   END
END
IF FILEINFO(OPERATION,0)=0 THEN
   OPEN '','OPERATION' TO OPERATION ELSE
      ERRMSG<1,-1>='OPERATION FILE IS MISSING'
   END
END
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
  DIM TRANS.REC(10)
  EQU TRAN.NO       TO TRANS.REC(1)
  EQU TRAN.DATE     TO TRANS.REC(2)
  EQU TRAN.REASON   TO TRANS.REC(3)
  EQU TRAN.COST     TO TRANS.REC(4)
  EQU TRAN.SALE     TO TRANS.REC(5)
  EQU TRAN.TYPE     TO TRANS.REC(6)
  EQU TRAN.JOB      TO TRANS.REC(7)
  EQU TRAN.OPER     TO TRANS.REC(8)
  EQU TRAN.OPER.DES TO TRANS.REC(9)
  EQU TRAN.PERIOD   TO TRANS.REC(10)
MAT TRANS.REC = ''
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)
STATUS=RBO.getProperty('','DEPT',DEPT)
STATUS=RBO.getProperty('','CCTR',CCTR)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='MS'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


   SUB.CNT=DCOUNT(JOB.SUBS,VM)+1
END ELSE SUB.CNT = 1
PTR=1
LOOP
   LOCATE DEPT IN JOB.MS.DEPT<1>,PTR SETTING FND ELSE
      PTR = 0
   END
   IF PTR # 0 THEN
      IF CCTR = JOB.MS.CCTR<1,FND> THEN
         PTR = FND
         FND = 0
      END ELSE
         PTR = FND + 1
      END
   END
WHILE PTR AND FND DO
REPEAT
IF NOT(PTR) THEN
   ERRMSG='Cannot locate Dept/Cctr ':DEPT:' ':CCTR:' in Job record!'
   GOTO 93000
END
MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT ELSE DEPT.DESC = UNKNOWN
MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR ELSE CCTR.DESC = UNKNOWN

  LN = 1; OLN = 1; LINES = JOB.MS.SEQ<1,PTR,1>
  MS.ID = CONO:JOB_NO:"!":JOB.MS.DEPT<1,PTR>:"!":JOB.MS.CCTR<1,PTR>:"!"
  GOSUB 100
  FOR K = 2 TO SUB.CNT
    MS.ID = CONO:JOB.SUBS<1,K-1>:"!":JOB.MS.DEPT<1,PTR>:"!":JOB.MS.CCTR<1,PTR>:"!"
    OLN = LINES + 1; LINES = LINES + JOB.MS.SEQ<1,PTR,K>
    GOSUB 100
  NEXT K

STATUS=RBO.setProperty('','DEPT_DESC',DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_DESC',CCTR.DESC)
STATUS=RBO.setProperty('','OPER',TRAN.OPER)
STATUS=RBO.setProperty('','MIS_TRAN',TRAN.NO)
STATUS=RBO.setProperty('','DATE',TRAN.DATE)
STATUS=RBO.setProperty('','PERIOD',TRAN.PERIOD)
STATUS=RBO.setProperty('','REASON',TRAN.REASON)
STATUS=RBO.setProperty('','TYPE',TRAN.TYPE)
STATUS=RBO.setProperty('','RCLS_JOB',TRAN.JOB)
STATUS=RBO.setProperty('','COST',TRAN.COST)
STATUS=RBO.setProperty('','SALE',TRAN.SALE)
STATUS=RBO.setProperty('','OPER_NAME',TRAN.OPER.DES)
GOTO 99999

100*
  FOR I = OLN TO LINES
    MATREAD JMS.REC FROM JOB.MISC, MS.ID:(I-OLN+1) ELSE MAT JMS.REC = ""
    MATREAD OPER.REC FROM OPERATION, CONO:JMS.OPER ELSE
      MATREAD INV.REC FROM INVENTORY, CONO:JMS.OPER ELSE INV.FULL.DESC = UNKNOWN
      OPER.DESC = INV.FULL.DESC
    END
    TRAN.NO<1,I> = JMS.SEQ
    TRAN.DATE<1,I> = OCONV(JMS.DATE,'D2/')
    TRAN.REASON<1,I> = JMS.REASON
    TRAN.COST<1,I> = OCONV(JMS.COST,'MD2,')
    TRAN.SALE<1,I> = OCONV(JMS.SALE,'MD2,')
    TRAN.TYPE<1,I> = JMS.RC.TYPE
    TRAN.JOB<1,I> = JMS.RC.JOB
    TRAN.OPER<1,I> = JMS.OPER
    TRAN.OPER.DES<1,I> = OPER.DESC
    TRAN.PERIOD<1,I> = JMS.MON<1,1>
  NEXT I
  RETURN

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN
