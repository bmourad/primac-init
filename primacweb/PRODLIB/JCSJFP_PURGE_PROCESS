SUBROUTINE JCSJFP_PURGE_PROCESS
********************************************************************************
*   Program name :- JCSJFP_PURGE_PROCESS
*   Created:- 8/18/2006
*   Programmer :- S.Suhail Hussain
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB SECURITY
$INCLUDE PMC.CPYLIB PMC_REPORTS
$INCLUDE CPYLIB CHAR

   OPEN 'COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING' ; GOTO 93000
   OPEN 'SECURITY' TO SECURITY ELSE ERRMSG = 'SECURITY FILE IS MISSING' ; GOTO 93000
   OPEN 'PMC_REPORTS' TO PMC_REPORTS ELSE ERRMSG = 'PMC_REPORTS FILE IS MISSING' ; GOTO 93000
   OPEN 'WIN.PRINTERS' TO WIN.PRINTERS ELSE ERRMSG = 'WIN.PRINTERS FILE IS MISSING' ; GOTO 93000
   OPEN 'CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING' ; GOTO 93000

   STATUS = RBO.getProperty("","PMCProperty",PMCProperty)
   STATUS = RBO.getProperty("","ALL_VALUES",INBUFFER)

   CONO        = PMCProperty<1,4>
   USER_ID     = PMCProperty<1,3>
   DIV_CODE    = INBUFFER<1,1>   ;* JC620
   NO_DATE     = INBUFFER<1,2>   ;* JC620
   DIV_NUM     = INBUFFER<1,3>   ;* JC520
   YNFLG       = INBUFFER<1,4>   ;* JC621
   BCKUP_UNIT  = INBUFFER<1,5>   ;* JC622
   CONT_FLG    = INBUFFER<1,6>   ;* JC623
   SCRN_FLG    = INBUFFER<1,7>   ;* CURRENT SCREEN NUMBER
   HOLD_PORT   = INBUFFER<1,8>   ;* PORT NUMBER USED. FORMAT OF PORT NUMBER IS : (DATE() + TIME()) + PORT.NUM. 
                                 ;* THIS IS GENERATED ONLY THE FIRST TIME WHEN THIS METHOD IS CALLED.
   SLSTNAME = ''   
   IF HOLD_PORT # "" THEN
      SLSTNAME<1,1> = "JOB.SUM":HOLD_PORT
      SLSTNAME<1,2> = "JPURGE.RPT":HOLD_PORT
   END

   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG = "Cannot read Company rec from COMPANY File!"
      GOTO 93000
   END
   READ COMPANY.SECURITY FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = 'Cannot Read Div.Security rec from CONTROL File!':CONO
      GOTO 93000
   END
   MATREAD SEC.REC FROM SECURITY, CONO:UPCASE(USER_ID) ELSE
      ERRMSG = "USER ":USER_ID:" NOT ON FILE"
      GOTO 93000
   END
   GOSUB PRE_PROCESS

MAIN_PROCESS:
   BEGIN CASE
      CASE SCRN_FLG = "JC520"
         GOTO JC520_LBL
      CASE SCRN_FLG = "JC621"
         SCRN_FLG = "JC520"
         GOTO JC621_LBL
      CASE SCRN_FLG = "JC622"
         GOTO JC622_LBL
      CASE SCRN_FLG = "JC623"
         GOTO JC623_LBL
   END CASE

   CMDERR = ''
   HOLD_PORT = ''
   PORT.NO = 'TTY'
   CALL SYSVARS.SUB(PORT.NO)
   HOLD_PORT = "_"  : DATE()+TIME() :"_":PORT.NO

   SLSTNAME<1,1> = "JOB.SUM":HOLD_PORT
   CMD = "SELECT JOB WITH JOB.STATUS = "7" AND WITH CONO = " : CONO
   IF DIV_CODE # "ALL" THEN CMD := " AND WITH JOB.DIV = " : DIV_CODE
   UDTEXECUTE CMD CAPTURING MSG
   CMD = "SAVE-LIST " : SLSTNAME<1,1>
   UDTEXECUTE CMD CAPTURING MSG
   CMD = "GET-LIST " : SLSTNAME<1,1>
   BUFFER = ""
   BUFFER<1> = CONO
   BUFFER<2> = ""
   BUFFER<3> = DIV_CODE
   BUFFER<4> = NO_DATE
   CALL JCSJFP_JOB_PURGE(BUFFER,ERR)

   IF BUFFER<1> = "END" THEN
      IF ERR = 1 THEN
         GOTO 93000
      END ELSE
         GOTO 999
      END
   END
   DISPMSG<-1> = "*** Creating all purge files. ***"

*v create purge files
   CMD = "CREATE-FILE PURGE.JOB":HOLD_PORT:" 1,1 307,1"
   GOSUB EXEC_CMD

   CMD = "CREATE-FILE PURGE.JAI":HOLD_PORT:" 1,1 307,1"
   GOSUB EXEC_CMD

   CMD = "CREATE-FILE PURGE.GANG":HOLD_PORT:" 1,1 101,1"
   GOSUB EXEC_CMD
*^
   SCRN_FLG = "JC520"

JC520_LBL:
   IF DIV_NUM = "" THEN GOTO 91000
   CMD = "SELECT JOB WITH JOB.STATUS = '8' AND WITH CONO = " : CONO
   IF DIV_NUM # "ALL" THEN CMD := " AND WITH JOB.DIV = " : DIV_NUM
   GOSUB EXEC_CMD

   CMD = "SAVE-LIST " : SLSTNAME<1,2>
   GOSUB EXEC_CMD

JC621_LBL:
   MATREAD PRPT.REC FROM PMC_REPORTS, SCRN_FLG ELSE MAT PRPT.REC = ""
   GOSUB BUILD_REPORT_HEAD
40*
   CMD = "GET-LIST " : SLSTNAME<1,2>
   GOSUB EXEC_CMD

   SCRN_FLG = "JC621"
   IF YNFLG = "" OR YNFLG = "Y" THEN
      CMD = 'SORT JOB ID-SUPP JOB.NO JOB.DESC JOB.CUST CUST.NAME HEADING "' : BUFFER<2> : "'L'" : '" LPTR'
      GOSUB EXEC_CMD
      GOTO 91000
   END

   CMD = "DELETE-LIST " : SLSTNAME<1,2>
   GOSUB EXEC_CMD

   READV DIV_ID FROM CONTROL,CONO:"JOB.PURGE",1 ELSE
      ERRMSG = "CAN'T READ CONTROL"
      GOTO 93000
   END

   CMD = "SELECT JOB WITH JOB.STATUS = '8' AND WITH CONO = " : CONO
   IF DIV_ID # "ALL" THEN CMD := " AND WITH JOB.DIV = " : DIV_ID
   GOSUB EXEC_CMD
122*
   CMD = "SAVE-LIST " : "P.JOB":HOLD_PORT
   GOSUB EXEC_CMD

   CMD = 'GET-LIST ' : "P.JOB":HOLD_PORT
   GOSUB EXEC_CMD

   CMD = 'COPY FROM JOB TO ' : "PURGE.JOB" : HOLD_PORT
   GOSUB EXEC_CMD

   DISPMSG<1,-1> = "*** Selecting and Copying (JOB.ADD.INFO) file. ***"
   CMD = 'SELECT JOB.ADD.INFO WITH JOB.STATUS = "8" AND WITH CONO = ' : CONO
   IF DIV_ID # "ALL" THEN CMD := " AND WITH JOB.DIV = " : DIV_ID
   GOSUB EXEC_CMD

   CMD = "SAVE-LIST " : "P.JAI" : HOLD_PORT
   GOSUB EXEC_CMD

   CMD = "GET-LIST " : "P.JAI" : HOLD_PORT
   GOSUB EXEC_CMD

   CMD = "COPY FROM JOB.ADD.INFO TO " : "PURGE.JAI" : HOLD_PORT
   GOSUB EXEC_CMD

   DISPMSG<1,-1> = "*** Copying (GANG.JOB) file. ***"

   CMD = 'GET-LIST ' : "P.JOB" : HOLD_PORT
   GOSUB EXEC_CMD

   CMD = "COPY FROM GANG.JOB TO PURGE.GANG" : HOLD_PORT
   GOSUB EXEC_CMD
50 *
*** Give the User a Chance to Quit Here. ***
   SCRN_FLG = "JC622"
JC622_LBL:
   IF BCKUP_UNIT = "" THEN GOTO 91000
   BUFFER<3> = "0" : BCKUP_UNIT
   GOSUB TAPE_ATT
   IF BUFFER<3> = "END" THEN BCKUP_UNIT = "";GOTO 50
   SCRN_FLG = "JC623"
JC623_LBL:
   IF CONT_FLG = "" THEN GOTO 91000
   IF CONT_FLG = "N" THEN GOTO 999
   BUFFER<7> = "PURGE.JOB" : HOLD_PORT
   GOSUB TAPE_DUMP
   IF BUFFER<7> = "END" THEN BCKUP_UNIT = "";CONT_FLG = "";GOTO 50
   BUFFER<7> = "PURGE.JAI" : HOLD_PORT
   GOSUB TAPE_DUMP
   IF BUFFER<7> = "END" THEN BCKUP_UNIT = "";CONT_FLG = "";GOTO 50
   BUFFER<7> = "PURGE.GANG" : HOLD_PORT
   GOSUB TAPE_DUMP
   IF BUFFER<7> = "END" THEN BCKUP_UNIT = "";CONT_FLG = "";GOTO 50

   CMD = "T.REW " : BUFFER<3>
   GOSUB EXEC_CMD
   CMD = "T.DET " : BUFFER<3>
   GOSUB EXEC_CMD

*** START PURGING FILES ***
   BUFFER<1> = "JOB"
   CMD = "GET-LIST " : "P.JOB":HOLD_PORT
   GOSUB EXEC_CMD
   GOSUB PURGE_DEL

   BUFFER<1> = "JOB.ADD.INFO"
   CMD = "GET-LIST " : "P.JAI":HOLD_PORT
   GOSUB EXEC_CMD
   GOSUB PURGE_DEL

   BUFFER<1> = "GANG.JOB"
   CMD = "GET-LIST " : "P.JOB":HOLD_PORT
   GOSUB EXEC_CMD
   GOSUB PURGE_DEL

   CMD = "DELETE-LIST " : "P.JOB":HOLD_PORT
   GOSUB EXEC_CMD
   CMD = "DELETE-LIST " : "P.JAI":HOLD_PORT
   GOSUB EXEC_CMD
   CMD = "DELETE-FILE " : "PURGE.JOB" : HOLD_PORT : " FORCE"
   GOSUB EXEC_CMD
   CMD = "DELETE-FILE " : "PURGE.JAI" : HOLD_PORT : " FORCE"
   GOSUB EXEC_CMD
   CMD = "DELETE-FILE " : "PURGE.GANG" : HOLD_PORT : " FORCE"
   GOSUB EXEC_CMD

999
RETURN

****SUBROUTINES****
EXEC_CMD:
   UDTEXECUTE CMD CAPTURING MSG
   CMDERR<1,-1> = MSG
   CMD = ''
RETURN

BUILD_REPORT_HEAD:
*
   IH_HEADINGS = ""
   TEMP_HEAD = PRPT.HEADING
   IF INDEX(TEMP_HEAD,"^",1) > 0 THEN
      TEMP_HEAD = CHANGE(TEMP_HEAD,"^",AM)
      IH_COUNT = DCOUNT(TEMP_HEAD,AM)
      FOR II = IH_COUNT TO 1 STEP - 1
         IF REM(II,2) = 0 THEN
            TEMP_HEAD<II> = IH_HEADINGS<INT(II/2)>
         END
         IF TEMP_HEAD<II> = "" THEN
            TEMP_HEAD = DELETE(TEMP_HEAD,II,0,0)
         END
      NEXT II
      TEMP_HEAD = CHANGE(TEMP_HEAD,AM,"")
   END
   PTR = INDEX(TEMP_HEAD,"(",1)
   IF PTR = 0 THEN
      TEMP_HEAD = TRIM(TEMP_HEAD)
      REPORT.SUFFIX = ""
   END ELSE
      REPORT.SUFFIX = TRIM(TEMP_HEAD[PTR,99])
      TEMP_HEAD = TRIM(TEMP_HEAD[1,PTR-1])
   END
   IHL = LEN(TEMP_HEAD)
   SAVE_TEMP_HEAD = ""
   FOR P = 1 TO IHL
      SAVE_TEMP_HEAD = SAVE_TEMP_HEAD:" ":TEMP_HEAD[P,1]
   NEXT P
   IF LEN(SAVE_TEMP_HEAD) < 81 THEN
      TEMP_HEAD = SAVE_TEMP_HEAD[2,80]
   END
   HLINE1 = "RUN D - T: ":OCONV(DATE(), "D2/"):" - ":OCONV(TIME(), "MTS")
   HLINE1 = HLINE1 "L#33"
   HLINE2 = SPACE(INT((85-LEN(CO.NAME))/2)):CO.NAME
   HLINE2 = HLINE2 "L#85"
   HLINE3 = SPACE(3):"PAGE 'PL'"
   HLINE  = HLINE1:HLINE2:HLINE3
	HLINE4 = "RPT #-BY : ": SCRN_FLG : "-" : USER_ID
   HLINE4 = HLINE4 "L#33"
   HLINE5 = SPACE(INT((85-LEN(TEMP_HEAD))/2)):TEMP_HEAD
   HLINE5 = HLINE5:"   ":REPORT.SUFFIX
   HLINEA = HLINE4:HLINE5
   BUFFER<2> = HLINE:HLINEA
RETURN

PURGE_DEL:
   FILE.NAME = BUFFER<1>
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      FOUND = 1
      READU TEMP.REC FROM FILE.NAME, KEY ELSE
         TEMP.REC = ""
         FOUND = 0
      END
      IF FOUND THEN
         DELETE FILE.NAME , KEY
      END ELSE
         RELEASE FILE.NAME , KEY
      END
   REPEAT
RETURN

TAPE_ATT:
   UNIT = BUFFER<3>
   UDTEXECUTE  "T.ATT ":UNIT:" BLKSIZE 8192" CAPTURING ERRMSSG
   FINDSTR "failed" IN ERRMSSG,1 SETTING FLG THEN
      BUFFER<3> = "END"
   END
   FINDSTR "error" IN ERRMSSG,1 SETTING FLG THEN
      BUFFER<3> = "END"
   END
   UDTEXECUTE "T.REW ":UNIT CAPTURING ERRMSSG
   FINDSTR "error" IN ERRMSSG,1 SETTING FLG THEN
      BUFFER<3> = "END"
   END
   WRITEV BUFFER<3> ON CONTROL,BUFFER<1>:'JOB.PURGE.TAPE',1
   UNIT = ""
RETURN

TAPE_DUMP:
   READV UNIT FROM CONTROL, BUFFER<1>:'JOB.PURGE.TAPE', 1 ELSE
      BUFFER<3> = 'END'
      GOTO 10
   END
   BUFFER<3> = UNIT
   FILE = BUFFER<7>
   UDTEXECUTE "T.DUMP ":FILE:" MU ":UNIT CAPTURING ERRMSSG
   FINDSTR "dumped" IN ERRMSSG,1 SETTING FLG THEN
      BUFFER<7> = "OK"
   END
   DISPMSG<1,-1> = BUFFER<7>
   DISPMSG<1,-1> = ERRMSSG
10*
RETURN

PRE_PROCESS:
   SETPTR_QUEUE = SEC.DFLT.QUEUE
   SETPTR_FORMATSTR = ''
   OS_TYPE = UPCASE(SYSTEM(33))
   IF OS_TYPE[1,7] = "WINDOWS" THEN
      READ WIN.PRINTERS.REC FROM WIN.PRINTERS,SETPTR_QUEUE THEN
         SETPTR_FORMATSTR = "Orientation=":WIN.PRINTERS.REC<2>:","
         SETPTR_FORMATSTR := "Font=":WIN.PRINTERS.REC<3>:","
         SETPTR_FORMATSTR := "FontSize=":WIN.PRINTERS.REC<4>:","
         SETPTR_FORMATSTR := "TopMargin=":WIN.PRINTERS.REC<5>:","
         SETPTR_FORMATSTR := "BottomMargin=":WIN.PRINTERS.REC<6>
         SETPTR_PRINT = 'SETPTR 0,132,,,,1,AT '
         SETPTR_PRINT := WIN.PRINTERS.REC<1>
         SETPTR_PRINT := ',"':SETPTR_FORMATSTR:'",NHEAD,BRIEF'
      END
   END
   IF OS_TYPE[1,7] = "UNIX" THEN
      SETPTR_PRINT = 'SETPTR ,,,,,,NHEAD,BRIEF,DEST '
      SETPTR_PRINT := SETPTR_QUEUE:',NOMESSAGE'
   END
   UDTEXECUTE SETPTR_PRINT CAPTURING MSG
RETURN

91000*
   OUTBUFFER<1,1> = SCRN_FLG
   OUTBUFFER<1,2> = HOLD_PORT
   STATUS = RBO.setProperty("","ServerMessage",CMDERR)
   STATUS = RBO.setProperty("","BUF_VALS",OUTBUFFER)
RETURN

93000*
   STATUS = RBO.setProperty("","ServerStatus",1)
   STATUS = RBO.setProperty("","ServerMessage",ERRMSG)
RETURN

