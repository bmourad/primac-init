*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - APD.EOM.CLOSE
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
* DESCRIPTION -
*MODIFIED	- (01/30/06) BY ZAHOOR AHMED
*		   FOR printflag and EOD.ERROR flag
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB SALESDATES
$INCLUDE PMC.CPYLIB FISCAL
$INCLUDE PMC.CPYLIB EOM.TRANS
$INCLUDE PMC.CPYLIB EOM.TRANS.HIST              ; * T25975
$INCLUDE PMC.CPYLIB VEND
$INCLUDE APS.CPYLIB OAP
$INCLUDE APS.CPYLIB MCD
$INCLUDE GLS.CPYLIB GLA
$INCLUDE PMC.CPYLIB COA
$INCLUDE GLS.CPYLIB COA.SYS
$INCLUDE PMC.CPYLIB POST.REJECTS
$INCLUDE CPYLIB CHAR
*
***** SETUP ERRMSG ROUTINE
*
ERRMSG=''
REJECT.RPT.PRT=''
*
* ERROR.FLAG is for any error from EOD_CLOSE_PreProcess & Printfalg is view/mail/print/file
ERROR.FLAG = ''
Printfalg = ''
EMAIL_ADDR = ''
*
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','PMCProperty',PMCProperty)
STATUS = RBO.getProperty('','PRINT_FLAG',PRINT.FLAG)
STATUS = RBO.getProperty('','ERR_FLAG',ERROR.FLAG)
STATUS = RBO.getProperty('','EmailAddress',EMAIL_ADDR)
CONO = ID[1,3]
STATUS = RBO.getProperty('','DM_Flag',D.M.FLG)
IF D.M.FLG = "PERIOD" THEN
   RPT.PER = "PERIOD"
END ELSE
   RPT.PER = "DAY"
END
STATUS = RBO.getProperty('','FR_CURR_PER',FR.CURR.PER)
STATUS = RBO.getProperty('','FR_CURR_DATE',FR.CURR.DATE)
STATUS = RBO.getProperty('','FR_NEXT_PER',FR.NEXT.PER)
STATUS = RBO.getProperty('','FR_NEXT_DATE',FR.NEXT.DATE)
STATUS = RBO.getProperty('','FR_PRINT',FR.PRINT)
STATUS = RBO.getProperty('','FR_CLOSE_DATE',FR.CLOSE.DATE)
STATUS = RBO.getProperty('','FR_D_M_FLG',FR.D.M.FLG)
STATUS = RBO.getProperty('','DivPos',POS)
FOR P = 1 TO DCOUNT(FR.CURR.PER,VM)
   IF INDEX(FR.CURR.DATE<1,P>,'/',1) > 0 THEN FR.CURR.DATE<1,P> = ICONV(FR.CURR.DATE<1,P>,'D2/')
   IF INDEX(FR.NEXT.DATE<1,P>,'/',1) > 0 THEN FR.NEXT.DATE<1,P> = ICONV(FR.NEXT.DATE<1,P>,'D2/')
   IF INDEX(FR.CLOSE.DATE<1,P>,'/',1) > 0 THEN FR.CLOSE.DATE<1,P> = ICONV(FR.CLOSE.DATE<1,P>,'D2/')
NEXT P
STATUS = RBO.getProperty('','DivCode',DIV.CODE)
IF DIV.CODE = '' THEN DIV.CODE = 'ALL'
*
*Get the printflag view/Email/File/Print
*
BEGIN CASE
   CASE PRINT.FLAG = 'PrintData'
	Printfalg = 'View'
   CASE PRINT.FLAG = 'EmailData'
	Printfalg = 'Email'
   CASE PRINT.FLAG = 'File'
	Printfalg = 'Print'
   CASE 1
	Printfalg = 'Print'
END CASE
*
**** INTITIALIZATION
*
TODAY = DATE()
UNKNOWN = "NOT ON FILE"
SYS.FISCAL = "APDFISCAL"
PROG.ID = @TTY:"!":CONO:"!APD.EOM"
NORETURN=0
*
***** OPEN FILES
*
TRANFILE='APD.EOM.TRANS':CONO:DIV.CODE
REJECTFILE='APD.POST.REJECTS':CONO:DIV.CODE
OPEN "","CONTROL" TO CONTROL ELSE
   ERRMSG="CONTROL FILE MISSING"
   GOTO 93000
END
OPEN "",TRANFILE TO EOM.TRANS ELSE
   ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD"
   GOTO 93000
END
OPEN "",REJECTFILE TO POST.REJECTS ELSE
   ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD"
   GOTO 93000
END
OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
OPEN "","SECURITY" TO SECURITY ELSE ERRMSG="SECURITY FILE MISSING";GOTO 93000
OPEN "","VEND" TO VEND ELSE ERRMSG="VEND FILE IS MISSING";GOTO 93000
OPEN "","MCD" TO MCD ELSE ERRMSG="MCD FILE IS MISSING";GOTO 93000
OPEN "","MCD.TAG" TO MCD.TAG ELSE ERRMSG="MCD.TAG FILE IS MISSING";GOTO 93000
OPEN "","OAP" TO OAP ELSE ERRMSG="OAP FILE IS MISSING";GOTO 93000
OPEN "","EOD.HIST" TO EOD.HIST ELSE ERRMSG="EOD.HIST FILE IS MISSING";GOTO 93000
OPEN "","APD.EOM.TRANS.HIST" TO APD.EOM.TRANS.HIST ELSE ERRMSG="APD.EOM.TRANS.HIST FILE IS MISSING";GOTO 93000
*
*If ERROR.FLAG = 1 from EOD_CLOSE_PreProcess then call Generatereport_Sub
*
IF ERROR.FLAG = 1 THEN
 GOTO 90000
END
*
*
MATREAD COMP.REC FROM COMPANY,CONO ELSE
   ERRMSG = "CANNOT LOCATE COMPANY # ":CONO; GOTO 93000
END
IN.ACCT.LEN = LEN(CO.ACCT.PIC)
ACCT.LEN = IN.ACCT.LEN + 10
IF CO.GLS # "N" THEN
   IF CO.GL.LINK<1,2> = "Y" THEN
      OPEN "","GLA" TO GLA ELSE ERRMSG="GLA FILE IS MISSING";GOTO 93000
   END
   OPEN "","COA" TO COA ELSE ERRMSG="COA FILE IS MISSING";GOTO 93000
   EQV.FLG = 1
   OPEN "","COA.EQUIV" TO COA.EQUIV ELSE EQV.FLG = 0
END
CLEARFILE POST.REJECTS
PRR.SEQ = 10000
READ MCD.PURGE.PDS FROM CONTROL, CONO:"MCD.PURGE.PERIODS" ELSE MCD.PURGE.PDS = 1
READ NUM.PERIODS FROM CONTROL, CONO:"ACCT.PERIODS" ELSE NUM.PERIODS = 12
READ FISCALMON FROM CONTROL, CONO:'FISCAL' ELSE
   RELEASE CONTROL, CONO:SYS.FISCAL
   ERRMSG = 'CANNOT LOCATE CONTROL, FISCAL'
   GOTO 93000
END
READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
  SECURITY.REC = 'N'; SECURITY.REC<2> = 'N'
END
GLPER = FISCALMON<1>
GLYR = GLPER[1,4]
GLPD = GLPER[5,2]
IF (GLPD - MCD.PURGE.PDS) < 1 THEN
   GLPD = GLPD - MCD.PURGE.PDS + NUM.PERIODS
   GLYR = GLYR - 1
END ELSE
   GLPD = GLPD - MCD.PURGE.PDS
END
GLPD = GLPD 'R%2'
GLPER = GLYR:GLPD
*
***** MAIN PROCESSING
*
**** MCD ****
WRITE "MCD" ON CONTROL , PROG.ID
IF D.M.FLG = "PERIOD" THEN
   SELECT MCD
END ELSE
   SELECT MCD.TAG
END
DATA = 1
LOOP
   READNEXT MCD.ID ELSE DATA = 0
WHILE DATA DO
   IF MCD.ID[1,3] # CONO THEN GOTO 1999
   MATREADU MCD.REC FROM MCD, MCD.ID ELSE
      RELEASE MCD, MCD.ID
      GOTO 1999
   END
   IF MCD.DIV<1,1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
      RELEASE MCD, MCD.ID
      GOTO 1999
   END
   IF MCD.MON <= FR.CURR.PER<1,POS> THEN;* T23278
      DELETE MCD.TAG, MCD.ID
   END
   IF MCD.GLA.DATE = "" THEN
      RELEASE MCD, MCD.ID
      GOTO 1999
   END
   IF MCD.MON < GLPER THEN
      IF D.M.FLG = 'PERIOD' THEN
         DELETE MCD, MCD.ID
      END
   END ELSE
      IF MCD.MON = FR.CURR.PER<1,POS> AND MCD.GLA.DATE = 'P' THEN;* T23278
         MCD.GLA.DATE = TODAY
         MATWRITE MCD.REC ON MCD,MCD.ID
      END
      RELEASE MCD, MCD.ID
   END
1999 REPEAT   
IF D.M.FLG # 'PERIOD' THEN GOTO 6000
*
**** VEND ****
WRITE "VEND" ON CONTROL , PROG.ID
SELECT VEND
DATA = 1
LOOP
   READNEXT VEND.ID ELSE DATA = 0
WHILE DATA DO
   IF VEND.ID[1,3] # CONO THEN GOTO 2999
   MATREADU VEND.REC FROM VEND, VEND.ID ELSE
      RELEASE VEND, VEND.ID
      GOTO 2999
   END
   VEND.PD.M.T.D = ""
   VEND.OP.BAL = 0
   MATWRITE VEND.REC ON VEND, VEND.ID
2999 REPEAT   
*
**** OAP ****
WRITE "OAP" ON CONTROL , PROG.ID
SELECT OAP
DATA = 1
LOOP
   READNEXT OAP.ID ELSE DATA = 0
WHILE DATA DO
   IF OAP.ID[1,3] # CONO THEN GOTO 3999
   MATREADU OAP.REC FROM OAP, OAP.ID ELSE
      RELEASE OAP, OAP.ID
      GOTO 3999
   END
   IF OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.PAID = 0 THEN
      RELEASE OAP, OAP.ID
   END ELSE
      RELEASE OAP, OAP.ID
      IF OAP.VEND[9,1] # "" THEN
         GOTO 3999
      END
      OAP.VEND = OAP.VEND[1,5]
      MATREADU VEND.REC FROM VEND, CONO : OAP.VEND ELSE
         RELEASE VEND, CONO : OAP.VEND
         GOTO 3999
      END
      VEND.OP.BAL = VEND.OP.BAL + OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.PAID
      MATWRITE VEND.REC ON VEND, CONO : OAP.VEND
   END
3999 REPEAT   
6000 IF CO.GLS = "N" THEN GOTO 8000
IF CO.GL.LINK<1,2> # "Y" THEN GOTO 8000
**** GLA ****
7000 *
MATREAD CSY.REC FROM CONTROL, CONO : "COA.SYS" ELSE MAT CSY.REC = ""
LOCATE "CD" IN CSY.SOURCE<1>,1 SETTING FND ELSE NULL
L.GLA = CSY.LEVEL<1,FND>
TYPE.ID = "A" : AM : "C" : AM : "H" : AM : "V"
TYPE.DESC = "AUTO" : AM : "CASH" : AM : "HAND" : AM : "VOID"
ACCOUNTS = ""; AMOUNTS = ""
MAT GLA.REC = ""
GLA.DATE = TODAY
GLA.SRC = "CD"
GLA.MON = FR.CURR.PER<1,POS>;* T23278
ODATE = " " : OCONV(FR.CURR.DATE<1,POS>,"D2/");* T23278
SELECT EOM.TRANS
READU COUNTER FROM CONTROL, CONO : "GLACOUNTER" ELSE COUNTER = 0
WRITE "GLA " : COUNTER ON CONTROL , PROG.ID
DATA = 1
BEGIN CASE
   CASE L.GLA = "R"
      LOOP
         READNEXT ETR.ID ELSE DATA = 0
      WHILE DATA DO
         IF ETR.ID[1,3] # CONO THEN GOTO 7199
         MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7180
         CHK.TYP = ETR.ID[ACCT.LEN + 1,1]
         TYPE = FIELD(FIELD(ETR.ID,"!",3),"*",1)
         LOCATE CHK.TYP IN TYPE.ID,1 SETTING FND ELSE GOTO 7180
         ACCT = ETR.ID[1,ACCT.LEN]
         IF EQV.FLG = 1 THEN
            READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
            ACCT = N.ACCT
         END
         COA.ID = ACCT[11,IN.ACCT.LEN]
         MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
            MAT COA.REC = ""
            COA.DESC = UNKNOWN
         END
         BEGIN CASE
            CASE COA.DETAIL = "R" OR COA.DETAIL = "D"
               LOOP
                  COUNTER = COUNTER + 1
                  IF COUNTER > 999998 THEN COUNTER = 1
                  GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                  READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
               WHILE DUMMY # "" DO REPEAT
               GLA.REF = TYPE.DESC<FND>
               GLA.DESC = COA.DESC "L#25" : ODATE
               GLA.AMT = ETR.AMT
               MATWRITE GLA.REC ON GLA, GLA.ID
            CASE 1
               LOCATE ACCT IN ACCOUNTS,1 SETTING AFND ELSE
                  ACCOUNTS<AFND> = ACCT
                  AMOUNTS<AFND> = ""
               END
               IF TYPE = "D" THEN
                  AMOUNTS<AFND,1> = AMOUNTS<AFND,1> + ETR.AMT
               END ELSE
                  AMOUNTS<AFND,2> = AMOUNTS<AFND,2> + ETR.AMT
               END
         END CASE
7180     RELEASE EOM.TRANS, ETR.ID
7199  REPEAT
   CASE L.GLA = "D"
      LOOP
         READNEXT ETR.ID ELSE DATA = 0
      WHILE DATA DO
         IF ETR.ID[1,3] # CONO THEN GOTO 7299
         MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7280
         CHK.TYP = ETR.ID[ACCT.LEN + 1,1]
         LOCATE CHK.TYP IN TYPE.ID,1 SETTING FND ELSE GOTO 7280
         ACCT = ETR.ID[1,ACCT.LEN]
         TYPE = FIELD(FIELD(ETR.ID,"!",3),"*",1)
         IF EQV.FLG = 1 THEN
            READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
            ACCT = N.ACCT
         END
         COA.ID = ACCT[11,IN.ACCT.LEN]
         MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
            MAT COA.REC = ""
            COA.DESC = UNKNOWN
         END
         BEGIN CASE
            CASE COA.DETAIL = "D"
               TCNT = COUNT(ETR.TRAN,VM) + (ETR.TRAN # "")
               FOR I = 1 TO TCNT
                  LOOP
                     COUNTER = COUNTER + 1
                     IF COUNTER > 999998 THEN COUNTER = 1
                     GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                     READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                  WHILE DUMMY # "" DO REPEAT
                  GLA.REF = TYPE.DESC<FND>
                  GLA.DESC = ETR.RDATE<1,I> "L#8" : ETR.TRAN<1,I> : "-" : ETR.REF<1,I>
                  GLA.DESC = GLA.DESC "L#25" : " " : OCONV(ETR.DATE<1,I>,"D2/")
                  GLA.AMT = ETR.TAMT<1,I>
                  MATWRITE GLA.REC ON GLA, GLA.ID
               NEXT I
            CASE COA.DETAIL = "R"
               LOOP
                  COUNTER = COUNTER + 1
                  IF COUNTER > 999998 THEN COUNTER = 1
                  GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                  READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
               WHILE DUMMY # "" DO REPEAT
               GLA.REF = TYPE.DESC<FND>
               GLA.DESC = COA.DESC "L#25" : ODATE
               GLA.AMT = ETR.AMT
               MATWRITE GLA.REC ON GLA, GLA.ID
            CASE 1
               LOCATE ACCT IN ACCOUNTS,1 SETTING AFND ELSE
                  ACCOUNTS<AFND> = ACCT
                  AMOUNTS<AFND> = ""
               END
               IF TYPE = "D" THEN
                  AMOUNTS<AFND,1> = AMOUNTS<AFND,1> + ETR.AMT
               END ELSE
                  AMOUNTS<AFND,2> = AMOUNTS<AFND,2> + ETR.AMT
               END
         END CASE
7280     RELEASE EOM.TRANS, ETR.ID
7299  REPEAT
   CASE 1
      LOOP
         READNEXT ETR.ID ELSE DATA = 0
      WHILE DATA DO
         IF ETR.ID[1,3] # CONO THEN GOTO 7350
         MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7330
         CHK.TYP = ETR.ID[ACCT.LEN + 1,1]
         LOCATE CHK.TYP IN TYPE.ID,1 SETTING FND ELSE GOTO 7330
         ACCT = ETR.ID[1,ACCT.LEN]
         TYPE = FIELD(FIELD(ETR.ID,"!",3),"*",1)
         IF EQV.FLG = 1 THEN
            READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
            ACCT = N.ACCT
         END
         LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
            ACCOUNTS<FND> = ACCT
            AMOUNTS<FND> = ""
         END
         IF TYPE = "D" THEN
            AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
         END ELSE
            AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
         END
7330     RELEASE EOM.TRANS, ETR.ID
7350  REPEAT
END CASE
GLA.REF = ""
ACNT = COUNT(ACCOUNTS,AM) + (ACCOUNTS # "")
FOR I = 1 TO ACNT
   ACCT = ACCOUNTS<I>
   COA.ID = ACCT[11,IN.ACCT.LEN]
   MATREAD COA.REC FROM COA, CONO : COA.ID ELSE COA.DESC = UNKNOWN
   FOR J = 1 TO 2
      IF AMOUNTS<I,J> = "" THEN GOTO 7360
      LOOP
         COUNTER = COUNTER + 1
         IF COUNTER > 999998 THEN COUNTER = 1
         GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
         READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
      WHILE DUMMY # "" DO REPEAT
      GLA.AMT = AMOUNTS<I,J>
      GLA.DESC = COA.DESC "L#25" : ODATE
      MATWRITE GLA.REC ON GLA, GLA.ID
7360 NEXT J
NEXT I
WRITE COUNTER ON CONTROL, CONO : "GLACOUNTER"
ACCOUNTS = ""
AMOUNTS = ""
**** EOM.TRANS (1) ****
8000 *
SELECT EOM.TRANS
WRITE "EOM.TRANS (1)" ON CONTROL , PROG.ID
DATA = 1
LOOP
   READNEXT ETR.ID ELSE DATA = 0
WHILE DATA DO
   IF CONO # ETR.ID[1,3] THEN GOTO 8999
   MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE
      RELEASE EOM.TRANS, ETR.ID
      GOTO 8999
   END
   CURR.PER = FR.CURR.PER<1,POS>
   MAT ETH.REC = MAT ETR.REC
   DONE = 0
   SEQ = 0
   ETH.ID = FIELD(ETR.ID,"*",1):"*":CURR.PER
   LOOP UNTIL DONE = 1 DO
      SEQ = SEQ + 1
      ETH.KEY = ETH.ID:"*":SEQ
      READ CHECKIT FROM APD.EOM.TRANS.HIST, ETH.KEY ELSE DONE = 1
   REPEAT
   MATWRITE ETH.REC ON APD.EOM.TRANS.HIST, ETH.KEY
   DELETE EOM.TRANS, ETR.ID
8999 REPEAT   
WRITE "EOM.TRANS (2)" ON CONTROL, PROG.ID
EOD.HID = ''
IF SECURITY.REC<2> = 'Y' THEN EOD.HID = '!':DIV.CODE
READ EOD.HIST.REC FROM EOD.HIST, CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID ELSE;* T23278
   EOD.HIST.REC = ""
END
EOD.HIST.REC = INSERT(EOD.HIST.REC,-1,0,0,TODAY)
WRITE EOD.HIST.REC ON EOD.HIST,CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID ;* T23278
READ APDFISCAL FROM CONTROL, CONO : "APDFISCAL" ELSE APDFISCAL = ""
SELECT EOM.TRANS
READNEXT ETR.ID ELSE
   IF D.M.FLG = "PERIOD" THEN
      IF FR.NEXT.PER<1,POS> = "" THEN
       	FR.NEXT.PER<1,POS> = APDFISCAL<3>
      END
      IF FR.NEXT.DATE<1,POS> = "" THEN
		FR.NEXT.DATE<1,POS> = APDFISCAL<4>
      END
      FR.CURR.PER<1,POS> = FR.NEXT.PER<1,POS>
      FR.CURR.DATE<1,POS> = FR.NEXT.DATE<1,POS>
      FR.CLOSE.DATE<1,POS> = TODAY
      STATUS = RBO.setProperty('','FR_CURR_PER',FR.CURR.PER)
      STATUS = RBO.setProperty('','FR_CURR_DATE',FR.CURR.DATE)
      STATUS = RBO.setProperty('','FR_CLOSE_DATE',FR.CLOSE.DATE)
   END
   FR.NEXT.PER<1,POS> = ""; FR.NEXT.DATE<1,POS> = ""
   FR.PRINT<1,POS> = ""; FR.D.M.FLG<1,POS> = ""
   STATUS = RBO.setProperty('','FR_NEXT_PER',FR.NEXT.PER)
   STATUS = RBO.setProperty('','FR_NEXT_DATE',FR.NEXT.DATE)
   STATUS = RBO.setProperty('','FR_PRINT',FR.PRINT)
   STATUS = RBO.setProperty('','FR_D_M_FLG',FR.D.M.FLG)
   DELETE CONTROL, PROG.ID
   GOSUB 90000
   CMD='DELETE.FILE ': TRANFILE :' FORCE'
   UDTEXECUTE CMD CAPTURING JUNK
   IF REJECT.RPT.PRT = '' THEN
      CMD='DELETE.FILE ': REJECTFILE : ' FORCE'
      UDTEXECUTE CMD CAPTURING JUNK
   END
   IF ERRMSG # '' THEN GOTO 93000
   GOTO 99999
END
NORETURN=1
90000 
READ JUNK FROM POST.REJECTS,'10001' THEN
   UserID = PMCProperty<1,3>
   IF D.M.FLG = 'DAY' THEN RPTID = 'AP302' ELSE RPTID = 'AP304'
   *IN_PARAM = UserID:"!APPCEOPC!111!":RPTID:"!":CONO:"!Print"
   IN_PARAM = UserID:"!APPCEOPC!111!":RPTID:"!":CONO:"!":Printfalg
   IN_PARAM<2> = ''
   IN_PARAM<3> = 'EOD.REJECT.LISTING'
   IN_PARAM<4> = 'P'
   IN_PARAM<5> = 'PMCPROCS'
   IN_PARAM<6> = REJECTFILE
   IN_PARAM<7> = 'REJECTS'
   IN_PARAM<8> = EMAIL_ADDR ;*Email Address as Entered by the user
   OUT_PARAM=''
   UDTEXECUTE 'SSELECT ':REJECTFILE CAPTURING JUNK
   CALL GenerateReport_Sub(ERRMSG,IN_PARAM,OUT_PARAM)
   REJECT.RPT.PRT='WARNING !!! MAKE SURE TO PICKUP CLOSING REJECTS REPORT FROM PRINTER'
   STATUS=RBO.setProperty('','RejectRptPrinted',REJECT.RPT.PRT)
   STATUS=RBO.setProperty('','ReportImage',OUT_PARAM<2>)
END ELSE
	IF Printfalg # "Print" THEN	ERRMSG  = "There is no data to ":Printfalg
	GOTO 93000
END
IF NOT(NORETURN) THEN RETURN
FR.PRINT<1,POS> = "X";* T23278
STATUS = RBO.setProperty('','FR_PRINT',FR.PRINT)
ERRMSG = "E R R O R !!! CANNOT LOCATE ALL TRANSACTIONS"
93000 WRITEV ERRMSG ON CONTROL, PROG.ID,2
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
*** END OF JOB ***
99999 
MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
END
