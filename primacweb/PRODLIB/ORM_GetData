SUBROUTINE ORM_GetData
********************************************************************************
*   Program name :- ORM_GetData
*   Created:- 01/09/2003
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
*
*
* Out Properties:
* ---------------
*
********************************************************************************

* Insert method code here
$INCLUDE WWINSERT RBO.H
$INCLUDE JCS.CPYLIB JOB
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE ARS.CPYLIB OPEN.RECV
$INCLUDE JCS.CPYLIB INVOICE
$INCLUDE ARS.CPYLIB MANUAL.INVOICE
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB TERMS
$INCLUDE CPYLIB CHAR
DIM OVC.REC(60)
EQU OVC.DATE    TO OVC.REC(1)
EQU OVC.PO.NO   TO OVC.REC(33)
EQU OVC.TERMS   TO OVC.REC(38)
EQU OVC.DUE.DATE   TO OVC.REC(39)

ERRMSG = ''

CantGetProperty='Cannot get property '
CantSetProperty='Cannot set property '

* Open files
OPEN '','COMPANY' TO COMPANY ELSE
   ERRMSG='Cannot open COMPANY file!'
   GOTO 93000
END
OPEN '','CONTROL' TO CONTROL ELSE
   ERRMSG='Cannot open CONTROL file!'
   GOTO 93000
END

OPEN '','OPEN.RECV' TO OPEN.RECV ELSE ERRMSG = 'OPEN.RECV FILE IS MISSING';GOTO 93000
O.R.H = 1
OPEN "","OPEN.RECV.HIST" TO OPEN.RECV.HIST ELSE O.R.H = 0
OPEN '','INVOICE' TO INVOICE ELSE ERRMSG = 'INVOICE FILE IS MISSING' ; GOTO 93000
OPEN '','MANUAL.INVOICE' TO MANUAL.INVOICE ELSE ERRMSG = 'MANUAL.INVOICE FILE IS MISSING' ; GOTO 93000
OPEN '','TERMS' TO TERMS ELSE ERRMSG = 'TERMS FILE IS MISSING' ; GOTO 93000
* Get Company
STATUS = RBO.getProperty('','ID',ID)
CONO=ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
   ERRMSG='Cannot read Company record for ':CONO; GOTO 93000
END
IF CO.JCS = "Y" THEN
   OPEN '','JOB' TO JOB ELSE ERRMSG = "JOB FILE IS MISSING" ; GOTO 93000
END
IF CO.OPS = "Y" THEN
   OPEN '','ORDER' TO ORDER ELSE ERRMSG = "ORDER FILE IS MISSING" ; GOTO 93000
   OPEN '','ORDER.INVOICE' TO ORDER.INVOICE ELSE ERRMSG = "ORDER.INVOICE FILE IS MISSING" ; GOTO 93000
END
* Get all properties

MAT OR.REC = '' ;  MAT JOB.REC = ''; MAT ORD.REC = ''
MAT MIV.REC = ''; MAT IVC.REC = ''; MAT OVC.REC = ''; MAT TERMS.REC = ''
MATREAD OR.REC FROM OPEN.RECV, ID ELSE
   IF O.R.H THEN
      MATREAD OR.REC FROM OPEN.RECV.HIST, ID ELSE
         ERRMSG = "CANNOT LOCATE INVOICE- " : ID[4,99]
         GOTO 93000
      END
   END ELSE
      ERRMSG = "CANNOT LOCATE INVOICE- " : ID[4,99]
      GOTO 93000
   END
END
DIV.CODE = OR.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
IF ERRMSG # '' THEN
   GOTO 93000
END
INV.NO = ID[4,8]
INVOICE.TYPE = INV.NO[LEN(INV.NO)-1,2]
IF INVOICE.TYPE # "MI" THEN
   MATREAD IVC.REC FROM INVOICE,CONO:INV.NO THEN
      INVOICE.TYPE = 'I'
   END ELSE
      IF CO.OPS = 'Y' THEN
         MATREAD OVC.REC FROM ORDER.INVOICE,CONO:INV.NO THEN INVOICE.TYPE = 'O'
      END
   END
END ELSE
   MATREAD MIV.REC FROM MANUAL.INVOICE,CONO:INV.NO ELSE INVOICE.TYPE=""
END
IF OR.ORDER.FLG = "Y" THEN
   IF CO.OPS = 'Y' AND OR.JOB # '' THEN
      MATREAD ORD.REC FROM ORDER, CONO:OR.JOB ELSE MAT ORD.REC = ''
   END
END ELSE
   IF CO.JCS = 'Y' AND OR.JOB # '' THEN
      MATREAD JOB.REC FROM JOB, CONO:OR.JOB ELSE MAT JOB.REC = ''
   END
END
IF OR.ORDER.FLG = "Y" THEN
   DEFAULT.PO = ORD.PO
END ELSE
   DEFAULT.PO = JOB.CUST.PO
END
TERMS.CODE = ''
BEGIN CASE
   CASE INVOICE.TYPE = "MI"
      TERMS.CODE = MIV.TERMS
   CASE INVOICE.TYPE = "I"
      TERMS.CODE = IVC.TERMS
   CASE INVOICE.TYPE = "O"
      TERMS.CODE = OVC.TERMS
END CASE
IF TERMS.CODE # "" THEN
   MATREAD TERMS.REC FROM TERMS, CONO : TERMS.CODE ELSE
      MAT TERMS.REC = ""
   END
END ELSE MAT TERMS.REC = ''
FOR I = 1 TO DCOUNT(OR.TYPE,VM)
   OR.TR.DATE<1,I> = OCONV(OR.TR.DATE<1,I>,'D2/')
   OR.INV.AMT<1,I> = OCONV(OR.INV.AMT<1,I>,'MD2')
   OR.CHK.DATE<1,I> = OCONV(OR.CHK.DATE<1,I>,'D2/')
   OR.CHK.AMT<1,I> = OCONV(OR.CHK.AMT<1,I>,'MD2')
NEXT I
OR.INV.DATE = OCONV(OR.INV.DATE,'D2/')
OR.AS.DATE = OCONV(OR.AS.DATE,'D2/')
OR.DUE.DATE = OCONV(OR.DUE.DATE,'D2/')
OR.BAL = OCONV(OR.BAL,'MD2')
*
* Set properties
STATUS=RBO.setProperty('','OR_CUST',OR.CUST)
STATUS=RBO.setProperty('','OR_PO',OR.PO)
STATUS=RBO.setProperty('','OR_JOB',OR.JOB)
STATUS=RBO.setProperty('','OR_INV_DATE',OR.INV.DATE)
STATUS=RBO.setProperty('','OR_AS_DATE',OR.AS.DATE)
STATUS=RBO.setProperty('','OR_BAL',OR.BAL)
STATUS=RBO.setProperty('','OR_TYPE',OR.TYPE)
STATUS=RBO.setProperty('','OR_TR_DATE',OR.TR.DATE)
STATUS=RBO.setProperty('','OR_INV_AMT',OR.INV.AMT)
STATUS=RBO.setProperty('','OR_CHECK',OR.CHECK)
STATUS=RBO.setProperty('','OR_CHK_DATE',OR.CHK.DATE)
STATUS=RBO.setProperty('','OR_CHK_AMT',OR.CHK.AMT)
STATUS=RBO.setProperty('','OR_DIV',OR.DIV)
STATUS=RBO.setProperty('','OR_ORDER_FLG',OR.ORDER.FLG)
STATUS=RBO.setProperty('','OR_DUE_DATE',OR.DUE.DATE)
STATUS=RBO.setProperty('','INVOICE_NO',INV.NO)
STATUS=RBO.setProperty('','DEFAULT_PO',DEFAULT.PO)
STATUS=RBO.setProperty('','TERMS_DUE_DAYS',TERMS.DUE.DAYS)

GOTO 99999

93000 
IF ERRMSG THEN
   CALL RBO_ERROR_SUB(ERRMSG)
END

* End of method code
99999 
RETURN
