   SUBROUTINE APVOUDM_POSTWRITE
*********************************************************************
* REVISION    - [12.0]
*
* SYSTEM      - PRIMAC
*
* PROGRAM     - APVOUDM_POSTWRITE
*
* BY          - CHRIS MYKLEBUST
*
* DATE        - 11/11/2002
*
* DESCRIPTION - Updates the other files besides the OAP file.
*********************************************************************
   $INCLUDE WWINSERT RBO.H
   $INCLUDE APS.CPYLIB OAP
   $INCLUDE APS.CPYLIB SQV
   $INCLUDE APS.CPYLIB HDCH
   $INCLUDE APS.CPYLIB APCHECK
   $INCLUDE JCS.CPYLIB JOB.OSP
   $INCLUDE PMC.CPYLIB COMPANY
   $INCLUDE PMC.CPYLIB VEND
   $INCLUDE CPYLIB FILE.VARS
   $INCLUDE CPYLIB CHAR
   DIM L.VOU(2)
   EQU VOU.NO TO L.VOU(1)
   EQU VOU.DATE TO L.VOU(2)
*
*---- OPEN FILES
*
   IF FILEINFO(COMPANY,0) = 0 THEN
      OPEN 'COMPANY' TO COMPANY ELSE
         ERRMSG = 'CANNOT OPEN COMPANY FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(CONTROL,0) = 0 THEN
      OPEN '','CONTROL' TO CONTROL ELSE
         ERRMSG='CANNOT OPEN CONTROL FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(OAP,0) = 0 THEN
      OPEN '','OAP' TO OAP ELSE
         ERRMSG='Cannot open OAP'
         GOTO 93000
      END
   END
   IF FILEINFO(APCHECK,0) = 0 THEN
      OPEN 'APCHECK' TO APCHECK ELSE
         ERRMSG = 'CANNOT OPEN APCHECK FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(SQV,0) = 0 THEN
      OPEN 'SQV' TO SQV ELSE
         ERRMSG = 'CANNOT OPEN SQV FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(HDCH,0) = 0 THEN
      OPEN 'HDCH' TO HDCH ELSE
         ERRMSG = 'CANNOT OPEN HDCH FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(VEND,0) = 0 THEN
      OPEN 'VEND' TO VEND ELSE
         ERRMSG = 'CANNOT OPEN VEND FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(JOB.OSP,0) = 0 THEN
      OPEN 'JOB.OSP' TO JOB.OSP ELSE
         ERRMSG = 'CANNOT OPEN JOB.OSP FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(INV.XREF,0) = 0 THEN
      OPEN 'INV.XREF' TO INV.XREF ELSE
         ERRMSG = 'CANNOT OPEN INV.XREF FILE'
         GOTO 93000
      END
   END
*
*---- INITIALIZATION
*
   STATUS = RBO.getProperty('','ID',ID)
   STATUS = RBO.getProperty('','OAP_DUE_DATE',OAP.DUE.DATE)
   STATUS = RBO.getProperty('','OAP_CHK_NO',OAP.CHK.NO)
   STATUS = RBO.getProperty('','OAP_VEND',OAP.VEND)
   STATUS = RBO.getProperty('','OAP_INV',OAP.INV)
   STATUS = RBO.getProperty('','SAVE_INV',SAVE.INV)
   STATUS = RBO.getProperty('','NEW_GRS',NEW.GRS)
   STATUS = RBO.getProperty('','NEW_DSC',NEW.DSC)
   STATUS = RBO.getProperty('','NEW_PAID_DATE',NEW.PAID.DATE)
   STATUS = RBO.getProperty('','NEW_DISB_ACCT',NEW.DISB.ACCT)
   STATUS = RBO.getProperty('','NEW_CHK_NO',NEW.CHK.NO)
   NEW.PAID.DATE = ICONV(NEW.PAID.DATE,'D2/')
   CONO = ID[1,3]
   V.CODE = ID[4,99]
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG = "COMPANY RECORD IS MISSING FOR COMPANY ":CONO
      GOTO 93000
   END
   MATREADU OAP.REC FROM OAP,ID LOCKED
      ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
      GOTO 93000
   END ELSE
      ERRMSG = "VOUCHER OAP record is missing"
      GOTO 93000
   END
*
*---- MAIN PROCESS
*
   MATREADU HDCK.REC FROM HDCH, ID LOCKED
      ERRMS = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
      GOTO 93000
   END ELSE
      MAT HDCK.REC = ""
   END
   IF OAP.DUE.DATE = "PAID" OR NEW.CHK.NO # "" THEN
      *** UPDATE THE HAND CHECK FILE ***
      HDCK.VEND = OAP.VEND
      HDCK.INV = OAP.INV
      HDCK.INV.DATE = OAP.INV.DATE
      HDCK.DUE.DATE = 'PAIDC'
      HDCK.GRS.AMT = NEW.GRS
      HDCK.NER.AMT = OAP.MER.AMT
      HDCK.DSC.AMT = NEW.DSC
      HDCK.CTR = OAP.CTR
      HDCK.ACCT = OAP.ACCT
      HDCK.DIST.AMT = OAP.DIST.AMT
      HDCK.PAID.AMT = NEW.GRS - NEW.DSC
      HDCK.PAID.DATE = NEW.PAID.DATE
      HDCK.JOB = OAP.JOB
      HDCK.PO = OAP.PO
      HDCK.DISB.ACCT = NEW.DISB.ACCT
      HDCK.NUM = NEW.CHK.NO
      HDCK.MON = OAP.MON
      HDCK.DATE = NEW.PAID.DATE
      HDCK.TERMS = OAP.TERMS
      HDCK.DIV = OAP.DIV
      HDCK.DEPT = OAP.DEPT
      HDCK.CCTR = OAP.CCTR
      HDCK.AP.ACCT = OAP.AP.ACCT
      HDCK.AP.AMT = OAP.AP.AMT
      HDCK.AP.DIV = OAP.AP.DIV
      HDCK.AP.DEPT = OAP.AP.DEPT
      HDCK.AP.CCTR = OAP.AP.CCTR
      MATWRITE HDCK.REC ON HDCH , ID
      *** UPDATE THE SQV FILE ***
      SQV.FLG = 0
      MATREADU SQV.REC FROM SQV, ID LOCKED
         ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
         GOTO 93000
      END ELSE
         SQV.FLG = 1
      END
      IF SQV.FLG = 0 THEN
         P.CNT = DCOUNT(SQV.PAID.AMT,VM) + 1
         SQV.DUE.DATE = OAP.DUE.DATE
         SQV.INV = OAP.INV
         SQV.PAID.AMT<1,P.CNT> = NEW.GRS - NEW.DSC
         SQV.PAID.DATE<1,P.CNT> = NEW.PAID.DATE
         SQV.CHK.NO<1,P.CNT> = NEW.CHK.NO
         SQV.DISB.ACCT<1,P.CNT> = NEW.DISB.ACCT
         SQV.DSC.PAID<1,P.CNT> = NEW.DSC
         MATWRITE SQV.REC ON SQV, ID
      END
      *** UPDATE THE APCHECK FILE ***
      IF CO.CHK.RECON # 'N' THEN
         LOCATE V.CODE IN APCK.VOUCH<1>,1 SETTING VPTR ELSE
            APCK.VOUCH<1,VPTR> = V.CODE
         END
         APCK.AMT = APCK.AMT + (NEW.GRS - NEW.DSC)
         APCK.DATE = NEW.PAID.DATE
         APCK.DIV = OAP.DIV
         APCK.DEPT = OAP.DEPT
         APCK.CCTR = OAP.CCTR
         MATWRITE APCK.REC ON APCHECK , CONO:NEW.DISB.ACCT:NEW.CHK.NO
      END
      *** UPDATE THE VENDOR FILE ***
      MATREAD VEND.REC FROM VEND,CONO:OAP.VEND[1,5] ELSE MAT VEND.REC = ''
      VEND.PD.M.T.D = VEND.PD.M.T.D + (NEW.GRS - NEW.DSC)
      VEND.PD.Y.T.D = VEND.PD.Y.T.D + (NEW.GRS - NEW.DSC)
      VEND.DATE.PD = NEW.PAID.DATE
      VEND.PA.AMT = NEW.GRS - NEW.DSC
      VEND.OP.BAL = VEND.OP.BAL - NEW.GRS
      VEND.CK.NO.PD = NEW.CHK.NO
      MATWRITE VEND.REC ON VEND,CONO:OAP.VEND<1,1>[1,5]
   END ELSE
      IF SQV.FLG = 0 THEN
         SQV.DUE.DATE = OAP.DUE.DATE
         SQV.INV = OAP.INV
         MATWRITE SQV.REC ON SQV, ID
      END
   END
   *** UPDATE INV.XREF FILE ***
   IF OAP.INV # SAVE.INV THEN
      DELETE INV.XREF, CONO:SAVE.INV'R%10':OAP.VEND<1,1>
      VOU.NO = V.CODE
      VOU.DATE = DATE()
      MATWRITE L.VOU ON INV.XREF, CONO:OAP.INV'R%10':OAP.VEND<1,1>
   END
   *** UPDATE JOB.OSP WHEN APPROPRIATE ***
   IF CO.JCS = 'Y' AND CO.VOU.PO.FLG = 'Y' THEN
      MATREAD JOS.REC FROM JOB.OSP,OAP.JCS.TRANS ELSE MAT JOS.REC = ""  
      JOS.JOB = FIELD(OAP.JCS.TRANS,"!",1)[4,99]
      IF JOS.JOB # "" THEN
         JOS.INV = OAP.INV
         MATWRITE JOS.REC ON JOB.OSP , OAP.JCS.TRANS 
      END  
   END
   RETURN
93000 *
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
   RETURN


