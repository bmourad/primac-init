  SUBROUTINE IVCJSM_POSTWRITE
$INCLUDE CPYLIB COMMON1
$INCLUDE JCS.CPYLIB COM.INVOICE
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - IVCJSM_POSTWRITE
*
* AUTHOR   - ALEJANDRO DELGADO
*
* DATE     - 01/30/2003
*
* DESCRIPTION
*
* Data is retrieve from the JOB.SALES.STATS file to load display through RBO.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
  $INCLUDE WWINSERT RBO.H
  $INCLUDE PMC.CPYLIB INVOICE.SALES.STATS
  $INCLUDE PMC.CPYLIB JOB.SALES.STATS
  $DEFINE INVOICE
  $INCLUDE JCS.CPYLIB INVOICE
  $DEFINE JOB
  $INCLUDE JCS.CPYLIB JOB
  $DEFINE COMPANY
  $INCLUDE PMC.CPYLIB COMPANY
  $DEFINE FILEVARS
  $INCLUDE CPYLIB FILE.VARS
  $INCLUDE CPYLIB CHAR
*
*---- INITIALIZATION
*
  STATUS = RBO.getProperty('','ID',ID)
  CONO = ID[1,3] ; JOB.NO = TRIM(ID[4,99])
*
*---- OPEN FILES
*
  IF FILEINFO(JOB.SALES.STATS,0) = 0 THEN
    OPEN '','JOB.SALES.STATS' TO JOB.SALES.STATS ELSE
      ERRMSG = 'Cannot open JOB.SALES.STATS file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(JOB,0) = 0 THEN
    OPEN '','JOB' TO JOB ELSE
      ERRMSG = 'Cannot open JOB file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(INVOICE,0) = 0 THEN
    OPEN '','INVOICE' TO INVOICE ELSE
      ERRMSG = 'Cannot open INVOICE file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(COMPANY,0) = 0 THEN
    OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = 'Cannot open COMPANY file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(INVOICE.SALES.STATS,0) = 0 THEN
    OPEN '','INVOICE.SALES.STATS' TO INVOICE.SALES.STATS ELSE
      ERRMSG = 'Cannot open INVOICE.SALES.STATS file!'
      GOTO SET.ERROR
    END
  END
*
*---- MAIN PROCESS
*
  MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ''
  MATREAD JSS.REC FROM JOB.SALES.STATS, ID THEN
    MATREAD JOB.REC FROM JOB, ID ELSE MAT JOB.REC = ''
    JSS.CUST.ID = JOB.CUST
    JSS.SLSM.ID = JOB.SLSMN
    JSS.SALES.CODE = JOB.SALES.CODE
    JSS.JOB.CAT = JOB.CATG
    JSS.NO.COLORS = JOB.COLORS
    MATWRITE JSS.REC ON JOB.SALES.STATS, ID
    FND = 0
    ICNT = DCOUNT(JOB.INV.NO,VM)
    FOR I = 1 TO ICNT UNTIL FND
      MATREAD IVC.REC FROM INVOICE, CONO:JOB.INV.NO<1,I> THEN
        IF IVC.LAST = "Y" THEN
          ISS.KEY = CONO:"_J_":JOB.NO:"_":JOB.INV.NO<1,I>:"_0_0"
          MATREAD ISS.REC FROM INVOICE.SALES.STATS, ISS.KEY THEN
            ISS.ADJUST.AMT = JSS.ADJUST.AMT
            ADJ.COST = ISS.ADJUST.AMT<1,1>
            SPOIL = ISS.ADJUST.AMT<1,2>
            PLANT.CON = ISS.ADJUST.AMT<1,3>
            OTH = ISS.ADJUST.AMT<1,4>
            ADJ.FACT.COST = ISS.COST.TOTAL + ADJ.COST + SPOIL + PLANT.CON + OTH
            ISS.ADJUST.COST<1,1> = ADJ.FACT.COST
            ISS.MARKET.COST = JSS.MARKET.COST
            ISS.ADJUST.COST<1,2> = JSS.ADJUST.COST<1,2>
            ISS.COMMISSION<1,1> = JSS.COMMISSION<1,1>
            MATWRITE ISS.REC ON INVOICE.SALES.STATS, ISS.KEY
            FND = 1
          END
        END
      END
    NEXT I
    IF CO.COMMISSION = "Y" THEN
      DIM SAVE.REC(55)
      MAT SAVE.REC = ''
      SAVE.REC(2) = JOB.SLSMN
      SAVE.REC(4) = JSS.COST.TOTAL
      SAVE.REC(5) = JSS.ADJUST.COST<1,2>
      SAVE.REC(6) = JSS.ADJUST.COST<1,1>
      SAVE.REC(7) = JSS.COMMISSION<1,1>
      SAVE.REC(8) = JSS.VALUE.ADDED
      SAVE.REC(52) = JOB.NO
      PROG.TYPE = "JSALES.MAINT"
      CALL COMMISSION_UPD_SUB(CONO,MAT SAVE.REC,PROG.TYPE)
    END
  END
  RELEASE JOB.SALES.STATS, ID
  RETURN
*
SET.ERROR: 
*
  STATUS = RBO.setProperty('','ServerStatus',1)
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  RETURN
END
