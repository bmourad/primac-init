SUBROUTINE GET_GLX_DEPT
********************************************************************************
*   Program name :- GET_GLX_DEPT
*   Created:- 12/9/2002
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
* ID,PERIOD,ACCT,DIV
*
* Out Properties:
* ---------------
* DeptCodes,DeptDescs
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB DIVISION
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE GLS.CPYLIB GLX
$INCLUDE GLS.CPYLIB GLM
$INCLUDE GLS.CPYLIB COA.BAL
$INCLUDE GLS.CPYLIB ACCT.TYPE
$INCLUDE GLS.CPYLIB ACCT.CATG
$INCLUDE GLS.CPYLIB ACCT.SUB.CATG
$INCLUDE GLS.CPYLIB CATG.AND.SUB
$INCLUDE GLS.CPYLIB GLX
$INCLUDE CPYLIB CHAR

* Open files
ERRMSG = ''
CALL RBO_OPEN_FILE('GLX',GLX,ERRMSG)
CALL RBO_OPEN_FILE('GLM',GLM.FILE,ERRMSG)
CALL RBO_OPEN_FILE('GLA',GLA.FILE,ERRMSG)
CALL RBO_OPEN_FILE('DIVISION',DIVISION,ERRMSG)
CALL RBO_OPEN_FILE('DEPARTMENT',DEPARTMENT,ERRMSG)
CALL RBO_OPEN_FILE('COST.CNTR',COST.CNTR,ERRMSG)
CALL RBO_OPEN_FILE('CO.COA.BAL',CO.COA.BAL,ERRMSG)
CALL RBO_OPEN_FILE('CC.COA.BAL',CC.COA.BAL,ERRMSG)
CALL RBO_OPEN_FILE('DP.COA.BAL',DP.COA.BAL,ERRMSG)
CALL RBO_OPEN_FILE('DV.COA.BAL',DV.COA.BAL,ERRMSG)
CALL RBO_OPEN_FILE('CONTROL',CONTROL,ERRMSG)
CALL RBO_OPEN_FILE('COMPANY',COMPANY,ERRMSG)
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG)
  RETURN
END

* Get properties
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','PERIOD',PERIOD)
STATUS = RBO.getProperty('','ACCT',ACCT)
STATUS = RBO.getProperty('','DIV',DIV)

* Initialize variables
ALL.DIV = ''
GEN.DIV = '00'
ALL.DEPT = ''
GEN.DEPT = '00'
ALL.CCTR = ''
GEN.CCTR = '000'
LINE.CODE = ''

* Get CONO
CONO = ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
  ERRMSG = 'Invalid Company ID (':CONO:').'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Get period
READ FISCAL.REC FROM CONTROL, CONO : 'FISCAL' ELSE FISCAL.REC = ''
READ PERIOD.REC FROM CONTROL, CONO : 'ACCT.PERIODS' ELSE 
  PERIOD.REC = ''
  PERIOD.REC<1> = 12
END
NUM.PERIODS = PERIOD.REC<1>
SEL.PERIOD = FISCAL.REC<1>
IF PERIOD THEN SEL.PERIOD = PERIOD

IF SEL.PERIOD # FISCAL.REC<1> THEN
  GLM = GLA.FILE
END ELSE
  GLM = GLM.FILE
END
YR.OFFSET = (SEL.PERIOD[1,4] - FISCAL.REC<1>[1,4]) * NUM.PERIODS
PER.OFFSET = SEL.PERIOD[5,2] + YR.OFFSET
OPN = CB.OPN + PER.OFFSET

IF PERIOD[5,2] = NUM.PERIODS THEN
  OPNVAL = 55
END ELSE
  OPNVAL = OPN+1
END

IN.ACCT.LEN = LEN(CO.ACCT.PIC)
BASE.LEN = FIELD(CO.ACCT.STRUC,"-",1)
IF NOT(NUM(BASE.LEN)) OR BASE.LEN > IN.ACCT.LEN THEN
  ERRMSG = 'Error in the account structure.'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Select dept
XCODE<2> = 'S'
XDESC<2> = 'Summary'
GOSUB SELECT.DEPT

* Set properties
STATUS = RBO.setProperty('','DeptCodes',"S" : VM : CONVERT(AM,VM,XCODE))
STATUS = RBO.setProperty('','DeptDescs',"S-Summary" : VM : CONVERT(AM,VM,XDESC))

* End of method code
RETURN

SELECT.DEPT:
  ;* Select departments
  IF SEL.PERIOD = FISCAL.REC<1> THEN
    READ ALL.DEPT FROM GLX, CONO : DIV : ACCT ELSE ALL.DEPT = ''
   END ELSE
    SELID = CONO : DIV : ACCT
    GOSUB GET.ALL.DEPT
  END
  IF ALL.DEPT = '' THEN
    ERRMSG<1,-1> = 'No activities for division - ' : DIV
  END
  ;* Insert department descriptions
  XCODE = ALL.DEPT
  XDESC = ''
  XLINES = DCOUNT(ALL.DEPT,AM)
  FOR XLN = 1 TO XLINES
    IF XCODE<XLN> = GEN.DEPT THEN
      MAT DEPT.REC = ''
      DEPT.DESC = 'General Department'
    END ELSE
      MATREAD DEPT.REC FROM DEPARTMENT, CONO : XCODE<XLN> ELSE
        MAT DEPT.REC = ''
        DEPT.DESC = 'Unknown'
      END
    END
    XDESC<XLN> = XCODE<XLN> :"-": DEPT.DESC
  NEXT XLN
RETURN

GET.ALL.DEPT: 
  STMT = 'SSELECT GLA WITH CONO_DV_ACCT = ' : QUOTE(SELID)
  STMT:= ' AND WITH PERIOD = ' : QUOTE(SEL.PERIOD)
  UDTEXECUTE STMT CAPTURING CAP RETURNING RET
  ALL.DEPT = ''; LCD.CT = 1
  LOOP
    READNEXT GLAID ELSE EXIT
    LOCATE GLAID[6,2] IN ALL.DEPT,1 SETTING DFND ELSE
      ALL.DEPT<LCD.CT> = GLAID[6,2]
      LCD.CT = LCD.CT + 1
    END
  REPEAT
RETURN
