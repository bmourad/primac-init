SUBROUTINE JOBINQ_LB_GetData
********************************************************************************
*   Program name :- JOBINQ_LB_GetData
*   Created:- 12/13/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'LB' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TOT.REC
$INCLUDE JCS.CPYLIB OPERATION
$INCLUDE CPYLIB CHAR

DEFFUN CalcStkQty(COST.QTY,MAT INV.CNV.REC,ROND,LN)
;* open files

ERRMSG=''
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF FILEINFO(OPERATION,0)=0 THEN
   OPEN '','OPERATION' TO OPERATION ELSE
      ERRMSG<1,-1>='OPERATION FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM CCTR(10)
EQU CCTR.NO    TO CCTR(1)
EQU CCTR.NAME  TO CCTR(2)
EQU OPER.NO    TO CCTR(3)
EQU OPER.NAME  TO CCTR(4)
EQU OPER.TYP  TO CCTR(5)
EQU OPER.TIME  TO CCTR(6)
EQU OPER.IMPR  TO CCTR(7)
EQU OPER.DATE  TO CCTR(8)
EQU OPER.COST  TO CCTR(9)
EQU OPER.SALE  TO CCTR(10)
MAT CCTR = ''
DIM OTYPE(5)
OTYPE(1) = 'R,'
OTYPE(2) = 'N,'
OTYPE(3) = 'O,'
OTYPE(4) = 'S,'
OTYPE(5) = 'C'
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''
LB.DEPT.PTR = ''
LB.DEPT.CHG = ''
LB.DEPT.NCHG = ''
LB.DEPT.CORD = ''
LB.DEPT.SPOIL = ''
LB.DEPT.CNCR = ''
LB.DEPT.COST = ''
LB.DEPT.SALE = ''
LB.TOT.CHG = 0
LB.TOT.NCHG = 0
LB.TOT.CORD = 0
LB.TOT.SPOIL = 0
LB.TOT.CNCR = 0
LB.TOT.COST = 0
LB.TOT.SALE = 0
OPER.NO = ''
OPER.NAME = ''
OPER.TIME = ''
OPER.IMPR = ''
OPER.DATE = ''
OPER.COST = ''
OPER.SALE = ''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='LB'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


END
MAT JOB.TOT.REC = ''
JPTR = 0
JCNT = 0
CNT = COUNT(JOB.LB.DEPT,VM) + (JOB.LB.DEPT # '')
FOR I = 1 TO CNT
   TEMP = JOB.LB.DEPT<1,I>
   LOCATE TEMP IN LB.DEPT.NO<1>,1 SETTING JCNT ELSE
      JPTR = JPTR + 1; JCNT = JPTR
      LB.DEPT.NO<1,JCNT> = TEMP
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:TEMP ELSE DEPT.DESC = UNKNOWN
      LB.DEPT.DESC<1,JCNT> = DEPT.DESC
   END
   LB.DEPT.PTR<1,JCNT,-1> = I
   LB.DEPT.CHG<1,JCNT> = LB.DEPT.CHG<1,JCNT> + JOB.LB.TCOST<1,I,1>
   LB.DEPT.NCHG<1,JCNT> = LB.DEPT.NCHG<1,JCNT> + JOB.LB.TCOST<1,I,2>
   LB.DEPT.CORD<1,JCNT> = LB.DEPT.CORD<1,JCNT> + JOB.LB.TCOST<1,I,3>
   LB.DEPT.SPOIL<1,JCNT> = LB.DEPT.SPOIL<1,JCNT> + JOB.LB.TCOST<1,I,4>
   LB.DEPT.CNCR<1,JCNT> = LB.DEPT.CNCR<1,JCNT> + JOB.LB.TCOST<1,I,5>
   LB.DEPT.COST<1,JCNT> = LB.DEPT.COST<1,JCNT> + JOB.LB.COST<1,I>
   LB.DEPT.SALE<1,JCNT> = LB.DEPT.SALE<1,JCNT> + JOB.LB.SALE<1,I>
NEXT I
FOR I = 1 TO JPTR
   LB.TOT.CHG = LB.TOT.CHG + LB.DEPT.CHG<1,I>
   LB.TOT.NCHG = LB.TOT.NCHG + LB.DEPT.NCHG<1,I>
   LB.TOT.CORD = LB.TOT.CORD + LB.DEPT.CORD<1,I>
   LB.TOT.SPOIL = LB.TOT.SPOIL + LB.DEPT.SPOIL<1,I>
   LB.TOT.CNCR = LB.TOT.CNCR + LB.DEPT.CNCR<1,I>
   LB.TOT.COST = LB.TOT.COST + LB.DEPT.COST<1,I>
   LB.TOT.SALE = LB.TOT.SALE + LB.DEPT.SALE<1,I>
NEXT I
LINES = COUNT(LB.DEPT.NO,VM) + (LB.DEPT.NO # '')
FOR I = 1 TO LINES
   CNT = COUNT(LB.DEPT.PTR<1,I>,SVM) + (LB.DEPT.PTR<1,I> # '')
   FOR J = 1 TO CNT
      PTR = LB.DEPT.PTR<1,I,J>
      CCTR.NO<1,I,J> = JOB.LB.CCTR<1,PTR>
      MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR.NO<1,I,J> ELSE CCTR.DESC = UNKNOWN
      CCTR.NAME<1,I,J> = CCTR.DESC
      OPER.NO<1,I,J> = JOB.LB.OPER<1,PTR>
      MATREAD OPER.REC FROM OPERATION, CONO : OPER.NO<1,I,J> ELSE OPER.DESC = UNKNOWN
      OPER.NAME<1,I,J> = OPER.DESC
      OPER.TIME<1,I,J> = OCONV(JOB.LB.HRS<1,PTR>,'MD2,')
      OPER.IMPR<1,I,J> = OCONV(JOB.LB.IMP<1,PTR>,'MD2,')
      OPER.DATE<1,I,J> = OCONV(JOB.LB.DATE<1,PTR,1>,'D2/')
      OPER.COST<1,I,J> = OCONV(JOB.LB.COST<1,PTR>,'MD2,')
      OPER.SALE<1,I,J> = OCONV(JOB.LB.SALE<1,PTR>,'MD2,')
      FOR K = 1 TO 5
         IF JOB.LB.TCOST<1,PTR,K> > 0 THEN OPER.TYP<1,I,J> = OPER.TYP<1,I,J> : OTYPE(K)
      NEXT K
   NEXT J
   LB.DEPT.CHG<1,I> = OCONV(LB.DEPT.CHG<1,I>,'MD2,')
   LB.DEPT.NCHG<1,I> = OCONV(LB.DEPT.NCHG<1,I>,'MD2,')
   LB.DEPT.CORD<1,I> = OCONV(LB.DEPT.CORD<1,I>,'MD2,')
   LB.DEPT.SPOIL<1,I> = OCONV(LB.DEPT.SPOIL<1,I>,'MD2,')
   LB.DEPT.CNCR<1,I> = OCONV(LB.DEPT.CNCR<1,I>,'MD2,')
   LB.DEPT.COST<1,I> = OCONV(LB.DEPT.COST<1,I>,'MD2,')
   LB.DEPT.SALE<1,I> = OCONV(LB.DEPT.SALE<1,I>,'MD2,')
NEXT I

STATUS=RBO.setProperty('','LB_DEPT_NO',LB.DEPT.NO)
STATUS=RBO.setProperty('','LB_DEPT_CHG',LB.DEPT.CHG)
STATUS=RBO.setProperty('','LB_TOT_CHG',OCONV(LB.TOT.CHG,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_NCHG',LB.DEPT.NCHG)
STATUS=RBO.setProperty('','LB_TOT_NCHG',OCONV(LB.TOT.NCHG,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_CORD',LB.DEPT.CORD)
STATUS=RBO.setProperty('','LB_TOT_CORD',OCONV(LB.TOT.CORD,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_SPOIL',LB.DEPT.SPOIL)
STATUS=RBO.setProperty('','LB_TOT_SPOIL',OCONV(LB.TOT.SPOIL,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_CNCR',LB.DEPT.CNCR)
STATUS=RBO.setProperty('','LB_TOT_CNCR',OCONV(LB.TOT.CNCR,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_COST',LB.DEPT.COST)
STATUS=RBO.setProperty('','LB_TOT_COST',OCONV(LB.TOT.COST,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_SALE',LB.DEPT.SALE)
STATUS=RBO.setProperty('','LB_TOT_SALE',OCONV(LB.TOT.SALE,'MD2,'))
STATUS=RBO.setProperty('','LB_DEPT_DESC',LB.DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_NO',CCTR.NO)
STATUS=RBO.setProperty('','CCTR_NAME',CCTR.NAME)
STATUS=RBO.setProperty('','OPER_NO',OPER.NO)
STATUS=RBO.setProperty('','OPER_NAME',OPER.NAME)
STATUS=RBO.setProperty('','OPER_TYPE',OPER.TYP)
STATUS=RBO.setProperty('','OPER_TIME',OPER.TIME)
STATUS=RBO.setProperty('','OPER_IMPR',OPER.IMPR)
STATUS=RBO.setProperty('','OPER_DATE',OPER.DATE)
STATUS=RBO.setProperty('','OPER_COST',OPER.COST)
STATUS=RBO.setProperty('','OPER_SALE',OPER.SALE)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN

