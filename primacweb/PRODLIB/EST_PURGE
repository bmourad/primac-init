SUBROUTINE EST_PURGE(CONO,RVALS,ERRMSG)
********************************************************************************
*   Program name :- EST_PURGE
*   Created:- 2/21/2005
* DESCRIPTION
* This program is used to purge estimates from system based on criteria
* specified on manual estimate selection screen.
*
* Doc1 : The code commented is because of uncertainity of 
*        when & how the data is displayed . - suhail
**************************************************************************
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE JES.CPYLIB ESTIMATE
$INCLUDE JES.CPYLIB ESTIMATE.DEPT
$INCLUDE JES.CPYLIB ESTIMATE.PURGE
$INCLUDE JES.CPYLIB ESTIMATE.RES
$INCLUDE CPYLIB>CHAR
$INCLUDE CPYLIB>SYSCOM

*--- PROCREAD
*

DIV.CODE = RVALS<3>

*
*---- OPEN ALL FIILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG="CANNOT OPEN COMPANY FILE"
         GOSUB 99999
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG="CANNOT OPEN CONTROL FILE"
         GOSUB 99999
      END
      OPEN "","PREFIX" TO PREFIX ELSE
         ERRMSG="CANNOT OPEN PREFIX FILE"
         GOSUB 99999
      END
      OPEN "","ESTIMATE" TO ESTIMATE ELSE
         ERRMSG="CANNOT OPEN ESTIMATE FILE"
         GOSUB 99999
      END
      OPEN "","ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE
         ERRMSG="CANNOT OPEN ESTIMATE.DEPT FILE"
         GOSUB 99999
      END
      OPEN "","ESTIMATE.PURGE.BU" TO ESTIMATE.PURGE.BU ELSE
         ERRMSG="CANNOT OPEN ESTIMATE.PURGE.BU FILE"
         GOSUB 99999
      END
      OPEN "","CUST.EST.XREF" TO CUST.EST.XREF ELSE
         ERRMSG="CANNOT OPEN CUST.EST.XREF FILE"
         GOSUB 99999
      END
      OPEN "","PROSPECT.EST.XREF" TO PROSPECT.EST.XREF ELSE
         ERRMSG="CANNOT OPEN PROSPECT.EST.XREF FILE"
         GOSUB 99999
      END
      OPEN "","ESTIMATE.RES" TO ESTIMATE.RES ELSE
         ERRMSG = "CANNOT OPEN ESTIMATE.RES FILE"
         GOSUB 99999
      END
      OPEN "","ESTIMATE.RL" TO ESTIMATE.RL ELSE
         ERRMSG = "CANNOT OPEN ESTIMATE.RL FILE"
         GOSUB 99999
      END
*
*---- INITIALIZATION
*
      TODAY = DATE()
      MAT COMP.REC=""
      MATREADU ESTP.REC FROM CONTROL, CONO:"MANUAL.PURGE" ELSE
         GOTO 99999
      END
      PCNT = COUNT(ESTP.FROM.EST,VM) + (ESTP.FROM.EST # "")
*
*---- MAIN PROCESSING
*
      SELECT ESTIMATE
100*
      RELEASE
      READNEXT ID ELSE
         DELETE CONTROL, CONO:"MANUAL.PURGE"
         GOTO 99999
      END
      IF ID[1,3] # CONO THEN GOTO 100
      EST.KEY = ID[4,99]
      EST.NUM = FIELD(EST.KEY,"-",1)
      PURGE.FLAG = 0
      FOR P = 1 TO PCNT UNTIL PURGE.FLAG
         FROM.EST = ESTP.FROM.EST<1,P>
         FROM.NUM = FIELD(FROM.EST,"-",1)
         TO.EST = ESTP.TO.EST<1,P>
         TO.NUM = FIELD(TO.EST,"-",1)
         DIV.CODE = ESTP.DIV<1,P> ; *T23278
         BEGIN CASE
         CASE EST.KEY = FROM.EST
            PURGE.FLAG = 1
         CASE EST.NUM >= FROM.NUM AND EST.NUM <= TO.NUM
            PURGE.FLAG = 1
         END CASE
      NEXT P
      IF NOT(PURGE.FLAG) THEN GOTO 100
      MATREADU EST.REC FROM ESTIMATE, CONO:EST.KEY ELSE GOTO 100
      IF EST.MASTER # "" AND EST.MASTER # EST.KEY THEN GOTO 100
      IF EST.PURGE.TYPE # "M" THEN GOTO 100
      IF EST.DIV[1,2] # DIV.CODE THEN GOTO 100;* T23278 T25719
      STATUS = EST.STATUS<1,1>
      STATUS.DATE = EST.STAT.DATE<1,1>
      BEGIN CASE
      CASE STATUS = "BOOKED" AND EST.JOB.PURGE.DATE = ""
         NULL
      CASE 1
         GOSUB 2000
      END CASE
      GOTO 100
*
*---- PURGE ESTIMATE
*
2000*

********** Doc1 v
*P_X  = 0 ; P_Y = 21 ; P_VALUE = "Processing Estimate - ":EST.KEY ; P_OPT = "CL"
*CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
********** Doc1 ^

      DEL.KEY = EST.KEY
      DEL.DEPT.COMP = EST.DEPT.COMP
      DEL.SUBS = EST.SUBS
      IF DEL.SUBS # "" THEN
         SAVE.KEY = DEL.KEY
         SAVE.DEPT.COMP = DEL.DEPT.COMP
         SC = COUNT(DEL.SUBS,VM) + (DEL.SUBS # "")
         FOR SP = 1 TO SC
            DEL.KEY = DEL.SUBS<1,SP>
            MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE GOTO 2090
            GOSUB 3000
2090     NEXT SP
         DEL.KEY = SAVE.KEY
         DEL.DEPT.COMP = SAVE.DEPT.COMP
         MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE
            MAT EST.REC = ""
         END
      END
      GOSUB 3000
      RETURN
*
*---- DELETE ESTIMATE RECORDS
*
3000*
      DC = COUNT(DEL.DEPT.COMP,VM) + (DEL.DEPT.COMP # "")
      FOR DP = 1 TO DC
         DDKEY = DEL.KEY:"!":DEL.DEPT.COMP<1,DP>
         READU REC FROM ESTIMATE.DEPT, CONO:DDKEY ELSE
            RELEASE ESTIMATE.DEPT, CONO:DDKEY
            GOTO 3090
         END
         WRITE REC ON ESTIMATE.PURGE.BU, CONO:DDKEY
         DELETE ESTIMATE.DEPT, CONO:DDKEY
3090  NEXT DP
      MATWRITE EST.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY
      BEGIN CASE
      CASE EST.CUST = "P"
         CALL GEN_XREF_MAINT(CONO,DEL.KEY,EST.CUST.NAME,"",PROSPECT.EST.XREF,PREFIX)
      CASE 1
         CALL GEN_XREF_MAINT(CONO,DEL.KEY,EST.CUST,"",CUST.EST.XREF,PREFIX)
      END CASE
      DELETE ESTIMATE, CONO:DEL.KEY
*T25237 v
      MATREADU EST.RL.REC FROM ESTIMATE.RES, CONO:DEL.KEY THEN
         MATWRITE EST.RL.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY:'!RES'
         DELETE ESTIMATE.RES, CONO:DEL.KEY
      END
      READU OLD.REC FROM ESTIMATE.RL, CONO:DEL.KEY THEN
         WRITE OLD.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY:'!RL'
         DELETE ESTIMATE.RL, CONO:DEL.KEY
      END
*T25237 ^
      RETURN
99999*
	RETURN

