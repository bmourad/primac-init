   SUBROUTINE EPRIMAC_XREF_UPD(ERRMSG, IN_PARAM, OUT_PARAM, EPRIMAC_XREF)
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* DESCRIPTION  -
*ENDOC
*********************************************************************
*
$INCLUDE CPYLIB CHAR

*
*
******************
* INITIALIZATION *
******************
   ERRMSG = ""
   IF FILEINFO(EPRIMAC_XREF,0) = 0 THEN
      OPEN '','EPRIMAC_XREF' TO EPRIMAC_XREF ELSE
         ERRMSG = 'Cannot open EPRIMAC_XREF file!'
         RETURN
      END
   END
   CONO      = IN_PARAM<1>
   XREF_ID   = IN_PARAM<2>
   SORT_FLAG = IN_PARAM<3>
   ID_NO     = IN_PARAM<4>
   ID_DESC   = IN_PARAM<5>
   ID_GROUP  = IN_PARAM<6>
   ID_TYPE   = IN_PARAM<7>
   READU REC FROM EPRIMAC_XREF, CONO:XREF_ID ELSE REC = ""
   BEGIN CASE
   CASE ID_NO = "ALL" AND ID_GROUP # "" AND ID_DESC = "<DELETE>"
      LOOP
         LOCATE ID_GROUP IN REC<3>,1 SETTING PTR ELSE PTR = 0
      UNTIL PTR = 0 DO
         DEL REC<1,PTR>
         DEL REC<2,PTR>
         DEL REC<3,PTR>
         DEL REC<4,PTR>
      REPEAT
   CASE ID_NO = "ALL" AND ID_TYPE # "" AND ID_DESC = "<DELETE>"
      LOOP
         LOCATE ID_TYPE IN REC<4>,1 SETTING PTR ELSE PTR = 0
      UNTIL PTR = 0 DO
         DEL REC<1,PTR>
         DEL REC<2,PTR>
         DEL REC<3,PTR>
         DEL REC<4,PTR>
      REPEAT
   CASE 1
      LOCATE ID_NO IN REC<1>,1 SETTING PTR THEN
         DEL REC<1,PTR>
         DEL REC<2,PTR>
         DEL REC<3,PTR>
         DEL REC<4,PTR>
      END
   END CASE
   IF ID_DESC # "<DELETE>" THEN
      IF SORT_FLAG = '' THEN SORT_FLAG = "AR"
      LOCATE ID_NO IN REC<1> BY SORT_FLAG SETTING PTR ELSE
         INS ID_NO BEFORE REC<1,PTR>
         INS ""    BEFORE REC<2,PTR>
         INS ""    BEFORE REC<3,PTR>
         INS ""    BEFORE REC<4,PTR>
      END
      REC<2,PTR> = ID_NO:" ":ID_DESC
      REC<3,PTR> = ID_GROUP
      REC<4,PTR> = ID_TYPE
   END
   WRITE REC ON EPRIMAC_XREF, CONO:XREF_ID
   RETURN
