*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ARSBP
* PROGRAM     - ARC.EOM.CLOSE
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
*MODIFIED	- (06/01/06) BY ZAHOOR AHMED
*		   FOR printflag 
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB SALESDATES
$INCLUDE PMC.CPYLIB FISCAL
$INCLUDE PMC.CPYLIB EOM.TRANS
$INCLUDE PMC.CPYLIB EOM.TRANS.HIST              ; * T25975
$INCLUDE ARS.CPYLIB DAILY.CASH
$INCLUDE GLS.CPYLIB GLA
$INCLUDE PMC.CPYLIB COA
$INCLUDE GLS.CPYLIB COA.SYS
$INCLUDE PMC.CPYLIB POST.REJECTS
$INCLUDE CPYLIB CHAR
*
***** SETUP ERRMSG ROUTINE
*
ERRMSG=''
REJECT.RPT.PRT=''
*
* ERROR.FLAG is for any error from EOD_CLOSE_PreProcess & Printfalg is view/mail/print/file
ERROR.FLAG = ''
Printfalg = ''
EMAIL_ADDR = ''
*
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','PMCProperty',PMCProperty)
STATUS = RBO.getProperty('','PRINT_FLAG',PRINT.FLAG)
STATUS = RBO.getProperty('','ERR_FLAG',ERROR.FLAG)
STATUS = RBO.getProperty('','EmailAddress',EMAIL_ADDR)
CONO = ID[1,3]

STATUS = RBO.getProperty('','DM_Flag',D.M.FLG)
IF D.M.FLG = "PERIOD" THEN
   RPT.PER = "PERIOD"
END ELSE
   RPT.PER = "DAY"
END
STATUS = RBO.getProperty('','FR_CURR_PER',FR.CURR.PER)
STATUS = RBO.getProperty('','FR_CURR_DATE',FR.CURR.DATE)
STATUS = RBO.getProperty('','FR_NEXT_PER',FR.NEXT.PER)
STATUS = RBO.getProperty('','FR_NEXT_DATE',FR.NEXT.DATE)
STATUS = RBO.getProperty('','FR_PRINT',FR.PRINT)
STATUS = RBO.getProperty('','FR_CLOSE_DATE',FR.CLOSE.DATE)
STATUS = RBO.getProperty('','FR_D_M_FLG',FR.D.M.FLG)
STATUS = RBO.getProperty('','DivPos',POS)
FOR P = 1 TO DCOUNT(FR.CURR.PER,VM)
   IF INDEX(FR.CURR.DATE<1,P>,'/',1) > 0 THEN FR.CURR.DATE<1,P> = ICONV(FR.CURR.DATE<1,P>,'D2/')
   IF INDEX(FR.NEXT.DATE<1,P>,'/',1) > 0 THEN FR.NEXT.DATE<1,P> = ICONV(FR.NEXT.DATE<1,P>,'D2/')
   IF INDEX(FR.CLOSE.DATE<1,P>,'/',1) > 0 THEN FR.CLOSE.DATE<1,P> = ICONV(FR.CLOSE.DATE<1,P>,'D2/')
NEXT P
STATUS = RBO.getProperty('','DivCode',DIV.CODE)
IF DIV.CODE = '' THEN DIV.CODE = 'ALL'
*
*Get the printflag view/Email/File/Print
*
BEGIN CASE
   CASE PRINT.FLAG = 'PrintData'
	Printfalg = 'View'
   CASE PRINT.FLAG = 'EmailData'
	Printfalg = 'Email'
   CASE PRINT.FLAG = 'File'
	Printfalg = 'Print'
   CASE 1
	Printfalg = 'Print'
END CASE
*
**** INTITIALIZATION
*
TODAY = DATE()
UNKNOWN = "NOT ON FILE"
SYS.FISCAL = "ARCFISCAL"
PROG.ID = @TTY:"!":CONO:"!ARC.EOM"
NORETURN=0
*
***** OPEN FILES
*
TRANFILE='ARC.EOM.TRANS':CONO:DIV.CODE
REJECTFILE='ARC.POST.REJECTS':CONO:DIV.CODE
OPEN "","CONTROL" TO CONTROL ELSE
   ERRMSG="CONTROL FILE MISSING"
   GOTO 93000
END
OPEN "",TRANFILE TO EOM.TRANS ELSE
   ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD SAJ"
   GOTO 93000
END
OPEN "",REJECTFILE TO POST.REJECTS ELSE
   ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD SSS"
   GOTO 93000
END
OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
OPEN "","CONTROL" TO CONTROL ELSE ERRMSG="CONTROL FILE MISSING";GOTO 93000
OPEN "","SECURITY" TO SECURITY ELSE ERRMSG="SECURITY FILE MISSING";GOTO 93000
OPEN "","CHECK.XREF" TO CHECK.XREF ELSE ERRMSG="CHECK.XREF FILE IS MISSING";GOTO 93000
OPEN "","DAILY.CASH" TO DAILY.CASH ELSE ERRMSG="DAILY.CASH FILE IS MISSING";GOTO 93000
OPEN "","DAILY.CASH.TAG" TO DAILY.CASH.TAG ELSE ERRMSG="DAILY.CASH.TAG FILE IS MISSING";GOTO 93000
OPEN "","EOD.HIST" TO EOD.HIST ELSE ERRMSG="EOD.HIST FILE IS MISSING";GOTO 93000
OPEN "","ARC.EOM.TRANS.HIST" TO ARC.EOM.TRANS.HIST ELSE ERRMSG="ARC.EOM.TRANS.HIST FILE IS MISSING";GOTO 93000
**
*If ERROR.FLAG = 1 from EOD_CLOSE_PreProcess then call Generatereport_Sub
*
IF ERROR.FLAG = 1 THEN
 GOTO 90000
END
*
MATREAD COMP.REC FROM COMPANY,CONO ELSE
   ERRMSG = "CANNOT LOCATE COMPANY # ":CONO; GOTO 93000
END
IN.ACCT.LEN = LEN(CO.ACCT.PIC)
ACCT.LEN = IN.ACCT.LEN + 10
EQV.FLG=''
IF CO.GLS # "N" THEN
   IF CO.GL.LINK<1,1> = "Y" THEN
      OPEN "","GLA" TO GLA ELSE ERRMSG="GLA FILE IS MISSING";GOTO 93000
   END
   OPEN "","COA" TO COA ELSE ERRMSG="COA FILE IS MISSING";GOTO 93000
   EQV.FLG = 1
   OPEN "","COA.EQUIV" TO COA.EQUIV ELSE EQV.FLG = 0
END
READ SECURITY.REC FROM CONTROL, CONO:'DIV.SECURITY' ELSE
  SECURITY.REC = 'N'; SECURITY.REC<2> = 'N'
END
CLEARFILE POST.REJECTS
PRR.SEQ = 10000
IF CO.GLS = "N" THEN GOTO 8000
IF CO.GL.LINK<1,1> # "Y" THEN GOTO 8000
**** EOM.TRANS (1) ****
7000 *
ACCOUNTS = ""; AMOUNTS = ""
SELECT EOM.TRANS
WRITE "EOM.TRANS (1)" ON CONTROL , PROG.ID
MATREAD CSY.REC FROM CONTROL, CONO : "COA.SYS" ELSE MAT CSY.REC = ""
LOCATE "CR" IN CSY.SOURCE<1>,1 SETTING FND ELSE NULL
L.GLA = CSY.LEVEL<1,FND>
MAT GLA.REC = ""
GLA.DATE = TODAY
GLA.SRC = "CR"
GLA.MON = FR.CURR.PER<1,POS>
ODATE = " " : OCONV(FR.CURR.DATE<1,POS>,"D2/")
READU COUNTER FROM CONTROL, CONO : "GLACOUNTER" ELSE COUNTER = 1
WRITE "GLA ":COUNTER ON CONTROL, PROG.ID
DATA = 1
BEGIN CASE
   CASE L.GLA = "R"
      LOOP
         READNEXT ETR.ID ELSE DATA = 0
      WHILE DATA DO
         IF ETR.ID[1,3] # CONO THEN GOTO 7200
         MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7100
         TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
         ACCT = ETR.ID[1,ACCT.LEN]
         CUST.NO = FIELD(ETR.ID,"!",1)[ACCT.LEN+1,99]
         IF EQV.FLG = 1 THEN
            READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
            ACCT = N.ACCT
         END
         COA.ID = ACCT[11,IN.ACCT.LEN]
         MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
            MAT COA.REC = ""
            COA.DESC = UNKNOWN
         END
         BEGIN CASE
            CASE COA.DETAIL = "R" OR COA.DETAIL = "D"
               LOOP
                  COUNTER = COUNTER + 1
                  IF COUNTER > 999998 THEN COUNTER = 1
                  GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                  READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
               WHILE DUMMY # "" DO REPEAT
               GLA.DESC = COA.DESC "L#25" : ODATE
               GLA.AMT = ETR.AMT
               GLA.REF = CUST.NO
               MATWRITE GLA.REC ON GLA, GLA.ID
            CASE 1
               LOCATE ACCT IN ACCOUNTS,1 SETTING AFND ELSE
                  ACCOUNTS<AFND> = ACCT
                  AMOUNTS<AFND> = ""
               END
               IF TYPE = "D" THEN
                  AMOUNTS<AFND,1> = AMOUNTS<AFND,1> + ETR.AMT
               END ELSE
                  AMOUNTS<AFND,2> = AMOUNTS<AFND,2> + ETR.AMT
               END
         END CASE
7100     RELEASE EOM.TRANS, ETR.ID
7200  REPEAT
   CASE L.GLA = "D"
      LOOP
         READNEXT ETR.ID ELSE DATA = 0
      WHILE DATA DO
         IF ETR.ID[1,3] # CONO THEN GOTO 7400
         MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7300
         ACCT = ETR.ID[1,ACCT.LEN]
         TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
         CUST.NO = FIELD(ETR.ID,"!",1)[ACCT.LEN+1,99]
         IF EQV.FLG = 1 THEN
            READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
            ACCT = N.ACCT
         END
         COA.ID = ACCT[11,IN.ACCT.LEN]
         MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
            MAT COA.REC = ""
            COA.DESC = UNKNOWN
         END
         BEGIN CASE
            CASE COA.DETAIL = "D"
               TCNT = COUNT(ETR.TRAN,VM) + (ETR.TRAN # "")
               FOR I = 1 TO TCNT
                  LOOP
                     COUNTER = COUNTER + 1
                     IF COUNTER > 999998 THEN COUNTER = 1
                     GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                     READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                  WHILE DUMMY # "" DO REPEAT
                  GLA.DESC = ETR.RDATE<1,I> "L#8" : ETR.TRAN<1,I> : "-" : ETR.REF<1,I>
                  GLA.DESC = GLA.DESC "L#25" : " " : OCONV(ETR.DATE<1,I>,"D2/")
                  GLA.AMT = ETR.TAMT<1,I>
                  GLA.REF = CUST.NO
                  MATWRITE GLA.REC ON GLA, GLA.ID
               NEXT I
            CASE COA.DETAIL = "R"
               LOOP
                  COUNTER = COUNTER + 1
                  IF COUNTER > 999998 THEN COUNTER = 1
                  GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                  READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
               WHILE DUMMY # "" DO REPEAT
               GLA.DESC = COA.DESC "L#25" : ODATE
               GLA.AMT = ETR.AMT
               GLA.REF = CUST.NO
               MATWRITE GLA.REC ON GLA, GLA.ID
            CASE 1
               LOCATE ACCT IN ACCOUNTS,1 SETTING AFND ELSE
                  ACCOUNTS<AFND> = ACCT
                  AMOUNTS<AFND> = ""
               END
               IF TYPE = "D" THEN
                  AMOUNTS<AFND,1> = AMOUNTS<AFND,1> + ETR.AMT
               END ELSE
                  AMOUNTS<AFND,2> = AMOUNTS<AFND,2> + ETR.AMT
               END
         END CASE
7300     RELEASE EOM.TRANS, ETR.ID
7400  REPEAT
   CASE 1
      LOOP
         READNEXT ETR.ID ELSE DATA = 0
      WHILE DATA DO
         IF ETR.ID[1,3] # CONO THEN GOTO 7600
         MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7500
         ACCT = ETR.ID[1,ACCT.LEN]
         TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
         IF EQV.FLG = 1 THEN
            READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
            ACCT = N.ACCT
         END
         LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
            ACCOUNTS<FND> = ACCT
            AMOUNTS<FND> = ""
         END
         IF TYPE = "D" THEN
            AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
         END ELSE
            AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
         END
7500     RELEASE EOM.TRANS, ETR.ID
7600  REPEAT
END CASE
GLA.REF = ""
ACNT = COUNT(ACCOUNTS,AM)+(ACCOUNTS # "")
FOR I = 1 TO ACNT
   ACCT = ACCOUNTS<I>
   COA.ID = CONO : ACCT[11,IN.ACCT.LEN]
   MATREAD COA.REC FROM COA, COA.ID ELSE COA.DESC = UNKNOWN
   GLA.DESC = COA.DESC "L#25" : ODATE
   FOR J = 1 TO 2
      IF AMOUNTS<I,J> = "" THEN GOTO 7900
      LOOP
         COUNTER = COUNTER + 1
         IF COUNTER > 999998 THEN COUNTER = 1
         GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
         READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
      WHILE DUMMY # "" DO REPEAT
      GLA.AMT = AMOUNTS<I,J>
      MATWRITE GLA.REC ON GLA, GLA.ID
7900 *
   NEXT J
NEXT I
WRITE COUNTER ON CONTROL, CONO : "GLACOUNTER"
ACCOUNTS = ""
AMOUNTS = ""
**** EOM.TRANS (2) ****
8000 *
SELECT EOM.TRANS
WRITE "EOM.TRANS (2)" ON CONTROL , PROG.ID
DATA = 1
LOOP
   READNEXT ETR.ID ELSE DATA = 0
WHILE DATA DO
   IF CONO # ETR.ID[1,3] THEN GOTO 8599
   MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE
      RELEASE EOM.TRANS, ETR.ID
      GOTO 8599
   END
   CURR.PER = FR.CURR.PER<1,POS>
   MAT ETH.REC = MAT ETR.REC
   DONE = 0
   SEQ = 0
   ETH.ID = FIELD(ETR.ID,"*",1):"*":CURR.PER
   LOOP UNTIL DONE = 1 DO
      SEQ = SEQ + 1
      ETH.KEY = ETH.ID:"*":SEQ
      READ CHECKIT FROM ARC.EOM.TRANS.HIST, ETH.KEY ELSE DONE = 1
   REPEAT
   MATWRITE ETH.REC ON ARC.EOM.TRANS.HIST, ETH.KEY
   DELETE EOM.TRANS, ETR.ID
8599 *
REPEAT
WRITE "EOM.TRANS (3)" ON CONTROL, PROG.ID
IF SECURITY.REC<2> = 'Y' THEN EOD.HID = '!':DIV.CODE ELSE EOD.HID = ''
READU EOD.HIST.REC FROM EOD.HIST, CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID ELSE;* T23278
   EOD.HIST.REC = ""
END
EOD.HIST.REC = INSERT(EOD.HIST.REC,-1,0,0,TODAY)
WRITE EOD.HIST.REC ON EOD.HIST,CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID
SELECT EOM.TRANS
READNEXT ETR.ID ELSE
**** DAILY.CASH ****
   WRITE "DAILY.CASH" ON CONTROL, PROG.ID
   IF D.M.FLG = "PERIOD" THEN GOTO 8700
   SELECT DAILY.CASH.TAG
   DATA = 1
   LOOP
      READNEXT DC.ID ELSE DATA = 0
   WHILE DATA DO
      IF DC.ID[1,3] # CONO THEN GOTO 8600
      MATREADU DC.REC FROM DAILY.CASH, DC.ID ELSE
         RELEASE DAILY.CASH, DC.ID
         GOTO 8600
      END
      ICNT = DCOUNT(DC.CASH.APPL,VM)
      MCNT = DCOUNT(DC.AMT,VM)
      IF MCNT > ICNT THEN ICNT = MCNT
      FOR I = 1 TO ICNT 
         IF DC.CASH.APPL<1,I> # '' THEN
            JCNT = DCOUNT(DC.CASH.APPL<1,I>,SM)
         END ELSE
            JCNT = DCOUNT(DC.AMT<1,I>,SM)
         END
         FOR J = 1 TO JCNT 
            IF DC.DIV<1,I,J> = DIV.CODE OR DIV.CODE = 'ALL' THEN
               IF DC.GLA.DATE<1,I,J> = 'P' THEN
                  DC.GLA.DATE<1,I,J> = TODAY
               END
            END
         NEXT J
      NEXT I
      MATWRITE DC.REC ON DAILY.CASH, DC.ID
      DELETE.OK = 1
      FOR I = 1 TO ICNT UNTIL NOT(DELETE.OK)
         IF DC.CASH.APPL<1,I> # '' THEN
            JCNT = DCOUNT(DC.CASH.APPL<1,I>,SM)
         END ELSE
            JCNT = DCOUNT(DC.AMT<1,I>,SM)
         END
         FOR J = 1 TO JCNT UNTIL NOT(DELETE.OK)
            IF DC.GLA.DATE<1,I,J> = "" OR DC.GLA.DATE<1,I,J> = 'P' THEN DELETE.OK = 0
         NEXT J
      NEXT I
      IF DELETE.OK THEN
         DELETE DAILY.CASH.TAG, DC.ID
      END ;* T23278
8600  
   REPEAT
   GOTO 8999
8700 SELECT DAILY.CASH
   DATA = 1
   LOOP
      READNEXT DC.ID ELSE DATA = 0
   WHILE DATA DO
      IF DC.ID[1,3] # CONO THEN GOTO 8800
      MATREADU DC.REC FROM DAILY.CASH, DC.ID ELSE
         RELEASE DAILY.CASH, DC.ID
         GOTO 8800
      END
      DELETE.OK = 1
      ICNT = DCOUNT(DC.CASH.APPL,VM)
      MCNT = DCOUNT(DC.AMT,VM)
      IF MCNT > ICNT THEN ICNT = MCNT
      FOR I = 1 TO ICNT
         IF DC.CASH.APPL<1,I> # '' THEN
            JCNT = DCOUNT(DC.CASH.APPL<1,I>,SM)
         END ELSE
            JCNT = DCOUNT(DC.AMT<1,I>,SM)
         END
         FOR J = 1 TO JCNT
            IF DC.DIV<1,I,J> = DIV.CODE OR DIV.CODE = "ALL" THEN
               IF DC.GLA.DATE<1,I,J> # "" THEN
                  DC.GLA.DATE<1,I,J> = TODAY
               END
            END
            IF DC.GLA.DATE<1,I,J> = "" OR DC.GLA.DATE<1,I,J> = 'P' THEN DELETE.OK = 0
         NEXT J
      NEXT I
      IF DELETE.OK THEN
         DELETE DAILY.CASH, DC.ID
         DELETE DAILY.CASH.TAG, DC.ID
      END ELSE
         MATWRITE DC.REC ON DAILY.CASH, DC.ID
      END
8800 *
   REPEAT
**** CHECK.XREF ****
   SELECT CHECK.XREF
   WRITE "CHECK.XREF" ON CONTROL, PROG.ID
   DATA = 1
   LOOP
      READNEXT CHK.ID ELSE DATA = 0
   WHILE DATA DO
      IF CHK.ID[1,3] # CONO THEN GOTO 8900
      READU DUMMY FROM CHECK.XREF, CHK.ID ELSE
         RELEASE CHECK.XREF, CHK.ID
         GOTO 8900
      END
      DELETE CHECK.XREF, CHK.ID
8900 *
   REPEAT
8999 *
READ ARCFISCAL FROM CONTROL, CONO:"ARCFISCAL" ELSE ARCFISCAL = ""
   IF D.M.FLG = "PERIOD" THEN
      IF FR.NEXT.PER<1,POS> = "" THEN
       	FR.NEXT.PER<1,POS> = ARCFISCAL<3>
      END
      IF FR.NEXT.DATE<1,POS> = "" THEN
		FR.NEXT.DATE<1,POS> = ARCFISCAL<4>
      END
      FR.CURR.PER<1,POS> = FR.NEXT.PER<1,POS>
      FR.CURR.DATE<1,POS> = FR.NEXT.DATE<1,POS>
      FR.CLOSE.DATE<1,POS> = TODAY
      STATUS = RBO.setProperty('','FR_CURR_PER',FR.CURR.PER)
      STATUS = RBO.setProperty('','FR_CURR_DATE',FR.CURR.DATE)
      STATUS = RBO.setProperty('','FR_CLOSE_DATE',FR.CLOSE.DATE)
   END
   FR.NEXT.PER<1,POS> = ""; FR.NEXT.DATE<1,POS> = ""
   FR.PRINT<1,POS> = ""; FR.D.M.FLG<1,POS> = ""
   STATUS = RBO.setProperty('','FR_NEXT_PER',FR.NEXT.PER)
   STATUS = RBO.setProperty('','FR_NEXT_DATE',FR.NEXT.DATE)
   STATUS = RBO.setProperty('','FR_PRINT',FR.PRINT)
   STATUS = RBO.setProperty('','FR_D_M_FLG',FR.D.M.FLG)
   DELETE CONTROL, PROG.ID
   GOSUB 90000
   CMD='DELETE.FILE ': TRANFILE :' FORCE'
   UDTEXECUTE CMD CAPTURING JUNK
   IF REJECT.RPT.PRT='' THEN
      CMD='DELETE.FILE ': REJECTFILE : ' FORCE'
      UDTEXECUTE CMD CAPTURING JUNK
   END
   IF ERRMSG # '' THEN GOTO 93000
   GOTO 99999
END
NORETURN=1
90000*
READ JUNK FROM POST.REJECTS,'10001' THEN
  UserID = PMCProperty<1,3>
  IF D.M.FLG = 'DAY' THEN RPTID = 'AR302' ELSE RPTID = 'AR304'
   IN_PARAM = UserID:"!ARCEOMC!111!":RPTID:"!":CONO:"!Print"
   * IN_PARAM = UserID:"!ARCEOMC!111!":RPTID:"!":CONO:"!":Printfalg
   IN_PARAM<2> = ''
  IN_PARAM<3> = 'EOD.REJECT.LISTING'
   IN_PARAM<4> = 'P'
   IN_PARAM<5> = 'PMCPROCS'
   IN_PARAM<6> = REJECTFILE
   IN_PARAM<7> = 'REJECTS'
   OUT_PARAM=''
   UDTEXECUTE 'SSELECT ':REJECTFILE CAPTURING JUNK
   CALL GenerateReport_Sub(ERRMSG,IN_PARAM,OUT_PARAM)
   REJECT.RPT.PRT='WARNING !!! MAKE SURE TO PICKUP CLOSING REJECTS REPORT FROM PRINTER'
   STATUS=RBO.setProperty('','RejectRptPrinted',REJECT.RPT.PRT)
   STATUS=RBO.setProperty('','ReportImage',OUT_PARAM<2>)
END ELSE
	IF Printfalg # "Print" THEN	ERRMSG  = "There is no data to ":Printfalg
	*GOTO 93000
END
IF NOT(NORETURN) THEN RETURN
FR.PRINT<1,POS> = "X";* T23278
STATUS = RBO.setProperty('','FR_PRINT',FR.PRINT)
ERRMSG = "E R R O R !!! CANNOT LOCATE ALL TRANSACTIONS"
93000 WRITEV ERRMSG ON CONTROL, PROG.ID,2
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
*** END OF JOB ***
99999 
MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
END
