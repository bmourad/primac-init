  SUBROUTINE APCRM_PREREAD
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - APCRM_PREREAD
*
* AUTHOR   - ALEJANDRO DELGADO
*
* DATE     - 01/23/2003
*
* DESCRIPTION
*
* Data is retrieve from the APCHECK file to load display through RBO.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
  $INCLUDE WWINSERT RBO.H
  $INCLUDE APS.CPYLIB APCHECK
  $INCLUDE PMC.CPYLIB COMPANY
  $INCLUDE PMC.CPYLIB VEND
  $INCLUDE CPYLIB FILE.VARS
  $INCLUDE CPYLIB CHAR
*
*---- INITIALIZATION
*
  STATUS = RBO.getProperty('','ID',ID)
  STATUS = RBO.getProperty('','DIV_NO',DIV.NO)
  STATUS = RBO.getProperty('','START_CHECK_NO',START.CHECK.NO)
  STATUS = RBO.getProperty('','END_CHECK_NO',END.CHECK.NO)
  STATUS = RBO.getProperty('','ACCOUNT_NO',ACCOUNT.NO)
  CONO = ID[1,3]
*
*---- OPEN FILES
*
  IF FILEINFO(APCHECK,0) = 0 THEN
    OPEN '','APCHECK' TO APCHECK ELSE
      ERRMSG = 'Cannot open APCHECK file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(VEND,0) = 0 THEN
    OPEN '','VEND' TO VEND ELSE
      ERRMSG = 'Cannot open VEND file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(COMPANY,0) = 0 THEN
    OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = 'Cannot open COMPANY file!'
      GOTO SET.ERROR
    END
  END
*
*---- MAIN PROCESS
*
  MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ''
  SYSLIST = ''
  CMD = 'SSELECT APCHECK WITH CHECK-NO >= "':START.CHECK.NO
  CMD := '" AND WITH CHECK-NO <= "':END.CHECK.NO
  CMD := '" AND WITH BANK = "':ACCOUNT.NO
  CMD := '" AND WITH CO_ = "':CONO:'"'
  IF DIV.NO # 'ALL' THEN
    CMD := ' AND WITH DIV = "':DIV.NO:'"'
  END

STATUS = RBO.setProperty('','TEST',CMD)
  PERFORM CMD RTNLIST SYSLIST CAPTURING RESPONSE
  *
  SAPC.IDS = ''
  SAPC.CHECK = ''
  SAPC.VEND = ''
  SAPC.VEND.DESC = ''
  SAPC.DATE = ''
  SAPC.CHK.AMT = ''
  SAPC.CHK.AMT.PAID = ''
  SAPC.C.FLAG = ''
  SAPC.REM = ''
  TOT.AMT = '' ; TOT.AMT.PAID = ''
  MV.CNT = 0
  LOOP.DONE = 1
  LOOP
    READNEXT ID FROM SYSLIST ELSE LOOP.DONE = 0
  WHILE LOOP.DONE = 1 DO
    MATREAD APCK.REC FROM APCHECK , ID ELSE CONTINUE
    MV.CNT = MV.CNT + 1
    IF APCK.VEND<1,2> = "" THEN
      MATREAD VEND.REC FROM VEND , CONO:APCK.VEND<1,1> ELSE
        VEND.DESC = "????????????"
      END
    END ELSE
      VEND.DESC = APCK.VEND<1,2>
    END
    SAPC.IDS<1,MV.CNT> = ID
    SAPC.CHECK<1,MV.CNT> = ID[4+LEN(CO.ACCT.PIC),6]
    SAPC.VEND<1,MV.CNT> = APCK.VEND<1,1>
    SAPC.VEND.DESC<1,MV.CNT> = VEND.DESC
    SAPC.DATE<1,MV.CNT> = OCONV(APCK.DATE,'D2/')
    SAPC.CHK.AMT<1,MV.CNT> = OCONV(APCK.AMT,'MD2')
    SAPC.CHK.AMT.PAID<1,MV.CNT> = OCONV(APCK.AMT.PAID,'MD2')
    IF APCK.SEQ = '' THEN
      SAPC.C.FLAG<1,MV.CNT> = 'N'
    END ELSE
      SAPC.C.FLAG<1,MV.CNT> = "Y"
    END
    SAPC.REM<1,MV.CNT> = APCK.COMM
    TOT.AMT = TOT.AMT + APCK.AMT
    TOT.AMT.PAID = TOT.AMT.PAID + APCK.AMT.PAID
  REPEAT
  TOT.AMT = OCONV(TOT.AMT, 'MD2')
  TOT.AMT.PAID = OCONV(TOT.AMT.PAID, 'MD2')
  *
  STATUS = RBO.setProperty('','SAPC_IDS',SAPC.IDS)
  STATUS = RBO.setProperty('','SAPC_CHECK',SAPC.CHECK)
  STATUS = RBO.setProperty('','SAPC_VEND',SAPC.VEND)
  STATUS = RBO.setProperty('','SAPC_VEND_DESC',SAPC.VEND.DESC)
  STATUS = RBO.setProperty('','SAPC_DATE',SAPC.DATE)
  STATUS = RBO.setProperty('','SAPC_CHK_AMT',SAPC.CHK.AMT)
  STATUS = RBO.setProperty('','SAPC_CHK_AMT_PAID',SAPC.CHK.AMT.PAID)
  STATUS = RBO.setProperty('','SAPC_C_FLAG',SAPC.C.FLAG)
  STATUS = RBO.setProperty('','SAPC_REM',SAPC.REM)
  STATUS = RBO.setProperty('','TOT_AMT',TOT.AMT)
  STATUS = RBO.setProperty('','TOT_AMT_PAID',TOT.AMT.PAID)
  RETURN
*
SET.ERROR: 
*
  STATUS = RBO.setProperty('','ServerStatus',1)
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  RETURN
END
