SUBROUTINE JOBINQ_MT_DET_GetData
********************************************************************************
*   Program name :- JOBINQ_MT_DET_GetData
*   Created:- 12/16/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'MT Detail' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.MATL
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR

DEFFUN CalcStkQty(COST.QTY,MAT INV.CNV.REC,ROND,LN)
;* open files
ERRMSG=''
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(JOB.MATL,0)=0 THEN
   OPEN '','JOB.MATL' TO JOB.MATL ELSE
      ERRMSG<1,-1>='JOB.MATL FILE IS MISSING'
   END
END
IF FILEINFO(INVENTORY,0)=0 THEN
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
  DIM TRANS.REC(11)
  EQU TRAN.WHSE     TO TRANS.REC(1)
  EQU TRAN.LOC      TO TRANS.REC(2)
  EQU TRAN.TYPE     TO TRANS.REC(3)
  EQU TRAN.QTY      TO TRANS.REC(4)
  EQU TRAN.NO       TO TRANS.REC(5)
  EQU TRAN.DATE     TO TRANS.REC(6)
  EQU TRAN.COST     TO TRANS.REC(7)
  EQU TRAN.SALE     TO TRANS.REC(8)
  EQU TRAN.JOB      TO TRANS.REC(9)
  EQU TRAN.PERIOD   TO TRANS.REC(10)
  EQU TRAN.TRAN     TO TRANS.REC(11)
MAT TRANS.REC = ''
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)
STATUS=RBO.getProperty('','DEPT',DEPT)
STATUS=RBO.getProperty('','CCTR',CCTR)
STATUS=RBO.getProperty('','PROD',PROD)
STATUS=RBO.getProperty('','WHSE',WHSE)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='MT'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


   SUB.CNT=DCOUNT(JOB.SUBS,VM)+1
END ELSE SUB.CNT=1
PTR=1
LOOP
   LOCATE DEPT IN JOB.MT.DEPT<1>,PTR SETTING FND ELSE
      PTR = 0
   END
   IF PTR # 0 THEN
      BEGIN CASE
         CASE CCTR = JOB.MT.CCTR<1,FND>
            IF PROD = JOB.MT.PROD<1,FND> AND WHSE = JOB.MT.WHSE<1,FND> THEN
               PTR = FND
               FND = 0
            END ELSE
               PTR = FND + 1
            END
         CASE 1
            PTR = FND + 1
      END CASE
   END
WHILE PTR AND FND DO
REPEAT
IF NOT(PTR) THEN
   ERRMSG='Cannot locate Dept/Cctr/Prod/Whse ':DEPT:' ':CCTR:' ':PROD:' ':WHSE:' in Job record!'
   GOTO 93000
END
MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT ELSE DEPT.DESC = UNKNOWN
MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR ELSE CCTR.DESC = UNKNOWN
MATREAD INV.REC FROM INVENTORY,CONO:JOB.MT.PROD<1,PTR> ELSE
   MAT INV.REC = ""
   INV.FULL.DESC = UNKNOWN
END
$INCLUDE ICSBP INV.UM.CNV
ROND=''; LN=''
LN = 1; OLN = 1; LINES = JOB.MT.SEQ<1,PTR,1>
MT.ID = CONO:JOB_NO:"!":JOB.MT.DEPT<1,PTR>:"!":JOB.MT.CCTR<1,PTR>
MT.ID = MT.ID:"!":JOB.MT.PROD<1,PTR>:"!":JOB.MT.WHSE<1,PTR>:"!"
GOSUB 100
FOR K = 2 TO SUB.CNT
   MT.ID = CONO:JOB.SUBS<1,K-1>:"!":JOB.MT.DEPT<1,PTR>:"!":JOB.MT.CCTR<1,PTR>
   MT.ID = MT.ID:"!":JOB.MT.PROD<1,PTR>:"!":JOB.MT.WHSE<1,PTR>:"!"
   OLN = LINES + 1; LINES = LINES + JOB.MT.SEQ<1,PTR,K>
   GOSUB 100
NEXT K

STATUS=RBO.setProperty('','DEPT_DESC',DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_NAME',CCTR.DESC)
STATUS=RBO.setProperty('','PROD_NAME',INV.FULL.DESC)
STATUS=RBO.setProperty('','LOC',TRAN.LOC)
STATUS=RBO.setProperty('','TYPE',TRAN.TYPE)
STATUS=RBO.setProperty('','MTL_TRAN',TRAN.NO)
STATUS=RBO.setProperty('','DATE',TRAN.DATE)
STATUS=RBO.setProperty('','PERIOD',TRAN.PERIOD)
STATUS=RBO.setProperty('','QTY',TRAN.QTY)
STATUS=RBO.setProperty('','COST',TRAN.COST)
STATUS=RBO.setProperty('','SALE',TRAN.SALE)
STATUS=RBO.setProperty('','RCLS_JOB',TRAN.JOB)
GOTO 99999

100 FOR I = OLN TO LINES
   QTY_VALUE = ''
   MATREAD JMT.REC FROM JOB.MATL, MT.ID:(I-OLN+1) ELSE MAT JMT.REC = ''
   TRAN.LOC<1,I> = JMT.LOC
   TRAN.TYPE<1,I> = JMT.TYPE
***** COMMENTED THE BELOW THREE LINES AND REPLACED THE VALUE FOR TRAN.QTY WITH QTY_VALUE
   *JMT.QTY = CalcStkQty(JMT.QTY,MAT INV.CNV.REC,ROND,OLN)
   *JMT.QTY = OCONV(JMT.QTY,ICR.CNV1)
   *TRAN.QTY<1,I> = JMT.QTY
   IF JMT.QTY < 0 THEN
     QTY_VALUE = OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5),ICR.CNV)
   END ELSE
     QTY_VALUE = OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5),ICR.CNV)
   END
   TRAN.QTY<1,I> = QTY_VALUE
   TRAN.NO<1,I> = JMT.SEQ
   TRAN.DATE<1,I> = OCONV(JMT.DATE,'D2/')
   TRAN.COST<1,I> = OCONV(JMT.COST,'MD2,')
   TRAN.SALE<1,I> = OCONV(JMT.SALE,'MD2,')
   TRAN.JOB<1,I> = JMT.RC.JOB
   TRAN.PERIOD<1,I> = JMT.MON<1,1>
NEXT I
RETURN


93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN
