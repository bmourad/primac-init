SUBROUTINE ORDINQ_JR_POSTREAD
********************************************************************************
*   Program name :- ORDINQ_JR_POSTREAD
*   Created:- 12/04/2002
*   Author:- L. Ross
*   Gets info for Order Inquiry 'JR' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE OPS.CPYLIB ORDER.DETAIL.INQ
$INCLUDE OPS.CPYLIB ORDER.JOB

;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(ORDER,0)=0 THEN
  OPEN '','ORDER' TO ORDER ELSE
    ERRMSG<1,-1>='ORDER FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.DETAIL,0)=0 THEN
  OPEN '','ORDER.DETAIL' TO ORDER.DETAIL ELSE
    ERRMSG<1,-1>='ORDER.DETAIL FILE IS MISSING'
  END
END
IF ERRMSG#'' THEN GOTO 93000

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
STATUS=RBO.getProperty('','JOBNO',JOBNO)
CONO=ID[1,3]
ORDNO=ID[4,99]

;* get database values

MATREAD ORD.REC FROM ORDER,ID ELSE
  ERRMSG='Cannot read ORDER ':ID:' record!'
  GOTO 93000
END
SCNT=DCOUNT(ORD.SHIP.TO,VM)
MAT ORD.DET.INQ=""
FOR S=1 TO SCNT
  SHPNO=ORD.SHIP.TO<1,S>
  OSD.ID=CONO:ORDNO:"!":SHPNO
  MATREAD ORD.DET.REC FROM ORDER.DETAIL, OSD.ID ELSE
    MAT ORD.DET.REC=""
  END
  PCNT=DCOUNT(OSD.PROD,VM)
  FOR P=1 TO PCNT
    PTR=1
    LOOP
      LOCATE OSD.PROD<1,P> IN ODQ.PROD<1>,PTR SETTING L THEN
        IF (OSD.WHSE<1,P>=ODQ.WHSE<1,L>) AND (OSD.KIT<1,P>=ODQ.KIT<1,L>) AND (OSD.PROD.SEQ<1,P>=ODQ.PROD.SEQ<1,L>) THEN
          VCNT=DCOUNT(OSD.JOB<1,P>,SVM)
          FOR V=1 TO VCNT
            LOCATE OSD.JOB<1,P,V> IN ODQ.JOB<1,L>,1 SETTING FND ELSE
              ODQ.JOB<1,L,-1>=OSD.JOB<1,P,V>
              ODQ.JOB.QTY<1,L,-1>=OSD.JOB.QTY<1,P,V>
            END
          NEXT V
          PTR=0
        END
      END ELSE
        ODQ.PROD<1,L>=OSD.PROD<1,P>
        ODQ.WHSE<1,L>=OSD.WHSE<1,P>
        ODQ.KIT<1,L>=OSD.KIT<1,P>
        ODQ.PROD.SEQ<1,L>=OSD.PROD.SEQ<1,P>
        ODQ.JOB<1,L>=OSD.JOB<1,P>
        ODQ.JOB.QTY<1,L>=OSD.JOB.QTY<1,P>
        PTR=0
      END
    WHILE PTR DO
      PTR=L+1
    REPEAT
  NEXT P
NEXT S
LOCATE JOBNO IN ORD.JOB<1>,1 SETTING SPTR ELSE
  ORD.JOB<1,SPTR> = JOBNO
END
MAT ORD.JOB.REC = ""
LCNT = DCOUNT(ODQ.PROD,VM)
FOR L = 1 TO LCNT
  JPTR = 1
  LOOP
    LOCATE JOBNO IN ODQ.JOB<1,L>,JPTR SETTING JL THEN
      PTR = 1
      LOOP
        LOCATE ODQ.PROD<1,L> IN OJD.PROD<1>,PTR SETTING P ELSE
          OJD.PROD<1,P> = ODQ.PROD<1,L>
          OJD.WHSE<1,P> = ODQ.WHSE<1,L>
          OJD.A.QTY<1,P> = 0
        END
        IF OJD.WHSE<1,P> = ODQ.WHSE<1,L> THEN
          OJD.A.QTY<1,P> = OJD.A.QTY<1,P> + ODQ.JOB.QTY<1,L,JL>
          PTR = 0
        END
      WHILE PTR DO
        PTR = P + 1
      REPEAT
    END ELSE
      JPTR = 0
    END
  WHILE JPTR DO
    JPTR = JL + 1
  REPEAT
NEXT L
PCNT = DCOUNT(OJD.PROD,VM)
PRODDESCS=''
FOR P = 1 TO PCNT
  MATREAD INV.REC FROM INVENTORY,CONO:OJD.PROD<1,P> ELSE
    INV.FULL.DESC = 'Unknown'; INV.UNIT<1,3> = 'EA'; INV.M.LINE = 'FNGD'
  END
  CALL GetInvUMCnv(ERRMSG,IN_PARAM,OUT_PARAR,MAT INV.CNV.REC,MAT INV.REC)
  PRODDESCS<1,P> = INV.FULL.DESC
  OJD.A.QTY<1,P> = OCONV(INT(((OJD.A.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)"R#10"
NEXT P

STATUS=RBO.setProperty('','ProdCodes',OJD.PROD)
STATUS=RBO.setProperty('','WhseCodes',OJD.WHSE)
STATUS=RBO.setProperty('','ProdDescs',PRODDESCS)
STATUS=RBO.setProperty('','Manufact',OJD.A.QTY)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG<1,-1>)  

99999
RETURN

