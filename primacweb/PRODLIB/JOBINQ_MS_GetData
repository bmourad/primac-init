SUBROUTINE JOBINQ_MS_GetData
********************************************************************************
*   Program name :- JOBINQ_MS_GetData
*   Created:- 12/16/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'MS' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB OPERATION
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TOT.REC
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(INVENTORY,0)=0 THEN
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF FILEINFO(OPERATION,0)=0 THEN
   OPEN '','OPERATION' TO OPERATION ELSE
      ERRMSG<1,-1>='OPERATION FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM CCTR(10)
EQU CCTR.NO    TO CCTR(1)
EQU OPER.NO    TO CCTR(2)
EQU OPER.NAME  TO CCTR(3)
EQU OPER.DATE  TO CCTR(4)
EQU OPER.COST  TO CCTR(5)
EQU OPER.SALE  TO CCTR(6)
EQU CCTR.NAME  TO CCTR(7)
MAT CCTR = ''
DIM OTYPE(5)
OTYPE(1) = 'R,'
OTYPE(2) = 'N,'
OTYPE(3) = 'O,'
OTYPE(4) = 'S,'
OTYPE(5) = 'C'
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='MS'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


END
MAT JOB.TOT.REC = ''
JPTR = 0
JCNT = 0
CNT = COUNT(JOB.MS.DEPT,VM) + (JOB.MS.DEPT # '')
FOR I = 1 TO CNT
   TEMP = JOB.MS.DEPT<1,I>
   LOCATE TEMP IN MS.DEPT.NO<1>,1 SETTING JCNT ELSE
      JPTR = JPTR + 1; JCNT = JPTR
      MS.DEPT.NO<1,JCNT> = TEMP
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:TEMP ELSE DEPT.DESC = UNKNOWN
      MS.DEPT.DESC<1,JCNT> = DEPT.DESC
   END
   MS.DEPT.PTR<1,JCNT,-1> = I
   MS.DEPT.CHG<1,JCNT> = MS.DEPT.CHG<1,JCNT> + JOB.MS.TCOST<1,I,1>
   MS.DEPT.NCHG<1,JCNT> = MS.DEPT.NCHG<1,JCNT> + JOB.MS.TCOST<1,I,2>
   MS.DEPT.COST<1,JCNT> = MS.DEPT.COST<1,JCNT> + JOB.MS.COST<1,I>
   MS.DEPT.SALE<1,JCNT> = MS.DEPT.SALE<1,JCNT> + JOB.MS.SALE<1,I>
NEXT I
FOR I = 1 TO JPTR
   MS.TOT.CHG = MS.TOT.CHG + MS.DEPT.CHG<1,I>
   MS.TOT.NCHG = MS.TOT.NCHG + MS.DEPT.NCHG<1,I>
   MS.TOT.COST = MS.TOT.COST + MS.DEPT.COST<1,I>
   MS.TOT.SALE = MS.TOT.SALE + MS.DEPT.SALE<1,I>
NEXT I
LINES = COUNT(MS.DEPT.NO,VM) + (MS.DEPT.NO # '')
FOR I = 1 TO LINES
   CNT = COUNT(MS.DEPT.PTR<1,I>,SVM) + (MS.DEPT.PTR<1,I> # '')
   FOR J = 1 TO CNT
      PTR = MS.DEPT.PTR<1,I,J>
      CCTR.NO<1,I,J> = JOB.MS.CCTR<1,PTR>
      OPER.NO<1,I,J> = JOB.MS.OPER<1,PTR>
      MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR.NO<1,I,J> ELSE CCTR.DESC = UNKNOWN
      MATREAD OPER.REC FROM OPERATION, CONO : OPER.NO<1,I,J> ELSE 
         MATREAD INV.REC FROM INVENTORY, CONO : OPER.NO<1,I,J> ELSE INV.FULL.DESC = UNKNOWN
         OPER.DESC = INV.FULL.DESC
      END
      CCTR.NAME<1,I,J> = CCTR.DESC
      OPER.NAME<1,I,J> = OPER.DESC
      OPER.DATE<1,I,J> = OCONV(JOB.MS.DATE<1,PTR,1>,'D2/')
      OPER.COST<1,I,J> = OCONV(JOB.MS.COST<1,PTR>,'MD2,')
      OPER.SALE<1,I,J> = OCONV(JOB.MS.SALE<1,PTR>,'MD2,')
   NEXT J
   MS.DEPT.CHG<1,I> = OCONV(MS.DEPT.CHG<1,I>,'MD2,')
   MS.DEPT.NCHG<1,I> = OCONV(MS.DEPT.NCHG<1,I>,'MD2,')
   MS.DEPT.COST<1,I> = OCONV(MS.DEPT.COST<1,I>,'MD2,')
   MS.DEPT.SALE<1,I> = OCONV(MS.DEPT.SALE<1,I>,'MD2,')
NEXT I
STATUS=RBO.setProperty('','MS_DEPT_NO',MS.DEPT.NO)
STATUS=RBO.setProperty('','MS_DEPT_CHG',MS.DEPT.CHG)
STATUS=RBO.setProperty('','MS_TOT_CHG',OCONV(MS.TOT.CHG,'MD2,'))
STATUS=RBO.setProperty('','MS_DEPT_NCHG',MS.DEPT.NCHG)
STATUS=RBO.setProperty('','MS_TOT_NCHG',OCONV(MS.TOT.NCHG,'MD2,'))
STATUS=RBO.setProperty('','MS_DEPT_COST',MS.DEPT.COST)
STATUS=RBO.setProperty('','MS_TOT_COST',OCONV(MS.TOT.COST,'MD2,'))
STATUS=RBO.setProperty('','MS_DEPT_SALE',MS.DEPT.SALE)
STATUS=RBO.setProperty('','MS_TOT_SALE',OCONV(MS.TOT.SALE,'MD2,'))
STATUS=RBO.setProperty('','MS_DEPT_DESC',MS.DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_NO',CCTR.NO)
STATUS=RBO.setProperty('','OPER_NO',OPER.NO)
STATUS=RBO.setProperty('','OPER_DATE',OPER.DATE)
STATUS=RBO.setProperty('','OPER_COST',OPER.COST)
STATUS=RBO.setProperty('','OPER_SALE',OPER.SALE)
STATUS=RBO.setProperty('','CCTR_NAME',CCTR.NAME)
STATUS=RBO.setProperty('','OPER_NAME',OPER.NAME)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN

