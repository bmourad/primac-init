SUBROUTINE ORDINQ_I_POSTREAD
********************************************************************************
*   Program name :- ORDINQ_I_POSTREAD
*   Created:- 12/03/2002
*   Author:- L. Ross
*   Gets info for Order Inquiry 'I' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE OPS.CPYLIB ORDER.DETAIL.INQ
$INCLUDE OPS.CPYLIB ORDER.RELEASE

;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(ORDER,0)=0 THEN
  OPEN '','ORDER' TO ORDER ELSE
    ERRMSG<1,-1>='ORDER FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.DETAIL,0)=0 THEN
  OPEN '','ORDER.DETAIL' TO ORDER.DETAIL ELSE
    ERRMSG<1,-1>='ORDER.DETAIL FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.RELEASE,0)=0 THEN
  OPEN '','ORDER.RELEASE' TO ORDER.RELEASE ELSE
    ERRMSG<1,-1>='ORDER.RELEASE FILE IS MISSING'
  END
END
IF ERRMSG#'' THEN GOTO 93000

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
ORDNO=ID[4,99]
GEN.SHPNO = '000'

;* get database values

MATREAD ORD.REC FROM ORDER,ID ELSE
  ERRMSG='Cannot read ORDER ':ID:' record!'
  GOTO 93000
END
SCNT=DCOUNT(ORD.SHIP.TO,VM)
MAT ORD.DET.INQ=""
FOR S=1 TO SCNT
  SHPNO=ORD.SHIP.TO<1,S>
  OSD.ID=CONO:ORDNO:"!":SHPNO
  MATREAD ORD.DET.REC FROM ORDER.DETAIL, OSD.ID ELSE
    MAT ORD.DET.REC=""
  END
  PCNT=DCOUNT(OSD.PROD,VM)
  FOR P=1 TO PCNT
    PTR=1
    LOOP
      LOCATE OSD.PROD<1,P> IN ODQ.PROD<1>,PTR SETTING L THEN
        IF (OSD.WHSE<1,P>=ODQ.WHSE<1,L>) AND (OSD.KIT<1,P>=ODQ.KIT<1,L>) AND (OSD.PROD.SEQ<1,P>=ODQ.PROD.SEQ<1,L>) THEN
          ODQ.G.QTY<1,L>=ODQ.G.QTY<1,L>+OSD.G.QTY<1,P>
          ODQ.O.QTY<1,L>=ODQ.O.QTY<1,L>+OSD.O.QTY<1,P>
          ODQ.R.QTY<1,L>=ODQ.R.QTY<1,L>+OSD.R.QTY<1,P>
          ODQ.A.QTY<1,L>=ODQ.A.QTY<1,L>+OSD.A.QTY<1,P>
          ODQ.F.QTY<1,L>=ODQ.F.QTY<1,L>+OSD.F.QTY<1,P>
          ODQ.S.QTY<1,L>=ODQ.S.QTY<1,L>+OSD.S.QTY<1,P>
          ODQ.I.QTY<1,L>=ODQ.I.QTY<1,L>+OSD.I.QTY<1,P>
          ODQ.KIT.O.QTY<1,L>=ODQ.KIT.O.QTY<1,L>+OSD.KIT.O.QTY<1,P>
          ODQ.KIT.R.QTY<1,L>=ODQ.KIT.R.QTY<1,L>+OSD.KIT.R.QTY<1,P>
          ODQ.KIT.A.QTY<1,L>=ODQ.KIT.A.QTY<1,L>+OSD.KIT.A.QTY<1,P>
          ODQ.KIT.F.QTY<1,L>=ODQ.KIT.F.QTY<1,L>+OSD.KIT.F.QTY<1,P>
          ODQ.KIT.S.QTY<1,L>=ODQ.KIT.S.QTY<1,L>+OSD.KIT.S.QTY<1,P>
          ODQ.KIT.I.QTY<1,L>=ODQ.KIT.I.QTY<1,L>+OSD.KIT.I.QTY<1,P>
          ODQ.KIT.G.QTY<1,L>=ODQ.KIT.G.QTY<1,L>+OSD.KIT.G.QTY<1,P>
          ODQ.JOB<1,L>=OSD.JOB<1,P>
          PTR=0
        END
      END ELSE
        ODQ.PROD<1,L>=OSD.PROD<1,P>
        ODQ.WHSE<1,L>=OSD.WHSE<1,P>
        ODQ.G.QTY<1,L>=OSD.G.QTY<1,P>
        ODQ.O.QTY<1,L>=OSD.O.QTY<1,P>
        ODQ.R.QTY<1,L>=OSD.R.QTY<1,P>
        ODQ.A.QTY<1,L>=OSD.A.QTY<1,P>
        ODQ.F.QTY<1,L>=OSD.F.QTY<1,P>
        ODQ.S.QTY<1,L>=OSD.S.QTY<1,P>
        ODQ.I.QTY<1,L>=OSD.I.QTY<1,P>
        ODQ.KIT<1,L>=OSD.KIT<1,P>
        ODQ.PROD.SEQ<1,L>=OSD.PROD.SEQ<1,P>
        ODQ.JOB<1,L>=OSD.JOB<1,P>
        ODQ.KIT.O.QTY<1,L>=OSD.KIT.O.QTY<1,P>
        ODQ.KIT.R.QTY<1,L>=OSD.KIT.R.QTY<1,P>
        ODQ.KIT.A.QTY<1,L>=OSD.KIT.A.QTY<1,P>
        ODQ.KIT.F.QTY<1,L>=OSD.KIT.F.QTY<1,P>
        ODQ.KIT.S.QTY<1,L>=OSD.KIT.S.QTY<1,P>
        ODQ.KIT.I.QTY<1,L>=OSD.KIT.I.QTY<1,P>
        ODQ.KIT.G.QTY<1,L>=OSD.KIT.G.QTY<1,P>
        PTR=0
      END
    WHILE PTR DO
      PTR=L+1
    REPEAT
  NEXT P
NEXT S
PROD.SCRN = ""
WHSE.SCRN = ""
ORDER.GQTY = ""
ORDER.OQTY = ""
ORDER.IQTY = ""
ORDER.FQTY = ""
ORDER.SHIP = ""
ORDER.ALLO = ""
ORDER.RESV = ""
ORDER.JOB = ""
NUM.PROD.INQ = DCOUNT(ODQ.PROD,VM)
FOR I = 1 TO NUM.PROD.INQ
  IF ODQ.KIT<1,I> # "M" THEN
    LOCATE ODQ.PROD<1,I> IN PROD.SCRN<1>,1 BY 'AR' SETTING INDX  ELSE
      PROD.SCRN = INSERT(PROD.SCRN,1,INDX,0,ODQ.PROD<1,I>)
      WHSE.SCRN = INSERT(WHSE.SCRN,1,INDX,0,ODQ.WHSE<1,I>)
      ORDER.GQTY = INSERT(ORDER.GQTY,1,INDX,0,"")
      ORDER.OQTY = INSERT(ORDER.OQTY,1,INDX,0,"")
      ORDER.IQTY = INSERT(ORDER.IQTY,1,INDX,0,"")
      ORDER.FQTY = INSERT(ORDER.FQTY,1,INDX,0,"")
      ORDER.SHIP = INSERT(ORDER.SHIP,1,INDX,0,"")
      ORDER.ALLO = INSERT(ORDER.ALLO,1,INDX,0,"")
      ORDER.RESV = INSERT(ORDER.RESV,1,INDX,0,"")
      ORDER.JOB = INSERT(ORDER.JOB,1,INDX,0,"")
    END
    IF ODQ.KIT<1,I> = "N" THEN
      ORDER.GQTY<1,INDX> = ORDER.GQTY<1,INDX> + ODQ.G.QTY<1,I>
      ORDER.OQTY<1,INDX> = ORDER.OQTY<1,INDX> + ODQ.O.QTY<1,I>
      ORDER.IQTY<1,INDX> = ORDER.IQTY<1,INDX> + ODQ.I.QTY<1,I>
      ORDER.FQTY<1,INDX> = ORDER.FQTY<1,INDX> + ODQ.F.QTY<1,I>
      ORDER.SHIP<1,INDX> = ORDER.SHIP<1,INDX> + ODQ.S.QTY<1,I>
      ORDER.ALLO<1,INDX> = ORDER.ALLO<1,INDX> + ODQ.A.QTY<1,I>
      ORDER.RESV<1,INDX> = ORDER.RESV<1,INDX> + ODQ.R.QTY<1,I>
      ORDER.JOB<1,INDX>  = ODQ.JOB<1,I>
    END ELSE
      ORDER.GQTY<1,INDX> = ORDER.GQTY<1,INDX> + ODQ.KIT.G.QTY<1,I>
      ORDER.OQTY<1,INDX> = ORDER.OQTY<1,INDX> + ODQ.KIT.O.QTY<1,I>
      ORDER.IQTY<1,INDX> = ORDER.IQTY<1,INDX> + ODQ.KIT.I.QTY<1,I>
      ORDER.FQTY<1,INDX> = ORDER.FQTY<1,INDX> + ODQ.KIT.F.QTY<1,I>
      ORDER.SHIP<1,INDX> = ORDER.SHIP<1,INDX> + ODQ.KIT.S.QTY<1,I>
      ORDER.ALLO<1,INDX> = ORDER.ALLO<1,INDX> + ODQ.KIT.A.QTY<1,I>
      ORDER.RESV<1,INDX> = ORDER.RESV<1,INDX> + ODQ.KIT.R.QTY<1,I>
    END
  END
NEXT I
PRODDESCS=''
LINE_STATUS=''
PCNT=DCOUNT(PROD.SCRN,VM)
FOR P = 1 TO PCNT
  MATREAD INV.REC FROM INVENTORY,CONO:PROD.SCRN<1,P> ELSE
    INV.FULL.DESC = 'Unknown'; INV.UNIT<1,3> = 'EA'; INV.M.LINE = 'FNGD'
  END
  CALL GetInvUMCnv(ERRMSG,IN_PARAM,OUT_PARAR,MAT INV.CNV.REC,MAT INV.REC)
  PRODDESCS<1,P> = INV.FULL.DESC
  ORDER.GQTY<1,P> = OCONV(INT(((ORDER.GQTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) 'R#10'
  ORDER.RESV<1,P> = OCONV(INT(((ORDER.RESV<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) 'R#10'
  ORDER.ALLO<1,P> = OCONV(INT(((ORDER.ALLO<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) 'R#10'
  ORDER.SHIP<1,P> = OCONV(INT(((ORDER.SHIP<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) 'R#10'
  IF ORDER.JOB<1,P,2> = "" THEN
    ORDER.JOB<1,P> = ORDER.JOB<1,P>'L#8'
  END ELSE
    ORDER.JOB<1,P> = 'MULTI' 'L#8'
  END
  BEGIN CASE
    CASE ORDER.IQTY<1,P> >= ORDER.OQTY<1,P>
      LINE_STATUS<1,P> = "I"
    CASE ORDER.SHIP<1,P> >= ORDER.OQTY<1,P>
      LINE_STATUS<1,P> = "S"
    CASE ORDER.ALLO<1,P> < 1
      LINE_STATUS<1,P> = "O"
    CASE ORDER.FQTY<1,P> >= ORDER.ALLO<1,P>
      LINE_STATUS<1,P> = "F"
    CASE 1
      LINE_STATUS<1,P> = "M"
  END CASE
NEXT P

STATUS=RBO.setProperty('','ProdCodes',PROD.SCRN)
STATUS=RBO.setProperty('','WhseCodes',WHSE.SCRN)
STATUS=RBO.setProperty('','ProdDescs',PRODDESCS)
STATUS=RBO.setProperty('','Ordered',ORDER.GQTY)
STATUS=RBO.setProperty('','Reserved',ORDER.RESV)
STATUS=RBO.setProperty('','Shipped',ORDER.SHIP)
STATUS=RBO.setProperty('','Allocated',ORDER.ALLO)
STATUS=RBO.setProperty('','Job_Nos',ORDER.JOB)
STATUS=RBO.setProperty('','Line_Status',LINE_STATUS)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG<1,-1>)  

99999
RETURN

