SUBROUTINE JOBINQ_FG_GetData
********************************************************************************
*   Program name :- JOBINQ_FG_GetData
*   Created:- 12/11/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'FG' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE OPS.CPYLIB JOB.FNGD.STATS

DEFFUN CalcStkQty(COST.QTY,MAT INV.CNV.REC,ROND,LN)
;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
  OPEN '','JOB' TO JOB ELSE
    ERRMSG<1,-1>='JOB FILE IS MISSING'
  END
END
IF FILEINFO(JOB.FNGD.STATS,0)=0 THEN
  OPEN '','JOB.FNGD.STATS' TO JOB.FNGD.STATS ELSE
    ERRMSG<1,-1>='JOB.FNGD.STATS FILE IS MISSING'
  END
END
IF ERRMSG#'' THEN GOTO 93000

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
  ERRMSG='Cannot read JOB record ':ID
  GOTO 93000
END
JSTATDESC=''
CALL JOB.STATUS.SUB(JOB.STATUS,JOB.TRACK.DATE,JSTATDESC)
MATREAD JFS.REC FROM JOB.FNGD.STATS,ID ELSE
  ERRMSG='Cannot read JOB.FNGD.STATS record ':ID
  GOTO 93000
END
PCNT=DCOUNT(JFS.PROD,VM)
PRODDESCS=''; UMS=''
BALANCE=''
FOR P = 1 TO PCNT
  MATREAD INV.REC FROM INVENTORY,CONO:JFS.PROD<1,P> ELSE
    INV.FULL.DESC = 'Unknown'
  END
  $INCLUDE ICSBP INV.UM.CNV
  ROND=''; LN=''
  PRODDESCS<1,P> = INV.FULL.DESC
  UMS<1,P> = INV.UNIT<1,2>
  JFS.M.QTY<1,P> = OCONV(INT(((JFS.M.QTY<1,P>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
  JFS.A.QTY<1,P> = OCONV(INT(((JFS.A.QTY<1,P>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
  JFS.ORD.QTY<1,P> = OCONV(INT(((JFS.ORD.QTY<1,P>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
  JFS.F.QTY<1,P> = OCONV(INT(((JFS.F.QTY<1,P>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV) 

  IF JOB.STATUS<1,1> = 4 OR JOB.STATUS<1,1> > 5 THEN
    BALANCE<1,P> = 0
  END ELSE
    BALANCE<1,P> = JFS.M.QTY<1,P> - JFS.F.QTY<1,P> - JFS.S.QTY<1,P>
  END
  IF BALANCE<1,P> < 0 THEN BALANCE<1,P> = 0
  BALANCE<1,P> = CalcStkQty(BALANCE<1,P>,MAT INV.CNV.REC,ROND,LN)
  IF JFS.ORDER<1,P,2> # '' THEN
    JFS.ORDER<1,P> = 'MULTI'
  END
NEXT P

STATUS=RBO.setProperty('','ProdDescs',PRODDESCS)
STATUS=RBO.setProperty('','Ums',UMS)
STATUS=RBO.setProperty('','Balance',BALANCE)
STATUS=RBO.setProperty('','JobStatusDesc',JSTATDESC)
STATUS=RBO.setProperty('','Order',JFS.ORDER)
STATUS=RBO.setProperty('','JFS_M_QTY',JFS.M.QTY)
STATUS=RBO.setProperty('','JFS_F_QTY',JFS.F.QTY)
STATUS=RBO.setProperty('','JFS_A_QTY',JFS.A.QTY)
*STATUS=RBO.setProperty('','Order',JFS.ORD.QTY)


GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999
RETURN

