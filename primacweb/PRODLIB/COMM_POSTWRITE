SUBROUTINE COMM_POSTWRITE
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - COMM_POSTWRITE
*
* AUTHOR   - ALEJANDRO DELGADO
*
* DATE     - 01/30/2003
*
* DESCRIPTION
*
* Data is retrieve from the COMMISSION file to load display through RBO.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
$INCLUDE WWINSERT RBO.H
$INCLUDE JCS.CPYLIB COMMISSION
$INCLUDE JCS.CPYLIB JOB
$INCLUDE CPYLIB FILE.VARS
$INCLUDE CPYLIB CHAR
*
*---- INITIALIZATION
*
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','COM_JOB',COM.JOB)
STATUS = RBO.getProperty('','COM_DUE',COM.DUE)
STATUS = RBO.getProperty('','COM_PAID',COM.PAID)
CONO = ID[1,3]
*
*---- OPEN FILES
*
IF FILEINFO(JOB,0) = 0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG = 'Cannot open JOB file!'
      GOTO SET.ERROR
   END
END
IF FILEINFO(COMMISSION,0) = 0 THEN
   OPEN '','COMMISSION' TO COMMISSION ELSE
      ERRMSG = 'Cannot open COMMISSION file!'
      GOTO SET.ERROR
   END
END
*
*---- MAIN PROCESS
*
CR.CNT = COUNT(COM.JOB,VM) + (COM.JOB # "")
FOR W = 1 TO CR.CNT
   JOB.NO = COM.JOB<1,W>
   MATREADU COMM.REC FROM COMMISSION,CONO:JOB.NO ELSE
      RELEASE COMMISSION,CONO:JOB.NO
      CONTINUE
   END
   COMM.DUE = COM.DUE<1,W>
   COMM.DUE = ICONV(COMM.DUE,'MD2')
   COMM.ADJ = COMM.DUE - COMM.CALC
   COMM.PAID = COM.PAID<1,W>
   COMM.PAID = ICONV(COMM.PAID,'MD2')
   COMM.PAID.DATE = DATE()
   IF COMM.DIV = "" THEN
      MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE MAT JOB.REC = ""
      COMM.DIV = JOB.DIV
   END
   MATWRITE COMM.REC ON COMMISSION,CONO:JOB.NO
NEXT W
RETURN
*
SET.ERROR: 
*
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
RETURN
END
