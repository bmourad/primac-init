SUBROUTINE INV_AVG_COST_MAINT(MAT IWH.REC,COST.WT,CUR.PERIOD)
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - EPRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV_AVG_COST_MAINT
* BY          - KHAJA ZIAUDDIN
* DATE        - 06/16/2003  (mm/dd/yyyy)
* DESCRIPTION
* This program calculates the average inventory receipt cost based
* upon all receipts for this product as stored in the FIFO buckets.
*
*********************************************************************
*
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INV.WHSE

*
*---- INITIALIZATION
*
      TOT.QTY = 0
      TOT.COST = 0
      IF COST.WT + 0 = 0 THEN COST.WT = 100
*
*---- MAIN PROCESS
*
      CNT = COUNT(IWH.PO.NO.FI,@VM) + (IWH.PO.NO.FI # "")
      IF CNT = 0 THEN CNT = 1     ;* P538 RLB
      FOR I = 1 TO CNT
	IF IWH.RECP.PERIOD<1,I> <= CUR.PERIOD THEN
         TOT.QTY = TOT.QTY + (IWH.QTY.FI<1,I>/1000)
         TOT.COST = TOT.COST + ((IWH.COST.FI<1,I>/10000)*(IWH.QTY.FI<1,I>/1000))
	END ELSE
      	  TOT.QTY = TOT.QTY + (IWH.ORG.FI<1,I>/1000)
         TOT.COST = TOT.COST + ((IWH.COST.FI<1,I>/10000)*(IWH.ORG.FI<1,I>/1000))
    	END
      NEXT I
      BEGIN CASE
        CASE TOT.QTY < 0
         IWH.AVG.COST = INT(((TOT.COST / TOT.QTY) * 10000) - 0.5)
        CASE TOT.QTY > 0
         IWH.AVG.COST = INT(((TOT.COST / TOT.QTY) * 10000) + 0.5)
      END CASE
999999*
      RETURN
   END


