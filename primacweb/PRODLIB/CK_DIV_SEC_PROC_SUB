SUBROUTINE CK_DIV_SEC_PROC_SUB
********************************************************************************
*   Program name :- CK_DIV_SEC_PROC_SUB
*   Created:- 6/3/2003
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
*  Cono,DIV_CODE,USER_ID
*
* Out Properties:
* ---------------
*  This program validates user access for a specific
*  division.  It is called by a proc. If the SEC.DIVISION
*  attribute is null, the user has unrestricted access
*  (i.e., can process ALL divisions).
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB SECURITY

* Insert method code here
ERRMSG = ''
OPEN '','CONTROL' TO CONTROL ELSE
	ERRMSG = "CONTROL FILE IS MISSING"
      	GOTO 99999
END
OPEN '','SECURITY' TO SECURITY ELSE
	ERRMSG = "SECURITY FILE IS MISSING"
      	GOTO 99999
END
OPEN '','DIVISION' TO F.DIV ELSE
	ERRMSG = "DIVISION FILE IS MISSING"
      	GOTO 99999
END

STATUS = RBO.getProperty('','Cono',CONO)
STATUS = RBO.getProperty('','DIV_CODE',DIV.CODE)
STATUS = RBO.getProperty('','USER_ID',USER.ID)
USER.ID = UPCASE(USER.ID)
  
XX<1> = CONO
XX<99> = DIV.CODE

IF DIV.CODE # "ALL" THEN
	READV DESC FROM F.DIV,CONO:DIV.CODE,1 ELSE
		ERRMSG = "INVALID ID TRY AGAIN !"
	      	GOTO 99999
	END
END

STATUS = RBO.setProperty('','DIV_CODE',DESC)
READ XYZ FROM CONTROL, CONO:"DIV.SECURITY" ELSE GOTO 99999
IF XYZ<1> # 'Y' THEN GOTO 99999
MATREAD SEC.REC FROM SECURITY, CONO:USER.ID ELSE
	ERRMSG = "USER ":USER.ID:" NOT ON FILE"
      	GOTO 99999
END
  
*
IF SEC.DIVISION = "" THEN GOTO 99999
LOCATE DIV.CODE IN SEC.DIVISION<1>,1 SETTING FOUND THEN GOTO 99999 ELSE
	ERRMSG = "USER ":USER.ID:" NOT AUTHORIZED FOR DIVISION ":DIV.CODE
END
STATUS = RBO.setProperty('','INPUT_BUFF',LOWER(XX))
  
* End of method code
99999
IF (TRIM(ERRMSG) # "") THEN
	STATUS = RBO.setProperty('','ServerStatus','1')
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
END ELSE
       STATUS = RBO.setProperty('','ServerStatus','')
       STATUS = RBO.setProperty('','ServerMessage','')
END 
RETURN

