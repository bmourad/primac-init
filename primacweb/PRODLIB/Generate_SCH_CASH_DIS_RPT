SUBROUTINE Generate_SCH_CASH_DIS_RPT
********************************************************************************
*   Program name :- Generate_SCH_CASH_DIS_RPT
*   Created:- 3/27/2006
*   Auther:-Sk. zahoor Ahmed
*   Description :-This program list all vouchers scheduled to be paid to
*                   a vendor with a due date spacified.
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

$INCLUDE APS.CPYLIB OAP
$INCLUDE PMC.CPYLIB VEND
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB GLTABLE
$INCLUDE CPYLIB CHAR
   DIM SVEND.REC(10)
   EQU SVEND.NO TO SVEND.REC(1)
   EQU SVEND.DS TO SVEND.REC(2)
   EQU SVEND.PT TO SVEND.REC(3)
**********************************
   STATUS = RBO.getProperty('','DIV_CODE',SEL.DIV)
   STATUS = RBO.getProperty('','FISCAL_PERIOD',FISCAL.PERIOD)
   STATUS = RBO.getProperty('','ID',PPSD_ID)
   STATUS = RBO.getProperty('','PAYMENT_DATE',PAY_DATE)
   PAID.DATE=ICONV(PAY_DATE,"D2")
*
   UserID = FIELD(PPSD_ID, "!", 1) ;* User ID
   PPSID = FIELD(PPSD_ID, "!", 2) ;* PMC_PROCESS ID(ASP file name)
   SessionID = FIELD(PPSD_ID, "!", 3) ;* IE Session ID
   PRPT_ID = FIELD(PPSD_ID, "!", 4) ;* PMC_REPORTS ID
   Cono = FIELD(PPSD_ID, "!", 5)
   CONO = Cono
   ERRMSG = ''
   **** INITIALZATION
   LINE.COUNT = 99 ; PAGE.NO = 0
   TOT.GRS = 0 ; TOT.PAD = 0 ; TOT.DSC = 0 ; TOT.NET = 0
   GTOT.GRS = 0 ; GTOT.PAD = 0 ; GTOT.DSC = 0 ; GTOT.NET = 0
   SP1 = " "
   PREV.VEND = '' ; PRT.VEND.FLG = 1
   C.LINE1 = SPACE(47):STR("*",72)
   C.LINE2 = SPACE(47):"*  THIS VENDOR HAS A NEGATIVE (OR ZERO) BALANCE AND WILL NOT BE PAID!! *"
   
**** OPEN ALL FILES
   OPEN "","OAP" TO OAP ELSE ERRMSG = "OAP FILE IS MISSING" ; GOTO 99999
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING";GOTO 99999
   OPEN "","VEND" TO VEND ELSE ERRMSG = "VENDOR FILE IS MISSING" ; GOTO 99999
**** GET VALUES FROM CONTROL FILES
*T26493 v
*  READV PAID.DATE FROM CONTROL , CONO:"PPAID.DATE",1 ELSE ERRMSG = "NO REPORT DATE";GOTO 99999
*  PAID.DATE=ICONV(PAID.DATE,"D2")
*T26493 ^
   MATREAD SVEND.REC FROM CONTROL , CONO:"VENDORS.PRT" ELSE FLG = 1 ; MAT SVEND.REC = ''
***** SETUP HEADING
*
*T26493 v
*  CONO.NAME = X<2>
*  REPORT.NAME = "SCHEDULED.CASH.DISBURSEMENTS JOURNAL"
*  REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = PRPT_ID
*T26493 *
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   STATUS = RBO.setProperty('','USER_ID',UserID)
   CALL GET_REPORT_PROG_HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
**** OPEN ALL FILES
*
* T25759 v
*  HD3 = "VENDOR #           VENDOR NAME          VOUCH# INVOICE NO INV.DATE DUE DATE   GROSS AMT     PAID AMT      DISC AMT     NET AMOUNT  "
   HD3 = "VENDOR #   VENDOR NAME/REMIT COMMENTS   VOUCH# INVOICE NO INV.DATE DUE DATE   GROSS AMT     PAID AMT      DISC AMT     NET AMOUNT  "
* T25759 ^
   HD4 = "-------- ------------------------------ ------ ---------- -------- -------- ------------- ------------- ------------- -------------"
   HD5 = "                                                                            ------------- ------------- ------------- -------------"
   HD6 = "                                                                            ============= ============= ============= ============="
* 
***Build and Execute Select Query
* 
   STR = "SSELECT OAP BY VEND_ WITH BAL-OPEN NE '0' AND WITH DUE.STAT # 'HOLD' AND WITH VND.STAT # 'H' AND "
   STR = STR : "WITH DUE.DATE LE '" : OCONV(PAID.DATE,"D2/") :"' AND WITH CO_ = '" : CONO :"' AND WITH PERIOD LE "
   STR = STR : '"' : FISCAL.PERIOD :'"'
   IF SEL.DIV # "ALL" THEN
	STR = STR :" AND WITH VCH.DIV.OWNER = '" : SEL.DIV :"'"
   END
   *EXECUTE THE METHOD GET_SELNAME WITH SCD.RPT ASSIGNED TO IT AND USE THAT IN SAVE-LIST
   STATUS = RBO.setProperty('','LIST_NAME','SCD.RPT')
   STATUS=RBO.callMethod('','GET_SELNAME')
   STATUS = RBO.getProperty('','LIST_NAME',LIST.NAME)
   UDTEXECUTE "DELETE.LIST ":LIST.NAME
   UDTEXECUTE STR CAPTURING STRMSG
   UDTEXECUTE "SAVE-LIST " : LIST.NAME ;* save list to get back the contents in cash.disb
   UDTEXECUTE "GET-LIST " : LIST.NAME
   *****
*
   PRINTER ON
*
**** READ AND PROCESS FIRST REPORD
*
100*
   READNEXT ID ELSE
      PRINTER OFF
      ERRMSG = "NO ITEMS SELECETED" ; GOTO 99999
   END
   MATREAD OAP.REC FROM OAP, ID ELSE GOTO 100
   PREV.VEND = OAP.VEND<1,1>
   IF OAP.VEND<1,2> # "" THEN
      VEND.DESC = OAP.VEND<1,2>
   END ELSE
      MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
         VEND.DESC = "VEND NOT ON FILES"
      END
   END
   GOSUB 1000
**** PROCESS ALL RECORDS
200*
   DATA = 1
   LOOP
   READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
   MATREAD OAP.REC FROM OAP , ID ELSE GOTO 299
   GOSUB 900
   GOSUB 1000
299REPEAT
   IF PRT.VEND.FLG = 1 THEN
      GOSUB 2000
   END
   GOSUB 3000
   DELETE CONTROL , CONO:"VENDORS.PRT"
   GOTO 99999
*
**** FIND BREAK
*
900*
   IF OAP.VEND<1,1> # PREV.VEND THEN
      IF PRT.VEND.FLG = 1 THEN
         GOSUB 2000
      END 
      PRT.VEND.FLG = 1
      PRT.FLG = 1
      TOT.GRS = 0 ; TOT.PAD = 0 ; TOT.DSC = 0 ; TOT.NET = 0
      PREV.VEND = OAP.VEND<1,1>
      IF OAP.VEND<1,2> # "" THEN
         VEND.DESC = OAP.VEND<1,2>
      END ELSE
         MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
            VEND.DESC = "VEND NOT ON FILES"
         END
      END
   END
   RETURN
*
**** PRINT VOUCHER LINES AND ADD VENDOR TOTALS
*
1000*
   IF OAP.DUE.DATE > PAID.DATE THEN GOTO 1999
   IF ABS(OAP.GRS.AMT - OAP.DSC.AMT) <= ABS(OAP.PAID.AMT) THEN GOTO 1999
   LOCATE OAP.VEND<1,1> IN SVEND.NO<1>,1 SETTING VFND ELSE VFND = 0
   BEGIN CASE
   CASE VFND = 0 AND SVEND.PT = "P"
      PRT.VEND.FLG = 0
      GOTO 1999
   CASE VFND # 0 AND SVEND.PT # "P"
      PRT.VEND.FLG = 0
      GOTO 1999
   END CASE
   IF LINE.COUNT > 55 THEN
      GOSUB 8000
      PRT.FLG = 1
   END
   P.LINE = ""
* T25759 v
*  IF PRT.FLG = 1 THEN
*     P.LINE = P.LINE: OAP.VEND<1,1> "L#8":SP1
*     P.LINE = P.LINE: VEND.DESC "L#30":SP1
*     PRT.FLG = 0
*  END ELSE
*     P.LINE = SPACE(40)
*  END
   IF PRT.FLG = 1 THEN
      P.LINE := OAP.VEND<1,1> "L#8":SP1
      P.LINE := VEND.DESC "L#30":SP1
      PRINT P.LINE
      PRT.FLG = 0
      LINE.COUNT += 1
   END
   P.LINE = ""
   P.LINE = SPACE(9)
   P.LINE = P.LINE:OAP.REM.COMM "L#30":SP1 ; *T25759
   P.LINE = P.LINE:ID[4,6] "L#6":SP1
   P.LINE = P.LINE:OAP.INV "L#10":SP1
   P.LINE = P.LINE:OCONV(OAP.INV.DATE, "D2/") "R#8":SP1
   P.LINE = P.LINE:OCONV(OAP.DUE.DATE, "D2/") "L#8":SP1
   P.LINE = P.LINE:OCONV(OAP.GRS.AMT , "MD2,") "R#13":SP1
   P.LINE = P.LINE:OCONV(OAP.PAID.AMT, "MD2,") "R#13":SP1
   P.LINE = P.LINE:OCONV(OAP.DSC.AMT , "MD2,") "R#13":SP1
   P.LINE = P.LINE:OCONV(OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.AMT, "MD2,") "R#13"
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   TOT.GRS = TOT.GRS + OAP.GRS.AMT
   TOT.PAD = TOT.PAD + OAP.PAID.AMT
   TOT.DSC = TOT.DSC + OAP.DSC.AMT
   TOT.NET = TOT.NET + (OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.AMT)
1999RETURN
*
**** PRINT VENDOR TOTAL LINE
*
2000*
   IF LINE.COUNT + 3 > 55 THEN
      GOSUB 8000
   END
   IF TOT.NET < 1 THEN
      PRINT C.LINE1
      PRINT C.LINE2
      PRINT C.LINE1
      IF SVEND.PT = 'S' THEN
         VNO.CNT = COUNT(SVEND.NO,VM) + (SVEND.NO # "") + 1
         SVEND.NO<1,VNO.CNT> = PREV.VEND
         SVEND.DS<1,VNO.CNT> = VEND.DESC
      END ELSE
         LOCATE PREV.VEND IN SVEND.NO<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN
            SVEND.NO = DELETE(SVEND.NO,1,FND,0)
            SVEND.DS = DELETE(SVEND.DS,1,FND,0)
         END
      END
   END ELSE
      PRINT HD5
      T.LINE = ""
      T.LINE = T.LINE:SPACE(53):"VENDOR "
      T.LINE = T.LINE:PREV.VEND "L#8"
      T.LINE = T.LINE:"  TOTAL "
      T.LINE = T.LINE:OCONV(TOT.GRS, "MD2,") "R#13":SP1
      T.LINE = T.LINE:OCONV(TOT.PAD, "MD2,") "R#13":SP1
      T.LINE = T.LINE:OCONV(TOT.DSC, "MD2,") "R#13":SP1
      T.LINE = T.LINE:OCONV(TOT.GRS - TOT.PAD - TOT.DSC, "MD2,") "R#13"
      PRINT T.LINE
      PRINT
      GTOT.GRS = GTOT.GRS + TOT.GRS
      GTOT.PAD = GTOT.PAD + TOT.PAD
      GTOT.DSC = GTOT.DSC + TOT.DSC
      GTOT.NET = GTOT.NET + TOT.NET
   END
   LINE.COUNT = LINE.COUNT + 3
   TOT.GRS = 0 ; TOT.PAD = 0 ; TOT.DSC = 0 ; TOT.NET = 0
   RETURN
*
***** PRINT GRAND TOTAL
*
3000*
      IF LINE.COUNT + 2 >  55 THEN
         GOSUB 8000
      END
      PRINT HD6
      T.LINE = ""
      T.LINE = T.LINE:SPACE(63):"GRAND  TOTAL "
      T.LINE = T.LINE:OCONV(GTOT.GRS, "MD2,") "R#13":SP1
      T.LINE = T.LINE:OCONV(GTOT.PAD, "MD2,") "R#13":SP1
      T.LINE = T.LINE:OCONV(GTOT.DSC, "MD2,") "R#13":SP1
      T.LINE = T.LINE:OCONV(GTOT.GRS - GTOT.PAD - GTOT.DSC, "MD2,") "R#13"
      PRINT T.LINE
   RETURN
*
**** PRINT HEADING
*
8000*
     PRINT CHAR(12)
     PAGE.NO = PAGE.NO + 1
     PRINT HD1:PAGE.NO
     PRINT HD2
     PRINT "DIVISION : ":SEL.DIV   ;* T20016
     PRINT SPACE(52):"SCHEDULED THROUGH : ":PAY_DATE
     PRINT
     PRINT HD3
     PRINT HD4
     LINE.COUNT = 7
     RETURN
99999
   PRINTER OFF
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','ServerStatus','E')
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END ELSE
       STATUS = RBO.setProperty('','ServerStatus','')
       STATUS = RBO.setProperty('','ServerMessage','')
  END 
RETURN

