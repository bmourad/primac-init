SUBROUTINE OPSCTNL_PRINT
********************************************************************************
*   Auther	   :-Sk. Zahoor Ahmed
*   Program name :- OPSCTNL_PRINT
*   Created:- 12/27/2005
*   Description:- This program will print the CARTON LABEL.
*
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB>COMPANY
$INCLUDE OPS.CPYLIB>CARTON
$INCLUDE OPS.CPYLIB>ORDER
$INCLUDE PMC.CPYLIB>SHIP.TO
$INCLUDE CPYLIB>CHAR
$INCLUDE PMC.CPYLIB SECURITY
*---- OPEN ALL FILES
*
ERRMSG = ''
OPEN '','SECURITY' TO SECURITY ELSE
      ERRMSG = 'Cannot open SECURITY file!'
      GOTO 99999
END
OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = 'Cannot open COMPANY file!'
      GOTO 99999
END
OPEN '','WIN.PRINTERS' TO WIN.PRINTERS ELSE
      ERRMSG = 'Cannot open WIN.PRINTERS file!'
      GOTO 99999
END

OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'Cannot open CONTROL file!'
      GOTO 99999
END
OPEN '', 'BOL'     TO BOL ELSE
      ERRMSG = 'Cannot open BOL file!'
      GOTO 99999
END
OPEN '', 'CARTON'  TO CARTON ELSE
      ERRMSG = 'Cannot open CARTON file!'
      GOTO 99999
END
OPEN '', 'CUSTOMER' TO CUSTOMER ELSE
      ERRMSG = 'Cannot open CUSTOMER file!'
      GOTO 99999
END
OPEN '', 'ORDER'   TO ORDER ELSE
      ERRMSG = 'Cannot open ORDER file!'
      GOTO 99999
END
OPEN '', 'SHIP.TO' TO SHIP.TO ELSE
      ERRMSG = 'Cannot open SHIP.TO file!'
      GOTO 99999
END
*
*---- INITIALIZATION
*
STATUS=RBO.getProperty('','PMCProperty',PMCProperty)
STATUS=RBO.getProperty('','ID',ID)
CONO= PMCProperty<1,4>
ID=ID[4,99]
STATUS=RBO.getProperty('','ORD_BOL',BOL.NUM)
STATUS=RBO.getProperty('','BOL_SHIP_TO',CARTON.SHIP.TO)
STATUS=RBO.getProperty('','START_LABEL',START.LABEL.NUMBER)
STATUS=RBO.getProperty('','END_LABEL',END.LABEL.NUMBER)
STATUS=RBO.getProperty('','CARTON_NEEDED',CARTON.NEED)
CARTON.ID = CONO:"!":ID:"!":BOL.NUM
MATREAD  CARTON.REC FROM CARTON,CARTON.ID ELSE MAT CARTON.REC = ""
*
*TO RETURN IF NO DATA
*
IF (ID=0 OR ID="") THEN
	RETURN
END
UserID = FIELD(PMCProperty, VM, 3) ;* User ID
MATREAD SEC.REC FROM SECURITY, CONO:UPCASE(UserID) ELSE
      ERRMSG = "USER ":UserID:" NOT ON FILE":PMCProperty
      GOTO 99999
END
MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "USER ":UserID:" NOT ON FILE"
      GOTO 99999
END
   CALL SYSVARS.SUB(PORT_NO)
   OS.TYPE = UPCASE(SYSTEM(33))

SETPTR_QUEUE = SEC.DFLT.QUEUE
SETPTR_CMD = ""
*
*--- SET PRINTER
*
 BEGIN CASE
   CASE OS.TYPE[1,7] = "WINDOWS"	
  	READ WIN.PRINTERS.REC FROM WIN.PRINTERS,SETPTR_QUEUE THEN
         SETPTR_FORMATSTR = "Orientation=":WIN.PRINTERS.REC<5>:","
         SETPTR_FORMATSTR := "Font=":WIN.PRINTERS.REC<6>:","
         SETPTR_FORMATSTR := "FontSize=":WIN.PRINTERS.REC<7>:","
         SETPTR_FORMATSTR := "TopMargin=":WIN.PRINTERS.REC<8>:","
         SETPTR_FORMATSTR := "BottomMargin=":WIN.PRINTERS.REC<9>
*
         SETPTR_CMD = 'SETPTR 0,132,,,,1,AT '
         SETPTR_CMD := WIN.PRINTERS.REC<3>
         SETPTR_CMD := ',"':SETPTR_FORMATSTR:'",NHEAD,BRIEF'
      END
   CASE OS.TYPE[1,7] = "UNIX"
      	  SETPTR_CMD = 'SETPTR ,,,,,,NHEAD,BRIEF,DEST '
      	  SETPTR_CMD := SETPTR_QUEUE:',NOMESSAGE'
 END CASE
       UDTEXECUTE SETPTR_CMD CAPTURING JUNK
*---- PRINT LABELS
*
4000 *
 PRINTER ON
  MATREAD ORD.REC FROM ORDER,CONO:ID ELSE
    MAT ORD.REC = ""
  END
 MATREAD SPT.REC FROM SHIP.TO,CONO:ORD.CUST:"!":CARTON.SHIP.TO ELSE 
   MAT SPT.REC = ""
 END
 NUM.LABELS.PRINTED = 0
*  PRINTER ON
 FOR I = START.LABEL.NUMBER TO END.LABEL.NUMBER
*     PRINT; PRINT; PRINT
*      PRINT "X"
   PRINT
   PRINT ; PRINT ; PRINT
   PRINT SPACE(52):BOL.NUM'R#10'
   PRINT
   PRINT SPACE(45):'ORD# ':ID"R#8"
   PRINT
   PRINT SPACE(13):CO.NAME 'L#30'
   PRINT SPACE(13):CO.ADDRESS 'L#30'
   IF CO.ADDRESS.2 # "" THEN
     PRINT SPACE(13):CO.ADDRESS.2 'L#30'
     PRINT SPACE(13):TRIM(CO.CITY):", ":TRIM(CO.STATE):SPACE(2):CO.ZIP"L#11"
   END ELSE
     PRINT SPACE(13):TRIM(CO.CITY):", ":TRIM(CO.STATE):SPACE(2):CO.ZIP"L#11"
     PRINT
   END
   PRINT ; PRINT
   PRINT SPACE(13):SPT.NAME "L#30"
   PRINT SPACE(13):SPT.ADDR1 "L#30"
   IF SPT.ADDR2 # "" THEN
     PRINT SPACE(13):SPT.ADDR2 "L#30"
     PRINT SPACE(13):TRIM(SPT.CITY):", ":TRIM(SPT.STATE):SPACE(2):SPT.ZIP"L#11"
   END ELSE
     PRINT SPACE(13):TRIM(SPT.CITY):", ":TRIM(SPT.STATE):SPACE(2):SPT.ZIP"L#11"
     PRINT
   END
   PRINT
   PRINT SPACE(33):"BOX ":I"R#4":" OF ":CARTON.NEED"R#4"
   PRINT; PRINT; PRINT; PRINT
   NUM.LABELS.PRINTED = NUM.LABELS.PRINTED + 1
 NEXT I
 PRINTER OFF
 PRINTER CLOSE
 LOCATE DATE() IN CARTON.PRT.DATE<1>,1 SETTING INDX THEN
   CARTON.PRINT<1,INDX> = CARTON.PRINT<1,INDX> + NUM.LABELS.PRINTED
 END ELSE
   CARTON.PRINT = INSERT(CARTON.PRINT,1,1,0,NUM.LABELS.PRINTED)
   CARTON.PRT.DATE = INSERT(CARTON.PRT.DATE,1,1,0,DATE())
 END
 MATWRITE CARTON.REC ON CARTON,CARTON.ID
 MATWRITE CARTON.REC ON CONTROL,"02CAR"
 MATREAD  CARTON.REC FROM CARTON,CARTON.ID ELSE MAT CARTON.REC = ""
 RETURN
*
*****
*
99999*
PRINTER OFF
PRINTER CLOSE
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)

RETURN

