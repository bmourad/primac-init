SUBROUTINE ORDM_JOB_ALLOC_SEL
********************************************************************************
*   Program name :- ORDM_JOB_ALLOC_SEL
*   Created:- 2/7/2006
*   Programmer :- Suhail Hussain S
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE ICS.CPYLIB FNGD.STATS
$INCLUDE ICS.CPYLIB FNGD.JOB.STATS
$INCLUDE CPYLIB CHAR

OPEN "","ORDER.DETAIL" TO ORDER.DETAIL ELSE ERRMSG = "CANNOT OPEN ORDER.DETAIL FILE";GOTO 91000
OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CANNOT OPEN CUSTOMER FILE";GOTO 91000
OPEN "","INVENTORY" TO INVENTORY ELSE ERRMSG = "CANNOT OPEN INVENTORY FILE";GOTO 91000
OPEN "","FNGD.STATS" TO FNGD.STATS ELSE ERRMSG = "CANNOT OPEN FNGD.STATS FILE";GOTO 91000
OPEN "","FNGD.JOB.STATS" TO FNGD.JOB.STATS ELSE ERRMSG = "CANNOT OPEN FNGD.JOB.STATS FILE";GOTO 91000

* Insert method code here

   STATUS   = RBO.getProperty("","INARR",INARR)
   STATUS   = RBO.getProperty("","DISPARR",OSDARR)
   STATUS   = RBO.getProperty("","PMCProperty",PMCProperty)

   CONO     = PMCProperty<1,4>
   ORDNO    = INARR<1,1>
   PROD_NO  = INARR<1,2>
   WHSE_NO  = INARR<1,3>
   INDX     = INARR<1,4>
   DELIM    = "ð"

   MATREAD INV.REC FROM INVENTORY, CONO: PROD_NO ELSE MAT INV.REC = ""
   $INCLUDE ICSBP INV.UM.CNV
   MATPARSE ORD.DET.REC FROM OSDARR,DELIM

   OSD_JOB_QTY = OSD.JOB.QTY<1,INDX>
   OSD_JOB     = OSD.JOB<1,INDX>
   OSD_A_QTY   = OSD.A.QTY<1,INDX>

   IWH.ID = CONO:PROD_NO:"!":WHSE_NO
   MATREAD FGS.REC FROM FNGD.STATS, IWH.ID ELSE
     MAT FGS.REC = ""
   END
   IF FGS.JOB = "" THEN
      ERRMSG = "Cannot locate any jobs with on order/manufacture qty"
      GOTO 91000
   END
   AQTY = ""
   RCNT = DCOUNT(OSD_JOB<1,1>,SVM)
   OLD.QTY = OSD_JOB_QTY;* T20234
   FOR R = 1 TO RCNT
     LOCATE OSD_JOB<1,1,R> IN FGS.JOB<1>,1 SETTING JPTR THEN
       AQTY<1,JPTR> = OSD_JOB_QTY<1,1,R>
     END
     OSD_JOB_QTY<1,1,R> = 0
   NEXT R
   ATYPE = "M";ATOT = OSD_A_QTY
   GOSUB ORDER.JOB.SEL
   
STATUS = RBO.setProperty("","DISPARR",DISPARR)
STATUS = RBO.setProperty("","RETARR",RET_ARR)
RETURN
   
****
ORDER.JOB.SEL:
  ALOC.TOT = ATOT
  ALOC.QTY = AQTY

 LINES = DCOUNT(FGS.JOB,VM)
 S.A.QTY = ""; S.CUST = ""; S.DATE = ""
 LN = 0; IWH.ID = CONO:PROD_NO:"!":WHSE_NO
 FOR I = 1 TO LINES
   MATREAD FJS.REC FROM FNGD.JOB.STATS, IWH.ID:"!":FGS.JOB<1,I> ELSE MAT FJS.REC = ""
   S.CUST<I> = FJS.CUST
   S.DATE<I> = FJS.DATE
   LOCATE ORDNO IN FJS.ORD<1>,1 SETTING OLOC THEN
     IF FJS.ORD.QTY<1,OLOC> > ALOC.QTY<1,I> THEN
       S.A.QTY<I> = FJS.A.QTY - FJS.ORD.QTY<1,OLOC>
     END ELSE
       S.A.QTY<I> = FJS.A.QTY - ALOC.QTY<1,I>
     END
   END ELSE
     S.A.QTY<I> = FJS.A.QTY
   END
   IF S.A.QTY<I> < 0 THEN S.A.QTY<I> = 0
   S.A.QTY<I> = FJS.M.QTY - FJS.F.QTY - S.A.QTY<I>
   IF S.A.QTY<I> < 0 THEN S.A.QTY<I> = 0
   IF LN = 0 THEN
     IF FJS.M.QTY - FJS.A.QTY > 0 THEN
       LN = I
     END
   END
 NEXT I
 IF LN = 0 THEN LN = 1
*
 DISPARR<1,1> = SUM(FGS.M.QTY)
 DISPARR<1,2> = SUM(FGS.A.QTY)
 DISPARR<1,3> = SUM(S.A.QTY)
 DISPARR<1,4> = SUM(ALOC.QTY)
 FOR I = 1 TO 4
   DISPARR<1,I> = OCONV(INT(((DISPARR<1,I>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
 NEXT I

 FOR N = 1 TO LINES
   RET_ARR<1,N,1> = FGS.JOB<1,N>
   RET_ARR<1,N,2> = OCONV(S.DATE<N>,"D2/")
   RET_ARR<1,N,3> = OCONV(INT(((FGS.M.QTY<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
   RET_ARR<1,N,4> = OCONV(INT(((FGS.A.QTY<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
   RET_ARR<1,N,5> = OCONV(INT((((S.A.QTY<N>)/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
   RET_ARR<1,N,6> = OCONV(INT(((ALOC.QTY<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
   RET_ARR<1,N,7> = S.CUST<N>
   MATREAD CUST.REC FROM CUSTOMER, CONO:S.CUST<N> ELSE
     MAT CUST.REC = ""; CUST.NAME = STR("?",30)
   END
   RET_ARR<1,N,8> = CUST.NAME
   RET_ARR<1,N,9> = INT((((S.A.QTY<N>)/ICR.DV1)*ICR.MT1)/ICR.DV2+.5)
 NEXT N
RETURN

* End of method code
91000*
  STATUS = RBO.setProperty("","ServerStatus",1)
  STATUS = RBO.setProperty("","ServerMessage",ERRMSG)
RETURN

