SUBROUTINE ORDM_PROCESS_PROMPTS_ARR(PROMPTS,MODE,PNO,P.TYPE,P.RQD,P.MSG,P.DEF,P.VALID,P.IP)
********************************************************************************************************************
*     Program name :- ORDM_PROCESS_PROMPTS_ARR                                                                     *
*     Created:- 3/17/2006                                                                                          *
*     Programmer :- Suhail Hussain S                                                                               *
********************************************************************************************************************
*                                                                                                                  *
*     MODE = 1 => SET OR GET PROMPT      [ USED TO GET PROMPT VALUE OR TO SET NEW PROMPT MESG / WARNING MESG ]     *
*     MODE = 2 => DELETE PROMT           [ USED TO JUST DELETE A VALUE EITHER PROMPT OR WARNING ]                  *
*     MODE = 3 => DELETE AND SET PROMPT  [ USED FOR DELETING AND SETTING NEW PROMPT ERROR / WARNING MESSAGES ]     *
*                                                                                                                  *
*     IF MODE=1(GET PROMPT) AND P.TYPE = I THEN PROCESS IS TO TERMINATED AND THE CONTROL GIVEN BACK TO CLIENT      *
*                                                                                                                  *
********************************************************************************************************************
*  THE VARIABLE PROMPTS HOLDS THE VALUES OF ALL THE USER INPUTS. THE SYNTAX OF IF IS AS FOLLOWS... :               *
*                                                                                                                  *
*     PROMPTS<1,X,1> = PROMPT NUMBER                                                                               *
*     PROMPTS<1,X,2> = I / M / D    [ I => INPUT   M => WARNING / INFO MESSAGE  D => DISPLAY VALUE AT PROMPT NUM ] *
*     PROMPTS<1,X,3> = Y / N     [ Y => USER INTERACTION REQUIRED       N => USER INTERACTION NOT REQUIRED ]       *
*     PROMPTS<1,X,4> = PROMPT / WARNING / INFO MESSAGE STRING                                                      *
*     PROMPTS<1,X,5> = DEFAULT VALUE FOR THE INPUT                                                                 *
*     PROMPTS<1,X,6> = POSSIBLE VALID VALUES FOR INPUT                                                             *
*     PROMPTS<1,X,7> = USER PROVIDED INPUT FOR THE REQUIRED PROMPT                                                 *
*                                                                                                                  *
********************************************************************************************************************

$INCLUDE WWINSERT RBO.H

OPEN "","CONTROL" TO CONTROL ELSE STOP

INDX = ""
P.LOC = ""
IP.REQ = 0

   BEGIN CASE
      CASE MODE = 1
         GOSUB CHECK.PROMPT
      CASE MODE = 2
         GOSUB DELETE.PROMPT 
      CASE MODE = 3
         GOSUB DELETE.PROMPT
         GOSUB CHECK.PROMPT
   END CASE
   GOTO 89000
RETURN

CHECK.PROMPT:
   PCNT = "";PFND = -1
   PCNT = DCOUNT(PROMPTS,@VM)

   FOR P = 1 TO PCNT 
      IF PROMPTS<1,P,1> = PNO THEN PFND = P
      WHILE PFND = -1
   NEXT P

   IF PFND # -1 THEN
      P.IP = PROMPTS<1,PFND,7>
   END ELSE
      P.LOC = PCNT+1
      GOSUB SET.PROMPT
      IF P.TYPE = "I" THEN IP.REQ = 1
   END
RETURN

DELETE.PROMPT:
   DCNT = "";DFND = -1
   DCNT = DCOUNT(PROMPTS,@VM)
   
   FOR D = 1 TO DCNT
      IF PROMPTS<1,D,1> = PNO THEN DFND = D
      WHILE DFND = -1
   NEXT D

   IF DFND # -1 THEN
      PROMPTS = DELETE(PROMPTS,DFND)
   END
RETURN

SET.PROMPT:
   IF PNO = "" OR P.TYPE = "" OR P.MSG = "" THEN RETURN
   PROMPTS<1,P.LOC,1> = PNO
   PROMPTS<1,P.LOC,2> = P.TYPE
   PROMPTS<1,P.LOC,3> = P.RQD
   PROMPTS<1,P.LOC,4> = P.MSG
   PROMPTS<1,P.LOC,5> = P.DEF
   PROMPTS<1,P.LOC,6> = P.VALID
   PROMPTS<1,P.LOC,7> = P.IP
RETURN

89000*
   STATUS = RBO.setProperty("","PROMPTS",PROMPTS)
   STATUS = RBO.setProperty("","IP_FLAG",IP.REQ)
RETURN
