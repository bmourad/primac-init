SUBROUTINE GET_ACTYPE_CATG_CODES
********************************************************************************
*   Program name :- GET_ACTYPE_CATG_CODES
*   Created:- 6/23/2003
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
*  Cono
*
* Out Properties:
* ---------------
*  AT_TYPE,CATG_CODES,CATG_DESCS
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE GLS.CPYLIB ACCT.TYPE
$INCLUDE GLS.CPYLIB ACCT.CATG
$INCLUDE GLS.CPYLIB ACCT.SUB.CATG

* Insert method code here
ERRMSG = ''
OPEN '','ACCT.TYPE' TO ACCT.TYPE ELSE
	ERRMSG = 'Cannot open ACCT.TYPE file!'
      	GOTO 99999
END

OPEN '','ACCT.CATG' TO ACCT.CATG ELSE
	ERRMSG = 'Cannot open ACCT.CATG file!'
      	GOTO 99999
END

OPEN '','ACCT.SUB.CATG' TO ACCT.SUB.CATG ELSE
	ERRMSG = 'Cannot open ACCT.SUB.CATG file!'
      	GOTO 99999
END

AT_TYPE = ""
AC_CATG_CODES = ""
SUB_CATG_CODES = ""
AC_CATG_DESCS = ""
SUB_CATG_DESCS = ""

CTL = 1
STATUS = RBO.getProperty('','Cono',CONO)

*STR = "SELECT ACCT.TYPE WITH TYP LIKE " : CONO : "..."
STR = "SELECT ACCT.TYPE"


UDTEXECUTE STR 
UDTEXECUTE "SAVE-LIST 2"
UDTEXECUTE "GET-LIST 2"
DATA = 1
K = 1
LOOP
	READNEXT ID ELSE DATA = 0
WHILE DATA DO
	IF ((ID = "A")OR(ID = "I")OR(ID = "O")OR(ID = "E")OR(ID = "L")) THEN
		MATREAD AT.REC FROM ACCT.TYPE, ID ELSE
			MAT AT.REC = ''
		END
		AT_TYPE<1,CTL> = ID
		AC_CATG_CODES<1,CTL> = LOWER(AT.CATG) : @VM : AC_CATG_CODES<1,CTL>
		CNT = DCOUNT(AT.CATG,@VM)
		DESC = ""
		FOR I = 1 TO CNT
			MATREAD AC.REC FROM ACCT.CATG, AT.CATG<1,I> ELSE
				MAT AC.REC = ''
			END
			DESC<1,I> = AT.CATG<1,I> : "-" :AC.DESC
			SUB_CATG_CODES<1,K> = LOWER(AC.SUB)
			CNT1 = DCOUNT(AC.SUB,@VM)
			FOR J = 1 TO CNT1
				MATREAD ASC.REC FROM ACCT.SUB.CATG, AC.SUB<1,J> ELSE
					MAT ASC.REC = ''
				END
				SUB_CATG_DESCS<1,K,J> =AC.SUB<1,J> : "-" : ASC.DESC
			NEXT J
			K += 1
		NEXT I
		AC_CATG_DESCS<1,CTL> = LOWER(DESC) : @VM : AC_CATG_DESCS<1,CTL>
		CTL += 1
	END	
REPEAT
			
STATUS = RBO.setProperty('','AT_TYPE',AT_TYPE)
STATUS = RBO.setProperty('','CATG_CODES',AC_CATG_CODES)
STATUS = RBO.setProperty('','CATG_DESCS',AC_CATG_DESCS)
STATUS = RBO.setProperty('','SUB_CATG_CODES',SUB_CATG_CODES)
STATUS = RBO.setProperty('','SUB_CATG_DESCS',SUB_CATG_DESCS)

UDTEXECUTE "DELETE-LIST 2"
* End of method code
99999
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','ServerStatus','1')
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END ELSE
       STATUS = RBO.setProperty('','ServerStatus','')
       STATUS = RBO.setProperty('','ServerMessage','')
  END 
RETURN
