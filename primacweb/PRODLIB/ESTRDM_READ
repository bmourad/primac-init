   SUBROUTINE ESTRDM_READ
*********************************************************************
* REVISION    - [12.0]
*
* SYSTEM      - PRIMAC
*
* PROGRAM     - ESTRDM_READ
*
* BY          - CHRIS MYKLEBUST
*
* DATE        - 11/06/2002
*
* DESCOPTIION - This program gets the press information so it can
*               be displayed in Die Maintenance.
*********************************************************************
   $INCLUDE WWINSERT RBO.H
   $INCLUDE JES.CPYLIB ESTIMATE.RES.DIE
   $INCLUDE JES.CPYLIB ESTIMATE.RES.GEARSET
   $INCLUDE JES.CPYLIB EST.RES.FDIE.SPEEDS
   $INCLUDE PMC.CPYLIB COST.CNTR
   $INCLUDE CPYLIB SCREEN.COM
   $INCLUDE CPYLIB FILE.VARS
   $INCLUDE CPYLIB CHAR
*
*---- OPEN FILES
*
   IF FILEINFO(ESTIMATE.RES.DIE,0) = 0 THEN
      OPEN 'ESTIMATE.RES.DIE' TO ESTIMATE.RES.DIE ELSE
         ERRMSG = 'CANNOT OPEN ESTIMATE.RES.DIE FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(ESTIMATE.RES.GEARSET,0) = 0 THEN
      OPEN '','ESTIMATE.RES.GEARSET' TO ESTIMATE.RES.GEARSET ELSE
         ERRMSG='CANNOT OPEN ESTIMATE.RES.GEARSET FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(EST.RES.FDIE.SPEEDS,0) = 0 THEN
      OPEN '','EST.RES.FDIE.SPEEDS' TO EST.RES.FDIE.SPEEDS ELSE
         ERRMSG='Cannot open EST.RES.FDIE.SPEEDS'
         GOSUB 93000
      END
   END
   IF FILEINFO(EQUIPMENT,0) = 0 THEN
      OPEN 'EQUIPMENT' TO EQUIPMENT ELSE
         ERRMSG = 'CANNOT OPEN EQUIPMENT FILE'
         GOTO 93000
      END
   END
   IF FILEINFO(COST.CNTR,0) = 0 THEN
      OPEN 'COST.CNTR' TO COST.CNTR ELSE
         ERRMSG = 'CANNOT OPEN COST.CNTR FILE'
         GOTO 93000
      END
   END
*
*---- INITIALIZATION
*
   CCTR = ""
   DESC = ""
*
*---- MAIN PROCESS
*
   ERRMSG = RBO.getProperty('','ID',DIE.ID)
   CONO = DIE.ID[1,3]
   DIE.KEY = DIE.ID[4,10]
   MATREADU RLDIE.REC FROM ESTIMATE.RES.DIE,DIE.ID LOCKED
      ERRMSG = 'RES.DIE record is locked by user - ':GETUSERNAME(STATUS())
      GOTO 93000
   END ELSE
      MAT RLDIE.REC = ''
      NEW.REC = 1
   END
*
*---- GET COST CENTERS FOR ROTARY
*
   SELECTINDEX "GSROTARY",DIE.KEY[1,2] FROM EQUIPMENT
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      IF ID[1,3] = CONO THEN
         ID = ID[4,99]
         LOCATE ID IN CCTR<1>,1 BY "AR" SETTING PTR ELSE
            CCTR = INSERT(CCTR,1,PTR,0,ID)
         END
      END
   REPEAT
   SELECTINDEX "GSFBED",DIE.KEY[1,2] FROM EQUIPMENT
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      IF ID[1,3] = CONO THEN
         ID = ID[4,99]
         LOCATE ID IN CCTR<1>,1 BY "AR" SETTING PTR ELSE
            CCTR = INSERT(CCTR,1,PTR,0,ID)
         END
      END
   REPEAT
   CNT = DCOUNT(CCTR,VM)
   FOR C = 1 TO CNT
      MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR<1,C> ELSE
         CCTR.DESC = STR("?",25)
      END
      DESC<1,C> = CCTR.DESC "L#28"
   NEXT C
*
*---- SET RBO FIELDS
*
   STATUS = RBO.setProperty('','CCTR',CCTR)
   STATUS = RBO.setProperty('','DESC',DESC)
   RETURN
93000 *
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
   RETURN

