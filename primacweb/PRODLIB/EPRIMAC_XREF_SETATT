   SUBROUTINE EPRIMAC_XREF_SETATT (ERRMSG, THIS_INPARAM, THIS_OUTPARAM, EPRIMAC_XREF, MAT EPMCX_REC)
********************************************************************************
*   Program name :- EPRIMAC_XREF_SETATT
*   Created:- 11/07/2002
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
   $INCLUDE PMC.CPYLIB EPRIMAC_XREF
   $INCLUDE CPYLIB CHAR
* Insert method code here
   ERRMSG    = ""
   CONO      = THIS_INPARAM<1>
   ID        = THIS_INPARAM<2>
   XREF_ID   = THIS_INPARAM<3>
   LL        = THIS_INPARAM<4>
* GET INFO FROM THE TABLE
   IF LL < 1 THEN
      LOCATE XREF_ID IN EPMCX_TABLE<1>,1 SETTING LL ELSE GOTO 99999
   END
   DATA_FILE = XREF_ID
   DESC_FILE = ""
   DESC_ATT = EPMCX_DESC_ATT<1,LL>
   SORT_FLAG = EPMCX_SORT_FLAG<1,LL>
   GROUP_ATT = EPMCX_GROUP_ATT<1,LL>
   TYPE_ATT = EPMCX_TYPE_ATT<1,LL>
   TYPE_CODE = ""
* CHECK FOR EQUIPMENT FILE
10*
   IF NOT(NUM(DESC_ATT)) THEN
      DESC_FILE = DESC_ATT
      LOCATE DESC_FILE IN EPMCX_TABLE<1>,1 SETTING FND ELSE
         ERRMSG = "Cannot locate " : DESC_FILE : " in EPRIMAC_XREF 'TABLES'"
         GOTO 99999
      END
      DESC_ATT = EPMCX_DESC_ATT<1,FND>
      IF DESC_FILE = "COST.CNTR" THEN
         DATA_FILE = "EQUIPMENT"
         TYPE_ATT = 1
         TYPE_CODE = ""
         BEGIN CASE
            CASE XREF_ID = "EQUIPMENT"
               TYPE_CODE = "ALL"
            CASE XREF_ID = "PRESS"
               TYPE_CODE = "01"
            CASE XREF_ID = "CUTTER"
               TYPE_CODE = "04"
            CASE XREF_ID = "FOLDER"
               TYPE_CODE = "06"
            CASE XREF_ID = "STITCHER"
               TYPE_CODE = "08"
            CASE XREF_ID = "PERFECT"
               TYPE_CODE = "10"
            CASE XREF_ID = "BINDER"
               TYPE_CODE = "12"
            CASE XREF_ID = "GATHER"
               TYPE_CODE = "22"
            CASE XREF_ID = "TRIMMER"
               TYPE_CODE = "24"
            CASE XREF_ID = "CASEMAKER"
               TYPE_CODE = "26"
            CASE XREF_ID = "CASESTAMP"
               TYPE_CODE = "28"
            CASE XREF_ID = "CASEIN"
               TYPE_CODE = "30"
            CASE XREF_ID = "JACKETER"
               TYPE_CODE = "32"
            CASE XREF_ID = "COLLATOR"
               TYPE_CODE = "34"
            CASE XREF_ID = "DRIL"
               TYPE_CODE = "36"
            CASE XREF_ID = "ROLL.LABEL.PRESS"
               TYPE_CODE = "40"
            CASE XREF_ID = "REWINDER.FAN.FOLDER"
               TYPE_CODE = "42"
            CASE XREF_ID = "VERTICAL.BAGGER"
               TYPE_CODE = "44"
         END CASE
      END
      IF TYPE_CODE = "" THEN GOTO 99999
      GOTO 10
   END
* OPEN DATA FILES
   SECOND_FILE = ""
   OPEN '',DATA_FILE TO OPEN_FILE ELSE
      ERRMSG = DATA_FILE : " File is Missing"
      GOTO 99999
   END
   IF DESC_FILE # "" THEN
      OPEN '',DESC_FILE TO SECOND_FILE ELSE
         ERRMSG = DESC_FILE : " File is Missing"
         GOTO 99999
      END
   END
* PROCESS SINGLE ID
   IF ID # "<SELECT>" THEN
      GOSUB 1000
      GOTO 99999
   END
* SELECT AND PROCESS ALL RECORDS
   SELECT OPEN_FILE
   DONE = 0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      GOSUB 1000
      IF ERRMSG # "" THEN EXIT
   REPEAT
   GOTO 99999
*
*   SUBROUTINES
*
* GET DATA VALUES
1000*
   ID_DESC = ""
   ID_GROUP = ""
   ID_TYPE = ""
   BEGIN CASE
      CASE XREF_ID = "SECURITY"
         IF ID[1,1] = "R" OR ID[1,1] = "U" OR ID[1,1] = "H" OR ID[1,3] = "SID" THEN GOTO 1999
         IF NOT(NUM(ID[1,3])) THEN GOTO 1999
         CONO = ID[1,3]
         ID_NO = ID[4,99]       
      CASE XREF_ID = "COMPANY"
         CONO = ""
         ID_NO = ID
      CASE XREF_ID = "ACCT.TYPE"
         CONO = ""
         ID_NO = ID
      CASE XREF_ID = "ACCT.CATG"
         CONO = ""
         ID_NO = ID
      CASE XREF_ID = "ACCT.SUB.CATG"
         CONO = ""
         ID_NO = ID
*ADDED 10/04/2004 BY WAY v
      CASE XREF_ID = "PRIMAC_SECURITY"
         CONO = ""
         ID_NO = ID
      CASE XREF_ID = "PMC_PROCESS"
         CONO = ""
         ID_NO = ID
*ADDED 10/04/2004 BY WAY ^
      CASE XREF_ID = "ESTIMATE.MATL"
         CONO = ID[1,3]
         ID_NO = ID[4,99]
         GOSUB 3000
         GOTO 1999
      CASE 1
         CONO = ID[1,3]
         ID_NO = ID[4,99]
   END CASE
   READ REC FROM OPEN_FILE, ID ELSE
      ID_DESC = "<Delete>"
      GOTO 1888
   END
   IF NUM(GROUP_ATT) AND GROUP_ATT > 0 THEN
      ID_GROUP = REC<GROUP_ATT,1,1>
   END
   IF NUM(TYPE_ATT) AND TYPE_ATT > 0 THEN
      ID_TYPE = REC<TYPE_ATT,1,1>
      IF DESC_FILE # "" THEN
         IF "X":ID_TYPE # "X":TYPE_CODE AND TYPE_CODE # "ALL" THEN GOTO 1999
         READ REC FROM SECOND_FILE, ID ELSE GOTO 1999
      END
   END
   ID_DESC = REC<DESC_ATT,1,1>
   IF XREF_ID = "EMPLOYEE" THEN
      ID_DESC := " " : REC<DESC_ATT+1,1,1>
   END
1888*
   GOSUB 8000
1999*
   RETURN
* ESTIMATE.MATL FILE LOGIC
3000*
   READ REC FROM OPEN_FILE, ID ELSE REC = ""
   ID_GROUP = ID_NO
   ID_NO = "ALL"
   ID_DESC = "<Delete>"
   GOSUB 8000
   IF REC<1> = "" THEN GOTO 3999
   ID_COUNT = DCOUNT(REC<1>,VM)
   FOR II = 1 TO ID_COUNT
      ID_NO = REC<1,II,1>
      IF NUM(TYPE_ATT) AND TYPE_ATT > 0 THEN
         ID_TYPE = REC<TYPE_ATT,II,1>
      END
      ID_DESC = REC<DESC_ATT,II,1>
      GOSUB 8000
   NEXT II
3999*
   RETURN
* UPDATE EPRIMAC_XREF
8000*
   IN_PARAM = CONO
   IN_PARAM<2> = XREF_ID
   IN_PARAM<3> = SORT_FLAG
   IN_PARAM<4> = ID_NO
   IN_PARAM<5> = ID_DESC
   IN_PARAM<6> = ID_GROUP
   IN_PARAM<7> = ID_TYPE
   OUT_PARAM = ""
   CALL EPRIMAC_XREF_UPD(ERRMSG, IN_PARAM, OUT_PARAM, EPRIMAC_XREF)
   RETURN
99999*
* End of method code
   RETURN
