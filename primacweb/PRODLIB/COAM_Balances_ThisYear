SUBROUTINE COAM_Balances_ThisYear
********************************************************************************
*   Program name :- COAM_Balances_ThisYear
*   Created:- 1/17/2003
*------------------------------------------------------------------------------
*
*
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE PMC.CPYLIB COA
$INCLUDE GLS.CPYLIB COA.BAL
$INCLUDE PMC.CPYLIB DIVISION
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE CPYLIB CHAR

* Open files
ERRMSG = ''
OPEN 'COMPANY' TO COMPANY ELSE ERRMSG = 'CANNOT OPEN COMPANY FILE'
OPEN 'CONTROL' TO CONTROL ELSE ERRMSG = 'CANNOT OPEN CONTROL FILE'
OPEN 'COA' TO COA ELSE ERRMSG = 'CANNOT OPEN COA FILE'
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG)
  RETURN
END

* Get properties
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','Account',ACCT.NO)
STATUS = RBO.getProperty('','CCtr',CCTR.CODE)
STATUS = RBO.getProperty('','ThisYearOpenBal',ThisYearOpenBal)
STATUS = RBO.getProperty('','COAM_Balances_Rec',COAM_Balances_Rec)
MATPARSE CB.REC FROM COAM_Balances_Rec, VM

* Get CONO
CONO = ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
  ERRMSG = 'Invalid Company ID (':CONO:').'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Get Acct
MATREAD COA.REC FROM COA, CONO : ACCT.NO ELSE
  ERRMSG = 'Cannot locate account - ' : ACCT.NO CO.ACCT.MASK
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

READ REC FROM CONTROL, CONO:"FISCAL" ELSE
  ERRMSG = "CONTROL FISCAL IS NOT ON FILE"
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END
YEAR = REC<1>[1,4]
CURR = REC<1>[5,2]
TAPE.FLG = "Y"
READ REC FROM CONTROL, CONO:(YEAR-1):"GLS.TAPE" ELSE TAPE.FLG = "N"

* Initialize and read values
DIM OLD.CB(300)
DIM NEW.CB(300)
DIM OCOA.REC(20)
ERRMSG = ""
READ PER.DATES FROM CONTROL, CONO:"OPENDATES" ELSE
  ERRMSG = "CONTROL RECORD OPENDATES IS MISSING"
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END
READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
  PERIOD.REC = 12
END
NUM.PERIODS = PERIOD.REC<1>

IN.ACCT.LEN = LEN(CO.ACCT.PIC)
GEN.DIV = "00"
GEN.DEPT = "00"
GEN.CCTR = "000"
ACCT.NUM = ACCT.NO
MAT OCOA.REC = MAT COA.REC
READV ALL.DIV FROM CONTROL, CONO:"DIVISIONS", 1 ELSE ALL.DIV = "01"
DIV.CNT = DCOUNT(ALL.DIV,VM)
DIVSN.CODE = ''
NUM.PERIODS = PERIOD.REC<1> + 1

* Set output values
IF CCTR.CODE = "" OR TAPE.FLG # "N" OR CURR # "01" THEN
  ERRMSG = "No action taken."
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END
PR.LN = 1
GOSUB GET.THIS.YEAR.OPEN.BAL

MATBUILD COAM_Balances_Rec FROM CB.REC
COAM_Balances_Rec = LOWER(COAM_Balances_Rec)

* Set output properties
STATUS = RBO.setProperty('','ThisYearOpenBal',ThisYearOpenBal)
STATUS = RBO.setProperty('','COAM_Balances_Rec',COAM_Balances_Rec)

* End of method code
RETURN

GET.THIS.YEAR.OPEN.BAL: 
IF ThisYearOpenBal<1,PR.LN> + 0 <> 0 THEN
  IF COA.NORM = "CR" THEN ThisYearOpenBal<1,PR.LN> = ThisYearOpenBal<1,PR.LN> * (-1)
END ELSE
  ThisYearOpenBal<1,PR.LN> = ""
END
CB.REC(CB.OPN+PR.LN) = ThisYearOpenBal<1,PR.LN>
RETURN

