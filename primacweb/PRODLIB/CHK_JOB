SUBROUTINE CHK_JOB
********************************************************************************
*   Program name :- CHK_JOB
*   Created:- 7/27/2005
*   By :- S.Suhail Hussain  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.CUTOFF.NO
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE PMC.CPYLIB DIVISION

OPEN "","JOB" TO JOB ELSE ERRMSG="FILE JOB IS MISSING";GOTO 91000
OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="FILE COMPANY IS MISSING";GOTO 91000
OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG="FILE CUSTOMER IS MISSING";GOTO 91000
OPEN "","DIVISION" TO DIVISION ELSE ERRMSG="FILE DIVISION IS MISSING";GOTO 91000
OPEN "","CONTROL" TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING";GOTO 91000
STATUS = RBO.getProperty('','CHK_JOB',CHK_JOB)

CONO = CHK_JOB<1,1>
JOB.NO = CHK_JOB<1,2>
UNAME = CHK_JOB<1,3>
JOB.ID = CONO:JOB.NO


MATREAD JOB.CUTOFF.REC FROM CONTROL,CONO:"JOB.CUTOFF.NO" ELSE
  MAT JOB.CUTOFF.REC=""
  J.CUTOFF.NUM=0
END
MATREAD COMP.REC FROM COMPANY,CONO ELSE
   MAT COMP.REC = ''
END

*** MAIN PROCESSING 

    POST.OSJ=0
    MATREAD JOB.REC FROM JOB,JOB.ID ELSE
      MAT JOB.REC=""
      FND=0
      IF NUM(JOB.NO) THEN
        IF JOB.NO < J.CUTOFF.NUM THEN FND=1
      END ELSE
        JC.CNT=DCOUNT(J.CUTOFF.PREFIX,@VM)
        FOR L=1 TO JC.CNT WHILE FND=0
          PREFX.LEN=LEN(J.CUTOFF.PREFIX<1,L>)
          IF J.CUTOFF.PREFIX<1,L>=JOB.NO[1,PREFX.LEN] THEN
            CHK.VALUE=JOB.NO[PREFX.LEN+1,LEN(JOB.NO)-PREFX.LEN]
            IF NUM(CHK.VALUE) THEN
              IF CHK.VALUE < J.CUTOFF.PRE.NO<1,L> THEN FND=1
            END
          END
        NEXT L
      END
      IF FND=1 THEN
        JOB.DESC<1,1>="* * OUTSIDE JOB * *"
        POST.OSJ=1
      END ELSE
        ERRMSG="JOB IS NOT ON FILE. TRY AGAIN !!"
        GOTO 91000
      END
    END
    DIV.CODE = JOB.DIV; USER.ID = UPCASE(UNAME); ERRMSG = ''
    CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
    IF ERRMSG # '' THEN
      GOTO 91000
    END
    BEGIN CASE
      CASE JOB.STATUS<1,1>=9
        ERRMSG="THIS JOB HAS BEEN CANCELLED";GOTO 91000
      CASE JOB.STATUS<1,1>=7
        ERRMSG="THIS JOB IS READY TO BE PURGED";GOTO 91000
      CASE JOB.STATUS<1,1>=8
        ERRMSG="THIS JOB HAS BEEN PURGED";GOTO 91000
    END CASE
    
    ****** TO CHK IF  CRDT AUTH. IS RQD
    PMSG = ""
    IF (JOB.STATUS<1,1> > 1 AND JOB.STATUS<1,1> # 5) OR JOB.TRACK.DATE<1,7> # "" OR JOB.TRACK.DATE<1,8> # "" THEN
        PMSG="This job is not in process. Enter authorization"
    END

    IF CO.JOB.CUST.FLG="C" AND POST.OSJ=0 THEN
      CUST.ID=CONO:JOB.CUST
      MATREAD CUST.REC FROM CUSTOMER,CUST.ID ELSE
        CUST.NAME="UNKNOWN"
      END
      JOB.DESC<1,1>=CUST.NAME
    END
    
    IF POST.OSJ=0 THEN
      DMT.DIV=JOB.DIV
      DIV.ID=CONO:DMT.DIV
      MATREAD DIV.REC FROM DIVISION,DIV.ID ELSE DIV.DESC="UNKNOWN"
    END

    RETVAL = ""
    RETVAL = PMSG : @VM : JOB.DESC<1,1> : @VM : JOB.DIV : @VM : DIV.DESC : @VM : JOB.TYPE : @VM : CO.SPOIL.FLG
    STATUS = RBO.setProperty('','CHK_JOB',RETVAL)

RETURN

91000:
       STATUS = RBO.setProperty('','ServerStatus',1)
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
RETURN
