SUBROUTINE CUSTM_ShipTo_FORMAT
********************************************************************************
*   Program name :- CUSTM_ShipTo_FORMAT
*   Created:- 1/13/2003
*------------------------------------------------------------------------------
*
*
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE PMC.CPYLIB SALESMAN
$INCLUDE PMC.CPYLIB TAX
$INCLUDE PMC.CPYLIB SHIP.TO
$INCLUDE PMC.CPYLIB SHIP.VIA
$INCLUDE PMC.CPYLIB FOB
$INCLUDE PMC.CPYLIB COUNTRY.CODE 

* Open files
ERRMSG = ''
OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"
OPEN "COUNTRY.CODE" TO COUNTRY.CODE ELSE ERRMSG = "COUNTRY.CODE FILE IS MISSING"
OPEN "SALESMAN" TO SALESMAN ELSE ERRMSG = "SALESMAN FILE IS MISSING"
OPEN "SHIP.VIA" TO SHIP.VIA ELSE ERRMSG = "SHIP.VIA FILE IS MISSING"
OPEN "FOB" TO FOB ELSE ERRMSG = "FOB FILE IS MISSING"
OPEN "TAX" TO TAX ELSE ERRMSG = "TAX FILE IS MISSING"
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG)
  RETURN
END

* Get properties
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','SPT_CNTY',SPT.CNTY)
STATUS = RBO.getProperty('','SPT_SLSMN',SPT.SLSMN)
STATUS = RBO.getProperty('','SPT_SHIP_VIA',SPT.SHIP.VIA)
STATUS = RBO.getProperty('','SPT_FOB',SPT.FOB)
STATUS = RBO.getProperty('','SPT_TAX_JUR',SPT.TAX.JUR)
STATUS = RBO.getProperty('','SPT_EXMT_DATE',SPT.EXMT.DATE)

* Get CONO
CONO = ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
  ERRMSG = 'Invalid Company ID (':CONO:').'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Read descriptions
CountryDesc = SPT.CNTY
MATREAD CTY.REC FROM COUNTRY.CODE, CONO:SPT.CNTY THEN
  CountryDesc = CTY.DESC
END

MATREAD SALESMAN.REC FROM SALESMAN, CONO:SPT.SLSMN ELSE
  MAT SALESMAN.REC = ""; SALS.NAME = "Unknown"
END

MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:SPT.SHIP.VIA ELSE
  MAT SHIP.VIA.REC = ""
END

MATREAD FOB.REC FROM FOB, CONO:SPT.FOB ELSE
  MAT FOB.REC = ""
END

TaxJurDescs = ''
FOR IDX = 1 TO DCOUNT(SPT.TAX.JUR,@VM)
  MATREAD TAX.REC FROM TAX, CONO:SPT.TAX.JUR<1,IDX> ELSE TAX.JUR = "Unknown"
  TaxJurDescs<1,IDX> = TAX.JUR
  SPT.EXMT.DATE<1,IDX> = OCONV(SPT.EXMT.DATE<1,IDX>,"D2/")
NEXT IDX

* Set properties
STATUS = RBO.setProperty('','CountryDesc',CountryDesc)
STATUS = RBO.setProperty('','SalesName',SALS.NAME)
STATUS = RBO.setProperty('','ShipViaDesc',SHPV.DESC)
STATUS = RBO.setProperty('','FOBDesc',FOB.DESC)
STATUS = RBO.setProperty('','TaxJurDescs',TaxJurDescs)
STATUS = RBO.setProperty('','SPT_EXMT_DATE',SPT.EXMT.DATE)

* End of method code
RETURN

