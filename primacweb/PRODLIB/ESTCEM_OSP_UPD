SUBROUTINE ESTCEM_OSP_UPD
********************************************************************************
*   Program name :- ESTCEM_OSP_UPD
*   Written by   :- Ramakrishna Pusuluri
*   Created:- 6/1/2005
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
* This program will update the summary O/P data stored on the
* estimate from the detail data as sepecified by the action code.
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

*---- MAIN PROCESSING
$INCLUDE JES.CPYLIB SCOMMON.ESTIMATE
$DEFINE ESTIMATE
$INCLUDE JES.CPYLIB ESTIMATE
$DEFINE ESTIMATEDEPT
$INCLUDE JES.CPYLIB ESTIMATE.DEPT
$DEFINE ESTIMATETEMP
$INCLUDE JES.CPYLIB ESTIMATE.TEMP
*
*---- INITIALIZATION
*
   ESTDEPTINFO    = ""
   ERR = RBO.getProperty('','DREFCREF',VALUECONTENT)
   ERR = RBO.getProperty('','PMCProperty',PMCProperty)
*   ERR = RBO.getProperty('','ESTDEPTINFO',ESTDEPTINFO)

	CONO           = PMCProperty<1,4>
	ACTION	        = FIELD(VALUECONTENT,"^",1)
	DPTR           = FIELD(VALUECONTENT,"^",2)
	COMP           = FIELD(VALUECONTENT,"^",3)
	EQTY           = FIELD(VALUECONTENT,"^",4)
	LPTR	        = FIELD(VALUECONTENT,"^",5)

	EST.NO         = FIELD(VALUECONTENT,"^",6)
	DEPT		 = FIELD(VALUECONTENT,"^",7)
	TEMP.TYPE	 = FIELD(VALUECONTENT,"^",8)	
	TEMP.OPV       = FIELD(VALUECONTENT,"^",9)
	TEMP.CODE      = FIELD(VALUECONTENT,"^",10)
	TEMP.QTY       = FIELD(VALUECONTENT,"^",11)
	TEMP.STD       = FIELD(VALUECONTENT,"^",12)
	ERRMSG		 = ""

*A^1^1^25000^10^1029-01^15^PM^10000^ART^9^12
*KEY = CONO:EST#!DEPT#!COMP!QTY

DEPTID=CONO:EST.NO:"!":DPTR:"!":COMP:"!":EQTY

OPEN '','ESTIMATE' TO ESTIMATE ELSE RETURN
MATREAD EST.REC FROM ESTIMATE,CONO:EST.NO ELSE
     MAT EST.REC = ""
*     ERRMSG="No ESTIMATE defined for this type. Try again! "
*     GOTO 99999
END
OPEN '','ESTIMATE.DEPT' TO ESTIMATE.DEPT ELSE RETURN
MATREAD ESTD.REC FROM ESTIMATE.DEPT,DEPTID ELSE
     MAT ESTD.REC = ""
*     ERRMSG = "CANNOT Locate ESTIMATE.DEPT"
*     GOTO 99999
END
*	
     LOCATE EQTY IN EST.QTY<1>,1 SETTING EPTR ELSE GOTO 99999
*
*---- MAIN PROCESSING
*
100*
      BEGIN CASE
         CASE ACTION = "P"
            OSP.DC = DPTR:"!":COMP
            LOCATE OSP.DC IN EST.DEPT.OSP<1>,1 SETTING DCP ELSE DCP = 0
            IF DCP > 0 THEN
               BEGIN CASE
                  CASE EPTR = 1
                     EST.DEPT.OSP = DELETE(EST.DEPT.OSP,1,DCP,0)
                     EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DCP,0)
                     EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DCP,0)
                     EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DCP,0)
                     EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DCP,0)
                  CASE 1
                     OPCNT = DCOUNT(EST.DEPT.OSP.ID<1,DCP>,@SM)
                     FOR VCP = 1 TO OPCNT
*		          ESTCEM_MACRO_REPVAL(STRING_VARIABLE,SEPARATOR,OCCURANCE_NUMBER,NEW_DATA)

		          CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.QTY<1,DCP,VCP>,"!",EPTR,"")
		          CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR,"") 
                       
*   			    MACRO REPVAL;EST.DEPT.OSP.QTY<1,DCP,VCP>;"!";EPTR;""
*                        MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;""
                     NEXT VCP
               END CASE
            END
         CASE ACTION = "D" OR (ACTION = "C" AND TEMP.CODE # ESTD.CODE<1,LPTR>)
            OSP.DC = DPTR:"!":COMP
            LOCATE OSP.DC IN EST.DEPT.OSP<1>,1 SETTING DCP ELSE DCP = 0
            IF DCP > 0 THEN
               OSP.ID = ESTD.OPV<1,LPTR>:"!":ESTD.CODE<1,LPTR>
               LOCATE OSP.ID IN EST.DEPT.OSP.ID<1,DCP>,1 SETTING VCP ELSE VCP = 0
               IF VCP > 0 THEN
                  OPCNT = DCOUNT(EST.DEPT.OSP.ID<1,DCP>,@SM)
                  BEGIN CASE
                     CASE OPCNT = 1 AND EPTR = 1
                        EST.DEPT.OSP = DELETE(EST.DEPT.OSP,1,DCP,0)
                        EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DCP,0)
                        EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DCP,0)
                        EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DCP,0)
                        EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DCP,0)
                     CASE EPTR = 1
                        EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DCP,VCP)
                        EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DCP,VCP)
                        EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DCP,VCP)
                        EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DCP,VCP)
                     CASE 1
			   CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.QTY<1,DCP,VCP>,"!",EPTR,"")
		          CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR,"")				
                        
*			   MACRO REPVAL;EST.DEPT.OSP.QTY<1,DCP,VCP>;"!";EPTR;""
*                       MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;""
                  END CASE
               END
            END
      END CASE
      IF ACTION = "A" OR ACTION = "C" OR ACTION = "I" OR ACTION = "R" THEN
         OSP.DC = DPTR:"!":COMP
         OSP.ID = TEMP.OPV:"!":TEMP.CODE
         DCFND = 1
         VCFND = 0
         LOCATE OSP.DC IN EST.DEPT.OSP<1>,1 SETTING DCP ELSE
            DCFND = 0
            DCP = 0
            LOOP
               DCP = DCP + 1
               DC = EST.DEPT.OSP<1,DCP>
               D = FIELD(DC,"!",1)
               C = FIELD(DC,"!",2)
            UNTIL (DC = "") OR (D > DPTR) OR (D = DPTR AND C > COMP) DO
            REPEAT
         END
         IF DCFND THEN
            VCFND = 1
            LOCATE OSP.ID IN EST.DEPT.OSP.ID<1,DCP>,1 SETTING VCP ELSE VCFND = 0
         END
         BEGIN CASE
            CASE DCFND AND VCFND
               IF TEMP.TYPE = "P" THEN
                  EST.DEPT.OSP.UM<1,DCP,VCP> = "EA"
               END ELSE
                  EST.DEPT.OSP.UM<1,DCP,VCP> = TEMP.TYPE[2,1]
               END
               CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.QTY<1,DCP,VCP>,"!",EPTR,TEMP.QTY*100)
*               MACRO REPVAL;EST.DEPT.OSP.QTY<1,DCP,VCP>;"!";EPTR;TEMP.QTY*100
               BEGIN CASE
                  CASE ACTION = "R"
                     PRICE = FIELD(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR)
* T20636
*                     IF PRICE = "" THEN
                        IF TEMP.GRP.QPARAM = "030" THEN
		             CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR,TEMP.STD*100)
*                           MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;TEMP.STD*100
                        END ELSE
*T22603
                           PRICE = FIELD(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR)
*T22603
		             CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR,PRICE)
*                           MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;PRICE
                        END
*                     END
                     PRICE = FIELD(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR)
* T20636
                     IF PRICE # "" THEN TEMP.STD = PRICE/100
                  CASE 1
                    CALL ESTCEM_MACRO_REPVAL(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR,TEMP.STD*100)
*                    MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;TEMP.STD*100
               END CASE
            CASE DCFND
               EST.DEPT.OSP.ID<1,DCP,VCP> = OSP.ID
               IF TEMP.TYPE = "P" THEN
                  EST.DEPT.OSP.UM<1,DCP,VCP> = "EA"
               END ELSE
                  EST.DEPT.OSP.UM<1,DCP,VCP> = TEMP.TYPE[2,1]
               END
               EST.DEPT.OSP.QTY<1,DCP,VCP> = TEMP.QTY*100
               EST.DEPT.OSP.PRICE<1,DCP,VCP> = TEMP.STD*100
            CASE 1
               EST.DEPT.OSP = INSERT(EST.DEPT.OSP,1,DCP,0,OSP.DC)
               EST.DEPT.OSP.ID = INSERT(EST.DEPT.OSP.ID,1,DCP,0,OSP.ID)
               IF TEMP.TYPE = "P" THEN
                  EST.DEPT.OSP.UM = INSERT(EST.DEPT.OSP.UM,1,DCP,0,"EA")
               END ELSE
                  EST.DEPT.OSP.UM = INSERT(EST.DEPT.OSP.UM,1,DCP,0,TEMP.TYPE[2,1])
               END
               EST.DEPT.OSP.QTY = INSERT(EST.DEPT.OSP.QTY,1,DCP,0,TEMP.QTY*100)
               EST.DEPT.OSP.PRICE = INSERT(EST.DEPT.OSP.PRICE,1,DCP,0,TEMP.STD*100)
         END CASE
      END

ESTDEPTINFO ="EST.DEPT.OSP=":EST.DEPT.OSP:"~EST.DEPT.OSP.ID=":EST.DEPT.OSP.ID:"~EST.DEPT.OSP.UM=":EST.DEPT.OSP.UM:"~EST.DEPT.OSP.QTY=":EST.DEPT.OSP.QTY:"~EST.DEPT.OSP.PRICE=":EST.DEPT.OSP.PRICE
STATUS = RBO.setProperty('', 'ESTDEPTINFO', ESTDEPTINFO)
RETURN
*
*---- END OF PROGRAM
*
99999*
STATUS = RBO.setProperty('', 'ServerMessage', ERRMSG)
RETURN
