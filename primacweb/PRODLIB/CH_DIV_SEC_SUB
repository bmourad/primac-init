SUBROUTINE CK_DIV_SEC_SUB
*********************************************************************
* Copyright 1998 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* PROGRAM      - CHECK.DIVISION.ACCESS
* BY           - ABED ALI
* DATE         - 05/27/2003
* DESCRIPTION  - This subroutine validates user access for a specific division.
*                If the SEC.DIVISION attribute is null, the user has
*                unrestricted access (i.e., can process ALL divisions).
*                This version of the subroutine is for programs using SCRN.EDIT.
*                Use SI.CK.DIV.SEC.SUB for programs using SCREEN.INPUT.
*ENDDOC
*********************************************************************
* In Properties:
* --------------
* Cono,USER_ID,DIV_CODE

* Out Properties:
* ---------------
*  AUTH_FLAG,AUTH_MSG
****************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB SECURITY
$INCLUDE PMC.CPYLIB COMPANY
**************
* OPEN FILES *
**************
  ERRMSG = ""
  OPEN '','CONTROL' TO CONTROL ELSE
     ERRMSG = "CONTROL FILE IS MISSING"
     GOTO 99999
  END
  OPEN '','SECURITY' TO SECURITY ELSE
     ERRMSG = "SECURITY FILE IS MISSING"
     GOTO 99999
  END
OPEN "","COMPANY" TO COMPANY ELSE
	ERRMSG="Unable to open COMPANY File"
	GOTO 99999
END

**************
* INITIALIZE *
**************
*
  STATUS = RBO.getProperty('','Cono',CONO)
  STATUS = RBO.getProperty('','USER_ID',USER.ID)
  STATUS = RBO.getProperty('','DIV_CODE',DIV.CODE)

  USER.ID = UPCASE(USER.ID)
  ALLOCFLAG=''
  MATREAD COMP.REC FROM COMPANY,CONO THEN
    ALLOCFLAG=CO.ALLOC.FLG
  END
  STATUS=RBO.setProperty('','CO_ALLOC_FLG',ALLOCFLAG)

  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE GOTO 99999
  IF SECURITY.REC<1> # "Y" THEN GOTO 99999
  MATREAD SEC.REC FROM SECURITY, CONO:USER.ID ELSE
     ERRMSG = "USER ":USER.ID:" NOT ON FILE"
     GOTO 99999
  END
*
  IF SEC.DIVISION = "" THEN
     IF DIV.CODE = "00" AND SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
        ERRMSG = "DIV '00' INVALID WHEN DIVISION CLOSING IS ENABLED"
        GOTO 99999
     END ELSE
        GOTO 99999
     END
  END
*
  LOCATE DIV.CODE IN SEC.DIVISION<1>,1 SETTING FOUND THEN GOTO 99999
  ELSE
     ERRMSG = "USER ":USER.ID:" NOT AUTHORIZED FOR DIVISION ":DIV.CODE
  END
*
99999 
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','AUTH_FLAG',"Un")
       STATUS = RBO.setProperty('','AUTH_MSG',ERRMSG)
  END
RETURN

