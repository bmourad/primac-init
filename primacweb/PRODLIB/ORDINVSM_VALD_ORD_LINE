SUBROUTINE ORDINVSM_VALD_ORD_LINE
********************************************************************************
* REVISION    - [e12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM    - PRIMAC
* AUTHOR - ABED ALI
* Program name :- ORDINVSM_VALD_ORD_LINE
*   Created:- 9/16/2003
*------------------------------------------------------------------------------*
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE OPS.CPYLIB ORDER.DETAIL
* Insert method code here
ERRMSG = ''
OPEN '','ORDER' TO ORDER ELSE
	ERRMSG = 'UNABLE TO OPEN ORDER FILE'
	GOTO 99999
END
OPEN '','ORDER.DETAIL' TO ORDER.DETAIL ELSE
	ERRMSG = 'UNABLE TO OPEN ORDER.DETAIL FILE'
	GOTO 99999
END

VAL = RBO.getProperty('','IVC_ORDER_NO',IVC.ORDER.NO) ;* CONO : CODE
VAL = RBO.getProperty('','IVC_SHIP_TO',IVC.SHIP.TO)
VAL = RBO.getProperty('','IVC_PROD',IVC.PROD)
VAL = RBO.getProperty('','MTYPE',MTYPE) ;* CODE

CONO = IVC.ORDER.NO[1,3]
MATREAD ORD.REC FROM ORDER, IVC.ORDER.NO ELSE
	ERRMSG = "Invalid Order number. Try again! "
    	GOTO 99999
END

ORD.ID = IVC.ORDER.NO:"!":IVC.SHIP.TO

MATREAD ORD.DET.REC FROM ORDER.DETAIL, ORD.ID ELSE
	ERRMSG="Cannot locate order detail for specifed ship-to."
    	GOTO 99999
END


TEMP.WHSE = ''
ECD.MAXV = ''
LOCATE IVC.PROD IN OSD.PROD<1>,1 SETTING PPTR THEN
	TEMP.WHSE = OSD.WHSE<1,PPTR>
END ELSE
	ERRMSG="Cannot locate product on order for specifed ship-to."
    	GOTO 99999
END

IF MTYPE = "CREDIT" OR MTYPE = "DEBIT" THEN
	ECD.MAXV = ''
END ELSE
	IF OSD.KIT<1,PPTR> = "K" THEN
     		ECD.MAXV = OSD.KIT.O.QTY<1,PPTR>
	END ELSE
     		ECD.MAXV = OSD.O.QTY<1,PPTR>
	END
END

VAL = RBO.setProperty('','IVC_WHSE',TEMP.WHSE)
VAL = RBO.setProperty('','MTYPE',ECD.MAXV)
VAL = RBO.setProperty('','IVC_SHP_COST',OCONV(OSD.PRICE<1,PPTR>,'MD4')) ;*THIS IS FOR UNIT PRICE

99999
CLOSE ORDER
CLOSE ORDER.DETAIL
IF ERRMSG # "" THEN
	VAL = RBO.setProperty('','ServerStatus','E')
	VAL = RBO.setProperty('','ServerMessage',ERRMSG)
END ELSE
	VAL = RBO.setProperty('','ServerStatus','')
END
* End of method code
RETURN

