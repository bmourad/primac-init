SUBROUTINE JOBINQ_SP_GetData
********************************************************************************
*   Program name :- JOBINQ_SP_GetData
*   Created:- 12/16/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'SP' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB OPERATION
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TOT.REC
$INCLUDE PMC.CPYLIB SHIP.VIA
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(SHIP.VIA,0)=0 THEN
   OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
      ERRMSG<1,-1>='SHIP.VIA FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF FILEINFO(OPERATION,0)=0 THEN
   OPEN '','OPERATION' TO OPERATION ELSE
      ERRMSG<1,-1>='OPERATION FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM CCTR(10)
EQU CCTR.NO    TO CCTR(1)
EQU OPER.NO    TO CCTR(2)
EQU OPER.NAME  TO CCTR(3)
EQU OPER.TYP  TO CCTR(4)
EQU OPER.SHIP  TO CCTR(5)
EQU OPER.QTY   TO CCTR(6)
EQU OPER.DATE  TO CCTR(7)
EQU OPER.COST  TO CCTR(8)
EQU OPER.SALE  TO CCTR(9)
EQU CCTR.NAME  TO CCTR(10)
MAT CCTR = ''
DIM OTYPE(5)
OTYPE(1) = 'R,'
OTYPE(2) = 'N,'
OTYPE(3) = 'O,'
OTYPE(4) = 'S,'
OTYPE(5) = 'C'
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='SP'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046

END
MAT JOB.TOT.REC = ''
JPTR = 0
JCNT = 0
CNT = COUNT(JOB.SP.DEPT,VM) + (JOB.SP.DEPT # '')
FOR I = 1 TO CNT
   TEMP = JOB.SP.DEPT<1,I>
   LOCATE TEMP IN SP.DEPT.NO<1>,1 SETTING JCNT ELSE
      JPTR = JPTR + 1; JCNT = JPTR
      SP.DEPT.NO<1,JCNT> = TEMP
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:TEMP ELSE DEPT.DESC = UNKNOWN
      SP.DEPT.DESC<1,JCNT> = DEPT.DESC
   END
   SP.DEPT.PTR<1,JCNT,-1> = I
   SP.DEPT.CHG<1,JCNT> = SP.DEPT.CHG<1,JCNT> + JOB.SP.TCOST<1,I,1>
   SP.DEPT.NCHG<1,JCNT> = SP.DEPT.NCHG<1,JCNT> + JOB.SP.TCOST<1,I,2>
   SP.DEPT.CORD<1,JCNT> = SP.DEPT.CORD<1,JCNT> + JOB.SP.TCOST<1,I,3>
   SP.DEPT.SPOIL<1,JCNT> = SP.DEPT.SPOIL<1,JCNT> + JOB.SP.TCOST<1,I,4>
   SP.DEPT.CNCR<1,JCNT> = SP.DEPT.CNCR<1,JCNT> + JOB.SP.TCOST<1,I,5>
   SP.DEPT.COST<1,JCNT> = SP.DEPT.COST<1,JCNT> + JOB.SP.COST<1,I>
   SP.DEPT.SALE<1,JCNT> = SP.DEPT.SALE<1,JCNT> + JOB.SP.SALE<1,I>
NEXT I
FOR I = 1 TO JPTR
   SP.TOT.CHG = SP.TOT.CHG + SP.DEPT.CHG<1,I>
   SP.TOT.NCHG = SP.TOT.NCHG + SP.DEPT.NCHG<1,I>
   SP.TOT.CORD = SP.TOT.CORD + SP.DEPT.CORD<1,I>
   SP.TOT.SPOIL = SP.TOT.SPOIL + SP.DEPT.SPOIL<1,I>
   SP.TOT.CNCR = SP.TOT.CNCR + SP.DEPT.CNCR<1,I>
   SP.TOT.COST = SP.TOT.COST + SP.DEPT.COST<1,I>
   SP.TOT.SALE = SP.TOT.SALE + SP.DEPT.SALE<1,I>
NEXT I
LINES = COUNT(SP.DEPT.NO,VM) + (SP.DEPT.NO # '')
FOR I = 1 TO LINES
   CNT = COUNT(SP.DEPT.PTR<1,I>,SVM) + (SP.DEPT.PTR<1,I> # '')
   FOR J = 1 TO CNT
      PTR = SP.DEPT.PTR<1,I,J>
      CCTR.NO<1,I,J> = JOB.SP.CCTR<1,PTR>
      OPER.NO<1,I,J> = JOB.SP.OPER<1,PTR>
      MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR.NO<1,I,J> ELSE CCTR.DESC = UNKNOWN
      MATREAD OPER.REC FROM OPERATION, CONO : OPER.NO<1,I,J> ELSE OPER.DESC = UNKNOWN
      MATREAD SHIP.VIA.REC FROM SHIP.VIA , CONO:JOB.SP.VIA<1,PTR> ELSE
         SHPV.DESC = "???????????"
      END
      CCTR.NAME<1,I,J> = CCTR.DESC
      OPER.NAME<1,I,J> = OPER.DESC
      OPER.SHIP<1,I,J> = SHPV.DESC
      OPER.QTY<1,I,J> = OCONV(JOB.SP.QTY<1,PTR>,'MD0')
      OPER.DATE<1,I,J> = OCONV(JOB.SP.DATE<1,PTR,1>,'D2/')
      OPER.COST<1,I,J> = OCONV(JOB.SP.COST<1,PTR>,'MD2,')
      OPER.SALE<1,I,J> = OCONV(JOB.SP.SALE<1,PTR>,'MD2,')
      FOR K = 1 TO 5
         IF JOB.SP.TCOST<1,PTR,K> > 0 THEN OPER.TYP<1,I,J> = OPER.TYP<1,I,J> : OTYPE(K)
      NEXT K
   NEXT J
   SP.DEPT.CHG<1,I> = OCONV(SP.DEPT.CHG<1,I>,'MD2,')
   SP.DEPT.NCHG<1,I> = OCONV(SP.DEPT.NCHG<1,I>,'MD2,')
   SP.DEPT.CORD<1,I> = OCONV(SP.DEPT.CORD<1,I>,'MD2,')
   SP.DEPT.CNCR<1,I> = OCONV(SP.DEPT.CNCR<1,I>,'MD2,')
   SP.DEPT.SPOIL<1,I> = OCONV(SP.DEPT.SPOIL<1,I>,'MD2,')
   SP.DEPT.COST<1,I> = OCONV(SP.DEPT.COST<1,I>,'MD2,')
   SP.DEPT.SALE<1,I> = OCONV(SP.DEPT.SALE<1,I>,'MD2,')
NEXT I

STATUS=RBO.setProperty('','SP_DEPT_NO',SP.DEPT.NO)
STATUS=RBO.setProperty('','SP_DEPT_CHG',SP.DEPT.CHG)
STATUS=RBO.setProperty('','SP_TOT_CHG',OCONV(SP.TOT.CHG,'MD2,'))
STATUS=RBO.setProperty('','SP_DEPT_NCHG',SP.DEPT.NCHG)
STATUS=RBO.setProperty('','SP_TOT_NCHG',OCONV(SP.TOT.NCHG,'MD2,'))
STATUS=RBO.setProperty('','SP_DEPT_CORD',SP.DEPT.CORD)
STATUS=RBO.setProperty('','SP_TOT_CORD',OCONV(SP.TOT.CORD,'MD2,'))
STATUS=RBO.setProperty('','SP_DEPT_SPOIL',SP.DEPT.SPOIL)
STATUS=RBO.setProperty('','SP_TOT_SPOIL',OCONV(SP.TOT.SPOIL,'MD2,'))
STATUS=RBO.setProperty('','SP_DEPT_COST',SP.DEPT.COST)
STATUS=RBO.setProperty('','SP_TOT_COST',OCONV(SP.TOT.COST,'MD2,'))
STATUS=RBO.setProperty('','SP_DEPT_SALE',SP.DEPT.SALE)
STATUS=RBO.setProperty('','SP_TOT_SALE',OCONV(SP.TOT.SALE,'MD2,'))
STATUS=RBO.setProperty('','SP_DEPT_DESC',SP.DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_NO',CCTR.NO)
STATUS=RBO.setProperty('','OPER_NO',OPER.NO)
STATUS=RBO.setProperty('','OPER_SHIP',OPER.SHIP)
STATUS=RBO.setProperty('','OPER_TYPE',OPER.TYP)
STATUS=RBO.setProperty('','OPER_QTY',OPER.QTY)
STATUS=RBO.setProperty('','OPER_DATE',OPER.DATE)
STATUS=RBO.setProperty('','OPER_COST',OPER.COST)
STATUS=RBO.setProperty('','OPER_SALE',OPER.SALE)
STATUS=RBO.setProperty('','CCTR_NAME',CCTR.NAME)
STATUS=RBO.setProperty('','OPER_NAME',OPER.NAME)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN

