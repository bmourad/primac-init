SUBROUTINE EST_CALC_COST
***************************************************************************
*   Program name :- EST_CALC_COST
*   Created:- 3/15/2005  Razi Mohiuddin
*   Modified by   :- Ramakrishna Pusuluri
*   PROGRAM  - EST.CALC.COST
*   DESCRIPTION
*
* This subroutine calculates the cost and sale amount for the specified
* line of the estimate detail.
*
*T27716 cmykleb 10/08/2003 * Add Pre-press inclusive functionality.
***************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE JES.CPYLIB SCOMMON.ESTIMATE
$DEFINE SCOMMONESTIMATEQP
$DEFINE ESTIMATE
$INCLUDE JES.CPYLIB ESTIMATE
$DEFINE ESTIMATETEMP
$INCLUDE JES.CPYLIB ESTIMATE.TEMP
$INCLUDE JES.CPYLIB ESTIMATE.MARKUP
$INCLUDE JCS.CPYLIB FOH.TABLE
$DEFINE FILEVARS
$INCLUDE CPYLIB FILE.VARS
$DEFINE JESFILEVARS
$INCLUDE JES.CPYLIB JES.FILE.VARS
$INCLUDE CPYLIB CHAR
$DEFINE COMPANY
$INCLUDE PMC.CPYLIB COMPANY


ERRMSG = ""
ESTDEPTINFO = ""
*TEMP.TSALE = 0 
*TEMP.HRS = 0
*TEMP.DCOST = 0
*TEMP.COST = 0
*TEMP.SALE = 0
OPEN '','ESTIMATE' TO ESTIMATE ELSE 
	ERRMSG = 'Cannot open ESTIMATE file!'
	GOTO 99999    
END
OPEN '','ESTIMATE.MARKUP' TO ESTIMATE.MARKUP ELSE 
	ERRMSG = 'Cannot open ESTIMATE.MARKUP file!'
	GOTO 99999     
END
OPEN "FOH.TABLE" TO FOH.TABLE ELSE ERRMSG="CANNOT OPEN FOH.TABLE FILE";GOTO 99999
OPEN '','CONTROL' TO CONTROL ELSE RETURN;* added 
OPEN '','COMPANY' TO COMPANY ELSE 
	ERRMSG = "Cannot open COMPANY file!"
*	RETURN
END
*
*---- INITIALIZATION
*
*T27716 v


*T27716 ^
*
*---- MAIN PROCESSING
*
	
    ERR = RBO.getProperty('','DREFCREF',VALUECONTENT)
    ERR = RBO.getProperty('','PMCProperty',PMCProperty)


*(CONO, EQTY, DEPT, COMP, REF.NO, MAT ESTG.REC)

	CONO           = PMCProperty<1,4>	
	DPTR           = FIELD(VALUECONTENT,"^",1)
	TEMP.TYPE	 = FIELD(VALUECONTENT,"^",2)
	TEMP.OPV.TYPE  = FIELD(VALUECONTENT,"^",3)
	TEMP.QTY	 = FIELD(VALUECONTENT,"^",4)
	TEMP.FCTR	 = FIELD(VALUECONTENT,"^",5)
	TEMP.STD	 = FIELD(VALUECONTENT,"^",6)
	TEMP.RATE 	 = FIELD(VALUECONTENT,"^",7)
	TEMP.UM	 = FIELD(VALUECONTENT,"^",8)
	TEMP.IND.1	 = FIELD(VALUECONTENT,"^",9)
	TEMP.IND.2	 = FIELD(VALUECONTENT,"^",10)
	TEMP.IND.3	 = FIELD(VALUECONTENT,"^",11)
	TEMP.IND.4	 = FIELD(VALUECONTENT,"^",12)
	TEMP.MARKUP	 = FIELD(VALUECONTENT,"^",13)
	EST.KEY	 = FIELD(VALUECONTENT,"^",14)
	NEW_ITEM 	= FIELD(VALUECONTENT,"^",15)
	EST.PP.INCLUDE= FIELD(VALUECONTENT,"^",16)

   MATREAD EST.REC FROM ESTIMATE, CONO:EST.KEY ELSE
	MAT EST.REC = ""
   END
   READ EST.PP FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE EST.PP = ''
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      MAT COMP.REC = ""
   END
   IF NEW_ITEM = 1 THEN
	FOR M=1 TO 6
            EST.MARKUP<1,M>=CO.JES.PARAM<1,M>
       NEXT M
   END


100*
   BEGIN CASE
      CASE TEMP.TYPE[1,1] = "L"
         IF TEMP.OPV.TYPE = "F" THEN
            TEMP.HRS = INT(TEMP.QTY*TEMP.FCTR*TEMP.STD/10+0.5)
         END ELSE
            BEGIN CASE
               CASE TEMP.FCTR+0 = 0 OR TEMP.STD+0 = 0
                  TEMP.HRS = 0
               CASE 1
                  TEMP.HRS = INT(TEMP.QTY/(TEMP.FCTR/1000*TEMP.STD)*100+0.5)
            END CASE
         END
         TEMP.DCOST = INT(TEMP.HRS*TEMP.RATE/100+0.5)
         BASE.COST = TEMP.RATE
      CASE TEMP.TYPE = "PT"
         IF TEMP.UM+0 = 0 THEN TEMP.UM = 1
         TEMP.DCOST = INT((TEMP.FCTR/1000)*(TEMP.STD*100)+0.5)
         BASE.COST = TEMP.DCOST
      CASE 1
         IF TEMP.UM+0 = 0 THEN TEMP.UM = 1
         TEMP.DCOST = INT((TEMP.QTY/TEMP.UM)*(TEMP.FCTR/1000)*(TEMP.STD*100)+0.5)
         BASE.COST = TEMP.DCOST
   END CASE
 
   IND.COST = 0
   FOR TI = 1 TO 4
      BEGIN CASE
         CASE TI = 1
            TEMP.IND = TEMP.IND.1
         CASE TI = 2
            TEMP.IND = TEMP.IND.2
         CASE TI = 3
            TEMP.IND = TEMP.IND.3
         CASE TI = 4
            TEMP.IND = TEMP.IND.4
      END CASE

      BEGIN CASE
         CASE TEMP.IND = ""
         CASE TEMP.IND[1,1] = "$"
            IND.AMT = TEMP.IND[2,99]
            IND.COST = IND.COST+IND.AMT
         CASE TEMP.IND[1,1] = "%" OR NUM(TEMP.IND)
            IF TEMP.IND[1,1] = "%" THEN IND.PCT = TEMP.IND[2,99] ELSE IND.PCT = TEMP.IND
            IND.COST = IND.COST+INT(BASE.COST*(IND.PCT/10000)+0.5)
         CASE 1
            MATREAD FTR.REC FROM FOH.TABLE, CONO:TEMP.IND ELSE
               MAT FTR.REC = ""
            END
            LOCATE BASE.COST IN FTR.QTY<1>,1 BY "AR" SETTING FP ELSE NULL
            IF FP > COUNT(FTR.QTY,VM) + 1 THEN FP = COUNT(FTR.QTY,VM) + 1
            IF FTR.TYPE = "$" THEN
               IND.COST = IND.COST+FTR.PCT<1,FP>
            END ELSE
               IND.COST = IND.COST+INT(BASE.COST*(FTR.PCT<1,FP>/10000)+0.5)
            END
      END CASE
   NEXT TI
   BEGIN CASE
      CASE TEMP.TYPE[1,1] = "L"
         TEMP.COST = INT(TEMP.HRS*(TEMP.RATE+IND.COST)/100+0.5)
      CASE 1
         TEMP.COST = TEMP.DCOST+IND.COST
   END CASE
*T27716 v
   EST.PP.INCLUDE= FIELD(VALUECONTENT,"^",16)
   IF EST.PP.INCLUDE # 'Y' THEN
      LOCATE EST.DEPT<1,DPTR> IN EST.PP<1>,1 SETTING DPOS THEN
         TEMP.SALE = 0
         TEMP.TSALE = 0
         GOTO 99999
      END
   END
*T27716 ^
   TEMP.SALE = TEMP.COST
   BEGIN CASE
      CASE TEMP.MARKUP = ""
      CASE NUM(TEMP.MARKUP)
         TEMP.SALE = TEMP.SALE+INT(TEMP.COST*(TEMP.MARKUP/10000)+0.5)
      CASE 1
         MATREAD MKU.REC FROM ESTIMATE.MARKUP, CONO:TEMP.MARKUP ELSE
            MAT MKU.REC = ""
         END
         LOCATE TEMP.COST IN MKU.QTY<1>,1 BY "AR" SETTING FP ELSE NULL
         IF FP > COUNT(MKU.QTY,VM) + 1 THEN FP = COUNT(MKU.QTY,VM) + 1
         TEMP.SALE = TEMP.SALE+INT(TEMP.COST*(MKU.PCT<1,FP>/10000)+0.5)
   END CASE
   BEGIN CASE
      CASE TEMP.TYPE[1,1] = "L"
         TEMP.TSALE = INT(TEMP.SALE * (1+EST.MARKUP<1,1>/10000) + 0.5)
      CASE TEMP.TYPE = "MS" OR TEMP.TYPE = "MR"
         TEMP.TSALE = INT(TEMP.SALE * (1+EST.MARKUP<1,2>/10000) + 0.5)
      CASE TEMP.TYPE[1,1] = "M"
         TEMP.TSALE = INT(TEMP.SALE * (1+EST.MARKUP<1,3>/10000) + 0.5)
      CASE TEMP.TYPE[1,1] = "P"
         TEMP.TSALE = INT(TEMP.SALE * (1+EST.MARKUP<1,4>/10000) + 0.5)
      CASE TEMP.TYPE[1,1] = "S"
         TEMP.TSALE = INT(TEMP.SALE * (1+EST.MARKUP<1,5>/10000) + 0.5)
      CASE TEMP.TYPE[1,1] = "O"
         TEMP.TSALE = INT(TEMP.SALE * (1+EST.MARKUP<1,6>/10000) + 0.5)
   END CASE

*	  TEMP.TSALE = OCONV(TEMP.TSALE,'MD2')

	ESTDEPTINFO = TEMP.TSALE:"^":TEMP.HRS:"^":TEMP.DCOST:"^":TEMP.COST:"^":TEMP.SALE

	STATUS = RBO.setProperty('', 'ESTDEPTINFO', ESTDEPTINFO)
RETURN

99999*	
	IF ERRMSG # "" THEN		
		STATUS = RBO.setProperty('', 'ServerStatus', 1)
	      	STATUS = RBO.setProperty('', 'ServerMessage', ERRMSG)	   
	END
RETURN

