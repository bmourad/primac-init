*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Program Name :APCPCDM_CashDisbBuild
* BY          - WALID YAMOUT, C.B.A
* DATE        - 11/12/86
* DESCRIPTION - This program builds the APCPCDM_CashDisb file.
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
$INCLUDE WWINSERT RBO.H
$INCLUDE APS.CPYLIB APCPCDM.CASH.DISB
$INCLUDE APS.CPYLIB OAP
$INCLUDE APS.CPYLIB CKP
$INCLUDE PMC.CPYLIB VEND
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE CPYLIB CHAR
*
  DIM SVEND.REC(10)
  EQU SVEND.NO TO SVEND.REC(1)
  EQU SVEND.DS TO SVEND.REC(2)
  EQU SVEND.PT TO SVEND.REC(3)
*
  ERRMSG=''
  UNKNOWN=STR("?",30)
**************
* OPEN FILES *
**************
  OPEN '', 'VEND' TO VEND ELSE
    ERRMSG = 'VEND FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'APCPCDM.CASH.DISB' TO APCPCDM.CASH.DISB ELSE
    ERRMSG = 'APCPCDM.CASH.DISB FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'OAP' TO OAP ELSE
    ERRMSG = 'OAP FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'CKP' TO CKP ELSE
    ERRMSG = 'CKP FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'SQV' TO SQV ELSE
    ERRMSG = 'SQV FILE IS MISSING'
    GOTO 93000
  END
  MAT COMP.REC = ''
*
  STATUS=RBO.getProperty('','ID',ID)
  STATUS=RBO.getProperty('','DivCode',DIV.OWNER)
  STATUS=RBO.getProperty('','Disb_Mode',DISB.MODE)
  CONO=ID[1,3]
  TYPE='U'; FUNC=DISB.MODE
  CALL APSCheckPrcsChk(CONO,TYPE,DIV.OWNER,FUNC,ERRMSG)
  IF ERRMSG # "" THEN GOSUB 92000; GOTO 93000
  MATREAD COMP.REC FROM COMPANY ,CONO ELSE ERRMSG = 'COMPANY NOT ON FILE '; GOTO 93000
  READV DUE.DATE FROM CONTROL, CONO : "SCH.PAYDATE":DIV.OWNER,1 ELSE ERRMSG = "CONTROL RECORD 'SCH.PAYDATE":DIV.OWNER:"' IS MISSING"; GOTO 93000
  READU DUMMY FROM CONTROL, CONO :"PAYDATE":DIV.OWNER ELSE DUMMY = ""
  WRITE DUE.DATE ON CONTROL, CONO :"PAYDATE":DIV.OWNER
  DUE.DATE = ICONV(DUE.DATE, "D2")
  MATREAD SVEND.REC FROM CONTROL, CONO:"VENDORS":DIV.OWNER ELSE MAT SVEND.REC = ""
  IF SVEND.PT = "" THEN SVEND.PT = "S"
  MATREAD CDM.REC FROM APCPCDM.CASH.DISB, CONO:"VENDORS":DIV.OWNER ELSE
    MAT CDM.REC = ''
  END
  VEND.DESCS=''
  IF DISB.MODE = 'U' THEN GOTO 99999
  STMT = 'SELECT CKP WITH CONO = "':CONO:'"'
  IF DIV.OWNER # 'ALL' THEN
    STMT := ' AND WITH VCH.DIV.OWNER = "':DIV.OWNER:'"'
  END
  UDTEXECUTE STMT CAPTURING JUNK
  IF SYSTEM(11) > 0 THEN
    DATA=1
    LOOP
      READNEXT CKPID ELSE DATA = 0
    WHILE DATA DO
      DELETE CKP, CKPID
    REPEAT
  END
  DELETE APCPCDM.CASH.DISB, CONO : "VENDORS":DIV.OWNER
  MAT CDM.REC = ''
  IF DISB.MODE = 'M' THEN GOTO 200
******************
* INITIALIZATION *
******************
  GRS.AMT = 0
  DSC.AMT = 0
  NET.AMT = 0
*******************
* MAIN PROCESSING *
*******************
  PORT.NO='TTY'
  CALL SYSVARS.SUB(PORT.NO)
  LIST.NAME='SCD.RPT':PORT.NO
  UDTEXECUTE 'GET.LIST ':LIST.NAME CAPTURING JUNK
  IF SYSTEM(11) < 1 THEN GOTO 101
100*
  READNEXT OID ELSE
101 LINES = DCOUNT(CDM.VEND,VM)
    IF LINES < 1 THEN
      DELETE APCPCDM.CASH.DISB, CONO : "VENDORS":DIV.OWNER
      ERRMSG = "NO VENDORS WERE SELECTED TO BE PAID"
      GOSUB 92000; GOTO 93000
    END
    FOR LN = LINES TO 1 STEP -1
      IF SUM(CDM.NET.AMT<1,LN>) < 0 THEN
        FOR I = DCOUNT(CDM.VOUCH<1,LN>,SVM) TO 1 STEP -1
          DELETE CKP, CONO:CDM.VOUCH<1,LN,I>
          DEL CDM.VOUCH<1,LN,I>
          DEL CDM.INVOICE<1,LN,I>
          DEL CDM.PO<1,LN,I>
          DEL CDM.DUE.DATE<1,LN,I>
          DEL CDM.VGRS.AMT<1,LN,I>
          DEL CDM.VDSC.AMT<1,LN,I>
          DEL CDM.VNET.AMT<1,LN,I>
        NEXT I
        DEL CDM.VEND<1,LN>
        DEL CDM.GRS.AMT<1,LN>
        DEL CDM.DSC.AMT<1,LN>
        DEL CDM.NET.AMT<1,LN>
        LINES = LINES - 1
      END ELSE
        CDM.GRS.AMT<1,LN> = SUM(CDM.VGRS.AMT<1,LN>)
        CDM.DSC.AMT<1,LN> = SUM(CDM.VDSC.AMT<1,LN>)
        CDM.NET.AMT<1,LN> = SUM(CDM.VNET.AMT<1,LN>)
      END
    NEXT LN
    IF LINES < 1 THEN
      DELETE APCPCDM.CASH.DISB, CONO : "VENDORS":DIV.OWNER
      ERRMSG = "NO VENDORS WERE SELECTED TO BE PAID"
      GOSUB 92000; GOTO 93000
    END
    MATWRITE CDM.REC ON APCPCDM.CASH.DISB, CONO:"VENDORS":DIV.OWNER
200 *
    FOR I = 1 TO LINES
      CDM.GRS.AMT<1,LN> = OCONV(CDM.GRS.AMT<1,LN>,'MD2')
      CDM.DSC.AMT<1,LN> = OCONV(CDM.DSC.AMT<1,LN>,'MD2')
      CDM.NET.AMT<1,LN> = OCONV(CDM.NET.AMT<1,LN>,'MD2')
      FOR II = 1 TO DCOUNT(CDM.VOUCH<1,LN>,SVM)
        CDM.DUE.DATE<1,I,II>=OCONV(CDM.DUE.DATE<1,I,II>,'D2/')
        CDM.VGRS.AMT<1,I,II>=OCONV(CDM.VGRS.AMT<1,I,II>,'MD2')
        CDM.VDSC.AMT<1,I,II>=OCONV(CDM.VDSC.AMT<1,I,II>,'MD2')
        CDM.VNET.AMT<1,I,II>=OCONV(CDM.VNET.AMT<1,I,II>,'MD2')
      NEXT II
    NEXT I
    STATUS=RBO.setProperty('','VendorNo',CDM.VEND)
    STATUS=RBO.setProperty('','VendorDesc',VEND.DESCS)
    STATUS=RBO.setProperty('','GrossAmt',CDM.GRS.AMT)
    STATUS=RBO.setProperty('','DiscountAmt',CDM.DSC.AMT)
    STATUS=RBO.setProperty('','NetAmt',CDM.NET.AMT)
    STATUS=RBO.setProperty('','Voucher',CDM.VOUCH)
    STATUS=RBO.setProperty('','Invoice',CDM.INVOICE)
    STATUS=RBO.setProperty('','PONumber',CDM.PO)
    STATUS=RBO.setProperty('','DueDate',CDM.DUE.DATE)
    STATUS=RBO.setProperty('','VouchGross',CDM.VGRS.AMT)
    STATUS=RBO.setProperty('','VouchDiscount',CDM.VDSC.AMT)
    STATUS=RBO.setProperty('','VouchNet',CDM.VNET.AMT)
    GOTO 99999
  END
  IF CONO # OID[1,3] THEN GOTO 100
  MATREAD OAP.REC FROM OAP, OID ELSE GOTO 100
  READV THIS.DIV.OWNER FROM SQV, OID, 47 ELSE THIS.DIV.OWNER = ""
  IF THIS.DIV.OWNER # DIV.OWNER AND DIV.OWNER # "ALL" THEN GOTO 100
*
  IF SVEND.PT = "S" THEN
    LOCATE OAP.VEND<1,1> IN SVEND.NO<1>,1 SETTING SFND ELSE SFND = 0
    IF SFND THEN GOTO 100
  END ELSE
    LOCATE OAP.VEND<1,1> IN SVEND.NO<1>,1 SETTING SFND ELSE SFND = 0
    IF NOT(SFND) THEN GOTO 100
  END
  VOUCH.NO = OID[4,6]
  IF OAP.DUE.DATE > DUE.DATE THEN GOTO 100
  GRS.AMT = OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.PAID
  DSC.AMT = OAP.DSC.AMT - OAP.DSC.PAID
  IF DSC.AMT < 0 AND OAP.GRS.AMT > 0 THEN DSC.AMT = 0
  IF DSC.AMT > 0 AND OAP.GRS.AMT < 0 THEN DSC.AMT = 0
  NET.AMT = GRS.AMT - DSC.AMT
  IF GRS.AMT = 0 THEN GOTO 100
  IF NET.AMT = 0 THEN GOTO 100
  VCNT = COUNT(OAP.VEND,VM) + (OAP.VEND # "")
  FND = 1
  BEGIN CASE
    CASE VCNT < 1
      GOTO 100
    CASE VCNT = 1
      LOCATE OAP.VEND IN CDM.VEND,1 BY 'AL' SETTING VFND ELSE
        INS OAP.VEND BEFORE CDM.VEND<1,VFND>
        INS '' BEFORE CDM.GRS.AMT<1,VFND>
        INS '' BEFORE CDM.DSC.AMT<1,VFND>
        INS '' BEFORE CDM.NET.AMT<1,VFND>
        MATREAD VEND.REC FROM VEND, CONO:OAP.VEND THEN
          INS VEND.DESC BEFORE VEND.DESCS<1,VFND>
        END ELSE INS UNKNOWN BEFORE VEND.DESCS<1,VFND>
        FND = 0
      END
    CASE 1
      CONVERT VM TO SVM IN OAP.VEND
      LOCATE OAP.VEND IN CDM.VEND,1 BY 'AL' SETTING VFND ELSE
        INS OAP.VEND BEFORE CDM.VEND<1,VFND>
        INS '' BEFORE CDM.GRS.AMT<1,VFND>
        INS '' BEFORE CDM.DSC.AMT<1,VFND>
        INS '' BEFORE CDM.NET.AMT<1,VFND>
        INS OAP.VEND<1,1,2> BEFORE VEND.DESCS<1,VFND>
        FND = 0
      END
  END CASE
  IF NOT(FND) THEN
    INS '' BEFORE CDM.VOUCH<1,VFND>
    INS '' BEFORE CDM.INVOICE<1,VFND>
    INS '' BEFORE CDM.PO<1,VFND>
    INS '' BEFORE CDM.DUE.DATE<1,VFND>
    INS '' BEFORE CDM.VGRS.AMT<1,VFND>
    INS '' BEFORE CDM.VDSC.AMT<1,VFND>
    INS '' BEFORE CDM.VNET.AMT<1,VFND>
  END
  LOCATE VOUCH.NO IN CDM.VOUCH<1,VFND>,1 BY 'DR' SETTING VOU ELSE
    INS VOUCH.NO BEFORE CDM.VOUCH<1,VFND,VOU>
    INS OAP.INV BEFORE CDM.INVOICE<1,VFND,VOU>
    INS OAP.PO BEFORE CDM.PO<1,VFND,VOU>
    INS OAP.DUE.DATE BEFORE CDM.DUE.DATE<1,VFND,VOU>
    INS GRS.AMT BEFORE CDM.VGRS.AMT<1,VFND,VOU>
    INS DSC.AMT BEFORE CDM.VDSC.AMT<1,VFND,VOU>
    INS NET.AMT BEFORE CDM.VNET.AMT<1,VFND,VOU>
  END
  MAT CKP.REC = ""
  CKP.VEND = OAP.VEND
  CKP.INV = OAP.INV
  CKP.INV.DATE = OAP.INV.DATE
  CKP.DUE.DATE = OAP.DUE.DATE
  CKP.GRS.AMT = GRS.AMT
  CKP.MER.AMT = OAP.MER.AMT
  CKP.DSC.AMT = DSC.AMT
  CKP.CTR = OAP.CTR
  CKP.ACCT = OAP.ACCT
  CKP.DIST.AMT = OAP.DIST.AMT
  CKP.JOB = OAP.JOB
  CKP.PO = OAP.PO
  CKP.ASSET.ID = OAP.ASSET.ID
  CKP.JCS.TRANS = OAP.JCS.TRANS
  CKP.TERMS = OAP.TERMS
  CKP.DIV = OAP.DIV
  CKP.DEPT = OAP.DEPT
  CKP.CCTR = OAP.CCTR
  CKP.MON = OAP.MON
  CKP.REM.COMM = OAP.REM.COMM ; *T25759
  CKP.COMMENT = OAP.COMMENT
  CKP.MISC.CAT = OAP.MISC.CAT
  CKP.AP.ACCT = OAP.AP.ACCT
  CKP.AP.AMT = OAP.AP.AMT
  CKP.AP.DIV = OAP.AP.DIV
  CKP.AP.DEPT = OAP.AP.DEPT
  CKP.AP.CCTR = OAP.AP.CCTR
  MATWRITE CKP.REC ON CKP, OID
  GOTO 100
*****************
* ERROR ROUTINE *
*****************
92000*
  TYPE='C'; FUNC=DISB.MODE
  CALL APSCheckPrcsChk(CONO,TYPE,DIV.OWNER,FUNC,ERRMSG)
  RETURN

93000*
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG) 

99999*
END
