SUBROUTINE GET_GLHX_CCTR
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - GET_GLHX_CCTR
*
* AUTHOR   - CHRIS MYKLEBUST, PRIMAC SYSTEMS
*
* DATE     - 10/09/2002
*
* DESCRIPTION
*
* This program gets all of the active cctrs for the current acct/div/dept.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
   $INCLUDE WWINSERT RBO.H
   $INCLUDE PMC.CPYLIB COST.CNTR
   $INCLUDE CPYLIB CHAR
*
ERRMSG = ""
   IF FILEINFO(GLHX,0) = 0 THEN
      OPEN '','GLHX' TO GLHX ELSE
         ERRMSG = 'Cannot open GLHX file!'
         GOTO 1000
      END
   END
   IF FILEINFO(COST.CNTR,0) = 0 THEN
      OPEN '','COST.CNTR' TO COST.CNTR ELSE
         ERRMSG = 'Cannot open COST.CNTR file!'
         GOTO 1000
      END
   END
*
*---- INITIALIZATION
*
   GEN.CCTR = "000"
   STATUS = RBO.getProperty('','ID',ID)
   STATUS = RBO.getProperty('','PERIOD',PER)
   STATUS = RBO.getProperty('','ACCT',ACCT)
   STATUS = RBO.getProperty('','DIV',DIV)
   STATUS = RBO.getProperty('','DEPT',DEPT)
   CONO = ID[1,3]
   READ ALL.CCTR FROM GLHX, CONO:DIV:DEPT:ACCT:PER ELSE ALL.CCTR = ""

   XCCTR = 0 ; XDESC = "--Select--"
   XCCTR<1,2> = "S" ; XDESC<1,2> = "S-Summary"
   XLINES = DCOUNT(ALL.CCTR,AM)
   FOR X = 3 TO XLINES
      XCODE = ALL.CCTR<X>
      IF XCODE = GEN.CCTR THEN
         MAT CCTR.REC = ""
         CCTR.DESC = "General Cost Center"
      END ELSE
         MATREAD CCTR.REC FROM COST.CNTR,CONO:XCODE ELSE
            MAT CCTR.REC = ""
         END
         IF CCTR.DESC = "" THEN CCTR.DESC = "Unknown"
      END
      XCCTR<1,-1> = XCODE
      XDESC<1,-1> = XCODE :"-": CCTR.DESC
   NEXT X
   *** BUILD RBO RECORDS ***
   STATUS = RBO.setProperty('','CostCenterCodes',XCCTR)
   STATUS = RBO.setProperty('','CostCenterCodesDescs',XDESC)
1000 *
IF ERRMSG # "" THEN
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
END
   RETURN
