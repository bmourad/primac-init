SUBROUTINE GLATM_UPD_CATG_SUBCATG
********************************************************************************
*   Program name :- GLATM_UPD_CATG_SUBCATG
*   Created:- 6/17/2003
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  ID, AT_CATG, CATG_DESC,ORIG_CATG,SUB_CATG,SUB_DESC,SUB_C,SUB_F,SUB_P
*
* Out Properties:
* ---------------
*  COMMIT TRANSACTION TO DATA FILES COA, ACCT.TYPE ,ACCT.CATG, ACCT.SUB.CATG
********************************************************************************

$INCLUDE WWINSERT RBO.H
$INCLUDE GLS.CPYLIB ACCT.TYPE
$INCLUDE GLS.CPYLIB ACCT.CATG
$INCLUDE GLS.CPYLIB ACCT.SUB.CATG
$INCLUDE GLS.CPYLIB CATG.AND.SUB
$INCLUDE PMC.CPYLIB COA
$INCLUDE CPYLIB CHAR

* Insert method code here
ERRMSG = ''
OPEN '','ACCT.TYPE' TO ACCT.TYPE ELSE
	ERRMSG = 'Cannot open ACCT.TYPE file!'
	GOTO 99999
END

OPEN '','ACCT.CATG' TO ACCT.CATG ELSE
	ERRMSG = 'Cannot open ACCT.CATG file!'
      	GOTO 99999
END

OPEN '','ACCT.SUB.CATG' TO ACCT.SUB.CATG ELSE
	ERRMSG = 'Cannot open ACCT.SUB.CATG file!'
      	GOTO 99999
END

OPEN '','CATG.AND.SUB' TO CATG.AND.SUB ELSE
	ERRMSG = 'Cannot open CATG.AND.SUB file!'
      	GOTO 99999
END

OPEN '','COA' TO COA ELSE
	ERRMSG = 'Cannot open COA file!'
      	GOTO 99999
END
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','AT_CATG',PRT.CATG) ; * UPDATED CATG.CODES
STATUS = RBO.getProperty('','AT_DESC',AT_DESC) ; * UPDATED CATG.CODES
STATUS = RBO.getProperty('','CATG_DESC',PRT.DESC) ; * UPDATED CATG.DESC
STATUS = RBO.getProperty('','ORIG_CATG',AT.CATG) ; * ORIGINAL CATG.CODES

STATUS = RBO.getProperty('','SUB_CATG',PRT.SUB) ; * UPDATED SUB.CATG.CODES
STATUS = RBO.getProperty('','SUB_DESC',PRT.S.DESC); * SUB.CATG.DESC

STATUS = RBO.getProperty('','SUB_C',PRT.CURR) ; * SUB.CATG.CURR
STATUS = RBO.getProperty('','SUB_F',PRT.FIX) ; * SUB.CATG.FIX
STATUS = RBO.getProperty('','SUB_P',PRT.PROD) ; * SUB.CATG.PROD
STATUS = RBO.getProperty('','Cono',CONO)

 * CONO = ID[1,3]
*  TYPE.ID = TRIM(ID[1,3])
 *  TYPE.ID = TRIM(ID[4,99])
TYPE.ID=ID
  CMD = 'SSELECT COMPANY'
  PERFORM CMD CAPTURING MSG
  LOOP.DONE = 0  ; ALL.CONO = ''
  LOOP
    	READNEXT ID ELSE LOOP.DONE = 1
  UNTIL LOOP.DONE DO
    	IF LEN(ID) # 3 AND NOT(NUM(ID)) THEN CONTINUE
    	ALL.CONO<-1> = ID
  REPEAT
  IF ALL.CONO = "" THEN ALL.CONO = CONO
  	CCNT = DCOUNT(ALL.CONO, AM)
*--- ADD PROCESS
LINES = DCOUNT(PRT.CATG,VM)

FOR LN = 1 TO LINES
	LOCATE PRT.CATG<1,LN> IN AT.CATG<1>,1 SETTING CFND ELSE
      		AT.CATG<1,CFND> = PRT.CATG<1,LN>
    	END
    	MATREADU AC.REC FROM ACCT.CATG, AT.CATG<1,CFND> ELSE
      		MAT AC.REC = ""
    	END
    	AC.TYPE = TYPE.ID
    	AC.DESC = PRT.DESC<1,LN>
	LINES1 = DCOUNT(PRT.SUB<1,LN>,SVM)
	FOR LN1 = 1 TO LINES1
      		LOCATE PRT.SUB<1,LN,LN1> IN AC.SUB<1>,1 SETTING SFND ELSE
        		AC.SUB<1,SFND> = PRT.SUB<1,LN,LN1>
      		END

      		MATREADU ASC.REC FROM ACCT.SUB.CATG, AC.SUB<1,SFND> ELSE
        		MAT ASC.REC = ""
      		END

		ASC.DESC = PRT.S.DESC<1,LN,LN1> ;* ACCT SUB CATG
      		LOCATE TYPE.ID IN ASC.TYPES<1>,1 SETTING AFND ELSE
        		ASC.TYPES<1,AFND> = TYPE.ID
      		END

		FOR DL = 1 TO CCNT
	       	CAS.ID = ALL.CONO<DL> : AT.CATG<1,CFND> : AC.SUB<1,SFND>
        		MATREADU CAS.REC FROM CATG.AND.SUB, CAS.ID ELSE
          			MAT CAS.REC = ""
        		END
        		BEGIN CASE
				CASE PRT.CURR<1,LN,LN1> # CAS.CURR
            				MODIFIED = 1
            				CAS.CURR = PRT.CURR<1,LN,LN1>
            				CAS.FIX = PRT.FIX<1,LN,LN1>
            				CAS.PROD = PRT.PROD<1,LN,LN1>
          			CASE PRT.FIX<1,LN,LN1> # CAS.FIX
            				MODIFIED = 1
            				CAS.FIX = PRT.FIX<1,LN,LN1>
            				CAS.PROD = PRT.PROD<1,LN,LN1>
          			CASE PRT.PROD<1,LN,LN1> # CAS.PROD
            				MODIFIED = 1
           	 			CAS.PROD = PRT.PROD<1,LN,LN1>
			      	CASE 1
            				MODIFIED = 0
        		END CASE
			IF MODIFIED = 0 THEN GOTO 5080 ;* IF NOT MODIFIED THEN NO NEED OF UPDATE
			ACNT = DCOUNT(CAS.ACCT,VM)
			FOR K = ACNT TO 1 STEP - 1
		       	COA.ID = ALL.CONO<DL> : CAS.ACCT<1,K>
          			MATREADU COA.REC FROM COA, COA.ID ELSE
            				CAS.ACCT = DELETE(CAS.ACCT,1,K,0)
            				RELEASE COA, COA.ID
            				GOTO 5070
          			END
          			COA.TYPE = AC.TYPE
          			COA.CURR = CAS.CURR
          			COA.FIX = CAS.FIX
          			COA.PROD = CAS.PROD
          			MATWRITE COA.REC ON COA, COA.ID
			5070*
        		NEXT K
			5080*
        		MATWRITE CAS.REC ON CATG.AND.SUB, CAS.ID
      		NEXT DL
      		MATWRITE ASC.REC ON ACCT.SUB.CATG, AC.SUB<1,SFND>
    	NEXT LN1
	MATWRITE AC.REC ON ACCT.CATG, AT.CATG<1,CFND>
NEXT LN

*--- DELETE PROCESS
LINES = COUNT(AT.CATG,VM) + (AT.CATG # "")
FOR LN = LINES TO 1 STEP - 1
	LOCATE AT.CATG<1,LN> IN PRT.CATG<1>,1 SETTING CFND ELSE CFND = 0
	MATREADU AC.REC FROM ACCT.CATG, AT.CATG<1,LN> ELSE
		MAT AC.REC = ""
		CFND = 0
	END
	LINES1 = DCOUNT(AC.SUB,VM)
    	FOR LN1 = LINES1 TO 1 STEP - 1
      		IF CFND THEN
       		LOCATE AC.SUB<1,LN1> IN PRT.SUB<1,CFND>,1 SETTING SFND ELSE SFND = 0
      		END ELSE
       		SFND = 0
      		END
      		IF SFND THEN
       		DFND = 1
      		END ELSE
       		DFND = 0
       		CATG.CNT = COUNT(PRT.CATG,VM) + (PRT.CATG # "")
       		FOR DL = 1 TO CATG.CNT UNTIL DFND
       			LOCATE AC.SUB<1,LN1> IN PRT.SUB<1,DL>,1 SETTING DFND ELSE DFND = 0
       		NEXT DL
      		END
		MATREADU ASC.REC FROM ACCT.SUB.CATG, AC.SUB<1,LN1> ELSE
       		MAT ASC.REC = ""
       		SFND = 0
       		DFND = 0
      		END
		FOR DL = 1 TO CCNT WHILE SFND = 0
       		CAS.ID = ALL.CONO<DL> : AT.CATG<1,LN> : AC.SUB<1,LN1>
       		MATREADU CAS.REC FROM CATG.AND.SUB, CAS.ID ELSE
       			MAT CAS.REC = ""
       		END
       		ACNT = COUNT(CAS.ACCT,VM) + (CAS.ACCT # "")
       		FOR K = ACNT TO 1 STEP - 1
          			COA.ID = ALL.CONO<DL> : CAS.ACCT<1,K>
          			KFND = 1
          			MATREADU COA.REC FROM COA, COA.ID ELSE
            				MAT COA.REC = ""
            				KFND = 0
         	 		END
				IF KFND THEN
            				COA.CATG = ""
            				COA.SUB.CATG = ""
            				COA.CURR = ""
            				COA.FIX = ""
            				COA.PROD = ""
            				MATWRITE COA.REC ON COA, COA.ID
          			END ELSE
            				RELEASE COA, COA.ID
          			END
          			CAS.ACCT = DELETE(CAS.ACCT,1,K,0)
        		NEXT K
			DELETE CATG.AND.SUB, CAS.ID
      		NEXT DL
      		IF DFND THEN
        		MATWRITE ASC.REC ON ACCT.SUB.CATG, AC.SUB<1,LN1>
      		END ELSE
        		LOCATE TYPE.ID IN ASC.TYPES<1>,1 SETTING AFND ELSE AFND = 0
        		IF AFND THEN
          			ASC.TYPES = DELETE(ASC.TYPES,1,AFND,0)
        		END
        		IF ASC.TYPES = "" THEN
          			DELETE ACCT.SUB.CATG, AC.SUB<1,LN1>
        		END ELSE
          			MATWRITE ASC.REC ON ACCT.SUB.CATG, AC.SUB<1,LN1>
        		END
      		END
		IF SFND = 0 THEN
        		AC.SUB = DELETE(AC.SUB,1,LN1,0)
      		END
    	NEXT LN1
    	IF CFND THEN
      		MATWRITE AC.REC ON ACCT.CATG, AT.CATG<1,LN>
    	END ELSE
      		DELETE ACCT.CATG, AT.CATG<1,LN>
      		AT.CATG = DELETE(AT.CATG,1,LN,0)
    	END
  NEXT LN

  MATWRITE AT.REC ON ACCT.TYPE, TYPE.ID



* End of method code
99999
IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','ServerStatus',"D")
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)


END
RETURN

