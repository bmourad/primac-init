SUBROUTINE GetDeptCompDeatils
********************************************************************************
*   Program name :- GetDeptCompDeatils
*   Created:- 7/3/2003
*   Created By : B.Krishna
*------------------------------------------------------------------------------*
*
* DESCRIPTION
*
* This program gets the data from the ESTIMATE.DEPT file to load into the
* Estimate Dept/Comp Summary screen.
*  
********************************************************************************
*
*---- FILE COPY STATEMENTS
*
   $INCLUDE WWINSERT RBO.H
   $INCLUDE JES.CPYLIB JES.FILE.VARS
   $INCLUDE CPYLIB FILE.VARS
   $INCLUDE JES.CPYLIB ESTIMATE
   $INCLUDE JES.CPYLIB ESTIMATE.DEPT
   $INCLUDE JES.CPYLIB EST.DEPT.COMP.SUM
   $INCLUDE CPYLIB CHAR

*
*---- OPEN ALL FILES
*
   IN_PARAM = "" ; OUT_PARAM = ""
   ERRMSG = "CALL openEstimateFiles PROBLEM"
   CALL openEstimateFiles(ERRMSG,IN_PARAM,OUT_PARAM,MAT FILE.VARS,MAT JES.FILE.VARS)
   IF ERRMSG # "" THEN GOTO 93000
*
*---- INITIALIZATION
*
 FINALDEPTCOMP ="";DSUM.DEPT="";DSUM.DEPT.DESC="";DSUM.DEPT.LABOR.HRS="";DSUM.DEPT.LABOR.SALE="";OTHER.SALE="";TOTAL.DEPT="";TOTAL.HRS="";TOTAL.LABOR="";TOTAL.OTHER="";TOTAL.AMOUNT="";DEPT.COMP="";DEPT.COMP.DESC="";DEPT.COMP.HRS="";DEPT.COMP.LABOR="";DEPT.COMP.OTHER="";DEPT.COMP.AMOUNT=""

   ERRMSG = RBO.getProperty('','ID',ESTIMATE.ID)
   ERRMSG = RBO.getProperty('','Qty',QSEL) 
*   ERRMSG=RBO.getProperty('','NewQtyIds',DCNQTY)

   CONO = ESTIMATE.ID[1,3]
   EST.ID = TRIM(ESTIMATE.ID[4,99])
   MATREAD EST.REC FROM ESTIMATE, ESTIMATE.ID ELSE
      ERRMSG = 'Estimate not on file. Try again!'
      GOTO 93000
   END
   IF QSEL = "" THEN
      ERRMSG = "No quantity was chosen"
      GOTO 93000
   END

   DSEL = "ALL"
   CSEL = "ALL"
   QSEL = QSEL
   ESTIMATE.ID = CONO:EST.ID
   MAT DSUM.REC2 = ""


*---- PROCESSING
*

*FINALIDS = EST.DEPT.COMP : VM : DCNQTY
*DCNT = DCOUNT(FINALIDS,VM)

   DCNT = DCOUNT(EST.DEPT.COMP,VM)
  FOR DP = 1 TO DCNT
      ESTD.ID = EST.DEPT.COMP<1,DP>
	*ESTD.ID =FINALIDS<1,DP>
      IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
      IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
      IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
      MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.ID:"!":ESTD.ID ELSE GOTO 190
      DEPT = ESTD.DEPT
      COMP = FIELD(ESTD.ID,"!",2)
      LOCATE DEPT IN DSUM.DEPT<1>,1 BY "AL" SETTING P1 ELSE
         IF P1 <= DCNT THEN
            FOR N = 1 TO 40
               DSUM.REC2(N) = INSERT(DSUM.REC2(N),1,P1,0,"")
            NEXT N
         END
         DSUM.DEPT<1,P1> = DEPT
         LOCATE DEPT IN EST.DEPT<1>,1 SETTING PP ELSE NULL
         DSUM.DEPT.DESC<1,P1> = EST.DEPT.DESC<1,PP>
      END
      P2 = COMP
      DSUM.COMP<1,P1,P2> = COMP
      DSUM.COMP.DESC<1,P1,P2> = EST.PROD.DESC<1,COMP>
      TCNT = DCOUNT(ESTD.TYPE,VM)
      FOR TP = 1 TO TCNT
         TYPE = ESTD.TYPE<1,TP>[1,1]
         IF TYPE = "G" THEN GOTO 180
         HRS = ESTD.HRS<1,TP>
         COST = ESTD.COST<1,TP>
         SALE = ESTD.TSALE<1,TP>
         BEGIN CASE
            CASE TYPE = "L"
               DSUM.DEPT.LABOR.HRS<1,P1> += HRS
               DSUM.DEPT.LABOR.COST<1,P1> += COST
               DSUM.DEPT.LABOR.SALE<1,P1> += SALE
               DSUM.COMP.LABOR.HRS<1,P1,P2> += HRS
               DSUM.COMP.LABOR.COST<1,P1,P2> += COST
               DSUM.COMP.LABOR.SALE<1,P1,P2> += SALE
            CASE TYPE = "M" OR TYPE = "I"
               DSUM.DEPT.MATL.COST<1,P1> += COST
               DSUM.DEPT.MATL.SALE<1,P1> += SALE
               DSUM.COMP.MATL.COST<1,P1,P2> += COST
               DSUM.COMP.MATL.SALE<1,P1,P2> += SALE
            CASE TYPE = "P"
               DSUM.DEPT.OSP.COST<1,P1> += COST
               DSUM.DEPT.OSP.SALE<1,P1> += SALE
               DSUM.COMP.OSP.COST<1,P1,P2> += COST
               DSUM.COMP.OSP.SALE<1,P1,P2> += SALE
            CASE TYPE = "S"
               DSUM.DEPT.SHIP.COST<1,P1> += COST
               DSUM.DEPT.SHIP.SALE<1,P1> += SALE
               DSUM.COMP.SHIP.COST<1,P1,P2> += COST
               DSUM.COMP.SHIP.SALE<1,P1,P2> += SALE
            CASE TYPE = "O"
               DSUM.DEPT.MISC.COST<1,P1> += COST
               DSUM.DEPT.MISC.SALE<1,P1> += SALE
               DSUM.COMP.MISC.COST<1,P1,P2> += COST
               DSUM.COMP.MISC.SALE<1,P1,P2> += SALE
         END CASE
180   NEXT TP
190 NEXT DP
   *** COMBINE ALL OF THE NON LABOR INTO ONE FIELD AND TOTAL ALL ***
 * OTHER.SALE = "" ; TOTAL.DEPT = ""
 * TOTAL.HRS = "" ; TOTAL.LABOR = "" ; TOTAL.OTHER = "" TOTAL.AMOUNT = ""
   FOR DP = 1 TO DCNT
      OTHER.SALE<1,DP> += DSUM.DEPT.MATL.SALE<1,DP>
      OTHER.SALE<1,DP> += DSUM.DEPT.OSP.SALE<1,DP>
      OTHER.SALE<1,DP> += DSUM.DEPT.SHIP.SALE<1,DP>
      OTHER.SALE<1,DP> += DSUM.DEPT.MISC.SALE<1,DP>
      TOTAL.DEPT<1,DP> += DSUM.DEPT.LABOR.SALE<1,DP>
      TOTAL.DEPT<1,DP> += DSUM.DEPT.MATL.SALE<1,DP>
      TOTAL.DEPT<1,DP> += DSUM.DEPT.OSP.SALE<1,DP>
      TOTAL.DEPT<1,DP> += DSUM.DEPT.SHIP.SALE<1,DP>
      TOTAL.DEPT<1,DP> += DSUM.DEPT.MISC.SALE<1,DP>
      TOTAL.HRS += DSUM.DEPT.LABOR.HRS<1,DP>
      TOTAL.LABOR += DSUM.DEPT.LABOR.SALE<1,DP>
      TOTAL.OTHER += DSUM.DEPT.MATL.SALE<1,DP>
      TOTAL.OTHER += DSUM.DEPT.OSP.SALE<1,DP>
      TOTAL.OTHER += DSUM.DEPT.SHIP.SALE<1,DP>
      TOTAL.OTHER += DSUM.DEPT.MISC.SALE<1,DP>
   NEXT DP
   TOTAL.AMOUNT = TOTAL.LABOR + TOTAL.OTHER
*
*---- BUILD DETAIL DEPARTMENT DATA FOR ALL COMPONENTS
*
   DEPT.COMP = "" ; DEPT.COMP.DESC = "" ; DEPT.COMP.HRS = ""
   DEPT.COMP.LABOR = "" ; DEPT.COMP.OTHER = "" ; DEPT.COMP.AMOUNT = ""
   DCNT = DCOUNT(DSUM.DEPT<1>,VM)
   FOR DP = 1 TO DCNT
      CCNT = DCOUNT(DSUM.COMP<1,DP>,SM)
      FOR CP = 1 TO CCNT
         DEPT.COMP<1,DP,CP> = CP
         DEPT.COMP.DESC<1,DP,CP> = DSUM.COMP.DESC<1,DP,CP>
         DEPT.COMP.HRS<1,DP,CP> = DSUM.COMP.LABOR.HRS<1,DP,CP>
         DEPT.COMP.LABOR<1,DP,CP> = DSUM.COMP.LABOR.SALE<1,DP,CP>
         DEPT.COMP.OTHER<1,DP,CP> += DSUM.COMP.MATL.SALE<1,DP,CP>
         DEPT.COMP.OTHER<1,DP,CP> += DSUM.COMP.OSP.SALE<1,DP,CP>
         DEPT.COMP.OTHER<1,DP,CP> += DSUM.COMP.SHIP.SALE<1,DP,CP>
         DEPT.COMP.OTHER<1,DP,CP> += DSUM.COMP.MISC.SALE<1,DP,CP>
         DEPT.COMP.AMOUNT<1,DP,CP> = DEPT.COMP.LABOR<1,DP,CP> + DEPT.COMP.OTHER<1,DP,CP>

         DEPT.COMP.HRS<1,DP,CP> = OCONV(DEPT.COMP.HRS<1,DP,CP>,"MD2,")
         DEPT.COMP.LABOR<1,DP,CP> = OCONV(DEPT.COMP.LABOR<1,DP,CP>,"MD2,")
         DEPT.COMP.OTHER<1,DP,CP> = OCONV(DEPT.COMP.OTHER<1,DP,CP>,"MD2,")
         DEPT.COMP.AMOUNT<1,DP,CP> = OCONV(DEPT.COMP.AMOUNT<1,DP,CP>,"MD2,")

      NEXT CP
   NEXT DP
CNT = DCOUNT(DSUM.DEPT.LABOR.HRS,VM)
FOR I=1 TO CNT
    DSUM.DEPT.LABOR.HRS<1,I> = OCONV(DSUM.DEPT.LABOR.HRS<1,I>,"MD2,")
    DSUM.DEPT.LABOR.SALE<1,I> = OCONV(DSUM.DEPT.LABOR.SALE<1,I>,"MD2,")
    OTHER.SALE<1,I> = OCONV(OTHER.SALE<1,I>,"MD2,")
    TOTAL.DEPT<1,I> = OCONV(TOTAL.DEPT<1,I>,"MD2,")
NEXT I
TOTAL.HRS = OCONV(TOTAL.HRS,"MD2,")
TOTAL.LABOR = OCONV(TOTAL.LABOR,"MD2,")
TOTAL.OTHER = OCONV(TOTAL.OTHER,"MD2,")
TOTAL.AMOUNT = OCONV(TOTAL.AMOUNT,"MD2,")


FINALDEPTCOMP =FINALDEPTCOMP :"^":DSUM.DEPT:"^":DSUM.DEPT.DESC:"^":DSUM.DEPT.LABOR.HRS:"^":DSUM.DEPT.LABOR.SALE:"^":OTHER.SALE:"^":TOTAL.DEPT :"^":TOTAL.HRS:"^":TOTAL.LABOR:"^":TOTAL.OTHER:"^":TOTAL.AMOUNT:"^":DEPT.COMP:"^":DEPT.COMP.DESC:"^":DEPT.COMP.HRS:"^":DEPT.COMP.LABOR:"^":DEPT.COMP.OTHER:"^":DEPT.COMP.AMOUNT

*---- BUILD RBO RECORD
*
STATUS = RBO.setProperty('','DeptCOMPSumInfo',FINALDEPTCOMP)
93000 *
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
* End of method code
RETURN

