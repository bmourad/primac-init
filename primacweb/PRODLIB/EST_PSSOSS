SUBROUTINE EST_PSSOSS
********************************************************************************
*   Program name :- EST_PSSOSS
*   Created:- 6/24/2004 RAZI MOHIUDDIN
*------------------------------------------------------------------------------*
* In Properties:
* --------------
* Out Properties:
* ---------------
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE JES.CPYLIB ESTIMATE
$INCLUDE JES.CPYLIB ESTIMATE.MATL
$INCLUDE JES.CPYLIB ESTIMATE.BIND.SPOIL
$INCLUDE JES.CPYLIB ESTIMATE.PRESS.SPOIL
$INCLUDE JES.CPYLIB ESTIMATE.PAPER.GROUP
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE CPYLIB CHAR

OPEN "ESTIMATE" TO ESTIMATE ELSE ERRMSG="CANNOT OPEN ESTIMATE FILE";GOTO 90000
OPEN "ESTIMATE.MATL" TO ESTIMATE.MATL ELSE ERRMSG="CANNOT OPEN ESTIMATE.MATL FILE";GOTO 90000
OPEN "ESTIMATE.BIND.SPOIL" TO ESTIMATE.BIND.SPOIL ELSE ERRMSG="CANNOT OPEN ESTIMATE.BIND.SPOIL FILE";GOTO 90000
OPEN "ESTIMATE.PRESS.SPOIL" TO ESTIMATE.PRESS.SPOIL ELSE ERRMSG="CANNOT OPEN ESTIMATE.PRESS.SPOIL FILE";GOTO 90000
OPEN "ESTIMATE.PAPER.GROUP" TO ESTIMATE.PAPER.GROUP ELSE ERRMSG="CANNOT OPEN ESTIMATE.PAPER.GROUP FILE";GOTO 90000

STATUS = RBO.getProperty('', 'ID', ID)
STATUS = RBO.getProperty('', 'EST_OS_WIDTH', EST_OS_WIDTH)
STATUS = RBO.getProperty('', 'EST_OS_LENGTH',EST_OS_LENGTH)
STATUS = RBO.getProperty('', 'EST_OS_GRAIN', EST_OS_GRAIN)
STATUS = RBO.getProperty('', 'EST_PS_WIDTH', EST_PS_WIDTH)
STATUS = RBO.getProperty('', 'EST_PS_LENGTH',EST_PS_LENGTH)
STATUS = RBO.getProperty('', 'EST_PS_GRAIN', EST_PS_GRAIN)

STATUS = RBO.getProperty('', 'EST_PAPERGROUP',EST_PAPERGROUP)
STATUS = RBO.getProperty('', 'EST_BASIS_WT',EST_BASIS_WT)

COMP=1
MPTR=1 
CONO=ID[1,3]

EST.PROD.OS.WIDTH =EST_OS_WIDTH
EST.PROD.OS.LENGTH =EST_OS_LENGTH
EST.PROD.OS.GRAIN =EST_OS_GRAIN
EST.PROD.PS.WIDTH = EST_PS_WIDTH
EST.PROD.PS.LENGTH=EST_PS_LENGTH
EST.PROD.PS.GRAIN=EST_PS_GRAIN
EST.PROD.BASIS.WT=EST_BASIS_WT

MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP, CONO:EST_PAPERGROUP ELSE
    ERRMSG = "Invalid paper group"
    GOTO 90000
END
   
  OUT1 = 0
  OUT2 = 0
  OSW = EST.PROD.OS.WIDTH<1,COMP,MPTR>
  OSL = EST.PROD.OS.LENGTH<1,COMP,MPTR>
  OSG = EST.PROD.OS.GRAIN<1,COMP,MPTR>
  PSW = EST.PROD.PS.WIDTH<1,COMP,MPTR>
  PSL = EST.PROD.PS.LENGTH<1,COMP,MPTR>
  PSG = EST.PROD.PS.GRAIN<1,COMP,MPTR>
  
  IF OSG # PSG OR OSG = "" OR PSG = "" THEN
    CUT.W = INT(OSW/PSL-1+0.9999)
    OUT.W = INT(OSW/PSL)
    CUT.L = INT(OSL/PSW-1+0.9999)
    OUT.L = INT(OSL/PSW)
    CUT1 = CUT.W+CUT.L
    OUT1 = OUT.W*OUT.L
    REM.W = OSW - (PSL * OUT.W)
    IF REM.W >= PSW AND (OSG = "" OR PSG = "") THEN
      CUT.W = INT(REM.W/PSW-1+0.9999)
      OUT.W = INT(REM.W/PSW)
      CUT.L = INT(OSL/PSL-1+0.9999)
      OUT.L = INT(OSL/PSL)
      CUT1 = CUT1 + CUT.W + CUT.L
      OUT1 = OUT1 + (OUT.W * OUT.L)
    END
  END
  IF OSG = PSG OR OSG = "" OR PSG = "" THEN
    CUT.W = INT(OSW/PSW-1+0.9999)
    OUT.W = INT(OSW/PSW)
    CUT.L = INT(OSL/PSL-1+0.999)
    OUT.L = INT(OSL/PSL)
    CUT2 = CUT.W+CUT.L
    OUT2 = OUT.W*OUT.L
    REM.L = OSL - (PSL * OUT.L)
    IF REM.L >= PSW AND (OSG = "" OR PSG = "") THEN
      CUT.W = INT(OSW/PSL-1+0.9999)
      OUT.W = INT(OSW/PSL)
      CUT.L = INT(REM.L/PSW-1+0.9999)
      OUT.L = INT(REM.L/PSW)
      CUT2 = CUT2 + CUT.W + CUT.L
      OUT2 = OUT2 + (OUT.W * OUT.L)
    END
  END
  BEGIN CASE
    CASE OSG = "" OR PSG = ""
      IF OUT1 > OUT2 THEN
*           QTY = CUT1
        NUMBER.OUT = OUT1
      END ELSE
*           QTY = CUT2
        NUMBER.OUT = OUT2
      END
    CASE OSG # PSG
*        QTY = CUT1
      NUMBER.OUT = OUT1
    CASE OSG = PSG
*        QTY = CUT2
      NUMBER.OUT = OUT2
  END CASE

  BW = EST.PROD.BASIS.WT<1,COMP,MPTR>
  BSW = ESTPG.WIDTH/10000
  BSL = ESTPG.LENGTH/10000
  OSW = EST.PROD.OS.WIDTH<1,COMP,MPTR>/10000
  OSL = EST.PROD.OS.LENGTH<1,COMP,MPTR>/10000
  M.WEIGHT = 2 * BW * (OSW * OSL) / (BSW * BSL)
  M.WEIGHT = INT(M.WEIGHT * 100 + 0.5) / 100
  EST.PROD.M.WT<1,COMP,MPTR> = M.WEIGHT * 100
  EST.PROD.PSS.OSS<1,COMP,MPTR> = NUMBER.OUT
  EST_PROD_PSS_OSS<1,COMP,MPTR> = NUMBER.OUT
  
STATUS = RBO.setProperty('','EST_PROD_PSSOSS',EST.PROD.PSS.OSS<1,COMP,MPTR>)
STATUS = RBO.setProperty('','MTEST',CONO)

90000*
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
RETURN
