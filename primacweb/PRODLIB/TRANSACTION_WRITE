SUBROUTINE TRANSACTION_WRITE (ACTION,CONO,TDIV,FNAME,FID,FREC,FVAR,PSS.JOURNAL,ERRMSG)
********************************************************************************
*   Program name :- TRANSACTION_WRITE
*   Created:- 8/4/2003
*   AUTHOR :- JAWEED
* DESCRIPTION
*
* This program is used to write the specified record to the database while
* providing recovery capability through the use of a Journal file.
*
*    PARAM        DESCRIPTION
*    -----------  --------------------------------------------------------
*    ACTION       "WRITE" OR "WRITEU"
*    CONO         Company ID
*    TDIV         Division ID
*    FNAME        Name of file being updated
*    FID          Key of item to be written (includes CONO if present)
*    FREC         Dynamic array to be written
*    FVAR         File variable of file to be updated
*    PSS.JOURNAL  File variable of Journal file
*    ERRMSG       Status
*
*T22102 renee 07/25/1997 * TESTING WHAT IT'S WRITING
**************************************************************************
*
*
*---- COPY STATEMENTS
*
$INCLUDE PSS.CPYLIB PSS.JOURNAL
$INCLUDE WWINSERT RBO.H

*
*---- INITIALIZATION
*
      ERRMSG = ""
      FREC = TRIM(FREC,CHAR(254),"T")
      CDATE = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN CDATE = DATE()
*
*---- MAIN PROCESSING
*
      IF TDIV = "" THEN
         JFOUND = 0
      END ELSE
         JFOUND = 1
         MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!":TDIV ELSE
            RELEASE PSS.JOURNAL, CONO:"CONTROL!":TDIV
            JFOUND = 0
         END
      END
      IF NOT(JFOUND) THEN
         IF ACTION = "WRITEU" THEN
            WRITEU FREC ON FVAR,FID
         END ELSE
            WRITE FREC ON FVAR,FID
         END
*        ERRMSG = "NO TRANSACTIONS IN PROGRESS"
         GOTO 99999
      END
      IF JRNLC.TRAN = "" THEN
         JRNLC.TRAN = "1"
         JRNLC.DATE = CDATE
         JRNLC.TIME = CTIME
         JRNLC.DESC = "??????????"
         JRNLC.TPTR = 1
      END
      TCNT = DCOUNT(JRNLC.TRAN,@VM)
      CTRAN = JRNLC.TRAN<1,TCNT>
      JPTR = JRNLC.JPTR + JRNLC.JCNT
      JRNLC.JCNT += 1
      JRNLC.TCNT<1,TCNT> += 1
      MAT JRNLI.REC = ""
      JRNLI.FNAME = FNAME
      JRNLI.INAME = FID
      JRNLI.TRAN = CTRAN
      JRNLI.DATE = CDATE
      JRNLI.TIME = CTIME
      READU BEFORE.IMAGE FROM FVAR, FID THEN
         WRITE BEFORE.IMAGE ON PSS.JOURNAL, CONO:"XDATA!":TDIV:"!":JPTR
      END ELSE
         JRNLI.STATUS = "NEW"
      END
      IF ACTION = "WRITEU" THEN
         WRITEU FREC ON FVAR,FID
      END ELSE
         WRITE FREC ON FVAR,FID
      END
      MATWRITE JRNLI.REC ON PSS.JOURNAL, CONO:"INDEX!":TDIV:"!":JPTR
      MATWRITE JRNLC.REC ON PSS.JOURNAL, CONO:"CONTROL!":TDIV
*
*---- END OF PROGRAM
*
99999 *
      RETURN
93000
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage', ERRMSG)
END
