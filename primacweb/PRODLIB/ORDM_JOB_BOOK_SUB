SUBROUTINE ORDM_JOB_BOOK_SUB(CONO,ACTION,JOB.NO,OLD.EST,NEW.EST,MAT EST.PAR,ESTAT,MAT EST.REC,SAVE.INV.JS.REC,EST.MATL,PROMPTS,EST_INDX)
********************************************************************************
*   Program name :- ORDM_JOB_BOOK_SUB
*   Created:- 3/17/2006
*   Programmer :- Suhail Hussain S
********************************************************************************

$INCLUDE WWINSERT RBO.H
$INCLUDE CPYLIB COMMON1
$INCLUDE JCS.CPYLIB COM.JCS.LINK 
$INCLUDE ICS.CPYLIB COM.INV.MAIN  
$INCLUDE ICS.CPYLIB COM.INV.SERIAL
$INCLUDE JCS.CPYLIB COM.INV.STATS 
$INCLUDE PMC.CPYLIB COM.CUST
$INCLUDE OPS.CPYLIB COM.ORDER
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - JOB.BOOK.SUB
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 04/01/87
*
* DESCRIPTION
*
* This subroutine is used to retrieve estimate data and update job.
* If any errors are detected, the appropriate message is displayed
* and the ESTAT error flag is set to 1.
* The ESTIMATE.JOB item is constructed and is written when called
* with the appropriate action code.
*
* ACTION 1 = Pre-load master job with estimate data
* 2 = Pre-load sub-job with estimate data
*********************************************************************

$DEFINE INVENTORY
$DEFINE ORDER
$DEFINE ORDERDETAILINQ
$DEFINE COMPOPS
$DEFINE CUSTOMER
$DEFINE CATEGORY
$DEFINE INVWHSE
$DEFINE FNGDSTATS
$DEFINE FNGDORDERSTATS
$DEFINE JOB
$DEFINE FILEVARS
$DEFINE INVJOBSTATS
*
*---- COPY STATEMENTS
*
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JES.CPYLIB ESTIMATE
$INCLUDE JES.CPYLIB ESTIMATE.RES
$INCLUDE JES.CPYLIB ESTIMATE.DEPT
$INCLUDE JES.CPYLIB ESTIMATE.MATL
$INCLUDE JES.CPYLIB ESTIMATE.JOB
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB WAREHOUSE
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE ICS.CPYLIB INV.JOB.STATS
$INCLUDE ICS.CPYLIB CATEGORY
$INCLUDE PSS.CPYLIB JOB.SCHED
$INCLUDE CPYLIB FILE.VARS
$INCLUDE CPYLIB CHAR
$INCLUDE CPYLIB XREF.DATA             ; * T25916
$INCLUDE CPYLIB GEN.XREF.SUB          ; * T25916
*
*---- DIMENSIONED VARIABLES
*
   DIM EST.PAR(10)
   EQU EST.PAR.QTY    TO EST.PAR(1)
   EQU EST.PAR.COMP   TO EST.PAR(2)
   EQU EST.PAR.DEPT   TO EST.PAR(3)
   EQU EST.PAR.UPDM   TO EST.PAR(4)
   EQU EST.PAR.MATL   TO EST.PAR(5)
   DIM OLD.EST.PAR(10)
   MAT OLD.EST.PAR = ''
   DIM NEW.EST.PAR(10)
   MAT NEW.EST.PAR = ''
*
*---- PRE-INITIALIZATION
*
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = 'Cannot read company ':CONO:' record.'
      GOTO 99001
   END
   TODAY = DATE()
   IF ESTAT THEN GOTO 99999;*T22154 <
   ESTAT = 0
   MAT GEN.XREF.REC = "" ; * T25916
   GXR.CO = CONO ; * T25916
* T22896 v
   IF CO.PSS = "Y" THEN
      OPEN "","JOB.SCHED" TO JOB.SCHED ELSE
         ERRMSG = "CANNOT OPEN JOB.SCHED FILE"
         GOTO 99001
      END
   END
   JOB.FLAG = ""
   IF ACTION = "3" AND EST.PAR.QTY = "" AND OLD.EST = NEW.EST THEN
      IF CO.PSS = "Y" THEN
         IF NEW.EST # "" THEN
            MATREAD JBS.REC FROM JOB.SCHED, CONO:JOB.NO ELSE
               EST.NO = NEW.EST
               JOB.FLAG = "P"
               GOTO 3500;*T20925
            END
         END
         RETURN
      END
      RETURN ; * T27914
   END
*
*T22896
*---- OPEN ALL FILES
*
   OPEN "","ESTIMATE.JOB" TO ESTIMATE.JOB ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.JOB FILE"
      GOTO 99001
   END
   OPEN "","ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.DEPT FILE"
      GOTO 99001
   END
   OPEN "","ESTIMATE.MATL" TO ESTIMATE.MATL ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.MATL FILE"
      GOTO 99001
   END
   OPEN "","CATEGORY" TO CATEGORY ELSE
      ERRMSG = "CANNOT OPEN CATEGORY FILE"
      GOTO 99001
   END
   OPEN "","ESTIMATE.RES" TO ESTIMATE.RES ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.RES FILE"
      GOTO 99001
   END
   EST.ACT.FLG = 0
   OPEN "","EST.JOB.ACT" TO EST.JOB.ACT THEN EST.ACT.FLG = 1
   OPEN "","WAREHOUSE" TO WAREHOUSE ELSE
      ERRMSG = "CANNOT OPEN WAREHOUSE FILE"
      GOTO 99001
   END
*T25916 v
   OPEN "","XREF.DATA" TO XREF.DATA ELSE
      ERRMSG = "CANNOT OPEN XREF.DATA FILE"
      GOTO 99001
   END
*T25916 ^
***
*
*---- INITIALIZATION
*
   EST.NO = NEW.EST
*
*---- MAIN PROCESSING
*
FND_PMPT = ""
100 *
********************
   BEGIN CASE
      CASE ACTION = "1";* NEW JOB NEW ESTIMATE
         MAT EST.PAR = ""
         IF EST.NO = "" THEN
            GOTO 200
110*
            IF VALUE = "END" THEN GOTO 99002
            IF VALUE = "" THEN GOTO 99999
         END
         EST.PAR.QTY = ""
         EST.PAR.COMP = ""
         ECNT = COUNT(EST.QTY,VM) + (EST.QTY # "")
         DEPT = ""
         DPTR = 0
         GOSUB 1000
      CASE ACTION = "2";* NEW OR EXISTING JOB
         VALUE = ''
         GOTO 210
120*
         IF ECNT = 0 THEN GOTO 99001
         IF VALUE = "END" THEN GOTO 99002
         VALUE = ''
         GOTO 220
125*
         IF VALUE = "END" THEN GOTO 99002
         DEPT = ""
         DPTR = 0
         GOTO 2000
      CASE ACTION = "3";* FILE JOB
         IF OLD.EST # "" AND NEW.EST = "" THEN
            GOSUB 3000
         END ELSE
            ECNT = COUNT(EST.QTY,VM) + (EST.QTY # "")
            DEPT = ""
            DPTR = 0
            GOSUB 3000
         END
         IF EST.ACT.FLG THEN CALL EST.ACT.SUB(CONO,JOB.NO);* T21710
         IF CO.PSS = 'Y' THEN
            BEGIN CASE
               CASE OLD.EST = '' AND NEW.EST # ''
                  JOB.FLAG = 'P'
                  GOTO 3500
               CASE OLD.EST # '' AND NEW.EST = ''
                  JOB.FLAG = 'U'
                  GOTO 3500
               CASE OLD.EST # NEW.EST
                  JOB.FLAG = 'R'
                  GOTO 3500
            END CASE
         END
   END CASE
   GOTO 99999
*
*---- GET ESTIMATE
*
200 *
   PMSG="Estimate ID - "
   PMPT_NUM = "EN-" : EST_INDX;P.RQD = "Y";P.TYPE = "I";P.MSG = PMSG
   P.DEF = "";P.VALID = "";P.MODE = 1;VALUE = ""
   CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","",VALUE)
   GOSUB CHECK.FOR.INPUT
   IF IP.REQ = 1 THEN GOTO 99999
   IF VALUE = "END" THEN GOTO 99002
   *IF VALUE = "" THEN RETURN
   IF VALUE = "" THEN GOTO 110
   MATREAD EST.REC FROM ESTIMATE, CONO : VALUE ELSE
      ERRMSG = "Invalid Estimate ID. Try again! "
      PMPT_NUM = "ENX1-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
      CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
      PMPT_NUM = "EN-":EST_INDX;P.MODE = 2
      CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
      GOTO 200
   END
   IF EST.JOB # "" THEN
      IF JOB.MASTER # "" AND EST.JOB # JOB.MASTER AND EST.JOB # JOB.NO THEN
         ERRMSG = "Estimate belongs to Master Job# ":EST.JOB:". Try again! "
         PMPT_NUM = "ENX2-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
         CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
         PMPT_NUM = "EN-":EST_INDX;P.MODE = 2
         CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
         GOTO 200
      END
   END
   IF EST.STATUS<1,1> = "LOST" THEN
      ERRMSG = "Estimate was defined as lost on ":OCONV(EST.STAT.DATE<1,1>,"D2/"):". Try again! "
      PMPT_NUM = "ENX3-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
      CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
      PMPT_NUM = "EN-":EST_INDX;P.MODE = 2
      CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
      GOTO 200
   END
   EST.NO = VALUE
   GOTO 110
RETURN
*
*---- GET ESTIMATE QUANTITY
*
210 *
   ECNT = COUNT(EST.QTY,VM) + (EST.QTY # "")
   BEGIN CASE
      CASE ECNT = 0
         ERRMSG = "No quantities present for specified estimate."
         PMPT_NUM = "EQX1-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
         CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
         GOTO 120
      CASE ECNT = 1
         EST.PAR.QTY = EST.QTY
         QP = 1
      CASE 1
         PMSG="Estimate Quantity - ";DEFAULT=""
         IF EST.BOOK.QTY+0 > 0 THEN
            DEFAULT = EST.BOOK.QTY
            PMSG="Estimate Quantity (Default=":DEFAULT:") - "
         END
         PMPT_NUM = "EQ-" : EST_INDX;P.RQD = "Y";P.TYPE = "I";P.MSG = PMSG
         P.DEF = DEFAULT;VALUE = "";P.MODE = 1
         CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,P.DEF,"",VALUE)
         GOSUB CHECK.FOR.INPUT
         IF IP.REQ = 1 THEN GOTO 99999

         IF VALUE = "END" THEN GOTO 120
         LOCATE VALUE IN EST.QTY<1>,1 SETTING QP ELSE
            ERRMSG = "Invalid quantity. Try again! "
            PMPT_NUM = "EQX2-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
            CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")            
            PMPT_NUM = "EQ-":EST_INDX;P.MODE = 2
            CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
            GOTO 210
         END
         IF VALUE # EST.BOOK.QTY THEN
            JCNT = DCOUNT(EST.BOOK.JOB,VM)
            FOR JP = 1 TO JCNT
               IF EST.BOOK.JOB<1,JP> # JOB.NO THEN
                  ERRMSG="Estimate has been booked to Job# ":EST.BOOK.JOB<1,JP>:" for Qty ":EST.BOOK.QTY
                  PMPT_NUM = "EQX3-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
                  CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
                  PMPT_NUM = "EQ-":EST_INDX;P.MODE = 2
                  CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
                  GOTO 210
               END
            NEXT JP
         END
         EST.PAR.QTY = VALUE
   END CASE
   GOTO 120
RETURN
*
*---- GET COMPONENT NUMBER
*
220 *
   BEGIN CASE
      CASE EST.COMPONENT.CNT+0 = 0
         EST.PAR.COMP = ""
         SAVE.COMP.CNT = 0
      CASE ACTION = ''
      CASE 1
         TYP=1;X=0;Y=21;MAXL=20
         PMSG="Components - "
         CDEFAULT = ""
         IF EST.PAR.COMP # "" THEN
            CDEFAULT = EST.PAR.COMP
         END ELSE
            LOCATE JOB.NO IN EST.BOOK.JOB<1>,1 SETTING JP ELSE JP = 0
            IF JP > 0 THEN
               CDEFAULT = EST.BOOK.COMP<1,JP>
            END
         END
         IF CDEFAULT = "" THEN
            O.R = "O"
            DEFAULT = "ALL"
         END ELSE
            O.R = "O"
            DEFAULT = CDEFAULT<1,1,1>
            CC = COUNT(CDEFAULT,SM)+1
            FOR CP = 2 TO CC
               DEFAULT = DEFAULT:",":CDEFAULT<1,1,CP>
            NEXT CP
         END
         IF EST.PDEF.TYPE # "RL" THEN
            PMSG="Components (Default=":DEFAULT:") - "
            PMPT_NUM = "EC-" : EST_INDX;P.RQD = "Y";P.TYPE = "I";P.MSG = PMSG
            P.DEF = DEFAULT;P.VALID = "";VALUE = "";P.MODE = 1
            CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,P.DEF,"",VALUE)
            GOSUB CHECK.FOR.INPUT
            IF IP.REQ = 1 THEN GOTO 99999
         END ELSE
            VALUE = "ALL"
         END
         IF VALUE = "END" THEN ESTAT = 2;GOTO 125
         NUM.JOBS=DCOUNT(EST.BOOK.JOB,VM)
         IF VALUE = "ALL" THEN
            JFND=0
            FOR NJ = 1 TO NUM.JOBS WHILE JFND = 0
               IF EST.BOOK.JOB<1,NJ> # JOB.NO THEN JFND = NJ
            NEXT NJ
            IF JFND > 0 THEN
               ERRMSG="One or more components have been booked to Job# ":EST.BOOK.JOB<1,JFND>
               PMPT_NUM = "ECX1-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
               CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
               IF EST.PDEF.TYPE = "RL" THEN
                  VALUE = "END"
                  ESTAT = 2;GOTO 125
               END
               PMPT_NUM = "EC-":EST_INDX;P.MODE = 2
               CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
               GOTO 220
            END
            IF EST.COMPONENT.CNT = 1 THEN
               EST.PAR.COMP = 1
               SAVE.COMP.CNT = 1
            END ELSE
               EST.PAR.COMP = "ALL"
            END
         END ELSE
            COMPS = ""
            C2 = COUNT(VALUE,",") + 1
            FOR C = 1 TO C2
               CX = FIELD(VALUE,",",C)
               IF NOT(NUM(CX)) OR CX < "1" OR CX > EST.COMPONENT.CNT THEN
                  ERRMSG = "Invalid component - ":CX
                  PMPT_NUM = "ECX2-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
                  CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
                  PMPT_NUM = "EC-":EST_INDX;P.MODE = 2
                  CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
                  GOTO 220
               END
               JFND=0
               FOR NJ = 1 TO NUM.JOBS WHILE JFND = 0
                  IF EST.BOOK.JOB<1,NJ> # JOB.NO THEN
                     LOCATE CX IN EST.BOOK.COMP<1,NJ>,1 SETTING JFND ELSE JFND = 0
                  END
               NEXT NJ
               IF JFND THEN
                  ERRMSG="Component ":CX:" has been booked to Job# ":EST.BOOK.JOB<1,JFND>                  
                  PMPT_NUM = "ECX3-" : EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
                  CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
                  PMPT_NUM = "EC-":EST_INDX;P.MODE = 2
                  CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,"","","","","","")
                  GOTO 220
               END
               COMPS<1,1,C> = CX
            NEXT C
            IF EST.COMPONENT.CNT = 1 THEN
               EST.PAR.COMP = 1
               SAVE.COMP.CNT = 1
            END ELSE
               EST.PAR.COMP = COMPS
               SAVE.COMP.CNT = C2
            END
         END
   END CASE
   GOTO 125
RETURN

*
*---- GET JOB INFO FOR NEW JOB NEW EST
*
1000 *
   IF JOB.CUST = "" AND EST.CUST # "P" THEN JOB.CUST = EST.CUST
   IF JOB.SLSMN = "" THEN JOB.SLSMN = EST.SALESMAN
   IF JOB.DIV = "" THEN JOB.DIV = FIELD(EST.DIV,"-",1)
   IF JOB.MARKUP = "" THEN JOB.MARKUP = EST.MARKUP<1,1>
   IF JOB.TRACK.DATE<1,1> = "" THEN JOB.TRACK.DATE<1,1> = EST.DATE.ENTERED
   IF JOB.TRACK.DATE<1,2> = "" THEN JOB.TRACK.DATE<1,2> = DATE()
   IF JOB.TRACK.DATE<1,4> = "" THEN JOB.TRACK.DATE<1,4> = EST.DATE.REQUIRED
   IF JOB.TYPE = "" THEN JOB.TYPE = EST.TYPE
   JOB.EST = EST.NO
   JOB.EST.TYPE = EST.TYPE
   IF JOB.CATG = "" THEN JOB.CATG = EST.CATG
   IF JOB.SALES.CODE = "" THEN JOB.SALES.CODE = EST.SALES.CODE
   IF JOB.CUST.PO = "" THEN JOB.CUST.PO = EST.CUST.PO
   LOCATE EST.PAR.QTY IN EST.QTY<1>,1 SETTING QP ELSE QP = 0
   IF QP > 0 THEN
      JOB.PRICE.PER.THOU = EST.PRICE.THOU<1,QP>
   END
   CPT = 0
   DPT = 0
   CCNT1 = COUNT(EST.COMMENT.TYPE,VM) + 1
   CCNT2 = COUNT(EST.COMMENTS,VM) + 1
   IF CCNT2 > CCNT1 THEN CCNT = CCNT2 ELSE CCNT = CCNT1
   FOR CP = 1 TO CCNT
      IF EST.COMMENT.TYPE<1,CP> = "I" THEN
         CPT = CPT + 1
         JOB.COMMENTS<1,CPT> = EST.COMMENTS<1,CP>
      END
   NEXT CP
RETURN
*
*---- GET JOB INFO
*
2000 *
   JOB.TRACK.DATE<1,1> = EST.DATE.ENTERED
   JOB.EST = EST.NO
   JOB.EST.TYPE = EST.TYPE
   JOB.QTY<1,1> = EST.PAR.QTY
   IF JOB.MASTER = "" THEN JOB.MASTER = EST.JOB
   LOCATE EST.PAR.QTY IN EST.QTY<1>,1 SETTING QP ELSE QP = 0
   IF QP > 0 THEN
      JOB.PRICE.PER.THOU = EST.PRICE.THOU<1,QP>
   END
   JOB.EST.COST = ""
   JOB.EST.SALE = ""
   DC.CNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
   TOTAL.VSALE=0;*T21961
   FOR LN = 1 TO DC.CNT
      COMP = FIELD(EST.DEPT.COMP<1,LN>,"!",2)
      LOCATE COMP IN EST.PAR.COMP<1,1>,1 SETTING CMATCH ELSE CMATCH = 0
      IF EST.PAR.COMP = "ALL" OR CMATCH > 0 THEN
         IF EST.PAR.QTY = FIELD(EST.DEPT.COMP<1,LN>,"!",3) THEN
            JOB.EST.COST = JOB.EST.COST + EST.DEPT.COMP.COST<1,LN>
            JOB.EST.SALE = JOB.EST.SALE + EST.DEPT.COMP.TSALE<1,LN>
            TOTAL.VSALE=TOTAL.VSALE+EST.DEPT.COMP.VSALE<1,LN>;*T21961
         END
      END
   NEXT LN
   JOB.EST.SALE = JOB.EST.SALE + (INT(OCONV(JOB.EST.SALE,'MD2') * OCONV(EST.OM.PCT<1,QP>,'MD4') + .005 / 100))
   CPT = 0
   TOTAL.VSALE = TOTAL.VSALE + (INT(OCONV(TOTAL.VSALE,'MD2') * OCONV(EST.OM.PCT<1,QP>,'MD4') + .005 / 100))
   IF EST.PAR.COMP # 'ALL' THEN
      JOB.PRICE.PER.THOU = INT(TOTAL.VSALE / (EST.PAR.QTY/1000) + .5)
   END
   DPT = 0
   CPT = 0
   CCNT1 = COUNT(EST.COMMENT.TYPE,VM) + 1
   CCNT2 = COUNT(EST.COMMENTS,VM) + 1
   IF CCNT2 > CCNT1 THEN CCNT = CCNT2 ELSE CCNT = CCNT1
   FOR CP = 1 TO CCNT
      IF EST.COMMENT.TYPE<1,CP> = "I" THEN
         CPT = CPT + 1
         IF JOB.COMMENTS<1,CPT> = '' THEN
            JOB.COMMENTS<1,CPT> = EST.COMMENTS<1,CP>
         END
      END
   NEXT CP
   FOR CP = 1 TO CCNT
      IF EST.COMMENT.TYPE<1,CP> # "I" THEN
         IF EST.COMMENT.TYPE<1,CP> = "" OR EST.PAR.COMP = "ALL" THEN
            DPT = DPT + 1
            JOB.DESC<1,DPT> = EST.COMMENTS<1,CP>
         END ELSE
            LOCATE EST.COMMENT.TYPE<1,CP> IN EST.PAR.COMP<1,1>,1 SETTING FND2 ELSE FND2 = 0
            IF FND2 THEN
               DPT = DPT + 1
               JOB.DESC<1,DPT> = EST.COMMENTS<1,CP>
            END
         END
      END
   NEXT CP
   JOB.CONF.AMT = JOB.EST.SALE
*
*---- GET RESERVE PRODUCTS FROM ESTIMATE
*
   IF EST.MATL THEN
      MATREAD EST.RL.REC FROM ESTIMATE.RES, CONO:EST.NO THEN
         ROLL.LABEL = 'Y'
      END ELSE
         ROLL.LABEL = ''
      END
      JRM.CNT = COUNT(JOB.RESV.MATL,VM) + (JOB.RESV.MATL # "")
      FOR JM = 1 TO JRM.CNT
         IF SAVE.INV.JS.REC<JM> = '' THEN
            MATREAD WHSE.REC FROM WAREHOUSE,CONO:JOB.RESV.WHSE<1,JM> ELSE MAT WHSE.REC = ''
            IF WHS.DIV = JOB.DIV OR WHS.DIV = "00" THEN
               JSTAT.ID = JOB.RESV.MATL<1,JM>:"!":JOB.RESV.WHSE<1,JM>:"!":JOB.NO
               MATREAD INV.JS.REC FROM INV.JOB.STATS,CONO:JSTAT.ID ELSE
                  MAT INV.JS.REC = ''
               END
               SAVE.INV.JS.REC<JM,1> = IJS.REQ.DATE
               SAVE.INV.JS.REC<JM,2> = IJS.REQ.AMT
               SAVE.INV.JS.REC<JM,3> = IJS.REQ.QTY
            END
         END
      NEXT JM
      EST.PRODS = ""
      EST.QTYS = ""
      IF EST.PAR.COMP = "ALL" THEN
         COMP.CNT =EST.COMPONENT.CNT
      END ELSE
         COMP.CNT = SAVE.COMP.CNT
      END
      FOR CP = 1 TO COMP.CNT
         IF EST.PAR.COMP = "ALL" THEN COMP = CP ELSE COMP = EST.PAR.COMP<1,1,CP>
         WEB.CNT = COUNT(EST.PROD.OS.PROD<1,COMP>,SM) + 1
         FOR WB = 1 TO WEB.CNT
            IF EST.PROD.INV.ID<1,COMP,WB> # "" THEN
               PROD = EST.PROD.INV.ID<1,COMP,WB>
               IF ROLL.LABEL THEN
                  QTY = FIELD(EST.RL.PAP.MSI<1,COMP,WB>,"!",QP)
                  WIDTH = EST.PROD.OS.WIDTH<1,COMP,WB>
                  GOSUB 70000
               END ELSE
                  QTY = FIELD(EST.PROD.PQTY<1,COMP,WB>,"!",QP)
               END
               LOCATE PROD IN EST.PRODS<1>,1 SETTING X ELSE NULL
               EST.PRODS<1,X> = PROD
               EST.QTYS<1,X> = EST.QTYS<1,X> + QTY
            END
         NEXT WB
      NEXT CP
      IF ROLL.LABEL THEN
      END
      PROD.CNT = COUNT(EST.PRODS,VM) + (EST.PRODS # "")
      FOR I = 1 TO PROD.CNT
         MATREAD INV.REC FROM INVENTORY,CONO:EST.PRODS<1,I> ELSE MAT INV.REC = ''
         GOSUB 80000
         IF CASE1.FLAG THEN EST.QTYS<1,I> = EST.QTYS<1,I> * 100
         MATREAD CATG.REC FROM CATEGORY,CONO:INV.LINE ELSE MAT CATG.REC = ''
         WCNT = DCOUNT(INV.WHSE.CODE,VM)
         DVCNT=0;WPTR=0
         GNCNT=0;GPTR=0
         HMSG='Warehouse - '
         GMSG=''
         FOR W = 1 TO WCNT
            MATREAD WHSE.REC FROM WAREHOUSE,CONO:INV.WHSE.CODE<1,W> THEN
               IF WHS.DIV = JOB.DIV THEN
                  DVCNT += 1
                  IF WPTR = 0 THEN WPTR = W
                  HMSG=HMSG:INV.WHSE.CODE<1,W>:', '
                  VALDAT=VALDAT:INV.WHSE.CODE<1,W>:VM
               END
               IF WHS.DIV = '00' THEN
                  GMSG=GMSG:INV.WHSE.CODE<1,W>:', '
                  VALDAT=VALDAT:INV.WHSE.CODE<1,W>:VM
                  GNCNT += 1
                  IF GPTR = 0 THEN GPTR = W
               END
            END
         NEXT W
         IF GMSG # '' THEN HMSG=HMSG:GMSG
         IF HMSG[LEN(HMSG)-1,2]=', ' THEN HMSG=HMSG[1,LEN(HMSG)-2]
         IF VALDAT[LEN(VALDAT),1]=VM THEN VALDAT=VALDAT[1,LEN(VALDAT)-1]
         BEGIN CASE
            CASE DVCNT = 1
               WHSE = INV.WHSE.CODE<1,WPTR>
            CASE GNCNT = 1 AND DVCNT = 0
               WHSE = INV.WHSE.CODE<1,GPTR>
            CASE 1
               DISP.PROD = EST.PRODS<1,I>
               GOTO 85000
2100*
               IF ESTAT THEN GOTO 99001
         END CASE
2105*
         IF WHSE # "" AND CATG.REQ.BOOK = "Y" THEN
            IWH.ID=CONO:EST.PRODS<1,I>:"!":WHSE
            GOSUB GET.UNIT.PR
            JRM.CNT = COUNT(JOB.RESV.MATL,VM) + (JOB.RESV.MATL # "")
            FND = 0
            FOR JM = 1 TO JRM.CNT UNTIL FND
               IF JOB.RESV.MATL<1,JM> = EST.PRODS<1,I> AND JOB.RESV.WHSE<1,JM> = WHSE THEN
                  SAVE.INV.JS.REC<JM,2> = UNIT.PR
                  YEOW.QTY = INT(((EST.QTYS<1,I>/ICR.MT1)*ICR.DV1)*ICR.DV2 + .5)
                  SAVE.INV.JS.REC<JM,3> = SAVE.INV.JS.REC<JM,3> + YEOW.QTY
                  FND = 1
               END
            NEXT JM
            IF NOT(FND) THEN
               JRM.CNT = JRM.CNT + 1
               JOB.RESV.MATL<1,JRM.CNT> = EST.PRODS<1,I>
               JOB.RESV.WHSE<1,JRM.CNT> = WHSE
               JOB.ALOC.QTY<1,JRM.CNT> = 0
               JOB.RESV.QTY<1,JRM.CNT> = 0
               JOB.USED.QTY<1,JRM.CNT> = 0
               JOB.ALOC.AMT<1,JRM.CNT> = 0
               JOB.RESV.AMT<1,JRM.CNT> = 0
               JOB.USED.AMT<1,JRM.CNT> = 0
               SAVE.INV.JS.REC<JRM.CNT,2> = UNIT.PR
               SAVE.INV.JS.REC<JRM.CNT,3> = INT(((EST.QTYS<1,I>/ICR.MT1)*ICR.DV1)*ICR.DV2 + .5)
            END
         END
      NEXT I
   END
   GOTO 99999
RETURN

*
*---- CONSTRUCT ESTIMATE.JOB RECORD
*
3000 *
   BEGIN CASE
      CASE OLD.EST # "" AND EST.NO = ""
         DELETE ESTIMATE.JOB, CONO:JOB.NO
      CASE EST.NO # ""
         MATREADU ESTJ.REC FROM ESTIMATE.JOB, CONO:JOB.NO ELSE NULL
         MAT ESTJ.REC = ""
         ESTJ.EST.ID = EST.NO
         ESTJ.COMP = EST.PAR.COMP
         ESTJ.QTY = EST.PAR.QTY
         ESTJ.EST.TYPE = EST.TYPE
         ESTJ.DIV = FIELD(EST.DIV,"-",1)
         DC.CNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
         FOR LN = 1 TO DC.CNT
            COMP = FIELD(EST.DEPT.COMP<1,LN>,"!",2)
            LOCATE COMP IN EST.PAR.COMP<1,1>,1 SETTING CMATCH ELSE CMATCH = 0
            IF EST.PAR.COMP = "ALL" OR CMATCH > 0 THEN
               IF EST.PAR.QTY = FIELD(EST.DEPT.COMP<1,LN>,"!",3) THEN
                  GOSUB 10000
               END
            END
         NEXT LN
         MATWRITE ESTJ.REC ON ESTIMATE.JOB, CONO:JOB.NO
   END CASE
   IF OLD.EST # "" THEN
      FND1 = 1
      MATREADU EST.REC FROM ESTIMATE, CONO:OLD.EST ELSE FND1 = 0
      IF FND1 THEN
         LOCATE JOB.NO IN EST.BOOK.JOB<1>,1 SETTING FND2 ELSE FND2 = 0
         IF FND2 THEN
            EST.BOOK.JOB = DELETE(EST.BOOK.JOB,1,FND2,0)
            EST.BOOK.COMP = DELETE(EST.BOOK.COMP,1,FND2,0)
         END
         IF EST.BOOK.JOB = "" THEN
            EST.JOB = ""
            EST.BOOK.QTY = ""
            IF EST.STATUS<1,1> = "BOOKED" THEN
               EST.STATUS = DELETE(EST.STATUS,1,1,0)
               EST.STAT.DATE = DELETE(EST.STAT.DATE,1,1,0)
            END
         END
         MATWRITE EST.REC ON ESTIMATE, CONO:OLD.EST
      END ELSE
         RELEASE ESTIMATE, CONO:OLD.EST
      END
   END
   IF EST.NO # "" THEN
      FND1 = 1
      MATREADU EST.REC FROM ESTIMATE, CONO:EST.NO ELSE FND1 = 0
      IF FND1 THEN
         IF EST.STATUS<1,1> # "BOOKED" THEN
            EST.STATUS = INSERT(EST.STATUS,1,1,0,"BOOKED")
            EST.STAT.DATE = INSERT(EST.STAT.DATE,1,1,0,DATE())
         END
         LOCATE JOB.NO IN EST.BOOK.JOB<1>,1 SETTING JP ELSE
            EST.BOOK.JOB<1,JP> = JOB.NO
         END
         EST.BOOK.COMP<1,JP> = EST.PAR.COMP
         IF JOB.NO = JOB.MASTER OR JOB.MASTER = "" THEN
            EST.JOB = JOB.NO
         END
         EST.JOB = JOB.MASTER
         EST.BOOK.QTY = EST.PAR.QTY
         MATWRITE EST.REC ON ESTIMATE, CONO:EST.NO
      END ELSE
         RELEASE ESTIMATE, CONO:EST.NO
      END
   END
   MAT EST.PAR = ""
*
*---- EXTRACT JOB FOR SCHEDULING
*
3500 *
   IF EST.NO # "" THEN
      MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ""
      IF CO.PSS = "Y" THEN
         OPEN "","JOB.SCHED" TO JOB.SCHED THEN
            MATREAD JBS.REC FROM JOB.SCHED, CONO:JOB.NO ELSE
               BEGIN CASE
                  CASE JOB.FLAG = "P"
                     PMSG="Extract this job for scheduling (Y/N):"
                     PMPT_NUM = "ES-" : EST_INDX;P.RQD = "Y";P.TYPE = "I";P.MSG = PMSG
                     P.DEF = DEFAULT;VALUE="";P.MODE = 1
                     CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,P.DEF,"",VALUE)
                     GOSUB CHECK.FOR.INPUT
                     IF IP.REQ = 1 THEN GOTO 99999
                     IF VALUE = "Y" THEN
                        PARAM = ""
                        PARAM<1,1> = CONO
                        PARAM<1,2> = JOB.NO
                        PARAM<1,4> = JOB.FLAG ;* JOB.SCH.FLAG
                        *PARAM<1,5> = GUIFORM;* T21177
                        CALL PERFORM.PROG("JOB.SCHED.EXTRACT",PARAM);* THIS CALL MAY PROMPT WARNINGS/ERRORS AND HENCE MIGHT ARISE PROBS FOR EPRIMAC
                     END
                  CASE JOB.FLAG = "R"
                     PARAM = ""
                     PARAM<1,1> = CONO
                     PARAM<1,2> = JOB.NO
                     PARAM<1,3> = "N"
                     PARAM<1,4> = JOB.FLAG;* JBS.SCH.FLAG
                     *PARAM<1,5> = GUIFORM;* T21177
                     CALL PERFORM.PROG("JOB.SCHED.EXTRACT",PARAM);* THIS CALL MAY PROMPT WARNINGS/ERRORS AND HENCE MIGHT ARISE PROBS FOR EPRIMAC
                  CASE JOB.FLAG = "U"
                     MATREADU JBS.REC FROM JOB.SCHED, CONO:JOB.NO THEN
                        JBS.SCH.FLAG = "U"
                        MATWRITE JBS.REC ON JOB.SCHED, CONO:JOB.NO
                     END ELSE
                        RELEASE JOB.SCHED, CONO:JOB.NO
                     END
               END CASE
            END
         END
      END
   END
   GOTO 99999
RETURN

*
*-----------------------*
*--- SUBROUTINES   ---*
*-----------------------*
*
*--- LOAD ESTJ.REC FROM ESTD.REC
*
10000 *
   MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.NO:"!":EST.DEPT.COMP<1,LN> ELSE MAT ESTD.REC = ""
   TYPE.CNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
   FOR T = 1 TO TYPE.CNT
      ETYPE = ESTD.TYPE<1,T>
      MATREAD CCTR.REC FROM COST.CNTR, CONO:ESTD.CCTR<1,T> ELSE MAT CCTR.REC = ""
      BEGIN CASE
         CASE ETYPE = "G"
         CASE ETYPE = "L"
            GOSUB 10100
         CASE ETYPE[1,1] = "M"
            MT.TYPE = ETYPE[2,1]
            OPV = ESTD.OPV<1,T>
            PROD = ESTD.OPV<1,T>
            IF MT.TYPE="S" OR MT.TYPE = "R" OR MT.TYPE = "L" THEN
               WCNT = COUNT(EST.PROD.OS.PROD<1,COMP>,SM) + 1
               FOR WP = 1 TO WCNT
                  IF ESTD.DESC<1,T> = EST.PROD.OS.DESC<1,COMP,WP> AND EST.PROD.INV.ID<1,COMP,WP> # '' THEN
                     PROD = EST.PROD.INV.ID<1,COMP,WP>
                  END
               NEXT WP
            END
            GOSUB 10200
         CASE ETYPE = "I"
            OPV = ""
            PROD = ESTD.OPV<1,T>
            GOSUB 10200
         CASE ETYPE[1,1] = "P"
            GOSUB 10300
         CASE ETYPE = "S"
            GOSUB 10400
         CASE 1
            GOSUB 10500
      END CASE
   NEXT T
1999 *
   RETURN

*
*--- LABOR
*
10100 *
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.LB.DEPT,VM) + (ESTJ.LB.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
         CASE CCTR.DEPT = ESTJ.LB.DEPT<1,D>
            BEGIN CASE
               CASE ESTD.CCTR<1,T> = ESTJ.LB.CCTR<1,D>
                  BEGIN CASE
                     CASE ESTD.OPV<1,T> = ESTJ.LB.OPER<1,D>
                        DFND = D
                        MATCHED = 1
                     CASE ESTD.OPV<1,T> > ESTJ.LB.OPER<1,D>
                        DFND = D + 1
                  END CASE
               CASE ESTD.CCTR<1,T> > ESTJ.LB.CCTR<1,D>
                  DFND = D + 1
            END CASE
         CASE CCTR.DEPT > ESTJ.LB.DEPT<1,D>
            DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.LB.DEPT = INSERT(ESTJ.LB.DEPT,1,DFND,0,"")
         ESTJ.LB.CCTR = INSERT(ESTJ.LB.CCTR,1,DFND,0,"")
         ESTJ.LB.OPER = INSERT(ESTJ.LB.OPER,1,DFND,0,"")
         ESTJ.LB.HRS = INSERT(ESTJ.LB.HRS,1,DFND,0,"")
         ESTJ.LB.QTY = INSERT(ESTJ.LB.QTY,1,DFND,0,"")
         ESTJ.LB.DCOST = INSERT(ESTJ.LB.DCOST,1,DFND,0,"")
         ESTJ.LB.COST = INSERT(ESTJ.LB.COST,1,DFND,0,"")
         ESTJ.LB.SALE = INSERT(ESTJ.LB.SALE,1,DFND,0,"")
      END
      ESTJ.LB.DEPT<1,DFND> = CCTR.DEPT
      ESTJ.LB.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.LB.OPER<1,DFND> = ESTD.OPV<1,T>
   END
   ESTJ.LB.HRS<1,DFND> = ESTJ.LB.HRS<1,DFND> + ESTD.HRS<1,T>
   IF ESTD.OPV.TYPE<1,T> = "F" THEN
      ESTJ.LB.QTY<1,DFND> = ESTJ.LB.QTY<1,DFND> + 0
   END ELSE
      ESTJ.LB.QTY<1,DFND> = ESTJ.LB.QTY<1,DFND> + ESTD.QTY<1,T>
   END
   ESTJ.LB.DCOST<1,DFND> = ESTJ.LB.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.LB.COST<1,DFND> = ESTJ.LB.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.LB.SALE<1,DFND> = ESTJ.LB.SALE<1,DFND> + ESTD.TSALE<1,T>
   ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.TSALE<1,T>
   RETURN
*
*--- MATERIAL
*
10200 *
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.MT.DEPT,VM) + (ESTJ.MT.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
         CASE CCTR.DEPT = ESTJ.MT.DEPT<1,D>
            BEGIN CASE
               CASE ESTD.CCTR<1,T> = ESTJ.MT.CCTR<1,D>
                  BEGIN CASE
                     CASE PROD = ESTJ.MT.PROD<1,D>
                        BEGIN CASE
                           CASE ESTD.CODE<1,T> = ESTJ.MT.PLINE<1,D>
                              IF ESTD.UM<1,T> = ESTJ.MT.TYPE<1,D> THEN
                                 DFND = D
                                 MATCHED = 1
                              END ELSE
                                 DFND = D + 1
                              END
                           CASE ESTD.CODE<1,T> > ESTJ.MT.PLINE<1,D>
                              DFND = D + 1
                        END CASE
                     CASE PROD > ESTJ.MT.PROD<1,D>
                        DFND = D + 1
                  END CASE
               CASE ESTD.CCTR<1,T> > ESTJ.MT.CCTR<1,D>
                  DFND = D + 1
            END CASE
         CASE CCTR.DEPT > ESTJ.MT.DEPT<1,D>
            DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.MT.DEPT = INSERT(ESTJ.MT.DEPT,1,DFND,0,"")
         ESTJ.MT.CCTR = INSERT(ESTJ.MT.CCTR,1,DFND,0,"")
         ESTJ.MT.OPER = INSERT(ESTJ.MT.OPER,1,DFND,0,"")
         ESTJ.MT.PROD = INSERT(ESTJ.MT.PROD,1,DFND,0,"")
         ESTJ.MT.DESC = INSERT(ESTJ.MT.DESC,1,DFND,0,"")
         ESTJ.MT.PLINE = INSERT(ESTJ.MT.PLINE,1,DFND,0,"")
         ESTJ.MT.MLINE = INSERT(ESTJ.MT.MLINE,1,DFND,0,"")
         ESTJ.MT.QTY = INSERT(ESTJ.MT.QTY,1,DFND,0,"")
         ESTJ.MT.TYPE = INSERT(ESTJ.MT.TYPE,1,DFND,0,"")
         ESTJ.MT.DCOST = INSERT(ESTJ.MT.DCOST,1,DFND,0,"")
         ESTJ.MT.COST = INSERT(ESTJ.MT.COST,1,DFND,0,"")
         ESTJ.MT.SALE = INSERT(ESTJ.MT.SALE,1,DFND,0,"")
         ESTJ.MT.PROD.CLS = INSERT(ESTJ.MT.PROD.CLS,1,DFND,0,"")
      END
      ESTJ.MT.DEPT<1,DFND> = CCTR.DEPT
      ESTJ.MT.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.MT.OPER<1,DFND> = OPV
      ESTJ.MT.PROD<1,DFND> = PROD
      ESTJ.MT.DESC<1,DFND> = ESTD.DESC<1,T>
      ESTJ.MT.PLINE<1,DFND> = ESTD.CODE<1,T>
      ESTJ.MT.PROD.CLS<1,DFND> = ESTD.TYPE<1,T>
      MATREAD CATG.REC FROM CATEGORY, CONO:ESTD.CODE<1,T> ELSE MAT CATG.REC = ""
      ESTJ.MT.MLINE<1,DFND> = CATG.MAJ.LINE
      MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:MT.TYPE ELSE MAT ESTM.REC = ""
      LOCATE ESTD.OPV<1,T> IN ESTM.PROD<1>,1 SETTING PF ELSE PF = 0
      IF PF THEN
         ESTJ.MT.TYPE<1,DFND> = ESTM.UM<1,PF>
      END ELSE
         BEGIN CASE
            CASE CATG.TYPE = "S" OR MT.TYPE = "S"
               ESTJ.MT.TYPE<1,DFND> = "SHT"
            CASE CATG.TYPE = "P" OR CATG.TYPE = "F" OR CATG.TYPE = "O"
               ESTJ.MT.TYPE<1,DFND> = "EA"
            CASE MT.TYPE = "P" OR MT.TYPE = "F" OR MT.TYPE = "O"
               ESTJ.MT.TYPE<1,DFND> = "EA"
            CASE MT.TYPE = "B" OR MT.TYPE = "X"
               ESTJ.MT.TYPE<1,DFND> = "EA"
            CASE CATG.TYPE = "I" OR MT.TYPE = "I"
               ESTJ.MT.TYPE<1,DFND> = "LBS"
            CASE CATG.TYPE = "PC"
               ESTJ.MT.TYPE<1,DFND> = "MSI"
            CASE CATG.TYPE = "L" OR MT.TYPE = "R"
               ESTJ.MT.TYPE<1,DFND> = "LBS"
            CASE CATG.TYPE = "RL"
               ESTJ.MT.TYPE<1,DFND> = "MSI"
         END CASE
      END
   END
   ESTJ.MT.QTY<1,DFND> = ESTJ.MT.QTY<1,DFND> + ESTD.QTY<1,T>
   ESTJ.MT.DCOST<1,DFND> = ESTJ.MT.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.MT.COST<1,DFND> = ESTJ.MT.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.MT.SALE<1,DFND> = ESTJ.MT.SALE<1,DFND> + ESTD.TSALE<1,T>
   ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.TSALE<1,T>
   RETURN
*
*--- OUTSIDE PURCHASE
*
10300 *
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.OS.DEPT,VM) + (ESTJ.OS.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
         CASE CCTR.DEPT = ESTJ.OS.DEPT<1,D>
            BEGIN CASE
               CASE ESTD.CCTR<1,T> = ESTJ.OS.CCTR<1,D>
                  BEGIN CASE
                     CASE ESTD.OPV<1,T> = ESTJ.OS.OPER<1,D>
                        BEGIN CASE
                           CASE ESTD.CODE<1,T> = ESTJ.OS.PLINE<1,D>
                              BEGIN CASE
                                 CASE ESTD.VEND<1,T> = ESTJ.OS.VEND<1,D>
                                    DFND = D
                                    MATCHED = 1
                                 CASE ESTD.VEND<1,T> > ESTJ.OS.VEND<1,D>
                                    DFND = D + 1
                              END CASE
                           CASE ESTD.CODE<1,T> > ESTJ.OS.PLINE<1,D>
                              DFND = D + 1
                        END CASE
                     CASE ESTD.OPV<1,T> > ESTJ.OS.OPER<1,D>
                        DFND = D + 1
                  END CASE
               CASE ESTD.CCTR<1,T> > ESTJ.OS.CCTR<1,D>
                  DFND = D + 1
            END CASE
         CASE CCTR.DEPT > ESTJ.OS.DEPT<1,D>
            DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.OS.DEPT = INSERT(ESTJ.OS.DEPT,1,DFND,0,"")
         ESTJ.OS.CCTR = INSERT(ESTJ.OS.CCTR,1,DFND,0,"")
         ESTJ.OS.OPER = INSERT(ESTJ.OS.OPER,1,DFND,0,"")
         ESTJ.OS.DESC = INSERT(ESTJ.OS.DESC,1,DFND,0,"")
         ESTJ.OS.PLINE = INSERT(ESTJ.OS.PLINE,1,DFND,0,"")
         ESTJ.OS.VEND = INSERT(ESTJ.OS.VEND,1,DFND,0,"")
         ESTJ.OS.QTY = INSERT(ESTJ.OS.QTY,1,DFND,0,"")
         ESTJ.OS.DCOST = INSERT(ESTJ.OS.DCOST,1,DFND,0,"")
         ESTJ.OS.COST = INSERT(ESTJ.OS.COST,1,DFND,0,"")
         ESTJ.OS.SALE = INSERT(ESTJ.OS.SALE,1,DFND,0,"")
      END
      ESTJ.OS.DEPT<1,DFND> = CCTR.DEPT
      ESTJ.OS.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.OS.OPER<1,DFND> = ESTD.OPV<1,T>
      ESTJ.OS.DESC<1,DFND> = ESTD.DESC<1,T>
      ESTJ.OS.PLINE<1,DFND> = ESTD.CODE<1,T>
      ESTJ.OS.VEND<1,DFND> = ESTD.VEND<1,T>
   END
   ESTJ.OS.QTY<1,DFND> = ESTJ.OS.QTY<1,DFND> + ESTD.QTY<1,T>
   ESTJ.OS.DCOST<1,DFND> = ESTJ.OS.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.OS.COST<1,DFND> = ESTJ.OS.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.OS.SALE<1,DFND> = ESTJ.OS.SALE<1,DFND> + ESTD.TSALE<1,T>
   ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.TSALE<1,T>
   RETURN
*
*--- SHIPPING
*
10400 *
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.SP.DEPT,VM) + (ESTJ.SP.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
         CASE CCTR.DEPT = ESTJ.SP.DEPT<1,D>
            BEGIN CASE
               CASE ESTD.CCTR<1,T> = ESTJ.SP.CCTR<1,D>
                  BEGIN CASE
                     CASE ESTD.OPV<1,T> = ESTJ.SP.OPER<1,D>
                        BEGIN CASE
                           CASE ESTD.CODE<1,T> = ESTJ.SP.VIA<1,D>
                              DFND = D
                              MATCHED = 1
                           CASE ESTD.CODE<1,T> > ESTJ.SP.VIA<1,D>
                              DFND = D + 1
                        END CASE
                     CASE ESTD.OPV<1,T> > ESTJ.SP.OPER<1,D>
                        DFND = D + 1
                  END CASE
               CASE ESTD.CCTR<1,T> > ESTJ.SP.CCTR<1,D>
                  DFND = D + 1
            END CASE
         CASE CCTR.DEPT > ESTJ.SP.DEPT<1,D>
            DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.SP.DEPT = INSERT(ESTJ.SP.DEPT,1,DFND,0,"")
         ESTJ.SP.CCTR = INSERT(ESTJ.SP.CCTR,1,DFND,0,"")
         ESTJ.SP.OPER = INSERT(ESTJ.SP.OPER,1,DFND,0,"")
         ESTJ.SP.DESC = INSERT(ESTJ.SP.DESC,1,DFND,0,"")
         ESTJ.SP.VIA = INSERT(ESTJ.SP.VIA,1,DFND,0,"")
         ESTJ.SP.DCOST = INSERT(ESTJ.SP.DCOST,1,DFND,0,"")
         ESTJ.SP.COST = INSERT(ESTJ.SP.COST,1,DFND,0,"")
         ESTJ.SP.SALE = INSERT(ESTJ.SP.SALE,1,DFND,0,"")
      END
      ESTJ.SP.DEPT<1,DFND> = CCTR.DEPT
      ESTJ.SP.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.SP.OPER<1,DFND> = ESTD.OPV<1,T>
      ESTJ.SP.DESC<1,DFND> = ESTD.DESC<1,T>
      ESTJ.SP.VIA<1,DFND> = ESTD.CODE<1,T>
   END
   ESTJ.SP.DCOST<1,DFND> = ESTJ.SP.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.SP.COST<1,DFND> = ESTJ.SP.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.SP.SALE<1,DFND> = ESTJ.SP.SALE<1,DFND> + ESTD.TSALE<1,T>
   ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.TSALE<1,T>
   RETURN
*
*--- MISCELLANEOUS
*
10500 *
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.MS.DEPT,VM) + (ESTJ.MS.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
         CASE CCTR.DEPT = ESTJ.MS.DEPT<1,D>
            BEGIN CASE
               CASE ESTD.CCTR<1,T> = ESTJ.MS.CCTR<1,D>
                  BEGIN CASE
                     CASE ESTD.OPV<1,T> = ESTJ.MS.OPER<1,D>
                        IF ESTD.DESC<1,T> = ESTJ.MS.DESC<1,D> THEN
                           DFND = D
                           MATCHED = 1
                        END ELSE
                           DFND = D + 1
                        END
                     CASE ESTD.OPV<1,T> > ESTJ.MS.OPER<1,D>
                        DFND = D + 1
                  END CASE
               CASE ESTD.CCTR<1,T> > ESTJ.MS.CCTR<1,D>
                  DFND = D + 1
            END CASE
         CASE CCTR.DEPT > ESTJ.MS.DEPT<1,D>
            DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.MS.DEPT = INSERT(ESTJ.MS.DEPT,1,DFND,0,"")
         ESTJ.MS.CCTR = INSERT(ESTJ.MS.CCTR,1,DFND,0,"")
         ESTJ.MS.OPER = INSERT(ESTJ.MS.OPER,1,DFND,0,"")
         ESTJ.MS.DESC = INSERT(ESTJ.MS.DESC,1,DFND,0,"")
         ESTJ.MS.DCOST = INSERT(ESTJ.MS.DCOST,1,DFND,0,"")
         ESTJ.MS.COST = INSERT(ESTJ.MS.COST,1,DFND,0,"")
         ESTJ.MS.SALE = INSERT(ESTJ.MS.SALE,1,DFND,0,"")
      END
      ESTJ.MS.DEPT<1,DFND> = CCTR.DEPT
      ESTJ.MS.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.MS.OPER<1,DFND> = ESTD.OPV<1,T>
      ESTJ.MS.DESC<1,DFND> = ESTD.DESC<1,T>
   END
   ESTJ.MS.DCOST<1,DFND> = ESTJ.MS.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.MS.COST<1,DFND> = ESTJ.MS.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.MS.SALE<1,DFND> = ESTJ.MS.SALE<1,DFND> + ESTD.TSALE<1,T>
   ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.TSALE<1,T>
   RETURN

*
*---- CONVERT ROLL LABEL MSI TO STOCKING UNIT
*
70000 *
   MATREAD INV.REC FROM INVENTORY,CONO:PROD ELSE MAT INV.REC = ''
   GOSUB 80000
   NQTY = QTY
***************
***************   NEED TO USE EST WIDTH FROM PROD DEF SCREEN   ***************
*
   BEGIN CASE
      CASE INV.UNIT<1,2> = "MSI"
      CASE INV.UNIT<1,2> = "FT"
         IF WIDTH + 0 # 0 THEN
            NQTY = (NQTY * 1000) / (12 * OCONV(WIDTH,"MD4"))
         END ELSE
            NQTY = 0
         END
      CASE INV.UNIT<1,2> = "PC"
         IF WIDTH + 0 # 0 THEN
            NQTY = (NQTY * 1000) / (10 * OCONV(WIDTH,"MD4"))
         END ELSE
            NQTY = 0
         END
   END CASE
   QTY = NQTY
   RETURN
*
80000 *
   CASE1.FLAG = ""
   BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.DV1 = INV.M.WT
         ICR.MT1 = 1
         ICR.DV2 = 1
         ICR.CNV = "MD0"
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.DV1 = INV.PAP.WIDTH/100
         ICR.MT1 = 10
         ICR.DV2 = 1
         ICR.CNV = "MD0"
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.DV1 = INV.PAP.WIDTH/100
         ICR.MT1 = 100
         ICR.DV2 = 12
         ICR.CNV = "MD0"
      CASE 1
         ICR.DV1 = 10
         ICR.MT1 = 1
         ICR.DV2 = 1
         ICR.CNV = "MD2"
         CASE1.FLAG = "Y"
   END CASE
   RETURN
*
*---- GET WAREHOUSE
*
85000 *
   IF NOT(WPTR) AND NOT(GPTR) THEN
      PMSG='There are no warehouses for product ':DISP.PROD:' with Division ':JOB.DIV:' or 00 <RTN> '
      PMPT_NUM = "EWX1-" : I : "-" :EST_INDX;P.RQD = "Y";P.TYPE = "M";P.MSG = ERRMSG;P.MODE = 3
      CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,"","","")
      NEW.EST='';MAT EST.REC = '';JOB.EST='';ESTAT=1
      GOTO 2100
   END
   IF WPTR THEN DEFAULT = INV.WHSE.CODE<1,WPTR> ELSE IF GPTR THEN DEFAULT = INV.WHSE.CODE<1,GPTR>
   PMSG="Enter Whse Code for Product ":DISP.PROD
   IF DEFAULT # "" THEN
      PMSG=PMSG:" (Default=":DEFAULT:") - "
   END ELSE
      PMSG=PMSG:" - "
   END
   PMPT_NUM = "EW-" : I : "-" : EST_INDX;P.RQD = "Y";P.TYPE = "I";P.MSG = PMSG;P.MODE = 1
   P.DEF = DEFAULT;VALUE = ""
   CALL ORDM_PROCESS_PROMPTS_ARR(PROMPTS,P.MODE,PMPT_NUM,P.TYPE,P.RQD,P.MSG,P.DEF,JOB.DIV,VALUE)
   GOSUB CHECK.FOR.INPUT
   IF IP.REQ = 1 THEN GOTO 99999   
   WHSE = VALUE
85999* 
   GOTO 2105
RETURN
*
*****************
GET.UNIT.PR: 
*****************
*
   MATREAD IWH.REC FROM INV.WHSE,IWH.ID ELSE  MAT IWH.REC=''
   BEGIN CASE
      CASE CO.JES.PARAM<1,11> = 'R'
         DIV.POS='';FISCAL.FLAG='JC';TRAN.PERIOD=''
         ERR.FLG='';ERRMSG=''
         CALL GET.LAST.COST (IWH.ID,MAT IWH.REC,WAREHOUSE,CONTROL,ERR.FLG,ERRMSG,DIV.POS,FISCAL.FLAG,TRAN.PERIOD,LAST.PRICE)
         IF ERR.FLG='' THEN
            UNIT.PR=LAST.PRICE
         END ELSE
            UNIT.PR = 0
         END
      CASE CO.JES.PARAM<1,11> = 'S'
         UNIT.PR = IWH.STD.COST
      CASE CO.JES.PARAM<1,11> = 'A'
         UNIT.PR = IWH.AVG.COST
      CASE CO.JES.PARAM<1,11> = 'L'
         UNIT.PR = IWH.LIST.COST
      CASE 1
         UNIT.PR = 0
   END CASE
   RETURN

CHECK.FOR.INPUT:
   STATUS = RBO.getProperty("","IP_FLAG",IP.REQ)
RETURN

89000
   STATUS = RBO.setProperty("","PROMPTS",PROMPTS)
RETURN

*
*---- ERROR ROUTINE
*
99001 *
   ESTAT = 1
   GOTO 99999
99002 *
   ESTAT = 2
   GOTO 99999
99999 *
   RELEASE
   RETURN
END


