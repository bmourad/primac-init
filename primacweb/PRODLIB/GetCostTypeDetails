SUBROUTINE GetCostTypeDetails
****************************************************************************************************
*   Program name :- GetCostTypeDetails
*   Created      :- 7/8/2003
*   Created By   :- B.Krishna
****************DESCRIPTION**************************************************************************
* This subroutine will display estimate data summarized by cost type with  detail for material costs.
* Data may be summarized for one or all components for the specified order quantity.
*****************************************************************************************************

$INCLUDE WWINSERT RBO.H
$INCLUDE JES.CPYLIB EST.RES.COST.TYPE.SUM
$INCLUDE JES.CPYLIB ESTIMATE
$INCLUDE JES.CPYLIB ESTIMATE.RES
$INCLUDE JES.CPYLIB ESTIMATE.DEPT
$INCLUDE JES.CPYLIB JES.FILE.VARS
$INCLUDE CPYLIB FILE.VARS
$INCLUDE CPYLIB CHAR
$INCLUDE JES.CPYLIB EST.COMP.DEPT.SUM

*
*---- INITIALIZATION
*
      COST.TYPE.COMP="";COST.TYPE.QTY="";COST.TYPE.LB.HRS=""
      COST.TYPE.LB.DCOST="";COST.TYPE.MT.DCOST ="";COST.TYPE.OS.DCOST ="";COST.TYPE.SP.DCOST ="";COST.TYPE.MS.DCOST ="";COST.TYPE.LB.COST ="";COST.TYPE.MT.COST ="";COST.TYPE.OS.COST ="";COST.TYPE.SP.COST ="";COST.TYPE.MS.COST ="";COST.TYPE.LB.SALE ="";COST.TYPE.MT.SALE ="";COST.TYPE.OS.SALE =""
      COST.TYPE.SP.SALE="";COST.TYPE.MS.SALE="";COST.TYPE.TOTAL.HRS="";COST.TYPE.TOTAL.DCOST="";COST.TYPE.TOTAL.COST ="";COST.TYPE.TOTAL.TEMP="";COST.TYPE.TOTAL.SALE=""
      COST.MT.DCOST.DET1 ="";COST.MT.DCOST.DET2 ="";COST.MT.DCOST.DET3 ="";COST.MT.DCOST.DET4 ="";COST.MT.DCOST.DET5="";COST.MT.DCOST.DET6 ="";COST.MT.COST.DET1 ="";COST.MT.COST.DET2="";COST.MT.COST.DET3="";COST.MT.COST.DET4="";COST.MT.COST.DET5="";COST.MT.COST.DET6="";COST.MT.SALE.DET1="";COST.MT.SALE.DET2="";COST.MT.SALE.DET3="";COST.MT.SALE.DET4="";COST.MT.SALE.DET5="";COST.MT.SALE.DET6=""

      IN_PARAM = "" ; OUT_PARAM = ""
      ERRMSG = ""
      ERRMSG = RBO.getProperty('','ID',ESTIMATE.ID)
      ERRMSG = RBO.getProperty('','Qty',QSEL)
      CONO = ESTIMATE.ID[1,3]
      EST.KEY = TRIM(ESTIMATE.ID[4,99])
      FinalCostTypeResult =""
      DSEL = "ALL"
      CSEL = "ALL"
*     opening files
      ERRMSG = "CALL openEstimateFiles PROBLEM"
      CALL openEstimateFiles(ERRMSG,IN_PARAM,OUT_PARAM,MAT FILE.VARS,MAT JES.FILE.VARS)
      IF ERRMSG # "" THEN GOTO 93000
      MATREAD EST.REC FROM ESTIMATE, ESTIMATE.ID ELSE
      ERRMSG = 'Estimate not on file. Try again!'
      GOTO 93000
      END

      READ EST.RL.PP FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE EST.RL.PP=''
      MAT CSUM.REC = ""
*
*---- MAIN PROCESSING
*
100 *
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>[1,1]
            IF TYPE = "G" THEN GOTO 180
            TSUB = ESTD.TYPE<1,TP>[2,1]
            HRS = ESTD.HRS<1,TP>
            DCOST = ESTD.DCOST<1,TP>
            COST = ESTD.COST<1,TP>
            IF EST.RL.PP.INCLUDE#'Y' THEN
               LOCATE ESTD.DEPT IN EST.RL.PP<1>,1 SETTING DPOS THEN
                  SALE = 0
               END ELSE
                  SALE = ESTD.TSALE<1,TP>
               END
            END ELSE
               SALE = ESTD.TSALE<1,TP>
            END
            RATE = ESTD.RATE<1,TP>
            PROD = ESTD.OPV<1,TP>
            QCALC = ESTD.GRP.QCALC<1,TP>
            QPARAM = ESTD.GRP.QPARAM<1,TP>
            BEGIN CASE
               CASE TYPE = "L"
                  IF QCALC = "A" THEN
                     BEGIN CASE
                        CASE QPARAM = "100"
                           CSUM.LB.COPY = CSUM.LB.COPY + RATE
                        CASE QPARAM = "200"
                           CSUM.LB.COLOR = CSUM.LB.COLOR + RATE
                        CASE QPARAM = "300"
                           CSUM.LB.VERSION = CSUM.LB.VERSION + RATE
                     END CASE
                  END
                  CSUM.LB.HRS = CSUM.LB.HRS + HRS
                  CSUM.LB.DCOST = CSUM.LB.DCOST + DCOST
                  CSUM.LB.COST = CSUM.LB.COST + COST
                  CSUM.LB.SALE = CSUM.LB.SALE + SALE
               CASE TYPE = "M" OR TYPE = "I"
                  IF QCALC = "A" THEN
                     BEGIN CASE
                        CASE QPARAM = "100"
                           CSUM.MT.COPY<1,-1> = PROD
                        CASE QPARAM = "200"
                           CSUM.MT.COLOR<1,-1> = PROD
                        CASE QPARAM = "300"
                           CSUM.MT.VERSION<1,-1> = PROD
                     END CASE
                  END
                  CSUM.MT.DCOST = CSUM.MT.DCOST + DCOST
                  CSUM.MT.COST = CSUM.MT.COST + COST
                  CSUM.MT.SALE = CSUM.MT.SALE + SALE
                  BEGIN CASE
                     CASE ESTD.TYPE<1,TP> = 'MR' OR ESTD.TYPE<1,TP> = 'MS'
                        DET = 1
                     CASE ESTD.TYPE<1,TP> = 'MI'
                        DET = 2
                     CASE ESTD.TYPE<1,TP> = 'MF'
                        DET = 3
                     CASE ESTD.TYPE<1,TP> = 'MP'
                        DET = 4
                     CASE ESTD.TYPE<1,TP> = 'MC'
                        DET = 5
                     CASE ESTD.TYPE<1,TP> = 'MO'
                        DET = 6
                  END CASE
                  CSUM.MT.DCOST.DET<1,DET> = CSUM.MT.DCOST.DET<1,DET> + DCOST
                  CSUM.MT.COST.DET<1,DET> = CSUM.MT.COST.DET<1,DET> + COST
                  CSUM.MT.SALE.DET<1,DET> = CSUM.MT.SALE.DET<1,DET> + SALE
               CASE TYPE = "P"
                  CSUM.OS.DCOST = CSUM.OS.DCOST + DCOST
                  CSUM.OS.COST = CSUM.OS.COST + COST
                  CSUM.OS.SALE = CSUM.OS.SALE + SALE
               CASE TYPE = "S"
                  CSUM.SP.DCOST = CSUM.SP.DCOST + DCOST
                  CSUM.SP.COST = CSUM.SP.COST + COST
                  CSUM.SP.SALE = CSUM.SP.SALE + SALE
               CASE TYPE = "O"
                  CSUM.MS.DCOST = CSUM.MS.DCOST + DCOST
                  CSUM.MS.COST = CSUM.MS.COST + COST
                  CSUM.MS.SALE = CSUM.MS.SALE + SALE
            END CASE
            CSUM.TOT.DCOST = CSUM.TOT.DCOST + DCOST
            CSUM.TOT.COST = CSUM.TOT.COST + COST
            CSUM.TOT.SALE = CSUM.TOT.SALE + SALE
180      NEXT TP
190   NEXT DP


   *MAT TOT.REC = ""
   TOTAL.HRS = CSUM.LB.HRS
   TOTAL.DCOST = CSUM.LB.DCOST+CSUM.MT.DCOST+CSUM.OS.DCOST+CSUM.SP.DCOST+CSUM.MS.DCOST
   TOTAL.COST = CSUM.LB.COST+CSUM.MT.COST+CSUM.OS.COST+CSUM.SP.COST+CSUM.MS.COST
   TOTAL.SALE = CSUM.LB.SALE+CSUM.MT.SALE+CSUM.OS.SALE+CSUM.SP.SALE+CSUM.MS.SALE
   
   IF CSEL = "ALL" THEN
      COST.TYPE.COMP = "ALL COMPONENTS"
   END ELSE
      COST.TYPE.COMP = EST.PROD.DESC<1,CSEL>
   END

   COST.TYPE.QTY = OCONV(QSEL,'MD0,')
   COST.TYPE.LB.HRS = OCONV(CSUM.LB.HRS,'MD2,')
   COST.TYPE.LB.DCOST = OCONV(CSUM.LB.DCOST,'MD2,')
   COST.TYPE.MT.DCOST = OCONV(CSUM.MT.DCOST,'MD2,')
   COST.TYPE.OS.DCOST = OCONV(CSUM.OS.DCOST,'MD2,')
   COST.TYPE.SP.DCOST = OCONV(CSUM.SP.DCOST,'MD2,')
   COST.TYPE.MS.DCOST = OCONV(CSUM.MS.DCOST,'MD2,')
   COST.TYPE.LB.COST = OCONV(CSUM.LB.COST,'MD2,')
   COST.TYPE.MT.COST = OCONV(CSUM.MT.COST,'MD2,')
   COST.TYPE.OS.COST = OCONV(CSUM.OS.COST,'MD2,')
   COST.TYPE.SP.COST = OCONV(CSUM.SP.COST,'MD2,')
   COST.TYPE.MS.COST = OCONV(CSUM.MS.COST,'MD2,')
   COST.TYPE.LB.SALE = OCONV(CSUM.LB.SALE,'MD2,')
   COST.TYPE.MT.SALE = OCONV(CSUM.MT.SALE,'MD2,')
   COST.TYPE.OS.SALE = OCONV(CSUM.OS.SALE,'MD2,')
   COST.TYPE.SP.SALE = OCONV(CSUM.SP.SALE,'MD2,')
   COST.TYPE.MS.SALE = OCONV(CSUM.MS.SALE,'MD2,')
   COST.TYPE.TOTAL.HRS = OCONV(TOTAL.HRS,'MD2,')
   COST.TYPE.TOTAL.DCOST = OCONV(TOTAL.DCOST,'MD2,')
   COST.TYPE.TOTAL.COST = OCONV(TOTAL.COST,'MD2,')
   COST.TYPE.TOTAL.TEMP = OCONV(INT(TOTAL.SALE/(QSEL/1000)+.5),'MD2,')
   COST.TYPE.TOTAL.SALE = OCONV(TOTAL.SALE,'MD2,')

* BOTTOM DETAILS 

   COST.MT.DCOST.DET1 = OCONV(CSUM.MT.DCOST.DET<1,1>,'MD2,')
   COST.MT.DCOST.DET2 = OCONV(CSUM.MT.DCOST.DET<1,2>,'MD2,')
   COST.MT.DCOST.DET3 = OCONV(CSUM.MT.DCOST.DET<1,3>,'MD2,')
   COST.MT.DCOST.DET4 = OCONV(CSUM.MT.DCOST.DET<1,4>,'MD2,')
   COST.MT.DCOST.DET5 = OCONV(CSUM.MT.DCOST.DET<1,5>,'MD2,')
   COST.MT.DCOST.DET6 = OCONV(CSUM.MT.DCOST.DET<1,6>,'MD2,')
   COST.MT.COST.DET1 = OCONV(CSUM.MT.COST.DET<1,1>,'MD2,')
   COST.MT.COST.DET2 = OCONV(CSUM.MT.COST.DET<1,2>,'MD2,')
   COST.MT.COST.DET3 = OCONV(CSUM.MT.COST.DET<1,3>,'MD2,')
   COST.MT.COST.DET4 = OCONV(CSUM.MT.COST.DET<1,4>,'MD2,')
   COST.MT.COST.DET5 = OCONV(CSUM.MT.COST.DET<1,5>,'MD2,')
   COST.MT.COST.DET6 = OCONV(CSUM.MT.COST.DET<1,6>,'MD2,')
   COST.MT.SALE.DET1 = OCONV(CSUM.MT.SALE.DET<1,1>,'MD2,')
   COST.MT.SALE.DET2 = OCONV(CSUM.MT.SALE.DET<1,2>,'MD2,')
   COST.MT.SALE.DET3 = OCONV(CSUM.MT.SALE.DET<1,3>,'MD2,')
   COST.MT.SALE.DET4 = OCONV(CSUM.MT.SALE.DET<1,4>,'MD2,')
   COST.MT.SALE.DET5 = OCONV(CSUM.MT.SALE.DET<1,5>,'MD2,')
   COST.MT.SALE.DET6 = OCONV(CSUM.MT.SALE.DET<1,6>,'MD2,')

FinalCostTypeResult = COST.TYPE.COMP : "^" : COST.TYPE.QTY : "^" : COST.TYPE.LB.HRS : "^" : COST.TYPE.LB.DCOST : "^" : COST.TYPE.MT.DCOST : "^" : COST.TYPE.OS.DCOST   : "^" :  COST.TYPE.SP.DCOST  :"^":COST.TYPE.MS.DCOST  :"^": COST.TYPE.LB.COST  :"^": COST.TYPE.MT.COST :"^": COST.TYPE.OS.COST :"^": COST.TYPE.SP.COST  :"^": COST.TYPE.MS.COST  :"^": COST.TYPE.LB.SALE  :"^": COST.TYPE.MT.SALE  :"^": COST.TYPE.OS.SALE 
FinalCostTypeResult = FinalCostTypeResult :"^":COST.TYPE.SP.SALE:"^":COST.TYPE.MS.SALE:"^":COST.TYPE.TOTAL.HRS:"^":COST.TYPE.TOTAL.DCOST:"^":COST.TYPE.TOTAL.COST :"^":COST.TYPE.TOTAL.TEMP:"^":COST.TYPE.TOTAL.SALE   
FinalCostTypeResult = FinalCostTypeResult :"^": COST.MT.DCOST.DET1 :"^" : COST.MT.DCOST.DET2 : "^" : COST.MT.DCOST.DET3 :"^":COST.MT.DCOST.DET4 :"^": COST.MT.DCOST.DET5 :"^":COST.MT.DCOST.DET6 :"^":COST.MT.COST.DET1 :"^":COST.MT.COST.DET2:"^":COST.MT.COST.DET3:"^":COST.MT.COST.DET4:"^":COST.MT.COST.DET5:"^":COST.MT.COST.DET6 :"^" :COST.MT.SALE.DET1:"^" :COST.MT.SALE.DET2:"^" :COST.MT.SALE.DET3:"^" :COST.MT.SALE.DET4:"^" :COST.MT.SALE.DET5:"^" :COST.MT.SALE.DET6

	STATUS = RBO.setProperty('','CostTypeSummInfo',FinalCostTypeResult)

* End of method code
 93000*
   STATUS = RBO.setProperty('','ServerStatus',1)
   STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
   RETURN
