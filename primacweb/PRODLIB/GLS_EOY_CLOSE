SUBROUTINE GLS_EOY_CLOSE
********************************************************************************
*   Program name :- GLS_EOY_CLOSE
*   Created:- 9/15/2003
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
*** INSERT FILES EQUATES   
$INCLUDE PMC.CPYLIB COMPANY   
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE PMC.CPYLIB FISCAL
$INCLUDE PMC.CPYLIB GLTABLE
$INCLUDE PMC.CPYLIB COA
$INCLUDE GLS.CPYLIB GLM
$INCLUDE GLS.CPYLIB COA.BAL
$INCLUDE JCS.CPYLIB JOB
* Insert method code here

ERRMSG = ''
OPEN '','COMPANY' TO COMPANY ELSE
	ERRMSG = 'CANNOT LOCATE COMPANY FILE'; GOTO 99999
END
OPEN '','GLS.SCREENS' TO M.SCREENS ELSE
	ERRMSG = 'CANNOT LOCATE APS.SCREENS FILE'; GOTO 99999
END
OPEN '','PREFIX' TO PREFIX ELSE
  	ERRMSG = 'CANNOT LOCATE PREFIX FILE'; GOTO 99999
END
OPEN '','CONTROL' TO CONTROL ELSE
	ERRMSG = 'CANNOT LOCATE CONTROL FILE'; GOTO 99999
END
OPEN '','CUSTOMER' TO CUSTOMER ELSE
	ERRMSG = 'CANNOT LOCATE CUSTOMER'; GOTO 99999
END
OPEN '','COA' TO COA ELSE
  	ERRMSG = 'CANNOT LOCATE COA FILE'; GOTO 99999
END
OPEN '','CO.COA.BAL' TO CO.COA.BAL ELSE
	ERRMSG = 'CANNOT LOCATE CO.COA.BAL FILE'; GOTO 99999
END
OPEN '','DV.COA.BAL' TO DV.COA.BAL ELSE
	ERRMSG = 'CANNOT LOCATE DV.COA.BAL FILE'; GOTO 99999
END
OPEN '','DP.COA.BAL' TO DP.COA.BAL ELSE
	ERRMSG = 'CANNOT LOCATE DP.COA.BAL FILE'; GOTO 99999
END
OPEN '','CC.COA.BAL' TO CC.COA.BAL ELSE
	ERRMSG = 'CANNOT LOCATE CC.COA.BAL FILE'; GOTO 99999
END
OPEN '','JOB' TO JOB ELSE 
	ERRMSG = 'CANNOT LOCATE JOB FILE'; GOTO 99999
END

VAL = RBO.getProperty('','ID',ID)
CONO = ID[1,3]

READ PROCESS.CONTROL FROM CONTROL,CONO:'GLS.BACKUP.RESTORE' THEN
	IF PROCESS.CONTROL = 'R' THEN
		ERRMSG = 'GENERAL LEDGER RESTORE IN PROCESS'
		GOTO 99999
	END ELSE IF PROCESS.CONTROL = 'B' THEN
		ERRMSG = 'GENERAL LEDGER BACKUP IN PROCESS'
		GOTO 99999
	END
END

MATREAD COMP.REC FROM COMPANY,CONO ELSE
	MAT COMP.REC = ''
END
IF CO.GLS.HIST<1,1> = "Y" THEN
	OPEN '','GLH' TO GLH ELSE
    		ERRMSG = 'CANNOT LOCATE GLH FILE'; GOTO 99999
  	END
  	OPEN '','GLHX' TO GLHX ELSE
    		ERRMSG = 'CANNOT LOCATE GLHX FILE'; GOTO 99999
  	END
END

*--- MAIN PROCESS
*
READ PERIOD.REC FROM CONTROL, CONO : "ACCT.PERIODS" ELSE
	PERIOD.REC = ""
  	PERIOD.REC<1> = 12
END
NUM.PERIODS = PERIOD.REC<1>

MATREAD FISCAL.REC FROM CONTROL, CONO : "FISCALMON" ELSE
  	ERRMSG = "YOU NEED TO CLOSE PERIOD ":NUM.PERIODS:" BEFORE CLOSING THE YEAR"
  	GOTO 99999
END

IF FR.CURR.PER[5,2] <> 01 THEN
	ERRMSG = 'STARTING PERIOD FOR A NEW YEAR SHOULD BE 01'
  	GOTO 99999
END

READ SALESDATES FROM CONTROL , CONO : "SALESDATES" ELSE
	ERRMSG = "CANNOT LOCATE CONTROL, SALESDATES"
  	GOTO 99999
END

READ OPENDATES FROM CONTROL , CONO : "OPENDATES" ELSE
	ERRMSG = "CANNOT LOCATE CONTROL, OPENDATES"
  	GOTO 99999
END

MATREAD GLTABLE.REC FROM CONTROL, CONO : "GLTABLE" ELSE
	ERRMSG = "CANNOT LOCATE CONTROL, GLTABLE"
  	GOTO 99999
END

MATREAD COA.REC FROM COA,  CONO : GLTB.RET.EARN ELSE
	ERRMSG = "CANNOT LOCATE RETAINED EARNINGS ACCOUNT, ":GLTB.RET.EARN
  	GOTO 99999
END

IF COA.TYPE # "O" THEN
	ERRMSG = "RETAINED EARNINGS ACCOUNT TYPE IS INVALID"
  	GOTO 99999
END

R.E.LEVEL = COA.LEVEL

BEGIN CASE
	CASE R.E.LEVEL > 2
    		R.E.LEN = 10
    		R.E.SUF = ""
  	CASE R.E.LEVEL > 1
    		R.E.LEN = 7
    		R.E.SUF = "000"
  	CASE R.E.LEVEL > 0
    		R.E.LEN = 5
    		R.E.SUF = "00000"
  	CASE 1
    		R.E.LEN = 3
    		R.E.SUF = "0000000"
END CASE

R.E.PREFIX = ""
R.E.AMT = ""

FOR PER = 1 TO NUM.PERIODS
	OPENDATES<PER> = OCONV(OPENDATES<PER>,"D/")
  	NEXT.YEAR = OPENDATES<PER>[7,4] + 1
  	OPENDATES<PER> = OPENDATES<PER>[1,6] : NEXT.YEAR
 	OPENDATES<PER> = ICONV(OPENDATES<PER>,"D/")
NEXT PER

VAL = RBO.setProperty('','Per',FR.CURR.PER[5,2])
VAL = RBO.setProperty('','Mon',SALESDATES<2,2>)
VAL = RBO.setProperty('','date',OCONV(OPENDATES<2>-1,'D2/'))
VAL = RBO.setProperty('','period',NUM.PERIODS)
VAL = RBO.setProperty('','EndDate',SALESDATES<NUM.PERIODS+1,2>)

TAPE.PER.ID = CONO : FR.CURR.PER[1,4] - 1 : "GLS.TAPE"
READ TAPE.PERIOD FROM CONTROL, TAPE.PER.ID ELSE TAPE.PERIOD = ''
IF TAPE.PERIOD<NUM.PERIODS> = '' THEN
	VAL = RBO.setProperty('','Status',"ENTERED")
	VAL = RBO.setProperty('','Dates',DATE())
  	NO.CLOSE = 1
END ELSE
	VAL = RBO.setProperty('','Status',"CLOSED")
	VAL = RBO.setProperty('','Dates',OCONV(TAPE.PERIOD<NUM.PERIODS>,'D2/'))
	NO.CLOSE = 0
END
VAL = RBO.setProperty('','NO_CLOSE',NO.CLOSE)
VAL = RBO.setProperty('','R_E_LEVEL',R.E.LEVEL)
VAL = RBO.setProperty('','R_E_LEN',R.E.LEN)
VAL = RBO.setProperty('','R_E_SUF',R.E.SUF)

99999
IF ERRMSG # '' THEN
	VAL = RBO.setProperty('','ServerStatus','E')
	VAL = RBO.setProperty('','ServerMessage',ERRMSG)
END ELSE
	VAL = RBO.setProperty('','ServerStatus','')
END
* End of method code
RETURN

