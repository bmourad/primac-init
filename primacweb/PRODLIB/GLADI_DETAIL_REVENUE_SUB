SUBROUTINE GLADI_DETAIL_REVENUE_SUB
********************************************************************************
*   Program name :- GLADI_DETAIL_REVENUE_SUB
*   Created:- 1/10/2003
*------------------------------------------------------------------------------
*
*
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB COA
$INCLUDE PMC.CPYLIB SALESMAN
$INCLUDE PMC.CPYLIB SALES.CODE
$INCLUDE PMC.CPYLIB TAX
$INCLUDE JCS.CPYLIB JOB
$INCLUDE PMC.CPYLIB SHIP.VIA
$INCLUDE PMC.CPYLIB>GLTABLE
$INCLUDE CPYLIB>CHAR

* Open files
ERRMSG = ''
OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"
OPEN "SALES.CODE" TO SALES.CODE ELSE ERRMSG = "SALES.CODE FILE IS MISSING"
OPEN "CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING"
OPEN "COA" TO COA ELSE ERRMSG = "COA FILE IS MISSING"
OPEN "TAX" TO TAX ELSE ERRMSG = "TAX FILE IS MISSING"
OPEN "JOB" TO JOB ELSE ERRMSG = "JOB FILE IS MISSING"
OPEN "SHIP.VIA" TO SHIP.VIA ELSE ERRMSG = "SHIP.VIA FILE IS MISSING"
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG)
  RETURN
END

* Get properties
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','DetailLineNumber',LN)
STATUS = RBO.getProperty('','SourceFlag',SRC.FLAG)
STATUS = RBO.getProperty('','DetailChargeCode',CHG.CODE)
STATUS = RBO.getProperty('','DetailSalesCode',SALESCD)
STATUS = RBO.getProperty('','DetailJobNum',JOB.NO)
STATUS = RBO.getProperty('','DetailTaxJurs',TAX.JURS)
STATUS = RBO.getProperty('','DetailDivDeptCCtr',DVDPCC)
STATUS = RBO.getProperty('','DetailAmount',AMT)

* Get CONO
CONO = ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
  ERRMSG = 'Invalid Company ID (':CONO:').'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Read GLTABLE record
MATREAD GLTABLE.REC FROM CONTROL, CONO:'GLTABLE' ELSE
  ERRMSG = "CANNOT LOCATE CONTROL, GLTABLE"
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Verify sales code
IF SRC.FLAG # "MJ" THEN
  MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE MAT JOB.REC = ''
  SALESCD = JOB.SALES.CODE
END

* Find the GL account
BEGIN CASE
  CASE CHG.CODE<1,LN> = "MSC" OR CHG.CODE<1,LN> = "DSC"
    MATREAD SLC.REC FROM SALES.CODE, CONO:TAX.JURS<1,LN> ELSE MAT SLC.REC = ''
    GLACCT = SLC.GL.ACCT<1>
  CASE CHG.CODE<1,LN> = "TAX"
    MATREAD TAX.REC FROM TAX,CONO:TAX.JURS<1,LN> ELSE MAT TAX.REC = ''
    GLACCT = TAX.ACCT<1>
  CASE CHG.CODE<1,LN> = "DSC"
    MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:TAX.JURS<1,LN> ELSE SHPV.REC = ''
    GLACCT = SHPV.GL.NO
  CASE 1
    IF SALESCD = "" THEN
      GLACCT = GLTB.SALES
    END ELSE
      MATREAD SLC.REC FROM SALES.CODE,CONO:SALESCD ELSE MAT SLC.REC = ''
      GLACCT = SLC.GL.ACCT<1>
    END
END CASE

* Get GL account desc
MATREAD COA.REC FROM COA, CONO : GLACCT ELSE MAT COA.REC = ""
GLDESC = COA.DESC

* Format data
GLAccounts = ''
GLDescs    = ''
Divs       = ''
Depts      = ''
CCtrs      = ''
Amounts    = ''
FOR IDX = 1 TO DCOUNT(AMT<1,LN>,@SVM)
  GLAccounts<1,IDX> = GLACCT
  GLDescs<1,IDX> = GLDESC
  Divs<1,IDX> = DVDPCC<1,LN,IDX>[1,2]
  Depts<1,IDX> = DVDPCC<1,LN,IDX>[3,2]
  CCtrs<1,IDX> = DVDPCC<1,LN,IDX>[5,3]
  *Amounts<1,IDX> = OCONV(AMT<1,LN,IDX>,"MD2")
  Amounts<1,IDX> = AMT<1,LN,IDX>
NEXT IDX

* Set properties
STATUS = RBO.setProperty('','GLAccounts',GLAccounts)
STATUS = RBO.setProperty('','GLDescs',GLDescs)
STATUS = RBO.setProperty('','Divs',Divs)
STATUS = RBO.setProperty('','Depts',Depts)
STATUS = RBO.setProperty('','CCtrs',CCtrs)
STATUS = RBO.setProperty('','Amounts',Amounts)

* End of method code
RETURN

