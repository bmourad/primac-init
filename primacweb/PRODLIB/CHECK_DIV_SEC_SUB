SUBROUTINE CHECK_DIV_SEC_SUB
*********************************************************************
* Copyright 1998 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* PROGRAM      - CHECK.DIVISION.ACCESS
* BY           - ABED ALI
* DATE         - 05/27/2003
* DESCRIPTION  - This subroutine validates user access for a specific division.
*                If the SEC.DIVISION attribute is null, the user has
*                unrestricted access (i.e., can process ALL divisions).
*                This version of the subroutine is for programs using SCRN.EDIT.
*                Use SI.CK.DIV.SEC.SUB for programs using SCREEN.INPUT.
*ENDDOC
*********************************************************************
* In Properties:
* --------------
* Cono,USER_ID,DIV_CODE

* Out Properties:
* ---------------
*  AUTH_FLAG,AUTH_MSG

*T502 09/05/2005  TestTrack Redback
****************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB SECURITY

**************
* OPEN FILES *
**************
  ERRMSG = ""
  OPEN '','CONTROL' TO CONTROL ELSE
     ERRMSG = "CONTROL FILE IS MISSING"
     GOTO 99999
  END
  OPEN '','SECURITY' TO SECURITY ELSE
     ERRMSG = "SECURITY FILE IS MISSING"
     GOTO 99999
  END
**************
* INITIALIZE *
**************
*
  STATUS = 0
  STATUS = RBO.getProperty('','PMCProperty',PMCProperty)
  STATUS = RBO.getProperty('','DivCode',DIV.CODE)

  CONO = PMCProperty<1,4>	
  USER.ID = PMCProperty<1,3>
  USER.ID = UPCASE(USER.ID)

  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE GOTO 99999
  IF SECURITY.REC<1> # "Y" THEN GOTO 99999
  MATREAD SEC.REC FROM SECURITY, CONO:USER.ID ELSE
     ERRMSG = "USER ":USER.ID:" NOT ON FILE"
     GOTO 99999
  END
*
  IF SEC.DIVISION = "" THEN
     IF DIV.CODE = "00" AND SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
        ERRMSG = "DIV '00' INVALID WHEN DIVISION CLOSING IS ENABLED"
        GOTO 99999
     END ELSE
        GOTO 99999
     END
  END
*
  LOCATE DIV.CODE IN SEC.DIVISION<1>,1 SETTING FOUND THEN GOTO 99999
  ELSE
     ERRMSG = "USER ":USER.ID:" NOT AUTHORIZED FOR DIVISION ":DIV.CODE
  END
*
99999 
  IF (TRIM(ERRMSG) # "") THEN
*T502       STATUS = RBO.setProperty('','ServerStatus',"E")
      STATUS = RBO.setProperty('','ServerStatus',"1")
      STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END
RETURN

