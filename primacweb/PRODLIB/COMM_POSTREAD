  SUBROUTINE COMM_POSTREAD
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - COMM_POSTREAD
*
* AUTHOR   - ALEJANDRO DELGADO
*
* DATE     - 01/30/2003
*
* DESCRIPTION
*
* Data is retrieve from the COMMISSION file to load display through RBO.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
  $INCLUDE WWINSERT RBO.H
  $INCLUDE JCS.CPYLIB COMMISSION
  $INCLUDE JCS.CPYLIB JOB
  $INCLUDE CPYLIB FILE.VARS
  $INCLUDE CPYLIB CHAR
*
*---- INITIALIZATION
*
  STATUS = RBO.getProperty('','ID',ID)
  STATUS = RBO.getProperty('','COMM_JOBS',COMM.JOBS)
  STATUS = RBO.getProperty('','PMCProperty',PMCProperty)
  USER.ID=PMCProperty<1,3>
  CONO = ID[1,3] ; SLSM.NO = TRIM(ID[5,99])
*
*---- OPEN FILES
*
  IF FILEINFO(COMMISSION,0) = 0 THEN
    OPEN '','COMMISSION' TO COMMISSION ELSE
      ERRMSG = 'Cannot open COMMISSION file!'
      GOTO SET.ERROR
    END
  END
  IF FILEINFO(JOB,0) = 0 THEN
    OPEN '','JOB' TO JOB ELSE
      ERRMSG = 'Cannot open JOB file!'
      GOTO SET.ERROR
    END
  END
*
*---- MAIN PROCESS
*
  COM.JOB = '' ; COM.INV = ''
  COM.DATE = '' ; COM.BILLED = ''
  COM.LAST.PMT = '' ; COM.BAL.DUE = ''
  COM.DUE = '' ; COM.PAID = ''
  J.CNT = DCOUNT(COMM.JOBS, VM) ; POS = 1
  FOR JJ = 1 TO J.CNT
    MATREAD JOB.REC FROM JOB,CONO:COMM.JOBS<1,JJ> ELSE MAT JOB.REC = ""
    IN_PARAM = ''
    IN_PARAM<1,1> = CONO
    IN_PARAM<1,2> = UPCASE(USER.ID)
    IN_PARAM<1,3> = JOB.DIV
    ERRMSG=''; OUT_PARAM=''
    CALL DivPosSecSub(ERRMSG,IN_PARAM,OUT_PARAM)
    
  IF ERRMSG#'' THEN CONTINUE
    BEGIN CASE
      CASE JOB.STATUS<1,1> = "9" ; CONTINUE
      CASE JOB.STATUS<1,1> = "8" ; CONTINUE
    END CASE
    COM.JOB<1,POS> = COMM.JOBS<1,JJ>
    POS += 1
  NEXT JJ
*ERRMSG = 'COM JOBS ' : COM.JOB<1,POS> : ' COMM JOBS ' : COMM.JOBS<1,JJ>
*GOTO SET.ERROR
  *
  J.CNT = DCOUNT(COM.JOB, VM)
  FOR JJ = 1 TO J.CNT
    MATREAD COMM.REC FROM COMMISSION,CONO:COM.JOB<1,JJ> ELSE MAT COMM.REC = ""
    I.CNT = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "")
    COM.INV<1,JJ> = COMM.INV.NO<1,I.CNT>
    COM.DATE<1,JJ> = OCONV(COMM.INV.LAST.DATE, 'D4/')
    COM.BILLED<1,JJ> = OCONV(COMM.INV.TOT.AMT, 'MD2')
    COM.LAST.PMT<1,JJ> = OCONV(COMM.LAST.PMT.DATE, 'D4/')
    COM.BAL.DUE<1,JJ> = OCONV(COMM.INV.TOT.BAL, 'MD2')
    COM.DUE<1,JJ> = OCONV((COMM.CALC + COMM.ADJ), 'MD2')
    *COM.DUE<1,JJ> = COMM.CALC + COMM.ADJ
    COM.PAID<1,JJ> = OCONV(COMM.PAID,'MD2')
  NEXT JJ
  *
  STATUS = RBO.setProperty('','COM_JOB',COM.JOB)
  STATUS = RBO.setProperty('','COM_INV',COM.INV)
  STATUS = RBO.setProperty('','COM_DATE',COM.DATE)
  STATUS = RBO.setProperty('','COM_BILLED',COM.BILLED)
  STATUS = RBO.setProperty('','COM_LAST_PMT',COM.LAST.PMT)
  STATUS = RBO.setProperty('','COM_BAL_DUE',COM.BAL.DUE)
  STATUS = RBO.setProperty('','COM_DUE',COM.DUE)
  STATUS = RBO.setProperty('','COM_PAID',COM.PAID)
  RETURN
*
SET.ERROR: 
*
  STATUS = RBO.setProperty('','ServerStatus',1)
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  RETURN
END
