SUBROUTINE JCSJFP_JOB_PURGE(BUFFER,ERR)
********************************************************************************
*   Program name :- JCSJFP_JOB_PURGE
*   Created:- 8/18/2006
*   Programmer :- S.Suhail Hussain
*   DESCRIPTION - THIS PROGRAM UPDATE JOB FILE WITH STATUS = '7' READY TO BE PURGED.
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JES.CPYLIB ESTIMATE
$INCLUDE CPYLIB FILE.VARS
$INCLUDE CPYLIB CHAR

*** OPEN ALL FILES
   OPEN 'COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING' ; GOTO 93000
   OPEN 'CUSTOMER' TO CUSTOMER ELSE ERRMSG = 'CUSTOMER FILE IS MISSING' ; GOTO 93000
   OPEN 'JOB' TO JOB ELSE ERRMSG = 'JOB FILE IS MISSING' ; GOTO 93000
   OPEN 'CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING' ; GOTO 93000
   
   CONO = BUFFER<1>
   DIV.CODE = BUFFER<3>
   NO.DATE  = BUFFER<4>
   ERR = ""

   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = 'COMPANY RECORD IS MISSING' ; GOTO 93000
   END
   IF CO.JES = "Y" THEN
      OPEN 'ESTIMATE' TO ESTIMATE ELSE ERRMSG = 'ESTIMATE FILE IS MISSING' ; GOTO 93000
   END
10*
   WRITEV NO.DATE ON CONTROL, CONO:"TOT.DATES",1
   CNT = 0
   DATA = 0
   LOOP
      READNEXT ID ELSE DATA = 1
   WHILE DATA = 0 DO
      MATREADU JOB.REC FROM JOB ,ID ELSE GOTO 111
      CNT = CNT + 1
      FND = 1
      CFND = 1
      JOB.NO = ID[4,99]
      EFND = 0
      IF JOB.EST # "" AND CO.JES = "Y" THEN
         EFND = 1
         MATREAD EST.REC FROM ESTIMATE,CONO:JOB.EST ELSE EFND = 0
      END
      MATREADU CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE
         RELEASE CUSTOMER,CONO:JOB.CUST
         FND = 0
         CFND = 0
      END
      IF JOB.STATUS<1,1> = 7 THEN
         IF JOB.TOT.BAL + 0 = 0 THEN
            INV.CNT = COUNT(JOB.INV.NO, VM) + (JOB.INV.NO # "")
            IF INV.CNT + 0 > 0 THEN
               BEGIN CASE
                  CASE JOB.INV.DATE<1,INV.CNT> # ""
                     INV.DATE = JOB.INV.DATE<1,INV.CNT>
                  CASE JOB.TRACK.DATE<1,9> # ""
                     INV.DATE = JOB.TRACK.DATE<1,9>
                  CASE JOB.TRACK.DATE<1,10> # ""
                     INV.DATE = JOB.TRACK.DATE<1,10>
                  CASE 1
                     GOTO 111
               END CASE
               IF (DATE() - INV.DATE) > NO.DATE THEN
                  JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"8")
                  JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
                  GOSUB 1000
               END
            END ELSE ; * Job not invoiced
               STATS.UPD = 1
               IF ID[4,1] = "X" THEN
                  READ DIV.NUMS FROM CONTROL,CONO:"DIVISIONS" ELSE NULL
                  DFND = 0
                  LOCATE ID[9,2] IN DIV.NUMS<1>,1 SETTING DFND ELSE DFND = 0
                  IF DFND THEN
                     IF ID[11,1] = 1 OR ID[11,1] = 2 OR ID[11,1] = 3 OR ID[11,1] = 4 OR ID[11,1] = 5 THEN
                        IF JOB.TYPE = "N" AND JOB.TRACK.DATE<1,10> # "" THEN
                           IF (DATE() - JOB.TRACK.DATE<1,10>) > NO.DATE THEN
                              JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"8")
                              JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
                              GOSUB 1000
                              STATS.UPD = 0
                           END
                        END
                     END
                  END
               END
               IF STATS.UPD THEN; * Incomplete X-Job or Non-Invoiced reg job
                  FND = 0
                  LOCATE "9" IN JOB.STATUS<1>,1 SETTING FND ELSE FND = 0
                  IF FND THEN ; * Cancelled job
                     XX=FND
                     IF (DATE() - JOB.STAT.DATE<1,XX>) > NO.DATE THEN
                        JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"8")
                        JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
                        GOSUB 1000
                     END
                  END ELSE
                     IF JOB.TRACK.DATE<1,10> # "" THEN
                        IF (DATE() - JOB.TRACK.DATE<1,10>) > NO.DATE THEN
                           JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"8")
                           JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
                           GOSUB 1000
                        END
                     END
                  END
               END
            END
         END
      END
111*
      RELEASE JOB, ID
      RELEASE CUSTOMER, CONO:JOB.CUST
   REPEAT
   IF CNT = 0 THEN
      BUFFER<1> = "END"
      DELETE CONTROL, CONO:'JOB.PURGE'
   END ELSE
      WRITEV DIV.CODE ON CONTROL, CONO:'JOB.PURGE',1
   END
RETURN
*** WRITE BACK JOB RECORD
1000*
   IF EFND THEN
      EST.JOB.PURGE.DATE = DATE()
      MATWRITE EST.REC ON ESTIMATE,CONO:JOB.EST
   END
   IF CFND THEN
      LOCATE JOB.NO IN CUST.JOB<1>,1 SETTING JFND ELSE JFND = 0
      IF JFND # 0 THEN
         CUST.JOB = DELETE(CUST.JOB,1,JFND,0)
         CUST.JOB.BAL = DELETE(CUST.JOB.BAL,1,JFND,0)
      END
      MATWRITE CUST.REC ON CUSTOMER,CONO:JOB.CUST
   END
   MATWRITE JOB.REC ON JOB , ID
RETURN

93000*
   BUFFER<1> = "END"
   ERR = 1
RETURN

