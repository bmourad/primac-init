SUBROUTINE APCREM_GET_FISCAL_PERIOD
********************************************************************************
*   Program name :- APCREM_GET_FISCAL_PERIOD
*   Created:- 5/28/2003
*------------------------------------------------------------------------------*
* THIS FUNCTION WILL DOES BOTH DIVISION LEVEL SECURITY AND GETS THE DIV POSITION FROM CONTROL
* , GETS FISCAL MONTH AND PERIOD.

* In Properties:
* --------------
* ID,APCK_DIV  
*
* Out Properties:
* ---------------
*  FISCAL_PERIOD,DIV_POS
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB SECURITY
$INCLUDE PMC.CPYLIB FISCAL
* Insert method code here

  ERRMSG = ""
  OPEN '','CONTROL' TO CONTROL ELSE
     ERRMSG = "CONTROL FILE IS MISSING"
     GOTO 99999
  END
  OPEN '','SECURITY' TO SECURITY ELSE
     ERRMSG = "SECURITY FILE IS MISSING"
     GOTO 99999
  END

  STATUS = RBO.getProperty('','ID',ID)
  CONO = ID[1,3]
  STATUS = RBO.getProperty('','APCK_DIV',DIV.CODE)

  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE GOTO 99999
  READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE GOTO 99999
  
  IF DIV.CODE # "" THEN
  	IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
	    IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
	      ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
	      GOTO 99999
	    END
	    LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING POS ELSE 
	      ERRMSG = "Division ":DIV.CODE:" not found in Control File DIVISIONS Record."
	      GOTO 99999
	    END
	  END ELSE
	    POS = 1
	  END
  END ELSE
	POS = 1
  END


  MATREAD FISCAL.REC FROM CONTROL,CONO:'APVFISCAL' ELSE
    ERRMSG = "APVFISCAL CONTROL RECORD IS MISSING"; GOTO 99999
  END
  MON = FR.CURR.PER<1,POS>[5,2]
  
  READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
    PERIOD.REC = 12
  END
  NUM.PERIODS = PERIOD.REC<1>

  IF MON < 1 OR MON > NUM.PERIODS THEN
    ERRMSG = "ERROR IN CONTROL RECORD 'APVFISCAL'"; GOTO 99999
  END
  STATUS = RBO.setProperty('','FISCAL_PERIOD',MON)
  STATUS = RBO.setProperty('','FR_CURR_PERIOD',FR.CURR.PER<1,POS>)

  STATUS = RBO.setProperty('','DIV_POS',POS)

* End of method code
99999
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','AUTH_FLAG',"Un")
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END
RETURN

