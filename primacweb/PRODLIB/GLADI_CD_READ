SUBROUTINE GLADI_CD_READ
********************************************************************************
*   Program name :- GLADI_CD_READ
*   Created:- 1/9/2003
*   By:- Abdullah Jibaly
*   See:- GLSBP APS.CHECK.DET.INQ
*------------------------------------------------------------------------------
*
* In Properties:
* --------------
* ID
*
* Out Properties:
* ---------------
* ALL THE MCD_ VALUES IN ADDITION TO VendorDescription
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE APS.CPYLIB MCD
$INCLUDE PMC.CPYLIB VEND

* Open files
ERRMSG = ''
OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"
OPEN "MCD" TO MCD ELSE ERRMSG = "MCD FILE IS MISSING"
OPEN "VEND" TO VEND ELSE ERRMSG = "VEND FILE IS MISSING"
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG)
  RETURN
END

* Get properties
STATUS = RBO.getProperty('','ID',ID)

* Get CONO
CONO = ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
  ERRMSG = 'Invalid Company ID (':CONO:').'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Read MCD record
MCD.ID = ID
MCD.ID[1,3] = ''
MAT MCD.REC = ''
MATREAD MCD.REC FROM MCD,CONO:MCD.ID ELSE
  MATREAD MCD.REC FROM MCD,CONO:'C':MCD.ID[2,99] ELSE
    MATREAD MCD.REC FROM MCD,CONO:'H':MCD.ID[2,99] ELSE NULL
  END
END

* Get vendor description
MATREAD VEND.REC FROM VEND,CONO:MCD.VEND ELSE MAT VEND.REC = ''

* Format data
FOR IDX = 1 TO DCOUNT(MCD.VOUCH,@VM)
  MCD.VOUCH.GRS<1,IDX> = OCONV(MCD.VOUCH.GRS<1,IDX>,"MD2")
  MCD.VOUCH.DSC<1,IDX> = OCONV(MCD.VOUCH.DSC<1,IDX>,"MD2")
  MCD.VOUCH.NET<1,IDX> = OCONV(MCD.VOUCH.NET<1,IDX>,"MD2")
NEXT IDX

* Set output properties
STATUS = RBO.setProperty('','MCD_VEND',MCD.VEND)
STATUS = RBO.setProperty('','VendorDescription',VEND.DESC)
STATUS = RBO.setProperty('','MCD_CHK_DATE',OCONV(MCD.CHK.DATE,"D2/"))
STATUS = RBO.setProperty('','MCD_GRS_AMT',OCONV(MCD.GRS.AMT,"MD2"))
STATUS = RBO.setProperty('','MCD_DSC_AMT',OCONV(MCD.DSC.AMT,"MD2"))
STATUS = RBO.setProperty('','MCD_NET_AMT',OCONV(MCD.NET.AMT,"MD2"))
STATUS = RBO.setProperty('','MCD_MON',MCD.MON)
STATUS = RBO.setProperty('','MCD_VOUCH',MCD.VOUCH)
STATUS = RBO.setProperty('','MCD_VOUCH_GRS',MCD.VOUCH.GRS)
STATUS = RBO.setProperty('','MCD_VOUCH_DSC',MCD.VOUCH.DSC)
STATUS = RBO.setProperty('','MCD_VOUCH_NET',MCD.VOUCH.NET)
STATUS = RBO.setProperty('','MCD_DIV',MCD.DIV)
STATUS = RBO.setProperty('','MCD_DEPT',MCD.DEPT)
STATUS = RBO.setProperty('','MCD_CCTR',MCD.CCTR)

* End of method code
RETURN

