SUBROUTINE INVREGM_POSTREAD
********************************************************************************
*   Program name :- INVREGM_POSTREAD
*   Created:- 11/15/2002
*------------------------------------------------------------------------------*
*
*  This server event is triggered from within the {m:WW:uObject=ReadData}, {m- *
*  :WW:uObject=DeleteData} and {m:WW:uObject=WriteData} server events. In eac- *
*  h case, this {m:WW:uObject=PostRead} event occurs after the physical datab- *
*  ase read, but before values are extracted from the database record. This p- *
*  rovides a window of opportunity in which the database values may be direct- *
*  ly manipulated. The API functions
*  RBO.setDBVals() and RBO.getDBVals() are - *
*  used to do this.
*  
*                - *
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE CPYLIB CHAR
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE ICS.CPYLIB INV.WHSE.LOC
$INCLUDE ICS.CPYLIB INV.STATS
$INCLUDE ICS.CPYLIB CATEGORY

;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(CATEGORY,0)=0 THEN
  OPEN '','CATEGORY' TO CATEGORY ELSE
    ERRMSG<1,-1>='CATEGORY FILE IS MISSING'
   END
END
IF FILEINFO(INV.WHSE,0)=0 THEN
  OPEN '','INV.WHSE' TO INV.WHSE ELSE
    ERRMSG<1,-1>='INV.WHSE FILE IS MISSING'
  END
END
IF FILEINFO(INV.WHSE.LOC,0)=0 THEN
  OPEN '','INV.WHSE.LOC' TO INV.WHSE.LOC ELSE
    ERRMSG<1,-1>='INV.WHSE.LOC FILE IS MISSING'
  END
END
IF FILEINFO(INV.STATS,0)=0 THEN
  OPEN '','INV.STATS' TO INV.STATS ELSE
    ERRMSG<1,-1>='INV.STATS FILE IS MISSING'
  END
END
*IF ERRMSG#'' THEN GOSUB 93000
IF ERRMSG#'' THEN GOTO 93000

;* initialize

ROF='' ;*read only fields

;* get ID

STATUS=RBO.getProperty('','ID',INV.ID)
CONO=INV.ID[1,3]
PDNO=INV.ID[4,99]

;* get database values

NAMES='' ; VALUES=''
NAMES<1>='INV_PAP_TYPE'
NAMES<2>='INV_WHSE_CODE'
NAMES<3>='INV_LINE'
NAMES<4>='INV_M_LINE'
STATUS=RBO.getDBVals(NAMES,VALUES)
INV.PAP.TYPE=VALUES<1>
INV.WHSE.CODE=VALUES<2>
INV.LINE=VALUES<3>
INV.M.LINE=VALUES<4>
IF INV.PAP.TYPE#'REGULAR' OR INV.M.LINE='FNGD' THEN
  ERRMSG='This is not a regular product'
  GOTO 93000
END
GOSUB SetCatgVars
GOSUB GetQtyOnhand  
GOSUB CheckCustomerQtys
GOSUB SetReadOnlyFields
  
GOTO 99999

*******************************************************************************
*
**************
SetCatgVars:
**************
*
CATG.ID=CONO:INV.LINE
MATREAD CATG.REC FROM CATEGORY,CATG.ID THEN
  STATUS=RBO.setProperty('','CatgType',CATG.TYPE)
  STATUS=RBO.setProperty('','CatgMajLine',CATG.MAJ.LINE)
END ELSE
  ERRMSG<1,-1>='Category is not on file'
  GOTO 93000
END
RETURN
*
**************
GetQtyOnhand:
**************
*
PO.QTY=""
QTY.FLAG=0
WCNT = DCOUNT(INV.WHSE.CODE,VM) 
   
FOR A = WCNT TO 1 STEP -1
    MATREAD IWH.REC FROM INV.WHSE, CONO:PDNO:"!":INV.WHSE.CODE<1,A> THEN
   
       MATREAD INV.STAT.REC FROM INV.STATS, CONO:PDNO:"!":INV.WHSE.CODE<1,A> THEN
           PO.QTY = SUM(ISTAT.PO.QTY)
*T28266 v
*   IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND ISTAT.JOB = "" THEN
            IF PO.QTY * 1 = 0 AND IWH.RECP.NO = '' AND ISTAT.JOB = "" THEN
              LOC.CNT = COUNT(IWH.LOC,VM) + (IWH.LOC # "")
                FOR DL = LOC.CNT TO 1 STEP -1
                  MATREAD IWLO.REC FROM INV.WHSE.LOC,CONO:PDNO:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL> ELSE MAT IWLO.REC = "" 
                    IF IWLO.LOC.ON.HAND # "" AND IWLO.LOC.ON.HAND * 0 # 0 THEN QTY.FLAG = 1
                NEXT DL
           END ELSE
              QTY.FLAG = 1
           END
       END ELSE
         MAT INV.STAT.REC = ""
       END

   END ELSE
      MAT IWH.REC = ""
   END
NEXT A                        
  STATUS=RBO.setProperty('','QTYFLAG',QTY.FLAG)                                      
RETURN
*
*******************
CheckCustomerQtys:
*******************
*
CUST.QTY.FLAG = ''                                                     
IF INV.CUST # '' THEN                                                  
  WHSE.CNT = DCOUNT(INV.WHSE.CODE,VM)           
  IF WHSE.CNT = 0 THEN RETURN                                         
  FOR A = WHSE.CNT TO 1 STEP -1 
    IWH.ID= CONO:PDNO:"!":INV.WHSE.CODE<1,A>             
    MATREAD IWH.REC FROM INV.WHSE,IWH.ID ELSE MAT IWH.REC=''                 
    MATREAD INV.STAT.REC FROM INV.STATS, IWH.ID ELSE MAT INV.STAT.REC=''                                             
    PO.QTY = SUM(ISTAT.PO.QTY)                                       
    IF PO.QTY # 0 AND PO.QTY # '' THEN
      CUST.QTY.FLAG=1
      EXIT
    END             
    PJQ = 0                                                          
    PJQ.CNT = DCOUNT(ISTAT.PO,VM)                                    
    FOR PJQ.POS = 1 TO PJQ.CNT                                       
      PJQ += SUM(ISTAT.PO.JOB.QTY<1,PJQ.POS>)                       
    NEXT PJQ.POS                                                     
    IF PJQ # 0 AND PJQ # '' THEN
      CUST.QTY.FLAG = 1                   
      EXIT
    END
    IF IWH.RESV+0 # 0  THEN
      CUST.QTY.FLAG = 1          
      EXIT
    END
    IF IWH.INPRCS # 0 AND IWH.INPRCS # '' THEN
      CUST.QTY.FLAG = 1                                                            
      EXIT
    END
  NEXT A                                                              
END  
STATUS=RBO.setProperty('','CUSTQTYFLAG',CUST.QTY.FLAG)                                                             
RETURN
*
******************
SetReadOnlyFields:
******************
*
IF (QTY.FLAG) THEN
  ROF<1,-1>="INV_LINE"
END
IF CUST.QTY.FLAG=1 THEN
  ROF<1,-1>='INV_CUST'  
END
IF INV.PAP.TYPE='I' THEN
  ROF<1,-1>='INV_PAP_LEN'
  ROF<1,-1>='INV_PAP_WIDTH'
END
*STATUS=RBO.setProperty('','ReadOnlyFields',"FSDFSD":ROF)
RETURN

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

RETURN
99999
RETURN


