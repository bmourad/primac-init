SUBROUTINE CUST_DELETE_PURGED_INV
********************************************************************************
*   Program name :- CUST_DELETE_PURGED_INV
*   Created:- 1/6/2006
*   Programmer :- Suhail Hussain S
*
* DESCRIPTION :- 
*          This program deletes PURGED.OPEN.RECV records and cross references to
*          those records in the appropriate PURGED.OR.XREF records.
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ARS.CPYLIB PURGED.OPEN.RECV

   OPEN '','PURGED.OPEN.RECV' TO PURGED.OPEN.RECV ELSE ERRMSG='PURGED.OPEN.RECV FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','PURGED.OR.XREF' TO PURGED.OR.XREF ELSE ERRMSG='PURGED.OR.XREF FILE MISSING';GOTO 93000

* Insert method code here
   STATUS = RBO.getProperty("","PMCProperty",PMCProperty)
   CONO = PMCProperty<1,4>
*
*---- MAIN PROCESSING
*
*
*---- GET FISCAL BEGIN DATE FOR THIS COMPANY
*     THEN SUBTRACT 1095 DAYS TO GET CUTOFF DATE FOR RECORD DELETION
*     (ANY INVOICE DATE LESS THAN THIS DATE WILL BE DELETED FROM THE
*     PURGED FILE.)
*     IF THERE IS NO RECORD, THEN THIS DATE WILL BE CALCULATED TO BE
*     THE FIRST DAY OF THE CURRENT MONTH.
*
   READ CREC FROM CONTROL,CONO:"ARSFISCAL" THEN
      FISCAL.BEG.DATE = CREC<2,0,0>
   END ELSE
      TEMP.DATE = OCONV(DATE(),"D4-")
      FISCAL.BEG.DATE = ICONV(TEMP.DATE[1,3]:"01":TEMP.DATE[6,5],"D")
   END
   CUTOFF.DATE = FISCAL.BEG.DATE - 1095
*
*---- START CHECKING THE PURGED.OPEN.RECV FILE
*
   DONE = 0
   POR.KEYS = ""
   SELECT PURGED.OPEN.RECV TO POR.KEYS
   LOOP
      READNEXT POR.KEY FROM POR.KEYS ELSE DONE = 1
   UNTIL DONE DO
      MATREAD POR.REC FROM PURGED.OPEN.RECV,POR.KEY THEN
         IF POR.INV.DATE < CUTOFF.DATE<1> THEN
         * DETERMINE IF AN XREF RECORD EXISTS
            XREF.KEY = CONO:POR.CUST
            READ XREF.REC FROM PURGED.OR.XREF,XREF.KEY THEN
               XREF.FOUND = 1
            END ELSE
               XREF.FOUND = 0
            END
         * IF AN XREF RECORD WAS FOUND, DETERMINE IF THE PURGED ITEM
         * IS CONTAINED IN THE XREF RECORD.
            IF XREF.FOUND THEN
               LOCATE POR.KEY IN XREF.REC<1> SETTING REC.LOC ELSE REC.LOC = 0
               IF XREF.FOUND AND REC.LOC > 0 THEN
                  XREF.REC = DELETE(XREF.REC,REC.LOC,0,0)
               END
               WRITE XREF.REC ON PURGED.OR.XREF,XREF.KEY
            END
            DELETE PURGED.OPEN.RECV,POR.KEY
         END
      END
   REPEAT
RETURN
* End of method code
93000
   STATUS = RBO.setProperty("","ServerStatus",1)
   STATUS = RBO.setProperty("","ServerMessage",ERRMSG)
RETURN

