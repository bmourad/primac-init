*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - APV.EOM.CLOSE
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
* DESCRIPTION - Close End of Day/Period for A/P Vouchers.
*MODIFIED	- (01/30/2006) BY ZAHOOR AHMED
*		   FOR printflag and EOD.ERROR flag
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB SALESDATES
$INCLUDE PMC.CPYLIB FISCAL
$INCLUDE PMC.CPYLIB EOM.TRANS
$INCLUDE PMC.CPYLIB EOM.TRANS.HIST              ; * T25975
$INCLUDE PMC.CPYLIB VEND
$INCLUDE APS.CPYLIB OAP
$INCLUDE APS.CPYLIB VOUCHERS
$INCLUDE GLS.CPYLIB GLA
$INCLUDE PMC.CPYLIB COA
$INCLUDE GLS.CPYLIB COA.SYS
$INCLUDE PMC.CPYLIB POST.REJECTS
$INCLUDE CPYLIB CHAR
*
***** SETUP ERRMSG ROUTINE
*
ERRMSG=''
REJECT.RPT.PRT=''
*
* ERROR.FLAG is for any error from EOD_CLOSE_PreProcess & Printfalg is view/mail/print/file
ERROR.FLAG = ''
Printfalg = ''
EMAIL_ADDR = ''
*
STATUS = RBO.getProperty('','ID',ID)
STATUS = RBO.getProperty('','PMCProperty',PMCProperty)
STATUS = RBO.getProperty('','PRINT_FLAG',PRINT.FLAG)
STATUS = RBO.getProperty('','ERR_FLAG',ERROR.FLAG)
STATUS = RBO.getProperty('','EmailAddress',EMAIL_ADDR)
CONO = ID[1,3]
STATUS = RBO.getProperty('','DM_Flag',D.M.FLG)
IF D.M.FLG = "PERIOD" THEN
   RPT.PER = "PERIOD"
END ELSE
   RPT.PER = "DAY"
END
STATUS = RBO.getProperty('','FR_CURR_PER',FR.CURR.PER)
STATUS = RBO.getProperty('','FR_CURR_DATE',FR.CURR.DATE)
STATUS = RBO.getProperty('','FR_NEXT_PER',FR.NEXT.PER)
STATUS = RBO.getProperty('','FR_NEXT_DATE',FR.NEXT.DATE)
STATUS = RBO.getProperty('','FR_PRINT',FR.PRINT)
STATUS = RBO.getProperty('','FR_CLOSE_DATE',FR.CLOSE.DATE)
STATUS = RBO.getProperty('','FR_D_M_FLG',FR.D.M.FLG)
STATUS = RBO.getProperty('','DivPos',POS)
FOR P = 1 TO DCOUNT(FR.CURR.PER,VM)
   IF INDEX(FR.CURR.DATE<1,P>,'/',1) > 0 THEN FR.CURR.DATE<1,P> = ICONV(FR.CURR.DATE<1,P>,'D2/')
   IF INDEX(FR.NEXT.DATE<1,P>,'/',1) > 0 THEN FR.NEXT.DATE<1,P> = ICONV(FR.NEXT.DATE<1,P>,'D2/')
   IF INDEX(FR.CLOSE.DATE<1,P>,'/',1) > 0 THEN FR.CLOSE.DATE<1,P> = ICONV(FR.CLOSE.DATE<1,P>,'D2/')
NEXT P
STATUS = RBO.getProperty('','DivCode',DIV.CODE)
IF DIV.CODE = '' THEN DIV.CODE = 'ALL'
*
*Get the printflag view/Email/File/Print
*
BEGIN CASE
   CASE PRINT.FLAG = 'PrintData'
	Printfalg = 'View'
   CASE PRINT.FLAG = 'EmailData'
	Printfalg = 'Email'
   CASE PRINT.FLAG = 'File'
	Printfalg = 'Print'
   CASE 1
	Printfalg = 'Print'
END CASE
*
*
**** INTITIALIZATION
*
TODAY = DATE()
UNKNOWN = "NOT ON FILE"
SYS.FISCAL = "APVFISCAL"
PROG.ID = @TTY:"!":CONO:"!APV.EOM"
NORETURN=0
*
***** OPEN FILES
*
TRANFILE='APV.EOM.TRANS':CONO:DIV.CODE
REJECTFILE='APV.POST.REJECTS':CONO:DIV.CODE
OPEN "","CONTROL" TO CONTROL ELSE
   ERRMSG="CONTROL FILE MISSING"
   GOTO 93000
END
OPEN "",TRANFILE TO EOM.TRANS ELSE
   ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD"
   GOTO 93000
END
OPEN "",REJECTFILE TO POST.REJECTS ELSE
   ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD"
   GOTO 93000
END
OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
OPEN "","SECURITY" TO SECURITY ELSE ERRMSG="SECURITY FILE MISSING";GOTO 93000
OPEN "","VEND" TO VEND ELSE ERRMSG="VEND FILE IS MISSING";GOTO 93000
OPEN "","VOUCHERS" TO VOUCHERS ELSE ERRMSG="VOUCHERS FILE IS MISSING";GOTO 93000
OPEN "","VOUCHERS.TAG" TO VOUCHERS.TAG ELSE ERRMSG="VOUCHERS.TAG FILE IS MISSING";GOTO 93000
OPEN "","OAP" TO OAP ELSE ERRMSG="OAP FILE IS MISSING";GOTO 93000
OPEN "","EOD.HIST" TO EOD.HIST ELSE ERRMSG="EOD.HIST FILE IS MISSING";GOTO 93000
OPEN "","APV.EOM.TRANS.HIST" TO APV.EOM.TRANS.HIST ELSE ERRMSG="APV.EOM.TRANS.HIST FILE IS MISSING";GOTO 93000
*
*If ERROR.FLAG = 1 from EOD_CLOSE_PreProcess then call Generatereport_Sub
*
IF ERROR.FLAG = 1 THEN
 GOTO 90000
END
*
MATREAD COMP.REC FROM COMPANY,CONO ELSE
   ERRMSG = "CANNOT LOCATE COMPANY # ":CONO; GOTO 93000
END
   IF CO.POS = "Y" AND CO.VOU.PO.FLG = "Y" THEN
      UPDATE.JOB = 1
      OPEN "","OPOAPS.XREF" TO OPOAPS.XREF ELSE ERRMSG="OPOAPS.XREF FILE IS MISSING";GOTO 93000
   END ELSE
      UPDATE.JOB = 0
   END
IN.ACCT.LEN = LEN(CO.ACCT.PIC)
ACCT.LEN = IN.ACCT.LEN + 10
IF CO.GLS # "N" THEN
   IF CO.GL.LINK<1,2> = "Y" THEN
      OPEN "","GLA" TO GLA ELSE ERRMSG="GLA FILE IS MISSING";GOTO 93000
   END
   OPEN "","COA" TO COA ELSE ERRMSG="COA FILE IS MISSING";GOTO 93000
   EQV.FLG = 1
   OPEN "","COA.EQUIV" TO COA.EQUIV ELSE EQV.FLG = 0
END
CLEARFILE POST.REJECTS
PRR.SEQ = 10000
READ NUM.PERIODS FROM CONTROL, CONO:"ACCT.PERIODS" ELSE NUM.PERIODS = 12
READ FISCALMON FROM CONTROL, CONO:'FISCAL' ELSE
   RELEASE CONTROL, CONO:SYS.FISCAL
   ERRMSG = 'CANNOT LOCATE CONTROL, FISCAL'
   GOTO 93000
END
READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
  SECURITY.REC = 'N'; SECURITY.REC<2> = 'N'
END
**** VOUCHERS ****
   WRITE "VOUCHERS" ON CONTROL , PROG.ID
   IF D.M.FLG = "PERIOD" THEN GOTO 1500
   SELECT VOUCHERS.TAG
   DATA = 1
   LOOP
      READNEXT VCH.ID ELSE DATA = 0
   WHILE DATA DO
      IF VCH.ID[1,3] # CONO THEN GOTO 1499
      MATREADU VCH.REC FROM VOUCHERS, VCH.ID ELSE
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
      IF VCH.MON <> FR.CURR.PER<1,POS> THEN;* T23278
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
      IF VCH.GLA.DATE = "" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
      IF VCH.DIV<1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
      VCH.GLA.DATE = TODAY
      MATWRITE VCH.REC ON VOUCHERS, VCH.ID
      DELETE VOUCHERS.TAG, VCH.ID
1499 REPEAT
   GOTO 6000
1500 SELECT VOUCHERS
   DATA = 1
   LOOP
      READNEXT VCH.ID ELSE DATA = 0
   WHILE DATA DO
      IF VCH.ID[1,3] # CONO THEN GOTO 1999
      MATREADU VCH.REC FROM VOUCHERS, VCH.ID ELSE
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
      IF VCH.DIV<1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
      IF VCH.MON <= FR.CURR.PER<1,POS> THEN;* T23278
         DELETE VOUCHERS.TAG, VCH.ID
      END
      IF VCH.MON <> FR.CURR.PER<1,POS> THEN;* T23278
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
      IF VCH.GLA.DATE = "" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
      IF UPDATE.JOB AND VCH.JOB # "" THEN
         OPAX.ID = CONO : VCH.JOB
         READU OPAX FROM OPOAPS.XREF, OPAX.ID ELSE OPAX = ""
         LOCATE VCH.ID[4,6] IN OPAX<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN
            OPAX = DELETE(OPAX,1,FND,0)
            IF OPAX = "" THEN
               DELETE OPOAPS.XREF, OPAX.ID
            END ELSE
               WRITE OPAX ON OPOAPS.XREF, OPAX.ID
            END
         END ELSE
            RELEASE OPOAPS.XREF, OPAX.ID
         END
      END
      DELETE VOUCHERS, VCH.ID
1999 REPEAT
*
**** OAP ****
   WRITE "OAP" ON CONTROL , PROG.ID
   READ APDFISCAL FROM CONTROL, CONO : "APDFISCAL" ELSE APDFISCAL = ""
   IF APDFISCAL<6> = "" THEN
      APDFISCAL<6> = TODAY
      MM = OCONV(APDFISCAL<6>, "D/")[1,2]
      BLK = 0
      FOR TR = 1 TO 31 UNTIL BLK
         APDFISCAL<6> = APDFISCAL<6> - 1
         IF MM # OCONV(APDFISCAL<6>, "D/")[1,2] THEN BLK = 1
      NEXT TR
   END
   SELECT OAP
   DATA = 1
   LOOP
      READNEXT OAP.ID ELSE DATA = 0
   WHILE DATA DO
      IF OAP.ID[1,3] # CONO THEN GOTO 2999
      MATREADU OAP.REC FROM OAP, OAP.ID ELSE
         RELEASE OAP, OAP.ID
         GOTO 2999
      END
      IF OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.PAID = 0 THEN
         IF OAP.PAID.DATE > APDFISCAL<6> THEN
            RELEASE OAP, OAP.ID
            GOTO 2999
         END
         IX.ID = OAP.ID[1,3]:STR("0",10-LEN(OAP.INV)):OAP.INV:OAP.VEND<1,1>
         DELETE OAP, OAP.ID
      END ELSE
         RELEASE OAP, OAP.ID
      END
2999 REPEAT
*
**** INV.XREF ****
   WRITE "INV.XREF" ON CONTROL , PROG.ID
6000 IF CO.GLS = "N" THEN GOTO 8000
   IF CO.GL.LINK<1,2> # "Y" THEN GOTO 8000
**** GLA ****
7000 *
   MATREAD CSY.REC FROM CONTROL, CONO : "COA.SYS" ELSE MAT CSY.REC = ""
   LOCATE "VR" IN CSY.SOURCE<1>,1 SETTING FND ELSE NULL
   L.GLA = CSY.LEVEL<1,FND>
   ACCOUNTS = ""; AMOUNTS = ""
   MAT GLA.REC = ""
   GLA.DATE = TODAY
   GLA.SRC = "VR"
   GLA.MON = FR.CURR.PER<1,POS>;* T23278
   ODATE = " " : OCONV(FR.CURR.DATE<1,POS>,"D2/");* T23278
   SELECT EOM.TRANS
   READU COUNTER FROM CONTROL, CONO : "GLACOUNTER" ELSE COUNTER = 0
   WRITE "GLA " : COUNTER ON CONTROL , PROG.ID
   DATA = 1
   BEGIN CASE
      CASE L.GLA = "R"
         LOOP
            READNEXT ETR.ID ELSE DATA = 0
         WHILE DATA DO
            IF ETR.ID[1,3] # CONO THEN GOTO 7199
            MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7180
            ACCT = ETR.ID[1,ACCT.LEN]
            VEND.NO = FIELD(ETR.ID,"!",1)[ACCT.LEN + 1,99]
            TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
            IF EQV.FLG = 1 THEN
               READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
               ACCT = N.ACCT
            END
            COA.ID = ACCT[11,IN.ACCT.LEN]
            MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
               MAT COA.REC = ""
               COA.DESC = UNKNOWN
            END
            BEGIN CASE
               CASE COA.DETAIL = "R" OR COA.DETAIL = "D"
                  LOOP
                     COUNTER = COUNTER + 1
                     IF COUNTER > 999998 THEN COUNTER = 1
                     GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                     READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                  WHILE DUMMY # "" DO REPEAT
                  GLA.REF = VEND.NO
                  GLA.DESC = ETR.RDATE "L#25" : ODATE
                  GLA.AMT = ETR.AMT
                  MATWRITE GLA.REC ON GLA, GLA.ID
               CASE 1
                  LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
                     ACCOUNTS<FND> = ACCT
                     AMOUNTS<FND> = ""
                  END
                  IF TYPE = "D" THEN
                     AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
                  END ELSE
                     AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
                  END
            END CASE
7180        RELEASE EOM.TRANS, ETR.ID
7199     REPEAT
      CASE L.GLA = "D"
         LOOP
            READNEXT ETR.ID ELSE DATA = 0
         WHILE DATA DO
            IF ETR.ID[1,3] # CONO THEN GOTO 7299
            MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7280
            ACCT = ETR.ID[1,ACCT.LEN]
            VEND.NO = FIELD(ETR.ID,"!",1)[ACCT.LEN + 1,99]
            TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
            IF EQV.FLG = 1 THEN
               READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
               ACCT = N.ACCT
            END
            COA.ID = ACCT[11,IN.ACCT.LEN]
            MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
               MAT COA.REC = ""
               COA.DESC = UNKNOWN
            END
            BEGIN CASE
               CASE COA.DETAIL = "D"
                  TCNT = COUNT(ETR.TRAN,VM) + (ETR.TRAN # "")
                  FOR I = 1 TO TCNT
                     LOOP
                        COUNTER = COUNTER + 1
                        IF COUNTER > 999998 THEN COUNTER = 1
                        GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                        READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                     WHILE DUMMY # "" DO REPEAT
                     GLA.REF = VEND.NO
                     GLA.DESC = ETR.TRAN<1,I>[1,6] : " " : ETR.RDATE
                     GLA.DESC = GLA.DESC "L#25" : " " : OCONV(ETR.DATE<1,I>,"D2/")
                     GLA.AMT = ETR.TAMT<1,I>
                     MATWRITE GLA.REC ON GLA, GLA.ID
                  NEXT I
               CASE COA.DETAIL = "R"
                  LOOP
                     COUNTER = COUNTER + 1
                     IF COUNTER > 999998 THEN COUNTER = 1
                     GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                     READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                  WHILE DUMMY # "" DO REPEAT
                  GLA.REF = VEND.NO
                  GLA.DESC = ETR.RDATE "L#25" : ODATE
                  GLA.AMT = ETR.AMT
                  MATWRITE GLA.REC ON GLA, GLA.ID
               CASE 1
                  LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
                     ACCOUNTS<FND> = ACCT
                     AMOUNTS<FND> = ""
                  END
                  IF TYPE = "D" THEN
                     AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
                  END ELSE
                     AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
                  END
            END CASE
7280        RELEASE EOM.TRANS, ETR.ID
7299     REPEAT
      CASE 1
         LOOP
            READNEXT ETR.ID ELSE DATA = 0
         WHILE DATA DO
            IF ETR.ID[1,3] # CONO THEN GOTO 7350
            MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7330
            ACCT = ETR.ID[1,ACCT.LEN]
            TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
            IF EQV.FLG = 1 THEN
               READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
               ACCT = N.ACCT
            END
            LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
               ACCOUNTS<FND> = ACCT
               AMOUNTS<FND> = ""
            END
            IF TYPE = "D" THEN
               AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
            END ELSE
               AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
            END
7330        RELEASE EOM.TRANS, ETR.ID
7350     REPEAT
   END CASE
   GLA.REF = ""
   ACNT = COUNT(ACCOUNTS,AM) + (ACCOUNTS # "")
   FOR I = 1 TO ACNT
      ACCT = ACCOUNTS<I>
      COA.ID = ACCT[11,IN.ACCT.LEN]
      MATREAD COA.REC FROM COA, CONO : COA.ID ELSE COA.DESC = UNKNOWN
      FOR J = 1 TO 2
         IF AMOUNTS<I,J> = "" THEN GOTO 7360
         LOOP
            COUNTER = COUNTER + 1
            IF COUNTER > 999998 THEN COUNTER = 1
            GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
            READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
         WHILE DUMMY # "" DO REPEAT
         GLA.AMT = AMOUNTS<I,J>
         GLA.DESC = COA.DESC "L#25" : ODATE
         MATWRITE GLA.REC ON GLA, GLA.ID
7360  NEXT J
   NEXT I
   WRITE COUNTER ON CONTROL, CONO : "GLACOUNTER"
   ACCOUNTS = ""
   AMOUNTS = ""
**** EOM.TRANS (1) ****
8000 *
   SELECT EOM.TRANS
   WRITE "EOM.TRANS (1)" ON CONTROL , PROG.ID
   DATA = 1
   LOOP
      READNEXT ETR.ID ELSE DATA = 0
   WHILE DATA DO
      IF CONO # ETR.ID[1,3] THEN GOTO 8999
      MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE
         RELEASE EOM.TRANS, ETR.ID
         GOTO 8999
      END
      CURR.PER = FR.CURR.PER<1,POS>
      MAT ETH.REC = MAT ETR.REC
      DONE = 0
      SEQ = 0
      ETH.ID = FIELD(ETR.ID,"*",1):"*":CURR.PER
      LOOP UNTIL DONE = 1 DO
         SEQ = SEQ + 1
         ETH.KEY = ETH.ID:"*":SEQ
         READ CHECKIT FROM APV.EOM.TRANS.HIST, ETH.KEY ELSE DONE = 1
      REPEAT
      MATWRITE ETH.REC ON APV.EOM.TRANS.HIST, ETH.KEY
      DELETE EOM.TRANS, ETR.ID
8999 REPEAT
WRITE "EOM.TRANS (2)" ON CONTROL, PROG.ID
EOD.HID = ''
IF SECURITY.REC<2> = 'Y' THEN EOD.HID = '!':DIV.CODE
READ EOD.HIST.REC FROM EOD.HIST, CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID ELSE;* T23278
   EOD.HIST.REC = ""
END
EOD.HIST.REC = INSERT(EOD.HIST.REC,-1,0,0,TODAY)
WRITE EOD.HIST.REC ON EOD.HIST,CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID ;* T23278'
READ APDFISCAL FROM CONTROL, CONO : "APVFISCAL" ELSE APDFISCAL = ""
SELECT EOM.TRANS
READNEXT ETR.ID ELSE
   IF D.M.FLG = "PERIOD" THEN
      IF FR.NEXT.PER<1,POS> = "" THEN
        FR.NEXT.PER<1,POS> = APDFISCAL<3>
      END
      IF FR.NEXT.DATE<1,POS> = "" THEN
	FR.NEXT.DATE<1,POS> = APDFISCAL<4>
      END
      FR.CURR.PER<1,POS> = FR.NEXT.PER<1,POS>
      FR.CURR.DATE<1,POS> = FR.NEXT.DATE<1,POS>
      FR.CLOSE.DATE<1,POS> = TODAY
      STATUS = RBO.setProperty('','FR_CURR_PER',FR.CURR.PER)
      STATUS = RBO.setProperty('','FR_CURR_DATE',FR.CURR.DATE)
      STATUS = RBO.setProperty('','FR_CLOSE_DATE',FR.CLOSE.DATE)
   END
   FR.NEXT.PER<1,POS> = ""; FR.NEXT.DATE<1,POS> = ""
   FR.PRINT<1,POS> = ""; FR.D.M.FLG<1,POS> = ""
   STATUS = RBO.setProperty('','FR_NEXT_PER',FR.NEXT.PER)
   STATUS = RBO.setProperty('','FR_NEXT_DATE',FR.NEXT.DATE)
   STATUS = RBO.setProperty('','FR_PRINT',FR.PRINT)
   STATUS = RBO.setProperty('','FR_D_M_FLG',FR.D.M.FLG)
   DELETE CONTROL, PROG.ID
   GOSUB 90000
   CMD='DELETE.FILE ': TRANFILE :' FORCE'
   UDTEXECUTE CMD CAPTURING JUNK
   IF REJECT.RPT.PRT='' THEN
      CMD='DELETE.FILE ': REJECTFILE : ' FORCE'
      UDTEXECUTE CMD CAPTURING JUNK
   END
   IF ERRMSG # '' THEN GOTO 93000
   GOTO 99999
END
NORETURN=1
90000 
READ JUNK FROM POST.REJECTS,'10001' THEN
   UserID = PMCProperty<1,3>
   IF D.M.FLG = 'DAY' THEN RPTID = 'AP309' ELSE RPTID = 'AP311'
*   IN_PARAM = UserID:"!APEOPVPC!111!":RPTID:"!":CONO:"!Print"
   IN_PARAM = UserID:"!APEOPVPC!111!":RPTID:"!":CONO:"!":Printfalg
   IN_PARAM<2> = ''
   IN_PARAM<3> = 'EOD.REJECT.LISTING'
   IN_PARAM<4> = 'P'
   IN_PARAM<5> = 'PMCPROCS'
   IN_PARAM<6> = REJECTFILE
   IN_PARAM<7> = 'REJECTS'
   IN_PARAM<8> = EMAIL_ADDR ;*Email Address as Entered by the user
   OUT_PARAM=''
   UDTEXECUTE 'SSELECT ':REJECTFILE CAPTURING JUNK
   CALL GenerateReport_Sub(ERRMSG,IN_PARAM,OUT_PARAM)
   REJECT.RPT.PRT='WARNING !!! MAKE SURE TO PICKUP CLOSING REJECTS REPORT FROM PRINTER'
   STATUS=RBO.setProperty('','RejectRptPrinted',REJECT.RPT.PRT)
   STATUS=RBO.setProperty('','ReportImage',OUT_PARAM<2>)
END ELSE
	IF Printfalg # "Print" THEN	ERRMSG  = "There is no data to ":Printfalg
	GOTO 93000
END
IF NOT(NORETURN) THEN RETURN
FR.PRINT<1,POS> = "X";* T23278
STATUS = RBO.setProperty('','FR_PRINT',FR.PRINT)
ERRMSG = "E R R O R !!! CANNOT LOCATE ALL TRANSACTIONS"
93000 WRITEV ERRMSG ON CONTROL, PROG.ID,2
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
*** END OF JOB ***
99999 
MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
END
