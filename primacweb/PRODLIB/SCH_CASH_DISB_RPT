SUBROUTINE SCH_CASH_DISB_RPT
********************************************************************************
*   Program name :- SCH_CASH_DISB_RPT
*   Created:- 6/3/2003
*------------------------------------------------------------------------------*
* 
* In Properties:
* --------------
*  Cono,PAYMENT_DATE
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE APS.CPYLIB OAP
$INCLUDE PMC.CPYLIB VEND
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB GLTABLE
$INCLUDE CPYLIB CHAR

* Insert method code here
   ERRMSG = ''
   DIM SVEND.REC(10)
   EQU SVEND.NO TO SVEND.REC(1)
   EQU SVEND.DS TO SVEND.REC(2)
   EQU SVEND.PT TO SVEND.REC(3)

   TOT.GRS = "" ; TOT.PAD = "" ; TOT.DSC = "" ; TOT.NET = ""
   GTOT.GRS = 0 ; GTOT.PAD = 0 ; GTOT.DSC = 0 ; GTOT.NET = 0
   
   STATUS = RBO.getProperty('','Cono',CONO)
   STATUS = RBO.getProperty('','PAYMENT_DATE',PAYDATE)
   STATUS = RBO.getProperty('','FISCAL_PERIOD',FISCAL.PERIOD)
   STATUS = RBO.getProperty('','DIV_CODE',DIV.CODE)
   STATUS = RBO.getProperty('','USER_ID',USER.ID)
   PAID.DATE=ICONV(PAYDATE,"D2/")

   OAP.VEND.NO = ""
   OAP.VEND.NAME = ""
   OAP.REM = ""
   OAP.VOUCH = ""
   OAP.INV.NO = ""
   OAP.INV.DT = ""
   OAP.DUE.DT = ""
   OAP.GRS = ""
   OAP.PAID = ""
   OAP.DSC.VAL = ""
   OAP.NET = ""

   RCNT = 1
   CCNT = 1
   PREV.VEND = '' ; PRT.VEND.FLG = 1
   OPEN "","OAP" TO OAP ELSE 
	ERRMSG = "OAP FILE IS MISSING"
	GOTO 99999
   END
   OPEN "","CONTROL" TO CONTROL ELSE 
	ERRMSG = "CONTROL FILE IS MISSING"
	GOTO 99999
   END
   OPEN "","VEND" TO VEND ELSE 
	ERRMSG = "VENDOR FILE IS MISSING"
	GOTO 99999	
   END

   MATREAD SVEND.REC FROM CONTROL , CONO:"VENDORS":DIV.CODE ELSE FLG = 1 ; MAT SVEND.REC = ''

*
**** READ AND PROCESS FIRST REPORT
*
  
   STR = "SSELECT OAP BY VEND_ WITH BAL-OPEN NE 0 AND WITH DUE.STAT # 'HOLD' AND WITH VND.STAT # 'H' AND "
   STR = STR : "WITH DUE.DATE LE '" : OCONV(PAID.DATE,"D2/") :"' AND WITH CO_ = " : CONO :" AND WITH PERIOD LE "
   STR = STR : FISCAL.PERIOD 
   IF DIV.CODE # "ALL" THEN
	STR = STR :" AND WITH VCH.DIV.OWNER = " : DIV.CODE
   END
   *EXECUTE THE METHOD GET_SELNAME WITH SCD.RPT ASSIGNED TO IT AND USE THAT IN SAVE-LIST
   STATUS = RBO.setProperty('','LIST_NAME','SCD.RPT')
   STATUS=RBO.callMethod('','GET_SELNAME')
   STATUS = RBO.getProperty('','LIST_NAME',LIST.NAME)

   UDTEXECUTE STR 
   UDTEXECUTE "SAVE-LIST " : LIST.NAME ;* save list to get back the contents in cash.disb
   UDTEXECUTE "GET-LIST " : LIST.NAME

   ****PROCESS FIRST RECORD
100*
   READNEXT ID ELSE
	ERRMSG = "NO ITEMS SELECETED"
	GOTO 99999
   END
   MATREAD OAP.REC FROM OAP, ID ELSE GOTO 100
   PREV.VEND = OAP.VEND<1,1>
   IF OAP.VEND<1,2> # "" THEN
      VEND.DESC = OAP.VEND<1,2>
   END ELSE
      MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
         VEND.DESC = "VEND NOT ON FILES"
      END
   END
   GOSUB 1000

**** PROCESS ALL RECORDS
200*
DATA = 1
LOOP
   READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
   	MATREAD OAP.REC FROM OAP , ID ELSE GOTO 299
   	GOSUB 900
   	GOSUB 1000
299REPEAT

   IF PRT.VEND.FLG = 1 THEN
      GOSUB 2000
   END
  * DELETE CONTROL , CONO:"VENDORS.PRT"
  
STATUS = RBO.setProperty('','OAP_VEND_NO',OAP.VEND.NO)
STATUS = RBO.setProperty('','OAP_VEND_NAME',OAP.VEND.NAME)
STATUS = RBO.setProperty('','OAP_REM_COMM',OAP.REM)
STATUS = RBO.setProperty('','OAP_VOUCH',OAP.VOUCH)
STATUS = RBO.setProperty('','OAP_INV',OAP.INV.NO)
STATUS = RBO.setProperty('','OAP_INV_DATE',OAP.INV.DT)
STATUS = RBO.setProperty('','OAP_DUE_DATE',OAP.DUE.DT)
STATUS = RBO.setProperty('','OAP_GRS_AMT',OAP.GRS)
STATUS = RBO.setProperty('','OAP_PAID_AMT',OAP.PAID)
STATUS = RBO.setProperty('','OAP_DSC_AMT',OAP.DSC.VAL)
STATUS = RBO.setProperty('','OAP_NET_AMT',OAP.NET)

STATUS = RBO.setProperty('','TOT_GRS',TOT.GRS)
STATUS = RBO.setProperty('','TOT_PAD',TOT.PAD)
STATUS = RBO.setProperty('','TOT_DSC',TOT.DSC)
STATUS = RBO.setProperty('','TOT_NET',TOT.NET)

STATUS = RBO.setProperty('','GTOT_GRS',GTOT.GRS)
STATUS = RBO.setProperty('','GTOT_PAD',GTOT.PAD)
STATUS = RBO.setProperty('','GTOT_DSC',GTOT.DSC)
STATUS = RBO.setProperty('','GTOT_NET',GTOT.NET)

GOTO 99999

*
**** FIND BREAK
*
900*
   IF OAP.VEND<1,1> # PREV.VEND THEN
      IF PRT.VEND.FLG = 1 THEN
	  GOSUB 2000
      END 
      PRT.VEND.FLG = 1
      RCNT += 1
      CCNT = 1
      PREV.VEND = OAP.VEND<1,1>
      IF OAP.VEND<1,2> # "" THEN
         VEND.DESC = OAP.VEND<1,2>
      END ELSE
         MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
            VEND.DESC = "VEND NOT ON FILES"
         END
      END
   END
   RETURN

*
**** PRINT VOUCHER LINES AND ADD VENDOR TOTALS
*
1000*
    
   *IF ICONV(OAP.DUE.DATE,"D2/") > PAID.DATE THEN RETURN ;*---------
   IF OAP.DUE.DATE > PAID.DATE THEN RETURN
   IF ABS(OAP.GRS.AMT - OAP.DSC.AMT) <= ABS(OAP.PAID.AMT) THEN RETURN
   LOCATE OAP.VEND<1,1> IN SVEND.NO<1>,1 SETTING VFND ELSE VFND = 0
   BEGIN CASE
   CASE VFND = 0 AND SVEND.PT = "P"
      PRT.VEND.FLG = 0
      RETURN
   CASE VFND # 0 AND SVEND.PT # "P"
      PRT.VEND.FLG = 0
      RETURN
   END CASE
	   OAP.VEND.NO<1,RCNT> 	= OAP.VEND<1,1> 
	   OAP.VEND.NAME<1,RCNT>  	= VEND.DESC
	   OAP.REM<1,RCNT,CCNT>  	= OAP.REM.COMM
	   OAP.VOUCH<1,RCNT,CCNT>  	= ID[4,6]
	   OAP.INV.NO<1,RCNT,CCNT> 	= OAP.INV
	   OAP.INV.DT<1,RCNT,CCNT> 	= OCONV(OAP.INV.DATE,"D2/")
	   OAP.DUE.DT<1,RCNT,CCNT>  = OCONV(OAP.DUE.DATE,"D2/")
	   OAP.GRS<1,RCNT,CCNT>  	= OCONV(OAP.GRS.AMT,"MD2")
	   OAP.PAID<1,RCNT,CCNT> 	= OCONV(OAP.PAID.AMT,"MD2")
	   OAP.DSC.VAL<1,RCNT,CCNT> = OCONV(OAP.DSC.AMT,"MD2")
	   OAP.NET<1,RCNT,CCNT>  	= OCONV(OAP.GRS.AMT - (OAP.PAID.AMT + OAP.DSC.AMT),"MD2")
	   TOT.GRS<1,RCNT> += OAP.GRS<1,RCNT,CCNT>
	   TOT.PAD<1,RCNT> += OAP.PAID<1,RCNT,CCNT>
	   TOT.DSC<1,RCNT> += OAP.DSC.VAL<1,RCNT,CCNT>
	   TOT.NET<1,RCNT> += OAP.NET<1,RCNT,CCNT> 
	   GTOT.GRS += OAP.GRS<1,RCNT,CCNT>
	   GTOT.PAD += OAP.PAID<1,RCNT,CCNT>
	   GTOT.DSC += OAP.DSC.VAL<1,RCNT,CCNT>
	   GTOT.NET += OAP.NET<1,RCNT,CCNT> 
	   CCNT += 1
RETURN

2000*
   IF TOT.NET<1,RCNT> < 1 THEN
      IF SVEND.PT = 'S' THEN ;* IF VENDORS TO BE SUPPRESSED
         VNO.CNT = COUNT(SVEND.NO,VM) + (SVEND.NO # "") + 1
         SVEND.NO<1,VNO.CNT> = PREV.VEND
         SVEND.DS<1,VNO.CNT> = VEND.DESC
      END ELSE ;* IF VENDORS TO BE PAID
         LOCATE PREV.VEND IN SVEND.NO<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN
            SVEND.NO = DELETE(SVEND.NO,1,FND,0)
            SVEND.DS = DELETE(SVEND.DS,1,FND,0)
         END
      END
   END
RETURN

* End of method code
99999
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','ServerStatus','E')
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END ELSE
       STATUS = RBO.setProperty('','ServerStatus','')
       STATUS = RBO.setProperty('','ServerMessage','')
  END 
RETURN
