SUBROUTINE ACRDLM_READ
*********************************************************************
* Copyright 1998 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* PROGRAM      - ACRDLM_READ
* BY           - KHAJA ZIAUDDIN
* DATE         - 05/28/2003
* DESCRIPTION  - This program retrieves the record from ACCRUED.LIAB.HIST table
*                "Reference" parameter is to be passed.
*                Reference = PoType*Po#*Prod/Job
*                
*                
*ENDDOC
*********************************************************************

$INCLUDE WWINSERT RBO.H
$INCLUDE POS.CPYLIB ACCRUED.LIAB.HIST

* Insert method code here
IdList = ''
AcctList = ''
AmtList = ''
ClosedList = ''
CommentList = ''
DivDeptCctrList = ''
DtList = ''
MonList = ''
RefList = ''
SrcList = ''
UserList = ''
REF=''
MAT ALH.REC = ''

OPEN 'ACCRUED.LIAB.HIST' TO ACCRUED.LIAB.HIST ELSE
    ERRMSG = 'ACCRUED.LIAB.HIST IS MISSING'; GOTO 93000
END

STATUS = RBO.getProperty('','Reference',Ref)

CMD = "SELECT ACCRUED.LIAB.HIST WITH ALH_REF = '":Ref:"'"
UDTEXECUTE CMD
DATA = 1
LOOP
   READNEXT ID THEN
	IdList<1,-1> = ID
   END ELSE
      DATA = 0
   END 
WHILE DATA
REPEAT

FOR I = 1 TO DCOUNT(IdList,@VM)
   MATREAD ALH.REC FROM ACCRUED.LIAB.HIST,IdList<1,I> ELSE
	MAT ALH.REC = ""
   END	
   AcctList<1,-1> = ALH.ACCT
   AmtList<1,-1> = OCONV(ALH.AMT,"MD2")
   ClosedList<1,-1> = OCONV(ALH.CLOSED,"D2/")
   CommentList<1,-1> = ALH.COMMENT
   DivDeptCctrList<1,-1> = ALH.DV.DP.CC
   DtList<1,-1> = OCONV(ALH.DATE,"D2/")
   MonList<1,-1> = ALH.MON
   RefList<1,-1> = ALH.REF
   SrcList<1,-1> = ALH.SRC
   UserList<1,-1> = ALH.USER
NEXT
 
STATUS = RBO.setProperty('','Ids',IdList)
STATUS = RBO.setProperty('','Acct',AcctList)
STATUS = RBO.setProperty('','Amt',AmtList)
STATUS = RBO.setProperty('','Closed',ClosedList)
STATUS = RBO.setProperty('','Comment',CommentList)
STATUS = RBO.setProperty('','DivDeptCCtr',DivDeptCctrList)
STATUS = RBO.setProperty('','Dt',DtList)
STATUS = RBO.setProperty('','Mon',MonList)
STATUS = RBO.setProperty('','Ref',RefList)
STATUS = RBO.setProperty('','Src',SrcList)
STATUS = RBO.setProperty('','User',UserList)

ERRMSG = CMD
STATUS = RBO.setProperty('','ServerStatus',0)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG) 

* End of method code
RETURN

92000 
  STATUS = RBO.setProperty('','ServerStatus',0)        
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG) 
  RETURN

93000 
  STATUS = RBO.setProperty('','ServerStatus',1)        
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG) 
  RETURN

99999 
RETURN

