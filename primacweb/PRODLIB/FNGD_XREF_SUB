SUBROUTINE FNGD_XREF_SUB
********************************************************************************
*   Program name :- FNGD_XREF_SUB
*   Created:- 4/10/2004
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB CATEGORY
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE ICS.CPYLIB INV.CNV
* Insert method code here
ERRMSG = ''
OPEN '','INVENTORY' TO INVENTORY ELSE
	ERRMSG = "CAN'T OPEN INVENTORY FILE"
	GOTO 99999
END
OPEN '','CATEGORY' TO CATEGORY ELSE
	ERRMSG = "CAN'T OPEN CATEGORY FILE"
	GOTO 99999
END
OPEN '','INV.WHSE' TO INV.WHSE ELSE
	ERRMSG = "CAN'T OPEN INV.WHSE FILE"
	GOTO 99999
END
VAL = RBO.getProperty('','ID',CONO)
VAL = RBO.getProperty('','SOLD_TO',CUST.SEL)
VAL = RBO.getProperty('','JOB_ID',JOB.ID)
VAL = RBO.getProperty('','PROD_LINE',CATG.SEL)
VAL = RBO.getProperty('','WHSE',WHSE.SEL)
VAL = RBO.getProperty('','WIDTH',WID.SEL)
VAL = RBO.getProperty('','LENGTH',LEN.SEL)

IF CATG.SEL <> "" THEN
	MATREAD CATG.REC FROM CATEGORY, CONO:CATG.SEL ELSE
     		ERRMSG = "Cannot locate Category # ":CATG.SEL
		GOTO 99999
	END
END

BY.STMT = ""

STMT = 'SSELECT INV.WHSE WITH CONO = "':CONO:'" AND WITH M.LINE = "FNGD" AND WITH CUST = "':CUST.SEL:'"'
OR.STMT = ' OR WITH CONO = "':CONO:'" AND WITH M.LINE = "FNGD" AND WITH NO CUST'

IF CATG.SEL = "" THEN
	BY.STMT = BY.STMT : ' BY P.LINE'
END ELSE
	STMT = STMT : ' AND WITH P.LINE = "':CATG.SEL:'"'
	OR.STMT = OR.STMT : ' AND WITH P.LINE = "':CATG.SEL:'"'
END

IF WID.SEL = "" THEN
	BY.STMT = BY.STMT : ' BY WIDTH'
END ELSE
	STMT = STMT : ' AND WITH WIDTH = "':OCONV(WID.SEL,"MD4"):'"'
   	OR.STMT = OR.STMT : ' AND WITH WIDTH = "':OCONV(WID.SEL,"MD4"):'"'
END

IF LEN.SEL = "" THEN
	BY.STMT = BY.STMT : ' BY LENGTH'
END ELSE
	STMT = STMT : ' AND WITH LENGTH = "':OCONV(LEN.SEL,"MD4"):'"'
	OR.STMT = OR.STMT : ' AND WITH LENGTH = "':OCONV(LEN.SEL,"MD4"):'"'
END

IF WHSE.SEL = "" THEN
	BY.STMT = BY.STMT : ' BY WHSE'
END ELSE
	STMT = STMT : ' AND WITH WHSE = "':WHSE.SEL:'"'
	OR.STMT = OR.STMT : ' AND WITH WHSE = "':WHSE.SEL:'"'
END

STMT = STMT : OR.STMT : BY.STMT
PERFORM STMT CAPTURING RESPONSE
RESPONSE = ""; DATA = 1; LINES = 0

SEL.PROD = ""; SEL.WHSE = ""
SEL.PROD.DESC = ""
SEL.CATG = ""
SEL.WIDTH = ""
SEL.LENGTH = ""
SEL.AVAIL  = ""
SEL.MANF = ""

LOOP
	READNEXT SEL.ID ELSE DATA = 0
WHILE DATA DO
	LINES = LINES + 1
   	SEL.PROD<LINES> = FIELD(SEL.ID,"!",1)[4,15]

	INV.ID = CONO : SEL.PROD<LINES>
   	MATREAD INV.REC FROM INVENTORY, INV.ID ELSE
     		MAT INV.REC = ""; INV.FULL.DESC = "Unknown"
   	END
	SEL.PROD.DESC<LINES> = INV.DESC
	$INCLUDE ICSBP INV.UM.CNV

	SEL.WHSE<LINES> = FIELD(SEL.ID,"!",2)
	SEL.CATG<LINES> = "FNGD"
	MATREAD IWH.REC FROM INV.WHSE,SEL.ID ELSE
		MAT IWH.REC = ''
	END
	SEL.WIDTH<LINES> = OCONV(INV.PAP.WIDTH,"MD4")
	SEL.LENGTH<LINES> = OCONV(INV.PAP.LEN,"MD4")
	SEL.AVAIL<LINES> = OCONV(INT((((IWH.ON.HAND-IWH.RESV) / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
	SEL.MANF<LINES> = OCONV(INT(((IWH.INPRCS / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
REPEAT

VAL = RBO.setProperty('','CATG_LIST',LOWER(SEL.CATG))
VAL = RBO.setProperty('','ITEM_LIST',LOWER(SEL.PROD))
VAL = RBO.setProperty('','ITEM_DESC_LIST',LOWER(SEL.PROD.DESC))
VAL = RBO.setProperty('','WHSE_LIST',LOWER(SEL.WHSE))
VAL = RBO.setProperty('','WIDTH_LIST',LOWER(SEL.WIDTH))
VAL = RBO.setProperty('','LENGTH_LIST',LOWER(SEL.LENGTH))
VAL = RBO.setProperty('','AVAIL_LIST',LOWER(SEL.AVAIL))
VAL = RBO.setProperty('','MANF_LIST',LOWER(SEL.MANF))

99999
IF ERRMSG # "" THEN
	VAL = RBO.setProperty('','ServerStatus','E')
	VAL = RBO.setProperty('','ServerMessage',ERRMSG)
END
* End of method code
RETURN


