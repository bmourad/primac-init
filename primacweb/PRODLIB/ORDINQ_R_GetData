SUBROUTINE ORDINQ_R_GetData
********************************************************************************
*   Program name :- ORDINQ_R_GetData
*   Created:- 12/03/2002
*   Author:- L. Ross
*   This subroutine is used prior to the read of the ORDER.DETAIL record
*   to obtain the Release info for the Order Inquiry 'R'elease option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE OPS.CPYLIB ORDER.RELEASE
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.DETAIL,0)=0 THEN
  OPEN '','ORDER.DETAIL' TO ORDER.DETAIL ELSE
    ERRMSG<1,-1>='ORDER.DETAIL FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.RELEASE,0)=0 THEN
  OPEN '','ORDER.RELEASE' TO ORDER.RELEASE ELSE
    ERRMSG<1,-1>='ORDER.RELEASE FILE IS MISSING'
  END
END
IF ERRMSG#'' THEN GOTO 93000

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
ORDNO=FIELD(ID,"-",1)[4,8]
RELNO=ID[4,99]
;* get database values

MATREAD ORR.REC FROM ORDER.RELEASE, ID ELSE
  ERRMSG='Cannot read ORDER.RELEASE record ':ID
  GOTO 93000
END

SHPNO = ORR.SHIP.TO
OSD.ID = CONO:ORDNO:"!":SHPNO

MATREAD ORD.DET.REC FROM ORDER.DETAIL,OSD.ID ELSE
  ERRMSG='Cannot read ORDER.DETAIL record ':OSD.ID
  GOTO 93000
END
PCNT=DCOUNT(OSD.PROD,VM)
RELQTY=''
FOR P = 1 TO PCNT
  MATREAD INV.REC FROM INVENTORY,CONO:OSD.PROD<1,P> ELSE
    INV.FULL.DESC = 'Unknown'; INV.UNIT<1,3> = 'EA'; INV.M.LINE = 'FNGD'
  END
  RQTY = 0; PTR = 1
  LOOP
    LOCATE RELNO IN OSD.REL.NO<1,P>,PTR SETTING FND THEN
      RQTY = RQTY + OSD.REL.QTY<1,P,FND>
    END ELSE
      PTR = 0
    END
  WHILE PTR DO
    PTR = FND + 1
  REPEAT
  IF RQTY > 0 THEN
    CALL GetInvUMCnv(ERRMSG,IN_PARAM,OUT_PARAR,MAT INV.CNV.REC,MAT INV.REC)
    RELQTY<1,P> = OCONV(INT(((RQTY / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  END
NEXT P

STATUS=RBO.setProperty('','RelQty',RELQTY)
STATUS=RBO.setProperty('','ID',OSD.ID)
STATUS=RBO.setProperty('','ORDNO',ORDNO)
STATUS=RBO.setProperty('','RelDate',OCONV(ORR.DATE,'D2/'))
STATUS=RBO.setProperty('','ReqDate',OCONV(ORR.REQ.DATE,'D2/'))
STATUS=RBO.setProperty('','ShpDate',OCONV(ORR.DUE.DATE,'D2/'))
STATUS=RBO.setProperty('','CustRel',ORR.CUST.REL)
STATUS=RBO.setProperty('','RELNO',RELNO)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999
RETURN

