SUBROUTINE GLFRM_VALIDATE_CATG_SUB
********************************************************************************
*   Program name :- GLFRM_VALIDATE_CATG_SUB
*   Created:- 6/24/2003
*   AUTHOR - ABED ALI
*------------------------------------------------------------------------------*
* 
* In Properties:
* --------------
*  Cono,CATG_CODE,SUB_CATG
*
* Out Properties:
* ---------------
*  AcctCodes,AcctDescs
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE GLS.CPYLIB CATG.AND.SUB
$INCLUDE PMC.CPYLIB COA
* Insert method code here
ERRMSG = ""

OPEN '', 'COA' TO COA ELSE
	ERRMSG = 'COA FILE IS MISSING'
    	GOTO 99999
END

OPEN '', 'CATG.AND.SUB' TO CATG.AND.SUB ELSE
	ERRMSG = 'CATG.AND.SUB FILE IS MISSING'
    	GOTO 99999
END

STATUS = RBO.getProperty('','Cono',CONO)
STATUS = RBO.getProperty('','AT_TYPE',AT.TYPE)
STATUS = RBO.getProperty('','CATG_CODE',CATG.CODE)
STATUS = RBO.getProperty('','SUB_CATG',SUB.CATG)

MATREAD CAS.REC FROM CATG.AND.SUB, CONO:CATG.CODE:SUB.CATG ELSE MAT CAS.REC = ""
IF CAS.ACCT = "" THEN
	ERRMSG = "THERE ARE NO ACCOUNTS FOR THIS CATEGORY AND SUB COMBINATION"
	GOSUB 99999
END

ACCT.CODE = ""
ACCT.DESC = ""
CNT = 1
SELECT COA
	DATA = 1
   	LOOP
      		READNEXT ID ELSE DATA = 0
   	WHILE DATA DO
		IF ID[1,3] = CONO THEN
			MATREAD COA.REC FROM COA, ID ELSE
				MAT COA.REC = ""
       		END
			IF ((COA.TYPE = AT.TYPE ) AND (COA.CATG = CATG.CODE) AND (COA.SUB.CATG = SUB.CATG)) THEN
				ACCT.CODE<1,CNT> = ID[4,99]
				ACCT.DESC<1,CNT> = ID[4,99] : "-" : COA.DESC
				CNT += 1
			END	
		END
	REPEAT

STATUS = RBO.setProperty('','AcctCodes',ACCT.CODE)
STATUS = RBO.setProperty('','AcctDescs',ACCT.DESC)
STATUS = RBO.setProperty('','SUB_C',CAS.CURR)
STATUS = RBO.setProperty('','SUB_F',CAS.FIX)
STATUS = RBO.setProperty('','SUB_P',CAS.PROD)

* End of method code
99999
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','ServerStatus','E')
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END ELSE
       STATUS = RBO.setProperty('','ServerStatus','')
       STATUS = RBO.setProperty('','ServerMessage','')
  END 
RETURN


