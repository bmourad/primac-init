   SUBROUTINE JCS_EOM_POST_LB(MAT EOM.ACCT.REC,MAT JOB.REC,TRANFILE,REJECTFILE,WIPFILE,ERRMSG)
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JCS.EOM.POST.LB
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
* DESCRIPTION -
* TASK
* Process JOB.TIME records for EOD/EOP Process.
*ENDDOC
*********************************************************************
$INCLUDE JCS.CPYLIB EOM.ACCT
$INCLUDE PMC.CPYLIB POST.REJECTS
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TIME
$INCLUDE PMC.CPYLIB EOM.TRANS
$INCLUDE PMC.CPYLIB WIP.SALES.STATS
$INCLUDE CPYLIB CHAR
   MAT ETR.REC = ''
OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE IS MISSING';GOTO 9999
OPEN '','JOB.TIME' TO JOB.TIME ELSE ERRMSG='JOB.TIME FILE IS MISSING';GOTO 9999
OPEN '','JOB.TIME.TAG' TO JOB.TIME.TAG ELSE ERRMSG='JOB.TIME.TAG FILE IS MISSING';GOTO 9999
OPEN '',TRANFILE TO EOM.TRANS ELSE ERRMSG=TRANFILE:' FILE MISSING';GOTO 9999
OPEN '',REJECTFILE TO POST.REJECTS ELSE ERRMSG=REJECTFILE:' FILE IS MISSING';GOTO 9999
OPEN '',WIPFILE TO WIP.SALES.STATS ELSE ERRMSG=WIPFILE:' FILE IS MISSING';GOTO 9999
   MCNT = COUNT(JOB.LB.SEQ,VM) + (JOB.LB.SEQ # '')
   CHECK.WIP = 0; NEW.REC = 0
   FOR M = 1 TO MCNT
      DPNO = JOB.LB.DEPT<1,M>; CCNO = JOB.LB.CCTR<1,M>
      GLS.DEPT = DPNO[1,2]
      LB.ID = CONO:JOBNO:"!":DPNO:"!":CCNO:"!"
      FOR S = 1 TO JOB.LB.SEQ<1,M>
         JLB.ID = LB.ID:S
         MATREADU JLB.REC FROM JOB.TIME, JLB.ID ELSE
            MAT PRR.REC = ''
            PRR.JOB = JOBNO
            PRR.FILE = 'JOB.TIME'
            PRR.ID = JLB.ID[4,999]
            PRR.ERR = 'CANNOT LOCATE'
            PRR.SEQ = PRR.SEQ + 1
            MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
            GOTO 800
         END
         WIP.COST = JLB.WIP
         BEGIN CASE
         CASE JLB.MON = ""
            GOTO 800
         CASE JLB.MON<1,1> # PERIOD OR (JLB.GLA.DATE<1,1> # "" AND JLB.GLA.DATE<1,1> # "P")
            WCNT = COUNT(WIP.COST,VM) + 1
            WPTR = COUNT(WIP.COST<1,1>,SVM) + 1
            FOR W = 1 TO WPTR
               CHECK.WIP = CHECK.WIP + WIP.COST<1,1,W>
            NEXT W
            GOTO 600
         CASE WIP.COST = ""
            WCNT = 0; WPTR = 0
            WIP.CNTR = 0
         CASE JLB.COST < 0
            WCNT = COUNT(WIP.COST,VM) + 1
            WPTR = COUNT(WIP.COST<1,1>,SVM) + 1
            TOT.WIP = 0
            FOR W = 1 TO WPTR
               TOT.WIP = TOT.WIP + WIP.COST<1,1,W>
            NEXT W
            CHECK.WIP = CHECK.WIP + TOT.WIP
            FOR I = 2 TO WCNT
               IF JLB.MON<1,I> = PERIOD AND (JLB.GLA.DATE<1,I> = "" OR JLB.GLA.DATE<1,I> = "P") THEN
                  FOR W = 1 TO WPTR
                     TOT.WIP = TOT.WIP + WIP.COST<1,I,W>
                  NEXT W
                  JLB.GLA.DATE<1,I> = "P"
               END
            NEXT I
            IF TOT.WIP = 0 THEN
               WIP.CNTR = 2
               WCNT = 0; WPTR = 0
               WIP.COST = ""
            END ELSE
               WIP.CNTR = 1
            END
         CASE 1
            WCNT = COUNT(WIP.COST,VM) + 1
            WPTR = COUNT(WIP.COST<1,1>,SVM) + 1
            WIP.CNTR = 1
            FOR W = 1 TO WPTR
               CHECK.WIP = CHECK.WIP + WIP.COST<1,1,W>
            NEXT W
         END CASE
         WRITE "" ON JOB.TIME.TAG, JLB.ID
         MATWRITE JLB.REC ON JOB.TIME, JLB.ID
         COG.COST = JLB.DCOST<1,1>; COG.COST<2> = JLB.DCOST<1,2>
         COG.COST<3> = JLB.DCOST<1,3>; COG.COST<4> = JLB.DCOST<1,4>
         FOR W = 1 TO 4
            BEGIN CASE
            CASE COG.COST<W> + 0 = 0 AND WIP.COST<1,1,W> + 0 = 0
               GOTO 500
            CASE WIP.COST<1,1,W> + 0 = 0
               BEGIN CASE
               CASE LB.COG.LEVEL<W> < 1
                  DB.ID = GEN.DIV : GEN.DEPT : GEN.CCTR : LB.COG.ACCT<W>
               CASE LB.COG.LEVEL<W> < 2
                  DB.ID = JLB.DIV : GEN.DEPT : GEN.CCTR : LB.COG.ACCT<W>
               CASE LB.COG.LEVEL<W> < 3
                  DB.ID = JLB.DIV : GLS.DEPT : GEN.CCTR : LB.COG.ACCT<W>
               CASE 1
                  DB.ID = JLB.DIV : GLS.DEPT : CCNO : LB.COG.ACCT<W>
               END CASE
               WIP.AMT = COG.COST<W>
            CASE 1
               BEGIN CASE
               CASE LB.WIP.LEVEL<W> < 1
                  DB.ID = GEN.DIV : GEN.DEPT : GEN.CCTR : LB.WIP.ACCT<W>
               CASE LB.WIP.LEVEL<W> < 2
                  DB.ID = JLB.DIV : GEN.DEPT : GEN.CCTR : LB.WIP.ACCT<W>
               CASE LB.WIP.LEVEL<W> < 3
                  DB.ID = JLB.DIV : GLS.DEPT : GEN.CCTR : LB.WIP.ACCT<W>
               CASE 1
                  DB.ID = JLB.DIV : GLS.DEPT : CCNO : LB.WIP.ACCT<W>
               END CASE
               WIP.AMT = WIP.COST<1,1,W>
            END CASE
            BEGIN CASE
            CASE LB.APL.LEVEL<W> < 1
               CR.ID = GEN.DIV : GEN.DEPT : GEN.CCTR : LB.APL.ACCT<W>
            CASE LB.APL.LEVEL<W> < 2
               CR.ID = JLB.DIV : GEN.DEPT : GEN.CCTR : LB.APL.ACCT<W>
            CASE LB.APL.LEVEL<W> < 3
               CR.ID = JLB.DIV : GLS.DEPT : GEN.CCTR : LB.APL.ACCT<W>
            CASE 1
               CR.ID = JLB.DIV : GLS.DEPT : CCNO : LB.APL.ACCT<W>
            END CASE
            GOSUB 1000
            JLB.GLA.DATE<1,1> = "P"
500      NEXT W
         GOSUB 2000
600      FOR I = 2 TO WCNT
            IF JLB.MON<1,I> # PERIOD OR (JLB.GLA.DATE<1,I> # "" AND JLB.GLA.DATE<1,I> # "P") THEN
               GOTO 700
            END
            WRITE "" ON JOB.TIME.TAG, JLB.ID ;* T21377
            WIP.CNTR = 1
            FOR W = 1 TO WPTR
               IF JLB.FNGD<1,I> = "*" THEN
                  BEGIN CASE
                  CASE WIP.CLR.LEVEL < 1
                     DB.ID = GEN.DIV : GEN.DEPT : GEN.CCTR : WIP.CLR.ACCT
                  CASE WIP.CLR.LEVEL < 2
                     DB.ID = JLB.DIV : GEN.DEPT : GEN.CCTR : WIP.CLR.ACCT
                  CASE WIP.CLR.LEVEL < 3
                     DB.ID = JLB.DIV : GLS.DEPT : GEN.CCTR : WIP.CLR.ACCT
                  CASE 1
                     DB.ID = JLB.DIV : GLS.DEPT : CCNO : WIP.CLR.ACCT
                  END CASE
               END ELSE
                  BEGIN CASE
                  CASE LB.COG.LEVEL<W> < 1
                     DB.ID = GEN.DIV : GEN.DEPT : GEN.CCTR : LB.COG.ACCT<W>
                  CASE LB.COG.LEVEL<W> < 2
                     DB.ID = JLB.DIV : GEN.DEPT : GEN.CCTR : LB.COG.ACCT<W>
                  CASE LB.COG.LEVEL<W> < 3
                     DB.ID = JLB.DIV : GLS.DEPT : GEN.CCTR : LB.COG.ACCT<W>
                  CASE 1
                     DB.ID = JLB.DIV : GLS.DEPT : CCNO : LB.COG.ACCT<W>
                  END CASE
               END
               BEGIN CASE
               CASE LB.WIP.LEVEL<W> < 1
                  CR.ID = GEN.DIV : GEN.DEPT : GEN.CCTR : LB.WIP.ACCT<W>
               CASE LB.WIP.LEVEL<W> < 2
                  CR.ID = JLB.DIV : GEN.DEPT : GEN.CCTR : LB.WIP.ACCT<W>
               CASE LB.WIP.LEVEL<W> < 3
                  CR.ID = JLB.DIV : GLS.DEPT : GEN.CCTR : LB.WIP.ACCT<W>
               CASE 1
                  CR.ID = JLB.DIV : GLS.DEPT : CCNO : LB.WIP.ACCT<W>
               END CASE
               WIP.AMT = 0 - WIP.COST<1,I,W>
               JLB.GLA.DATE<1,I> = "P"
               GOSUB 1000
            NEXT W
700      NEXT I
         MATWRITE JLB.REC ON JOB.TIME, JLB.ID
800      RELEASE JOB.TIME, JLB.ID
      NEXT S
   NEXT M
   CHK.WIP = 0
   FOR W = 1 TO 4
      CHK.WIP = CHK.WIP + JOB.LB.WIP<1,2,W> + JOB.LB.WIP<1,3,W>
   NEXT W
   IF CHECK.WIP <> CHK.WIP THEN
      MAT PRR.REC = ''
      PRR.JOB = JOBNO
      PRR.ERR = 'TOTAL LB = ':CHK.WIP:' AND SEQ SUM = ':CHECK.WIP
      PRR.SEQ = PRR.SEQ + 1
      MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
   END
   GOTO 9999
1000 BEGIN CASE
   CASE WIP.AMT > 0
      ETR.DB = CONO : DB.ID : JOBNO : "!LB-D*" : NEW.REC
      ETR.CR = CONO : CR.ID : JOBNO : "!LB-C*" : NEW.REC
   CASE WIP.AMT + 0 < 0
      ETR.DB = CONO : DB.ID : JOBNO : "!LB-C*" : NEW.REC
      ETR.CR = CONO : CR.ID : JOBNO : "!LB-D*" : NEW.REC
   CASE 1
      GOTO 1999
   END CASE
   MATREAD ETR.REC FROM EOM.TRANS, ETR.DB ELSE
      MAT ETR.REC = ''
      ETR.AMT = 0
      ETR.REF = JOB.CUST
   END
   ETR.CNTR = ETR.CNTR + WIP.CNTR
   WIP.CNTR = 0
   ETR.AMT = ETR.AMT + WIP.AMT
   IF NO.DETAIL OR WIP.AMT + 0 = 0 THEN
      MATWRITE ETR.REC ON EOM.TRANS, ETR.DB
      MATREAD ETR.REC FROM EOM.TRANS, ETR.CR ELSE
         MAT ETR.REC = ''
         ETR.AMT = 0
         ETR.REF = JOB.CUST
      END
      ETR.AMT = ETR.AMT - WIP.AMT
      MATWRITE ETR.REC ON EOM.TRANS, ETR.CR
      GOTO 1999
   END
   LOCATE JLB.SEQ IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
   INS JLB.SEQ BEFORE ETR.TRAN<1,PTR>
   INS JLB.DATE BEFORE ETR.DATE<1,PTR>
   INS WIP.AMT BEFORE ETR.TAMT<1,PTR>
   MATWRITE ETR.REC ON EOM.TRANS, ETR.DB
   MATREAD ETR.REC FROM EOM.TRANS, ETR.CR ELSE
      MAT ETR.REC = ''
      ETR.AMT = 0
      ETR.REF = JOB.CUST
   END
   ETR.AMT = ETR.AMT - WIP.AMT
   LOCATE JLB.SEQ IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
   INS JLB.SEQ BEFORE ETR.TRAN<1,PTR>
   INS JLB.DATE BEFORE ETR.DATE<1,PTR>
   INS (0 - WIP.AMT) BEFORE ETR.TAMT<1,PTR>
   MATWRITE ETR.REC ON EOM.TRANS, ETR.CR
   IF PTR > 99 THEN NEW.REC = NEW.REC + 1
1999 RETURN
*--------
*
2000  * GET AND UPDATE THE WIP.SALES.STATS RECORD
*
*--------
   WSS.KEY = CONO:"_J_":JOBNO:"_":PERIOD:"XX_0_0"
   MATREADU WSS.REC FROM WIP.SALES.STATS, WSS.KEY ELSE
      MAT WSS.REC=''
   END
   IF JLB.DCOST<1,1> > JLB.WIP<1,1,1> THEN
      WSS.LABOR.DCOST = WSS.LABOR.DCOST + (JLB.DCOST<1,1> - JLB.WIP<1,1,1>)
   END
   IF JLB.DCOST<1,2> > JLB.WIP<1,1,2> THEN
      WSS.LABOR.FFOH = WSS.LABOR.FFOH + (JLB.DCOST<1,2> - JLB.WIP<1,1,2>)
   END
   IF JLB.DCOST<1,3> > JLB.WIP<1,1,3> THEN
      WSS.LABOR.VFOH = WSS.LABOR.VFOH + (JLB.DCOST<1,3> - JLB.WIP<1,1,3>)
   END
   IF JLB.DCOST<1,4> > JLB.WIP<1,1,4> THEN
      WSS.LABOR.SAOH = WSS.LABOR.SAOH + (JLB.DCOST<1,4> - JLB.WIP<1,1,4>)
   END
   WSS.INVOICE.DATE = DATE()
   WSS.CUST.ID = JOB.CUST
   WSS.SLSM.ID = JOB.SLSMN
   WSS.SALES.CODE = JOB.SALES.CODE
   WSS.JOB.CAT = JOB.CATG
   WSS.MASTER = JOB.MASTER
   WSS.NO.COLORS = JOB.COLORS
   WSS.POSTING.MONTH = PERIOD
   MATWRITE WSS.REC ON WIP.SALES.STATS, WSS.KEY
9999 RETURN
****************************************************************************
****************************************************************************
******************************** EOP ***************************************
****************************************************************************
****************************************************************************

