SUBROUTINE ORM_GetData
********************************************************************************
*   Program name :- ORM_GetData
*   Created:- 01/09/2003
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
*
*
* Out Properties:
* ---------------
*
********************************************************************************

* Insert method code here
$INCLUDE WWINSERT RBO.H
$INCLUDE JCS.CPYLIB JOB
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE ARS.CPYLIB OPEN.RECV
$INCLUDE JCS.CPYLIB INVOICE
$INCLUDE ARS.CPYLIB MANUAL.INVOICE
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB TERMS           ; * T25764
$INCLUDE CPYLIB CHAR
DIM OVC.REC(60)
EQU OVC.DATE    TO OVC.REC(1)
EQU OVC.PO.NO   TO OVC.REC(33)
EQU OVC.TERMS   TO OVC.REC(38)
EQU OVC.DUE.DATE   TO OVC.REC(39)

ERRMSG = ''


* Open files
OPEN '','COMPANY' TO COMPANY ELSE
   ERRMSG='Cannot open COMPANY file!'
   GOTO 93000
END
OPEN '','CONTROL' TO CONTROL ELSE
   ERRMSG='Cannot open CONTROL file!'
   GOTO 93000
END
OPEN '','OPEN.RECV' TO OPEN.RECV ELSE ERRMSG = 'OPEN.RECV FILE IS MISSING';GOTO 93000
O.R.H = 1
OPEN "","OPEN.RECV.HIST" TO OPEN.RECV.HIST ELSE O.R.H = 0
OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING" ; GOTO 93000
OPEN '','PREFIX' TO PREFIX ELSE ERRMSG = 'PREFIX FILE IS MISSING' ; GOTO 93000
OPEN '','INVOICE' TO INVOICE ELSE ERRMSG = 'INVOICE FILE IS MISSING' ; GOTO 93000
OPEN '','MANUAL.INVOICE' TO MANUAL.INVOICE ELSE ERRMSG = 'MANUAL.INVOICE FILE IS MISSING' ; GOTO 93000
OPEN '','TERMS' TO TERMS ELSE ERRMSG = 'TERMS FILE IS MISSING' ; GOTO 93000
* Get Company
STATUS = RBO.getProperty('','ID',ID)
CONO=ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
   ERRMSG='Cannot read Company record for ':CONO; GOTO 93000
END
IF CO.JCS = "Y" THEN
   OPEN '','JOB' TO JOB ELSE ERRMSG = "JOB FILE IS MISSING" ; GOTO 93000
END
IF CO.OPS = "Y" THEN
   OPEN '','ORDER' TO ORDER ELSE ERRMSG = "ORDER FILE IS MISSING" ; GOTO 93000
   OPEN '','ORDER.INVOICE' TO ORDER.INVOICE ELSE ERRMSG = "ORDER.INVOICE FILE IS MISSING" ; GOTO 93000
END
* Get all properties

MAT OR.REC = '' ; MAT CUST.REC = ''; MAT JOB.REC = ''; MAT ORD.REC = ''
MAT MIV.REC = ''; MAT IVC.REC = ''; MAT OVC.REC = ''; MAT TERMS.REC = ''
MATREADU OR.REC FROM OPEN.RECV, ID LOCKED
   ERRMSG = 'OPEN.RECV record is locked by user - ':GETUSERNAME(STATUS())
   GOTO 93000
END ELSE
   IF O.R.H THEN
      MATREADU OR.REC FROM OPEN.RECV.HIST, ID LOCKED
         ERRMSG = 'OPEN.RECV record is locked by user - ':GETUSERNAME(STATUS())
         GOTO 93000
      END ELSE
         ERRMSG = "CANNOT LOCATE OPEN.RECV - " : ID
         GOTO 93000
      END
      O.R.H = 2
   END ELSE
      ERRMSG = "CANNOT LOCATE OPEN.RECV - " : ID
      GOTO 93000
   END
END
DIV.CODE = OR.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
IF ERRMSG # '' THEN
   GOTO 93000
END
INV.NO = ID[4,8]
INVOICE.TYPE = INV.NO[LEN(INV.NO)-1,2]
IF INVOICE.TYPE # "MI" THEN
   MATREAD IVC.REC FROM INVOICE,CONO:INV.NO THEN
     INVOICE.TYPE = 'I'
   END ELSE
     IF CO.OPS = 'Y' THEN
        MATREAD OVC.REC FROM ORDER.INVOICE,CONO:INV.NO THEN INVOICE.TYPE="O"
     END
   END
END ELSE
   MATREAD MIV.REC FROM MANUAL.INVOICE,CONO:INV.NO ELSE INVOICE.TYPE=""
END
STATUS = RBO.getProperty('','OR_INV_DATE',OR.INV.DATE)
STATUS = RBO.getProperty('','OR_PO',OR.PO)
MIV.PO = OR.PO; IVC.PO.NO = OR.PO; OVC.PO.NO = OR.PO
OR.INV.DATE = ICONV(OR.INV.DATE,'D2/')
IF OR.ORDER.FLG = "Y" THEN
   IF CO.OPS = 'Y' AND OR.JOB # '' THEN
      MATREAD ORD.REC FROM ORDER, CONO:OR.JOB ELSE MAT ORD.REC = ''
   END
END ELSE
   IF CO.JCS = 'Y' AND OR.JOB # '' THEN
      MATREAD JOB.REC FROM JOB, CONO:OR.JOB ELSE MAT JOB.REC = ''
   END
END
TERMS.CODE = ''
BEGIN CASE
   CASE INVOICE.TYPE = "MI"
      TERMS.CODE = MIV.TERMS
   CASE INVOICE.TYPE = "O"
      TERMS.CODE = OVC.TERMS
   CASE INVOICE.TYPE = "I"
      TERMS.CODE = IVC.TERMS
END CASE
IF TERMS.CODE # "" THEN
   MATREAD TERMS.REC FROM TERMS, CONO : TERMS.CODE ELSE
      MAT TERMS.REC = ""
   END
   OR.DUE.DATE = OR.INV.DATE + TERMS.DUE.DAYS
   BEGIN CASE
      CASE INVOICE.TYPE = "MI"
         MIV.DUE.DATE = OR.DUE.DATE
      CASE INVOICE.TYPE = "O"
         OVC.DUE.DATE = OR.DUE.DATE
      CASE INVOICE.TYPE = "I"
         IVC.DUE.DATE = OR.DUE.DATE
   END CASE
END
IF O.R.H < 2 THEN
   MATWRITE OR.REC ON OPEN.RECV, CONO:INV.NO
END ELSE
   MATWRITE OR.REC ON OPEN.RECV.HIST, CONO:INV.NO
END
IF OR.ORDER.FLG = "Y" THEN
   IF CO.OPS = 'Y' AND OR.PO # "" AND ORD.PO # "" AND OR.PO # ORD.PO THEN
      FND = 1
      MATREADU ORD.REC FROM ORDER, CONO:OR.JOB ELSE
         RELEASE ORDER, CONO:OR.JOB
         FND = 0
      END
      IF FND THEN
         ORD.PO = OR.PO
         MATWRITE ORD.REC ON ORDER, CONO:OR.JOB
      END
   END
END ELSE
   IF CO.JCS = 'Y' AND OR.PO # "" AND JOB.CUST.PO # "" AND OR.PO # JOB.CUST.PO THEN
      FND = 1
      MATREADU JOB.REC FROM JOB, CONO:OR.JOB ELSE
         RELEASE JOB, CONO:OR.JOB
         FND = 0
      END
      IF FND THEN
         JOB.CUST.PO = OR.PO
         MATWRITE JOB.REC ON JOB, CONO:OR.JOB
      END
   END
END
IF INVOICE.TYPE="MI" THEN MATWRITE MIV.REC ON MANUAL.INVOICE,CONO:INV.NO
IF INVOICE.TYPE="I" THEN MATWRITE IVC.REC ON INVOICE,CONO:INV.NO
IF INVOICE.TYPE="O" AND CO.OPS = 'Y' THEN
   MATWRITE OVC.REC ON ORDER.INVOICE,CONO:INV.NO
END



*

GOTO 99999

93000 
IF ERRMSG THEN
   CALL RBO_ERROR_SUB(ERRMSG)
END

* End of method code
99999 
RETURN
