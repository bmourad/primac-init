SUBROUTINE JOBINQ_SP_GetData
********************************************************************************
*   Program name :- JOBINQ_SP_GetData
*   Created:- 12/17/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'SP Detail' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.SHIP
$INCLUDE JCS.CPYLIB OPERATION
$INCLUDE PMC.CPYLIB SHIP.VIA
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(SHIP.VIA,0)=0 THEN
   OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
      ERRMSG<1,-1>='SHIP.VIA FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(JOB.SHIP,0)=0 THEN
   OPEN '','JOB.SHIP' TO JOB.SHIP ELSE
      ERRMSG<1,-1>='JOB.SHIP FILE IS MISSING'
   END
END
IF FILEINFO(OPERATION,0)=0 THEN
   OPEN '','OPERATION' TO OPERATION ELSE
      ERRMSG<1,-1>='OPERATION FILE IS MISSING'
   END
END
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM TRANS.REC(14)
EQU TRAN.VIA         TO TRANS.REC(1)
EQU TRAN.INV         TO TRANS.REC(2)
EQU TRAN.FRT         TO TRANS.REC(3)
EQU TRAN.NO          TO TRANS.REC(4)
EQU TRAN.TYPE        TO TRANS.REC(5)
EQU TRAN.DATE        TO TRANS.REC(6)
EQU TRAN.QTY         TO TRANS.REC(7)
EQU TRAN.COST        TO TRANS.REC(8)
EQU TRAN.SALE        TO TRANS.REC(9)
EQU TRAN.JOB         TO TRANS.REC(10)
EQU TRAN.VIA.DESC    TO TRANS.REC(11)
EQU TRAN.OPER        TO TRANS.REC(12)
EQU TRAN.OPER.DESC   TO TRANS.REC(13)
EQU TRAN.PERIOD      TO TRANS.REC(14)
MAT TRANS.REC = ''
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)
STATUS=RBO.getProperty('','DEPT',DEPT)
STATUS=RBO.getProperty('','CCTR',CCTR)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='SP'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046 v 
  STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
  IF ERRMSG # "" THEN GOTO 93000
*T28046 v


   SUB.CNT=DCOUNT(JOB.SUBS,VM)+1
END ELSE SUB.CNT = 1
PTR=1
LOOP
   LOCATE DEPT IN JOB.SP.DEPT<1>,PTR SETTING FND ELSE
      PTR = 0
   END
   IF PTR # 0 THEN
      IF CCTR = JOB.SP.CCTR<1,FND> THEN
         PTR = FND
         FND = 0
      END ELSE
         PTR = FND + 1
      END
   END
WHILE PTR AND FND DO
REPEAT
IF NOT(PTR) THEN
   ERRMSG='Cannot locate Dept/Cctr ':DEPT:' ':CCTR:' in Job record!'
   GOTO 93000
END
MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT ELSE DEPT.DESC = UNKNOWN
MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR ELSE CCTR.DESC = UNKNOWN

LN = 1; OLN = 1; LINES = JOB.SP.SEQ<1,PTR,1>
SP.ID = CONO:JOB_NO:"!":JOB.SP.DEPT<1,PTR>:"!":JOB.SP.CCTR<1,PTR>:"!"
GOSUB 100
FOR K = 2 TO SUB.CNT
   SP.ID = CONO:JOB.SUBS<1,K-1>:"!":JOB.SP.DEPT<1,PTR>:"!":JOB.SP.CCTR<1,PTR>:"!"
   OLN = LINES + 1; LINES = LINES + JOB.SP.SEQ<1,PTR,K>
   GOSUB 100
NEXT K

STATUS=RBO.setProperty('','DEPT_DESC',DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_DESC',CCTR.DESC)
STATUS=RBO.setProperty('','OPER',TRAN.OPER)
STATUS=RBO.setProperty('','SHP_TRAN',TRAN.NO)
STATUS=RBO.setProperty('','DATE',TRAN.DATE)
STATUS=RBO.setProperty('','PERIOD',TRAN.PERIOD)
STATUS=RBO.setProperty('','VIA',TRAN.VIA)
STATUS=RBO.setProperty('','TYPE',TRAN.TYPE)
STATUS=RBO.setProperty('','RCLS_JOB',TRAN.JOB)
STATUS=RBO.setProperty('','FRT',TRAN.FRT)
STATUS=RBO.setProperty('','COST',TRAN.COST)
STATUS=RBO.setProperty('','QTY',TRAN.QTY)
STATUS=RBO.setProperty('','INV',TRAN.INV)
STATUS=RBO.setProperty('','SALE',TRAN.SALE)
STATUS=RBO.setProperty('','OPER_NAME',TRAN.OPER.DESC)
GOTO 99999

100*
FOR I = OLN TO LINES
   MATREAD JSP.REC FROM JOB.SHIP, SP.ID:(I-OLN+1) ELSE MAT JSP.REC = ""
   MATREAD SHIP.VIA.REC FROM SHIP.VIA , CONO:JSP.VIA ELSE
      SHPV.DESC = UNKNOWN
   END
   MATREAD OPER.REC FROM OPERATION, CONO:JSP.OPER ELSE
      OPER.DESC = UNKNOWN
   END
   TRAN.VIA<1,I> = JSP.VIA
   TRAN.FRT<1,I> = JSP.FRT.NO
   TRAN.NO<1,I> = JSP.SEQ
   TRAN.TYPE<1,I> = JSP.TYPE
   TRAN.INV<1,I> = JSP.INV
   TRAN.DATE<1,I> = OCONV(JSP.DATE,'D2/')
   TRAN.QTY<1,I> = JSP.QTY'R#8'
   TRAN.COST<1,I> = OCONV(JSP.COST,'MD2,')
   TRAN.SALE<1,I> = OCONV(JSP.SALE,'MD2,')
   TRAN.JOB<1,I> = JSP.RC.JOB
   TRAN.VIA.DESC<1,I> = SHPV.DESC
   TRAN.OPER<1,I> = JSP.OPER
   TRAN.OPER.DESC<1,I> = OPER.DESC
   TRAN.PERIOD<1,I> = JSP.MON<1,1>
NEXT I
RETURN

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN
