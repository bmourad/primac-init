SUBROUTINE VENDHI_POSTREAD_SUB (ERRMSG,IN_PARAM,OUT_PARAM)
*************************************************************************
* BY          -EDVARD PITKA
* IN_PARAM          - MULTIVALUED LIST OF SQV.ID
* OUT_PARAM<1>      - VOUCHER NO
*          <2>      - SQV.INV
*          <3>      - SQV.INV.DATE
*          <4>      - SQV.DUE.DATE
*          <5>      - SQV.GRS.AMT
*          <6>      - SQV.DSC.AMT
*          <7>      - SQV.DSC.PAID
*          <8>      - OPN.BAL
*          <9>      - SQV.CHK.NO
*************************************************************************
*
$INSERT APS.CPYLIB SQV
$INSERT CPYLIB CHAR
*
OPEN "","SQV" TO SQV ELSE
  ERRMSG="SQV FILE IS MISSING"
  GOTO 99999
END
*
*
VOUCH.NO = ""
IVC.NO = ""
IVC.DATE = ""
IVC.DUE.DATE = ""
GROSS.AMT = ""
DSC.AMT = ""
DSC.PAID = ""
OPN.BAL = ""
CHK.NO = ""
SQV.ID=IN_PARAM
ID.CNT=DCOUNT(SQV.ID,VM)
CNT=0
FOR II=1 TO ID.CNT
  MATREAD SQV.REC FROM SQV,SQV.ID<1,II> ELSE CONTINUE
  CNT+=1
  VAL.CNT=DCOUNT(SQV.CHK.NO,VM)
  VOUCH.NO<1,CNT>=SQV.ID<1,II>[4,6]"L#6"
  IVC.NO<1,CNT>=SQV.INV"L#10"
  IVC.DATE<1,CNT>=OCONV(SQV.INV.DATE,"D2/")
  IVC.DUE.DATE<1,CNT>=OCONV(SQV.DUE.DATE,"D2/")
  IF SQV.DUE.DATE="ADVANCE" THEN
    GROSS.AMT<1,CNT>=OCONV(SQV.GRS.AMT * (-1), "MD2")
  END ELSE
    GROSS.AMT<1,CNT>=OCONV(SQV.GRS.AMT, "MD2")
  END
  IF SQV.DUE.DATE="ADVANCE" THEN
    DSC.AMT<1,CNT>=OCONV(SQV.DSC.AMT * (-1), "MD2")
  END ELSE
    DSC.AMT<1,CNT>=OCONV(SQV.DSC.AMT, "MD2")
  END
  IF SQV.DUE.DATE="ADVANCE" THEN
    OPN.BAL<1,CNT>=SUM(SQV.PAID.AMT) + SUM(SQV.DSC.PAID)
    OPN.BAL<1,CNT>=OCONV(OPN.BAL<1,CNT>, "MD2,")
  END ELSE
  IF SQV.CHK.NO<1,VAL.CNT> # 'DELETE' THEN
      OPN.BAL<1,CNT>=SQV.GRS.AMT - (SUM(SQV.PAID.AMT) + SUM(SQV.DSC.PAID))
    END ELSE
      OPN.BAL<1,CNT>=0
    END
    IF OPN.BAL<1,CNT> # 0 THEN
      OPN.BAL<1,CNT>=OPN.BAL<1,CNT> - SQV.DSC.AMT
    END
    OPN.BAL<1,CNT>=OCONV(OPN.BAL<1,CNT>, "MD2,")
  END
  CHK.NO<1,CNT>=SQV.CHK.NO<1,VAL.CNT>

NEXT II
OUT_PARAM =  VOUCH.NO
OUT_PARAM<2> =  IVC.NO
OUT_PARAM<3> =  IVC.DATE
OUT_PARAM<4> =  IVC.DUE.DATE
OUT_PARAM<5> =  GROSS.AMT
OUT_PARAM<6> =  DSC.AMT
OUT_PARAM<7> =  DSC.PAID
OUT_PARAM<8> =  OPN.BAL
OUT_PARAM<9> =  CHK.NO
99999*
RETURN

