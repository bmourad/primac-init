SUBROUTINE JOBINQ_MT_GetData
********************************************************************************
*   Program name :- JOBINQ_MT_GetData
*   Created:- 12/13/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'MT' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TOT.REC
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR

DEFFUN CalcStkQty(COST.QTY,MAT INV.CNV.REC,ROND,LN)
;* open files

ERRMSG=''
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(INVENTORY,0)=0 THEN
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM CCTR(13)
EQU CCTR.NO      TO CCTR(1)
EQU PROD.NO      TO CCTR(2)
EQU PROD.NAME    TO CCTR(3)
EQU PROD.WHSE    TO CCTR(4)
EQU PROD.TYPE    TO CCTR(5)
EQU PROD.QTY     TO CCTR(6)
EQU PROD.UM      TO CCTR(7)
EQU PROD.DATE    TO CCTR(8)
EQU PROD.COST    TO CCTR(9)
EQU PROD.SALE    TO CCTR(10)
EQU CCTR.NAME    TO CCTR(11)
EQU PROD.M.WT    TO CCTR(12)
EQU PROD.P.TYPE  TO CCTR(13)
MAT CCTR = ''
DIM OTYPE(5)
OTYPE(1) = 'R,'
OTYPE(2) = 'N,'
OTYPE(3) = 'O,'
OTYPE(4) = 'S,'
OTYPE(5) = 'C'
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='MT'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


END
MAT JOB.TOT.REC = ''
JPTR = 0
JCNT = 0
CNT = COUNT(JOB.MT.DEPT,VM) + (JOB.MT.DEPT # '')
FOR I = 1 TO CNT
   TEMP = JOB.MT.DEPT<1,I>
   LOCATE TEMP IN MT.DEPT.NO<1>,1 SETTING JCNT ELSE
      JPTR = JPTR + 1; JCNT = JPTR
      MT.DEPT.NO<1,JCNT> = TEMP
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:TEMP ELSE DEPT.DESC = UNKNOWN
      MT.DEPT.DESC<1,JCNT> = DEPT.DESC
   END
   MT.DEPT.PTR<1,JCNT,-1> = I
   MT.DEPT.CHG<1,JCNT> = MT.DEPT.CHG<1,JCNT> + JOB.MT.TCOST<1,I,1>
   MT.DEPT.NCHG<1,JCNT> = MT.DEPT.NCHG<1,JCNT> + JOB.MT.TCOST<1,I,2>
   MT.DEPT.CORD<1,JCNT> = MT.DEPT.CORD<1,JCNT> + JOB.MT.TCOST<1,I,3>
   MT.DEPT.SPOIL<1,JCNT> = MT.DEPT.SPOIL<1,JCNT> + JOB.MT.TCOST<1,I,4>
   MT.DEPT.CNCR<1,JCNT> = MT.DEPT.CNCR<1,JCNT> + JOB.MT.TCOST<1,I,5>
   MT.DEPT.COST<1,JCNT> = MT.DEPT.COST<1,JCNT> + JOB.MT.COST<1,I>
   MT.DEPT.SALE<1,JCNT> = MT.DEPT.SALE<1,JCNT> + JOB.MT.SALE<1,I>
NEXT I
FOR I = 1 TO JPTR
   MT.TOT.CHG = MT.TOT.CHG + MT.DEPT.CHG<1,I>
   MT.TOT.NCHG = MT.TOT.NCHG + MT.DEPT.NCHG<1,I>
   MT.TOT.CORD = MT.TOT.CORD + MT.DEPT.CORD<1,I>
   MT.TOT.SPOIL = MT.TOT.SPOIL + MT.DEPT.SPOIL<1,I>
   MT.TOT.CNCR = MT.TOT.CNCR + MT.DEPT.CNCR<1,I>
   MT.TOT.COST = MT.TOT.COST + MT.DEPT.COST<1,I>
   MT.TOT.SALE = MT.TOT.SALE + MT.DEPT.SALE<1,I>
NEXT I
LINES = COUNT(MT.DEPT.NO,VM) + (MT.DEPT.NO # '')
FOR I = 1 TO LINES
   CNT = COUNT(MT.DEPT.PTR<1,I>,SVM) + (MT.DEPT.PTR<1,I> # '')
   FOR J = 1 TO CNT
      PTR = MT.DEPT.PTR<1,I,J>
      CCTR.NO<1,I,J> = JOB.MT.CCTR<1,PTR>
      PROD.NO<1,I,J> = JOB.MT.PROD<1,PTR>
      MATREAD CCTR.REC FROM COST.CNTR, CONO: JOB.MT.CCTR<1,PTR> ELSE MAT CCTR.REC = "";CCTR.DESC = UNKNOWN
      MATREAD INV.REC FROM INVENTORY,CONO:JOB.MT.PROD<1,PTR> ELSE
         MAT INV.REC = ""
         INV.FULL.DESC = UNKNOWN
      END
      $INCLUDE ICSBP INV.UM.CNV
      CCTR.NAME<1,I,J> = CCTR.DESC
      PROD.NAME<1,I,J> = INV.FULL.DESC
      PROD.M.WT<1,I,J> = INV.M.WT
      PROD.P.TYPE<1,I,J> = INV.PAP.TYPE
      PROD.WHSE<1,I,J> = JOB.MT.WHSE<1,PTR>
      PROD.UM<1,I,J> = INV.UNIT<1,2>
      ROND=''; LN=''
      PROD.QTY<1,I,J> = CalcStkQty(JOB.MT.QTY<1,PTR>,MAT INV.CNV.REC,ROND,LN)
      PROD.QTY<1,I,J> = OCONV(PROD.QTY<1,I,J>,ICR.CNV1)
      PROD.DATE<1,I,J> = OCONV(JOB.MT.DATE<1,PTR,1>,'D2/')
      PROD.COST<1,I,J> = OCONV(JOB.MT.COST<1,PTR>,'MD2,')
      PROD.SALE<1,I,J> = OCONV(JOB.MT.SALE<1,PTR>,'MD2,')
      FOR K = 1 TO 5
         IF JOB.MT.TCOST<1,PTR,K> > 0 THEN
            IF PROD.TYPE<1,I,J> = '' THEN
               PROD.TYPE<1,I,J> = OTYPE(K)
            END ELSE
               PROD.TYPE<1,I,J> = PROD.TYPE<1,I,J> : ',' : OTYPE(K)
            END
         END
      NEXT K
   NEXT J
   MT.DEPT.CHG<1,I> = OCONV(MT.DEPT.CHG<1,I>,'MD2,')
   MT.DEPT.NCHG<1,I> = OCONV(MT.DEPT.NCHG<1,I>,'MD2,')
   MT.DEPT.CORD<1,I> = OCONV(MT.DEPT.CORD<1,I>,'MD2,')
   MT.DEPT.SPOIL<1,I> = OCONV(MT.DEPT.SPOIL<1,I>,'MD2,')
   MT.DEPT.COST<1,I> = OCONV(MT.DEPT.COST<1,I>,'MD2,')
   MT.DEPT.SALE<1,I> = OCONV(MT.DEPT.SALE<1,I>,'MD2,')
NEXT I

STATUS=RBO.setProperty('','MT_DEPT_NO',MT.DEPT.NO)
STATUS=RBO.setProperty('','MT_DEPT_CHG',MT.DEPT.CHG)
STATUS=RBO.setProperty('','MT_TOT_CHG',OCONV(MT.TOT.CHG,'MD2'))
STATUS=RBO.setProperty('','MT_DEPT_NCHG',MT.DEPT.NCHG)
STATUS=RBO.setProperty('','MT_TOT_NCHG',OCONV(MT.TOT.NCHG,'MD2,'))
STATUS=RBO.setProperty('','MT_DEPT_CORD',MT.DEPT.CORD)
STATUS=RBO.setProperty('','MT_TOT_CORD',OCONV(MT.TOT.CORD,'MD2,'))
STATUS=RBO.setProperty('','MT_DEPT_SPOIL',MT.DEPT.SPOIL)
STATUS=RBO.setProperty('','MT_TOT_SPOIL',OCONV(MT.TOT.SPOIL,'MD2,'))
STATUS=RBO.setProperty('','MT_DEPT_COST',MT.DEPT.COST)
STATUS=RBO.setProperty('','MT_TOT_COST',OCONV(MT.TOT.COST,'MD2,'))
STATUS=RBO.setProperty('','MT_DEPT_SALE',MT.DEPT.SALE)
STATUS=RBO.setProperty('','MT_TOT_SALE',OCONV(MT.TOT.SALE,'MD2,'))
STATUS=RBO.setProperty('','MT_DEPT_DESC',MT.DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_NO',CCTR.NO)
STATUS=RBO.setProperty('','PROD_COST',PROD.COST)
STATUS=RBO.setProperty('','PROD_SALE',PROD.SALE)
STATUS=RBO.setProperty('','PROD_NO',PROD.NO)
STATUS=RBO.setProperty('','PROD_WHSE',PROD.WHSE)
STATUS=RBO.setProperty('','PROD_UM',PROD.UM)
STATUS=RBO.setProperty('','PROD_QTY',PROD.QTY)
STATUS=RBO.setProperty('','PROD_TYPE',PROD.TYPE)
STATUS=RBO.setProperty('','CCTR_NAME',CCTR.NAME)
STATUS=RBO.setProperty('','PROD_NAME',PROD.NAME)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN

