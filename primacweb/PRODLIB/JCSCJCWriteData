SUBROUTINE JCSCJCWriteData
********************************************************************************
*   Program name :- JCSCJCWriteData
*   Created:- 7/28/2003
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  PMCProperty,JOBNO,CUST_ID
*
* Out Properties:
* ---------------
* WRITE DATA 
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE JCS.CPYLIB JOB
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE ICS.CPYLIB INV.JOB.STATS


* Insert method code here
OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE IS MISSING';GOTO 99999
OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE IS MISSING';GOTO 99999
OPEN '','INV.JOB.STATS' TO INV.JOB.STATS ELSE ERRMSG='INV.JOB.STATS FILE IS MISSING';GOTO 99999


STATUS=RBO.getProperty('','PMCProperty',PMCProperty)
CONO = PMCProperty<1,4>
STATUS = RBO.getProperty('','JOBNO',JOB_NO)
STATUS = RBO.getProperty('','CUST_ID',NEW.CUST)

MATREADU JOB.REC FROM JOB, CONO:JOB_NO ELSE MAT JOB.REC = ""

JOB.ARR = INSERT(JOB.SUBS,1,1,0,JOB.MASTER)
NBR.LPS = DCOUNT(JOB.ARR,@VM)
FOR XX = 1 TO NBR.LPS
	MATREADU JOB.REC FROM JOB, CONO:JOB.ARR<1,XX> ELSE MAT JOB.REC = ""
    	MATREADU CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE MAT CUST.REC = ''
    	LOCATE JOB.ARR<1,XX> IN CUST.JOB<1>,1 SETTING FND ELSE FND = 0
    	IF FND THEN
		QUOTED.AMT = CUST.JOB.BAL<1,FND>
		CUST.JOB = DELETE(CUST.JOB,1,FND,0)
		CUST.JOB.BAL = DELETE(CUST.JOB.BAL,1,FND,0)
		MATWRITE CUST.REC ON CUSTOMER, CONO : JOB.CUST
	END ELSE
		QUOTED.AMT = 0
		RELEASE CUSTOMER, CONO : JOB.CUST
	END
	
	FND = 1
	MATREAD CUST.REC FROM CUSTOMER, CONO : NEW.CUST ELSE FND = 0
	IF FND THEN
		LOCATE JOB.ARR<1,XX> IN CUST.JOB<1>,1 SETTING FND ELSE FND = -1
		CUST.JOB<1,FND,0> = JOB.ARR<1,XX>
		CUST.JOB.BAL<1,FND,0> = QUOTED.AMT
		MATWRITE CUST.REC ON CUSTOMER, CONO : NEW.CUST
	END ELSE
		RELEASE CUSTOMER, CONO : NEW.CUST
	END
	
	JOB.CUST = NEW.CUST
	MATWRITE JOB.REC ON JOB, CONO : JOB.ARR<1,XX>
	MCNT = COUNT(JOB.RESV.MATL,@VM) + (JOB.RESV.MATL # "")
	FOR M = MCNT TO 1 STEP -1
		JSTAT.ID = CONO:JOB.RESV.MATL<1,M>:"!":JOB.RESV.WHSE<1,M>:"!":JOB.ARR<1,XX>
      		MATREADU INV.JS.REC FROM INV.JOB.STATS, JSTAT.ID ELSE
			RELEASE INV.JOB.STATS, JSTAT.ID
			GOTO 599
		END
      		IJS.JOB.CUST = JOB.CUST
		MATWRITE INV.JS.REC ON INV.JOB.STATS, JSTAT.ID
	599 NEXT M
 NEXT XX
STATUS = RBO.setProperty('','ServerMessage',"CODE successful")
RETURN

* End of method code
99999*
STATUS =  RBO.setProperty('','ServerStatus',1)
STATUS =  RBO.setProperty('','ServerMessage',ERRMSG)
RETURN

