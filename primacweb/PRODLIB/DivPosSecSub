SUBROUTINE DivPosSecSub (ERRMSG,IN_PARAM,OUT_PARAM)
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - GET.DIV.POS
* BY          - edvard; CBA
* DATE        - 08/21/2001
* DESCRIPTION -
*IN_PARAM: 1. CONO
*          2. USER.ID
*          3. DIV.CODE
*OUT_PARAM: 
*          1. DIV.POS
*ENDDOC
**********************************************
*
$INCLUDE PMC.CPYLIB SECURITY
*
CONO=IN_PARAM<1,1>
USER.ID=IN_PARAM<1,2>
DIV.CODE=IN_PARAM<1,3>
*
IF FILEINFO(CONTROL,0)=0 THEN
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG="CONTROL file is missing."
    GOTO 99999
  END
END
IF FILEINFO(SECURITY,0)=0 THEN
  OPEN '','SECURITY' TO SECURITY ELSE
    ERRMSG="SECURITY file is missing."
    GOTO 99999
  END
END
;* check security 
READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE 
  SECURITY.REC = 'N'                                     
  SECURITY.REC<2> = 'N'                                  
END                                                      
IF SECURITY.REC<1>='Y' THEN
  MATREAD SEC.REC FROM SECURITY,CONO:USER.ID ELSE
    ERRMSG='USER':USER.ID:' IS NOT ON FILE'
    GOTO 99999
  END
  IF SEC.DIVISION='' THEN
    IF DIV.CODE='00' AND SECURITY.REC<1>='Y' AND SECURITY.REC<2>='Y' THEN
      ERRMSG="DIV '00' INVALID WHEN DIVISION CLOSING IS ENABLED"
      GOTO 99999
    END
  END ELSE
    LOCATE DIV.CODE IN SEC.DIVISION<1>,1 SETTING FOUND ELSE
      ERRMSG="USER ":USER.ID:" IS NOT AUTHORIZED FOR DIVISION ":DIV.CODE
      GOTO 99999
    END
  END
END
;* check division and get its position
READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE    
  ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"         
  GOTO 99999                                             
END                                                      
IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
  IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
    ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION LEVEL POSTING & CLOSING IS SELECTED"
    GOTO 99999
  END
  LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING DIV.POS ELSE
    ERRMSG = "Division ":DIV.CODE:" not found in DIVISIONS Control Record"
    GOTO 99999
  END
END ELSE
  DIV.POS=1
END
OUT_PARAM=DIV.POS
99999 
RETURN
