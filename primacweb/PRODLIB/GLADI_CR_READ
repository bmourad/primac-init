SUBROUTINE GLADI_CR_READ
********************************************************************************
*   Program name :- GLADI_CR_READ
*   Created:- 1/10/2003
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H

* Insert method code here
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE ARS.CPYLIB CASH.HIST
$INCLUDE PMC.CPYLIB CUSTOMER

* Open files
ERRMSG = ''
OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"
OPEN "CASH.HIST" TO CASH.HIST ELSE ERRMSG = "CASH.HIST FILE IS MISSING"
OPEN "CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING"
IF ERRMSG THEN
  CALL RBO_ERROR_SUB(ERRMSG)
  RETURN
END

* Get properties
STATUS = RBO.getProperty('','ID',ID)

* Get CONO
CONO = ID[1,3]
MATREAD COMP.REC FROM COMPANY, CONO ELSE
  ERRMSG = 'Invalid Company ID (':CONO:').'
  CALL RBO_ERROR_SUB(ERRMSG); RETURN
END

* Read CASH HISTORY record
CHR.ID = ID
CHR.ID[1,3] = ''
MAT CHR.REC = ''
FOR IDX = 1 TO 10
  MATREAD CHR.REC FROM CASH.HIST, CONO:CHR.ID:IDX THEN EXIT
NEXT IDX

* Get customer description
CUST.ID = FIELD(CHR.ID,"!",1)
MATREAD CUST.REC FROM CUSTOMER, CONO:CUST.ID ELSE MAT CUST.REC = ''

* Format data
CheckDate = OCONV(FIELD(ID,"!",2),"D2/")
FOR IDX = 1 TO DCOUNT(CHR.INVOICE,@VM)
  CHR.CASH.APPL<1,IDX> = OCONV(CHR.CASH.APPL<1,IDX>,"MD2")
  CHR.GL.AMT<1,IDX>    = OCONV(CHR.GL.AMT<1,IDX>,"MD2")
NEXT IDX

* Set output properties
STATUS = RBO.setProperty('','CustomerNum',CUST.ID)
STATUS = RBO.setProperty('','CustomerName',CUST.NAME)
STATUS = RBO.setProperty('','CheckDate',CheckDate)
STATUS = RBO.setProperty('','CHR_MON',CHR.MON)
STATUS = RBO.setProperty('','CHR_CASH',CHR.CASH)
STATUS = RBO.setProperty('','CHR_INVOICE',CHR.INVOICE)
STATUS = RBO.setProperty('','CHR_CASH_APPL',CHR.CASH.APPL)
STATUS = RBO.setProperty('','CHR_GL_AMT',CHR.GL.AMT)
STATUS = RBO.setProperty('','CHR_GL_NO',CHR.GL.NO)
STATUS = RBO.setProperty('','CHR_DIV',CHR.DIV)
STATUS = RBO.setProperty('','CHR_DEPT',CHR.DEPT)
STATUS = RBO.setProperty('','CHR_CCTR',CHR.CCTR)
STATUS = RBO.setProperty('','CHR_CASH',OCONV(CHR.CASH,"MD2"))

* End of method code
RETURN

