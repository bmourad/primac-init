SUBROUTINE ORDINQL_KC_POSTREAD
********************************************************************************
*   Program name :- ORDINQL_KC_POSTREAD
*   Created:- 12/05/2002
*   Author:- L. Ross
*   Gets info for Order Inquiry (L)ine Inquiry 'KC' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
OPEN '', 'ORDER.DETAIL' TO ORDER.DETAIL ELSE
  ERRMSG='Cannot open ORDER.DETAIL file!'
  GOTO 93000
END
OPEN '', 'INVENTORY' TO INVENTORY ELSE
  ERRMSG='Cannot open INVENTORY file!'
  GOTO 93000
END

;* initialize

;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
STATUS=RBO.getProperty('','Line_No',Line_No)

;* get database values

MATREAD ORD.DET.REC FROM ORDER.DETAIL, ID ELSE
  ERRMSG='Cannot read ORDER.DETAIL record ':ID
  GOTO 93000
END
IF OSD.KIT<1,Line_No> # 'K' THEN
  ERRMSG='Product is not a kitted item'
  GOTO 93000
END


PCNT=DCOUNT(OSD.PROD,VM)
Proddescs=''; Ums=''
START.LINE=''
LAST.LINE=PCNT
FOR L = (Line_No+1) TO LAST.LINE UNTIL START.LINE
  IF OSD.KIT<1,L> # 'M' THEN START.LINE = L
NEXT L
IF START.LINE THEN
  FOR L = LAST.LINE TO START.LINE STEP -1
    DEL OSD.PROD<1,L>
    DEL OSD.WHSE<1,L>
    DEL OSD.KIT.G.QTY<1,L>
    DEL OSD.KIT.PRICE<1,L>
    DEL OSD.R.QTY<1,L>
    DEL OSD.JOB<1,L>
  NEXT L
END
IF Line_No > 1 THEN
  FOR L = (Line_No - 1) TO 1 STEP -1
    DEL OSD.PROD<1,L>
    DEL OSD.WHSE<1,L>
    DEL OSD.KIT.G.QTY<1,L>
    DEL OSD.KIT.PRICE<1,L>
    DEL OSD.R.QTY<1,L>
    DEL OSD.JOB<1,L>
  NEXT L
END
Line_No=1
LAST.LINE=DCOUNT(OSD.PROD,VM)
FOR P = LAST.LINE TO 1 STEP -1
  MATREAD INV.REC FROM INVENTORY,CONO:OSD.PROD<1,P> ELSE
    INV.FULL.DESC = 'Unknown'; INV.UNIT<1,3> = 'EA'; INV.M.LINE = 'FNGD'
  END
  CALL GetInvUMCnv(ERRMSG,IN_PARAM,OUT_PARAR,MAT INV.CNV.REC,MAT INV.REC)
  Proddescs<1,P> = INV.FULL.DESC
  Ums<1,P> = INV.UNIT<1,3>
  OSD.R.QTY<1,P> = OCONV(INT(((OSD.R.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  OSD.KIT.G.QTY<1,P> = OCONV(INT(((OSD.KIT.G.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  OSD.KIT.PRICE<1,P> = OCONV(OSD.KIT.PRICE<1,P>,'MD4')
  IF OSD.JOB<1,P> # '' THEN
    IF OSD.JOB<1,P,2> # '' THEN OSD.JOB<1,P> = 'MULTI'
  END
NEXT P

* Set Properties

STATUS=RBO.setProperty('','OSD_PROD',OSD.PROD)
STATUS=RBO.setProperty('','OSD_WHSE',OSD.WHSE)
STATUS=RBO.setProperty('','Ums',Ums)
STATUS=RBO.setProperty('','Proddescs',Proddescs)
STATUS=RBO.setProperty('','OSD_KIT_G_QTY',OSD.KIT.G.QTY)
STATUS=RBO.setProperty('','OSD_R_QTY',OSD.R.QTY)
STATUS=RBO.setProperty('','OSD_KIT_PRICE',OSD.KIT.PRICE)
STATUS=RBO.setProperty('','OSD_JOB',OSD.JOB)
STATUS=RBO.setProperty('','Line_No',Line_No)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999
RETURN

