SUBROUTINE JCSRVWP_FISCAL
********************************************************************************
*   Program name :- GetFiscalPeriods
*   Created:- 6/10/2003
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  T-Security Faheem 08/16/05
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB FISCAL
$INCLUDE PMC.CPYLIB DIVISION
$INCLUDE PMC.CPYLIB SECURITY
$INCLUDE CPYLIB CHAR
$INCLUDE JCS.CPYLIB JOB
* Insert method code here
ERRMSG = ""
*NUM.PERIODS = 12
   OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'Cannot open CONTROL file!'
      GOTO 1000
   END
   OPEN '','SECURITY' TO SECURITY ELSE
       ERRMSG = "SECURITY FILE IS MISSING"
       GOTO 1000
   END
   OPEN '','JOB' TO JOB ELSE
   	 ERRMSG = 'JOB FILE IS MISSING' ; GOTO 1000
   END
   STATUS = RBO.getProperty('','PMCProperty', PMCProperty)
   UserID    = PMCProperty<1,3>
   CONO      = PMCProperty<1,4>
   STATUS = RBO.getProperty('','ID',ID)	
  READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
    PERIOD.REC = ""
    PERIOD.REC<1> = 12
  END
  NUM.PERIODS = PERIOD.REC<1>

   IF UserID # "" THEN
       UserID = UPCASE(UserID)
   END

   READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
      ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"; GOTO 1000
   END

   READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"; GOTO 1000
   END

   MATREAD SEC.REC FROM SECURITY, CONO:UserID ELSE
      ERRMSG = "USER ":UserID:" NOT ON FILE"
      GOTO 1000
   END

   MATREAD FISCAL.REC FROM CONTROL, CONO:"JCFISCAL" ELSE
      ERRMSG = "CONTROL RECORD JCFISCAL IS MISSING";GOTO 1000
   END
*T-Security v
  MATREAD JOB.REC FROM JOB, CONO:ID THEN
  END ELSE
    ERRMSG = "CANNOT LOCATE JOB - " : ID
    GOTO 1000
  END
  DIV.CODE = JOB.DIV; USER.ID = UserID; ERRMSG = ''
  CALL CK_DIV_SEC_SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
  IF ERRMSG # '' THEN
     GOTO 1000
  END
  SEC.DIVISION = DIV.CODE
*T-Security ^
   IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
     LOCATE SEC.DIVISION IN DIVISION.REC<1>,1 SETTING POS ELSE
         ERRMSG = "Division ":SEC.DIVISION:"  not found in Control File DIVISIONS Record."
         GOTO 1000
      END
   END ELSE
      POS = 1
   END

   YEAR  = FR.CURR.PER<1,POS>[1,4]
   MONTH = FR.CURR.PER<1,POS>[5,2]
   IF MONTH < 1 OR MONTH > NUM.PERIODS THEN
      ERRMSG = "ERROR IN CONTROL RECORD JCFISCAL"; GOTO 1000
   END
   PERIODS = STR("0",2-LEN(MONTH)):MONTH
   IF MONTH < (NUM.PERIODS -1) THEN
      PERIODS = PERIODS:VM:STR("0",2-LEN(MONTH+1)):MONTH+1
      PERIODS = PERIODS:VM:STR("0",2-LEN(MONTH+2)):MONTH+2
   END ELSE
      IF MONTH = (NUM.PERIODS -1) THEN PERIODS = (NUM.PERIODS -1):VM:NUM.PERIODS:VM:"01"
      IF MONTH = NUM.PERIODS THEN PERIODS = NUM.PERIODS:VM:"01":VM:"02"
   END

   STATUS = RBO.setProperty('', 'FISCALPERIODS', PERIODS)
   STATUS = RBO.setProperty('', 'YEAR',YEAR)
1000*
   IF ERRMSG # "" THEN
      STATUS = RBO.setProperty('', 'ServerStatus', 1)
      STATUS = RBO.setProperty('', 'ServerMessage', ERRMSG)
   END
* End of method code
RETURN

