SUBROUTINE ORDM_VALIDATE_PROD
********************************************************************************
*   Program name :- ORDM_VALIDATE_PROD
*   Created:- 1/9/2006
*   Programmer :- Suhail Hussain S
*------------------------------------------------------------------------------*
*   This program validates the PROD entered in grid of order line maint in ordm
********************************************************************************

$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB WAREHOUSE
$INCLUDE ICS.CPYLIB CATEGORY
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE ICS.CPYLIB INV.FNGD
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE ICS.CPYLIB FNGD.BOM
$INCLUDE ICS.CPYLIB FNGD.STATS
$INCLUDE PMC.CPYLIB COMP.OPS
$INCLUDE JES.CPYLIB ESTIMATE
$INCLUDE CPYLIB CHAR

OPEN "","INVENTORY" TO INVENTORY ELSE ERRMSG = "CANNOT OPEN INVENTORY FILE";GOTO 91000
OPEN "","WAREHOUSE" TO WAREHOUSE ELSE ERRMSG = "CANNOT OPEN WAREHOUSE FILE";GOTO 91000
OPEN "","CATEGORY" TO CATEGORY ELSE ERRMSG = "CANNOT OPEN CATEGORY FILE";GOTO 91000
OPEN "","INV.FNGD" TO INV.FNGD ELSE ERRMSG = "CANNOT OPEN INV.FNGD FILE";GOTO 91000
OPEN "","ESTIMATE" TO ESTIMATE ELSE ERRMSG = "CANNOT OPEN ESTIMATE FILE";GOTO 91000
OPEN "","FNGD.BOM" TO FNGD.BOM ELSE ERRMSG = "CANNOT OPEN FNGD.BOM FILE";GOTO 91000
OPEN "","FNGD.STATS" TO FNGD.STATS ELSE ERRMSG = "CANNOT OPEN FNGD.STATS FILE";GOTO 91000
OPEN "","INV.WHSE" TO INV.WHSE ELSE ERRMSG = "CANNOT OPEN INV.WHSE FILE";GOTO 91000
OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CANNOT OPEN CONTROL FILE";GOTO 91000

STATUS = RBO.getProperty("","INARR",INARR)
STATUS = RBO.getProperty("","PMCProperty",PMCProperty)

CONO     = PMCProperty<1,4>
ORDNO    = INARR<1,1>
ORD.CUST = INARR<1,2>
ORD.DIV  = INARR<1,3>
SHPNO    = INARR<1,4>
VALUE    = INARR<1,5>
OSD.R.QTY= INARR<1,6>
OSD.A.QTY= INARR<1,7>

WARNING  = ""
TEMP.MF  = ""

GEN.SHPNO = "000"
MATREAD OPCO.REC FROM CONTROL,CONO:"OPS" ELSE MAT OPCO.REC = ""

* Insert method code here
7010* 
  MATREAD INV.REC FROM INVENTORY, CONO:VALUE ELSE
    ERRMSG = "Cannot locate finished goods # ":CONO:VALUE
    GOTO 91000
  END
  IF INV.M.LINE # "FNGD" THEN
    ERRMSG = "This is not a Finished Goods Product. Try again !!"
    GOTO 91000
  END
  IF INV.CUST # "" AND INV.CUST # ORD.CUST THEN
    ERRMSG = "A different customer owns this finished goods # ":CONO:VALUE
    GOTO 91000
  END
  MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
    ERRMSG = "Cannot locate Product Line # ":INV.LINE
    GOTO 91000
  END
  WCNT = DCOUNT(INV.WHSE.CODE,VM)
  IF WCNT < 1 THEN
    ERRMSG = "Cannot locate any warehouses for finished goods # ":CONO:VALUE
    GOTO 91000
  END
  $INCLUDE ICSBP INV.UM.CNV

  DISP_ARR<1,4> = VALUE
  DISP_ARR<1,6> = INV.UNIT<1,3>
  DISP_ARR<1,15> = INV.FULL.DESC  
*
  PDNO = VALUE
* SET UP FOR KIT EXPLOSION
*
  MATREAD IOF.REC FROM INV.FNGD, CONO:PDNO ELSE
    MAT IOF.REC = ""
  END
  BOM.KEY = CONO:IOF.BOM
  MATREAD BOM.REC FROM FNGD.BOM, CONO:IOF.BOM ELSE
    MAT BOM.REC = ""
  END
  IF BOM.TYPE = "K" THEN
    IF SHPNO = GEN.SHPNO THEN
      ERRMSG = "Cannot enter a Kitted FNGD item on a General Ship To"
      GOTO 91000
    END
    NUM.BOM.PROD = DCOUNT(BOM.PROD,VM)
    FOR NBP = 1 TO NUM.BOM.PROD
      PROD.KEY = CONO:BOM.PROD<1,NBP>
      READ REC FROM INVENTORY, PROD.KEY ELSE
        REC = ""
      END
      IF REC<19> # "" AND REC<19> # ORD.CUST THEN
        ERRMSG = "A different customer owns a product in this finished good: # ":CONO:VALUE
        GOTO 91000
      END
      IF ORDNO = "N" AND (OPCO.EST.BLD = "A" OR OPCO.EST.BLD = "P") THEN
        READ REC FROM INV.FNGD, PROD.KEY ELSE
          REC = ""
        END
        TEMP.EST = ""
        ECNT = DCOUNT(REC<111>,VM)
        FOR E = 1 TO ECNT WHILE TEMP.EST = ""
          MATREAD EST.REC FROM ESTIMATE, CONO:REC<111,E> THEN
            IF EST.DIV[1,2] = ORD.DIV THEN TEMP.EST = REC<111,E>
          END
        NEXT E
        IF TEMP.EST = "" THEN
          WARNING<1,-1> = "Cannot create estimate - No template defined for Div ":ORD.DIV:" / Prod ":BOM.PROD<1,NBP>
        END
      END
    NEXT NBP
  END ELSE
    IF ORDNO = "N" AND (OPCO.EST.BLD = "A" OR OPCO.EST.BLD = "P") THEN
      TEMP.EST = ""
      ECNT = DCOUNT(IOF.ESTIMATE,VM)
      FOR E = 1 TO ECNT WHILE TEMP.EST = ""
        MATREAD EST.REC FROM ESTIMATE, CONO:IOF.ESTIMATE<1,E> THEN
          IF EST.DIV[1,2] = ORD.DIV THEN TEMP.EST = IOF.ESTIMATE<1,E>
        END
      NEXT E
      IF TEMP.EST = "" THEN
        WARNING<1,-1> = "Cannot create estimate - No template defined for Div ":ORD.DIV:" / Prod ":VALUE
      END
    END
  END
  
  IF WCNT = 1 THEN
    GOSUB 7020
  END

  STATUS = RBO.setProperty("","DISPARR",DISP_ARR)
  STATUS = RBO.setProperty("","WARNINGARR",WARNINGS)
  STATUS = RBO.setProperty("","KIT_FLAG",BOM.TYPE)
RETURN


*---- Warehouse
7020* 
  VALUE = INV.WHSE.CODE<1,1>  
  IWH.ID = CONO:PDNO:"!":VALUE
  MATREAD IWH.REC FROM INV.WHSE, IWH.ID THEN
    ERR.FLG='';ERRMSG='';PERIOD='';OPEN.FLAG = 1
    CALL BUILD_IWH_FI(IWH.ID,MAT IWH.REC,PERIOD,ERR.FLG,ERRMSG,OPEN.FLAG) 
  END ELSE
    ERRMSG = "Cannot locate warehouse (":VALUE:") for item # ":PDNO
    GOTO 91000
  END
  *
  MATREAD FGS.REC FROM FNGD.STATS, IWH.ID ELSE
    MAT FGS.REC = ""
  END
*
  DISP_ARR<1,5> = INV.WHSE.CODE<1,1>
  
  *NUM.PROD = DCOUNT(ODQ.PROD,VM)
  *TEMP.KEY = CONO:TEMP.MF(1):"!":TEMP.MF(2)
  *LOCATE TEMP.KEY IN ORIG.PROD<1>,1 SETTING LINDX THEN
  *  TEMP.CONV = CONVERT(SVM,VM,ORIG.IWH.RESV<1,LINDX>)
  *  IWH.RSV.FI = TEMP.CONV
  *  TEMP.CONV = CONVERT(SVM,VM,ORIG.ALOC.AVL<1,LINDX>)
  *  FGS.A.QTY = TEMP.CONV
  *  IWH.RESV    = ORIG.IWH.QTY<1,LINDX>
  *END ELSE
  *  ORIG.IWH.QTY = INSERT(ORIG.IWH.QTY,1,LINDX,1,IWH.RESV)
  *  ORIG.PROD   =INSERT(ORIG.PROD,1,LINDX,1,IWH.ID)
  *  TEMP.STR = CONVERT(VM,SVM,IWH.RSV.FI)
  *  ORIG.IWH.RESV=INSERT(ORIG.IWH.RESV,1,LINDX,1,TEMP.STR)
  *  TEMP.STR = CONVERT(VM,SVM,FGS.A.QTY)
  *  ORIG.ALOC.AVL=INSERT(ORIG.ALOC.AVL,1,LINDX,0,TEMP.STR)
  *END

  IF IWH.ON.HAND - IWH.RESV < 0 THEN
    AVL.RESV = OSD.R.QTY
  END ELSE
    AVL.RESV = IWH.ON.HAND - IWH.RESV + OSD.R.QTY
  END
  AVL.ALOC = SUM(FGS.M.QTY) - SUM(FGS.A.QTY)
  IF AVL.ALOC < 1 THEN AVL.ALOC = 0
  AVL.ALOC = AVL.ALOC + TEMP.MF(16)<1,1,2>
*
  WHNO = VALUE
  MATREAD WHSE.REC FROM WAREHOUSE,CONO:WHNO ELSE
    MAT WHSE.REC =""
  END
  IF WHS.DIV # ORD.DIV THEN
    IF WHS.DIV # "00" THEN
      ERRMSG = "Warehouse Division ":WHS.DIV:" doesnot match Order Division ":ORD.DIV
      GOTO 91000
    END
  END
  RETURN
*

* End of method code
91000*
   STATUS = RBO.setProperty("","ServerStatus",1)
   STATUS = RBO.setProperty("","ServerMessage",ERRMSG)
RETURN

