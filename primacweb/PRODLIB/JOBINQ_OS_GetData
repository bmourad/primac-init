SUBROUTINE JOBINQ_OS_GetData
********************************************************************************
*   Program name :- JOBINQ_OS_GetData
*   Created:- 12/13/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'OS' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TOT.REC
$INCLUDE JCS.CPYLIB CATEGORY.OSP
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(CATEGORY.OSP,0)=0 THEN
   OPEN '','CATEGORY.OSP' TO CATEGORY.OSP ELSE
      ERRMSG<1,-1>='CATEGORY.OSP FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM CCTR(10)
EQU CCTR.NO    TO CCTR(1)
EQU PLINE.NO   TO CCTR(2)
EQU PLINE.DESC TO CCTR(3)
EQU OPER.TYP  TO CCTR(4)
EQU OPER.VEND  TO CCTR(5)
EQU OPER.QTY   TO CCTR(6)
EQU OPER.DATE  TO CCTR(7)
EQU OPER.COST  TO CCTR(8)
EQU OPER.SALE  TO CCTR(9)
EQU CCTR.NAME  TO CCTR(10)
MAT CCTR = ''
DIM OTYPE(5)
OTYPE(1) = 'R,'
OTYPE(2) = 'N,'
OTYPE(3) = 'O,'
OTYPE(4) = 'S,'
OTYPE(5) = 'C'
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='OS'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


END
MAT JOB.TOT.REC = ''
JPTR = 0
JCNT = 0
CNT = COUNT(JOB.OS.DEPT,VM) + (JOB.OS.DEPT # '')
FOR I = 1 TO CNT
   TEMP = JOB.OS.DEPT<1,I>
   LOCATE TEMP IN OS.DEPT.NO<1>,1 SETTING JCNT ELSE
      JPTR = JPTR + 1; JCNT = JPTR
      OS.DEPT.NO<1,JCNT> = TEMP
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:TEMP ELSE DEPT.DESC = UNKNOWN
      OS.DEPT.DESC<1,JCNT> = DEPT.DESC
   END
   OS.DEPT.PTR<1,JCNT,-1> = I
   OS.DEPT.CHG<1,JCNT> = OS.DEPT.CHG<1,JCNT> + JOB.OS.TCOST<1,I,1>
   OS.DEPT.NCHG<1,JCNT> = OS.DEPT.NCHG<1,JCNT> + JOB.OS.TCOST<1,I,2>
   OS.DEPT.CORD<1,JCNT> = OS.DEPT.CORD<1,JCNT> + JOB.OS.TCOST<1,I,3>
   OS.DEPT.SPOIL<1,JCNT> = OS.DEPT.SPOIL<1,JCNT> + JOB.OS.TCOST<1,I,4>
   OS.DEPT.CNCR<1,JCNT> = OS.DEPT.CNCR<1,JCNT> + JOB.OS.TCOST<1,I,5>
   OS.DEPT.COST<1,JCNT> = OS.DEPT.COST<1,JCNT> + JOB.OS.COST<1,I>
   OS.DEPT.SALE<1,JCNT> = OS.DEPT.SALE<1,JCNT> + JOB.OS.SALE<1,I>
NEXT I
FOR I = 1 TO JPTR
   OS.TOT.CHG = OS.TOT.CHG + OS.DEPT.CHG<1,I>
   OS.TOT.NCHG = OS.TOT.NCHG + OS.DEPT.NCHG<1,I>
   OS.TOT.CORD = OS.TOT.CORD + OS.DEPT.CORD<1,I>
   OS.TOT.SPOIL = OS.TOT.SPOIL + OS.DEPT.SPOIL<1,I>
   OS.TOT.CNCR = OS.TOT.CNCR + OS.DEPT.CNCR<1,I>
   OS.TOT.COST = OS.TOT.COST + OS.DEPT.COST<1,I>
   OS.TOT.SALE = OS.TOT.SALE + OS.DEPT.SALE<1,I>
NEXT I
LINES = COUNT(OS.DEPT.NO,VM) + (OS.DEPT.NO # '')
FOR I = 1 TO LINES
   CNT = COUNT(OS.DEPT.PTR<1,I>,SVM) + (OS.DEPT.PTR<1,I> # '')
   FOR J = 1 TO CNT
      PTR = OS.DEPT.PTR<1,I,J>
      CCTR.NO<1,I,J> = JOB.OS.CCTR<1,PTR>
      MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR.NO<1,I,J> ELSE
         CCTR.DESC = UNKNOWN
      END
      CCTR.NAME<1,I,J> = CCTR.DESC
      PLINE.NO<1,I,J> = JOB.OS.PLINE<1,PTR>
      IF JOB.OS.PLINE<1,PTR> = '' THEN
         CAOS.DESC = ''
      END ELSE
         MATREAD CAOS.REC FROM CATEGORY.OSP, CONO : PLINE.NO<1,I,J> ELSE
            CAOS.DESC = UNKNOWN
         END
      END
      PLINE.DESC<1,I,J> = CAOS.DESC
      OPER.VEND<1,I,J> = JOB.OS.VEND<1,PTR>
      OPER.QTY<1,I,J> = OCONV(JOB.OS.QTY<1,PTR>,'MD2,')
      OPER.DATE<1,I,J> = OCONV(JOB.OS.DATE<1,PTR,1>,'D2/')
      OPER.COST<1,I,J> = OCONV(JOB.OS.COST<1,PTR>,'MD2,')
      OPER.SALE<1,I,J> = OCONV(JOB.OS.SALE<1,PTR>,'MD2,')
      FOR K = 1 TO 5
         IF JOB.OS.TCOST<1,PTR,K> > 0 THEN OPER.TYP<1,I,J> = OPER.TYP<1,I,J> : OTYPE(K)
      NEXT K
   NEXT J
   OS.DEPT.CHG<1,I> = OCONV(OS.DEPT.CHG<1,I>,'MD2,')
   OS.DEPT.NCHG<1,I> = OCONV(OS.DEPT.NCHG<1,I>,'MD2,')
   OS.DEPT.CORD<1,I> = OCONV(OS.DEPT.CORD<1,I>,'MD2,')
   OS.DEPT.CNCR<1,I> = OCONV(OS.DEPT.CNCR<1,I>,'MD2,')
   OS.DEPT.SPOIL<1,I> = OCONV(OS.DEPT.SPOIL<1,I>,'MD2,')
   OS.DEPT.COST<1,I> = OCONV(OS.DEPT.COST<1,I>,'MD2,')
   OS.DEPT.SALE<1,I> = OCONV(OS.DEPT.SALE<1,I>,'MD2,')
NEXT I
STATUS=RBO.setProperty('','OS_DEPT_NO',OS.DEPT.NO)
STATUS=RBO.setProperty('','OS_DEPT_CHG',OS.DEPT.CHG)
STATUS=RBO.setProperty('','OS_TOT_CHG',OCONV(OS.TOT.CHG,'MD2,'))
STATUS=RBO.setProperty('','OS_DEPT_NCHG',OS.DEPT.NCHG)
STATUS=RBO.setProperty('','OS_TOT_NCHG',OCONV(OS.TOT.NCHG,'MD2,'))
STATUS=RBO.setProperty('','OS_DEPT_CORD',OS.DEPT.CORD)
STATUS=RBO.setProperty('','OS_TOT_CORD',OCONV(OS.TOT.CORD,'MD2,'))
STATUS=RBO.setProperty('','OS_DEPT_SPOIL',OS.DEPT.SPOIL)
STATUS=RBO.setProperty('','OS_TOT_SPOIL',OCONV(OS.TOT.SPOIL,'MD2,'))
STATUS=RBO.setProperty('','OS_DEPT_COST',OS.DEPT.COST)
STATUS=RBO.setProperty('','OS_TOT_COST',OCONV(OS.TOT.COST,'MD2,'))
STATUS=RBO.setProperty('','OS_DEPT_SALE',OS.DEPT.SALE)
STATUS=RBO.setProperty('','OS_TOT_SALE',OCONV(OS.TOT.SALE,'MD2,'))
STATUS=RBO.setProperty('','OS_DEPT_DESC',OS.DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_NO',CCTR.NO)
STATUS=RBO.setProperty('','PLINE_NO',PLINE.NO)
STATUS=RBO.setProperty('','OPER_VEND',OPER.VEND)
STATUS=RBO.setProperty('','OPER_TYPE',OPER.TYP)
STATUS=RBO.setProperty('','OPER_QTY',OPER.QTY)
STATUS=RBO.setProperty('','OPER_DATE',OPER.DATE)
STATUS=RBO.setProperty('','OPER_COST',OPER.COST)
STATUS=RBO.setProperty('','OPER_SALE',OPER.SALE)
STATUS=RBO.setProperty('','CCTR_NAME',CCTR.NAME)
STATUS=RBO.setProperty('','PLINE_DESC',PLINE.DESC)
GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN

