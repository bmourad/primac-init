SUBROUTINE GET_DIV_FISCAL_MONTH
********************************************************************************
*   Program name :- GET_DIV_FISCAL_MONTH
*   Created:- 2/4/2005
*------------------------------------------------------------------------------*
*
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB PMC_PROCESS
$INCLUDE CPYLIB FILE.VARS
$INCLUDE CPYLIB CHAR

OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING'; GOTO 91000

 STATUS = RBO.getProperty('','Cono',CONO)
 STATUS = RBO.getProperty('','TEMP',CTL.KEY)
 STATUS = RBO.getProperty('', 'DIV_CODE',DIV.CODE)
*
**** INTITIALIZATION
*
  TODAY = DATE()
  CURR = ""
  REC = ""
  REC.DATES = ""
  ERRMSG = ""
  READ REC FROM CONTROL, CONO:CTL.KEY ELSE
      POSTING.MON = "END"
      ERRMSG = "CANNOT LOCATE CONTROL ":CONO:CTL.KEY
      GOTO 91000
  END
***** T23278 v
  READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL DIVISIONS RECORD"
      GOTO 91000
  END
  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL DIV.SECURITY RECORD"
      GOTO 91000
  END
  IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
          ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
          GOTO 91000
      END
      LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING POS ELSE 
          ERRMSG = "Division ":DIV.CODE:" not found on Control File DIVISIONS Record"
          GOTO 91000
      END
  END ELSE
      POS = 1
  END
***** T23278 ^
  READ REC.DATES FROM CONTROL,CONO:"SALESDATES" ELSE
      POSTING.MON = "END"
      ERRMSG = "CANNOT LOCATE CONTROL SALESDATES"
      GOTO 91000
  END
  READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = ""
      PERIOD.REC<1> = 12
  END
  NUM.PERIODS = PERIOD.REC<1>
  NEXT.TO.LAST.PERIOD = NUM.PERIODS - 1

  YEAR = REC<1,POS>[1,4]
  CURR = REC<1,POS>[5,2]

  IF CURR < 1 OR CURR > NUM.PERIODS THEN
      POSTING.MON = "END"
      ERRMSG = "INVALID CURRENT PERIOD"; GOTO 91000
  END
  END.DATE = REC.DATES<CURR+1,1>

  IF CURR < NEXT.TO.LAST.PERIOD THEN
      ECD.VALDAT = STR("0",(2-LEN(CURR))):CURR:","
      ECD.VALDAT = ECD.VALDAT:STR("0",(2-LEN(CURR+1))):(CURR+1):","
      ECD.VALDAT = ECD.VALDAT:STR("0",(2-LEN(CURR+2))):(CURR+2)
      TEMP = STR("0",(2-LEN(CURR+1))):(CURR+1):","
      TEMP = TEMP:STR("0",(2-LEN(CURR+2))):(CURR+2)
  END ELSE
      IF CURR = NEXT.TO.LAST.PERIOD THEN
          ECD.VALDAT = NEXT.TO.LAST.PERIOD:",":NUM.PERIODS:",01"
*          TEMP = NUM.PERIODS:" or 01"
          TEMP = NUM.PERIODS:",01"

      END ELSE
          ECD.VALDAT = NUM.PERIODS:",01,02"
*          TEMP = "01 or 02"
          TEMP = "01,02"
      END
  END

STATUS = RBO.setProperty('', 'CURR',CURR:@VM:YEAR)
STATUS = RBO.setProperty('', 'END_DATE',OCONV(END.DATE,"D2/"))
STATUS = RBO.setProperty('', 'SUG_PERIOD',TEMP)
STATUS = RBO.setProperty('', 'chkDate',END.DATE:@VM:TODAY)
STATUS = RBO.setProperty('', 'ServerStatus', 2)
91000*
999999* 
STATUS = RBO.setProperty('', 'ServerMessage', ERRMSG)
RETURN
END

