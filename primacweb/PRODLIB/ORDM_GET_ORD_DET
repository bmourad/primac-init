SUBROUTINE ORDM_GET_ORD_DET
********************************************************************************
*   Program name   :- ORDM_GET_ORD_DET
*   Created on     :- 1/3/2006
*   Programmer     :- Suhail Hussain S
*   Doc : This programe gets the order.detail array on order number and shipto
********************************************************************************
$INCLUDE WWINSERT RBO.H

$INCLUDE CPYLIB COMMON1
$INCLUDE JCS.CPYLIB COM.JCS.LINK  
$INCLUDE ICS.CPYLIB COM.INV.MAIN  
$INCLUDE ICS.CPYLIB COM.INV.SERIAL
$INCLUDE JCS.CPYLIB COM.INV.STATS 
$INCLUDE PMC.CPYLIB COM.CUST 
$INCLUDE OPS.CPYLIB COM.ORDER
$INCLUDE OPS.CPYLIB COM.OPS.LINK

$DEFINE INVENTORY
$DEFINE ORDER
$DEFINE ORDERDETAILINQ

$INCLUDE OPS.CPYLIB ORDER
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE OPS.CPYLIB ORDER.DETAIL.INQ
$INCLUDE OPS.CPYLIB ORDER.RELEASE
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE PMC.CPYLIB SHIP.TO

OPEN "","ORDER" TO ORDER ELSE ERRMSG = "CANNOT OPEN ORDER FILE";GOTO 91000
OPEN "","ORDER.DETAIL" TO ORDER.DETAIL ELSE ERRMSG = "CANNOT OPEN ORDER.DETAIL FILE";GOTO 91000
OPEN "","ORDER.RELEASE" TO ORDER.RELEASE ELSE ERRMSG = "CANNOT OPEN ORDER.RELEASE FILE";GOTO 91000
OPEN "","INVENTORY" TO INVENTORY ELSE ERRMSG = "CANNOT OPEN INVENTORY FILE";GOTO 91000
OPEN "","SHIP.TO" TO SHIP.TO ELSE ERRMSG = "CANNOT OPEN SHIP.TO FILE";GOTO 91000

STATUS = RBO.getProperty("","INARR",INPUT_ARR)
STATUS = RBO.getProperty("","PMCProperty",PMCProperty)

ORDNO = INPUT_ARR<1,1>
SHIPTO = INPUT_ARR<1,2>
CONO = PMCProperty<1,4>
PRODCNT = 0
DISPARR = ""

* Insert method code here
   MATREAD ORD.REC FROM ORDER,CONO:ORDNO ELSE ERRMSG = "CANNOT READ ORDER FILE : " CONO:ORDNO; GOTO 91000
   STATUS = "L"; SHPNO = "ALL"
   CALL ORDER_LINE_UPD(CONO,ORDNO,SHPNO,STATUS)

   MATREAD ORD.DET.REC FROM ORDER.DETAIL ,CONO:ORDNO:"!":SHIPTO ELSE ERRMSG = "CANNOT READ ORDER.DETAIL FILE : " CONO:ORDNO:"!":SHIPTO; GOTO 91000
   SHPNO = SHIPTO
   LOCATE SHPNO IN ORD.SHIP.TO<1>,1 SETTING SPTR THEN
      STATUS = "D"
      CALL ORDER_LINE_UPD(CONO,ORDNO,SHPNO,STATUS)
   END

   MATREAD SPT.REC FROM SHIP.TO, CONO:ORD.CUST:"!":SHIPTO ELSE MAT SPT.REC = ""
   IF SHIPTO = "000" THEN OSD.PO = ORD.PO
   IF OSD.SHP.TERMS = "" AND SPT.SHP.TERMS # "" THEN
     OSD.SHP.TERMS = SPT.SHP.TERMS
   END
   DISPARR<1,1> = SHIPTO
   DISPARR<1,2> = OSD.PO
   DISPARR<1,3> = OSD.SHP.TERMS

   PRODCNT = DCOUNT(OSD.PROD,@VM)
   FOR INDX = 1 TO PRODCNT
     MATREAD INV.REC FROM INVENTORY, CONO:OSD.PROD<1,INDX> ELSE
       MAT INV.REC = "";INV.FULL.DESC = "Unknown"
     END
    
     $INCLUDE ICSBP INV.UM.CNV
   
     IF OSD.KIT<1,INDX> = "K" THEN
       RESV.PRINT = OSD.KIT.R.QTY<1,INDX>
       ORDER.PRICE = OSD.KIT.PRICE<1,INDX>
       ORDER.PRINT = OSD.KIT.G.QTY<1,INDX>    
     END ELSE
       RESV.PRINT = OSD.R.QTY<1,INDX>
       ORDER.PRICE = OSD.PRICE<1,INDX>
       ORDER.PRINT = OSD.G.QTY<1,INDX>
     END
 
     TMP.ID=CONO:OSD.PROD<1,INDX>:"!":OSD.WHSE<1,INDX>
     
     DISPARR<1,4,INDX> = OSD.PROD<1,INDX>
     DISPARR<1,5,INDX> = OSD.WHSE<1,INDX>
     DISPARR<1,6,INDX> = INV.UNIT<1,3>
     DISPARR<1,7,INDX> = OCONV(INT(((ORDER.PRINT / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
     DISPARR<1,8,INDX> = OCONV(INT(((RESV.PRINT / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
     DISPARR<1,9,INDX> = OCONV(INT(((OSD.A.QTY<1,INDX> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
     IF ORDER.PRICE > 0 THEN
       DISPARR<1,10,INDX> = OCONV(ORDER.PRICE,"MD4")
     END ELSE
       DISPARR<1,10,INDX> = ""
     END
     GOSUB 7920
     DISPARR<1,15,INDX> = INV.FULL.DESC
     IF OSD.JOB<1,INDX,2> # "" THEN
       DISPARR<1,16,INDX> = "MULTI"
     END ELSE
       DISPARR<1,16,INDX> = OSD.JOB<1,INDX>
     END
   NEXT INDX
   
   MATBUILD RETARR FROM ORD.DET.REC USING "ð"
   STATUS = RBO.setProperty("","RETARR",RETARR)
   STATUS = RBO.setProperty("","DISPARR",DISPARR)
RETURN

7920* 
  IF OSD.COMMENT<1,INDX> = "" THEN
    STATUS = ""
  END ELSE
    STATUS = "*"
  END
  DISPARR<1,11,INDX> =  STATUS
  DISPARR<1,17,INDX> =  OSD.COMMENT<1,INDX>
  PTR = 1

  LOOP
    LOCATE OSD.PROD<1,INDX> IN ODQ.PROD<1>,PTR SETTING PPTR THEN
      BEGIN CASE
        CASE (OSD.WHSE<1,INDX> # ODQ.WHSE<1,PPTR>) OR (OSD.KIT<1,INDX> # ODQ.KIT<1,PPTR>) OR (OSD.PROD.SEQ<1,INDX> # ODQ.PROD.SEQ<1,PPTR>) 
        CASE ODQ.G.QTY<1,PPTR>+0 > 0;*T20234
          STATUS = "*"
          PTR = 0
        CASE 1
          STATUS = ""
          PTR = 0
      END CASE
    END ELSE
      STATUS = ""
      PTR = 0
    END
  WHILE PTR DO
    PTR = PPTR + 1
  REPEAT
  DISPARR<1,12,INDX> =  STATUS

  STATUS = ""
  IF OSD.REL.NO<1,INDX> # "" THEN
    STATUS = "*"
  END ELSE
    OCNT = DCOUNT(ORD.REL.NO,@VM)
    FOR O = 1 TO OCNT UNTIL STATUS # ""
      MATREAD ORR.REC FROM ORDER.RELEASE, CONO:ORD.REL.NO<1,O> THEN
        IF ORR.STATUS<1,1> # "COMPLETED" AND ORR.SHIP.TO = SHIPTO AND ORR.QTY<1,INDX>+0 > 0 THEN
          STATUS = "*"
        END
      END
    NEXT O
  END
  DISPARR<1,13,INDX> =  STATUS

  IF OSD.KIT<1,INDX> = "N" THEN
    BEGIN CASE
      CASE OSD.O.QTY<1,INDX> < 1
        STATUS = "O"
      CASE OSD.I.QTY<1,INDX> >= OSD.O.QTY<1,INDX>
        STATUS = "I"
      CASE OSD.S.QTY<1,INDX> >= OSD.O.QTY<1,INDX>
        STATUS = "S"
      CASE OSD.A.QTY<1,INDX> < 1
        STATUS = "O"
      CASE OSD.F.QTY<1,INDX> >= OSD.A.QTY<1,INDX>
        STATUS = "F"
      CASE 1
        STATUS = "M"
    END CASE
  END
  IF OSD.KIT<1,INDX> = "K" THEN
    BEGIN CASE
      CASE OSD.KIT.O.QTY<1,INDX> < 1
        STATUS = "O"
      CASE OSD.KIT.I.QTY<1,INDX> >= OSD.KIT.O.QTY<1,INDX>
        STATUS = "I"
      CASE OSD.KIT.S.QTY<1,INDX> >= OSD.KIT.O.QTY<1,INDX>
        STATUS = "S"
      CASE OSD.KIT.A.QTY<1,INDX> < 1
        STATUS = "O"
      CASE OSD.KIT.F.QTY<1,INDX> >= OSD.KIT.A.QTY<1,INDX>
        STATUS = "F"
      CASE 1
        STATUS = "M"
    END CASE
  END
  DISPARR<1,14,INDX> =  STATUS
RETURN

* End of method code
91000:
   STATUS = RBO.setProperty("","ServerStatus",1)
   STATUS = RBO.setProperty("","ServerMessage",ERRMSG)
RETURN

