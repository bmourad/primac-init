SUBROUTINE XREF_GET_ORDM
********************************************************************************
*   Program name :- XREF_GET_ORDM
*   Created:- 9/27/2003
*------------------------------------------------------------------------------*
*
* PROGRAMMER G.PURUSHOTHAM RAO
* In Properties:
* --------------
*  
*
* Out Properties:
* ---------------
*  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB CATEGORY
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR

* Insert method code here
   OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = 'CANNOT LOCATE COMPANY FILE'
      GOTO 93000
   END
   OPEN '','CATEGORY' TO CATEGORY ELSE
      ERRMSG = 'CANNOT LOCATE CATEGORY FILE'
      GOTO 93000
   END
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'CANNOT LOCATE INVENTORY FILE'
      GOTO 93000
   END
   OPEN '','INV.WHSE' TO INV.WHSE ELSE
      ERRMSG = 'CANNOT LOCATE INV.WHSE FILE'
      GOTO 93000
   END

STATUS=RBO.getProperty('','XREF_CUSTOMER',CUST.SEL)
STATUS=RBO.getProperty('','XREF_CATG',CATG.SEL)
STATUS=RBO.getProperty('','XREF_WHSE',WHSE.SEL)
STATUS=RBO.getProperty('','XREF_LENGTH',LEN.SEL)
STATUS=RBO.getProperty('','XREF_WIDTH',WID.SEL)

STATUS=RBO.getProperty('','PMCProperty',PMCProperty)
CONO = PMCProperty<1,4>
USER.ID = PMCProperty<1,3>
*
*---- Get selected items
5000*
 SEL.PROD = ""; SEL.WHSE = "";SYSLIST = ""
 BY.STMT = ""
 STMT = 'SSELECT INV.WHSE WITH CONO = "':CONO:'" AND WITH M.LINE = "FNGD" AND WITH CUST = "':CUST.SEL:'"'
 OR.STMT = ' OR WITH CONO = "':CONO:'" AND WITH M.LINE = "FNGD" AND WITH NO CUST'
 IF CATG.SEL = "" THEN
   BY.STMT = BY.STMT : ' BY P.LINE'
 END ELSE
   STMT = STMT : ' AND WITH P.LINE = "':CATG.SEL:'"'
   OR.STMT = OR.STMT : ' AND WITH P.LINE = "':CATG.SEL:'"'
 END
 IF WID.SEL = "" THEN
   BY.STMT = BY.STMT : ' BY WIDTH'
 END ELSE
   STMT = STMT : ' AND WITH WIDTH = "': OCONV(ICONV(WID.SEL,"MD4"),"MD4") :'"'
   OR.STMT = OR.STMT : ' AND WITH WIDTH = "': OCONV(ICONV(WID.SEL,"MD4"),"MD4") :'"'
 END
 IF LEN.SEL = "" THEN
   BY.STMT = BY.STMT : ' BY LENGTH'
 END ELSE
   STMT = STMT : ' AND WITH LENGTH = "': OCONV(ICONV(LEN.SEL,"MD4"),"MD4") :'"'
   OR.STMT = OR.STMT : ' AND WITH LENGTH = "': OCONV(ICONV(LEN.SEL,"MD4"),"MD4") :'"'
 END
 IF WHSE.SEL = "" THEN
   BY.STMT = BY.STMT : ' BY WHSE'
 END ELSE
   STMT = STMT : ' AND WITH WHSE = "':WHSE.SEL:'"'
   OR.STMT = OR.STMT : ' AND WITH WHSE = "':WHSE.SEL:'"'
 END
 STMT = STMT : OR.STMT : BY.STMT
 PERFORM STMT RTNLIST SYSLIST CAPTURING RESPONSE
 RESPONSE = ""; DATA = 1; LINES = 0
 LOOP
   READNEXT SEL.ID FROM SYSLIST ELSE DATA = 0
 WHILE DATA DO
   LINES = LINES + 1
   SEL.PROD<LINES> = FIELD(SEL.ID,"!",1)[4,15]
   SEL.WHSE<LINES> = FIELD(SEL.ID,"!",2)
 REPEAT

* GOSUB 7000

 LAST.LINE = DCOUNT(SEL.PROD,AM)
 CNT = 1
 FOR N = 1 TO LAST.LINE
   SEL.ID = CONO : SEL.PROD<N>
   MATREAD INV.REC FROM INVENTORY, SEL.ID ELSE
     MAT INV.REC = ""; INV.FULL.DESC = "Unknown"
   END

   ***
   *
   $INCLUDE ICSBP INV.UM.CNV
   *
   ***
   SEL.ID = SEL.ID : "!" : SEL.WHSE<N>
   MATREAD IWH.REC FROM INV.WHSE, SEL.ID ELSE
     MAT IWH.REC = ""
   END
   XREF_CATG<1,N> = INV.LINE
   XREF_ITEM<1,N> = SEL.PROD<N>
   XREF_WHSE<1,N> = SEL.WHSE<N>
   XREF_LENGTH<1,N> = OCONV(INV.PAP.LEN,"MD4")
   XREF_WIDTH<1,N> = OCONV(INV.PAP.WIDTH,"MD4")
   XREF_AVAILABLE<1,N> = OCONV(INT((((IWH.ON.HAND-IWH.RESV) / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
   XREF_MANUFACT<1,N> = OCONV(INT(((IWH.INPRCS / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
   XREF_ITEM_DESC<1,N> = INV.FULL.DESC
 NEXT N
	STATUS = RBO.setProperty("","XREF_CATG",XREF_CATG)
	STATUS = RBO.setProperty("","XREF_ITEM",XREF_ITEM)
	STATUS = RBO.setProperty("","XREF_WHSE",XREF_WHSE)
	STATUS = RBO.setProperty("","XREF_LENGTH",XREF_LENGTH)
	STATUS = RBO.setProperty("","XREF_WIDTH",XREF_WIDTH)
	STATUS = RBO.setProperty("","XREF_AVAILABLE",XREF_AVAILABLE)
	STATUS = RBO.setProperty("","XREF_MANUFACT",XREF_MANUFACT)
	STATUS = RBO.setProperty("","XREF_ITEM_DESC",XREF_ITEM_DESC)
	*STATUS = RBO.setProperty("","ServerMessage",STMT)
* End of method code
RETURN
93000*
	STATUS = RBO.setProperty("","ServerMessage",ERRMSG)

