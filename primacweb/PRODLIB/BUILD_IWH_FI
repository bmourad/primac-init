SUBROUTINE BUILD.IWH.FI(IWH.ID,MAT IWH.REC, PERIOD,ERR.FLG,ERRMSG,OPEN.FLAG)
*
$INCLUDE CPYLIB COMMON1
*
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - EPRIMAC
* PROGRAM     - BUILD_IWH_FI
* BY          - KHAJA ZIAUDDIN
* DATE        - 07/16/2003  (mm/dd/yyyy)
* DESCRIPTION   
*
* THIS SUB BUILDS INV.WHSE FIFO buckets from
*                           INV_RECP_WHSE file.
*                           USE THIS PROGRAM ONLY IF NO UPDATES OF 
*                           INV.WHSE FILE ARE GOING TO TAKE PLACE
*                           OTHERWISE USE ICS.IWH.SUB
*                           CALL WITH ACTION=1 
*ERR.FLG=2 TERMINAL ERROR; GOSUB 93000 IN CALLING PROGRAM
*ERR.FLG=1 ; GOSUB 91000 IN CALLING PROGRAM

*ENDDOC
**********************************************
*
*
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE ICS.CPYLIB INV_RECEIPTS
$INCLUDE ICS.CPYLIB INV_RECP_WHSE
$INCLUDE ICS.CPYLIB INV_AUDIT_HIST
$DEFINE FILEVARS
$INCLUDE CPYLIB FILE.VARS
*
IF PERIOD<1,2>='EXCLUDE' THEN EXCLUDE=1 ELSE EXCLUDE=0
IF PERIOD<1,1>='' THEN PERIOD<1,1>='9999999'
*
IF (OPEN.FLAG) THEN
  OPEN.FLAG=0
  IF FILEINFO(INV_RECEIPTS,0)=0 THEN
    OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE               
      ERRMSG="INV_RECEIPTS FILE IS MISSING"
      ERR.FLG=2; GOTO 99999
    END                                                       
  END
  IF FILEINFO(INV_RECEIPTS_TEMP,0)=0 THEN
    OPEN '','INV_RECEIPTS_TEMP' TO INV_RECEIPTS_TEMP ELSE     
      ERRMSG="INV_RECEIPTS_TEMP FILE IS MISSING"
      ERR.FLG=2; GOTO 99999
    END                                                       
  END
  IF FILEINFO(INV_RECP_WHSE,0)=0 THEN
    OPEN '','INV_RECP_WHSE' TO INV_RECP_WHSE ELSE             
      ERRMSG="INV_RECP_WHSE FILE IS MISSING"
      ERR.FLG=2; GOTO 99999
    END                                                       
  END
  IF FILEINFO(INV_RECP_WHSE_TEMP,0)=0 THEN
    OPEN '','INV_RECP_WHSE_TEMP' TO INV_RECP_WHSE_TEMP ELSE   
      ERRMSG="INV_RECP_WHSE_TEMP FILE IS MISSING"
      ERR.FLG=2; GOTO 99999
    END                                                       
  END
  IF (EXCLUDE) THEN
    IF FILEINFO(INV_AUDIT_HIST,0)=0 THEN
      OPEN '','INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE
        ERRMSG='INV_AUDIT_HIST FILE IS MISSING'
        ERR.FLG=2; GOTO 99999
      END
    END
  END
END
;*
;*build IWH.REC information from receipts.
;*do not include future period receipts in receipt array
;* if PERIOD variable passed. If not then include all receipts, 
;* current and future.
;* If PERIOD<1,2>='EXCLUDE' then exclude
;* transactions that occur in the future period
;* when building fifo buckets.
;*
*
RECP.NO=IWH.RECP.NO<1>
RECP.PERIOD=IWH.RECP.PERIOD<1>
*
IWH.ORG.FI = ""
IWH.RSV.FI = ""
IWH.QTY.FI = ""
IWH.COST.FI=""
IWH.VDR.FI=''
IWH.PO.NO.FI=''
IWH.PO.LN.FI=''
IWH.RECV.FI=''
IWH.DATE.FI=''
IWH.ACT.COST=''
IWH.COST.FI=''
IWH.SALE.FI=''
IWH.RECP.NO=''
IWH.RECP.PERIOD=''
IWH.RECP.ENT.DATE=''
*
CONO=IWH.ID[1,3]
WHSE = OCONV(IWH.ID,"G1!1")
RCNT=0
RECP.CNT = DCOUNT(RECP.NO<1>,@VM)
FOR RR = 1 TO RECP.CNT
  RECP=RECP.NO<1,RR>
  IF RECP.PERIOD<1,RR> <= PERIOD<1,1> THEN
    INVR.ID=CONO:RECP
    MATREAD INVR.REC FROM INV_RECEIPTS,INVR.ID THEN
      IRW.ID=CONO:RECP:"!":WHSE
      MATREAD IRW.REC FROM INV_RECP_WHSE,IRW.ID THEN
        RCNT+=1
        IWH.ORG.FI<1,RCNT>=IRW.ORG.QTY   
        IWH.RSV.FI<1,RCNT>=IRW.RSVB.QTY   
        IF (EXCLUDE) THEN
          IWH.QTY.FI<1,RCNT>+=IRW.CUR.QTY        
        END ELSE
          IWH.QTY.FI<1,RCNT>=IRW.CUR.QTY        
        END
        IWH.COST.FI<1,RCNT>=IRW.UNIT.COST   
        IWH.VDR.FI<1,RCNT> = INVR.VEND          
        IWH.PO.NO.FI<1,RCNT>=INVR.PO            
        IWH.PO.LN.FI<1,RCNT>=INVR.PO.LN         
        IWH.RECV.FI<1,RCNT>=INVR.ENT.DATE       
        IWH.DATE.FI<1,RCNT>=INVR.POST.DATE      
        IWH.COST.FI<1,RCNT>=INVR.UNIT.COST   
        IWH.SALE.FI<1,RCNT>=INVR.UN.SALE           
        IWH.RECP.PERIOD<1,RCNT>=INVR.PERIOD     
        IWH.RECP.ENT.DATE<1,RCNT>=INVR.ENT.DATE 
        IWH.RECP.NO<1,RCNT>=RECP             
      END
      IF (EXCLUDE) THEN 
        GOSUB EXCLUDE.FUTURE.TRANSACTIONS
      END
    END
  END
NEXT RR
*
GOTO 99999
*
*************************************************************************
***** S U B R O U T I N E S *********************************************
*************************************************************************
*
****************************
EXCLUDE.FUTURE.TRANSACTIONS: 
****************************
*
HCNT=DCOUNT(INVR.AUDIT.NO,@VM)
FOR HH=1 TO HCNT
  INAH.ID=CONO:INVR.AUDIT.NO<1,HH>
  MATREAD INAH.REC FROM INV_AUDIT_HIST,INAH.ID THEN
    IF INAH.PERIOD>PERIOD<1,1> THEN
      IF INAH.WHSE=WHSE THEN
        BEGIN CASE
          CASE INAH.DEPL.RECP#''
            DPCNT=DCOUNT(INAH.DEPL.RECP<1>,@VM)
            FOR DD=1 TO DPCNT
              LOCATE INAH.DEPL.RECP<1,DD> IN IWH.RECP.NO<1> SETTING RPOS THEN
                IF IWH.RECP.PERIOD<1,RPOS><=PERIOD THEN
                  IWH.QTY.FI<1,RPOS>-=INAH.DEPL.QTY<1,DD>
                END
              END
            NEXT DD
          CASE 1
            LOCATE INAH.RECP.NO IN IWH.RECP.NO<1> SETTING RPOS THEN
              IWH.QTY.FI<1,RPOS>-=INAH.QTY
            END
        END CASE
      END
    END
  END
NEXT HH
RETURN
*
99999* 
END

