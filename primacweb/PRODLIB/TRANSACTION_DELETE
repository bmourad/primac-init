SUBROUTINE TRANSACTION_DELETE (CONO,TDIV,FNAME,FID,FVAR,PSS.JOURNAL,ERRMSG)
********************************************************************************
*   Program name :- TRANSACTION_DELETE
*   Created:- 8/4/2003
* DESCRIPTION
*
* This program is used to delete the specified record from the database 
* while providing recovery capability through the use of a Journal file.
*
*    PARAM        DESCRIPTION
*    -----------  --------------------------------------------------------
*    CONO         Company ID
*    TDIV         Division ID
*    FNAME        Name of file being updated
*    FID          Key of item to be deleted (includes CONO if present)
*    FVAR         File variable of file to be updated
*    PSS.JOURNAL  File variable of Journal file
*    ERRMSG       Status
*
**************************************************************************
*
*---- COPY STATEMENTS
*
$INCLUDE PSS.CPYLIB PSS.JOURNAL
$INCLUDE WWINSERT RBO.H

*
*---- INITIALIZATION
*
      ERRMSG = ""
      CDATE = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN CDATE = DATE()
*
*---- MAIN PROCESSING
*
      IF TDIV = "" THEN
         JFOUND = 0
      END ELSE
         JFOUND = 1
         MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!":TDIV ELSE
            RELEASE PSS.JOURNAL, CONO:"CONTROL!":TDIV
            JFOUND = 0
         END
      END
      IF NOT(JFOUND) THEN
         DELETE FVAR,FID
         ERRMSG = "NO TRANSACTIONS IN PROGRESS"
         GOTO 99999
      END
      IF JRNLC.TRAN = "" THEN
         JRNLC.TRAN = "1"
         JRNLC.DATE = CDATE
         JRNLC.TIME = CTIME
         JRNLC.DESC = "??????????"
         JRNLC.TPTR = 1
      END
      TCNT = DCOUNT(JRNLC.TRAN,@VM)
      CTRAN = JRNLC.TRAN<1,TCNT>
      JPTR = JRNLC.JPTR + JRNLC.JCNT
      JRNLC.JCNT += 1
      JRNLC.TCNT<1,TCNT> += 1
      MAT JRNLI.REC = ""
      JRNLI.FNAME = FNAME
      JRNLI.INAME = FID
      JRNLI.TRAN = CTRAN
      JRNLI.DATE = CDATE
      JRNLI.TIME = CTIME
      READU BEFORE.IMAGE FROM FVAR, FID THEN
         WRITE BEFORE.IMAGE ON PSS.JOURNAL, CONO:"XDATA!":TDIV:"!":JPTR
      END ELSE
         JRNLI.STATUS = "NEW"
      END
      DELETE FVAR, FID
      MATWRITE JRNLI.REC ON PSS.JOURNAL, CONO:"INDEX!":TDIV:"!":JPTR
      MATWRITE JRNLC.REC ON PSS.JOURNAL, CONO:"CONTROL!":TDIV
*
*---- END OF PROGRAM
*
99999 *
      RETURN

93000
STATUS = RBO.setProperty('','ServerStatus',1)
STATUS = RBO.setProperty('','ServerMessage', ERRMSG)
END

