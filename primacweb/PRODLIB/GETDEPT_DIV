SUBROUTINE GETDEPT_DIV
********************************************************************************
*   Program name :- GETDEPT_DIV
*   Created:- 11/3/2004
*   Written By:- Kauser Jahan
*------------------------------------------------------------------------------*
*

* In Properties:
* --------------
*  DIVISION,COST CENTER,CONO
*
* Out Properties:
* ---------------
* DIVISION,DIVISION DESC,DEPARTMENT,DEPARTMENT DESC  
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB DIVISION
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB OPERATION

ERRMSG = ""
* Insert method code here
OPEN '','DIVISION' TO DIVISION ELSE
	ERRMSG = 'Cannot open DIVISION file!'
	GOTO 99999
END
OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
	ERRMSG = 'Cannot open DEPARTMENT file!'
	GOTO 99999
END

OPEN '', 'COST.CNTR' TO COST.CNTR ELSE 
	ERRMSG = "COST.CNTR FILE IS MISSING"
	GOTO 99999
END

OPEN '', 'OPERATION' TO OPERATION ELSE
	ERRMSG = "OPERATION FILE IS MISSING"
	GOTO 99999
END

CTL = 1
CCTR_DEPT = "";CCTR_OPER = "";OPER_DESC = ""
STATUS = RBO.getProperty('','Cono',CONO)
STATUS = RBO.getProperty('','DMT_DIV',DIV_NO)
STATUS = RBO.getProperty('','DMT_CCTR',CCTR_NO)
DEPT = "";DIV = ""
	MATREAD CCTR.REC FROM COST.CNTR , CONO:CCTR_NO THEN
	   CCTR_DEPT = CCTR.DEPT
	   CCTR_OPER = CCTR.OPER
	END
	MATREAD DEPT.REC FROM DEPARTMENT,CONO:CCTR_DEPT THEN
	   LOCATE DIV_NO IN DEPT.DIV<1> SETTING L ELSE
		ERRMSG = "DEPARTMENT ":CCTR_DEPT:" DOES NOT BELONG TO THIS DIVISION"
		GOTO 99999   
	   END
	   DEPT = CCTR_DEPT : "!" : DEPT.DESC
	   MATREAD DIV.REC FROM DIVISION,CONO:DIV_NO THEN
		DIV = DIV_NO : "!" : DIV.DESC
	   END
	END ELSE
	   MAT DEPT.REC = ""
	END	
	CNT = DCOUNT(CCTR_OPER,@VM)
	FOR X = 1 TO CNT 
		MATREAD OPER.REC FROM OPERATION,CONO:CCTR_OPER<1,X> THEN
			OPER_DESC<1,X> = CCTR_OPER<1,X> : "_" : OPER.DESC
		END

	NEXT X

STATUS = RBO.setProperty('','DMT_DEPT',DEPT)
STATUS = RBO.setProperty('','DMT_DEPT_LIST',CCTR_DEPT)
STATUS = RBO.setProperty('','DMT_DIV',DIV)
STATUS = RBO.setProperty('','DMT_OPER_DESC',OPER_DESC)
STATUS = RBO.setProperty('','DMT_OPER_LIST',CCTR_OPER)
* End of method code
RETURN

99999
  IF (TRIM(ERRMSG) # "") THEN
       STATUS = RBO.setProperty('','ServerStatus','1')
       STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  END ELSE
       STATUS = RBO.setProperty('','ServerStatus','')
       STATUS = RBO.setProperty('','ServerMessage','')
  END 
RETURN
