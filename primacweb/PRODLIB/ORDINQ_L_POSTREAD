SUBROUTINE ORDINQ_L_POSTREAD
********************************************************************************
*   Program name :- ORDINQ_L_POSTREAD
*   Created:- 12/02/2002
*   Author:- L. Ross
*   Gets info for Order Inquiry 'L'ine option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV.CNV
$INCLUDE CPYLIB CHAR
$INCLUDE OPS.CPYLIB ORDER
$INCLUDE OPS.CPYLIB ORDER.DETAIL
$INCLUDE OPS.CPYLIB ORDER.RELEASE
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE PMC.CPYLIB SHIP.TO

;* open files

ERRMSG=''
IF FILEINFO(INVENTORY,0)=0 THEN
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG<1,-1>='INVENTORY FILE IS MISSING'
   END
END
IF FILEINFO(ORDER,0)=0 THEN
  OPEN '','ORDER' TO ORDER ELSE
    ERRMSG<1,-1>='ORDER FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.DETAIL,0)=0 THEN
  OPEN '','ORDER.DETAIL' TO ORDER.DETAIL ELSE
    ERRMSG<1,-1>='ORDER.DETAIL FILE IS MISSING'
  END
END
IF FILEINFO(ORDER.RELEASE,0)=0 THEN
  OPEN '','ORDER.RELEASE' TO ORDER.RELEASE ELSE
    ERRMSG<1,-1>='ORDER.RELEASE FILE IS MISSING'
  END
END
IF FILEINFO(SHIP.TO,0)=0 THEN
  OPEN '','SHIP.TO' TO SHIP.TO ELSE
    ERRMSG<1,-1>='SHIP.TO FILE IS MISSING'
  END
END
IF FILEINFO(CUSTOMER,0)=0 THEN
  OPEN '','CUSTOMER' TO CUSTOMER ELSE
    ERRMSG<1,-1>='CUSTOMER FILE IS MISSING'
  END
END

IF ERRMSG#'' THEN GOTO 93000

;* initialize
GEN.SHPNO = "000"

;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
ORDNO=FIELD(ID,"!",1)[4,8]
SHPNO=FIELD(ID,"!",2)

;* get database values

MATREAD ORD.REC FROM ORDER,CONO:ORDNO ELSE
  ERRMSG='Cannot read ORDER record ':CONO:ORDNO
  GOTO 93000
END
MATREADU CUST.REC FROM CUSTOMER, CONO:ORD.CUST THEN
  
END

MATREAD ORD.DET.REC FROM ORDER.DETAIL,ID ELSE
  ERRMSG='Cannot read ORDER.DETAIL record ':ID
  GOTO 93000
END
IF SHPNO = GEN.SHPNO THEN
    MAT SPT.REC = ""
    SPT.STATE = TRIM(FIELD(CUST.ADDR4,",",2))
    SPT.SHP.TERMS = CUST.ADDL.OPS<1,3>
END ELSE
    MATREAD SPT.REC FROM SHIP.TO, CONO:ORD.CUST:"!":SHPNO ELSE
      ERRMSG = "Cannot locate ship to # ":SHPNO
      GOTO 93000
    END
END
MATREADU CUST.REC FROM CUSTOMER, CONO:ORD.CUST THEN
   IF OSD.SHP.TERMS = "" AND SPT.SHP.TERMS # "" THEN
     OSD.SHP.TERMS = SPT.SHP.TERMS
   END
END

PCNT=DCOUNT(OSD.PROD,VM)
PRODDESCS=''; UMS=''
MSTATS=''; TSTATS=''; RSTATS=''; SSTATS=''
OQTYS=''; RQTYS=''; AQTYS=''; GQTYS=''; UPRICES=''
KIT.OQTYS=''; KIT.RQTYS=''; KIT.AQTYS=''; KIT.GQTYS=''; KIT.UPRICES=''
FOR P = 1 TO PCNT
  MATREAD INV.REC FROM INVENTORY,CONO:OSD.PROD<1,P> ELSE
    INV.FULL.DESC = 'Unknown'; INV.UNIT<1,3> = 'EA'; INV.M.LINE = 'FNGD'
  END
  CALL GetInvUMCnv(ERRMSG,IN_PARAM,OUT_PARAR,MAT INV.CNV.REC,MAT INV.REC)
  PRODDESCS<1,P> = INV.FULL.DESC
  UMS<1,P> = INV.UNIT<1,3>
  OQTYS<1,P> = OCONV(INT(((OSD.O.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  GQTYS<1,P> = OCONV(INT(((OSD.G.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  RQTYS<1,P> = OCONV(INT(((OSD.R.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  AQTYS<1,P> = OCONV(INT(((OSD.A.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  KIT.OQTYS<1,P> = OCONV(INT(((OSD.KIT.O.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  KIT.GQTYS<1,P> = OCONV(INT(((OSD.KIT.G.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  KIT.RQTYS<1,P> = OCONV(INT(((OSD.KIT.R.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  KIT.AQTYS<1,P> = OCONV(INT(((OSD.KIT.A.QTY<1,P> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)'R#10'
  UPRICES<1,P> = OCONV(OSD.PRICE<1,P>,'MD4')
  KIT.UPRICES<1,P> = OCONV(OSD.KIT.PRICE<1,P>,'MD4')
  IF OSD.COMMENT<1,P> = "" THEN
    MSTATS<1,P> = " "
  END ELSE
    MSTATS<1,P> = "*"
  END
  IF OSD.G.QTY<1,P>+0 > 0 THEN
    TSTATS<1,P> = " "
  END ELSE 
    TSTATS<1,P> = "*"
  END
  IF OSD.REL.NO<1,P> # "" THEN
    RSTATS<1,P> = "*"
  END ELSE
    OCNT = DCOUNT(ORD.REL.NO,VM)
    FOR O = 1 TO OCNT UNTIL RSTATS<1,P> # ""
      MATREAD ORR.REC FROM ORDER.RELEASE, CONO:ORD.REL.NO<1,O> THEN
        IF ORR.STATUS<1,1> # "COMPLETED" AND ORR.SHIP.TO = SHPNO AND ORR.QTY<1,P>+0 > 0 THEN
          RSTATS<1,P> = "*"
        END
      END
    NEXT O
  END
  IF OSD.KIT<1,P> = "N" THEN
    BEGIN CASE
      CASE OSD.O.QTY<1,P> < 1
        SSTATS<1,P> = "O"
      CASE OSD.I.QTY<1,P> >= OSD.O.QTY<1,P>
        SSTATS<1,P> = "I"
      CASE OSD.S.QTY<1,P> >= OSD.O.QTY<1,P>
        SSTATS<1,P> = "S"
      CASE OSD.A.QTY<1,P> < 1
        SSTATS<1,P> = "O"
      CASE OSD.F.QTY<1,P> >= OSD.A.QTY<1,P>
        SSTATS<1,P> = "F"
      CASE 1
        SSTATS<1,P> = "M"
    END CASE
  END
  IF OSD.KIT<1,P> = "K" THEN
    BEGIN CASE
      CASE OSD.KIT.O.QTY<1,P> < 1
        SSTATS<1,P> = "O"
      CASE OSD.KIT.I.QTY<1,P> >= OSD.KIT.O.QTY<1,P>
        SSTATS<1,P> = "I"
      CASE OSD.KIT.S.QTY<1,P> >= OSD.KIT.O.QTY<1,P>
        SSTATS<1,P> = "S"
      CASE OSD.KIT.A.QTY<1,P> < 1
        SSTATS<1,P> = "O"
      CASE OSD.KIT.F.QTY<1,P> >= OSD.KIT.A.QTY<1,P>
        SSTATS<1,P> = "F"
      CASE 1
        SSTATS<1,P> = "M"
    END CASE
  END
NEXT P

STATUS=RBO.setProperty('','Proddescs',PRODDESCS)
STATUS=RBO.setProperty('','Ums',UMS)
STATUS=RBO.setProperty('','OSD_PRICE',UPRICES)
STATUS=RBO.setProperty('','OSD_O_QTY',OQTYS)
STATUS=RBO.setProperty('','OSD_G_QTY',GQTYS)
STATUS=RBO.setProperty('','OSD_R_QTY',RQTYS)
STATUS=RBO.setProperty('','OSD_A_QTY',AQTYS)
STATUS=RBO.setProperty('','Mstats',MSTATS)
STATUS=RBO.setProperty('','Tstats',TSTATS)
STATUS=RBO.setProperty('','Rstats',RSTATS)
STATUS=RBO.setProperty('','Sstats',SSTATS)
STATUS=RBO.setProperty('','OSD_KIT_PRICE',KIT.UPRICES)
STATUS=RBO.setProperty('','OSD_KIT_O_QTY',KIT.OQTYS)
STATUS=RBO.setProperty('','OSD_KIT_G_QTY',KIT.GQTYS)
STATUS=RBO.setProperty('','OSD_KIT_R_QTY',KIT.RQTYS)
STATUS=RBO.setProperty('','OSD_KIT_A_QTY',KIT.AQTYS)
STATUS=RBO.setProperty('','OSD_SHP_TERMS',OSD.SHP.TERMS)
STATUS=RBO.setProperty('','SPT_STATE',SPT.STATE)

GOTO 99999

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG<1,-1>)  

99999
RETURN

