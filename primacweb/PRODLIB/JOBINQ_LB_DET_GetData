SUBROUTINE JOBINQ_LB_DET_GetData
********************************************************************************
*   Program name :- JOBINQ_LB_DET_GetData
*   Created:- 12/16/2002
*   Author:- L. Ross
*   Gets info for Job Inquiry 'LB Detail' option.
*------------------------------------------------------------------------------*
*
*  

* In Properties:
* --------------
*  
IN_PARAM=''
*
* Out Properties:
* ---------------
*  
OUT_PARAM=''
********************************************************************************
$INCLUDE WWINSERT RBO.H
$INCLUDE PMC.CPYLIB EMPLOYEE
$INCLUDE PMC.CPYLIB DEPARTMENT
$INCLUDE PMC.CPYLIB COST.CNTR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TIME
$INCLUDE JCS.CPYLIB OPERATION
$INCLUDE CPYLIB CHAR

;* open files

ERRMSG=''
IF FILEINFO(EMPLOYEE,0)=0 THEN
   OPEN '','EMPLOYEE' TO EMPLOYEE ELSE
      ERRMSG<1,-1>='EMPLOYEE FILE IS MISSING'
   END
END
IF FILEINFO(JOB,0)=0 THEN
   OPEN '','JOB' TO JOB ELSE
      ERRMSG<1,-1>='JOB FILE IS MISSING'
   END
END
IF FILEINFO(JOB.TIME,0)=0 THEN
   OPEN '','JOB.TIME' TO JOB.TIME ELSE
      ERRMSG<1,-1>='JOB.TIME FILE IS MISSING'
   END
END
IF FILEINFO(OPERATION,0)=0 THEN
   OPEN '','OPERATION' TO OPERATION ELSE
      ERRMSG<1,-1>='OPERATION FILE IS MISSING'
   END
END
IF FILEINFO(DEPARTMENT,0)=0 THEN
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG<1,-1>='DEPARTMENT FILE IS MISSING'
   END
END
IF FILEINFO(COST.CNTR,0)=0 THEN
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG<1,-1>='COST.CNTR FILE IS MISSING'
   END
END
IF ERRMSG#'' THEN GOTO 93000
DIM TRANS.REC(15)
EQU TRAN.TYPE     TO TRANS.REC(1)
EQU TRAN.CNCR     TO TRANS.REC(2)
EQU TRAN.EMP      TO TRANS.REC(3)
EQU TRAN.EMP.NAME TO TRANS.REC(4)
EQU TRAN.NO       TO TRANS.REC(5)
EQU TRAN.DATE     TO TRANS.REC(6)
EQU TRAN.HRS      TO TRANS.REC(7)
EQU TRAN.IMP      TO TRANS.REC(8)
EQU TRAN.COST     TO TRANS.REC(9)
EQU TRAN.SALE     TO TRANS.REC(10)
EQU TRAN.FCTR     TO TRANS.REC(11)
EQU TRAN.JOB      TO TRANS.REC(12)
EQU TRAN.OPER     TO TRANS.REC(13)
EQU TRAN.OPER.DES TO TRANS.REC(14)
EQU TRAN.PERIOD   TO TRANS.REC(15)
MAT TRANS.REC = ''
UNKNOWN = STR("?",20)
SAVE.INV.JS.REC=''

;* initialize


;* get ID

STATUS=RBO.getProperty('','ID',ID)
CONO=ID[1,3]
JOB_NO=ID[4,8]
STATUS=RBO.getProperty('','MAST_SUB_FLAG',MAST.SUB.FLAG)
STATUS=RBO.getProperty('','DEPT',DEPT)
STATUS=RBO.getProperty('','CCTR',CCTR)

;* get database values

MATREAD JOB.REC FROM JOB,ID ELSE
   ERRMSG='Cannot read JOB record ':ID
   GOTO 93000
END
IF MAST.SUB.FLAG THEN
   ITYPE='LB'
   CALL MasterSummarySub(CONO,JOB_NO,SAVE.INV.JS.REC,MAT JOB.REC,ITYPE)

*T28046
   STATUS = RBO.getProperty('','ERRMSG',ERRMSG) 
   IF ERRMSG # "" THEN GOTO 93000
*T28046


   SUB.CNT=DCOUNT(JOB.SUBS,VM)+1
END ELSE SUB.CNT = 1
PTR=1
LOOP
   LOCATE DEPT IN JOB.LB.DEPT<1>,PTR SETTING FND ELSE
      PTR = 0
   END
   IF PTR # 0 THEN
      IF CCTR = JOB.LB.CCTR<1,FND> THEN
         PTR = FND
         FND = 0
      END ELSE
         PTR = FND + 1
      END
   END
WHILE PTR AND FND DO
REPEAT
IF NOT(PTR) THEN
   ERRMSG='Cannot locate Dept ':DEPT:' Cost Center ':CCTR:' in JOB record!'
   GOTO 93000
END
MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT ELSE DEPT.DESC = UNKNOWN
MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR ELSE CCTR.DESC = UNKNOWN

LN = 1; OLN = 1; LINES = JOB.LB.SEQ<1,PTR,1>
LB.ID = CONO:JOB_NO:"!":JOB.LB.DEPT<1,PTR>:"!":JOB.LB.CCTR<1,PTR>:"!"
GOSUB 100
FOR K = 2 TO SUB.CNT
   LB.ID = CONO:JOB.SUBS<1,K-1>:"!":JOB.LB.DEPT<1,PTR>:"!":JOB.LB.CCTR<1,PTR>:"!"
   OLN = LINES + 1; LINES = LINES + JOB.LB.SEQ<1,PTR,K>
   GOSUB 100
NEXT K

STATUS=RBO.setProperty('','DEPT_DESC',DEPT.DESC)
STATUS=RBO.setProperty('','CCTR_DESC',CCTR.DESC)
STATUS=RBO.setProperty('','OPER',TRAN.OPER)
STATUS=RBO.setProperty('','FACTOR',TRAN.FCTR)
STATUS=RBO.setProperty('','TYPE',TRAN.TYPE)
STATUS=RBO.setProperty('','CONCUR',TRAN.CNCR)
STATUS=RBO.setProperty('','LB_TRANS',TRAN.NO)
STATUS=RBO.setProperty('','DATE',TRAN.DATE)
STATUS=RBO.setProperty('','EMP',TRAN.EMP)
STATUS=RBO.setProperty('','PERIOD',TRAN.PERIOD)
STATUS=RBO.setProperty('','UNITS',TRAN.HRS)
STATUS=RBO.setProperty('','IMPRESS',TRAN.IMP)
STATUS=RBO.setProperty('','RCLS_JOB',TRAN.JOB)
STATUS=RBO.setProperty('','COST',TRAN.COST)
STATUS=RBO.setProperty('','EMP_NAME',TRAN.EMP.NAME)
STATUS=RBO.setProperty('','OPER_NAME',TRAN.OPER.DES)
STATUS=RBO.setProperty('','SALE',TRAN.SALE)
GOTO 99999

100*
FOR I = OLN TO LINES
   MATREAD JLB.REC FROM JOB.TIME, LB.ID:(I-OLN+1) ELSE MAT JLB.REC = ""
   MATREAD EMP.REC FROM EMPLOYEE , CONO:JLB.EMP ELSE
      EMP.LAST.NAME = UNKNOWN; EMP.FRST.NAME = UNKNOWN
   END
   MATREAD OPER.REC FROM OPERATION, CONO:JLB.OPER ELSE
      OPER.DESC = UNKNOWN
   END
   TRAN.TYPE<1,I> = JLB.TYPE
   IF JLB.CNCR + 0 <> 0 THEN
      TRAN.CNCR<1,I> = "Y"
   END ELSE
      TRAN.CNCR<1,I> = "N"
   END
   TRAN.EMP<1,I> = JLB.EMP
   TRAN.NO<1,I> = JLB.SEQ
   TRAN.DATE<1,I> = OCONV(JLB.DATE,'D2/')
   TRAN.HRS<1,I> = OCONV(JLB.HRS,'MD2')
   IF MOD(JLB.IMP,100) THEN
      TRAN.IMP<1,I> = OCONV(JLB.IMP, "MD2")
   END ELSE
      TRAN.IMP<1,I> = INT(JLB.IMP / 100)
   END
   TRAN.COST<1,I> = OCONV(JLB.COST,'MD2')
   TRAN.SALE<1,I> = OCONV(JLB.SALE,'MD2')
   TRAN.FCTR<1,I> = JLB.FCTR
   TRAN.JOB<1,I> = JLB.RC.JOB
   TRAN.EMP.NAME<1,I> = EMP.LAST.NAME:' ':EMP.FRST.NAME
   TRAN.OPER<1,I> = JLB.OPER
   TRAN.OPER.DES<1,I> = OPER.DESC
   TRAN.PERIOD<1,I> = JLB.MON<1,1>
NEXT I
RETURN

93000 
STATUS = RBO.setProperty('','ServerStatus',1)        
STATUS = RBO.setProperty('','ServerMessage',ERRMSG)  

99999 
RETURN

