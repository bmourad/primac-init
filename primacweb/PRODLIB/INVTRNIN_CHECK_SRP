  SUBROUTINE INVTRNIN_CHECK_SRP
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - INVTRNIN_CHECK_SRP
*
* AUTHOR   - ALEJANDRO DELGADO
*
* DATE     - 11/27/2002
*
* DESCRIPTION
*
* The program builds the need RBO string for Transaction History Inq.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
  $INCLUDE WWINSERT RBO.H
  $INCLUDE ICS.CPYLIB INVENTORY
  $INCLUDE ICS.CPYLIB CATEGORY
  $INCLUDE ICS.CPYLIB INV_SERIAL
  $INCLUDE ICS.CPYLIB INV_RECEIPTS
  $INCLUDE CPYLIB CHAR
  $INCLUDE CPYLIB FILE.VARS
*
**** OPEN FILES
*
  ERRMSG = "" ; IN_PARAM = "" ; OUT_PARAM = ""
  IN_PARAM<1,1> = 'INV_SERIAL'
  IN_PARAM<1,2> = 'INV_RECEIPTS'
  IN_PARAM<1,3> = 'INVENTORY'
  IN_PARAM<1,4> = 'CATEGORY'
  CALL GetInvOpenFiles(ERRMSG,IN_PARAM,OUT_PARAM,MAT FILE.VARS)
  IF ERRMSG # "" THEN GOTO SET.ERROR
*
*---- INITIALIZATION
*
  ERRMSG = RBO.getProperty('','ID',ID)
  CONO = ID[1,3]
  ID = ID[4,99]
  *
  ID.FLAG = ''
  SER.EXISTS=1;RECP.EXISTS=1;PROD.EXISTS=1
  MATREAD ISTK.REC FROM INV_SERIAL,CONO:ID ELSE SER.EXISTS=0
  MATREAD INVR.REC FROM INV_RECEIPTS,CONO:ID ELSE RECP.EXISTS=0
  MATREAD INV.REC FROM INVENTORY, CONO:ID ELSE PROD.EXISTS=0
  EXISTS.SUM=SER.EXISTS+RECP.EXISTS+PROD.EXISTS
  BEGIN CASE
    CASE EXISTS.SUM=0
      ERRMSG='PRODUCT, SERIAL, AND RECEIPT ':ID:' DOES NOT EXIST.'
      GOTO SET.ERROR
    CASE EXISTS.SUM=1
      BEGIN CASE
        CASE PROD.EXISTS
          ID.FLAG = 'P'
          PROD.SEL=ID; PROD.DESC.SEL=INV.FULL.DESC
        CASE SER.EXISTS
          ID.FLAG = 'S'
          PROD.SEL=ISTK.PROD
          SERIAL=ID
          INV.ID=CONO:ISTK.PROD
          MATREAD INV.REC FROM INVENTORY,INV.ID ELSE
            ERRMSG="PRODUCT ":ISTK.PROD:" FOR SERIAL ":ID:" IS MISSING."
            GOTO SET.ERROR
          END
        CASE RECP.EXISTS
          ID.FLAG = 'R'
          PROD.SEL=INVR.PROD
          RECP=ID
          INV.ID=CONO:INVR.PROD
          MATREAD INV.REC FROM INVENTORY,INV.ID ELSE
            ERRMSG="PRODUCT ":INVR.PROD:" FOR RECEIPT ":ID:" IS MISSING."
            GOTO SET.ERROR
          END
      END CASE
    CASE EXISTS.SUM>1
      ERRMSG = 'INVALID ENTRY'
      GOTO SET.ERROR
  END CASE
  DESCRIPTION = PROD.SEL:'- ':INV.FULL.DESC
  STOCK.UOM = INV.UNIT<1,2>
  MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
    ERRMSG="CATEGORY  ": CONO : INV.LINE :" IS MISSING"
    GOTO SET.ERROR
  END
*
  STATUS = RBO.setProperty('','INV_FULL_DESC',DESCRIPTION)
  STATUS = RBO.setProperty('','STOCK_UOM',STOCK.UOM)
  STATUS = RBO.setProperty('','ID_FLAG',ID.FLAG)
  STATUS = RBO.setProperty('','CATG_TRK_LVL',CATG.TRK.LVL)

  RETURN
*
SET.ERROR: 
*
  STATUS = RBO.setProperty('','ServerStatus',1)
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  RETURN
END
