  SUBROUTINE INVINQ_J_READ
*********************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - INVINQ_J_READ
*
* AUTHOR   - ALEJANDRO DELGADO
*
* DATE     - 09/27/2002
*
* DESCRIPTION
*
* This program builds the data from INVENTORY file to load into 
* Job Reserved Inquiry screen.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
  $INCLUDE WWINSERT RBO.H
  $INCLUDE ICS.CPYLIB INVENTORY
  $INCLUDE ICS.CPYLIB INV.STATS
  $INCLUDE ICS.CPYLIB INV.JOB.STATS
  $INCLUDE ICS.CPYLIB INV.CNV
  $INCLUDE PMC.CPYLIB CUSTOMER
  $INCLUDE CPYLIB>FILE.VARS
  $INCLUDE CPYLIB CHAR
  DEFFUN CALC.STK.QTY(COST.QTY,MAT INV.CNV.REC,ROND,LN)
*
*---- INITIALIZATION
*
  ERRMSG = RBO.getProperty('','ID',PRODUCT.ID)
  ERRMSG = RBO.getProperty('','WHSE_ID',WHSE.ID)
  CONO = PRODUCT.ID[1,3]
  PRODUCT.ID = PRODUCT.ID[4,99]

  
 *TEST = '#'

  *
  ERRMSG = "" ; IN_PARAM = "" ; OUT_PARAM = ""
  IN_PARAM<1,1> = 'INVENTORY'
  IN_PARAM<1,2> = 'INV.STATS'
  IN_PARAM<1,3> = 'INV.JOB.STATS'
  IN_PARAM<1,4> = 'CUSTOMER'
  CALL GetInvOpenFiles(ERRMSG,IN_PARAM,OUT_PARAM,MAT FILE.VARS)
  IF ERRMSG # '' THEN GOTO SET.ERROR
  MATREAD INV.REC FROM INVENTORY, CONO:PRODUCT.ID THEN
   $INCLUDE ICSBP INV.UM.CNV
  END ELSE
    ERRMSG = 'Inventory is not on file.  Try again'
    GOTO SET.ERROR
  END
  IWH.ID = CONO:PRODUCT.ID:'!':WHSE.ID
*  TEST = TEST : "~" : IWH.ID

  MATREAD INV.STAT.REC FROM INV.STATS, IWH.ID ELSE MAT INV.STAT.REC = ''
  ERRMSG = "" ; IN_PARAM = "" ; OUT_PARAM = "" ; MAT INV.CNV.REC = ""
  CALL GetInvUMCnv(ERRMSG,IN_PARAM,OUT_PARAM,MAT INV.CNV.REC,MAT INV.REC)
*
*---- PROCESSING
*
  JOB.CUST = '' ; JOB.CUST.NAME = ''
  JOB.ALLOCATED = '' ; JOB.TOT.ALLOCATED = 0
  JOB.RESERVED = '' ; JOB.TOT.RESERVED = 0
  JOB.USED = '' ; JOB.TOT.USED = 0
  JOB.RSV.DATE = ''
  PCNT = DCOUNT(ISTAT.JOB, VM)
  FOR I = 1 TO PCNT
    ID = CONO:PRODUCT.ID:"!":WHSE.ID:"!":ISTAT.JOB<1,I>
    TEST = TEST : "~" : ID

    MATREAD INV.JS.REC FROM INV.JOB.STATS,ID ELSE
      MAT INV.JS.REC = ""
    END
    CUST.ID = CONO:IJS.JOB.CUST
    MATREAD CUST.REC FROM CUSTOMER,CUST.ID ELSE MAT CUST.REC = ''
    JOB.CUST.NAME<1,I> = CUST.NAME
    JOB.CUST<1,I> = IJS.JOB.CUST
    JOB.RSV.DATE<1,I> = OCONV(IJS.JOB.DATE, 'D2/')
    TMP = CALC.STK.QTY(IJS.JOB.ALOC,MAT INV.CNV.REC,'','')
    JOB.ALLOCATED<1,I> = OCONV(TMP,ICR.CNV1)
    TMP = CALC.STK.QTY(IJS.JOB.QTY,MAT INV.CNV.REC,'','')
    JOB.RESERVED<1,I> = OCONV(TMP,ICR.CNV1)
    TMP = CALC.STK.QTY(IJS.JOB.USED,MAT INV.CNV.REC,'','')
    JOB.USED<1,I> = OCONV(TMP,ICR.CNV1)
    JOB.TOT.ALLOCATED +=IJS.JOB.ALOC
    JOB.TOT.RESERVED += IJS.JOB.QTY
    JOB.TOT.USED+=IJS.JOB.USED
  NEXT I

  TMP=CALC.STK.QTY(JOB.TOT.ALLOCATED,MAT INV.CNV.REC,'','')
  JOB.TOT.ALLOCATED = OCONV(TMP,ICR.CNV1)
  TMP=CALC.STK.QTY(JOB.TOT.RESERVED,MAT INV.CNV.REC,'','')
  JOB.TOT.RESERVED = OCONV(TMP,ICR.CNV1)
  TMP=CALC.STK.QTY(JOB.TOT.USED,MAT INV.CNV.REC,'','')
  JOB.TOT.USED = OCONV(TMP,ICR.CNV1)
*
*---- BUILD RBO RECORD
*
  STATUS = RBO.setProperty('','JOB_NUMBER',ISTAT.JOB)
  STATUS = RBO.setProperty('','JOB_CUST',JOB.CUST)
  STATUS = RBO.setProperty('','JOB_CUST_NAME',JOB.CUST.NAME)
  STATUS = RBO.setProperty('','JOB_RSV_DATE',JOB.RSV.DATE)
  STATUS = RBO.setProperty('','JOB_ALLOCATED',JOB.ALLOCATED)
  STATUS = RBO.setProperty('','JOB_RESERVED',JOB.RESERVED)
  STATUS = RBO.setProperty('','JOB_USED',JOB.USED)
  STATUS = RBO.setProperty('','JOB_TOT_ALLOCATED',JOB.TOT.ALLOCATED)
  STATUS = RBO.setProperty('','JOB_TOT_RESERVED',JOB.TOT.RESERVED)
  STATUS = RBO.setProperty('','JOB_TOT_USED',JOB.TOT.USED)
  RETURN
*
SET.ERROR: 
*
  STATUS = RBO.setProperty('','ServerStatus',1)
  STATUS = RBO.setProperty('','ServerMessage',ERRMSG)
  RETURN
END



