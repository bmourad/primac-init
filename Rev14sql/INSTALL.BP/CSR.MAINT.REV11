*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - CSR.MAINT
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC.
* DATE        - 08/03/92
* DESCRIPTION -
* This program will be used for Customer Service Rep Maintenance.
*T22664 rick 03/26/1998 * Modify CSR Maint to allow for User Login name
*T22664 rick 04/16/1998 * CSR/SLS mod
*T22664 rick 04/16/1998 * csr/sls
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CSR.CODE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
OPEN '','CSR.CODE' TO CSR.CODE ELSE ERRMSG = 'CSR.CODE FILE MISSING';GOTO 93000
OPEN '','PREFIX' TO PREFIX ELSE ERRMSG = 'PREFIX FILE MISSING';GOTO 93000
OPEN '','XREF.DATA' TO XREF.DATA ELSE ERRMSG = 'XREF.DATA FILE MISSING';GOTO 93000
*T22664 v
OPEN "SECURITY" TO SECURITY ELSE ERRMSG="SECURITY FILE MISSING" ; GOTO 93000
*T22664 ^
*
*---- INITIALIZATION
*
MAT EDIT.COM.DRIVER = ""
ERRMSG = ''
*
*---- GET COMPANY NUMBER
*
CONO = ''
CALL GET.CONO(CONO,MAT COMP.REC)
IF CONO = 'END' THEN GOTO 99999
MAT GEN.XREF.REC = ""
GXR.CO = CONO
*
*---- PRINT SCREEN
*
ECD.SCRN.CNT = 1
ECD.SCRN.NAME<1> = 'CSR.MAINT'
ECD.ACTION = 1;CALL SCRN.EDIT
ECD.SCRN.NO = 1
MAT SCV.REC = ""
ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
NEW.CSR = 0
ECD.NUM = 1;ECD.ACTION = 4;CALL SCRN.EDIT
BEGIN CASE
CASE ECD.RET.VALUE = 'END'
  GOTO 99999
CASE ECD.RET.VALUE = "" OR ECD.RET.VALUE = "???"
  GXR.NAME = "GEN.CODE"
  GXR.SORT.FILE = "CSR.CODE"
  GXR.FILE = CSR.CODE
  CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
  ECD.ACTION = 2; CALL SCRN.EDIT
  IF GXR.ID = "" THEN GOTO 10
  ECD.RET.VALUE = GXR.ID
  MATREADU CSR.REC FROM CSR.CODE, CONO:ECD.RET.VALUE ELSE
    ERRMSG = "CSR CODE NOT ON FILE"
    GOSUB 91000
    GOTO 10
  END
  SCV.REC(1)<ECD.SCRN.NO> = ECD.RET.VALUE
  ECD.NUM = 1; ECD.ACTION = 5; CALL SCRN.EDIT
CASE 1
  SAVE.CSR = ECD.RET.VALUE
  MATREADU CSR.REC FROM CSR.CODE, CONO:SAVE.CSR ELSE
    ECD.NUM = 4; ECD.ACTION = 4; CALL SCRN.EDIT
    IF ECD.RET.VALUE # "Y" THEN
      RELEASE CSR.CODE, CONO:SAVE.CSR
      GOTO 10
    END
    NEW.CSR = 1; MAT CSR.REC = ""
  END
  ECD.RET.VALUE = SAVE.CSR
END CASE
CSR.KEY = ECD.RET.VALUE
IF NEW.CSR THEN
*T22664 v 
  FOR III = 1 TO 3
    ON III GOSUB 20,30,40
    IF ECD.RET.VALUE = 'END' THEN
      GOSUB 300;GOTO 10
    END
  NEXT III
*T22664 ^
END ELSE
  SCV.REC(3)<ECD.SCRN.NO,1> = CSR.DESC
  ECD.NUM = 3;ECD.ACTION = 5;CALL SCRN.EDIT
*T22664 v          
  SCV.REC(6)<ECD.SCRN.NO,1> = CSR.USER
  ECD.NUM = 6;ECD.ACTION = 5;CALL SCRN.EDIT
*T22664 ^          
  SCV.REC(5)<ECD.SCRN.NO,1> = CSR.EMAIL
  ECD.NUM = 5;ECD.ACTION = 5;CALL SCRN.EDIT
END
GOSUB 200
GOSUB 300
GOTO 10
*
*---- ENTER CSR CODE DESCRIPTION
*
20*
ECD.NUM = 3;ECD.ACTION = 4;CALL SCRN.EDIT
IF ECD.RET.VALUE # 'END' THEN
  CSR.DESC = ECD.RET.VALUE
END
RETURN
*
*---- ENTER OPTIONS
*
*T22664 v
30*
ECD.NUM = 6;ECD.ACTION = 4;CALL SCRN.EDIT
IF ECD.RET.VALUE # "" THEN
  IF ECD.RET.VALUE # 'END' THEN
    READ XX FROM SECURITY, CONO:ECD.RET.VALUE THEN
      CSR.USER = ECD.RET.VALUE
    END ELSE
      ERRMSG=ECD.RET.VALUE:" is not a valid user"
      GOSUB 91000
    END
  END
END ELSE
  CSR.USER = ECD.RET.VALUE
END
RETURN
*T22664 ^
*
*---- ENTER CSR E-MAIL ADDRESS
*
40*
ECD.NUM = 5 ; ECD.ACTION = 4 ; CALL SCRN.EDIT
IF ECD.RET.VALUE # 'END' THEN
  CSR.EMAIL = ECD.RET.VALUE
END
RETURN
*
200*
MORE = 1
LOOP
  ECD.NUM = 2;ECD.ACTION = 4;CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
  CASE OPTION = 'END' OR OPTION = 'E'
    MORE = 1
  CASE OPTION = '1'
    GOSUB 20
    MORE = 0
*T22664 v
  CASE OPTION = '2'
    GOSUB 30
    MORE = 0
*T22664 ^                    
  CASE OPTION = '3'
    GOSUB 40
    MORE = 0
  CASE OPTION = 'F'
    GOSUB 400
    MORE = 1
  END CASE
WHILE MORE = 0 DO REPEAT
RETURN
*
*---- CLEAR SCREEN & RE-INITIALIZE
*
300*
ECD.ACTION = 6;CALL SCRN.EDIT
MAT SCV.REC = ""
RETURN
*
*---- UPDATE CSR FILE
*
400*
MATWRITE CSR.REC ON CSR.CODE , CONO:CSR.KEY
RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
END
