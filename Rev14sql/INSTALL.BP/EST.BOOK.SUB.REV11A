      SUBROUTINE EST.BOOK.JOB (CONO,ACTION,EST.NO)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.BOOK.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/28/87
*
* DESCRIPTION
*
* This subroutine is used to re-calculate job related estimate data when
* a booked estimate is modified.
*
* MOD TASK 17918 DMT 8/19/94 ADD BACKMARGIN CALC
*T21710 doug 03/31/1997 * Add subroutine call to update EST.JOB.ACT
*                         for PowerBuilder
*T21723 sfc 06/03/1997 * Add product class type to each material entry
*T23945 gat 05/11/1999 * FIX MO FINAL PRICE
*T25008 edvard 04/03/2000 * FIX PROBLEM WITH JOB.CONF.AMT. SEE CSF 36042
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JCS.CPYLIB>JOB
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.JOB
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>ICS.CPYLIB>CATEGORY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
      OPEN "","JOB" TO JOB ELSE
         ERRMSG = "CANNOT OPEN JOB FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","ESTIMATE.JOB" TO ESTIMATE.JOB ELSE
         ERRMSG = "CANNOT OPEN ESTIMATE.JOB FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CATEGORY" TO CATEGORY ELSE
         ERRMSG = "CANNOT OPEN CATEGORY FILE"
         GOSUB 90000
         GOTO 99999
      END
* T21710 v
      EST.ACT.FLG = 0
      OPEN "","EST.JOB.ACT" TO EST.JOB.ACT THEN EST.ACT.FLG = 1
* T21710 ^
*
*---- INITIALIZATION
*
      JCNT = COUNT(EST.BOOK.JOB,VM) + (EST.BOOK.JOB # "")
      LOCATE EST.BOOK.QTY IN EST.QTY<1>,1 SETTING QP ELSE QP = 0
      IF @LOGNAME = 'edvard' THEN DEBUG
*
*---- MAIN PROCESSING
*
100*
      FOR JP = 1 TO JCNT
         JOB.NO = EST.BOOK.JOB<1,JP>
         MATREADU JOB.REC FROM JOB, CONO:JOB.NO ELSE
            RELEASE JOB, CONO:JOB.NO
            DELETE ESTIMATE.JOB, CONO:JOB.NO
            GOTO 290
         END
         DEPT = JOB.DEPT
         LOCATE DEPT IN EST.DEPT<1>,1 SETTING DPTR ELSE DPTR = 0
         MAT ESTJ.REC = ""
         ESTJ.EST.ID = EST.NO
         ESTJ.COMP = EST.BOOK.COMP<1,JP>
         ESTJ.QTY = EST.BOOK.QTY
         ESTJ.EST.TYPE = EST.TYPE
         ESTJ.DIV = FIELD(EST.DIV,"-",1)
         DC.CNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
         FOR LN = 1 TO DC.CNT
            IF DEPT = "" OR DPTR = FIELD(EST.DEPT.COMP<1,LN>,"!",1) THEN
               COMP = FIELD(EST.DEPT.COMP<1,LN>,"!",2)
               LOCATE COMP IN EST.BOOK.COMP<1,JP>,1 SETTING CMATCH ELSE CMATCH = 0
               IF EST.BOOK.COMP<1,JP> = "ALL" OR CMATCH > 0 THEN
                  IF EST.BOOK.QTY = FIELD(EST.DEPT.COMP<1,LN>,"!",3) THEN
                     GOSUB 1000
                  END
               END
            END
         NEXT LN
         MATWRITE ESTJ.REC ON ESTIMATE.JOB, CONO:JOB.NO
         JOB.EST.TYPE = EST.TYPE
         IF QP > 0 THEN
            JOB.PRICE.PER.THOU = EST.PRICE.THOU<1,QP>
         END
         JOB.EST.COST = ""
         JOB.EST.SALE = ""
         DC.CNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
         FOR LN = 1 TO DC.CNT
            IF DEPT = "" OR DPTR = FIELD(EST.DEPT.COMP<1,LN>,"!",1) THEN
               COMP = FIELD(EST.DEPT.COMP<1,LN>,"!",2)
               LOCATE COMP IN EST.BOOK.COMP<1,JP>,1 SETTING CMATCH ELSE CMATCH = 0
               IF EST.BOOK.COMP<1,JP> = "ALL" OR CMATCH > 0 THEN
                  IF EST.BOOK.QTY = FIELD(EST.DEPT.COMP<1,LN>,"!",3) THEN
                     JOB.EST.COST = JOB.EST.COST + EST.DEPT.COMP.COST<1,LN>
                     JOB.EST.SALE = JOB.EST.SALE + EST.DEPT.COMP.TSALE<1,LN>
                  END
               END
            END
         NEXT LN
         JOB.EST.SALE = JOB.EST.SALE + (INT(OCONV(JOB.EST.SALE,'MD2') * OCONV(EST.OM.PCT<1,QP>,'MD4') + .005 / 100))
*25008 v
*T23945
*         JOB.CONF.AMT = EST.FINAL.PRICE<1,QP>
         IF QP > 0 THEN
            JOB.CONF.AMT = EST.FINAL.PRICE<1,QP>
         END
*T23945
*25008 ^
         MATWRITE JOB.REC ON JOB, CONO:JOB.NO
         IF EST.ACT.FLG THEN CALL EST.ACT.SUB(CONO,JOB.NO) ;* T21710
290   NEXT JP
      GOTO 99999
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- LOAD ESTJ.REC FROM ESTD.REC
*
1000*
      MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.NO:"!":EST.DEPT.COMP<1,LN> ELSE MAT ESTD.REC = ""
      TYPE.CNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
      FOR T = 1 TO TYPE.CNT
         ETYPE = ESTD.TYPE<1,T>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:ESTD.CCTR<1,T> ELSE MAT CCTR.REC = ""
         BEGIN CASE
            CASE ETYPE = "G"
            CASE ETYPE = "L"
               GOSUB 10100
            CASE ETYPE[1,1] = "M"
               MT.TYPE = ETYPE[2,1]
               OPV = ESTD.OPV<1,T>
               PROD = ESTD.OPV<1,T>
               IF MT.TYPE="S" OR MT.TYPE = "R" OR MT.TYPE = "L" THEN
                  WCNT = COUNT(EST.PROD.OS.PROD<1,COMP>,SM) + 1
                  FOR WP = 1 TO WCNT
                     IF PROD = EST.PROD.OS.PROD<1,COMP,WP> AND EST.PROD.INV.ID<1,COMP,WP> # "" THEN
                        PROD = EST.PROD.INV.ID<1,COMP,WP>
                     END
                  NEXT WP
               END
               GOSUB 10200
            CASE ETYPE = "I"
               OPV = ""
               PROD = ESTD.OPV<1,T>
               GOSUB 10200
            CASE ETYPE[1,1] = "P"
               GOSUB 10300
            CASE ETYPE = "S"
               GOSUB 10400
            CASE 1
               GOSUB 10500
         END CASE
      NEXT T
1999*
      RETURN
*
*--- LABOR
*
10100*
      DFND = 1
      MATCHED = 0
      DCNT = COUNT(ESTJ.LB.DEPT,VM) + (ESTJ.LB.DEPT # "")
      FOR D = 1 TO DCNT UNTIL MATCHED
         BEGIN CASE
            CASE CCTR.DEPT = ESTJ.LB.DEPT<1,D>
               BEGIN CASE
                  CASE ESTD.CCTR<1,T> = ESTJ.LB.CCTR<1,D>
                     BEGIN CASE
                        CASE ESTD.OPV<1,T> = ESTJ.LB.OPER<1,D>
                           DFND = D
                           MATCHED = 1
                        CASE ESTD.OPV<1,T> > ESTJ.LB.OPER<1,D>
                           DFND = D + 1
                     END CASE
                  CASE ESTD.CCTR<1,T> > ESTJ.LB.CCTR<1,D>
                     DFND = D + 1
               END CASE
            CASE CCTR.DEPT > ESTJ.LB.DEPT<1,D>
               DFND = D + 1
         END CASE
      NEXT D
      IF MATCHED = 0 THEN
         IF DFND <= DCNT THEN
            ESTJ.LB.DEPT = INSERT(ESTJ.LB.DEPT,1,DFND,0,"")
            ESTJ.LB.CCTR = INSERT(ESTJ.LB.CCTR,1,DFND,0,"")
            ESTJ.LB.OPER = INSERT(ESTJ.LB.OPER,1,DFND,0,"")
            ESTJ.LB.HRS = INSERT(ESTJ.LB.HRS,1,DFND,0,"")
            ESTJ.LB.QTY = INSERT(ESTJ.LB.QTY,1,DFND,0,"")
            ESTJ.LB.DCOST = INSERT(ESTJ.LB.DCOST,1,DFND,0,"")
            ESTJ.LB.COST = INSERT(ESTJ.LB.COST,1,DFND,0,"")
            ESTJ.LB.SALE = INSERT(ESTJ.LB.SALE,1,DFND,0,"")
         END
         ESTJ.LB.DEPT<1,DFND> = CCTR.DEPT
         ESTJ.LB.CCTR<1,DFND> = ESTD.CCTR<1,T>
         ESTJ.LB.OPER<1,DFND> = ESTD.OPV<1,T>
      END
      ESTJ.LB.HRS<1,DFND> = ESTJ.LB.HRS<1,DFND> + ESTD.HRS<1,T>
      IF ESTD.OPV.TYPE<1,T> = "F" THEN
         ESTJ.LB.QTY<1,DFND> = ESTJ.LB.QTY<1,DFND> + 0
      END ELSE
         ESTJ.LB.QTY<1,DFND> = ESTJ.LB.QTY<1,DFND> + ESTD.QTY<1,T>
      END
      ESTJ.LB.DCOST<1,DFND> = ESTJ.LB.DCOST<1,DFND> + ESTD.DCOST<1,T>
      ESTJ.LB.COST<1,DFND> = ESTJ.LB.COST<1,DFND> + ESTD.COST<1,T>
      ESTJ.LB.SALE<1,DFND> = ESTJ.LB.SALE<1,DFND> + ESTD.SALE<1,T>
      ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
      ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
      ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
      RETURN
*
*--- MATERIAL
*
10200*
      DFND = 1
      MATCHED = 0
      DCNT = COUNT(ESTJ.MT.DEPT,VM) + (ESTJ.MT.DEPT # "")
      FOR D = 1 TO DCNT UNTIL MATCHED
         BEGIN CASE
            CASE CCTR.DEPT = ESTJ.MT.DEPT<1,D>
               BEGIN CASE
                  CASE ESTD.CCTR<1,T> = ESTJ.MT.CCTR<1,D>
                     BEGIN CASE
                        CASE PROD = ESTJ.MT.PROD<1,D>
                           BEGIN CASE
                              CASE ESTD.CODE<1,T> = ESTJ.MT.PLINE<1,D>
                                 IF ESTD.UM<1,T> = ESTJ.MT.TYPE<1,D> THEN
                                    DFND = D
                                    MATCHED = 1
                                 END ELSE
                                    DFND = D + 1
                                 END
                              CASE ESTD.CODE<1,T> > ESTJ.MT.PLINE<1,D>
                                 DFND = D + 1
                           END CASE
                        CASE PROD > ESTJ.MT.PROD<1,D>
                           DFND = D + 1
                     END CASE
                  CASE ESTD.CCTR<1,T> > ESTJ.MT.CCTR<1,D>
                     DFND = D + 1
               END CASE
            CASE CCTR.DEPT > ESTJ.MT.DEPT<1,D>
               DFND = D + 1
         END CASE
      NEXT D
      IF MATCHED = 0 THEN
         IF DFND <= DCNT THEN
            ESTJ.MT.DEPT = INSERT(ESTJ.MT.DEPT,1,DFND,0,"")
            ESTJ.MT.CCTR = INSERT(ESTJ.MT.CCTR,1,DFND,0,"")
            ESTJ.MT.OPER = INSERT(ESTJ.MT.OPER,1,DFND,0,"")
            ESTJ.MT.PROD = INSERT(ESTJ.MT.PROD,1,DFND,0,"")
            ESTJ.MT.DESC = INSERT(ESTJ.MT.DESC,1,DFND,0,"")
            ESTJ.MT.PLINE = INSERT(ESTJ.MT.PLINE,1,DFND,0,"")
            ESTJ.MT.MLINE = INSERT(ESTJ.MT.MLINE,1,DFND,0,"")
            ESTJ.MT.QTY = INSERT(ESTJ.MT.QTY,1,DFND,0,"")
            ESTJ.MT.TYPE = INSERT(ESTJ.MT.TYPE,1,DFND,0,"")
            ESTJ.MT.DCOST = INSERT(ESTJ.MT.DCOST,1,DFND,0,"")
            ESTJ.MT.COST = INSERT(ESTJ.MT.COST,1,DFND,0,"")
            ESTJ.MT.SALE = INSERT(ESTJ.MT.SALE,1,DFND,0,"")
            ESTJ.MT.PROD.CLS = INSERT(ESTJ.MT.PROD.CLS,1,DFND,0,"")
         END
         ESTJ.MT.DEPT<1,DFND> = CCTR.DEPT
         ESTJ.MT.CCTR<1,DFND> = ESTD.CCTR<1,T>
         ESTJ.MT.OPER<1,DFND> = OPV
         ESTJ.MT.PROD<1,DFND> = PROD
         ESTJ.MT.DESC<1,DFND> = ESTD.DESC<1,T>
         ESTJ.MT.PLINE<1,DFND> = ESTD.CODE<1,T>
*T21723
         ESTJ.MT.PROD.CLS<1,DFND> = ESTD.TYPE<1,T>
*
         MATREAD CATG.REC FROM CATEGORY, CONO:ESTD.CODE<1,T> ELSE MAT CATG.REC = ""
         ESTJ.MT.MLINE<1,DFND> = CATG.MAJ.LINE
         MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:MT.TYPE ELSE MAT ESTM.REC = ""
         LOCATE ESTD.OPV<1,T> IN ESTM.PROD<1>,1 SETTING PF ELSE PF = 0
         IF PF THEN
            ESTJ.MT.TYPE<1,DFND> = ESTM.UM<1,PF>
         END ELSE
            BEGIN CASE
               CASE CATG.TYPE = "S" OR MT.TYPE = "S"
                  ESTJ.MT.TYPE<1,DFND> = "SHT"
               CASE CATG.TYPE = "P" OR CATG.TYPE = "F" OR CATG.TYPE = "O"
                  ESTJ.MT.TYPE<1,DFND> = "EA"
               CASE MT.TYPE = "P" OR MT.TYPE = "F" OR MT.TYPE = "O"
                  ESTJ.MT.TYPE<1,DFND> = "EA"
               CASE MT.TYPE = "B" OR MT.TYPE = "X"
                  ESTJ.MT.TYPE<1,DFND> = "EA"
               CASE CATG.TYPE = "I" OR MT.TYPE = "I"
                  ESTJ.MT.TYPE<1,DFND> = "LBS"
*CSF 25237 v
*           CASE CATG.TYPE = "L" OR MT.TYPE = "R"
               CASE CATG.TYPE = "L" OR (MT.TYPE = "R" AND CATG.TYPE # 'RL')
*CSF 25237 ^
                  ESTJ.MT.TYPE<1,DFND> = "LBS"
               CASE CATG.TYPE = "RL"
                  ESTJ.MT.TYPE<1,DFND> = "MSI"
               CASE CATG.TYPE = "PC"
                  ESTJ.MT.TYPE<1,DFND> = "MSI"
            END CASE
         END
      END
      ESTJ.MT.QTY<1,DFND> = ESTJ.MT.QTY<1,DFND> + ESTD.QTY<1,T>
      ESTJ.MT.DCOST<1,DFND> = ESTJ.MT.DCOST<1,DFND> + ESTD.DCOST<1,T>
      ESTJ.MT.COST<1,DFND> = ESTJ.MT.COST<1,DFND> + ESTD.COST<1,T>
      ESTJ.MT.SALE<1,DFND> = ESTJ.MT.SALE<1,DFND> + ESTD.SALE<1,T>
      ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
      ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
      ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
      RETURN
*
*--- OUTSIDE PURCHASE
*
10300*
      DFND = 1
      MATCHED = 0
      DCNT = COUNT(ESTJ.OS.DEPT,VM) + (ESTJ.OS.DEPT # "")
      FOR D = 1 TO DCNT UNTIL MATCHED
         BEGIN CASE
            CASE CCTR.DEPT = ESTJ.OS.DEPT<1,D>
               BEGIN CASE
                  CASE ESTD.CCTR<1,T> = ESTJ.OS.CCTR<1,D>
                     BEGIN CASE
                        CASE ESTD.OPV<1,T> = ESTJ.OS.OPER<1,D>
                           BEGIN CASE
                              CASE ESTD.CODE<1,T> = ESTJ.OS.PLINE<1,D>
                                 BEGIN CASE
                                    CASE ESTD.VEND<1,T> = ESTJ.OS.VEND<1,D>
                                       DFND = D
                                       MATCHED = 1
                                    CASE ESTD.VEND<1,T> > ESTJ.OS.VEND<1,D>
                                       DFND = D + 1
                                 END CASE
                              CASE ESTD.CODE<1,T> > ESTJ.OS.PLINE<1,D>
                                 DFND = D + 1
                           END CASE
                        CASE ESTD.OPV<1,T> > ESTJ.OS.OPER<1,D>
                           DFND = D + 1
                     END CASE
                  CASE ESTD.CCTR<1,T> > ESTJ.OS.CCTR<1,D>
                     DFND = D + 1
               END CASE
            CASE CCTR.DEPT > ESTJ.OS.DEPT<1,D>
               DFND = D + 1
         END CASE
      NEXT D
      IF MATCHED = 0 THEN
         IF DFND <= DCNT THEN
            ESTJ.OS.DEPT = INSERT(ESTJ.OS.DEPT,1,DFND,0,"")
            ESTJ.OS.CCTR = INSERT(ESTJ.OS.CCTR,1,DFND,0,"")
            ESTJ.OS.OPER = INSERT(ESTJ.OS.OPER,1,DFND,0,"")
            ESTJ.OS.DESC = INSERT(ESTJ.OS.DESC,1,DFND,0,"")
            ESTJ.OS.PLINE = INSERT(ESTJ.OS.PLINE,1,DFND,0,"")
            ESTJ.OS.VEND = INSERT(ESTJ.OS.VEND,1,DFND,0,"")
            ESTJ.OS.QTY = INSERT(ESTJ.OS.QTY,1,DFND,0,"")
            ESTJ.OS.DCOST = INSERT(ESTJ.OS.DCOST,1,DFND,0,"")
            ESTJ.OS.COST = INSERT(ESTJ.OS.COST,1,DFND,0,"")
            ESTJ.OS.SALE = INSERT(ESTJ.OS.SALE,1,DFND,0,"")
         END
         ESTJ.OS.DEPT<1,DFND> = CCTR.DEPT
         ESTJ.OS.CCTR<1,DFND> = ESTD.CCTR<1,T>
         ESTJ.OS.OPER<1,DFND> = ESTD.OPV<1,T>
         ESTJ.OS.DESC<1,DFND> = ESTD.DESC<1,T>
         ESTJ.OS.PLINE<1,DFND> = ESTD.CODE<1,T>
         ESTJ.OS.VEND<1,DFND> = ESTD.VEND<1,T>
      END
      ESTJ.OS.QTY<1,DFND> = ESTJ.OS.QTY<1,DFND> + ESTD.QTY<1,T>
      ESTJ.OS.DCOST<1,DFND> = ESTJ.OS.DCOST<1,DFND> + ESTD.DCOST<1,T>
      ESTJ.OS.COST<1,DFND> = ESTJ.OS.COST<1,DFND> + ESTD.COST<1,T>
      ESTJ.OS.SALE<1,DFND> = ESTJ.OS.SALE<1,DFND> + ESTD.SALE<1,T>
      ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
      ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
      ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
      RETURN
*
*--- SHIPPING
*
10400*
      DFND = 1
      MATCHED = 0
      DCNT = COUNT(ESTJ.SP.DEPT,VM) + (ESTJ.SP.DEPT # "")
      FOR D = 1 TO DCNT UNTIL MATCHED
         BEGIN CASE
            CASE CCTR.DEPT = ESTJ.SP.DEPT<1,D>
               BEGIN CASE
                  CASE ESTD.CCTR<1,T> = ESTJ.SP.CCTR<1,D>
                     BEGIN CASE
                        CASE ESTD.OPV<1,T> = ESTJ.SP.OPER<1,D>
                           BEGIN CASE
                              CASE ESTD.CODE<1,T> = ESTJ.SP.VIA<1,D>
                                 DFND = D
                                 MATCHED = 1
                              CASE ESTD.CODE<1,T> > ESTJ.SP.VIA<1,D>
                                 DFND = D + 1
                           END CASE
                        CASE ESTD.OPV<1,T> > ESTJ.SP.OPER<1,D>
                           DFND = D + 1
                     END CASE
                  CASE ESTD.CCTR<1,T> > ESTJ.SP.CCTR<1,D>
                     DFND = D + 1
               END CASE
            CASE CCTR.DEPT > ESTJ.SP.DEPT<1,D>
               DFND = D + 1
         END CASE
      NEXT D
      IF MATCHED = 0 THEN
         IF DFND <= DCNT THEN
            ESTJ.SP.DEPT = INSERT(ESTJ.SP.DEPT,1,DFND,0,"")
            ESTJ.SP.CCTR = INSERT(ESTJ.SP.CCTR,1,DFND,0,"")
            ESTJ.SP.OPER = INSERT(ESTJ.SP.OPER,1,DFND,0,"")
            ESTJ.SP.DESC = INSERT(ESTJ.SP.DESC,1,DFND,0,"")
            ESTJ.SP.VIA = INSERT(ESTJ.SP.VIA,1,DFND,0,"")
            ESTJ.SP.DCOST = INSERT(ESTJ.SP.DCOST,1,DFND,0,"")
            ESTJ.SP.COST = INSERT(ESTJ.SP.COST,1,DFND,0,"")
            ESTJ.SP.SALE = INSERT(ESTJ.SP.SALE,1,DFND,0,"")
         END
         ESTJ.SP.DEPT<1,DFND> = CCTR.DEPT
         ESTJ.SP.CCTR<1,DFND> = ESTD.CCTR<1,T>
         ESTJ.SP.OPER<1,DFND> = ESTD.OPV<1,T>
         ESTJ.SP.DESC<1,DFND> = ESTD.DESC<1,T>
         ESTJ.SP.VIA<1,DFND> = ESTD.CODE<1,T>
      END
      ESTJ.SP.DCOST<1,DFND> = ESTJ.SP.DCOST<1,DFND> + ESTD.DCOST<1,T>
      ESTJ.SP.COST<1,DFND> = ESTJ.SP.COST<1,DFND> + ESTD.COST<1,T>
      ESTJ.SP.SALE<1,DFND> = ESTJ.SP.SALE<1,DFND> + ESTD.SALE<1,T>
      ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
      ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
      ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
      RETURN
*
*--- MISCELLANEOUS
*
10500*
      DFND = 1
      MATCHED = 0
      DCNT = COUNT(ESTJ.MS.DEPT,VM) + (ESTJ.MS.DEPT # "")
      FOR D = 1 TO DCNT UNTIL MATCHED
         BEGIN CASE
            CASE CCTR.DEPT = ESTJ.MS.DEPT<1,D>
               BEGIN CASE
                  CASE ESTD.CCTR<1,T> = ESTJ.MS.CCTR<1,D>
                     BEGIN CASE
                        CASE ESTD.OPV<1,T> = ESTJ.MS.OPER<1,D>
                           IF ESTD.DESC<1,T> = ESTJ.MS.DESC<1,D> THEN
                              DFND = D
                              MATCHED = 1
                           END ELSE
                              DFND = D + 1
                           END
                        CASE ESTD.OPV<1,T> > ESTJ.MS.OPER<1,D>
                           DFND = D + 1
                     END CASE
                  CASE ESTD.CCTR<1,T> > ESTJ.MS.CCTR<1,D>
                     DFND = D + 1
               END CASE
            CASE CCTR.DEPT > ESTJ.MS.DEPT<1,D>
               DFND = D + 1
         END CASE
      NEXT D
      IF MATCHED = 0 THEN
         IF DFND <= DCNT THEN
            ESTJ.MS.DEPT = INSERT(ESTJ.MS.DEPT,1,DFND,0,"")
            ESTJ.MS.CCTR = INSERT(ESTJ.MS.CCTR,1,DFND,0,"")
            ESTJ.MS.OPER = INSERT(ESTJ.MS.OPER,1,DFND,0,"")
            ESTJ.MS.DESC = INSERT(ESTJ.MS.DESC,1,DFND,0,"")
            ESTJ.MS.DCOST = INSERT(ESTJ.MS.DCOST,1,DFND,0,"")
            ESTJ.MS.COST = INSERT(ESTJ.MS.COST,1,DFND,0,"")
            ESTJ.MS.SALE = INSERT(ESTJ.MS.SALE,1,DFND,0,"")
         END
         ESTJ.MS.DEPT<1,DFND> = CCTR.DEPT
         ESTJ.MS.CCTR<1,DFND> = ESTD.CCTR<1,T>
         ESTJ.MS.OPER<1,DFND> = ESTD.OPV<1,T>
         ESTJ.MS.DESC<1,DFND> = ESTD.DESC<1,T>
      END
      ESTJ.MS.DCOST<1,DFND> = ESTJ.MS.DCOST<1,DFND> + ESTD.DCOST<1,T>
      ESTJ.MS.COST<1,DFND> = ESTJ.MS.COST<1,DFND> + ESTD.COST<1,T>
      ESTJ.MS.SALE<1,DFND> = ESTJ.MS.SALE<1,DFND> + ESTD.SALE<1,T>
      ESTJ.TOT.DCOST = ESTJ.TOT.DCOST + ESTD.DCOST<1,T>
      ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
      ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
