SUBROUTINE GET.JBS.USER.FIELDS (CONO,JOB.NUM, MAT JOB.REC, MAT JBS.REC, XXX,YYYY,ZZZZ)
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PSSBP
* PROGRAM     - GET.JSB.USER.FIELDS
* BY          - edvard; CBA
* DATE        - 04/02/2001
* DESCRIPTION -
*
*T25704 edvard 04/02/2001 * Get data for user fields defined in USER
*                           FIELD MAINTENANCE.
*ENDDOC
**********************************************
*
*COPY>JCS.CPYLIB>JOB
*COPY>PSS.CPYLIB>USER.FIELDS
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
OPEN '','PSS.CONTROL' TO PSS.CONTROL ELSE STOP
OPEN '','INVENTORY' TO INVENTORY ELSE STOP
OPEN '','CATEGORY' TO CATEGORY ELSE STOP
*
MATREADU UFIELD.REC FROM PSS.CONTROL,CONO:"USER.FIELDS" THEN
  FILE.CNT = DCOUNT(UF.FILE<1>,VM)
  FOR F = 1 TO FILE.CNT
    IF UF.ACTIVE<1,F> = "Y" THEN
      FILE.NAME = UF.FILE<1,F>
      ATT = UF.ATTRIBUTE<1,F> 
      MV  = UF.VM<1,F>        
      SMV = UF.SVM<1,F>       
      IF FILE.NAME = "JOB" THEN
        BEGIN CASE
          CASE ATT = 31
            CODE = UF.CODE<1,F>
            ITEM.CNT = DCOUNT(JOB.RESV.MATL<1>,VM)
            FOR ITEM.POS = 1 TO ITEM.CNT
              INV.ID = CONO:JOB.RESV.MATL<1,ITEM.POS>  
              MATREAD INV.REC FROM INVENTORY,INV.ID THEN
                CATG.ID = CONO:INV.LINE
                MATREAD CATG.REC FROM CATEGORY,CATG.ID THEN
                  IF CATG.TYPE = CODE THEN
                    MV = ITEM.POS
                    ITEM.POS = ITEM.CNT ; * get out of the loop
                  END
                END
              END
            NEXT ITEM.POS
          CASE 1
        END CASE
        JBS.REC(JBS.UFIELD.BEG+F) = JOB.REC(ATT)<MV,SMV>
        UF.PROCESSED<1,F> = "Y"
      END ELSE
        BEGIN CASE
          CASE FILE.NAME = "SALESMAN"
            ID = CONO:JOB.SLSMN
          CASE FILE.NAME = "CUSTOMER"
            ID = CONO:JOB.CUST
          CASE FILE.NAME = "DIVISION"
            ID = CONO:JOB.DIV
          CASE FILE.NAME = "SHIP.VIA"
            ID = CONO:JOB.SHIP.VIA
          CASE FILE.NAME = "ESTIMATE"
            ID = CONO:JOB.EST
          CASE FILE.NAME = "JOB.CATEGORY"
            ID = CONO:JOB.CATG
          CASE FILE.NAME = "SALES.CODE"
            ID = CONO:JOB.SALES.CODE
          CASE FILE.NAME = "INVENTORY"
            ID = CONO:JOB.RESV.MATL
          CASE FILE.NAME = "WAREHOUSE"
            ID = CONO:JOB.RESV.WHSE
          CASE FILE.NAME = "INVOICE"
            ID = CONO:JOB.INV.NO
          CASE FILE.NAME = "CATEGORY.OSP"
            ID = CONO:JOB.OS.PLINE
          CASE FILE.NAME = "VEND"
            ID = CONO:JOB.OS.VEND
          CASE FILE.NAME = "CSR.CODE"
            ID = CONO:JOB.CSR
        END CASE
        OPEN "",FILE.NAME TO FILENAME THEN
          READV REC FROM FILENAME,ID,ATT THEN
            IF UF.CONV<1,F>#"" THEN
              JBS.REC(JBS.UFIELD.BEG+F)=OCONV(REC<1,MV,SMV>,UF.CONV<1,F>)
            END ELSE
              JBS.REC(JBS.UFIELD.BEG+F) = REC<1,MV,SMV>
            END
            UF.PROCESSED<1,F> = "Y"
          END
        END
      END
    END
  NEXT F
  MATWRITE UFIELD.REC ON PSS.CONTROL, CONO:"USER.FIELDS"
END
