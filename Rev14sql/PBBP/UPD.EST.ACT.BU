*  TEST PROGRAM TO BUILD ESTIMATE TO ACTUAL DATA FOR POWERBUILD DEMO
*
*COPY>JES.CPYLIB>EST.JOB.ACT
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>JCS.CPYLIB>JOB.OSP
*COPY>CPYLIB>CHAR
OPEN '','JOB' TO JOB ELSE STOP
OPEN '','JOB.TIME' TO JOB.TIME ELSE STOP
OPEN '','JOB.MATL' TO JOB.MATL ELSE STOP
OPEN '','JOB.OSP' TO JOB.OSP ELSE STOP
OPEN '','EST.JOB.ACT' TO EST.JOB.ACT ELSE STOP
OPEN '','ESTIMATE.JOB' TO ESTIMATE.JOB ELSE STOP
*
* PROCESS JOBS
*
DATA = 1
LOOP
  READNEXT ID ELSE DATA = 0
WHILE DATA DO
  MATREAD JOB.REC FROM JOB, ID THEN
    MATREAD ESTJA.REC FROM ESTIMATE.JOB, ID THEN
*   MATREAD ESTJA.REC FROM EST.JOB.ACT, ID THEN
      GOSUB 2000
      GOSUB 3000
      GOSUB 4000
      MATWRITE ESTJA.REC ON EST.JOB.ACT, ID
    END
  END
REPEAT
GOTO 99999
*
* LABOR
*
2000 *
LCNT = DCOUNT(JOB.LB.SEQ,VM)
FOR L = 1 TO LCNT
  JLB.DEPT = JOB.LB.DEPT<1,L>
  JLB.CCTR = JOB.LB.CCTR<1,L>
  FOR S = 1 TO JOB.LB.SEQ<1,L>
    JLB.ID = ID:"!":JLB.DEPT:"!":JLB.CCTR:"!":S
    MATREAD JLB.REC FROM JOB.TIME, JLB.ID THEN
      PTR = 1
      MATCHED = 0
      LOOP
        LOCATE JLB.DEPT IN ESTJA.LB.DEPT<1>,PTR BY "AR" SETTING D THEN
          BEGIN CASE
          CASE JLB.CCTR = ESTJA.LB.CCTR<1,D>
            BEGIN CASE
            CASE JLB.OPER = ESTJA.LB.OPER<1,D>
              PTR = 0
              MATCHED = 1
            CASE JLB.OPER < ESTJA.LB.OPER<1,D>
              PTR = 0
            CASE 1
              PTR = D + 1
            END CASE
          CASE JLB.CCTR < ESTJA.LB.CCTR<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        END ELSE
          PTR = 0
        END
      WHILE PTR DO REPEAT
      IF MATCHED = 0 THEN
        ESTJA.LB.DEPT = INSERT(ESTJA.LB.DEPT,1,D,0,JLB.DEPT)
        ESTJA.LB.CCTR = INSERT(ESTJA.LB.CCTR,1,D,0,JLB.CCTR)
        ESTJA.LB.OPER = INSERT(ESTJA.LB.OPER,1,D,0,JLB.OPER)
        ESTJA.LB.HRS = INSERT(ESTJA.LB.HRS,1,D,0,"0")
        ESTJA.LB.QTY = INSERT(ESTJA.LB.QTY,1,D,0,"0")
        ESTJA.LB.DCOST = INSERT(ESTJA.LB.DCOST,1,D,0,"")
        ESTJA.LB.COST = INSERT(ESTJA.LB.COST,1,D,0,"0")
        ESTJA.LB.SALE = INSERT(ESTJA.LB.SALE,1,D,0,"0")
        ESTJA.LB.ACT.HRS = INSERT(ESTJA.LB.ACT.HRS,1,D,0,"0")
        ESTJA.LB.ACT.QTY = INSERT(ESTJA.LB.ACT.QTY,1,D,0,"0")
        ESTJA.LB.ACT.COST = INSERT(ESTJA.LB.ACT.COST,1,D,0,"0")
        ESTJA.LB.ACT.SALE = INSERT(ESTJA.LB.ACT.SALE,1,D,0,"0")
      END
      ESTJA.LB.ACT.HRS<1,D> = ESTJA.LB.ACT.HRS<1,D> + JLB.HRS
      ESTJA.LB.ACT.QTY<1,D> = ESTJA.LB.ACT.QTY<1,D> + JLB.IMP
      ESTJA.LB.ACT.COST<1,D> = ESTJA.LB.ACT.COST<1,D> + JLB.COST
      ESTJA.LB.ACT.SALE<1,D> = ESTJA.LB.ACT.SALE<1,D>+ JLB.SALE
    END
  NEXT S
NEXT L
RETURN
*
* MATL
*
3000 *
LCNT = DCOUNT(JOB.MT.SEQ,VM)
FOR L = 1 TO LCNT
  JMT.DEPT = JOB.MT.DEPT<1,L>
  JMT.CCTR = JOB.MT.CCTR<1,L>
  JMT.PROD = JOB.MT.PROD<1,L>
  JMT.WHSE = JOB.MT.WHSE<1,L>
  FOR S = 1 TO JOB.MT.SEQ<1,L>
    JMT.ID = ID:"!":JMT.DEPT:"!":JMT.CCTR:"!":JMT.PROD:"!":JMT.WHSE:"!":S
    MATREAD JMT.REC FROM JOB.MATL, JMT.ID THEN
      PTR = 1
      MATCHED = 0
      LOOP
        LOCATE JMT.DEPT IN ESTJA.MT.DEPT<1>,PTR BY "AR" SETTING D THEN
          BEGIN CASE
          CASE JMT.CCTR = ESTJA.MT.CCTR<1,D>
            BEGIN CASE
            CASE JMT.PROD = ESTJA.MT.PROD<1,D>
              PTR = 0
              MATCHED = 1
            CASE JMT.PROD < ESTJA.MT.PROD<1,D>
              PTR = 0
            CASE 1
              PTR = D + 1
            END CASE
          CASE JMT.CCTR < ESTJA.MT.CCTR<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        END ELSE
          PTR = 0
        END
      WHILE PTR DO REPEAT
      IF MATCHED = 0 THEN
        ESTJA.MT.DEPT = INSERT(ESTJA.MT.DEPT,1,D,0,JMT.DEPT)
        ESTJA.MT.CCTR = INSERT(ESTJA.MT.CCTR,1,D,0,JMT.CCTR)
        ESTJA.MT.OPER = INSERT(ESTJA.MT.OPER,1,D,0,JMT.PROD)
        ESTJA.MT.PROD = INSERT(ESTJA.MT.PROD,1,D,0,JMT.PROD)
        ESTJA.MT.DESC = INSERT(ESTJA.MT.DESC,1,D,0,"")
        ESTJA.MT.PLINE = INSERT(ESTJA.MT.PLINE,1,D,0,"")
        ESTJA.MT.MLINE = INSERT(ESTJA.MT.MLINE,1,D,0,"")
        ESTJA.MT.QTY = INSERT(ESTJA.MT.QTY,1,D,0,"0")
        ESTJA.MT.TYPE = INSERT(ESTJA.MT.TYPE,1,D,0,"0")
        ESTJA.MT.DCOST = INSERT(ESTJA.MT.DCOST,1,D,0,"")
        ESTJA.MT.COST = INSERT(ESTJA.MT.COST,1,D,0,"0")
        ESTJA.MT.SALE = INSERT(ESTJA.MT.SALE,1,D,0,"0")
        ESTJA.MT.ACT.QTY = INSERT(ESTJA.MT.ACT.QTY,1,D,0,"0")
        ESTJA.MT.ACT.COST = INSERT(ESTJA.MT.ACT.COST,1,D,0,"0")
        ESTJA.MT.ACT.SALE = INSERT(ESTJA.MT.ACT.SALE,1,D,0,"0")
      END
      ESTJA.MT.ACT.QTY<1,D> = ESTJA.MT.ACT.QTY<1,D> + JMT.QTY
      ESTJA.MT.ACT.COST<1,D> = ESTJA.MT.ACT.COST<1,D> + JMT.COST
      ESTJA.MT.ACT.SALE<1,D> = ESTJA.MT.ACT.SALE<1,D>+ JMT.SALE
    END
  NEXT S
NEXT L
RETURN
*
* OUTSIDE
*
4000 *
LCNT = DCOUNT(JOB.OS.SEQ,VM)
FOR L = 1 TO LCNT
  JOS.DEPT = JOB.OS.DEPT<1,L>
  JOS.CCTR = JOB.OS.CCTR<1,L>
  JOS.PLINE = JOB.OS.PLINE<1,L>
  FOR S = 1 TO JOB.OS.SEQ<1,L>
    JOS.ID = ID:"!":JOS.DEPT:"!":JOS.CCTR:"!":JOS.PLINE:"!":S
    MATREAD JOS.REC FROM JOB.OSP, JOS.ID THEN
      PTR = 1
      MATCHED = 0
      LOOP
        LOCATE JOS.DEPT IN ESTJA.OS.DEPT<1>,PTR BY "AR" SETTING D THEN
          BEGIN CASE
          CASE JOS.CCTR = ESTJA.OS.CCTR<1,D>
            BEGIN CASE
            CASE JOS.VEND = ESTJA.OS.OPER<1,D>
              BEGIN CASE
              CASE JOS.PLINE = ESTJA.OS.PLINE<1,D>
                PTR = 0
                MATCHED = 1
              CASE JOS.PLINE < ESTJA.OS.PLINE<1,D>
                PTR = 1
              CASE 1
                PTR = D + 1
              END CASE
            CASE JOS.VEND < ESTJA.OS.OPER<1,D>
              PTR = 0
            CASE 1
              PTR = D + 1
            END CASE
          CASE JOS.CCTR < ESTJA.OS.CCTR<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        END ELSE
          PTR = 0
        END
      WHILE PTR DO REPEAT
      IF MATCHED = 0 THEN
        ESTJA.OS.DEPT = INSERT(ESTJA.OS.DEPT,1,D,0,JOS.DEPT)
        ESTJA.OS.CCTR = INSERT(ESTJA.OS.CCTR,1,D,0,JOS.CCTR)
        ESTJA.OS.OPER = INSERT(ESTJA.OS.OPER,1,D,0,JOS.VEND)
        ESTJA.OS.DESC = INSERT(ESTJA.OS.DESC,1,D,0,JOS.DESC)
        ESTJA.OS.PLINE = INSERT(ESTJA.OS.PLINE,1,D,0,JOS.PLINE)
        ESTJA.OS.VEND = INSERT(ESTJA.OS.VEND,1,D,0,JOS.VEND)
        ESTJA.OS.QTY = INSERT(ESTJA.OS.QTY,1,D,0,"0")
        ESTJA.OS.DCOST = INSERT(ESTJA.OS.DCOST,1,D,0,"")
        ESTJA.OS.COST = INSERT(ESTJA.OS.COST,1,D,0,"0")
        ESTJA.OS.SALE = INSERT(ESTJA.OS.SALE,1,D,0,"0")
        ESTJA.OS.ACT.QTY = INSERT(ESTJA.OS.ACT.QTY,1,D,0,"0")
        ESTJA.OS.ACT.COST = INSERT(ESTJA.OS.ACT.COST,1,D,0,"0")
        ESTJA.OS.ACT.SALE = INSERT(ESTJA.OS.ACT.SALE,1,D,0,"0")
      END
      ESTJA.OS.ACT.QTY<1,D> = ESTJA.OS.ACT.QTY<1,D> + JOS.QTY
      ESTJA.OS.ACT.COST<1,D> = ESTJA.OS.ACT.COST<1,D> + JOS.COST
      ESTJA.OS.ACT.SALE<1,D> = ESTJA.OS.ACT.SALE<1,D>+ JOS.SALE
    END
  NEXT S
NEXT L
RETURN
*
*
*
99999 *
CRT @(0,23):"UPDATES COMPLETED":
END
