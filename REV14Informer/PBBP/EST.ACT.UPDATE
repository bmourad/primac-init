*
* THIS PROGRAM UPDATES THE ESTIMATE TO ACTUAL FILE USED BY
* POWERBUILDER, ONLY UNFLAGED JOB DETAIL RECORDS ARE PROCESSED.
*
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>JCS.CPYLIB>JOB.OSP
*COPY>JCS.CPYLIB>JOB.SHIP
*COPY>JCS.CPYLIB>JOB.MISC
*COPY>JES.CPYLIB>EST.JOB.ACT
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>CPYLIB>CHAR
OPEN '','JOB' TO JOB ELSE PRINT 'JOB MISSING'; STOP
OPEN '','JOB.TIME' TO JOB.TIME ELSE PRINT 'JOB.TIME MISSING'; STOP
OPEN '','JOB.MATL' TO JOB.MATL ELSE PRINT 'JOB.MATL MISSING'; STOP
OPEN '','JOB.OSP' TO JOB.OSP ELSE PRINT 'JOB.OSP MISSING'; STOP
OPEN '','JOB.SHIP' TO JOB.SHIP ELSE PRINT 'JOB.SHIP MISSING'; STOP
OPEN '','JOB.MISC' TO JOB.MISC ELSE PRINT 'JOB.MISC MISSING'; STOP
OPEN '','EST.JOB.ACT' TO EST.JOB.ACT ELSE PRINT 'EST.JOB.ACT MISSING'; STOP
OPEN '','ESTIMATE.JOB' TO ESTIMATE.JOB ELSE PRINT 'ESTIMATE.JOB MISSING'; STOP
OPEN '','INVENTORY' TO INVENTORY ELSE PRINT 'INVENTORY MISSING'; STOP
OPEN '','CATEGORY' TO CATEGORY ELSE PRINT 'CATEGORY MISSING'; STOP
OPEN '','CONTROL' TO CONTROL ELSE PRINT 'CONTROL MISSING'; STOP
OPEN '','COMPANY' TO COMPANY ELSE PRINT 'COMPANY MISSING'; STOP
OPEN '','PB.JOB.TIME' TO PB.JOB.TIME ELSE PRINT 'PB.JOB.TIME MISSING'; STOP
OPEN '','PB.JOB.MATL' TO PB.JOB.MATL ELSE PRINT 'PB.JOB.MATL MISSING'; STOP
OPEN '','PB.EST.JOB.ACT' TO PB.EST.JOB.ACT ELSE PRINT 'PB.EST.JOB.ACT MISSING'; STOP
*
* PROCESS
*
TODAY = DATE()
CONOS= ""; CUTOFF = ""
DATA = 1
SELECT COMPANY
LOOP
  READNEXT CONO ELSE DATA = 0
WHILE DATA DO
  CONOS<-1> = CONO
  READ MON FROM CONTROL, CONO:"PB.EXTRACT" ELSE MON = 3
  CUTOFF<-1> = TODAY - (MON*30)
REPEAT
*
FOR I = 1 TO 5
  BEGIN CASE
  CASE I = 1
    UDTEXECUTE 'SELECT JOB.TIME WITH JLB_EST_UPD = ""' CAPTURING JUNK
  CASE I = 2
    UDTEXECUTE 'SELECT JOB.MATL WITH JMT_EST_NUM # "" AND WITH JMT_EST_UPD = ""' CAPTURING JUNK
  CASE I = 3
    UDTEXECUTE 'SELECT JOB.OSP WITH JOS_EST_NUM # "" AND WITH JOS_EST_UPD = ""' CAPTURING JUNK
  CASE I = 4
    UDTEXECUTE 'SELECT JOB.SHIP WITH JSP_EST_NUM # "" AND WITH JSP_EST_UPD = ""' CAPTURING JUNK
  CASE I = 5
    UDTEXECUTE 'SELECT JOB.MISC WITH JMS_EST_NUM # "" AND WITH JMS_EST_UPD = ""' CAPTURING JUNK
  END CASE
  DATA = 1
  LOOP
    READNEXT ID ELSE DATA = 0
  WHILE DATA DO
    FLAG = 1
    JOB.ID = FIELD(ID,'!',1)
    MATREADU ESTJA.REC FROM EST.JOB.ACT, JOB.ID ELSE
      MATREAD ESTJA.REC FROM ESTIMATE.JOB, JOB.ID ELSE FLAG = 0
    END
    IF FLAG THEN
      ON I GOSUB 1000,2000,3000,4000,5000
      MATWRITE ESTJA.REC ON EST.JOB.ACT, JOB.ID
      MATWRITE ESTJA.REC ON PB.EST.JOB.ACT, JOB.ID
    END ELSE
      IF I = 1 THEN
        MATREADU JLB.REC FROM JOB.TIME, ID THEN
          JLB.EST.UPD = TODAY
          MATWRITE JLB.REC ON JOB.TIME, ID
          LOCATE ID[1,3] IN CONOS,1 SETTING PTR THEN
            IF JLB.DATE >= CUTOFF<PTR> THEN
              MATWRITE JLB.REC ON PB.JOB.TIME, ID
            END
          END
        END
        RELEASE JOB.TIME, ID
      END
    END
    RELEASE EST.JOB.ACT, JOB.ID
199 *
  REPEAT
NEXT I
*
SELECT PB.JOB.TIME
DATA = 1
LOOP
  READNEXT ID ELSE DATA = 0
  MATREAD JLB.REC FROM PB.JOB.TIME, ID THEN
    LOCATE ID[1,3] IN CONOS,1 SETTING PTR THEN
      IF JLB.DATE < CUTOFF<PTR> THEN
        DELETE PB.JOB.TIME, ID
      END
    END
  END
WHILE DATA DO
REPEAT
*
SELECT PB.JOB.MATL
DATA = 1
LOOP
  READNEXT ID ELSE DATA = 0
  MATREAD JMT.REC FROM PB.JOB.MATL, ID THEN
    LOCATE ID[1,3] IN CONOS,1 SETTING PTR THEN
      IF JMT.DATE < CUTOFF<PTR> THEN
        DELETE PB.JOB.MATL, ID
      END
    END
  END
WHILE DATA DO
REPEAT
*
SELECT PB.EST.JOB.ACT
DATA = 1
LOOP
  READNEXT ID ELSE DATA = 0
  MATREAD JOB.REC FROM JOB, ID THEN
    LOCATE ID[1,3] IN CONOS,1 SETTING PTR THEN
      IF JOB.TRACK.DATE<1,10> # "" AND JOB.TRACK.DATE<1,10> < CUTOFF<PTR> THEN
        DELETE PB.EST.JOB.ACT, ID
      END
    END
  END
WHILE DATA DO
REPEAT
WRITE TIMEDATE() ON CONTROL, "EST.ACT.UPDATE"
GOTO 99999
*
* LABOR
*
1000 *
MATREADU JLB.REC FROM JOB.TIME, ID THEN
  IF JLB.EST.UPD = "" THEN
    JLB.DEPT = FIELD(ID,'!',2)
    JLB.CCTR = FIELD(ID,'!',3)
    PTR = 1
    MATCHED = 0
    LOOP
      LOCATE JLB.DEPT IN ESTJA.LB.DEPT<1>,PTR BY "AR" SETTING D THEN
        BEGIN CASE
        CASE JLB.CCTR = ESTJA.LB.CCTR<1,D>
          BEGIN CASE
          CASE JLB.OPER = ESTJA.LB.OPER<1,D>
            PTR = 0
            MATCHED = 1
          CASE JLB.OPER < ESTJA.LB.OPER<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        CASE JLB.CCTR < ESTJA.LB.CCTR<1,D>
          PTR = 0
        CASE 1
          PTR = D + 1
        END CASE
      END ELSE
        PTR = 0
      END
    WHILE PTR DO REPEAT
    IF MATCHED = 0 THEN
      ESTJA.LB.DEPT = INSERT(ESTJA.LB.DEPT,1,D,0,JLB.DEPT)
      ESTJA.LB.CCTR = INSERT(ESTJA.LB.CCTR,1,D,0,JLB.CCTR)
      ESTJA.LB.OPER = INSERT(ESTJA.LB.OPER,1,D,0,JLB.OPER)
      ESTJA.LB.HRS = INSERT(ESTJA.LB.HRS,1,D,0,"0")
      ESTJA.LB.QTY = INSERT(ESTJA.LB.QTY,1,D,0,"0")
      ESTJA.LB.DCOST = INSERT(ESTJA.LB.DCOST,1,D,0,"")
      ESTJA.LB.COST = INSERT(ESTJA.LB.COST,1,D,0,"0")
      ESTJA.LB.SALE = INSERT(ESTJA.LB.SALE,1,D,0,"0")
      ESTJA.LB.ACT.HRS = INSERT(ESTJA.LB.ACT.HRS,1,D,0,"0")
      ESTJA.LB.ACT.QTY = INSERT(ESTJA.LB.ACT.QTY,1,D,0,"0")
      ESTJA.LB.ACT.COST = INSERT(ESTJA.LB.ACT.COST,1,D,0,"0")
      ESTJA.LB.ACT.SALE = INSERT(ESTJA.LB.ACT.SALE,1,D,0,"0")
    END
    ESTJA.LB.ACT.HRS<1,D> = ESTJA.LB.ACT.HRS<1,D> + JLB.HRS
    ESTJA.LB.ACT.QTY<1,D> = ESTJA.LB.ACT.QTY<1,D> + JLB.IMP
    ESTJA.LB.ACT.COST<1,D> = ESTJA.LB.ACT.COST<1,D> + JLB.COST
    ESTJA.LB.ACT.SALE<1,D> = ESTJA.LB.ACT.SALE<1,D>+ JLB.SALE
    JLB.EST.UPD = TODAY
    MATWRITE JLB.REC ON JOB.TIME, ID
    MATWRITE JLB.REC ON PB.JOB.TIME, ID
  END
END
RELEASE JOB.TIME, ID
RETURN
*
* MATL
*
2000 *
MATREADU JMT.REC FROM JOB.MATL, ID THEN
  IF JMT.EST.UPD = "" THEN
    JMT.DEPT = FIELD(ID,'!',2)
    JMT.CCTR = FIELD(ID,'!',3)
    JMT.PROD = FIELD(ID,'!',4)
    JMT.WHSE = FIELD(ID,'!',5)
    PTR = 1
    MATCHED = 0
    LOOP
      LOCATE JMT.DEPT IN ESTJA.MT.DEPT<1>,PTR BY "AR" SETTING D THEN
        BEGIN CASE
        CASE JMT.CCTR = ESTJA.MT.CCTR<1,D>
          BEGIN CASE
          CASE JMT.PROD = ESTJA.MT.PROD<1,D>
            PTR = 0
            MATCHED = 1
          CASE JMT.PROD < ESTJA.MT.PROD<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        CASE JMT.CCTR < ESTJA.MT.CCTR<1,D>
          PTR = 0
        CASE 1
          PTR = D + 1
        END CASE
      END ELSE
        PTR = 0
      END
    WHILE PTR DO REPEAT
    IF MATCHED = 0 THEN
      ESTJA.MT.DEPT = INSERT(ESTJA.MT.DEPT,1,D,0,JMT.DEPT)
      ESTJA.MT.CCTR = INSERT(ESTJA.MT.CCTR,1,D,0,JMT.CCTR)
      ESTJA.MT.OPER = INSERT(ESTJA.MT.OPER,1,D,0,JMT.PROD)
      ESTJA.MT.PROD = INSERT(ESTJA.MT.PROD,1,D,0,JMT.PROD)
      ESTJA.MT.DESC = INSERT(ESTJA.MT.DESC,1,D,0,"")
      ESTJA.MT.QTY = INSERT(ESTJA.MT.QTY,1,D,0,"0")
      MATREAD INV.REC FROM INVENTORY, ID[1,3]:JMT.PROD THEN
        ESTJA.MT.TYPE = INSERT(ESTJA.MT.TYPE,1,D,0,INV.UNIT<1,2>)
        ESTJA.MT.PLINE = INSERT(ESTJA.MT.PLINE,1,D,0,INV.LINE)
        ESTJA.MT.MLINE = INSERT(ESTJA.MT.MLINE,1,D,0,INV.M.LINE)
        MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE THEN
          BEGIN CASE
          CASE CATG.TYPE = "L" OR CATG.TYPE = "LR" OR CATG.TYPE = "PC"
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MR")
          CASE CATG.TYPE = "S"
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MS")
          CASE CATG.TYPE = "I"
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MI")
          CASE CATG.TYPE = "F"
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MF")
          CASE CATG.TYPE = "O"
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MO")
          CASE CATG.TYPE = "P"
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MP")
          CASE 1
            ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"")
          END CASE
        END ELSE
          ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"")
        END
      END ELSE
        ESTJA.MT.TYPE = INSERT(ESTJA.MT.TYPE,1,D,0,"0")
        ESTJA.MT.PLINE = INSERT(ESTJA.MT.PLINE,1,D,0,"")
        ESTJA.MT.MLINE = INSERT(ESTJA.MT.MLINE,1,D,0,"")
        ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"")
      END
      ESTJA.MT.DCOST = INSERT(ESTJA.MT.DCOST,1,D,0,"")
      ESTJA.MT.COST = INSERT(ESTJA.MT.COST,1,D,0,"0")
      ESTJA.MT.SALE = INSERT(ESTJA.MT.SALE,1,D,0,"0")
      ESTJA.MT.ACT.QTY = INSERT(ESTJA.MT.ACT.QTY,1,D,0,"0")
      ESTJA.MT.ACT.COST = INSERT(ESTJA.MT.ACT.COST,1,D,0,"0")
      ESTJA.MT.ACT.SALE = INSERT(ESTJA.MT.ACT.SALE,1,D,0,"0")
    END
    ESTJA.MT.ACT.QTY<1,D> = ESTJA.MT.ACT.QTY<1,D> + JMT.QTY
    ESTJA.MT.ACT.COST<1,D> = ESTJA.MT.ACT.COST<1,D> + JMT.COST
    ESTJA.MT.ACT.SALE<1,D> = ESTJA.MT.ACT.SALE<1,D>+ JMT.SALE
    JMT.EST.UPD = TODAY
    MATWRITE JMT.REC ON JOB.MATL, ID
    MATWRITE JMT.REC ON PB.JOB.MATL, ID
  END
END
RELEASE JOB.MATL, ID
RETURN
*
* OUTSIDE
*
3000 *
JOS.DEPT = FIELD(ID,'!',2)
JOS.CCTR = FIELD(ID,'!',3)
JOS.PLINE = FIELD(ID,'!',4)
MATREADU JOS.REC FROM JOB.OSP, ID THEN
  IF JOS.EST.UPD = "" THEN
    PTR = 1
    MATCHED = 0
    LOOP
      LOCATE JOS.DEPT IN ESTJA.OS.DEPT<1>,PTR BY "AR" SETTING D THEN
        BEGIN CASE
        CASE JOS.CCTR = ESTJA.OS.CCTR<1,D>
          BEGIN CASE
          CASE JOS.VEND = ESTJA.OS.OPER<1,D>
            BEGIN CASE
            CASE JOS.PLINE = ESTJA.OS.PLINE<1,D>
              PTR = 0
              MATCHED = 1
            CASE JOS.PLINE < ESTJA.OS.PLINE<1,D>
              PTR = 0
            CASE 1
              PTR = D + 1
            END CASE
          CASE JOS.VEND < ESTJA.OS.OPER<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        CASE JOS.CCTR < ESTJA.OS.CCTR<1,D>
          PTR = 0
        CASE 1
          PTR = D + 1
        END CASE
      END ELSE
        PTR = 0
      END
    WHILE PTR DO REPEAT
    IF MATCHED = 0 THEN
      ESTJA.OS.DEPT = INSERT(ESTJA.OS.DEPT,1,D,0,JOS.DEPT)
      ESTJA.OS.CCTR = INSERT(ESTJA.OS.CCTR,1,D,0,JOS.CCTR)
      ESTJA.OS.OPER = INSERT(ESTJA.OS.OPER,1,D,0,JOS.VEND)
      ESTJA.OS.DESC = INSERT(ESTJA.OS.DESC,1,D,0,JOS.DESC)
      ESTJA.OS.PLINE = INSERT(ESTJA.OS.PLINE,1,D,0,JOS.PLINE)
      ESTJA.OS.VEND = INSERT(ESTJA.OS.VEND,1,D,0,JOS.VEND)
      ESTJA.OS.QTY = INSERT(ESTJA.OS.QTY,1,D,0,"0")
      ESTJA.OS.DCOST = INSERT(ESTJA.OS.DCOST,1,D,0,"")
      ESTJA.OS.COST = INSERT(ESTJA.OS.COST,1,D,0,"0")
      ESTJA.OS.SALE = INSERT(ESTJA.OS.SALE,1,D,0,"0")
      ESTJA.OS.ACT.QTY = INSERT(ESTJA.OS.ACT.QTY,1,D,0,"0")
      ESTJA.OS.ACT.COST = INSERT(ESTJA.OS.ACT.COST,1,D,0,"0")
      ESTJA.OS.ACT.SALE = INSERT(ESTJA.OS.ACT.SALE,1,D,0,"0")
    END
    ESTJA.OS.ACT.QTY<1,D> = ESTJA.OS.ACT.QTY<1,D> + JOS.QTY
    ESTJA.OS.ACT.COST<1,D> = ESTJA.OS.ACT.COST<1,D> + JOS.COST
    ESTJA.OS.ACT.SALE<1,D> = ESTJA.OS.ACT.SALE<1,D>+ JOS.SALE
    JOS.EST.UPD = TODAY
    MATWRITE JOS.REC ON JOB.OSP, ID
  END
END
RELEASE JOB.OSP, ID
RETURN
*
* SHIPPING
*
4000 *
JSP.DEPT = FIELD(ID,'!',2)
JSP.CCTR = FIELD(ID,'!',3)
MATREADU JSP.REC FROM JOB.SHIP, ID THEN
  IF JSP.EST.UPD = "" THEN
    PTR = 1
    MATCHED = 0
    LOOP
      LOCATE JSP.DEPT IN ESTJA.SP.DEPT<1>,PTR BY "AR" SETTING D THEN
        BEGIN CASE
        CASE JSP.CCTR = ESTJA.SP.CCTR<1,D>
          BEGIN CASE
          CASE JSP.OPER = ESTJA.SP.OPER<1,D>
            PTR = 0
            MATCHED = 1
          CASE JSP.OPER < ESTJA.SP.OPER<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        CASE JSP.CCTR < ESTJA.SP.CCTR<1,D>
          PTR = 0
        CASE 1
          PTR = D + 1
        END CASE
      END ELSE
        PTR = 0
      END
    WHILE PTR DO REPEAT
    IF MATCHED = 0 THEN
      ESTJA.SP.DEPT = INSERT(ESTJA.SP.DEPT,1,D,0,JSP.DEPT)
      ESTJA.SP.CCTR = INSERT(ESTJA.SP.CCTR,1,D,0,JSP.CCTR)
      ESTJA.SP.OPER = INSERT(ESTJA.SP.OPER,1,D,0,JSP.OPER)
      ESTJA.SP.DESC = INSERT(ESTJA.SP.DESC,1,D,0,JSP.DESC)
      ESTJA.SP.VIA = INSERT(ESTJA.SP.VIA,1,D,0,JSP.VIA)
      ESTJA.SP.DCOST = INSERT(ESTJA.SP.DCOST,1,D,0,"")
      ESTJA.SP.COST = INSERT(ESTJA.SP.COST,1,D,0,"0")
      ESTJA.SP.SALE = INSERT(ESTJA.SP.SALE,1,D,0,"0")
      ESTJA.SP.ACT.COST = INSERT(ESTJA.SP.ACT.COST,1,D,0,"0")
      ESTJA.SP.ACT.SALE = INSERT(ESTJA.SP.ACT.SALE,1,D,0,"0")
    END
    ESTJA.SP.ACT.COST<1,D> = ESTJA.SP.ACT.COST<1,D> + JSP.COST
    ESTJA.SP.ACT.SALE<1,D> = ESTJA.SP.ACT.SALE<1,D>+ JSP.SALE
    JSP.EST.UPD = TODAY
    MATWRITE JSP.REC ON JOB.SHIP, ID
  END
END
RELEASE JOB.SHIP, ID
RETURN
*
* MISCELLANEOUS
*
5000 *
JMS.DEPT = FIELD(ID,'!',2)
JMS.CCTR = FIELD(ID,'!',3)
MATREADU JMS.REC FROM JOB.MISC, ID THEN
  IF JMS.EST.UPD = "" THEN
    PTR = 1
    MATCHED = 0
    LOOP
      LOCATE JMS.DEPT IN ESTJA.MS.DEPT<1>,PTR BY "AR" SETTING D THEN
        BEGIN CASE
        CASE JMS.CCTR = ESTJA.MS.CCTR<1,D>
          BEGIN CASE
          CASE JMS.OPER = ESTJA.MS.OPER<1,D>
            PTR = 0
            MATCHED = 1
          CASE JMS.OPER < ESTJA.MS.OPER<1,D>
            PTR = 0
          CASE 1
            PTR = D + 1
          END CASE
        CASE JMS.CCTR < ESTJA.MS.CCTR<1,D>
          PTR = 0
        CASE 1
          PTR = D + 1
        END CASE
      END ELSE
        PTR = 0
      END
    WHILE PTR DO REPEAT
    IF MATCHED = 0 THEN
      ESTJA.MS.DEPT = INSERT(ESTJA.MS.DEPT,1,D,0,JMS.DEPT)
      ESTJA.MS.CCTR = INSERT(ESTJA.MS.CCTR,1,D,0,JMS.CCTR)
      ESTJA.MS.OPER = INSERT(ESTJA.MS.OPER,1,D,0,JMS.OPER)
      ESTJA.MS.DESC = INSERT(ESTJA.MS.DESC,1,D,0,JMS.DESC<1,1>)
      ESTJA.MS.DCOST = INSERT(ESTJA.MS.DCOST,1,D,0,"")
      ESTJA.MS.COST = INSERT(ESTJA.MS.COST,1,D,0,"0")
      ESTJA.MS.SALE = INSERT(ESTJA.MS.SALE,1,D,0,"0")
      ESTJA.MS.ACT.COST = INSERT(ESTJA.MS.ACT.COST,1,D,0,"0")
      ESTJA.MS.ACT.SALE = INSERT(ESTJA.MS.ACT.SALE,1,D,0,"0")
    END
    ESTJA.MS.ACT.COST<1,D> = ESTJA.MS.ACT.COST<1,D> + JMS.COST
    ESTJA.MS.ACT.SALE<1,D> = ESTJA.MS.ACT.SALE<1,D>+ JMS.SALE
    JMS.EST.UPD = TODAY
    MATWRITE JMS.REC ON JOB.MISC, ID
  END
END
RELEASE JOB.MISC, ID
RETURN
*
99999 *
PRINT "*** EST.ACT.UPDATE FINISHED ***"
END
