***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.AUTO.PURGE
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/23/86
*
* DESCRIPTION
*
* This program is used to purge estimates from system based on criteria
* specified on the COMPANY file.
*
**************************************************************************
*
*--------- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FIILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG="CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG="CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","PREFIX" TO PREFIX ELSE
         ERRMSG="CANNOT OPEN PREFIX FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","ESTIMATE" TO ESTIMATE ELSE
         ERRMSG="CANNOT OPEN ESTIMATE FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE
         ERRMSG="CANNOT OPEN ESTIMATE.DEPT FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","ESTIMATE.PURGE.BU" TO ESTIMATE.PURGE.BU ELSE
         ERRMSG="CANNOT OPEN ESTIMATE.PURGE.BU FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","CUST.EST.XREF" TO CUST.EST.XREF ELSE
         ERRMSG="CANNOT OPEN CUST.EST.XREF FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","PROSPECT.EST.XREF" TO PROSPECT.EST.XREF ELSE
         ERRMSG="CANNOT OPEN PROSPECT.EST.XREF FILE"
         GOSUB 90000
         STOP
      END
OPEN "","JOB" TO JOB ELSE
  ERRMSG="CANNOT OPEN TEMPORARY JOB FILE"
  GOSUB 90000
  STOP
END
*
*---- INITIALIZATION
*
CNT = 0
      TODAY = DATE()
      CONO=""
      MAT COMP.REC=""
      CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         MAT COMP.REC = ""
      END
      IF CO.JES.PURGE.DAYS<1,1> = "" THEN
         DATE1 = TODAY - 365
      END ELSE
         DATE1 = TODAY - CO.JES.PURGE.DAYS<1,1>
      END
      IF CO.JES.PURGE.DAYS<1,2> = "" THEN
         DATE2 = TODAY - 30
      END ELSE
         DATE2 = TODAY - CO.JES.PURGE.DAYS<1,2>
      END
      IF CO.JES.PURGE.DAYS<1,3> = "" THEN
         DATE3 = TODAY - 30
      END ELSE
         DATE3 = TODAY - CO.JES.PURGE.DAYS<1,3>
      END
      IF CO.JES.PURGE.DAYS<1,4> = "" THEN
         DATE4 = TODAY - 180
      END ELSE
         DATE4 = TODAY - CO.JES.PURGE.DAYS<1,4>
      END
*
*---- MAIN PROCESSING
*
      SELECT ESTIMATE
100*
      RELEASE
      READNEXT ID ELSE
         GOTO 99999
      END
      IF ID[1,3] # CONO THEN GOTO 100
      IF INDEX(ID,"-",1) = 0 THEN GOTO 100
      EST.KEY = ID[4,99]
      MATREADU EST.REC FROM ESTIMATE, CONO:EST.KEY ELSE GOTO 100
      IF EST.MASTER # "" AND EST.MASTER # EST.KEY THEN GOTO 100
      IF EST.PURGE.TYPE # "A" THEN GOTO 100
      STATUS = EST.STATUS<1,1>
      STATUS.DATE = EST.STAT.DATE<1,1>
      BEGIN CASE
      CASE STATUS = "BOOKED" AND EST.JOB.PURGE.DATE = ""
         NULL
*  TEMPORARILY ADDED TO PURGE DATA - SFC
*  MATREAD JOB.REC FROM JOB, CONO:EST.BOOK.JOB<1,1> ELSE
*    CNT = CNT + 1
*    PRINT @(24,23):CNT
*    GOSUB 2000
*  END
      CASE STATUS = "BOOKED" AND EST.JOB.PURGE.DATE < DATE1
         GOSUB 2000
      CASE STATUS = "LOST" AND STATUS.DATE < DATE2
         GOSUB 2000
      CASE STATUS = "CANCELLED" AND STATUS.DATE < DATE3
         GOSUB 2000
      CASE STATUS = "" AND EST.DATE.ENTERED < DATE4
         GOSUB 2000
      END CASE
      GOTO 100
*
*---- PURGE ESTIMATE
*
2000*
      PRINT @(0,21):CL:"Processing Estimate - ":EST.KEY:
      DEL.KEY = EST.KEY
      DEL.DEPT.COMP = EST.DEPT.COMP
      DEL.SUBS = EST.SUBS
      IF DEL.SUBS # "" THEN
         SAVE.KEY = DEL.KEY
         SAVE.DEPT.COMP = DEL.DEPT.COMP
         SC = COUNT(DEL.SUBS,VM) + (DEL.SUBS # "")
         FOR SP = 1 TO SC
            DEL.KEY = DEL.SUBS<1,SP>
            MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE GOTO 2090
            DEL.DEPT.COMP = EST.DEPT.COMP
            GOSUB 3000
2090     NEXT SP
         DEL.KEY = SAVE.KEY
         DEL.DEPT.COMP = SAVE.DEPT.COMP
         MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE
            MAT EST.REC = ""
         END
      END
      GOSUB 3000
      RETURN
*
*---- DELETE ESTIMATE RECORDS
*
3000*
      DC = COUNT(DEL.DEPT.COMP,VM) + (DEL.DEPT.COMP # "")
      FOR DP = 1 TO DC
         DDKEY = DEL.KEY:"!":DEL.DEPT.COMP<1,DP>
         READU REC FROM ESTIMATE.DEPT, CONO:DDKEY ELSE
            RELEASE ESTIMATE.DEPT, CONO:DDKEY
            GOTO 3090
         END
*        WRITE REC ON ESTIMATE.PURGE.BU, CONO:DDKEY
         DELETE ESTIMATE.DEPT, CONO:DDKEY
3090  NEXT DP
*     MATWRITE EST.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY
      BEGIN CASE
      CASE EST.CUST = "P"
         CALL GEN.XREF.MAINT(CONO,DEL.KEY,EST.CUST.NAME,"",PROSPECT.EST.XREF,PREFIX)
      CASE 1
         CALL GEN.XREF.MAINT(CONO,DEL.KEY,EST.CUST,"",CUST.EST.XREF,PREFIX)
      END CASE
      DELETE ESTIMATE, CONO:DEL.KEY
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):CL:
      RETURN
*---- END OF PROGRAM
*
99999*
      PRINT @(-1):
      STOP
   END
