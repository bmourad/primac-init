*COPY>CPYLIB>COM1
***********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - REPORT.OPTION.SCRN
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 03/01/86
* DESCRIPTION - This program is used to enter proc input or change options.
*T25978 adelgado 01/30/2002 * Add the use of prompts (SF,SR,SB,ST).
*T26090 wyamout 03/27/2002 * Add new fields
*T26090 wyamout 04/03/2002 * Add DATE() as a File Name for TYPE = 16
*T27410 bmourad 06/05/2003 * Allow multi file names for authentication
*                            delimited by ','.
*ENDDOC
************************************************************************
*
***** FILE INSERTS
*
*COPY>PMC.CPYLIB>REPORT.PROC.FILE
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SETUP SYSTEM ERRMSGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
  OPEN '','REPORT.PROC.FILE' TO REPORT.PROC.FILE ELSE
    ERRMSG = 'REPORT.PROC.FILE FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'M.SCREENS FILES IS MISSING'
    GOSUB 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','VOC' TO MD ELSE
    ERRMSG = 'VOC FILE IS MISSING'
    GOSUB 93000
  END
  BEGIN.PAGE = 15; PAGE.SIZE = 5; LINE.SPACE = 1
*
***** MAIN PROCESSING
*
  MAT EDIT.COM.DRIVER = ""
  MAT RPT.PROC.REC = ""
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME = "REPORT.OPTION.SCRN"
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
*
**** PRINT SCREEN
*
  ECD.ACTION=2;CALL SCRN.EDIT
*
**** CLEAR SCREEN CONTENT
*
100*
  MAT SCV.REC = ""
  MAT RPT.PROC.REC = ""
  ECD.ACTION = 6 ; CALL SCRN.EDIT
  LINES = 0 ; OLD.START.LINE = 0 ; LN = 1 ; GOSUB 2900
  LINES = 0 ; OLD.START.LINE = 0 ; LN = 1 ; GOSUB 3900
  OPTION = ""
  SCV.REC(1)<ECD.SCRN.NO,1> = DATE()
  ECD.NUM = 1
  ECD.ACTION = 5 ; CALL SCRN.EDIT
*
**** ENTER OPTION NUMBER
*
200*
  LINES = 0; LN = 1
  ECD.NUM = 2
  ECD.VALDAT.CODE = 4
  ECD.VALDAT.FILE = REPORT.PROC.FILE
  ECD.ACTION=4;CALL SCRN.EDIT
  OPTION.NUM = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
      FOR SAVE = 1 TO RPT.PROC.REC.SIZE
        RPT.PROC.REC(SAVE) = ECD.VALDAT.ITEM<SAVE>
      NEXT SAVE
      SCV.REC(3)<ECD.SCRN.NO,1> = RPR.DESC
      SCV.REC(4)<ECD.SCRN.NO,1> = RPR.TYPE
      SCV.REC(8)<ECD.SCRN.NO,1> = RPR.SCLR
      SCV.REC(5)<ECD.SCRN.NO,1> = RPR.VALID
      SCV.REC(6)<ECD.SCRN.NO,1> = RPR.MAXL
      SCV.REC(7)<ECD.SCRN.NO,1> = RPR.HMSG
      SCV.REC(9)<ECD.SCRN.NO,1> = RPR.FILE
      SCV.REC(12)<ECD.SCRN.NO,1> = RPR.DEFAULT
      SCV.REC(13)<ECD.SCRN.NO,1> = RPR.R.FLG
* T26090 v
      SCV.REC(23)<ECD.SCRN.NO> = RPR.VAL.HEAD
      SCV.REC(25)<ECD.SCRN.NO> = RPR.I.ID
      SCV.REC(26)<ECD.SCRN.NO> = RPR.I.ATT
      SCV.REC(27)<ECD.SCRN.NO> = RPR.I.MV
* T26090 ^
      ECD.ACTION = 3 ; CALL SCRN.EDIT
      IF RPR.FILE # "" THEN
        LINES = COUNT(RPR.F.A.NO, VM) + (RPR.F.A.NO # "")
        OLD.START.LINE = 0; GOSUB 2900
      END ELSE
        LINES = COUNT(RPR.R.VAL, VM) + (RPR.R.VAL # "")
        OLD.START.LINE = 0; GOSUB 3900
      END
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
* T26090 v
*     FOR I = 1 TO 8
*       ON I GOSUB 1000,1100,1200,1300,1350,1400,1500,1600
      FOR I = 1 TO 9 UNTIL ECD.RET.VALUE = "END"
        ON I GOSUB 1000,1100,1300,1200,1250,1350,1400,1500,1600
* T26090 ^
      NEXT I
      IF ECD.RET.VALUE = 'END' THEN GOTO 100
      OLD.START.LINE = 0
      IF RPR.FILE # "" THEN
        GOSUB 2000
      END ELSE
        GOSUB 3000
      END
    CASE 1
      GOTO 100
  END CASE
  MORE = 0
  LOOP
    ECD.NUM = 10
    ECD.ACTION=4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END'
        MORE = 1
      CASE NUM(OPTION)
* T26090 v
*       ON OPTION GOSUB 1000,1100,1200,1300,1350,1400,1500,1600
        ON OPTION GOSUB 1000,1100,1300,1200,1250,1350,1400,1500,1600
* T26090 ^
        MORE = 0
      CASE OPTION = "A" AND RPR.FILE = "" AND RPR.R.FLG = "N"
        ERRMSG = "Please enter file name before adding attributes or response flag as (Y)" ; GOSUB 91000
      CASE OPTION = 'A' AND RPR.FILE # ""
        LOOP
          LN = LINES + 1
          OLD.LINES = LINES
          GOSUB 2100
        WHILE LINES > OLD.LINES DO REPEAT
        LN = LINES
      CASE OPTION = 'A' AND RPR.R.FLG = "Y"
        LOOP
          LN = LINES + 1
          OLD.LINES = LINES
          GOSUB 3100
        WHILE LINES > OLD.LINES DO REPEAT
        LN = LINES
      CASE OPTION = 'C' AND RPR.FILE # ""
        GOSUB 2800
        IF LN.NO THEN
          LN = LN.NO
          GOSUB 2100
        END
      CASE OPTION = 'C' AND RPR.R.FLG = "Y"
        GOSUB 2800
        IF LN.NO THEN
          LN = LN.NO
          GOSUB 3100
        END
      CASE OPTION = 'D' AND RPR.FILE # ""
        GOSUB 2800
        IF LN.NO THEN
          LN = LN.NO
          RPR.F.A.NO = DELETE(RPR.F.A.NO,1,LN,0)
          RPR.F.A.NA = DELETE(RPR.F.A.NA,1,LN,0)
          RPR.C.OP = DELETE(RPR.C.OP,1,LN,0)
          RPR.F.A.TY = DELETE(RPR.F.A.TY,1,LN,0)
          RPR.F.A.CNV = DELETE(RPR.F.A.CNV,1,LN,0)
          LINES = COUNT(RPR.F.A.NO, VM) + (RPR.F.A.NO # "")
          IF LN > 1 AND LN > LINES THEN LN = LN - 1
          IF LN < 1 THEN LN = 1
          OLD.START.LINE = 0
        END
        GOSUB 2900
      CASE OPTION = 'D' AND RPR.R.FLG = "Y"
        GOSUB 2800
        IF LN.NO THEN
          LN = LN.NO
          RPR.R.VAL = DELETE(RPR.R.VAL,1,LN,0)
          RPR.R.OPT = DELETE(RPR.R.OPT,1,LN,0)
          LINES = COUNT(RPR.R.VAL, VM) + (RPR.R.VAL # "")
          IF LN > 1 AND LN > LINES THEN LN = LN - 1
          IF LN < 1 THEN LN = 1
          OLD.START.LINE = 0
        END
        GOSUB 3900
      CASE OPTION = 'S' AND RPR.FILE # ""
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 2900
      * T25978 v
      CASE OPTION = 'SR' AND RPR.FILE # ""
        LN = LN - PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 2900
      CASE OPTION = 'SB' AND RPR.FILE # ""
        LN = LINES
        GOSUB 2900
      CASE OPTION = 'ST' AND RPR.FILE # ""
        LN = 1
        GOSUB 2900
      * T25978 ^
      CASE OPTION = 'S' AND RPR.R.FLG = "Y"
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 3900
      * T25978 v
      CASE OPTION = 'SR' AND RPR.R.FLG = "Y"
        LN = LN - PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 3900
      CASE OPTION = 'SB' AND RPR.R.FLG = "Y"
        LN = LINES
        GOSUB 3900
      CASE OPTION = 'ST' AND RPR.R.FLG = "Y"
        LN = 1
        GOSUB 3900
      * T25978 ^
      CASE OPTION = 'F'
        MATWRITE RPT.PROC.REC ON REPORT.PROC.FILE, OPTION.NUM
        MORE = 1
      CASE OPTION = 'E'
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  GOTO 100
*
***** ENTER OPTION DESC
*
1000*
  ECD.NUM = 3
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    RPR.DESC = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER OPTION TYPE
*
1100*
  ECD.NUM = 4
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
* T26090 v
    BEGIN CASE
      CASE ECD.RET.VALUE = 1 OR ECD.RET.VALUE = 11
      CASE ECD.RET.VALUE = 2 OR ECD.RET.VALUE = 12
      CASE ECD.RET.VALUE = 3 OR ECD.RET.VALUE = 13
      CASE ECD.RET.VALUE = 4 OR ECD.RET.VALUE = 14
      CASE ECD.RET.VALUE = 6 OR ECD.RET.VALUE = 16
      CASE ECD.RET.VALUE = 8 OR ECD.RET.VALUE = 18
      CASE 1
        ERRMSG = "Invalid Type"
        GOSUB 91000
        GOTO 1000
    END CASE
* T26090 ^
    RPR.TYPE = ECD.RET.VALUE
* T26090 v
*   IF RPR.TYPE = "4" THEN
    IF RPR.TYPE = 4 OR RPR.TYPE = 14 THEN
* T26090 ^
      ECD.NUM = 8
      ECD.ACTION = 4 ; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" THEN
        RPR.SCLR = ECD.RET.VALUE
      END ELSE
        GOTO 1100
      END
    END
  END ELSE
    RPR.SCLR = ""
    ECD.NUM = 8
    SCV.REC(8)<ECD.SCRN.NO,1> = RPR.SCLR
    ECD.ACTION = 5 ; CALL SCRN.EDIT
  END
  RETURN
*
**** ENTER OPTION VALID INPUT
*
1200*
  ECD.NUM = 5
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    RPR.VALID = ECD.RET.VALUE
  END
* T26090 v
  IF RPR.VALID = "" THEN
    ECD.NUM = 23
    RPR.VAL.HEAD = ""
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = RPR.VAL.HEAD
    ECD.ACTION = 5
    CALL SCRN.EDIT
  END
* T26090 ^
  RETURN
* T26090 v
*
**** ENTER INCLUDE HEADING
*
1250*
  IF RPR.VALID = "" THEN RETURN
  ECD.NUM = 23
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    RPR.VAL.HEAD = ECD.RET.VALUE
  END
  RETURN
* T26090 ^
*
**** ENTER OPTION MAXL
*
1300*
  ECD.NUM = 6
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    RPR.MAXL = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER DEFAULT VALUE
*
1350*
  ECD.NUM = 12
  ECD.MAXL = RPR.MAXL
  IF RPR.VALID # "" THEN
    ECD.VALDAT = RPR.VALID
    ECD.HMSG = "Valid entries ":RPR.VALID
  END
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    RPR.DEFAULT = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER OPTION HELP MESSAGE
*
1400*
  ECD.NUM = 7
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    RPR.HMSG = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER FILE NAME
*
1500*
  IF RPR.R.FLG # "Y" THEN
    ECD.NUM = 9
    ECD.ACTION = 4 ; CALL SCRN.EDIT
    IF ECD.RET.VALUE # "END" THEN
      IF ECD.RET.VALUE # "" THEN
* T26090 v
        IF ECD.RET.VALUE = "DATE()" AND RPR.TYPE = 16 THEN
          RPR.FILE = ECD.RET.VALUE
        END ELSE
* BMOURAD
  NAME.TMP = ""
  FILE.NUM = DCOUNT(ECD.RET.VALUE,",")
  IF RPR.TYPE > 9 AND FILE.NUM > 1 THEN
     ERRMSG = "MULTI FILE ENTRY CANNOT BE USED WITH TYPES 10-18"
     GOSUB 91000
     GOTO 1500
  END
  FOR I = 1 TO FILE.NUM
    NAME.TMP = FIELD(ECD.RET.VALUE,',',I)
*BMOURAD          READ S.CP.REC FROM MD , ECD.RET.VALUE ELSE
          READ S.CP.REC FROM MD , NAME.TMP ELSE
*BMOURAD            ERRMSG = ECD.RET.VALUE :" is not a valid file name"
            ERRMSG = NAME.TMP :" is not a valid file name"
            GOSUB 91000
            GOTO 1500
          END
          IF S.CP.REC<1> # "f" AND S.CP.REC<1> # "F" THEN
*BMOURAD            ERRMSG = ECD.RET.VALUE :" is not a valid file name"
            ERRMSG = NAME.TMP :" is not a valid file name"
            GOSUB 91000
            GOTO 1500
          END
NEXT I
*BMOURAD ^
        END
        RPR.FILE = ECD.RET.VALUE
        GOSUB ADD_ITEM_ATT_MV
* T26090 ^
      END ELSE
        RPR.FILE = "" ; RPR.F.A.NO = "" ; RPR.F.A.NA = "" ; RPR.F.A.CNV = "" ; LINES = 0 ; LN = 1
        OLD.START.LINE = 0
        GOSUB 2900
      END
    END
  END ELSE
    ERRMSG = "This option can't be used with response flag equal (Y)" ; GOSUB 91000
  END
  RETURN
*
**** ENTER RESPONSE FLAG
*
1600*
* T26090 v
* IF RPR.FILE = "" THEN
  IF RPR.FILE = "" AND RPR.TYPE < 11 THEN
* T26090 ^
    ECD.NUM = 13
    ECD.ACTION = 4 ; CALL SCRN.EDIT
    IF ECD.RET.VALUE = "N" THEN
      RPR.R.FLG = "" ; RPR.R.VAL = "" ; RPR.R.OPT = ""
      LINES = 0 ; LN = 1 ; OLD.START.LINE = 0 ; GOSUB 3900
    END ELSE
      RPR.R.FLG = ECD.RET.VALUE
    END
  END ELSE
    IF OPTION # "" THEN
      ERRMSG = "This option can't be used with file validation" ;GOSUB 91000
    END
  END
  RETURN
*
**** ENTER ATTRIBUTE # AND DESCRIPTION
2000*
* T26090 v
* IF RPR.F.A.NO = "" AND RPR.FILE # "" THEN
  IF RPR.F.A.NO = "" AND RPR.FILE # "" AND RPR.TYPE > 10 THEN
* T26090 ^
    LINES = 0
    OPTION = 'A'
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 2100
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END
  RETURN
*
**** ENTER MULTI VALUE
*
2100 GOSUB 2900
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = SPACE(50) ; P_OPT = ""
  P_X  := AM:0 ; P_Y := AM:SLN ; P_VALUE := AM:LN "R#3"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
**** ENTER ATTRIBUTE NUMBER
*
2200 *
  X = 4 ; Y = SLN ; TYP = 3 ;MAXL = 3 ; O.R = "R"
  HMSG = "Enter valid attribute # to pull form the file"
  IF RPR.F.A.NO<1,LN> # "" THEN
    O.R = "O" ; DEFAULT = RPR.F.A.NO<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 2299
  IF VALUE = "END" THEN
    RPR.F.A.NO = DELETE(RPR.F.A.NO,1,LN,0)
    RPR.F.A.NA = DELETE(RPR.F.A.NA,1,LN,0)
    RPR.C.OP = DELETE(RPR.C.OP,1,LN,0)
    RPR.F.A.TY = DELETE(RPR.F.A.TY,1,LN,0)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = SPACE(50) ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 2299
  END
  LOCATE VALUE IN RPR.F.A.NO<1>,1 SETTING FND ELSE FND = LN
  IF FND <> LN THEN
    ERRMSG = "Attribute # already exists on line # - ":FND
    GOSUB 91000; GOTO 2200
  END
  RPR.F.A.NO<1,LN> = VALUE
*
**** ENTER ATTRIBUTE NAME
*
2300*
  X = 8 ; Y = SLN ; TYP = 1 ; MAXL = 30 ; O.R = "R"
  HMSG = "Enter Attribute description"
  IF RPR.F.A.NA<1,LN> # "" THEN
    O.R = "O" ; DEFAULT = RPR.F.A.NA<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION = "A" THEN GOTO 2100
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 2299
  LOCATE VALUE IN RPR.F.A.NA<1>,1 SETTING FND ELSE FND = LN
  IF FND <> LN THEN
    ERRMSG = "Attribute name already exists on line # - ":FND
    GOSUB 91000; GOTO 2300
  END
  RPR.F.A.NA<1,LN> = VALUE
*
**** ENTER ATTRIBUTE TYPE
*
2400*
  X = 39 ; Y = SLN ; TYP = 1 ; MAXL = 1 ; O.R = "O"
  IF RPR.F.A.TY<1,LN> # "" THEN DEFAULT = RPR.F.A.TY<1,LN>
  VALDAT = "M":VM:"S"
  HMSG = "Valid option (M)ulti value, (S)ub multi value or <RETURN>"
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION = "A" THEN GOTO 2100
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 2299
  RPR.F.A.TY<1,LN> = VALUE
*
**** ENTER ASSOCIATED OPTION
*
2500*
  X = 41 ; Y = SLN ; TYP = 3 ; MAXL = 4 ; O.R = "O"
  IF RPR.C.OP<1,LN> # "" THEN DEFAULT = RPR.C.OP<1,LN>
  HMSG = "Enter valid associated option # "
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION = "A" THEN GOTO 2100
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 2299
  IF VALUE # "" THEN
    READ CKX FROM REPORT.PROC.FILE , VALUE ELSE
      ERRMSG = "Invalid option " ; GOSUB 91000 ; GOTO 2500
    END
    IF VALUE = OPTION.NUM THEN
      ERRMSG = "You can't associate the same option to itself" ; GOSUB 91000 ; GOTO 2500
    END
  END
  RPR.C.OP<1,LN> = VALUE
*
**** ENTER OUTPUT CONVERATION
*
2600*
  X = 46 ; Y = SLN ; TYP = 1 ; MAXL = 3 ; O.R = "O"
  IF RPR.F.A.CNV<1,LN> # "" THEN
    DEFAULT = RPR.F.A.CNV<1,LN>
  END
  HMSG = "Enter valid output converation ,(MD0), (MD2), (MD3), (MD4), (D2/)"
  VALDAT = "MD0":VM:"MD2":VM:"MD3":VM:"MD4":VM:"D2/"
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION = "A" THEN GOTO 2100
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 2299
  RPR.F.A.CNV<1,LN> = VALUE
  LINES = COUNT(RPR.F.A.NO,VM) + (RPR.F.A.NO # "")
2299 RETURN
*
**** ENTER LINE.NUMBER
*
2800 GOSUB 2900
  ECD.NUM = 11; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
  ECD.MINV = OLD.START.LINE; ECD.MAXV = LAST.LINE
  ECD.ACTION = 4; CALL SCRN.EDIT
  LN.NO = ECD.RET.VALUE
  IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
  RETURN
*
**** SCROLL SUB
*
2900 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE = OLD.START.LINE THEN GOTO 2990
  OLD.START.LINE = START.LINE
  CNT = 1
  FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.F.A.NO<1,N> "R#3"
    P_X  := AM:8 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.F.A.NA<1,N> "L#30"
    P_X  := AM:39 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.F.A.TY<1,N> "L#1"
    P_X  := AM:41 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.C.OP<1,N> "R#4"
    P_X  := AM:46 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.F.A.CNV<1,N> "L#3"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
2990 RETURN
*
**** ENTER RESPONSE ASSCOIATION
*
3000*
  IF RPR.R.FLG = "Y" THEN
    LINES = 0
    OPTION = 'A'
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 3100
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END
  RETURN
*
**** ENTER LINE OPTION FOR RESPONSES
*
3100 GOSUB 3900
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 53 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
**** ENTER VALID RESPONSES
*
3200*
  X = 57 ; Y = SLN ; TYP = 1 ;MAXL = 8 ; O.R = "R"
  HMSG = "Enter valid response to pull form the valid inputs"
  IF RPR.R.VAL<1,LN> # "" THEN
    O.R = "O" ; DEFAULT = RPR.R.VAL<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 3390
  IF VALUE = "END" THEN
    RPR.R.VAL = DELETE(RPR.R.VAL,1,LN,0)
    RPR.R.OPT = DELETE(RPR.R.OPT,1,LN,0)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 3390
  END
  RPR.R.VAL<1,LN> = VALUE
*
**** ENTER OPTION
*
3300*
  X = 66 ; Y = SLN ; TYP = 3 ;MAXL = 4 ; O.R = "R"
  HMSG = "Enter valid option number"
  IF RPR.R.OPT<1,LN> # "" THEN
    O.R = "O" ; DEFAULT = RPR.R.OPT<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" AND OPTION # "A" THEN GOTO 3390
  IF VALUE = "END" AND OPTION = "A" THEN GOTO 3100
  LOCATE VALUE IN RPR.R.OPT<1>,1 SETTING FND ELSE FND = LN
  IF FND <> LN THEN
    ERRMSG = "Option # already exists on line # - ":FND
    GOSUB 91000; GOTO 3300
  END
  READ V.REC FROM REPORT.PROC.FILE , VALUE ELSE
    ERRMSG = "Option not on file , Please set it up before using it"
    GOSUB 91000 ; GOTO 3300
  END
  RPR.R.OPT<1,LN> = VALUE
  LINES = COUNT(RPR.R.OPT,VM) + (RPR.R.OPT # "")
3390 RETURN
*
**** SCROLL SUB
*
3900 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE = OLD.START.LINE THEN GOTO 3990
  OLD.START.LINE = START.LINE
  CNT = 1
  FOR N = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 53 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
    P_X  := AM:57 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.R.VAL<1,N> "L#8"
    P_X  := AM:66 ; P_Y := AM:SLN ; P_VALUE := AM:RPR.R.OPT<1,N> "R#4"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 52 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
3990 RETURN
* T26090 v
*
*---- ADD ITEM, ATT, MV
*
ADD_ITEM_ATT_MV:
*
  IF RPR.FILE = "" OR RPR.TYPE < 11 OR (RPR.FILE = "DATE()" AND RPR.TYPE = 16) THEN
    RPR.I.ID = ""
    RPR.I.ATT = ""
    RPR.I.MV = ""
  END ELSE
    ECD.NUM = 25
    ECD.ACTION = 4
    CALL SCRN.EDIT
    IF ECD.RET.VALUE # "END" THEN
      IF ECD.RET.VALUE # RPR.I.ID THEN
        RPR.I.ID = ECD.RET.VALUE
        RPR.I.ATT = 1
        RPR.I.MV = 1
      END
      ECD.NUM = 26
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = RPR.I.ATT
      ECD.ACTION = 4
      CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" THEN
        RPR.I.ATT = ECD.RET.VALUE
        ECD.NUM = 27
        SCV.REC(ECD.NUM)<ECD.SCRN.NO> = RPR.I.MV
        ECD.ACTION = 4
        CALL SCRN.EDIT
        IF ECD.RET.VALUE # "END" THEN RPR.I.MV = ECD.RET.VALUE
      END
    END
  END
  ECD.NUM = 25
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = RPR.I.ID
  ECD.ACTION = 5
  CALL SCRN.EDIT
  ECD.NUM = 26
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = RPR.I.ATT
  ECD.ACTION = 5
  CALL SCRN.EDIT
  ECD.NUM = 27
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = RPR.I.MV
  ECD.ACTION = 5
  CALL SCRN.EDIT
  RETURN
* T26090 ^
*
***** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1):
END
