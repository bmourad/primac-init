  SUBROUTINE VEND.VOU.ACT(LN.NO)
*COPY>CPYLIB>COM1  
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>POS.CPYLIB>COM.MPO  
*COPY>APS.CPYLIB>COM.APS.FILE.VARS
*COPY>APS.CPYLIB>COM.TEMP.VOUCHERS
*COPY>APS.CPYLIB>COM.VOUCHER.MAINT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* PROGRAM       - VEND.VOU.ACT
* BY            - JIHAD YAMOUT, C.B.A
* DATE          - 10/31/86
* DESCRIPTION   
*  This Program is used to dispays all voucher for a vendor.
*T25978 adelgado 01/30/2002 * Add the use of prompts (SF,SR,SB,ST).
*T28752 lross 12/15/2005 * Expand Invoice to 10 chars as in other
*                          screens.
*********************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>APS.CPYLIB>VEND.STATS
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>INVENTORY
*COPY>PMC.CPYLIB>VEND
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>APS.CPYLIB>APS.FILE.VARS
*COPY>CPYLIB>CHAR
*** INT
*
  BEGIN.PAGE = 8
  PAGE.SIZE = 12
  LINE.SPACE = 1
  LINES = 0
  OLD.START.LINE=0
  VOUCH.NOS = ""
  MAT INV.CNV.REC = ""
*
**** SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  IF LN.NO = "ALL" THEN
    PO.VOU = COUNT(VSTAT.VOUCH.NOS,VM) + (VSTAT.VOUCH.NOS # "")
    FOR X = 1 TO PO.VOU
      VOU.CNT = COUNT(VSTAT.VOUCH.NOS<1,X>,SVM) + (VSTAT.VOUCH.NOS<1,X> # "")
      FOR XX = 1 TO VOU.CNT
        LOCATE VSTAT.VOUCH.NOS<1,X,XX> IN VOUCH.NOS<1>,1 SETTING FND ELSE
          VOUCH.NOS<1,X> = VSTAT.VOUCH.NOS<1,X,XX>
        END
      NEXT XX
    NEXT X
  END ELSE
    VOU.CNT = COUNT(VSTAT.VOUCH.NOS<1,LN.NO>,SVM) + (VSTAT.VOUCH.NOS<1,LN.NO> # "")
    FOR XX = 1 TO VOU.CNT
      LOCATE VSTAT.VOUCH.NOS<1,LN.NO,XX> IN VOUCH.NOS<1>,1 SETTING FND ELSE
        VOUCH.NOS<1,XX> = VSTAT.VOUCH.NOS<1,LN.NO,XX>
      END
    NEXT XX
  END
  LINES = COUNT(VOUCH.NOS,VM) + (VOUCH.NOS # "")
  LN = 1
  IF LINES > 0 THEN
    GOSUB 10000
  END
  MORE = 1
  LOOP
    ECD.NUM = 15
    GOSUB 70000
    SCV.REC(3)<ECD.SCRN.NO> = '' ; ECD.ACTION = 4 ; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'E' OR OPTION = 'END' OR OPTION = ''
        MORE = 0
      CASE OPTION = 'S'
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 10000
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 10000
      CASE OPTION = 'ST'
        LN = 1
        GOSUB 10000
      CASE OPTION = 'SB'
        LN = LINES
        GOSUB 10000
      * T25978 ^
      CASE NUM(OPTION)
        LN = OPTION
        MATREAD VVOS.REC FROM VEND.VOUCH.STATS, CONO:TVO.VEND<1,1>:"!":VOUCH.NOS<1,LN> ELSE
          MAT VVOS.REC = ""
        END
        IF VVOS.PROD = "" AND VVOS.SEQ.NO = "" THEN
          ERRMSG = "No Product associated with this voucher." ; GOSUB 91000
        END ELSE
          ECD.SCRN.NO = 8
          SCV.REC(1)<ECD.SCRN.NO> = TVO.VEND<1,1>
          IF TVO.VEND<1,2> = "" THEN
            SCV.REC(2)<ECD.SCRN.NO> = VEND.DESC
          END ELSE
            SCV.REC(2)<ECD.SCRN.NO> = TVO.VEND<1,2>
          END
          SCV.REC(16)<ECD.SCRN.NO> = VOUCH.NOS<1,LN>
          BEGIN CASE
            CASE TVO.PO.TYPE = "R"
              SCV.REC(18)<ECD.SCRN.NO> = "Product"
              SCV.REC(19)<ECD.SCRN.NO> = "Whse   U/M"
              SCV.REC(20)<ECD.SCRN.NO> = "------ ---"
            CASE TVO.PO.TYPE = "M"
              SCV.REC(18)<ECD.SCRN.NO> = "Product"
              SCV.REC(19)<ECD.SCRN.NO> = "Catg   U/M"
              SCV.REC(20)<ECD.SCRN.NO> = "------ ---"
            CASE 1
              SCV.REC(18)<ECD.SCRN.NO> = "Job Num"
              SCV.REC(19)<ECD.SCRN.NO> = "Catg   U/M"
              SCV.REC(20)<ECD.SCRN.NO> = "------ ---"
          END CASE
          ECD.ACTION = 3 ; CALL SCRN.EDIT
          CALL VOUCHER.PO.INQ(MAT VVOS.REC)
          ECD.SCRN.NO = 6
          ECD.ACTION = 3 ; CALL SCRN.EDIT
          OLD.START.LINE = 0 ; GOSUB 10000
        END
    END CASE
  WHILE MORE DO
  REPEAT
  GOTO 99999
*
***** SCROLL ROUTINE
*
10000*
  START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE=OLD.START.LINE THEN GOTO 10001
  OLD.START.LINE=START.LINE
  CNT=1
  FOR N=START.LINE TO LAST.LINE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    MATREAD VVOS.REC FROM VEND.VOUCH.STATS, CONO:TVO.VEND<1,1>:"!":VOUCH.NOS<1,N> ELSE
      MAT VVOS.REC = ""
    END
    P_X  = 1 ; P_Y = SLN ; P_VALUE = N "L#3" ; P_OPT = ""
    P_X  := AM:6 ; P_Y := AM:SLN ; P_VALUE := AM:VOUCH.NOS<1,N> "L#8"
    P_X  := AM:16 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VVOS.VOU.DATE, "D2/") "L#8"
*T28752    P_X  := AM:26 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.INV.NO "L#8"
    P_X  := AM:26 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.INV.NO "L#10"
*T28752    P_X  := AM:36 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VVOS.INV.DATE, "D2/") "L#8"
    P_X  := AM:38 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VVOS.INV.DATE, "D2/") "L#8"
*T28752    P_X  := AM:46 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VVOS.TOT.COST, "MD2") "R#10"
    P_X  := AM:48 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VVOS.TOT.COST, "MD2") "R#10"
*T28752    P_X  := AM:58 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.VOU.PERIOD "R#6"
    P_X  := AM:60 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.VOU.PERIOD "R#6"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT=CNT + 1
  NEXT N
  FOR M=CNT TO PAGE.SIZE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
10001*
  RETURN
*** ENTER LINE NUMBER
70000*
  GOSUB 10000                  
  * T25978 v
  * ECD.VALDAT = "S"
  ECD.VALDAT = "S,SR,SB,ST"
  * T25978 ^
  FOR XXX = START.LINE TO LAST.LINE
    ECD.VALDAT = ECD.VALDAT:",":XXX
  NEXT XXX
  RETURN
***** CALLS FOR SYSCOM
91000*
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
