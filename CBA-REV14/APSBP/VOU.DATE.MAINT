*
***************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SORCE      - BP
* PROGRAM    - VOU.DATE.MAINT
* BY         - JIHAD YAMOUT , C.B.A
* DATE       - 09/26/83
* DESCRIPTION- THIS PROGRAM WILL UPDATE THE PRINT DATE ON TEMP.VOUCHERS
*              FILE.
*T23278 markt 11/25/1998 * Make fiscal data multi-value by division.
*ENDDOC
**************************************************************************
*
****** INSERT FILE EQUETES
*
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>PMC.CPYLIB>FISCAL
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
***** SETUP ERRMSG ROUTINE
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
  OPEN '','TEMP.VOUCHERS' TO TEMP.VOUCHERS ELSE
      ERRMSG = 'TEMP.VOUCHERS FILE IS MISSING'
      GOTO 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'CONTROL FILE IS MISSING'
      GOTO 93000
  END
*
****** LOAD PROC VARIABLES
*
***** T23278 v
  PROCREAD XX ELSE
      ERRMSG = "MUST BE RUN FROM A PROC"
      GOTO 93000
  END
  DIV.CODE = XX<3>
***** T23278 ^
*
****** MAIN PROCESS
*
  M.FLG = 0
  TODAY = DATE()
  MAT TVO.REC = ''
  DATA = 1
  LOOP
      READNEXT KEY ELSE DATA = 0
  WHILE DATA DO
      IF M.FLG = 0 THEN
          M.FLG = 1
          MATREAD FISCAL.REC FROM CONTROL, KEY[1,3]:"APVFISCAL" ELSE
              ERRMSG = "CONTROL RECORD 'APVFISCAL' IS MISSING"
              GOSUB 91000 ; GOTO 99999
          END
          READ PERIOD.REC FROM CONTROL, KEY[1,3]:"ACCT.PERIODS" ELSE
              PERIOD.REC = 12
          END
          NUM.PERIODS = PERIOD.REC<1>
***** T23278 v
          READ DIVISIONS.REC FROM CONTROL, KEY[1,3]:"DIVISIONS" ELSE
              ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
              GOSUB 91000; GOTO 99999
          END
          READ SECURITY.REC FROM CONTROL, KEY[1,3]:"DIV.SECURITY" ELSE
              ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
              GOSUB 91000; GOTO 99999
          END
*
          IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
              IF DIV.CODE = "ALL" OR DIV.CODE = "00" THEN
                  ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
                  GOSUB 91000; GOTO 99999
              END ELSE
                  LOCATE DIV.CODE IN DIVISIONS.REC<1>,1 SETTING POS ELSE
                      ERRMSG = "Division ":DIV.CODE:" not on Control File DIVISIONS Record"
                      GOSUB 91000; GOTO 99999
                  END
              END
          END ELSE
              POS = 1
          END
*        YEAR = FR.CURR.PER[1,4]
*        MONTH = FR.CURR.PER[5,2]
          YEAR  = FR.CURR.PER<1,POS>[1,4]
          MONTH = FR.CURR.PER<1,POS>[5,2]
***** T23278 ^
          MONTH = STR("0",2-LEN(MONTH)):MONTH
          IF MONTH < 1 OR MONTH > NUM.PERIODS THEN
              ERRMSG = "ERROR IN CONTROL RECORD 'APVFISCAL'"; GOSUB 91000; GOTO 99999
          END
      END
      MATREAD TVO.REC FROM TEMP.VOUCHERS,KEY ELSE
          ERRMSG = 'CANNOT LOCATE TEMP.VOUCHERS # ':KEY
          GOSUB 92000
          GOTO 111
      END
*    IF MONTH # KEY[4,2] THEN 
*      TVO.STATUS = "THIS VOUCHER DOES NOT BELONG TO THIS FISCAL PERIOD"
*      MATWRITE TVO.REC ON TEMP.VOUCHERS, KEY
*      GOTO 111
*    END
      IF TVO.PRT.DATE = '' THEN
          TVO.PRT.DATE = TODAY
          TVO.STATUS = ''
          MATWRITE TVO.REC ON TEMP.VOUCHERS, KEY
      END
111 REPEAT
  GOTO 99999
*
***** CALL ERRMSG ROUTINE
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** END OF PROGRAM
*
99999*
END
