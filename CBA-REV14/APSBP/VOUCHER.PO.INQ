  SUBROUTINE VOUCHER.PO.INQ(MAT VVOS.REC)
*COPY>CPYLIB>COM1  
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>POS.CPYLIB>COM.MPO  
*COPY>APS.CPYLIB>COM.APS.FILE.VARS
*COPY>APS.CPYLIB>COM.TEMP.VOUCHERS
*COPY>APS.CPYLIB>COM.VOUCHER.MAINT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* PROGRAM     - VOUCHER.PO.INQ
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 01/26/87
* DESCRIPTION   
*  This program is used to reveiew all po's enter for voucher.
*T25978 adelgado 01/31/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>INVENTORY
*COPY>PMC.CPYLIB>VEND
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>APS.CPYLIB>APS.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*** INT
*
  BEGIN.PAGE = 6
  PAGE.SIZE = 6
  LINE.SPACE = 2
  LINES = 0
  LN = 0
  OLD.START.LINE=0
  TOTAL.VOUCH = 0
  PAY.VALUES = 0
  DIM S.INV.CNV.REC(10)
  MAT S.INV.CNV.REC = MAT INV.CNV.REC
  MAT INV.CNV.REC = ""
  QUESTION = ''
*
**** SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
**** MAIN PROCESS
*
  LINES = COUNT(VVOS.PO.NO, VM) + (VVOS.PO.NO # "")
  TOTAL.VOUCH = 0
  FOR TA = 1 TO LINES
    TOTAL.VOUCH = TOTAL.VOUCH + VVOS.PROD.COST<1,TA>
  NEXT TA
  ECD.NUM = 13 ; SCV.REC(13)<ECD.SCRN.NO> = TOTAL.VOUCH
  ECD.ACTION = 5 ; CALL SCRN.EDIT
  LN = 1
  GOSUB 10000
*
***** ENTER OPTIONS
*
  MORE = 1
  LOOP
    ECD.NUM = 14
    SCV.REC(14)<ECD.SCRN.NO> = "" ; ECD.ACTION = 4 ; CALL SCRN.EDIT
    BEGIN CASE
      CASE ECD.RET.VALUE = 'E' OR ECD.RET.VALUE = 'END' OR ECD.RET.VALUE = ''
        MAT INV.CNV.REC = MAT S.INV.CNV.REC
        MORE = 0
      CASE ECD.RET.VALUE = 'S'
        LN = START.LINE + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 10000
      * T25978 v
      CASE ECD.RET.VALUE = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 10000
      CASE ECD.RET.VALUE = 'ST'
        LN = 1
        GOSUB 10000
      CASE ECD.RET.VALUE = 'SB'
        LN = LINES
        GOSUB 10000
      * T25978 ^
    END CASE
  WHILE MORE DO REPEAT
  GOTO 99999
*
**** ENTER LINE INFORMATION
*
1000*
  GOSUB 10000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN -1, PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RETURN
*
**** SET UP INVENTORY CONVERSION
*
9000*
  BEGIN CASE
    CASE VVOS.U.M<1,N> = "SHT" AND INV.UNIT<1,3> = "LBS" AND VVOS.PO.TYPE<1,N> = "R" 
      ICR.CNV = "MD0"; ICR.DV2 = 1
      ICR.DV1 = INV.M.WT; ICR.MT1 = 10
    CASE VVOS.U.M<1,N> = "PC" AND INV.UNIT<1,3> = "MSI" AND VVOS.PO.TYPE<1,N> = "R"
      ICR.CNV = "MD0"; ICR.DV2 = 1
      ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100
    CASE VVOS.U.M<1,N> = "FT" AND INV.UNIT<1,3> = "MSI" AND VVOS.PO.TYPE<1,N> = "R"
      ICR.CNV = "MD0"; ICR.DV2 = 12
      ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 1000
    CASE 1
      ICR.CNV = "MD2"; ICR.DV2 = 1
      ICR.DV1 = 1; ICR.MT1 = 1
  END CASE
  RETURN
*
***** SCROLL ROUTINE
*
10000*
  START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF START.LINE=OLD.START.LINE THEN GOTO 10001
  OLD.START.LINE=START.LINE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  CNT=1
  FOR N=START.LINE TO LAST.LINE UNTIL N > LINES
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = N "L#3" ; P_OPT = ""
    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.PO.NO<1,N> "L#8"
    P_X  := AM:13 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.PROD<1,N> "L#15"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    IF TVO.PO.TYPE = "O" THEN
      P_X  = 29 ; P_Y = SLN ; P_VALUE = VVOS.WHSE<1,N> "L#7" ; P_OPT = ""
      P_X  := AM:36 ; P_Y := AM:SLN ; P_VALUE := AM:VVOS.U.M<1,N> "L#3"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      MAT INV.REC = ""
      ICR.CNV = "MD2"; ICR.DV2 = 1
      ICR.DV1 = 1; ICR.MT1 = 1
    END ELSE
      IF TVO.PO.TYPE = "M" THEN
        P_X  = 29 ; P_Y = SLN ; P_VALUE = VVOS.MISC.CATG<1,N> "L#4" ; P_OPT = ""
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END ELSE
        P_X  = 29 ; P_Y = SLN ; P_VALUE = VVOS.WHSE<1,N> "L#4" ; P_OPT = ""
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      P_X  = 36 ; P_Y = SLN ; P_VALUE = VVOS.U.M<1,N> "L#3" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      MATREAD INV.REC FROM INVENTORY , CONO:VVOS.PROD<1,N> ELSE
        MAT INV.REC = ""
      END
      GOSUB 9000
    END
    IF ICR.CNV = "MD0" THEN
      P_X  = 41 ; P_Y = SLN ; P_VALUE = INT(((VVOS.VOU.QTY<1,N>/ICR.DV1) * ICR.MT1)/ICR.DV2 + .5) "R#10" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END ELSE
      P_X  = 41 ; P_Y = SLN ; P_VALUE = OCONV(VVOS.VOU.QTY<1,N>, "MD2") "R#10" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    P_X  = 53 ; P_Y = SLN ; P_VALUE = OCONV(VVOS.VOU.UN.COST<1,N>, "MD4") "R#9" ; P_OPT = ""
    P_X  := AM:64 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VVOS.PROD.COST<1,N>, "MD2") "R#10"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    P_X  = 13 ; P_Y = SLN+1 ; P_VALUE = VVOS.PROD.DESC<1,N> "L#45" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT=CNT + 1
  NEXT N
  FOR M=CNT TO PAGE.SIZE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
10001 RETURN
*
**** ENTER PROGRAM QUESTION
*
11000*
  S.VALUE = VALUE
  X = 0 ; Y = 23 ; TYP = 18 ; PMSG = QUESTION
  CALL EDIT.SUB
  IF VALUE = "END" THEN VALUE = "N"
  REPLY = VALUE
  VALUE = S.VALUE
  P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RETURN
***** CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
  RETURN
END
