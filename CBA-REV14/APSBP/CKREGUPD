******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - CKREGUPD
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 06/01/86
* DESCRIPTION - This program update (OAP) & (APCHECK) & (AP.HIST)
*                 & (VEND) files after check register .
* T20269 JR ALLOW CHECK RUNS FOR DIFFERENT DIVISIONS TO RUN SIMULTANEOUSLY
*T23401 11/04/98 Lanny - Misc Vend Name missing on Check Recon Maint.
*T23278 markt 03/18/1999 * Write DIV to APCHECK record.
*ENDDOC
******************************************************
*COPY>APS.CPYLIB>OAP
*COPY>APS.CPYLIB>SQV
*COPY>APS.CPYLIB>CKR
*COPY>APS.CPYLIB>APCHECK
*COPY>APS.CPYLIB>AP.HIST
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** INITIALZATION
*
   TOT.GRS = 0 ; TOT.DSC = 0 ; TOT.NET = 0
*
**** SETUP FOR SYSTEM ERRMSGS
*
   SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
*
**** GET PROC VALUES
*
   PROCREAD PROC.VALUES ELSE
      ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC" ; GOTO 93000
   END
   CONO = PROC.VALUES<1>
   DIV.OWNER = PROC.VALUES<4>; * TASK 20269
*
**** OPEN ALL FILES
*
   OPEN "","CKR" TO CKR ELSE ERRMSG="CKR FILE IS MISSING";GOTO 93000
   OPEN "","OAP" TO OAP ELSE ERRMSG="OAP FILE IS MISSING";GOTO 93000
   OPEN "","SQV" TO SQV ELSE ERRMSG="SQV FILE IS MISSING";GOTO 93000
   OPEN "","VEND" TO VEND ELSE ERRMSG="VEND FILE IS MISSING";GOTO 93000
   OPEN "","APCHECK" TO APCHECK ELSE ERRMSG="APCHECK FILE IS MISSING";GOTO 93000
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING";GOTO 93000
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING";GOTO 93000
   MATREAD COMP.REC FROM COMPANY,CONO ELSE ERRMSG="COMPANY RECORD IS MISSING";GOTO 93000
   IF CO.APHIST.FLG = "Y" THEN
      OPEN "","AP.HIST" TO AP.HIST ELSE ERRMSG="AP.HIST FILE IS MISSING";GOTO 93000
   END
   MATREAD GLTABLE.REC FROM CONTROL,CONO:"GLTABLE" ELSE ERRMSG="GLTABLE RECORD IS MISSING FROM CONTROL";GOTO 93000
   OPEN "DICT","CKP" TO FD.CKP ELSE ERRMSG="DICT CKP FILE IS MISSING";GOTO 93000
* TASK 20269
*    READV PRINT.DATE FROM FD.CKP,CONO:"CHECKDT",1 ELSE ERRMSG="PAYDATE IS MISSING FROM DICT CKP";GOTO 93000
   READV PRINT.DATE FROM FD.CKP,CONO:"CHECKDT":DIV.OWNER,1 ELSE ERRMSG="PAYDATE IS MISSING FROM DICT CKP";GOTO 93000
* TASK 20269
   PRINT.DATE = ICONV(PRINT.DATE, "D2")
   PRINT@(5,16):"Check register update in process":
   PRINT @(5,17):"Now updating vendor : ":
* P_X  := 5 ; P_Y := 16 ; P_VALUE := "Check register update in process" ; P_OPT = ""
* P_X  := AM:5 ; P_Y := AM:17 ; P_VALUE := AM:"Now updating vendor:"
* CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   MAT APCK.REC = ""
*
**** PROCESS FIRST RECORD
*
100*
   READNEXT ID ELSE
      ERRMSG = "NO ITEMS SELECTED" ; GOTO 93000
   END
   MATREAD CKR.REC FROM CKR, ID ELSE GOTO 100
   PREV.CHECK = CKR.CHK.NO ; PREV.VEND = CKR.VEND<1,1>
*T23401 APCK.VEND = CKR.VEND<1,1>
   APCK.VEND = CKR.VEND
   APCK.DATE = PRINT.DATE
   GOSUB 1000
*
*** PROCESS ALL RECORD
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
      MATREAD CKR.REC FROM CKR , ID ELSE GOTO 111
      GOSUB 900
      GOSUB 1000
111*
   REPEAT
   GOSUB 2000
* TASK 20269
*  DELETE CONTROL,CONO:"REPRINT"
*  DELETE CONTROL,CONO:"VENDORS"
   DELETE CONTROL,CONO:"REPRINT":DIV.OWNER
   DELETE CONTROL,CONO:"VENDORS":DIV.OWNER
* TASK 20269    
   DELETE FD.CKP,CONO:"FLAG"
   GOTO 99999
*
**** FIND A BREAK
*
900*
   IF PREV.CHECK # CKR.CHK.NO THEN
      GOSUB 2000
      PREV.CHECK = CKR.CHK.NO
      PREV.VEND = CKR.VEND<1,1>
*T23401  APCK.VEND = CKR.VEND<1,1>
      APCK.VEND = CKR.VEND
      APCK.DATE = PRINT.DATE
      TOT.GRS = 0 ; TOT.DSC = 0 ; TOT.NET = 0
   END
   RETURN
*
**** START UPDATING ALL FILES
*
1000*
   IF CO.APHIST.FLG = "Y" THEN
      MATREADU APH.REC FROM AP.HIST, CONO:CKR.CHK.NO:PRINT.DATE ELSE
         MAT APH.REC = ""
      END
      IF APH.VEND = "" THEN
         APH.VEND = CKR.VEND
         APH.MON = CKR.MON
      END
      VOU.CNT = COUNT(APH.VOUCHER, VM) + (APH.VOUCHER # "") + 1
      APH.VOUCHER<1,VOU.CNT> = ID[4,6]
      APH.INV<1,VOU.CNT> = CKR.INV
      APH.INV.DATE<1,VOU.CNT> = CKR.INV.DATE
      APH.GRS.AMT<1,VOU.CNT> = CKR.GRS.AMT
      APH.MER.AMT<1,VOU.CNT> = CKR.GRS.AMT - CKR.DSC.AMT
      APH.DSC.AMT<1,VOU.CNT> = CKR.DSC.AMT
      APH.FLG=""
      MATWRITE APH.REC ON AP.HIST,CONO:CKR.CHK.NO:PRINT.DATE
   END
*
***** ADD VEND TOTAL AND UPDATE OAP FILE
*
   TOT.GRS = TOT.GRS + CKR.GRS.AMT
   TOT.DSC = TOT.DSC + CKR.DSC.AMT
   TOT.NET = TOT.NET + CKR.GRS.AMT - CKR.DSC.AMT
   MATREADU OAP.REC FROM OAP, ID ELSE GOTO 1999
   OAP.PAID.AMT = OAP.PAID.AMT + CKR.PAID.AMT
   OAP.PAID.DATE = PRINT.DATE
   OAP.DISB.ACCT = CKR.ASSET.ID
   OAP.CHK.NO = CKR.CHK.NO
   OAP.DSC.PAID = OAP.DSC.PAID + CKR.DSC.AMT
   MATWRITE OAP.REC ON OAP,ID
   MATREADU SQV.REC FROM SQV, ID ELSE GOTO 1999
   P.CNT = COUNT(SQV.PAID.AMT, VM) + (SQV.PAID.AMT # "") + 1
   SQV.PAID.AMT<1,P.CNT> = CKR.PAID.AMT
   SQV.PAID.DATE<1,P.CNT> = PRINT.DATE
   SQV.DISB.ACCT<1,P.CNT> = CKR.ASSET.ID
   SQV.CHK.NO<1,P.CNT> = CKR.CHK.NO
   SQV.DSC.PAID<1,P.CNT> = CKR.DSC.AMT
   MATWRITE SQV.REC ON SQV,ID
*
*--- LOAD APCHECK VOUCHERS
*
   IF CO.CHK.RECON # "N" THEN
      LOCATE ID[4,6] IN APCK.VOUCH<1>,1 SETTING PTR ELSE
         APCK.VOUCH<1,PTR> = ID[4,6]
      END
   END
1999*
   RETURN
**** UPDATE VEND FILE AND APCHECK
2000*
* TASK 20269
*    MATREAD VEND.REC FROM VEND,CONO:PREV.VEND[1,5] ELSE MAT VEND.REC = ""
2010 *
   REC.LOCK = 0
   MATREADU VEND.REC FROM VEND, CONO:PREV.VEND[1,5] LOCKED REC.LOCK=1 ELSE
      MAT VEND.REC = ""
   END
   IF REC.LOCK THEN
      CRT @(0,22):CL:"VENDOR ":PREV.VEND[1,5]:" IS LOCKED PLEASE WAIT ":
      GOTO 2010
   END
   PRINT@(31,17):PREV.VEND"L#5":"  ": VEND.DESC "L#30":
   VEND.PD.M.T.D = VEND.PD.M.T.D + TOT.NET
   VEND.PD.Y.T.D = VEND.PD.Y.T.D + TOT.NET
   VEND.DATE.PD = PRINT.DATE
   VEND.PA.AMT = TOT.NET
   VEND.CK.NO.PD = PREV.CHECK
   VEND.OP.BAL = VEND.OP.BAL - TOT.GRS
   MATWRITE VEND.REC ON VEND,CONO:PREV.VEND[1,5]
   IF CO.CHK.RECON # "N" THEN
      APCK.AMT = TOT.NET
      APCK.DIV = DIV.OWNER;* T23278
      MATWRITE APCK.REC ON APCHECK, CONO:CKR.ASSET.ID:PREV.CHECK
      MAT APCK.REC = ""
   END
   RETURN
***** CALLS FOR SYSCOM
91000*
   ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
