*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - APSBP
* PROGRAM      - VOUCHER.CREATION
* BY           - JIHAD YAMOUT, CBA
* DESCRIPTION  - THIS PROGRAM CREATES AUTOMATIC VOUCHERS.
*
* MOD 11/13/94 CLW Task 17919 - Accrual account
* T19149 RKB 05/01/95 * add support for ap div,dept,cctr
*T20016 RKB 01/31/96 * add support for div,dept,cctr for ap side
*T22288 rick 10/15/1997 * Year 2000 compliance. Make sure date calcs use
*                         four digit years.
*T23278 markt 11/24/1998 * Make fiscal data multi-value by division.
*T25759 cmykleb 05/17/2002 * Add remitt comments.
*********************************************************************
*COPY>APS.CPYLIB>AUTO.VOUCHERS
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>GLTABLE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
   DIM VOU.NOS(20)
   MAT VOU.NOS = ''
   DIM L.VOU(5)
*T19149 v
   GEN.DIV='00' ;GEN.DEPT='00' ;GEN.CCTR='000'
*T19149 ^      
*
*---- INTITIALIZATION
*
***** T23278 v
   PROCREAD XX ELSE
      ERRMSG = "MUST BE RUN FROM A PROC"
      GOSUB 91000
      GOTO  99990
   END
   DIV.CODE = XX<3>
***** T23278 ^
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   MAT FILE.VARS = '' ; MAT EDIT.COM = '' ; MAT EDIT.COM.DRIVER = '' ; MAT COMP.REC = '' ; MAT TVO.REC = '' ; MAT AVO.REC = '' ; MAT L.VOU = ''
   OPEN 'COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING'; GOTO 93000
   OPEN 'AUTO.VOUCHERS' TO AUTO.VOUCHERS ELSE ERRMSG = "AUTO.VOUCHERS FILE IS MISSING" ; GOTO 93000
   OPEN 'TEMP.VOUCHERS' TO TEMP.VOUCHERS ELSE ERRMSG = "TEMP.VOUCHERS FILE IS MISSING" ; GOTO 93000
   OPEN 'APS.SCREENS' TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE IS MISSING" ; GOTO 93000
   OPEN 'PREFIX' TO PREFIX ELSE ERRMSG = "PREFIX FILE IS MISSING" ; GOTO 93000
   OPEN 'CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING'; GOTO 93000
   OPEN 'INV.XREF' TO INV.XREF ELSE ERRMSG = 'INV.XREF FILE MISSING'; GOTO 93000
   OPEN 'OAP' TO OAP ELSE ERRMSG = 'OAP FILE MISSING'; GOTO 93000
   OPEN 'SQV' TO SQV ELSE ERRMSG = 'SQV FILE MISSING'; GOTO 93000
*
*---- SETUP VALUES FROM M.SCREENS
*
   ECD.ACTION = 1
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = "VOUCHER.CREATION"
   CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION = 2
   CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
   READNEXT ID ELSE GOTO 99990
   CONO = ID[1,3]
   SEQ.ID = ID[4,99]
   MATREAD COMP.REC FROM COMPANY,CONO ELSE GOTO 99990
   READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = 12
   END
   NUM.PERIODS = PERIOD.REC<1>
*
*---- ENTER FISCAL MONTH/PERIOD
10 *
   MATREAD FISCAL.REC FROM CONTROL,CONO:"APVFISCAL" ELSE
      ERRMSG = "APVFISCAL CONTROL RECORD IS MISSING"
      GOSUB 91000
      GOTO 99990
   END
   MATREAD GLTABLE.REC FROM CONTROL,CONO:"GLTABLE" ELSE
      ERRMSG = "GLTABLE CONTROL RECORD IS MISSING"
      GOSUB 91000
      GOTO 99990
   END
***** T23278 v
   READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
      ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
      GOSUB 91000
      GOTO  99990
   END
   READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
      GOSUB 91000
      GOTO  99990
   END
*
   IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
         ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
         GOSUB 91000; GOTO 99990
      END
      LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING POS ELSE
         ERRMSG = "Division ":DIV.CODE:" not found on Control File DIVISIONS Record"
         GOSUB 91000
         GOTO  99990
      END
   END ELSE
      POS = 1
   END
*  YEAR = FR.CURR.PER[1,4]
*  MON = FR.CURR.PER[5,2]
   YEAR = FR.CURR.PER[1,4]<1,POS>
   MON  = FR.CURR.PER[5,2]<1,POS>
***** T23278 ^
   MON = STR("0",2-LEN(MON)):MON
   IF MON < 1 OR MON > NUM.PERIODS THEN
      ERRMSG = "ERROR IN CONTROL RECORD APVFISCAL"
      GOSUB 91000
      GOTO 99990
   END
   READ DATES FROM CONTROL,CONO:"OPENDATES" ELSE
      ERRMSG = "SALESDATES RECORD IS MISSING"
      GOSUB 91000
      GOTO 99990
   END
* CSF 17492 *
   READV FIS.YR FROM CONTROL, CONO:"FISCAL",1 ELSE
      ERRMSG="FISCAL CONTROL RECORD IS MISSING"
      GOSUB 91000
      GOTO 99990
   END
   IF YEAR > FIS.YR[1,4] THEN
      YDIF = YEAR - FIS.YR[1,4]
      FOR Y = 1 TO NUM.PERIODS
         TMP = OCONV(DATES<Y>, 'D4/')
         TMP = TMP[1,6] : (TMP[7,4] + YDIF)
         DATES<Y> = ICONV(TMP, 'D')
      NEXT Y
   END
* END CSF 17492 *
   PER.DATE = DATES<MON>
   PER.DATE = OCONV(PER.DATE,"D4/")  ;*T22288 use 4 digit year
   C.MON = FIELD(PER.DATE,"/",1)
   C.YY = FIELD(PER.DATE,"/",3)
   SCV.REC(5)<1> = MON
   ECD.NUM = 5 ; ECD.ACTION = 5 ; CALL SCRN.EDIT
   ECD.NUM = 6 ; ECD.ACTION = 4 ; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 99990
   GOSUB 1000
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
      CONO = ID[1,3]
      SEQ.ID = ID[4,99]
   WHILE DATA DO
      GOSUB 1000
   REPEAT
   GOTO 99990
1000 MATREADU AVO.REC FROM AUTO.VOUCHERS,ID ELSE
      RELEASE AUTO.VOUCHERS,ID ; GOTO 1999
   END
   IF AVO.ST.PER # MON AND AVO.MON = "" THEN GOTO 1999
   IF MON = AVO.MON THEN GOTO 1999
   MAT TVO.REC = ""
   TVO.VEND = AVO.VEND
* TASK 20016 v
   TVO.DIV.OWNER = AVO.DIV.OWNER
* TASK 20016 ^
   TVO.INV = AVO.INV
   INV.DATE = OCONV(AVO.INV.DATE,"D2/")
   MM = C.MON ;DD = INV.DATE[4,2] ;YY = C.YY
   LOOP
*T22288 v on next two lines use 4 digit year  
      INV.DATE=STR('0',2-LEN(MM)):MM:"/":STR('0',2-LEN(DD)):DD:"/":STR('0',4-LEN(YY)):YY
      TVO.INV.DATE = ICONV(INV.DATE,"D4/")
*t22288 ^  
   WHILE TVO.INV.DATE = "" DO
      DD = DD - 1
      IF DD < 1 THEN
         DD = 31
         MM = MM - 1
         IF MM < 1 THEN
            YY = YY - 1
            MM = 12
         END
      END
   REPEAT
*T22288 v on next two lines use 4 digit year  
   DATE.INV = OCONV(TVO.INV.DATE,"D4/")
   MM = DATE.INV[1,2] ; DD = DATE.INV[4,2] ; YY = DATE.INV[7,4]
*T22288 ^
   TRM.DAYS = AVO.TERMS<1,2>
   DISC = AVO.TERMS<1,1>
   IF TRM.DAYS[1,1] = 'P' THEN
      DD = TRM.DAYS[2,2]
      IF DD > CO.PROX.CUTOFF THEN
         NMM = MM + 2
      END ELSE
         NMM = MM + 1
      END
      IF NMM > 12 THEN
         NMM = NMM - 12
         YY = YY + 1
      END
      LOOP
*T22288 v on next two lines use 4 digit year  
         DUE.DATE=STR('0',2-LEN(NMM)):NMM:"/":STR('0',2-LEN(DD)):DD:"/":STR("0",4-LEN(YY)):YY
         TVO.DUE.DATE = ICONV(DUE.DATE,"D4/")
*T22288 ^    
      WHILE TVO.DUE.DATE = "" DO
         DD = DD - 1
         IF DD < 1 THEN
            DD = 31
            NMM = NMM - 1
            IF NMM < 1 THEN
               YY = YY - 1
               NMM = 12
            END
         END
      REPEAT
   END ELSE
      TVO.DUE.DATE = TVO.INV.DATE + (DISC[5,2]*1)
   END
   TVO.GRS.AMT = AVO.GRS.AMT
   TVO.MER.AMT = AVO.MER.AMT
   TVO.DSC.AMT = AVO.DSC.AMT
   TVO.CTR = AVO.CTR
   TVO.ACCT = AVO.ACCT
   TVO.DIST.AMT = AVO.DIST.AMT
   TVO.ASSET.ID = AVO.ASSET.ID
   TVO.DIV = AVO.DIV
   TVO.DEPT = AVO.DEPT
   TVO.CCTR = AVO.CCTR
*  TVO.MON = FR.CURR.PER;* T23278
   TVO.MON = FR.CURR.PER<1,POS>;* T23278
   TVO.TERMS = AVO.TERMS
   TVO.PO = AVO.PO
   TVO.REM.COMM = AVO.REM.COMM ; * T25759
   TVO.A.C.FLG = SEQ.ID
   TVO.AP.ACCT = GLTB.AP       ;*  Task 17919
   TVO.AP.AMT = (0 - AVO.GRS.AMT)       ;*  Task 17919
*T19149 v
*T20016 v
*  TVO.AP.DIV = GEN.DIV
*  TVO.AP.DEPT = GEN.DEPT
*  TVO.AP.CCTR = GEN.CCTR
   TVO.AP.DIV = AVO.AP.DIV
   TVO.AP.DEPT = AVO.AP.DEPT
   TVO.AP.CCTR = AVO.AP.CCTR
*T20016 ^      
*T19149 ^      
   MATREADU VOU.NOS FROM CONTROL,CONO:"VOUNUMBERS" ELSE
      MAT VOU.NOS = 0
   END
   LOOP
      FND = 1
      TFND = 1
      VOU.NOS(MON) = VOU.NOS(MON) + 1
      IF VOU.NOS(MON) > 9999 THEN VOU.NOS(MON) = 1
      V.CODE = STR('0',2-LEN(MON)):MON:STR('0',4-LEN(VOU.NOS(MON))):VOU.NOS(MON)
      READU O.RECORD FROM TEMP.VOUCHERS, CONO : V.CODE ELSE TFND = 0
      READ O.RECORD FROM OAP, CONO:V.CODE ELSE
         READ O.RECORD FROM SQV, CONO:V.CODE ELSE
            FND = 0
         END
      END
   WHILE FND OR TFND DO
      RELEASE TEMP.VOUCHERS, CONO : V.CODE
   REPEAT
*     MN = MON - 6
*     IF MN < 1 THEN MN = MN + 12
*     VOU.NOS(MN) = ""
   MATWRITE VOU.NOS ON CONTROL,CONO:"VOUNUMBERS"
   IF AVO.INV # "" THEN
      L.VOU(1) = V.CODE
      MATWRITE L.VOU ON INV.XREF,CONO:STR('0',10-LEN(AVO.INV)):AVO.INV:AVO.VEND
   END
   IF AVO.N.P.P + 1 > AVO.N.O.P THEN
      TVO.STATUS = "AUTOMATIC VOUCHER NUMBER (":ID[4,99]:") HAS BEEN PAID IN FULL"
   END
   IF AVO.N.P.P + 1 = AVO.N.O.P THEN
      TVO.STATUS = "THIS IS THE LAST PAYMENT ON AUTOMATIC VOUCHER (":ID[4,99]:")"
   END
   IF AVO.N.P.P < AVO.N.O.P THEN
      AVO.N.P.P = AVO.N.P.P + 1
   END
   MATWRITE TVO.REC ON TEMP.VOUCHERS,CONO:V.CODE
   AVO.MON = MON
   MATWRITE AVO.REC ON AUTO.VOUCHERS,ID
1999 RETURN
   WRITEV "Y" ON CONTROL,CONO:"VOUCHER.CREATION",1
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
   GOTO 99999
*
99990 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
*
*---- END OF JOB
99999 *
   END

