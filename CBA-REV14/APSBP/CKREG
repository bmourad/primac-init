*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* PROGRAM     - CKREG
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 06/01/86
* DESCRIPTION - This program is the account payable check register.
* 10/14/94 CLW Task 17919 - Accrual accounting
* T19149 RKB 05/02/95 add support for ap credit div,dept,cctr
* T20269 JR ALLOW CHECK RUNS FOR DIFFERENT DIVISION TO RUN SIMULTANEOUSLY
*T23278 markt 11/25/1998 * Make fiscal data multi-value by division.
*T25535 lanny 11/20/2000 - Correct DIV/DEPT/CCTR on A/P MCD records.
*T25886 lanny 06/14/2001 * Clear A/P TEMP.MCD file at beginning of run
*                          to avoid having voucher amounts doubled.
*T26493 cmykleb 04/17/2002 * Change pgm to get the rpt # from the proc.
*T26556 adelgado 05/24/2002 * Change how error are handle
*ENDDOC
*********************************************************************
*COPY>APS.CPYLIB>CKR
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>FISCAL
*COPY>APS.CPYLIB>MCD
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>PRS.CPYLIB>PRSCHECK.STATS
*COPY>PMC.CPYLIB>COA
*COPY>ICS.CPYLIB>CATEGORY.MISC
*
*--- INITIALZATION
*
  LINE.COUNT = 99
  PAGE.NO = 0
  CHECK.CNT = 0
  ALL.CHECK = 0
  GTOT.GRS = 0
  GTOT.DSC = 0
  GTOT.NET = 0
  SP1 = " "
  ALL.ID = ""
*
*--- SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- GET PROC VALUES
*
  PROCREAD PROC.VALUES ELSE
    ERRMSG = "THIS REPORT MUST RUN FROM A PROC."
    GOTO 93000
  END
  CONO = PROC.VALUES<1>
  DIV.OWNER = PROC.VALUES<4>
  DIV.CODE = DIV.OWNER;* T23278
*
*--- OPEN ALL FILES
*
  OPEN "DICT","CKR" TO D.CKR ELSE ERRMSG = "CKR FILE IS MISSING"; GOTO 93000
  OPEN "","CKR" TO CKR ELSE ERRMSG = "CKR FILE IS MISSING"; GOTO 93000
  OPEN "","MCD" TO MCD ELSE ERRMSG = "MCD FILE IS MISSING"; GOTO 93000
  OPEN "","MCD.TAG" TO MCD.TAG ELSE ERRMSG = "MCD.TAG FILE IS MISSING"; GOTO 93000
  OPEN "","TEMP.MCD" TO TEMP.MCD ELSE ERRMSG = "TEMP.MCD FILE IS MISSING"; GOTO 93000
  OPEN "","VEND" TO VEND ELSE ERRMSG = "VENDOR FILE IS MISSING"; GOTO 93000
  OPEN "DICT","CKP" TO D.CKP ELSE ERRMSG = "CKP FILE IS MISSING"; GOTO 93000
* TASK 20269
*      READV PRINT.DATE FROM D.CKP, CONO:"CHECKDT",1 ELSE ERRMSG = "REPORT DATE IS MISSING"; GOTO 93000
* T26556 v
*   READV PRINT.DATE FROM D.CKP, CONO:"CHECKDT":DIV.OWNER,1 ELSE ERRMSG = "REPORT DATE IS MISSING"; GOTO 93000
  READV PRINT.DATE FROM D.CKP, CONO:"CHECKDT":DIV.OWNER,1 ELSE ERRMSG = "REPORT DATE IS MISSING"; GOSUB 91000; GOTO 99999
* T26556 ^
* TASK 20269
  PRINT.DATE = ICONV(PRINT.DATE , "D2")
  OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOTO 93000
  OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY NOT ON FILE"; GOTO 93000
  OPEN "","CHECK.STATS" TO CHECK.STATS ELSE ERRMSG = "CHECK.STATS NOT ON FILE"; GOTO 93000
  OPEN "","COA" TO COA ELSE ERRMSG = "COA NOT ON FILE"; GOTO 93000
  OPEN "","CATEGORY.MISC" TO CATEGORY.MISC ELSE ERRMSG = "CATEGORY.MISC NOT ON FILE"; GOTO 93000
*
*--- GET VALUES FROM CONTROL FILES
*
  MATREAD FISCAL.REC FROM CONTROL, CONO:"APDFISCAL" ELSE ERRMSG = "FISCAL RECORD IS MISSING"; GOTO 93000
  MATREAD GLTABLE.REC FROM CONTROL, CONO:"GLTABLE" ELSE ERRMSG = "GLTABLE IS MISSING"; GOTO 93000
  MATREAD COMP.REC FROM COMPANY, CONO ELSE ERRMSG = "COMPANY RECORD IS MISSING"; GOTO 93000
***** T23278 v
  READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
    ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
*     GOTO 93000      ;* T26556
    GOSUB 91000 ; GOTO 99999
  END
  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
    ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
*     GOTO 93000      ;* T26556
    GOSUB 91000 ; GOTO 99999
  END
*
  IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
    IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
      ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
      GOSUB 91000; GOTO 99999
    END
    LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING DIV.POS ELSE
      ERRMSG = "Division ":DIV.CODE:" not found in Control File DIVISIONS Record"
*        GOTO 93000      ;* T26556
      GOSUB 91000 ; GOTO 99999
    END
  END ELSE
    DIV.POS = 1
  END
***** T23278 ^
*T25886 v
  READV APKEYS FROM D.CKR, CONO:"APKEYS":DIV.OWNER,1 ELSE APKEYS = ''
  FOR I = 1 TO DCOUNT(APKEYS,VM)
    READU APKR FROM TEMP.MCD, CONO:APKEYS<1,I> ELSE NULL
    DELETE TEMP.MCD, CONO:APKEYS<1,I>
  NEXT I
*T25886 ^
*
*--- SETUP HEADING
*
*T26493 v
*  CONO.NAME = PROC.VALUES<2>
*  REPORT.NAME = "ACCOUNTS PAYABLE CHECK REGISTER"
*  REPORT.NUMBER = "XXXX"
  CONO.NAME = ""
  REPORT.NAME = ""
  REPORT.NUMBER = PROC.VALUES<2>
*T26493 ^
  REPORT.DATE = DATE()
  HD1 = ""
  HD2 = ""
  CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
  HD3 = "CHECK#  CK DATE VENDOR #           VENDOR NAME          INVOICE NO INV DATE VOUCH#   GROSS AMT     DISC AMT     NET AMOUNT  "
  HD4 = "------ -------- -------- ------------------------------ ---------- -------- ------ ------------- ------------- -------------"
  HD5 = "                                                                                   ------------- ------------- -------------"
  HD6 = "                                                                                   ============= ============= ============="
  PRINTER ON
  MAT MCD.REC = ""
  AP.ACCT = ""
*
*--- READ AND PROCESS FIRST RECORD
*
100 *
  READNEXT ID ELSE
    PRINTER OFF
    ERRMSG = "NO ITEMS SELECETED"
    GOSUB 91000
    GOTO 99999
  END
  MATREAD CKR.REC FROM CKR, ID ELSE GOTO 100
  ACCOUNT.NO = CKR.ASSET.ID
  MCD.CHECK = CKR.CHK.NO
  MCD.BANK.ACCT = CKR.ASSET.ID
  MCD.CHK.DATE = PRINT.DATE
  MCD.VEND = CKR.VEND
*  MCD.MON = FR.CURR.PER;* T23278
  MCD.MON = FR.CURR.PER<1,DIV.POS>;* T23278
  GOSUB 300
  GOSUB 1000
*
*--- PROCESS ALL RECORDS
*
200 *
  DATA = 1
  LOOP
    READNEXT ID ELSE DATA = 0
  WHILE DATA = 1 DO
    MATREAD CKR.REC FROM CKR, ID ELSE GOTO 299
    GOSUB 300
    GOSUB 900
    GOSUB 1000
299 *
  REPEAT
  GOSUB 2000
  GOSUB 3000
  GOTO 99999
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- GET VENDOR INFORMATION
*
300 *
  IF CKR.VEND<1,1> # "VOID" THEN
    IF CKR.VEND<1,2> # "" THEN
      VEND.DESC = CKR.VEND<1,2>
    END ELSE
      MATREAD VEND.REC FROM VEND, CONO:CKR.VEND<1,1> ELSE
        VEND.DESC = "VEND NOT ON FILE"
      END
    END
  END ELSE
    VEND.DESC = "VOIDED CHECK"
  END
  RETURN
*
*--- FIND BREAK
*
900 *
  IF CKR.CHK.NO # MCD.CHECK THEN
    GOSUB 2000
    PRT.FLG = 1
    ALL.CHECK = 0
    MCD.CHECK = CKR.CHK.NO
    MCD.BANK.ACCT = CKR.ASSET.ID
    MCD.CHK.DATE = PRINT.DATE
    MCD.VEND = CKR.VEND
*     MCD.MON = FR.CURR.PER;* T23278
    MCD.MON = FR.CURR.PER<1,DIV.POS>;* T23278
  END
  RETURN
*
*--- PRINT VOUCHER LINES AND TOTAL CHECKS
*
1000 *
  IF LINE.COUNT > 55 THEN
    GOSUB 8000
    PRT.FLG = 1
  END
  P.LINE = ""
  IF PRT.FLG = 1 THEN
    P.LINE = P.LINE: CKR.CHK.NO "R#6":SP1
    P.LINE = P.LINE: OCONV(CKR.PAID.DATE,"D2/") "R#8":SP1
    P.LINE = P.LINE: CKR.VEND<1,1> "L#8":SP1
    P.LINE = P.LINE: VEND.DESC "L#30":SP1
    PRT.FLG = 0
  END ELSE
    P.LINE = SPACE(56)
  END
  IF CKR.VEND<1,1> = "VOID" THEN
    MCD.NO.OF.CHK = 1
    MCD.GRS.AMT = 0
    MCD.DSC.AMT = 0
    MCD.NET.AMT = 0
    ID.MCD = "V" : MCD.BANK.ACCT : MCD.CHK.DATE : MCD.CHECK
    LOCATE ID.MCD IN ALL.ID,1 SETTING PTR ELSE
      ALL.ID<PTR> = ID.MCD
    END
    READU ID.REC FROM TEMP.MCD, CONO : ID.MCD ELSE ID.REC = ""
    MATWRITE MCD.REC ON TEMP.MCD, CONO : ID.MCD
    MAT MCD.REC = ""
    ID.REC = ""
    ALL.CHECK = 0
  END ELSE
    P.LINE = P.LINE:CKR.INV "L#10":SP1
    P.LINE = P.LINE:OCONV(CKR.INV.DATE, "D2/") "R#8":SP1
    P.LINE = P.LINE:ID[4,6] "L#6":SP1
    P.LINE = P.LINE:OCONV(CKR.GRS.AMT , "MD2,") "R#13":SP1
    P.LINE = P.LINE:OCONV(CKR.DSC.AMT , "MD2,") "R#13":SP1
    P.LINE = P.LINE:OCONV(CKR.GRS.AMT - CKR.DSC.AMT, "MD2,") "R#13"
    ACT.TOT = 0
    FOR X = 1 TO CKR.CTR
      ACT.TOT = ACT.TOT + CKR.DIST.AMT<1,X>
    NEXT X
    IF ACT.TOT # CKR.GRS.AMT THEN
      P.LINE = P.LINE :"PARTIAL"
    END
*
*--- LOAD MCD
*
    MCD.GRS.AMT = MCD.GRS.AMT + CKR.GRS.AMT
    MCD.DSC.AMT = MCD.DSC.AMT + CKR.DSC.AMT
    MCD.NET.AMT = MCD.NET.AMT + (CKR.GRS.AMT - CKR.DSC.AMT)
    CALL AMOUNT.DIST.SUB(CKR.DIV,CKR.DIST.AMT,CKR.GRS.AMT,ALL.GRS,CKR.DSC.AMT,ALL.DSC,ERRMSG)
    VFND = COUNT(MCD.VOUCH,VM) + (MCD.VOUCH # "") + 1
    MCD.VOUCH<1,VFND> = ID[4,6]
    MCD.VOUCH.GRS<1,VFND> = ""
    MCD.VOUCH.DSC<1,VFND> = ""
    MCD.VOUCH.NET<1,VFND> = ""
    MCD.DIV<1,VFND> = ""
    MCD.DEPT<1,VFND> = ""
    MCD.CCTR<1,VFND> = ""
    TR.CNT = COUNT(CKR.DIST.AMT,VM) + (CKR.DIST.AMT # "")
    FOR TR = 1 TO TR.CNT
      DPTR = 1
      LOOP
        LOCATE CKR.DIV<1,TR> IN MCD.DIV<1,VFND>,DPTR SETTING DFND ELSE
          MCD.DIV<1,VFND,DFND> = CKR.DIV<1,TR>
          MCD.DEPT<1,VFND,DFND> = CKR.DEPT<1,TR>
          MCD.CCTR<1,VFND,DFND> = CKR.CCTR<1,TR>
          MCD.VOUCH.GRS<1,VFND,DFND> = ALL.GRS<1,TR>
          MCD.VOUCH.DSC<1,VFND,DFND> = ALL.DSC<1,TR>
          MCD.VOUCH.NET<1,VFND,DFND> = ALL.GRS<1,TR> - ALL.DSC<1,TR>
          DPTR = 0
        END
        BEGIN CASE
          CASE DPTR = 0
          CASE MCD.DEPT<1,VFND,DFND> # CKR.DEPT<1,TR>
            DPTR = DFND + 1
          CASE MCD.CCTR<1,VFND,DFND> # CKR.CCTR<1,TR>
            DPTR = DFND + 1
          CASE 1
            MCD.VOUCH.GRS<1,VFND,DFND> = MCD.VOUCH.GRS<1,VFND,DFND> + ALL.GRS<1,TR>
            MCD.VOUCH.DSC<1,VFND,DFND> = MCD.VOUCH.DSC<1,VFND,DFND> + ALL.DSC<1,TR>
            MCD.VOUCH.NET<1,VFND,DFND> = MCD.VOUCH.NET<1,VFND,DFND> + ALL.GRS<1,TR> - ALL.DSC<1,TR>
            DPTR = 0
        END CASE
      WHILE DPTR DO
      REPEAT
    NEXT TR
*
* Distribute A/P details         TASK 17919
*
    GOSUB 4000
  END
  PRINT P.LINE
  LINE.COUNT = LINE.COUNT + 1
1999 *
  RETURN
*
*--- PRINT CHECK TOTAL LINE
*
2000 *
  IF LINE.COUNT + 3 > 55 THEN
    GOSUB 8000
  END
  PRINT HD5
  T.LINE = ""
  T.LINE = T.LINE:SPACE(62):"CHECK# "
  T.LINE = T.LINE:MCD.CHECK "L#6"
  T.LINE = T.LINE:"  TOTAL "
  T.LINE = T.LINE:OCONV(MCD.GRS.AMT, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(MCD.DSC.AMT, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(MCD.NET.AMT, "MD2,") "R#13"
  PRINT T.LINE
  PRINT
  GTOT.GRS = GTOT.GRS + MCD.GRS.AMT
  GTOT.DSC = GTOT.DSC + MCD.DSC.AMT
  GTOT.NET = GTOT.NET + MCD.NET.AMT
  CHECK.CNT = CHECK.CNT + 1
  ALL.CHECK = ALL.CHECK + 1
  MCD.NO.OF.CHK = ALL.CHECK
  ID.MCD = "A" : MCD.BANK.ACCT : MCD.CHK.DATE : MCD.CHECK
  LOCATE ID.MCD IN ALL.ID,1 SETTING PTR ELSE
    ALL.ID<PTR> = ID.MCD
  END
  READU ID.REC FROM TEMP.MCD, CONO : ID.MCD ELSE ID.REC = ""
  MATWRITE MCD.REC ON TEMP.MCD, CONO : ID.MCD
  MAT MCD.REC = ""
  ID.REC = ""
  LINE.COUNT = LINE.COUNT + 3
  RETURN
*
*--- PRINT GRAND TOTAL
*
3000 *
  IF LINE.COUNT + 2 >  55 THEN
    GOSUB 8000
  END
  PRINT HD6
  T.LINE = ""
  T.LINE = T.LINE:SPACE(26):CHECK.CNT "R#4":" CHECKS PRINTED       G/L DISB ACCT : ":ACCOUNT.NO CO.ACCT.MASK:SPACE(1)
  T.LINE = T.LINE:OCONV(GTOT.GRS, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(GTOT.DSC, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(GTOT.GRS - GTOT.DSC, "MD2,") "R#13"
  PRINT T.LINE
*******
  SUB.HEAD1 = "                                   START    LAST    START   LAST  LAST USED   CHECK"
  SUB.HEAD2 = "GL ACCT          ACCT DESC         UNUSED  UNUSED   VOID    VOID  CHECK NO. RUN DATE"
  UL1 =       "-------  ------------------------- ------  ------  ------  ------ --------- --------"
* TASK 20269
*      MATREAD PCS.REC FROM CHECK.STATS,CONO:ACCOUNT.NO ELSE GOTO 3100
  MATREAD PCS.REC FROM CHECK.STATS,CONO:ACCOUNT.NO:"!":DIV.OWNER ELSE GOTO 3100
* TASK 20269
  MATREAD COA.REC FROM COA,CONO:ACCOUNT.NO ELSE COA.DESC = "??????"
  IF LINE.COUNT+6 > 55 THEN
    GOSUB 8000
    PRT.FLG = 1
  END
  PRINT
  PRINT SUB.HEAD1
  PRINT SUB.HEAD2
  PRINT UL1
  PRINT
  LINE.COUNT = LINE.COUNT + 4
  PRINT ACCOUNT.NO "L#7  ":
  PRINT COA.DESC "L#25 ":
  UCNT = DCOUNT(PCS.START.UNUSED,VM)
  IF PCS.REC(2)<1,PCS.LST.MV.NO> = '' THEN
    PCS.LST.MV.NO=PCS.LST.MV.NO-1
  END
  FOR I = PCS.LST.MV.NO TO UCNT
    IF LINE.COUNT > 55 THEN
      GOSUB 8000
      PRT.FLG = 1
    END
    IF I > PCS.LST.MV.NO THEN PRINT SPACE(35):
    FOR PP = 2 TO 5
      IF PCS.REC(PP)<1,I> # "" THEN
        PCS.REC(PP)<1,I>=(STR("0",6-LEN(PCS.REC(PP)<1,I>)):PCS.REC(PP)<1,I>)
      END
    NEXT PP
    PRINT PCS.START.UNUSED<1,I> "R#6  ":
    PRINT PCS.END.UNUSED<1,I> "R#6  ":
    PRINT PCS.START.VOID<1,I> "R#6  ":
    PRINT PCS.END.VOID<1,I> "R#6 ":
    IF I=UCNT THEN
      PRINT "   ":(STR("0",6-LEN(PCS.LST.CHK.NO)):PCS.LST.CHK.NO) "R#6 ":
    END ELSE
      PRINT SPACE(10):
    END
    PRINT OCONV(PCS.VOID.DATE<1,I>,"D2/") "L#8"
    LINE.COUNT = LINE.COUNT + 1
    IF LINE.COUNT > 55 THEN
      GOSUB 8000
      PRT.FLG = 1
    END
  NEXT I
  IF UCNT=0 THEN
    PRINT SPACE(34):(STR("0",6-LEN(PCS.LST.CHK.NO)):PCS.LST.CHK.NO) "R#6"
  END
3100 *
*******
  PRINTER CLOSE
  PRINTER OFF
*T26493 v
*  PRINT @(0,0):"PRIMAC                     ACCOUNTS PAYABLE CHECK REGISTER                [08.1]":
*  PRINT @(0,1):"-------------------------------------------------------------------------------":
*  PRINT @(5,12):"************************** PROGRAM MESSAGES ***************************":
*  PRINT @(5,20):"***********************************************************************":
*  PRINT @(0,21):"-------------------------------------------------------------------------------":
*T26493 ^
  PRINT @(5,13):@(-3):"If 'Y' is answered, file will be updated and erased":
3001 *
  PRINT @(5,14):"Did the report print Satisfactorily ? (Y/N) ":
  INPUT X:
  PRINT @(5,13):SPACE(70)
  PRINT @(5,14):SPACE(70)
  IF X = "N" THEN
    PROC.VALUES<3> = X
    PROCWRITE PROC.VALUES
    ACNT = COUNT(ALL.ID,AM) + (ALL.ID # "")
    FOR A = 1 TO ACNT
      ID.MCD = ALL.ID<A>
      MATREADU MCD.REC FROM TEMP.MCD, CONO : ID.MCD ELSE MAT MCD.REC = ""
      DELETE TEMP.MCD, CONO : ID.MCD
    NEXT A
*
*  Delete A/P account distribution details from TEMP.MCD
*
    AP.CNT = DCOUNT(AP.ACCT<1>,VM)
    FOR I = 1 TO AP.CNT
      MATREADU MCD.REC FROM TEMP.MCD, CONO:AP.ACCT<1,I> ELSE MAT MCD.REC = ""
      DELETE TEMP.MCD, CONO:AP.ACCT<1,I>
    NEXT I
    GOTO 3999
  END ELSE
    IF X # "Y" THEN GOTO 3001
    PROC.VALUES<3> = X
    PROCWRITE PROC.VALUES
    ACNT = COUNT(ALL.ID,AM) + (ALL.ID # "")
    FOR A = 1 TO ACNT
      ID.MCD = ALL.ID<A>
      MATREADU MCD.REC FROM TEMP.MCD, CONO : ID.MCD ELSE
        RELEASE TEMP.MCD, CONO : ID.MCD
        GOTO 3199
      END
      READU ID.REC FROM MCD, CONO : ID.MCD ELSE ID.REC = ""
      MATWRITE MCD.REC ON MCD, CONO : ID.MCD
      TAG = ''
      WRITE TAG ON MCD.TAG, CONO : ID.MCD
      DELETE TEMP.MCD, CONO : ID.MCD
3199 *
    NEXT A
*
* Set up A/P account distribution details on MCD
*
    AP.CNT = DCOUNT(AP.ACCT<1>,VM)
    FOR I = 1 TO AP.CNT
      MATREADU MCD.REC FROM TEMP.MCD, CONO:AP.ACCT<1,I> ELSE
        RELEASE TEMP.MCD, CONO:AP.ACCT<1,I>
        GOTO 3200
      END
      MATWRITE MCD.REC ON MCD, CONO:AP.ACCT<1,I>
      WRITE '' ON MCD.TAG, CONO:AP.ACCT<1,I>
      DELETE TEMP.MCD, CONO:AP.ACCT<1,I>
3200*
    NEXT I
  END
3999 *
  RETURN
*
* Distribute A/P account details            TASK 17919
*
4000 *
*
* Check if miscellaneous po
*
  IF CKR.MISC.CAT # "" THEN
    MATREAD CAMISC.REC FROM CATEGORY.MISC, CONO:CKR.MISC.CAT ELSE GO 4100
    ACCT = CAMISC.AP.ACCT
    ALL.GRS = CKR.GRS.AMT
    AP.CNT = 1
    I = 1
*T19149 v         
    CDIV='00'
    CDEPT='00'
    CCCTR='000'
*T19149 ^         
    GOSUB 5000
    GOTO 4999
  END
4100 *
  AP.CNT = COUNT(CKR.AP.ACCT,VM) + (CKR.AP.ACCT # "")
  FOR I = 1 TO AP.CNT
    CKR.AP.AMT<1,I> = 0 - CKR.AP.AMT<1,I>
  NEXT I
  CALL AMOUNT.DIST.SUB(CKR.AP.ACCT,CKR.AP.AMT,CKR.GRS.AMT,ALL.GRS,0,ALL.DSC,ERRMSG)
  FOR I = 1 TO AP.CNT
    ACCT = CKR.AP.ACCT<1,I>
*T19149 v
    CDIV=CKR.AP.DIV<1,I>
    CDEPT=CKR.AP.DEPT<1,I>
    CCCTR=CKR.AP.CCTR<1,I>
*T19149 ^         
    GOSUB 5000
  NEXT I
4999 *
  RETURN
*
* Set up TEMP.MCD for A/P details
*
5000 *
  AP.ID = "A":ACCT:MCD.CHK.DATE:MCD.CHECK
  LOCATE AP.ID IN AP.ACCT<1>,1 SETTING POS ELSE
    AP.ACCT = INSERT(AP.ACCT,1,POS,0,AP.ID)
*T25886 v
    WRITE AP.ACCT ON D.CKR, CONO:"APKEYS":DIV.OWNER
*T25886 ^
  END
  READ APREC FROM TEMP.MCD,CONO:AP.ID ELSE
    APREC = ""
    APREC<1> = MCD.CHECK
    APREC<2> = ACCT
    APREC<4> = 0
    APREC<6> = 0
    APREC<7> = MCD.CHK.DATE
    APREC<8> = MCD.VEND
    APREC<9> = MCD.MON
  END
  VCH = ID[4,6]
  LOCATE VCH IN APREC<15>,1 SETTING POS ELSE
    APREC = INSERT(APREC,15,POS,0,VCH)
    APREC = INSERT(APREC,16,POS,0,0)
    APREC = INSERT(APREC,17,POS,0,"")
    APREC = INSERT(APREC,18,POS,0,0)
*T19149 v
    APREC = INSERT(APREC,10,POS,0,CDIV) ;* DIVISION
    APREC = INSERT(APREC,11,POS,0,CDEPT) ;* DEPT
    APREC = INSERT(APREC,12,POS,0,CCCTR) ;* COST CENTER
*T19149 ^
  END
*T25535 v
  LOCATE CDIV IN APREC<10,POS>,1 SETTING DPOS ELSE
    APREC = INSERT(APREC,10,POS,DPOS,CDIV) ;* DIVISION
    APREC = INSERT(APREC,11,POS,DPOS,CDEPT) ;* DEPT
    APREC = INSERT(APREC,12,POS,DPOS,CCCTR) ;* COST CENTER
  END
  APREC<6> = APREC<6> - ALL.GRS<1,I>
  APREC<4> = APREC<4> - ALL.GRS<1,I>
* APREC<16,POS> = APREC<16,POS> - ALL.GRS<1,I>
  APREC<16,POS,DPOS> = APREC<16,POS,DPOS> - ALL.GRS<1,I>
* APREC<18,POS> = APREC<18,POS> - ALL.GRS<1,I>
  APREC<18,POS,DPOS> = APREC<18,POS,DPOS> - ALL.GRS<1,I>
*T25535 ^
  WRITE APREC ON TEMP.MCD, CONO:AP.ID
5999 *
  RETURN
*
*--- PRINT HEADING
*
8000 *
  PRINT CHAR(12)
  PAGE.NO = PAGE.NO + 1
  PRINT HD1:PAGE.NO
  PRINT HD2
  PRINT "DIVISION : ":DIV.OWNER;* T20269
  PRINT
  PRINT HD3
  PRINT HD4
* T20269 v
*     LINE.COUNT = 5
  LINE.COUNT = 6
* T20269 ^      
  RETURN
*--- CALLS FOR SYSCOM
91000 *
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 *
  ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 *
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
  PRINTER OFF
END
