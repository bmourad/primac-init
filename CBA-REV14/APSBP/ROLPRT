******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - APSBP
* PROGRAM       - ROLPRT
* BY            - JIHAD YAMOUT , C.B.A
* DATE          - 01/01/85
* DESCRIPTION   - This program print 
*ENDDOC
******************************************************
*COPY>PMC.CPYLIB>VEND
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
DIM V.VALUES(5)
EQU V.ID    TO V.VALUES(1)
EQU V.CNT   TO V.VALUES(2)
MAT V.VALUES = ""
*
**** SET FOR SYSTEM ERRMSGS
*
    SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
*
*
**** GET PROC VALUES
*
    PROCREAD PROC.VALUES ELSE
       ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC"; GOTO 93000
    END
    CONO = PROC.VALUES<1>
**** OPEN FILE
    OPEN "","VEND" TO VEND ELSE ERRMSG = "VEND FILE IS MISSING" ; GOTO 93000
    OPEN "DICT","VEND" TO D.VEND ELSE ERRMSG = "DICT VEND FILE IS MISSING" ; GOTO 93000
*
**** ENTER NUMBER OF CARD / VEND
*
100*
    PROMPT = ""
    PRINT @(5,13):"How many cards per vendor ? ":
    INPUT CD.NUM
    IF CD.NUM = "END" THEN GOTO 99999
    IF CD.NUM = "" THEN GOTO 100
    IF CD.NUM > 9 THEN GOTO 100
    IF NOT(NUM(CD.NUM)) THEN GOTO 100
*
*** READ VALUES FROM VEND DICT FILE
*
    MATREADU V.VALUES FROM D.VEND , CONO:"ROLODEX" ELSE
       ERRMSG = "ROLODEX RECORD IS MISSING FROM DICT VEND" ; GOTO 93000
    END
*
**** MAIN PROCESS
*
    PRINTER ON
    FOR XXX = 1 TO V.CNT
       MATREAD VEND.REC FROM VEND, V.ID<1,XXX> ELSE GOTO 111
       GOSUB 1000
111 NEXT XXX
    PRINTER OFF
    PRINTER CLOSE
    PRINT @(5,13):"WARNING, If answered (Y) the pointers will be cleared!":
    PRINT @(5,15):"Did the rolodex cards print properly":
120*
    PRINT @(5,16):"Do you have all copies you need (Y/N)":
    INPUT ANS:
    IF ANS = "Y" THEN
       MAT V.VALUES = ""
       MATWRITE V.VALUES ON D.VEND, CONO:"ROLODEX"
    END ELSE
       IF ANS # "" AND ANS # "N" THEN GOTO 120
       RELEASE D.VEND , CONO:"ROLODEX"
    END
    GOTO 99999
*
**** PRINT CARD
*
1000*
    FOR XX=1 TO CD.NUM
       PRINT "*"
       PRINT
       PRINT V.ID<1,XXX>[1,3]:" ":V.ID<1,XXX>[4,8]
       PRINT VEND.DESC "L#40"
       PRINT VEND.ADDR1 "L#40"
       IF VEND.ADDR2 # "" THEN PRINT VEND.ADDR2 "L#40"
       PRINT VEND.CT.ST:" ":VEND.ZIP
       IF VEND.ADDR2 = "" THEN PRINT
       PRINT
       PRINT VEND.PHONE:SPACE(3):VEND.CONTACT
       FOR TT=1 TO 4;PRINT;NEXT TT
    NEXT XX
    RETURN
*
**** CALLS FOR SYSCOM
*
91000ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
    RETURN
92000ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
    RETURN
93000ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999*
    PRINTER OFF
 END
