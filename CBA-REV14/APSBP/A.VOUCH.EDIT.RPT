*********************************************************************
* REVISION    - [10.2]
* Copyright 1996 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - VOUCH.EDIT.RPT
* DATE        - 01/31/96
* BY          - RICK BROWN (VERCOM)
* DESCRIPTION - This program prints the voucher edit listing.
*T23278 markt 12/18/1998 * Add check for divisional security. Make
*                          fiscal data multi-value by division.
*T26493 cmykleb 03/29/2002 * Change pgm to get the rpt # from the proc
*                            and use GET.PROG.HEAD for the heading.
*ENDDOC
*********************************************************************
*
* File equates
*
*COPY>APS.CPYLIB>AUTO.VOUCHERS
*COPY>PMC.CPYLIB>VEND
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
* Initialization
*
PAGE.NO = 0
FIRST.TIME = 1
TOT.GROS = 0 ; TOT.DISC = 0 ; TOT.ACCT = 0 ; TOT.AP.ACCT = 0
*
* SYSCOM setup
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
* Open files
*
EM=''
BLANK.PRINTED=0
OPEN '','AUTO.VOUCHERS' TO AUTO.VOUCHERS ELSE EM=EM:'AUTO.VOUCHERS '
OPEN '','COMPANY' TO COMPANY ELSE EM=EM:'COMPANY '
OPEN '','CONTROL' TO CONTROL ELSE EM=EM:'CONTROL ';* T23278
OPEN '','VEND' TO VEND ELSE EM=EM:'VEND '
IF EM#'' THEN
  ERRMSG=EM:'FILE(S) MISSING'
  GOTO 93000
END
*
* Company
*
PROCREAD ID ELSE
  ERRMSG = ' CANNOT EXECUTE PROCREAD'
  GOTO 93000
END
CONO = ID<1>
HD1 = ID<2>
HDG.DIV = ID<3>
***** T23278 v
DIV.CODE = HDG.DIV
READ DIVISIONS.REC FROM CONTROL, CONO :"DIVISIONS" ELSE
  ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
  GOTO 93000
END
READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
  ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
  GOTO 93000
END
*
IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
  IF DIV.CODE = "ALL" OR DIV.CODE = "00" THEN
    ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
    GOTO 93000
  END ELSE
    LOCATE DIV.CODE IN DIVISIONS.REC<1>,1 SETTING POS ELSE
      ERRMSG = "Division ":DIV.CODE:" not on Control File DIVISIONS Record"
      GOTO 93000
    END
  END
END ELSE
  POS = 1
END
***** T23278 ^
PRINTER ON
*T26493 v
*  HD1 = HD1:"'L'"
*  HD1 = HD1:"DIVISION : ":HDG.DIV:"'L'"
*  HD1 = HD1:"VOU NO  VENDOR ID/NAME      PO NUMBER  INVOICE NO INV DATE    GROSS AMT DISCOUNT AMT     DIST AMT ACCT CODE DV DP CTR     A/P AMT'L'"
*  HD1 = HD1:"                                                  DUE DATE'L'"
*  HD1 = HD1:"------ -------------------- ---------- ---------- -------- ------------ ------------ ------------ --------- -- -- --- ------------'L'"
*  HEADING HD1
CONO.NAME = ""
REPORT.NAME = ""
REPORT.NUMBER = ID<2>
HD1 = "" ; HD2 = ""
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HD1,HD2)
HD1 = HD1: " 'PL'"
HD3 = "DIVISION : ":HDG.DIV:" 'L'"
HD4 = "                                                  INV DATE'L'"
HD5 = "VOU NO  VENDOR ID/NAME      PO NUMBER  INVOICE NO DUE DATE    GROSS AMT DISCOUNT AMT     DIST AMT ACCT CODE DV DP CTR     A/P AMT'L'"
HD6 = "------ -------------------- ---------- ---------- -------- ------------ ------------ ------------ --------- -- -- --- ------------'L'"
HEADING HD1:HD2:HD3:HD4:HD5:HD6
*T26493 ^
*
100*
*
* Heading lines
*
DATA.UL=SPACE(59):'------------ ------------ ------------':SPACE(21):'------------'
GTOT.UL=SPACE(59):'============ ============ ============':SPACE(21):'============'
*
* Process file
*
DATA = 0
LOOP
  READNEXT KEY ELSE DATA = 1
WHILE DATA = 0 DO
  MATREAD AVO.REC FROM AUTO.VOUCHERS,KEY THEN
    GOSUB 1000
  END
REPEAT
*
* End of processing
*
GOSUB 81000
PRINTER OFF
*
STOP
*   
*************************************************************************   
*
* Process record
*
1000*
MATREAD VEND.REC FROM VEND, CONO:AVO.VEND<1,1> ELSE 
  ERRMSG='VENDOR ':AVO.VEND:' IS NOT ON FILE'
  GOTO 93000
END
BLANK.PRINTED=0
VOU.TOT.ACCT=0;VOU.TOT.AP.ACCT=0
VOU.NO = KEY[4,99]
   * print line one
PRINT VOU.NO "L#6":" ":AVO.VEND<1,1> "L#9"
PRINT SPACE(7):VEND.DESC[1,20] "L#21":AVO.PO "L#11":AVO.INV "L#11":
PRINT OCONV(AVO.INV.DATE,"D2/") "L#9":
PRINT OCONV(AVO.GRS.AMT,"MD2") "R#12":" ":
PRINT OCONV(AVO.DSC.AMT,"MD2") "R#12":" ":
PRINT OCONV(AVO.DIST.AMT<1,1>,"MD2")"R#12":" ":
PRINT AVO.ACCT<1,1>"L#9":" ":
PRINT AVO.DIV<1,1> "L#3":AVO.DEPT<1,1> "L#3":AVO.CCTR<1,1> "L#4":
PRINT SPACE(12):" " 
*  PRINT OCONV(AVO.PRT.DATE,"D2/") "R#13"
TOT.ACCT = TOT.ACCT + AVO.DIST.AMT<1,1>
VOU.TOT.ACCT = VOU.TOT.ACCT + AVO.DIST.AMT<1,1>
*   print line 2   
PRINT SPACE(50):OCONV(AVO.DUE.DATE,"D2/") "L#9":SPACE(26):
ACCT.PTR=2
AP.PTR = 1
IF AVO.DIST.AMT<1,2> # "" THEN
  PRINT OCONV(AVO.DIST.AMT<1,2>,"MD2")"R#12":" ":AVO.ACCT<1,2>"L#9":" ":
  PRINT AVO.DIV<1,2> "L#3":AVO.DEPT<1,2> "L#3":AVO.CCTR<1,2> "L#4":
  PRINT SPACE(12):" "
*    PRINT AVO.STATUS[1,13]"L#13"
  ACCT.PTR=3
  TOT.ACCT = TOT.ACCT + AVO.DIST.AMT<1,2>
  VOU.TOT.ACCT = VOU.TOT.ACCT + AVO.DIST.AMT<1,2>
END ELSE
  PRINT """L#12":" ":AVO.AP.ACCT<1,1>"L#9":" ":
  PRINT AVO.AP.DIV<1,1>"L#3":AVO.AP.DEPT<1,1>"L#3":AVO.AP.CCTR<1,1>"L#4":
  PRINT OCONV(AVO.AP.AMT<1,1>,"MD2")"R#12":" "
*    PRINT AVO.STATUS[1,13]"L#13"
  AP.PTR=2
  TOT.AP.ACCT = TOT.AP.ACCT + AVO.AP.AMT<1,1>
  VOU.TOT.AP.ACCT = VOU.TOT.AP.ACCT + AVO.AP.AMT<1,1>
END
NUM.LN = DCOUNT(AVO.DIST.AMT,@VM)
FOR II = ACCT.PTR TO NUM.LN
  PRINT SPACE(73):
  PRINT OCONV(AVO.DIST.AMT<1,II>,"MD2")"R#12":" ":AVO.ACCT<1,II>"L#9":" ":
  PRINT AVO.DIV<1,II> "L#3":AVO.DEPT<1,II> "L#3":AVO.CCTR<1,II> "L#4":
  IF II = 2 THEN
*      PRINT AVO.STATUS[1,13]"L#13"
    PRINT
  END ELSE
    PRINT
  END
  TOT.ACCT = TOT.ACCT + AVO.DIST.AMT<1,II>
  VOU.TOT.ACCT = VOU.TOT.ACCT + AVO.DIST.AMT<1,II>
NEXT II
NUM.AP.LN = DCOUNT(AVO.AP.AMT,@VM)
FOR II = AP.PTR TO NUM.AP.LN
  PRINT SPACE(73):
  PRINT """R#12":" ":AVO.AP.ACCT<1,II>"L#9":" ":
  PRINT AVO.AP.DIV<1,II> "L#3":AVO.AP.DEPT<1,II> "L#3":AVO.AP.CCTR<1,II> "L#4":
  PRINT OCONV(AVO.AP.AMT<1,II>,"MD2")"R#12":
  IF II = 1 AND NUM.LN = 1 THEN
*      PRINT AVO.STATUS[1,13]"L#13"
    PRINT
  END ELSE
    PRINT
  END
  TOT.AP.ACCT = TOT.AP.ACCT + AVO.AP.AMT<1,II>
  VOU.TOT.AP.ACCT = VOU.TOT.AP.ACCT + AVO.AP.AMT<1,II>
NEXT II
TOT.GROS = TOT.GROS + AVO.GRS.AMT + 0
TOT.DISC = TOT.DISC + AVO.DSC.AMT + 0
DB.CT = COUNT(AVO.ACCT,VM) + (AVO.ACCT#"")
*
* Print voucher totals
*
IF NUM.AP.LN > 1 OR NUM.LN > 1 THEN
  PRINT DATA.UL
  PRINT SPACE(47):OCONV(AVO.GRS.AMT,"MD2") "R#12":" ":
  PRINT OCONV(AVO.DSC.AMT,"MD2") "R#12":" ":
  PRINT OCONV(VOU.TOT.ACCT,"MD2") "R#12":SPACE(21):
  PRINT OCONV(VOU.TOT.AP.ACCT,"MD2")"R#12"
  PRINT
  BLANK.PRINTED=1
END ELSE
  PRINT
  BLANK.PRINTED=1
END
RETURN
*
* Print grand totals
*
81000*
IF NOT(BLANK.PRINTED) THEN
  PRINT
END
PRINT GTOT.UL
PRINT ' *** GRAND TOTALS *** ' "L#59":OCONV(TOT.GROS,"MD2") "R#12":" ":
PRINT OCONV(TOT.DISC,"MD2") "R#12":" ":OCONV(TOT.ACCT,"MD2")"R#12":
PRINT SPACE(21):OCONV(TOT.AP.ACCT,"MD2") "R#12"
RETURN
*
* SYSCOM calls
*
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
999999*
PRINTER OFF
END
