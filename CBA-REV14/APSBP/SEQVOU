******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - SEQVON
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 06/04/86
* DESCRIPTION - This program list sequentialy list all vouchers in the
*                 (SQV) file.
*ENDDOC
******************************************************
*COPY>APS.CPYLIB>SQV
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** INITIALZATION
*
     LINE.COUNT = 99 ; PAGE.NO = 0
     TOT.AMT = 0 ; GTOT.AMT = 0
     VTOT.AMT = 0
     SP1 = " "
*
**** GET PROC VALUES
*
     PROCREAD PROC.VALUES ELSE
        ERRMSG = "THIS REPORT MUST RUN FROM A PROC" ; GOTO 93000
     END
     CONO = PROC.VALUES<1>
     CONO.NAME = PROC.VALUES<2>
     START.PER = PROC.VALUES<3>
     END.PER = PROC.VALUES<4>
     IF PROC.VALUES<5> = "V" THEN
         HD7 = SPACE(61):"BY VENDOR"
     END ELSE
         HD7 = SPACE(61):"BY VOUCHER"
     END
*
**** OPEN FILES
*
     OPEN "VEND" TO VEND ELSE ERRMSG = "VEND FILE IS MISSING";GOTO 93000
     OPEN "SQV" TO SQV ELSE ERRMSG = "SQV FILE IS MISSING";GOTO 93000
     OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING";GOTO 93000
     MATREAD COMP.REC FROM COMPANY , CONO ELSE ERRMSG = "CANNOT READ COMPANY RECORD" ; GOTO 93000
*
**** SETUP HEADING
*
     REPORT.NAME = "SEQUENTIAL VOUCHER LISTING"
     REPORT.NUMBER = "XXXX"
     REPORT.DATE = DATE()
     HD1 = "" ; HD2 = ""
     CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
     HD3 = "VOUCH# VENDOR #           VENDOR NAME          INVOICE NO INV DATE DUE DATE DISCOUNT    G/L ACCOUNT  DV DP CTR DIST AMOUNT  ASSET ID" 
     HD4 = "------ -------- ------------------------------ ---------- -------- -------- --------- -------------- -- -- --- ------------ --------"
     HD5 = "                                                                                                               ------------"
     HD6 = "                                                                                                               ============"
     PRINTER ON
1*
    READNEXT ID ELSE GOTO 99999
    MATREAD SQV.REC FROM SQV, ID ELSE GOTO 1
    PREV.VEND = SQV.VEND
    GOSUB 1000
*
**** START PROCESSING
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
      MATREAD SQV.REC FROM SQV, ID ELSE GOTO 111
      GOSUB 1000
111 REPEAT
    IF PROC.VALUES<5> = "V" THEN
       GOSUB 4000
    END
    GOSUB 8000
    GOTO 99999
*
**** PRINT LINES
*
1000*
    IF PREV.VEND # SQV.VEND AND PROC.VALUES<5> = "V" THEN 
       GOSUB 4000
       PREV.VEND = SQV.VEND
    END
    IF LINE.COUNT > 55 THEN
       GOSUB 9000
    END
    TOT.AMT = 0 ; PRT.FLG = 1
    FOR DA = 1 TO SQV.CTR
      IF LINE.COUNT > 55 THEN
         PRT.FLG = 1
         GOSUB 9000
      END
      P.LINE = ""
      IF PRT.FLG = 1 THEN
         GOSUB 2000
         PRT.FLG = 0
      END ELSE
         P.LINE = P.LINE :SPACE(86)
      END
      P.LINE = P.LINE :SQV.ACCT<1,DA> CO.ACCT.MASK:SP1
      P.LINE = P.LINE :SQV.DIV<1,DA> "R#2":SP1
      P.LINE = P.LINE :SQV.DEPT<1,DA> "R#2":SP1
      P.LINE = P.LINE :SQV.CCTR<1,DA> "R#3":SP1
      P.LINE = P.LINE :OCONV(SQV.DIST.AMT<1,DA>, "MD2") "R#12":SP1
      P.LINE = P.LINE :SQV.ASSET<1,DA> "L#8"
      TOT.AMT = TOT.AMT + SQV.DIST.AMT<1,DA>
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT + 1
    NEXT DA
    IF LINE.COUNT + 3 > 55 THEN
       GOSUB 9000
    END
    PRINT HD5
    T.LINE = ""
    T.LINE = T.LINE:SPACE(96):"VOUCHER TOTAL  "
    T.LINE = T.LINE:OCONV(TOT.AMT , "MD2") "R#12"
    PRINT T.LINE
    PRINT
    LINE.COUNT = LINE.COUNT + 3
    VTOT.AMT = VTOT.AMT + TOT.AMT
    GTOT.AMT = GTOT.AMT + TOT.AMT
    RETURN
*
**** SET COMMOM LINE VALUES
*
2000*
    P.LINE = P.LINE :ID[4,6] "L#6":SP1
    P.LINE = P.LINE :SQV.VEND<1,1> "L#8":SP1
    IF SQV.VEND<1,2> # "" THEN
       P.LINE = P.LINE :SQV.VEND<1,2> "L#30":SP1
    END ELSE
       MATREAD VEND.REC FROM VEND , CONO:SQV.VEND<1,1> ELSE VEND.DESC = "NOT ON FILE"
       P.LINE = P.LINE :VEND.DESC "L#30":SP1
    END
    P.LINE = P.LINE :SQV.INV "L#10":SP1
    P.LINE = P.LINE :OCONV(SQV.INV.DATE, "D2/") "R#8":SP1
    P.LINE = P.LINE :OCONV(SQV.DUE.DATE, "D2/") "R#8":SP1
    P.LINE = P.LINE :OCONV(SQV.DSC.AMT, "MD2") "R#9":SP1
    RETURN
*
**** PRINT VEND TOTAL
*
4000*
     IF LINE.COUNT + 3 > 55 THEN
        GOSUB 9000
     END
    PRINT HD5
    T.LINE = ""
    T.LINE = T.LINE:SPACE(96):"VENDOR TOTAL   "
    T.LINE = T.LINE:OCONV(VTOT.AMT , "MD2") "R#12"
    PRINT T.LINE
    PRINT
    LINE.COUNT = LINE.COUNT + 3
    VTOT.AMT = 0
    RETURN
*
**** PRINT GRAND TOTAL
*
8000*
     IF LINE.COUNT + 2 > 55 THEN
        GOSUB 9000
     END
    PRINT HD6
    T.LINE = ""
    T.LINE = T.LINE:SPACE(96):"GRAND TOTAL    "
    T.LINE = T.LINE:OCONV(GTOT.AMT , "MD2") "R#12"
    PRINT T.LINE
    RETURN
*
**** PRINT HEADING
*
9000*
     PRINT CHAR(12)
     PAGE.NO = PAGE.NO + 1
     PRINT HD1:PAGE.NO
     PRINT HD2
     PRINT HD7
     PRINT SPACE(50):"FROM PERIOD : ":START.PER:" TO : ":END.PER
     PRINT
     PRINT HD3
     PRINT HD4
     LINE.COUNT = 7
     RETURN
*
**** CALLS FOR SYSCOM
91000ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
92000ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999*
     PRINTER OFF
     END
