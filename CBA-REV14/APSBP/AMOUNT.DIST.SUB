   SUBROUTINE AMOUNT.DIST.SUB(ALL.DIV,ALL.AMOUNT,DIST.NET,ALL.NET,DIST.DSC,ALL.DSC,ACTION)
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - AMOUNT.DIST.SUB
* BY          - WALID YAMOUT, CBA
* DATE        - 06/23/86
* DESCRIPTION -
*               This program get ALL.DIV, ALL.AMOUNT, DIST.NET and
*               DIST.DSC. Then returns ALL.NET and ALL.DSC.
*ENDDOC
*********************************************************************
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*
*--- SETUP SYSTEM ERROR MESSAGES
*
   ACTION = 0
   ERRMSG = ""
*
*--- INITIALIZATION
*
   ALL.NET = ""
   ALL.DSC = ""
   TOTAL.AMOUNT = 0
*
*--- MAIN PROCESS
*
100*
   LINES = COUNT(ALL.DIV,VM) + (ALL.DIV # "")
   IF LINES < 1 THEN GOTO 999999
   FOR LN = 1 TO LINES
      TOTAL.AMOUNT = TOTAL.AMOUNT + ALL.AMOUNT<1,LN>
   NEXT LN
   IF TOTAL.AMOUNT = 0 THEN
      FOR LN = 1 TO LINES
         ALL.NET<1,LN> = 0
         ALL.DSC<1,LN> = 0
      NEXT LN
      GOTO 999999
   END
   IF DIST.NET + 0 = 0 THEN
      FOR LN = 1 TO LINES
         ALL.NET<1,LN> = 0
      NEXT LN
   END ELSE
      DIST.AMOUNT = DIST.NET
      GOSUB 1000
      ALL.NET = ALL.AMT
   END
   IF DIST.DSC + 0 = 0 THEN
      FOR LN = 1 TO LINES
         ALL.DSC<1,LN> = 0
      NEXT LN
   END ELSE
      DIST.AMOUNT = DIST.DSC
      GOSUB 1000
      ALL.DSC = ALL.AMT
   END
   GOTO 999999
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- DISTRIBUTION
*
1000*
   ALL.AMT = ""
   IF DIST.AMOUNT = TOTAL.AMOUNT THEN
      FOR LN = 1 TO LINES
         ALL.AMT<1,LN> = ALL.AMOUNT<1,LN>
      NEXT LN
      GOTO 1999
   END
   TOTAL.DIST = 0
   TEMP = DIST.AMOUNT / (TOTAL.AMOUNT / 1000)
   FOR LN = 1 TO LINES
      IF ALL.AMOUNT<1,LN> < 0 THEN RFAC = '-.0005' ELSE RFAC = '.0005'
      ALL.AMT<1,LN> = INT((ALL.AMOUNT<1,LN> / 1000 + RFAC) * TEMP)
      TOTAL.DIST = TOTAL.DIST + ALL.AMT<1,LN>
   NEXT LN
   IF TOTAL.DIST <> DIST.AMOUNT THEN
      PTR = 1
      FOR LN = 1 TO LINES
         IF ABS(ALL.AMT<1,LN>) >= ABS(ALL.AMT<1,PTR>) THEN PTR = LN
      NEXT LN
      ALL.AMT<1,PTR> = ALL.AMT<1,PTR> + DIST.AMOUNT - TOTAL.DIST
   END
1999*
RETURN
*
*--- CALLS FOR SYSCOM
*
91000 *
      ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
      ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
      ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
999999*
  RETURN
END
