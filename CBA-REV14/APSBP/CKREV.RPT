******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - APSBP
* PROGRAM       - CHREV.RPT
* BY            - JIHAD YAMOUT , C.B.A
* DATE          - 06/04/86
* DESCRIPTION   - This program prints a check reversal report
*T23826 markt 04/05/1999 * Add PRINTER CLOSE stmt to force report to
*                          print before PROC "satisfactory" prompt.
*T26493 cmykleb 04/02/2002 * Change pgm to get rpt # from proc.
*ENDDOC
******************************************************
*COPY>APS.CPYLIB>CKREV
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** INITIALZATION
*
  LINE.COUNT = 99 ; PAGE.NO = 0
  TOT.AMT = 0 ; GTOT.AMT = 0
  SP1 = " "
*
**** GET PROC VALUES
*
  PROCREAD PROC.VALUES ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC" ; GOTO 93000
  END
  CONO = PROC.VALUES<1>
  CONO.NAME = PROC.VALUES<2>
*
**** OPEN FILES
*
  OPEN "VEND" TO VEND ELSE ERRMSG = "VEND FILE IS MISSING";GOTO 93000
  OPEN "CKREV" TO CKREV ELSE ERRMSG = "CKREV FILE IS MISSING";GOTO 93000
  OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING";GOTO 93000
  MATREAD COMP.REC FROM COMPANY , CONO ELSE ERRMSG = "CANNOT READ COMPANY RECORD" ; GOTO 93000
*
**** SETUP HEADING
*
*T26493 v
* REPORT.NAME = "CHECK REVERSAL REPORT"
* REPORT.NUMBER = "XXXX"
  CONO.NAME = ""
  REPORT.NAME = ""
  REPORT.NUMBER = PROC.VALUES<2>
*T26493 ^
  REPORT.DATE = DATE()
  HD1 = "" ; HD2 = ""
  CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
  HD3 = "VENDOR #            VENDOR NAME         CHECK# CHK DATE  DISB ACCOUNT   G/L ACCOUNT   DV DP CTR DIST AMOUNT  ASSET ID"
  HD4 = "-------- ------------------------------ ------ -------- -------------- -------------- -- -- --- ------------ --------"
  HD5 = "                                                                                                ------------"
  HD6 = "                                                                                                ============"
  PRINTER ON
*
**** START PROCESSING
*
  DATA = 1
  LOOP
      READNEXT ID ELSE DATA = 0
  WHILE DATA = 1 DO
      MATREAD CKRV.REC FROM CKREV, ID ELSE GOTO 111
      GOSUB 1000
111 REPEAT
  GOSUB 8000
  GOTO 99999
*
**** PRINT LINES
*
1000*
  IF LINE.COUNT > 55 THEN
      GOSUB 9000
  END
  TOT.AMT = 0 ; PRT.FLG = 1
  FOR DA = 1 TO CKRV.CTR
      IF LINE.COUNT > 55 THEN
          PRT.FLG = 1
          GOSUB 9000
      END
      P.LINE = ""
      IF PRT.FLG = 1 THEN
          GOSUB 2000
          PRT.FLG = 0
      END ELSE
          P.LINE = P.LINE :SPACE(71)
      END
      P.LINE = P.LINE :CKRV.GL.ACCT<1,DA> CO.ACCT.MASK:SP1
      P.LINE = P.LINE :CKRV.DIV<1,DA> "R#2":SP1
      P.LINE = P.LINE :CKRV.DEPT<1,DA> "R#2":SP1
      P.LINE = P.LINE :CKRV.CCTR<1,DA> "R#3":SP1
      P.LINE = P.LINE :OCONV(CKRV.DIST.AMT<1,DA>, "MD2") "R#12":SP1
      P.LINE = P.LINE :CKRV.ASSET.ID<1,DA> "L#8"
      TOT.AMT = TOT.AMT + CKRV.DIST.AMT<1,DA>
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT + 1
  NEXT DA
  IF LINE.COUNT + 3 > 55 THEN
      GOSUB 9000
  END
  PRINT HD5
  T.LINE = ""
  T.LINE = T.LINE:SPACE(96)
  T.LINE = T.LINE:OCONV(TOT.AMT , "MD2") "R#12"
  PRINT T.LINE
  PRINT
  LINE.COUNT = LINE.COUNT + 3
  GTOT.AMT = GTOT.AMT + TOT.AMT
  RETURN
*
**** SET COMMOM LINE VALUES
*
2000*
  P.LINE = P.LINE :CKRV.VEND "L#8":SP1
  P.LINE = P.LINE :CKRV.VEND.NA "L#30":SP1
  P.LINE = P.LINE :CKRV.CHK.NO "R#6":SP1
  P.LINE = P.LINE :OCONV(CKRV.CHK.DATE, "D2/") "R#8":SP1
  P.LINE = P.LINE :CKRV.DISB.ACCT CO.ACCT.MASK :SP1
  RETURN
*
**** PRINT GRAND TOTAL
*
8000*
  IF LINE.COUNT + 2 > 55 THEN
      GOSUB 9000
  END
  PRINT HD6
  T.LINE = ""
  T.LINE = T.LINE:SPACE(96)
  T.LINE = T.LINE:OCONV(GTOT.AMT , "MD2") "R#12"
  PRINT T.LINE
  RETURN
*
**** PRINT HEADING
*
9000*
  PRINT CHAR(12)
  PAGE.NO = PAGE.NO + 1
  PRINT HD1:PAGE.NO
  PRINT HD2
  PRINT
  PRINT HD3
  PRINT HD4
  LINE.COUNT = 5
  RETURN
*
**** CALLS FOR SYSCOM
91000ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999*
  PRINTER CLOSE; * T23826
  PRINTER OFF
END
