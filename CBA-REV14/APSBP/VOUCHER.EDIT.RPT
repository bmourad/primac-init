*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - VOUCHER.EDIT.RPT
* DATE        - 21 MAY 91
* BY          - BERNIE PARKIN (PACRIM SOFTWARE PTY LTD)
* DESCRIPTION - This program prints the voucher summary edit listing.
*ENDDOC
*********************************************************************
*
* File equates
*
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
* Initialization
*
   PAGE.NO = 0
   FIRST.TIME = 1
   TOT.GROS = 0 ; TOT.DISC = 0 ; TOT.ACCT = 0
*
* SYSCOM setup
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
* Open files
*
   EM=''
   OPEN '','TEMP.VOUCHERS' TO TEMP.VOUCHERS ELSE EM=EM:'TEMP.VOUCHERS '
   OPEN '','COMPANY' TO COMPANY ELSE EM=EM:'COMPANY '
   IF EM#'' THEN
      ERRMSG=EM:'FILE(S) MISSING'
      GOTO 93000
   END
*
* Company
*
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT EXECUTE PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
   HD1 = ID<2>
   PRINTER ON
   HEADING HD1:"'LL'VOU NO  VENDOR  PO NUMBER  INVOICE NO INV DATE DUE DATE GROSS AMT  DISC AMT DIST ACCT DV DP CTR  DIST AMT  PRT DATE  NO POST STATUS'L'------ -------- ---------- ---------- -------- -------- ---------- -------- ---------- -- --- ---------- -------- --------------'L'"
*
100*
*
* Heading lines
*
   DATA.UL=SPACE(56):'---------- --------':SPACE(21):'----------'
   GTOT.UL=SPACE(56):'========== ========':SPACE(21):'=========='
*
* Process file
*
   DATA = 0
LOOP
   READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      MATREAD TVO.REC FROM TEMP.VOUCHERS,KEY ELSE
         MAT TVO.REC = ''
         GOTO 111
      END
      GOSUB 1000
111*
   REPEAT
*
* End of processing
*
   GOSUB 81000
   GOTO 999999
*
* Process record
*
1000*
   VOU.NO = KEY[4,99]
   PRINT VOU.NO "L#7":TVO.VEND<1,1> "L#9":TVO.PO "L#11":TVO.INV "L#11":
   PRINT OCONV(TVO.INV.DATE,"D2/") "L#9":OCONV(TVO.DUE.DATE,"D2/") "L#9":
   PRINT OCONV(TVO.GRS.AMT,"MD2") "R#10":OCONV(TVO.DSC.AMT,"MD2") "R#9":
   PRINT ' ':TVO.ACCT<1,1> "L#10":TVO.DIV<1,1> "L#3":TVO.DEPT<1,1> "L#3":TVO.CCTR<1,1> "L#4":
   PRINT OCONV(TVO.DIST.AMT<1,1>,"MD2") "R#10":OCONV(TVO.PRT.DATE,"D2/") "R#9":' ':TVO.STATUS[1,14] "L#14"
   TOT.GROS = TOT.GROS + TVO.GRS.AMT + 0
   TOT.DISC = TOT.DISC + TVO.DSC.AMT + 0
   VOU.ACCT = TVO.DIST.AMT<1,1> + 0
   TOT.ACCT = TOT.ACCT + TVO.DIST.AMT<1,1> + 0
   DB.CT = COUNT(TVO.ACCT,VM) + (TVO.ACCT#"")
   FOR DD=2 TO DB.CT
      PRINT SPACE(76):TVO.ACCT<1,DD> "L#10":TVO.DIV<1,DD> "L#3":TVO.DEPT<1,DD> "L#3":TVO.CCTR<1,DD> "L#4":OCONV(TVO.DIST.AMT<1,DD>,"MD2") "R#10"
      VOU.ACCT = VOU.ACCT + TVO.DIST.AMT<1,DD> + 0
      TOT.ACCT = TOT.ACCT + TVO.DIST.AMT<1,DD> + 0
   NEXT DD
   CR.CT = COUNT(TVO.AP.ACCT,VM) + (TVO.AP.ACCT#"")
   FOR CC=1 TO CR.CT
      PRINT SPACE(76):TVO.AP.ACCT<1,CC> "L#10":"00 00 000 ":OCONV(TVO.AP.AMT<1,CC>,"MD2") "R#10"
      VOU.ACCT = VOU.ACCT + TVO.AP.AMT<1,CC> + 0
      TOT.ACCT = TOT.ACCT + TVO.AP.AMT<1,CC> + 0
   NEXT CC
*
* Print voucher totals
*
   PRINT DATA.UL
   PRINT SPACE(56):OCONV(TVO.GRS.AMT,"MD2") "R#10":OCONV(TVO.DSC.AMT,"MD2") "R#9":SPACE(21):OCONV(VOU.ACCT,"MD2") "R#10"
   PRINT
   RETURN
*
* Print grand totals
*
81000*
   PRINT GTOT.UL
   PRINT ' *** GRAND TOTALS *** ' "L#56":OCONV(TOT.GROS,"MD2") "R#10":OCONV(TOT.DISC,"MD2") "R#9":SPACE(21):OCONV(TOT.ACCT,"MD2") "R#10"
   RETURN
*
* SYSCOM calls
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINTER OFF
   END
