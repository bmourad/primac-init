  SUBROUTINE VEND.INV.INQ(VENDNO,POTYPE)
*COPY>CPYLIB>COM1  
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>POS.CPYLIB>COM.MPO  
*COPY>APS.CPYLIB>COM.APS.FILE.VARS
*COPY>APS.CPYLIB>COM.TEMP.VOUCHERS
*COPY>APS.CPYLIB>COM.VOUCHER.MAINT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* PROGRAM       - VEND.INV.INQ
* BY            - WAYNE VAUGHAN, C.B.A
* DATE          - 05/16/06
* DESCRIPTION   
*  This Program displays all Vendor Invoice Stats for a Purchase Order.
*T28819 wvaughan 03/29/06 * Add Vendor Invoice Inquiry
*********************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>APS.CPYLIB>VEND.STATS
*COPY>APS.CPYLIB>SQV
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>INVENTORY
*COPY>PMC.CPYLIB>VEND
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>APS.CPYLIB>APS.FILE.VARS
*COPY>CPYLIB>CHAR
*** INT
*
  BEGIN.PAGE = 7
  PAGE.SIZE = 13
  LINE.SPACE = 1
  LINES = 0
  OLD.START.LINE=0
  VOUCH.NOS = ""
  MAT INV.CNV.REC = ""
*
**** SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  OPEN '','SQV' TO SQV ELSE
     ERRMSG="SQV FILE IS MISSING"
     GOTO 93000
  END
  CMD = 'SSELECT VEND.VOUCH.STATS WITH CONO_VEND_POTYPE = "':CONO:VENDNO:'!':POTYPE:'"'
  PERFORM CMD CAPTURING INV.ARR
  INV.ARR = ''
  CNTR = 0
  DONE = 0
  LOOP
     READNEXT ID ELSE DONE = 1
  UNTIL DONE DO 
     MATREAD VVOS.REC FROM VEND.VOUCH.STATS, ID THEN
        VOUCH.NO = FIELD(ID,"!",2)
        MATREAD SQV.REC FROM SQV, CONO:VOUCH.NO ELSE MAT SQV.REC= ""
        CKCNT = DCOUNT(SQV.CHK.NO, VM)
        NO.POS = DCOUNT(VVOS.PO.NO,VM)
        FOR I=1 TO NO.POS
           IF I > 1 AND VVOS.PO.NO<1,I> = INV.ARR<4,CNTR> THEN
              INV.ARR<6,CNTR> += VVOS.PROD.COST<1,I>
           END ELSE
              CNTR+=1
              INV.ARR<1,CNTR>=CNTR
              INV.ARR<2,CNTR>=VVOS.INV.NO<1,I>
              INV.ARR<3,CNTR>=VVOS.INV.DATE<1,I>
              INV.ARR<4,CNTR>=VVOS.PO.NO<1,I>
              INV.ARR<5,CNTR>=VOUCH.NO
              INV.ARR<6,CNTR>=VVOS.PROD.COST<1,I>
              INV.ARR<7,CNTR>=SQV.CHK.NO<1,CKCNT>
              INV.ARR<8,CNTR>=SQV.PAID.AMT<1,CKCNT>
              INV.ARR<9,CNTR>=SQV.PAID.DATE<1,CKCNT>
           END
        NEXT  I
     END
  REPEAT
  LINES = DCOUNT(INV.ARR<1>,VM)  
  LN = 1
  IF LINES > 0 THEN
     GOSUB 10000
  END
  MORE = 1
  LOOP
     ECD.NUM = 11
     SCV.REC(3)<ECD.SCRN.NO> = '' ; ECD.ACTION = 4 ; CALL SCRN.EDIT
     OPTION = ECD.RET.VALUE
     BEGIN CASE
        CASE OPTION = 'E' OR OPTION = 'END' OR OPTION = ''
           MORE = 0
        CASE OPTION = 'S' OR OPTION = 'SF'
           LN = LN + PAGE.SIZE
           IF LN > LINES THEN LN = 1
           GOSUB 10000
        CASE OPTION = 'SR'
           LN= LN - PAGE.SIZE
           IF LN < 1 THEN LN = 1
           GOSUB 10000
        CASE OPTION = 'ST'
           LN = 1
           GOSUB 10000
        CASE OPTION = 'SB'
           LN = LINES
           GOSUB 10000
        CASE OPTION = ""
           GOSUB 91000
        CASE OPTION[1,1] = "I"
           LNX = LN
           LOCATE OPTION[2,15] IN INV.ARR<2> SETTING LN ELSE
              LN = LNX
              ERRMSG = "INVOICE NUMBER NOT FOUND"
              GOSUB 91000
           END
           GOSUB 10000
     END CASE
  WHILE MORE DO
  REPEAT
  GOTO 99999
*
***** SCROLL ROUTINE
*
10000*
  START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE=OLD.START.LINE THEN GOTO 10001
  OLD.START.LINE=START.LINE
  CNT=1
  FOR N=START.LINE TO LAST.LINE
     SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
     *MATREAD VVOS.REC FROM VEND.VOUCH.STATS, CONO:TVO.VEND<1,1>:"!":VOUCH.NOS<1,N> ELSE
        *MAT VVOS.REC = ""
     *END
     P_X  = 1 ; P_Y = SLN ; P_VALUE = N "L#3" ; P_OPT = ""
     P_X  := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<2,N> "L#10"
     P_X  := AM:16 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<3,N> "D2/" "L#8"
     P_X  := AM:25 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<4,N> "R#8"
     P_X  := AM:34 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<5,N> "L#8"
     P_X  := AM:42 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<6,N> "MD2" "R#10"
     P_X  := AM:53 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<7,N> "R#6"
     P_X  := AM:60 ; P_Y := AM:SLN ; P_VALUE := AM:INV.ARR<8,N>"MD2" "R#10"
     P_X  := AM:71 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INV.ARR<9,N>,"D2/") "R#8"
     CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
     CNT=CNT + 1
  NEXT N
  FOR M=CNT TO PAGE.SIZE
     SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
     P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
     CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
10001*
  RETURN
*** ENTER LINE NUMBER
***** CALLS FOR SYSCOM
91000*
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
