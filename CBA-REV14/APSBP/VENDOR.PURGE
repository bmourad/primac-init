*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* PROGRAM     - VENDOR.PURGE
* BY          - CURTIS WILLIAMS, C.B.A
* DATE        - 03/03/87
* DESCRIPTION
* This Program is used to dispays all products for a vendor po .
*T23278 markt 10/28/1998 * Add check for divisional security
*T25626 lanny 02/09/2001 * Need to check correct type of PO for DIV
*                          SECURITY.
*T27646 thompson 08/19/2003 * MODIFY FOR MULTI PLATFORM ISSUE
********************************************************************
*
*----INSERT FILE EQUATES
*
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>APS.CPYLIB>VEND.STATS
*COPY>APS.CPYLIB>APS.FILE.VARS
*****T23278 v
*COPY>PMC.CPYLIB>PO
*COPY>POS.CPYLIB>OUTSIDE.PO ;*T25626
*COPY>POS.CPYLIB>MISC.PO ;*T25626
*COPY>PMC.CPYLIB>SECURITY
*****T23278 ^
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*----SETUP FOR SYSTEM ERRMSGS
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
OPEN "","VEND.PO.STATS" TO VEND.PO.STATS ELSE ERRMSG = "CANNOT OPEN VEND.PO.STATS FILE"; GOTO 93000
OPEN "","VEND.PROD.STATS" TO VEND.PROD.STATS ELSE ERRMSG = "CANNOT OPEN VEND.PROD.STATS FILE"; GOTO 93000
OPEN "","VEND.VOUCH.STATS" TO VEND.VOUCH.STATS ELSE ERRMSG = "CANNOT OPEN VEND.VOUCH.STATS FILE"; GOTO 93000
OPEN "","VEND.STATS" TO VEND.STATS ELSE ERRMSG = "CANNOT OPEN VEND.STATS FILE"; GOTO 93000
OPEN "","PO" TO  PO ELSE ERRMSG = "CANNOT OPEN PO FILE"; GOTO 93000;* T23278
OPEN "","OUTSIDE.PO" TO  OUTSIDE.PO ELSE ERRMSG = "CANNOT OPEN OUTSIDE.PO FILE"; GOTO 93000;* T25626
OPEN "","MISC.PO" TO  MISC.PO ELSE ERRMSG = "CANNOT OPEN MISC.PO FILE"; GOTO 93000;* T25626
OPEN "","SECURITY" TO SECURITY ELSE ERRMSG = "CANNOT OPEN SECURITY FILE"; GOTO 93000;* T23278
OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "CANNOT OPEN COMPANY FILE"; GOTO 93000
*
*----PROC VALUES
*
PROCREAD PROC.VALUES ELSE
   ERRMSG = "THIS REPORT MUST RUN FROM A PROC" ; GOTO 93000
END
CONO = PROC.VALUES<1>
PO.TYPE = PROC.VALUES<5>
SEL.DIV = PROC.VALUES<8> ;*T25626
MATREAD COMP.REC FROM COMPANY,CONO ELSE
   ERRMSG = "CANNOT LOCATE COMPANY NAME"
   GOTO 93000
END
*
*****T23278 v
LOGNAME = 'LOGNAME'; CALL SYSVARS.SUB(LOGNAME) ; *T27646
USER.ID = UPCASE(LOGNAME) ; *T27646
MATREAD SEC.REC FROM SECURITY, CONO:USER.ID ELSE
   ERRMSG = "SECURITY RECORD FOR ":USER.ID:" NOT FOUND"; GOTO 93000
END
DIV.FOUND = ''
*****T23278 ^
*
*--- PURGE RECORDS
*
DATA = 1
LOOP
   READNEXT ID ELSE DATA = 0
WHILE DATA DO
   IF CONO # ID[1,3] THEN GOTO 200
   MATREADU VPS.REC FROM VEND.PO.STATS, ID ELSE
      RELEASE VEND.PO.STATS, ID; GOTO 200
   END
   VEND.NUM = FIELD(ID,"!",1)[4,99]
   PO.NUM = FIELD(ID,"!",3)
***** T23278 v  25626 v
   IF SEL.DIV # 'ALL' THEN
     PODIV=''
     BEGIN CASE
        CASE PO.TYPE = 'R'
           MATREAD PO.REC FROM PO, CONO:PO.NUM ELSE GOTO 200
           PODIV = PO.DIV.OWNER
        CASE PO.TYPE = 'O'
           MATREAD OPO.REC FROM OUTSIDE.PO, CONO:PO.NUM ELSE GOTO 200
           PODIV = OPO.DIV.OWNER
        CASE PO.TYPE = 'M'
           MATREAD MPO.REC FROM MISC.PO, CONO:PO.NUM ELSE GOTO 200
           PODIV = MPO.DIV.OWNER
     END CASE
     ERRMSG=''
     CALL CK.DIV.SEC.SUB(CONO,PODIV,USER.ID,ERRMSG)
     IF ERRMSG # '' THEN GOTO 200
   END
***** T23278 ^  25626 ^
   MATREADU VSTAT.REC FROM VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE ELSE MAT VSTAT.REC = ""
   IF PO.TYPE = "M" THEN
      PD = COUNT(VPS.PROD.DESC,VM) + (VPS.PROD.DESC # "")
   END ELSE
      PD = COUNT(VPS.PROD,VM) + (VPS.PROD # "")
   END
   PL = 0
   LOCATE PO.NUM IN VSTAT.PO<1>,1 SETTING PL ELSE PL = 0
   FOR DL = PD TO 1 STEP -1
      IF VPS.FLAG<1,DL> = "Y" THEN
         IF PO.TYPE # "M" THEN
            DELETE VEND.PROD.STATS, ID:"!":VPS.PROD<1,DL>:"!":VPS.WHSE<1,DL>
         END ELSE
            DELETE VEND.PROD.STATS, ID:"!":VPS.SEQ.NO<1,DL>:"!":VPS.WHSE<1,DL>
         END
         IF PL # 0 THEN
            VSTAT.PO.ORDERD<1,PL> = VSTAT.PO.ORDERD<1,PL> - VPS.ORD.AMT<1,DL>
            VSTAT.PO.RECV<1,PL> = VSTAT.PO.RECV<1,PL> - VPS.REC.AMT<1,DL>
            VSTAT.PO.VOUCH<1,PL> = VSTAT.PO.VOUCH<1,PL> - VPS.VOU.AMT<1,DL>
         END
         VPS.PROD = DELETE(VPS.PROD,1,DL,0)
         VPS.PROD.DESC = DELETE(VPS.PROD.DESC,1,DL,0)
         VPS.U.M = DELETE(VPS.U.M,1,DL,0)
         VPS.WHSE = DELETE(VPS.WHSE,1,DL,0)
         VPS.ORD.QTY = DELETE(VPS.ORD.QTY,1,DL,0)
         VPS.ORD.AMT = DELETE(VPS.ORD.AMT,1,DL,0)
         VPS.REC.QTY = DELETE(VPS.REC.QTY,1,DL,0)
         VPS.REC.AMT = DELETE(VPS.REC.AMT,1,DL,0)
         VPS.VOU.QTY = DELETE(VPS.VOU.QTY,1,DL,0)
         VPS.VOU.AMT = DELETE(VPS.VOU.AMT,1,DL,0)
         VPS.SEQ.NO = DELETE(VPS.SEQ.NO,1,DL,0)
*             VVCNT = DCOUNT(VPS.VOU.NO<1,DL>,SM)
*             FOR V = VVCNT TO 1 STEP - 1
*               READU DUMMY FROM VEND.VOUCH.STATS, CONO:VEND.NUM:"!":VPS.VOU.NO<1,DL,V> THEN
*                 DELETE VEND.VOUCH.STATS, CONO:VEND.NUM:"!":VPS.VOU.NO<1,DL,V>
*               END ELSE
*                 RELEASE VEND.VOUCH.STATS, CONO:VEND.NUM:"!":VPS.VOU.NO<1,DL,V>
*               END
*             NEXT V
         VPS.VOU.NO = DELETE(VPS.VOU.NO,1,DL,0)
      END
   NEXT DL
   IF PO.TYPE = "M" THEN
      IF VPS.SEQ.NO = '' THEN
         IF PL # 0 THEN
            VSTAT.PO = DELETE(VSTAT.PO,1,PL,0)
            VSTAT.PO.ORDERD = DELETE(VSTAT.PO.ORDERD,1,PL,0)
            VSTAT.PO.RECV = DELETE(VSTAT.PO.RECV,1,PL,0)
            VSTAT.PO.VOUCH = DELETE(VSTAT.PO.VOUCH,1,PL,0)
            VSTAT.VOUCH.NOS = DELETE(VSTAT.VOUCH.NOS,1,PL,0)
         END
         DELETE VEND.PO.STATS,ID
      END ELSE
         MATWRITE VPS.REC ON VEND.PO.STATS,ID
      END
   END ELSE
      IF VPS.PROD = '' THEN
         IF PL # 0 THEN
            VSTAT.PO = DELETE(VSTAT.PO,1,PL,0)
            VSTAT.PO.ORDERD = DELETE(VSTAT.PO.ORDERD,1,PL,0)
            VSTAT.PO.RECV = DELETE(VSTAT.PO.RECV,1,PL,0)
            VSTAT.PO.VOUCH = DELETE(VSTAT.PO.VOUCH,1,PL,0)
            VSTAT.VOUCH.NOS = DELETE(VSTAT.VOUCH.NOS,1,PL,0)
         END
         DELETE VEND.PO.STATS,ID
      END ELSE
         MATWRITE VPS.REC ON VEND.PO.STATS,ID
      END
   END
   IF VSTAT.PO = '' THEN
      DELETE VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE
   END ELSE
      MATWRITE VSTAT.REC ON VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE
   END
200 REPEAT
GOTO 99999
93000*
ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
