*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM     - APD.EOM.HIST.LIST
*
* SYSTEM      - APSBP
*
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
*
* DATE        - 01/29/2002
*
* DESCRIPTION -
* This program produces the APD EOM history listing by detail, customer
* or a summary report.
*
*T25975 cmykleb 01/29/2002 * Inital programming for new program.
*T25901 cmykleb 02/20/2002 * Replace "Report No : XXXX" in heading to
*                            "Requested By : " User id.
*T26493 cmykleb 03/29/2002 * Change pgm to get the rpt # from the proc
*                            and use GET.PROG.HEAD for the heading.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>EOM.TRANS.HIST
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST RUN FROM A PROC"; GOTO 93000
   END
   CONO = BUFFER<1>
   DIV.CODE = BUFFER<6>;* T23278
   USER.ID = UPCASE(@LOGNAME) ; * T25901
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','COA' TO COA ELSE ERRMSG='COA FILE MISSING';GOTO 93000
   OPEN '','VEND' TO VEND ELSE ERRMSG='VEND FILE MISSING';GOTO 93000
   OPEN '','APD.EOM.TRANS.HIST' TO EOM.TRANS.HIST ELSE ERRMSG='APD.EOM.TRANS.HIST FILE MISSING';GOTO 93000
*
*---- READ COMPANY RECORD
*
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "CANNOT LOCATE COMPANY - " : CONO; GOTO 93000
   END
   MATREADU FISCAL.REC FROM CONTROL, CONO : "APDFISCAL" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL - APDFISCAL"; GOTO 93000
   END
   OPTION = BUFFER<3>
   SEL = BUFFER<4>
   VNDOR = BUFFER<5>
   SEL.DIV = BUFFER<6>
   LEVEL = BUFFER<7> + 1
   D.M.FLG = "PERIOD"
   PERIOD = BUFFER<8>
*
*---- DIMENSION RECORDS
*
   DIM NM(4)
   DIM PV(4)
   DIM BI(4)
   BI(1) = 11
   BI(2) = 4
   BI(3) = 6
   BI(4) = 8
   DIM EI(4)
   EI(1) = LEN(CO.ACCT.PIC)
   EI(2) = 2
   EI(3) = 2
   EI(4) = 3
   DIM BB(4)
   BB(1) = ""
   BB(2) = ""
   BB(3) = SPACE(3)
   BB(4) = SPACE(6)
   DIM AB(4,4)
   AB(1,1) = 0
   AB(1,2) = 0
   AB(2,1) = 3
   AB(2,2) = 8
   AB(3,1) = 6
   AB(3,2) = 5
   AB(4,1) = 10
   AB(4,2) = 1
   DIM DB(4)
   MAT DB = 0
   DIM CR(4)
   MAT CR = 0
   DIM BRK(4)
   BRK(1) = "ACT "
   BRK(2) = "DIV "
   BRK(3) = "DEP "
   BRK(4) = "CTR "
   DB.TOT = 0
   CR.TOT = 0
   SP = SPACE(1)
*
*---- SETUP HEADINGS
*
*T26493 v
*  N = (50 - LEN(CO.NAME)) / 2
*  HD1 = "REQUESTED BY : " : USER.ID : SPACE(20 + N) : CO.NAME : SPACE(15 + N) : " 'TL'"
*  HD2 = SPACE(12) : "TYP VENDOR   DIV LVL"
*T26493 ^
   HD4 = "ACCOUNT NUMBER    ACCOUNT DESCRIPTION    DV DP CTR "
   HD5 = "-------------- ------------------------- -- -- --- "
   TSP = 55
   DSH = SPACE(TSP) : STR("-",13) : SP : STR("-",13)
   BEGIN CASE
      CASE OPTION="S"
*T26493  HD2=HD2:SPACE(10):"CASH DISTRIBUTION SUMMARY ":D.M.FLG:" HISTORY LISTING" : SPACE(30)
         HD4 = HD4 : "    DEBIT AMOUNT  CREDIT AMOUNT"
         HD5 = HD5 : "    ------------- -------------"
      CASE OPTION="D"
*T26493  HD2 = HD2:SPACE(11) : "CASH DISTRIBUTION DETAIL ":D.M.FLG:" HISTORY LISTING" : SPACE(32)
         HD4 = HD4 : "TYP  VENDOR  DEBIT AMOUNT  CREDIT AMOUNT CHECK NO VOUCH  CHK DATE CHECK AMOUNT"
         HD5 = HD5 : "--- -------- ------------- ------------- -------- ------ -------- --------------"
         DSH = SPACE(9) :DSH : SPACE(26) : STR("-",14)
      CASE 1
*T26493  HD2=HD2:SPACE(12):"CASH DISTRIBUTION TYPE ":D.M.FLG:" HISTORY LISTING" : SPACE(30)
         HD4 = HD4 : "TYP  VENDOR           VENDOR NAME           DEBIT AMOUNT  CREDIT AMOUNT"
         HD5 = HD5 : "--- -------- ------------------------------ ------------- -------------"
         DSH = SPACE(40) : DSH
   END CASE
   SPN = SPACE(10)
*T26493 v
*  HD2 = HD2 : "PAGE : 'PL'"
*  HD3 = "SELECTION : " : SEL "L#3" : SP : VNDOR "L#8" : SP : SEL.DIV "L#3" : SP : (LEVEL-1) "L#1" : SPACE(25)
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HD1,HD2)
   HD1 = HD1 : " 'PL'"
   HD3 = ("SEL: TYPE: ":SEL:" VEND: ":VNDOR:" DIV: ":SEL.DIV:" LVL: ":(LEVEL-1))"L#65"
*T26493 ^
   HD3 = HD3 : "FOR PERIOD " : PERIOD
   HD3 = HD3 : " 'LL'"
   HD4 = HD4 : " 'L'"
*
*---- MAIN PROCESSING
*
   READNEXT ETH.ID ELSE GOTO 99999
   DATA=1     
   PRINTER ON
   HEADING HD1 : HD2 : HD3 : HD4 : HD5
   FOR I = 1 TO 4
      NM(I) = ETH.ID[BI(I),EI(I)]
      PV(I) = NM(I)
   NEXT I
   MATREAD COA.REC FROM COA, CONO : NM(1) ELSE COA.DESC = "UNKNOWN"
   OCOA.DESC = COA.DESC
   LOOP
      NAME = SPN
      NO.BREAK = 1
      FOR I = 1 TO LEVEL WHILE NO.BREAK
         IF PV(I) <> NM(I) THEN
            NO.BREAK = 0
            FOR J = LEVEL TO I STEP -1
               DB.CHK = DB(J); CR.CHK = CR(J)
               BEGIN CASE
                  CASE J = 1
                     DB.CHK = DB.CHK + 1
                     BLN = PV(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25" : SP
                     MATREAD COA.REC FROM COA, CONO : NM(1) ELSE COA.DESC = "UNKNOWN"
                  CASE J = I
                     DB.CHK = DB.CHK + 1
                     BLN = SPACE(41)
                     NAME = BB(J) : PV(J) : NAME[AB(J,1),AB(J,2)]
                  CASE 1
                     DB.CHK = DB(J-1); CR.CHK = CR(J-1)
                     BLN = SPACE(41)
                     NAME = BB(J) : PV(J) : NAME[AB(J,1),AB(J,2)]
               END CASE
               BEGIN CASE
                  CASE DB.CHK = DB(J) AND CR.CHK = CR(J)
                  CASE OPTION = "S"
                     PRINT PV(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25" : SP :
                     PRINT NAME:BRK(J):OCONV(DB(J),"MD2Z,") "R#13":SP:OCONV((0 - CR(J)),"MD2Z,") "R#13"
                     PRINT
                     NAME = SPN
                  CASE OPTION = "D"
                     PRINT DSH
                     PRINT BLN:NAME:BRK(J):SPACE(9):OCONV(DB(J),"MD2Z,") "R#13":SP:OCONV((0 - CR(J)),"MD2Z,") "R#13" :
                     PRINT SPACE(26) : OCONV(DB(J)+CR(J),"MD2,C") "R#14"
                     PRINT
                     NAME = SPN
                  CASE 1
                     PRINT DSH
                     PRINT BLN:NAME:BRK(J):SPACE(40):OCONV(DB(J),"MD2Z,") "R#13":SP:OCONV((0 - CR(J)),"MD2Z,") "R#13"
                     PRINT
                     NAME = SPN
               END CASE
            NEXT J
            FOR J = I TO LEVEL
               PV(J) = NM(J)
               DB(J) = 0
               CR(J) = 0
            NEXT J
            OCOA.DESC = COA.DESC
         END
      NEXT I
      BEGIN CASE
         CASE NO.BREAK AND OPTION = "D"
            PRINT
         CASE DATA = 0
            BEGIN CASE
               CASE OPTION = "S"
                  PRINT SPACE(TSP) : STR("=",13) : SP : STR("=",13)
                  PRINT SPACE(TSP) : OCONV(DB.TOT,"MD2,") "R#13" : SP : OCONV(CR.TOT,"MD2,") "R#13"
               CASE OPTION = "D"
                  PRINT SPACE(TSP) : SPACE(9) : STR("=",13) : SP : STR("=",13)
                  PRINT SPACE(TSP) : SPACE(9) : OCONV(DB.TOT,"MD2,") "R#13" : SP : OCONV(CR.TOT,"MD2,") "R#13"
               CASE 1
                  PRINT SPACE(TSP) : SPACE(40) : STR("=",13) : SP : STR("=",13)
                  PRINT SPACE(TSP) : SPACE(40) : OCONV(DB.TOT,"MD2,") "R#13" : SP : OCONV(CR.TOT,"MD2,") "R#13"
            END CASE
      END CASE
   WHILE DATA DO
      MATREAD ETH.REC FROM EOM.TRANS.HIST,ETH.ID ELSE MAT ETH.REC=''
      VEND.NO = FIELD(ETH.ID,"!",2)
      TYP=FIELD(ETH.ID,"!",1)[BI(1) + EI(1),99] "L#3"
      IF FIELD(FIELD(ETH.ID,"!",3),"*",1) = "D" THEN
         DEBIT = OCONV(ETH.AMT,"MD2Z,") "R#13"
         CREDIT = SPACE(13)
         FOR I = 1 TO LEVEL
            DB(I) = DB(I) + ETH.AMT
         NEXT I
         DB.TOT = DB.TOT + ETH.AMT
      END ELSE
         DEBIT = SPACE(13)
         CREDIT = OCONV((0 - ETH.AMT),"MD2Z,") "R#13"
         FOR I = 1 TO LEVEL
            CR(I) = CR(I) + ETH.AMT
         NEXT I
         CR.TOT = CR.TOT - ETH.AMT
      END
      IF OPTION # "S" THEN
         PRINT NM(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25" : SP :
         PRINT NM(2) "L#2" : SP : NM(3) "L#2" : SP : NM(4) "L#3" : SP : TYP :
         IF OPTION = "D" THEN
            PRINT  SP : VEND.NO "L#8" : SP : DEBIT : SP : CREDIT : SP : ETH.TRAN<1,1> "L#8" : SP : ETH.REF<1,1> "R#6" : SP : OCONV(ETH.DATE<1,1>,"D2/") "L#8" :
            PRINT SP : OCONV(ETH.TAMT<1,1>,"MD2,C") "R#14"
            CNT = COUNT(ETH.TAMT,VM) + 1
            FOR T = 2 TO CNT
               PRINT SPACE(83) : ETH.RDATE<1,T> "L#8" : SP : ETH.TRAN<1,T> "L#8" : SP : ETH.REF<1,T> "R#6" : SP : OCONV(ETH.DATE<1,T>,"D2/") "L#8" :
               PRINT SP : OCONV(ETH.TAMT<1,T>,"MD2,C") "R#14"
            NEXT T
         END ELSE
            IF ETH.RDATE = '' THEN
               MATREAD VEND.REC FROM VEND,CONO:VEND.NO ELSE VEND.DESC = VEND.NO:" IS MISSING"
            END ELSE 
               VEND.DESC = ETH.RDATE
            END
            PRINT SP : VEND.NO "L#8" : SP : VEND.DESC "L#30" : SP : DEBIT : SP : CREDIT
         END
      END
      READNEXT ETH.ID ELSE
         ETH.ID=STR('X',25)
         DATA=0
      END
      FOR I = 1 TO 4
         NM(I) = ETH.ID[BI(I),EI(I)]
      NEXT I
   REPEAT
   PRINTER OFF
   GOTO 99999
*   
*---- CALLS FOR SYSCOM   
*   
91000*   
   PRINTER OFF
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   PRINTER ON
   RETURN
93000*   
   PRINTER OFF
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*   
END
