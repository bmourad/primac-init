***************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - APSBP VEND.PO.STATS.RPT
* AUTHOR   - CURTIS WILLIAMS, CBA
* DATE     - 02/27/1987
* DESCRIPTION -
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
****************************************
*
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>COM1
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>VEND
*
*----PROCS VALUES
*
   PROCREAD PROC.VALUES ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC" ; GOTO 93000
   END
   CONO = PROC.VALUES<1>
*T26493 v
*  CONO.NAME = PROC.VALUES<2>
   REPORT.NUMBER = PROC.VALUES<2>
   CONO.NAME = ""
*T26493 ^
   PO.TYPE = PROC.VALUES<5>
   PRC.TYP = PROC.VALUES<6>
*
*
*----OPEN FILES
*
   OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG = "CANNOT OPEN INVENTORYFILE";GOTO 93000
   OPEN '','VEND.PO.STATS' TO VEND.PO.STATS ELSE ERRMSG = "CANNOT OPEN VEND.PO.STATS";GOTO 93000
   OPEN '','VEND' TO VEND ELSE ERRMSG = "CANNOT OPEN VEND";GOTO 93000
*
*----INIT
*
   LN.CNT = 99; PGNO = 0
   PO.ORD.TOT = 0; PO.REC.TOT = 0; PO.VOU.TOT = 0
   VEND.ORD.TOT = 0; VEND.REC.TOT = 0; VEND.VOU.TOT = 0
   GRD.ORD.TOT = 0; GRD.REC.TOT = 0; GRD.VOU.TOT = 0
   PREV.PO = ''; PREV.VEND = ''
   ORD.QTY = ''; REC.QTY = ''; VOU.QTY = ''
   SP = " "
   MAT INV.CNV.REC = ''
   FST.TIME = 1
*
*
*----SETUP HEADING
*
*T26493 v
*  IF PRC.TYP = "P" THEN
*     REPORT.NAME = "AUTO P/O PURGE"
*  END ELSE
*     REPORT.NAME = "VENDOR STATUS REPORT"
*  END
   REPORT.NAME = ""
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   BEGIN CASE
      CASE PO.TYPE = "R"
         HD3 = SPACE(69):"REGULAR P/O's"
      CASE PO.TYPE = "M"
         HD3 = SPACE(66):"MISCELLENOUS P/O's"
      CASE 1
         HD3 = SPACE(69):"OUTSIDE P/O's"
   END CASE
   HD4 = " <---- ORDERED ----> <--- RECEIVED ----> <--- VOUCHERED --->"
   HD5 = "PO NUMBER  VOUCH# "
   HD6 = "---------- ------ "
   IF PO.TYPE # "O" THEN
      HD4 = SPACE(12):"LAST":SPACE(2):"PRODUCT NUMBER":SPACE(37):HD4
      HD5 = HD5:"  PRODUCT DESCRIPTION                      WHSE U/M"
      HD6 = HD6:"------------------------------------------ ---- ---"
   END ELSE
      HD4 = SPACE(12):"LAST":SPACE(2):"JOB NUMBER":SPACE(41):HD4
      HD5 = HD5:"  JOB DESCRIPTION                          CATG OSP"
      HD6 = HD6:"------------------------------------------ --------"
   END
   HD5 = HD5:" QUANTITY   AMOUNT   QUANTITY   AMOUNT   QUANTITY   AMOUNT"
   HD6 = HD6:" -------- ---------- -------- ---------- -------- ----------"
   T.DASH = "----------          ----------          ----------"
*
*----MAINT PROCESS
*
   PRINTER ON
1*
   READNEXT ID ELSE GOTO 99999
   MATREAD VPS.REC FROM VEND.PO.STATS, ID ELSE GOTO 1
   GOSUB 1000
*
*----START PROCESSING
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
      MATREAD VPS.REC FROM VEND.PO.STATS, ID ELSE GOTO 111
      GOSUB 1000
111 REPEAT
   IF FST.TIME = 0 THEN GOSUB 8000
   GOTO 99999
*
1000*--TEST KEY
*
   VEND.NUM = FIELD(ID,"!",1)[4,10]
   PO.NUM = FIELD(ID,"!",3)
   PROD.CNT = COUNT(VPS.PROD,VM) + (VPS.PROD # "")
   BEGIN CASE
      CASE PROD.CNT = 0
         GOSUB 1999
      CASE VEND.NUM # PREV.VEND
         IF FST.TIME THEN
            GOSUB 9000
            FST.TIME = 0
         END ELSE
            GOSUB 7000
            GOSUB 9000
         END
         PREV.VEND = VEND.NUM
         PREV.PO = PO.NUM
      CASE PO.NUM # PREV.PO
         GOSUB 6000
         PREV.PO = PO.NUM
   END CASE
*
*--- PRINT DATA
*
   FOR PCNT = 1 TO PROD.CNT
      VCNT = COUNT(VPS.VOU.NO<1,PCNT>,SM) + (VPS.VOU.NO<1,PCNT> # "")
      GOSUB 10000
      P.LINE = ''
      IF PCNT = 1 THEN
         P.LINE = PREV.PO"L#10":SP
      END ELSE
         P.LINE = SPACE(11)
      END
      IF VCNT >= 2 THEN
         P.LINE = P.LINE:VPS.VOU.NO<1,PCNT,VCNT-1>"L#6":SP:VPS.PROD<1,PCNT>"L#15":SPACE(27):SP
      END ELSE
         P.LINE = P.LINE:VPS.VOU.NO<1,PCNT,VCNT>"L#6":SP:VPS.PROD<1,PCNT>"L#15":SPACE(27):SP
      END
      IF PO.TYPE = "O" THEN
         P.LINE = P.LINE:VPS.WHSE<1,PCNT>"L#8":SP
      END ELSE
         P.LINE = P.LINE:VPS.WHSE<1,PCNT>"L#4":SP:VPS.U.M<1,PCNT>"L#3":SP
      END
      P.LINE = P.LINE:OCONV(ORD.QTY,ICR.CNV)"R#8":SP:OCONV(VPS.ORD.AMT<1,PCNT>,"MD2")"R#10":SP
      P.LINE = P.LINE:OCONV(REC.QTY,ICR.CNV)"R#8":SP:OCONV(VPS.REC.AMT<1,PCNT>,"MD2")"R#10":SP
      P.LINE = P.LINE:OCONV(VOU.QTY,ICR.CNV)"R#8":SP:OCONV(VPS.VOU.AMT<1,PCNT>,"MD2")"R#10":SP
      IF VPS.FLAG<1,PCNT> = "Y" AND PRC.TYP = "P" THEN
         P.LINE = P.LINE:"<="
      END
      PRINT P.LINE; LN.CNT = LN.CNT + 1
      IF LN.CNT > 55 THEN GOSUB 9000
      P.LINE = ''
      IF VCNT >= 2 THEN
         P.LINE = SPACE(11):VPS.VOU.NO<1,PCNT,VCNT>"L#6":SPACE(3):VPS.PROD.DESC<1,PCNT>"L#44"
      END ELSE
         P.LINE = SPACE(20):VPS.PROD.DESC<1,PCNT>"L#44"
      END
      PRINT P.LINE; LN.CNT = LN.CNT + 1
      IF LN.CNT > 55 THEN GOSUB 9000
      PO.ORD.TOT = PO.ORD.TOT + VPS.ORD.AMT<1,PCNT>
      PO.REC.TOT = PO.REC.TOT + VPS.REC.AMT<1,PCNT>
      PO.VOU.TOT = PO.VOU.TOT + VPS.VOU.AMT<1,PCNT>
   NEXT PCNT
1999 RETURN
*
*----TOTAL FOR PO BREAK
*
6000 PRINT SPACE(79):T.DASH
   PRINT SPACE(67):"P/O TOTAL - ":OCONV(PO.ORD.TOT,"MD2")"R#10":SPACE(10):OCONV(PO.REC.TOT,"MD2")"R#10":SPACE(10):OCONV(PO.VOU.TOT,"MD2")"R#10"
   PRINT
   LN.CNT = LN.CNT + 3
   IF LN.CNT > 55 THEN GOSUB 9000
   VEND.ORD.TOT = VEND.ORD.TOT + PO.ORD.TOT
   VEND.REC.TOT = VEND.REC.TOT + PO.REC.TOT
   VEND.VOU.TOT = VEND.VOU.TOT + PO.VOU.TOT
   PO.ORD.TOT = 0; PO.REC.TOT = 0; PO.VOU.TOT = 0
   RETURN
*
*----TOTAL FOR VENDOR BREAK
*
7000 GOSUB 6000
   PRINT SPACE(79):T.DASH
   PRINT SPACE(64):"VENDOR TOTAL - ":OCONV(VEND.ORD.TOT,"MD2")"R#10":SPACE(10):OCONV(VEND.REC.TOT,"MD2")"R#10":SPACE(10):OCONV(VEND.VOU.TOT,"MD2")"R#10"
   PRINT
   LN.CNT = LN.CNT + 3
   IF LN.CNT > 55 THEN GOSUB 9000
   GRD.ORD.TOT = GRD.ORD.TOT + VEND.ORD.TOT
   GRD.REC.TOT = GRD.REC.TOT + VEND.REC.TOT
   GRD.VOU.TOT = GRD.VOU.TOT + VEND.VOU.TOT
   VEND.ORD.TOT = 0; VEND.REC.TOT = 0; VEND.VOU.TOT = 0
   RETURN
*
*----GRAND TOTAL
*
8000 GOSUB 7000
   PRINT SPACE(79):T.DASH
   PRINT SPACE(65):"GRAND TOTAL - ":OCONV(GRD.ORD.TOT,"MD2")"R#10":SPACE(10):OCONV(GRD.REC.TOT,"MD2")"R#10":SPACE(10):OCONV(GRD.VOU.TOT,"MD2")"R#10"
   RETURN
*
*----PRINT HEADING
*
9000 MATREAD VEND.REC FROM VEND, CONO:VEND.NUM ELSE VEND.DESC = "UNKOWN"
   PRINT CHAR(12)
   PGNO = PGNO + 1
   PRINT HD1:PGNO
   PRINT HD2
   PRINT HD3
   PRINT "VENDOR :":SP:VEND.NUM"L#8":SP:VEND.DESC"L#30"
   PRINT
   PRINT HD4
   PRINT HD5
   PRINT HD6
   LN.CNT = 8
   RETURN
*
*----CONVERT
*
10000 BEGIN CASE
      CASE PO.TYPE = "R"
         MATREAD INV.REC FROM INVENTORY, CONO:VPS.PROD<1,PCNT> ELSE
            MAT INV.REC = ""; INV.FULL.DESC = "UNKNOWN"
         END
      CASE 1
         MAT INV.REC = ''
   END CASE
   IF INV.SBR + 0 = 0 THEN INV.SBR = 1
   BEGIN CASE
      CASE VPS.U.M<1,PCNT> = "SHT" AND INV.UNIT<1,3> = "LBS" AND PO.TYPE = "R"
         ICR.CNV = "MD0"; ICR.DV2 = 1
         ICR.DV1 = INV.M.WT; ICR.MT1 = 10
      CASE VPS.U.M<1,PCNT> = "PC" AND INV.UNIT<1,3> = "MSI" AND PO.TYPE = "R"
         ICR.CNV = "MD0"; ICR.DV2 = 1
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100
      CASE VPS.U.M<1,PCNT> = "FT" AND INV.UNIT<1,3> = "MSI" AND PO.TYPE = "R"
         ICR.CNV = "MD0"; ICR.DV2 = 12
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 1000
      CASE 1
         ICR.CNV = "MD2"; ICR.DV2 = 1
         ICR.DV1 = 1; ICR.MT1 = 1
   END CASE
   ORD.QTY = INT(((VPS.ORD.QTY<1,PCNT>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5)
   REC.QTY = INT(((VPS.REC.QTY<1,PCNT>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5)
   VOU.QTY = INT(((VPS.VOU.QTY<1,PCNT>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5)
   RETURN
**** CALLS FOR SYSCOM
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER OFF
END
