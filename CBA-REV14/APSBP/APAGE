*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM          - PRIMAC
* PROGRAM         - APAGE
* BY              - JIHAD YAMOUT , C.B.A
* DATE            - 11/7/84
* DESCRIPTION     - This progeam list all vouchers for a vendor for a
*                   account payable ageing.
*ENDDOC
*********************************************************************
*COPY>APS.CPYLIB>OAP
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
**** INITIALZATION
    LINE.COUNT = 99 ; PAGE.NO = 0
    TOT.VND = 0 ; TOT.DA1 = 0 ; TOT.DA2 = 0 ; TOT.DA3 = 0 ; TOT.HLD = 0 ; TOT.FUR = 0 ; TOT.DSC = 0
    GTOT.VND = 0 ; GTOT.DA1 = 0 ; GTOT.DA2 = 0 ; GTOT.DA3 = 0 ; GTOT.HLD = 0 ; GTOT.FUR = 0 ; GTOT.DSC = 0
    SP1 = " "
**** SETUP FOR SYSTEM ERRMSGS
    SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
**** GET PROC VALUES
    PROCREAD X ELSE
       ERRMSG = "THIS REPORT MUST RUN FROM A PROC." ; GOTO 93000
    END
    CONO = X<1>
    AG.DATE = ICONV(X<5>, "D")
    LAST.PER = X<6>
    AGE.DATE1 = AG.DATE - 30
    AGE.DATE2 = AG.DATE - 60
    AGE.DATE3 = AG.DATE - 90
**** OPEN ALL FILES
    OPEN "","OAP" TO OAP ELSE ERRMSG = "OAP FILE IS MISSING" ; GOTO 93000
    OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING";GOTO 93000
    OPEN "","VEND" TO VEND ELSE ERRMSG = "VENDOR FILE IS MISSING" ; GOTO 93000
*
**** SETUP HEADING
*
    CONO.NAME = X<2>
    REPORT.NAME = "ACCOUNTS PAYABLE AGEING"
    REPORT.NUMBER = "XXXX"
    REPORT.DATE = DATE()
    HD1 = "" ; HD2 = ""
    CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
    HD3 = "      VENDOR        INVOICE #  INV.DATE VOUCH# DUE DATE  DISCOUNT   CURRENT    31 - 60    61 - 90      91 +       HOLD    TOTAL A/P"
    HD4 = "------------------- ---------- -------- ------ -------- ---------- ---------- ---------- ---------- ---------- ---------- ----------"
    HD5 = "                                                        ---------- ---------- ---------- ---------- ---------- ---------- ----------"
    HD6 = "                                                        ========== ========== ========== ========== ========== ========== =========="
    PRINTER ON
*
**** READ AND PROCESS FIRST REPORD
*
100*
    READNEXT ID ELSE
      PRINTER OFF
       ERRMSG = "NO ITEMS SELECTED PLEASE PRESS <REUTRN> TO CONTINUE" ; GOSUB 91000 ; GOTO 99999
    END
    MATREAD OAP.REC FROM OAP, ID ELSE GOTO 100
    BEGIN CASE
*     CASE LAST.PER = 12
*        IF ID[4,2] = 13 OR ID[4,2] = 1 THEN GOTO 100
*     CASE LAST.PER = 13
*        IF ID[4,2] = 1 THEN GOTO 100
*     CASE LAST.PER + 1 = ID[4,2]
*        GOTO 100
      CASE OAP.INV.DATE > AG.DATE
         GOTO 100
      CASE OAP.PAID.DATE > AG.DATE
         OAP.PAID.AMT = 0
         OAP.DSC.PAID = 0
      CASE OAP.PAID.AMT = OAP.GRS.AMT - OAP.DSC.PAID
         GOTO 100
      CASE 1
*        IF ID[4,2] = LAST.PER + 1 THEN GOTO 100
    END CASE
    PREV.VEND = OAP.VEND<1,1>
    MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
       VEND.DESC = "VEND NOT ON FILES"
    END
    IF OAP.VEND<1,2> # "" THEN
       VEND.DESC = OAP.VEND<1,2>
    END
    GOSUB 1000
**** PROCESS ALL RECORDS
200*
    DATA = 1
    LOOP
    READNEXT ID ELSE DATA = 0
    WHILE DATA = 1 DO
    MATREAD OAP.REC FROM OAP , ID ELSE GOTO 299
    BEGIN CASE
*     CASE LAST.PER = 12
*        IF ID[4,2] = 13 OR ID[4,2] = 1 THEN GOTO 299
*     CASE LAST.PER = 13
*        IF ID[4,2] = 1 THEN GOTO 299
*     CASE LAST.PER + 1 = ID[4,2]
*        GOTO 299
      CASE OAP.INV.DATE > AG.DATE
         GOTO 299
      CASE OAP.PAID.DATE > AG.DATE
         OAP.PAID.AMT = 0
         OAP.DSC.PAID = 0
      CASE OAP.PAID.AMT = OAP.GRS.AMT - OAP.DSC.PAID
         GOTO 299
      CASE 1
*        IF ID[4,2] = LAST.PER + 1 THEN GOTO 299
    END CASE
    GOSUB 900
    GOSUB 1000
299 REPEAT
    GOSUB 2000
    GOSUB 3000
    GOTO 99999
*
**** FIND BREAK
*
900*
    IF OAP.VEND<1,1> # PREV.VEND THEN
       GOSUB 2000
       PREV.VEND = OAP.VEND<1,1>
       MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
          VEND.DESC = "VEND NOT ON FILES"
       END
       PRT.FLG = 1
       IF OAP.VEND<1,2> # "" THEN
          VEND.DESC = OAP.VEND<1,2>
       END
    END
    RETURN
*
**** PRINT VOUCHER LINES AND ADD VENDOR TOTALS
*
1000*
    IF LINE.COUNT > 55 THEN
       GOSUB 8000
       PRT.FLG = 1
    END
    IF PRT.FLG = 1 THEN
       IF LINE.COUNT + 3 > 55 THEN
          GOSUB 8000
       END
       P.LINE = ""
       P.LINE = PREV.VEND:"    ":VEND.DESC
       PRINT
       PRINT P.LINE
       PRINT
       LINE.COUNT = LINE.COUNT + 3
       PRT.FLG = 0
    END
    P.LINE = ""
    P.LINE = P.LINE:SPACE(20)
    P.LINE = P.LINE :OAP.INV "L#10":SP1
    P.LINE = P.LINE :OCONV(OAP.INV.DATE , "D2/") "R#8":SP1
    P.LINE = P.LINE :ID[4,6] "L#6":SP1
    P.LINE = P.LINE :OCONV(OAP.DUE.DATE , "D2/") "R#8":SP1
    P.LINE = P.LINE :OCONV(OAP.DSC.AMT - OAP.DSC.PAID , "MD2") "R#10":SP1
    NET.AMT = OAP.GRS.AMT - (OAP.PAID.AMT + OAP.DSC.PAID)
    BEGIN CASE
       CASE VEND.STATUS = "H" OR OAP.DUE.DATE = "HOLD"
          P.LINE = P.LINE :SPACE(44)
          P.LINE = P.LINE :OCONV(NET.AMT, "MD2") "R#10"
          TOT.HLD = TOT.HLD + NET.AMT
       CASE OAP.INV.DATE >= AGE.DATE1
          P.LINE = P.LINE :OCONV(NET.AMT , "MD2") "R#10"
          TOT.DA1 = TOT.DA1 + NET.AMT
       CASE OAP.INV.DATE >= AGE.DATE2
          P.LINE = P.LINE :SPACE(11)
          P.LINE = P.LINE :OCONV(NET.AMT , "MD2") "R#10"
          TOT.DA2 = TOT.DA2 + NET.AMT
       CASE OAP.INV.DATE >= AGE.DATE3
          P.LINE = P.LINE :SPACE(22)
          P.LINE = P.LINE :OCONV(NET.AMT , "MD2")"R#10"
          TOT.DA3 = TOT.DA3 + NET.AMT
       CASE 1
          P.LINE = P.LINE :SPACE(33)
          P.LINE = P.LINE :OCONV(NET.AMT , "MD2") "R#10"
          TOT.FUR = TOT.FUR + NET.AMT
    END CASE
    TOT.VND = TOT.VND + NET.AMT
    DSC.AMT = OAP.DSC.AMT - OAP.DSC.PAID
    IF DSC.AMT < 0 AND OAP.GRS.AMT > 0 THEN DSC.AMT = 0
    IF DSC.AMT > 0 AND OAP.GRS.AMT < 0 THEN DSC.AMT = 0
    TOT.DSC = TOT.DSC + DSC.AMT
    PRINT P.LINE
    LINE.COUNT = LINE.COUNT + 1
1999RETURN
*
**** PRINT VENDOR TOTAL LINE
*
2000*
    IF LINE.COUNT + 3 > 55 THEN
       GOSUB 8000
    END
    PRINT HD5
    T.LINE = ""
    T.LINE = T.LINE:SPACE(42)
    T.LINE = T.LINE:"VENDOR TOTAL  "
    T.LINE = T.LINE:OCONV(TOT.DSC, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(TOT.DA1, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(TOT.DA2, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(TOT.DA3, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(TOT.FUR, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(TOT.HLD, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(TOT.VND, "MD2") "R#10"
    PRINT T.LINE
    PRINT
    LINE.COUNT = LINE.COUNT + 3
    GTOT.VND = GTOT.VND + TOT.VND
    GTOT.DA1 = GTOT.DA1 + TOT.DA1
    GTOT.DA2 = GTOT.DA2 + TOT.DA2
    GTOT.DA3 = GTOT.DA3 + TOT.DA3
    GTOT.HLD = GTOT.HLD + TOT.HLD
    GTOT.FUR = GTOT.FUR + TOT.FUR
    GTOT.DSC = GTOT.DSC + TOT.DSC
    TOT.VND = 0 ; TOT.DA1 = 0 ; TOT.DA2 = 0 ; TOT.DA3 = 0 ; TOT.HLD = 0 ; TOT.FUR = 0 ; TOT.DSC = 0
    RETURN
*
***** PRINT GRAND TOTAL
*
3000*
    IF LINE.COUNT + 2 >  55 THEN
       GOSUB 8000
    END
    PRINT HD6
    T.LINE = ""
    T.LINE = T.LINE:SPACE(42)
    T.LINE = T.LINE:"GRAND TOTAL   "
    T.LINE = T.LINE:OCONV(GTOT.DSC, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(GTOT.DA1, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(GTOT.DA2, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(GTOT.DA3, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(GTOT.FUR, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(GTOT.HLD, "MD2") "R#10":SP1
    T.LINE = T.LINE:OCONV(GTOT.VND, "MD2") "R#10"
    PRINT T.LINE
    RETURN
*
**** PRINT HEADING
*
8000*
    PRINT CHAR(12)
    PAGE.NO = PAGE.NO + 1
    PRINT HD1:PAGE.NO
    PRINT HD2
    PRINT
    PRINT "ENDING DATE : ":OCONV(AG.DATE, "D2/")
    PRINT "PERIOD  NUM : ":LAST.PER
    PRINT
    PRINT HD3
    PRINT HD4
    LINE.COUNT = 8
    RETURN
***** CALLS FOR SYSCOM
91000ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
    RETURN
92000ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
    RETURN
93000ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
    PRINTER OFF
 END
