*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - APV.EOM.CLOSE
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
* DESCRIPTION -
*T21177 diane 01/22/1997 * REV11 UPG ADD CLOSE
*T22561 rick 01/29/1998 * Modify delete logic to keep INV.XREF forever
*                         so that dup invoices can be found.
*T23278 markt 12/03/1998 * Make fiscal data multi-value by division.
*T25975 cmykleb 01/29/2002 * Save EOM transaction data so history
*                            reports can be run.
*T26685 lhelms 07/02/2002 * REV12 DIVISION SECURITY
*T28949 lross 07/07/2006 * OAP records not deleting.
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>PMC.CPYLIB>EOM.TRANS.HIST           ; * T25975
*COPY>PMC.CPYLIB>VEND
*COPY>APS.CPYLIB>VOUCHERS
*COPY>APS.CPYLIB>OAP
*COPY>GLS.CPYLIB>GLA
*COPY>PMC.CPYLIB>COA
*COPY>GLS.CPYLIB>COA.SYS
*COPY>PMC.CPYLIB>POST.REJECTS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
***** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*--- PROCREAD
*
   PROCREAD INBUFF ELSE
      ERRMSG = "MUST RUN FROM PROC"
      GOSUB 91000; GOTO 99999
   END
   CONO = INBUFF<1>
   D.M.FLG = INBUFF<6>
   IF D.M.FLG = "PERIOD" THEN
      RPT.PER = "PERIOD"
   END ELSE
      RPT.PER = "DAY"
   END
   DIV.CODE = INBUFF<7>;* T23278
   IF DIV.CODE = '' THEN DIV.CODE = 'ALL'
*
**** INTITIALIZATION
*
   MAT FILE.VARS = ""
   MAT EDIT.COM.DRIVER = ""
   TODAY = DATE()
   UNKNOWN = "NOT ON FILE"
   SYS.FISCAL = "APVFISCAL"
*      PROG.ID = FIELD(OCONV(0,"U50BB")," ",1):"!":CONO:"!APV.EOM"
*     PROG.ID = SYSTEM(18):"!":CONO:"!APV.EOM"
   PROG.ID = @TTY:"!":CONO:"!APV.EOM"
*
***** OPEN FILES
*
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE MISSING"
      GOSUB 91000; GOTO 99000
   END
   OPEN "",INBUFF<3> TO EOM.TRANS ELSE
      ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD"
      GOSUB 91000; GOTO 99000
   END
   OPEN "",INBUFF<4> TO POST.REJECTS ELSE
      ERRMSG = "YOU NEED TO RUN END OF ":RPT.PER:" POSTING BEFORE YOU CLOSE THE PERIOD"
      GOSUB 91000; GOTO 99000
   END
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
   OPEN "","PMC.SCREENS" TO M.SCREENS ELSE ERRMSG="M.SCREENS FILE IS MISSING";GOTO 93000
   OPEN "","VOUCHERS" TO VOUCHERS ELSE ERRMSG="VOUCHERS FILE IS MISSING";GOTO 93000
   OPEN "","VOUCHERS.TAG" TO VOUCHERS.TAG ELSE ERRMSG="VOUCHERS.TAG FILE IS MISSING";GOTO 93000
   OPEN "","VEND" TO VEND ELSE ERRMSG="VEND FILE IS MISSING";GOTO 93000
   OPEN "","OAP" TO OAP ELSE ERRMSG="OAP FILE IS MISSING";GOTO 93000
   OPEN "","EOD.HIST" TO EOD.HIST ELSE ERRMSG="EOD.HIST FILE IS MISSING";GOTO 93000
   OPEN "","INV.XREF" TO INV.XREF ELSE ERRMSG="INV.XREF FILE IS MISSING";GOTO 93000
*T25975 v
   OPEN "","APV.EOM.TRANS.HIST" TO APV.EOM.TRANS.HIST ELSE ERRMSG="APV.EOM.TRANS.HIST FILE IS MISSING";GOTO 93000
*T25975 ^
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG = "CANNOT LOCATE COMPANY # ":CONO; GOTO 93000
   END
   IF CO.POS = "Y" AND CO.VOU.PO.FLG = "Y" THEN
      UPDATE.JOB = 1
      OPEN "","OPOAPS.XREF" TO OPOAPS.XREF ELSE ERRMSG="OPOAPS.XREF FILE IS MISSING";GOTO 93000
   END ELSE
      UPDATE.JOB = 0
   END
   IF CO.GLS # "N" THEN
      IF CO.GL.LINK<1,2> = "Y" THEN
         OPEN "","GLA" TO GLA ELSE ERRMSG="GLA FILE IS MISSING";GOTO 93000
      END
      OPEN "","COA" TO COA ELSE ERRMSG="COA FILE IS MISSING";GOTO 93000
      EQV.FLG = 1
      OPEN "","COA.EQUIV" TO COA.EQUIV ELSE EQV.FLG = 0
   END
   STMT.HEAD = "VOUCHERS"
*** *COPY>PMCBP>EOD.CLOSE
*COPY>PMCBP>EOD.CLOSE
   IF ECD.RET.VALUE = "END" THEN
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      GOTO 99000
   END
   CLEARFILE POST.REJECTS
   PRR.SEQ = 10000
*
***** MAIN PROCESSING
*
**** VOUCHERS ****
   WRITE "VOUCHERS" ON CONTROL , PROG.ID
   ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING VOUCHERS FILE"
   ECD.ACTION = 5; CALL SCRN.EDIT
   IF D.M.FLG = "PERIOD" THEN GOTO 1500
   SELECT VOUCHERS.TAG
   DATA = 1
   LOOP
      READNEXT VCH.ID ELSE DATA = 0
   WHILE DATA DO
      IF VCH.ID[1,3] # CONO THEN GOTO 1499
      MATREADU VCH.REC FROM VOUCHERS, VCH.ID ELSE
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
*         IF VCH.MON <> FR.CURR.PER THEN;* T23278
      IF VCH.MON <> FR.CURR.PER<1,POS> THEN;* T23278
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
      IF VCH.GLA.DATE = "" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
***** T23278 v
      IF VCH.DIV<1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1499
      END
***** T23278 ^
      VCH.GLA.DATE = TODAY
      MATWRITE VCH.REC ON VOUCHERS, VCH.ID
      DELETE VOUCHERS.TAG, VCH.ID
1499 REPEAT
   GOTO 6000
1500 SELECT VOUCHERS
   DATA = 1
   LOOP
      READNEXT VCH.ID ELSE DATA = 0
   WHILE DATA DO
      IF VCH.ID[1,3] # CONO THEN GOTO 1999
      MATREADU VCH.REC FROM VOUCHERS, VCH.ID ELSE
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
***** T23278 v
      IF VCH.DIV<1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
***** T23278 ^
*         IF VCH.MON <= FR.CURR.PER THEN;* T23278
      IF VCH.MON <= FR.CURR.PER<1,POS> THEN;* T23278
         DELETE VOUCHERS.TAG, VCH.ID
      END
*         IF VCH.MON <> FR.CURR.PER THEN;* T23278
      IF VCH.MON <> FR.CURR.PER<1,POS> THEN;* T23278
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
      IF VCH.GLA.DATE = "" THEN
         RELEASE VOUCHERS, VCH.ID
         GOTO 1999
      END
      IF UPDATE.JOB AND VCH.JOB # "" THEN
         OPAX.ID = CONO : VCH.JOB
         READU OPAX FROM OPOAPS.XREF, OPAX.ID ELSE OPAX = ""
         LOCATE VCH.ID[4,6] IN OPAX<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN
            OPAX = DELETE(OPAX,1,FND,0)
            IF OPAX = "" THEN
               DELETE OPOAPS.XREF, OPAX.ID
            END ELSE
               WRITE OPAX ON OPOAPS.XREF, OPAX.ID
            END
         END ELSE
            RELEASE OPOAPS.XREF, OPAX.ID
         END
      END
      DELETE VOUCHERS, VCH.ID
1999 REPEAT
*
**** OAP ****
   WRITE "OAP" ON CONTROL , PROG.ID
   ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING OAP FILE"
   ECD.ACTION = 5; CALL SCRN.EDIT
   READ APDFISCAL FROM CONTROL, CONO : "APDFISCAL" ELSE APDFISCAL = ""
*T28949 v Found during csf 46411 03/28/07
*  IF APDFISCAL<6> = "" THEN
   IF APDFISCAL<6,POS> = "" THEN
*     APDFISCAL<6> = TODAY
      APDFISCAL<6,POS> = TODAY
*     MM = OCONV(APDFISCAL<6>, "D/")[1,2]
      MM = OCONV(APDFISCAL<6,POS>, "D/")[1,2]
      BLK = 0
      FOR TR = 1 TO 31 UNTIL BLK
*        APDFISCAL<6> = APDFISCAL<6> - 1
         APDFISCAL<6,POS> = APDFISCAL<6,POS> - 1
*        IF MM # OCONV(APDFISCAL<6>, "D/")[1,2] THEN BLK = 1
         IF MM # OCONV(APDFISCAL<6,POS>, "D/")[1,2] THEN BLK = 1
      NEXT TR
   END
   SELECT OAP
   DATA = 1
   LOOP
      READNEXT OAP.ID ELSE DATA = 0
   WHILE DATA DO
      IF OAP.ID[1,3] # CONO THEN GOTO 2999
      MATREADU OAP.REC FROM OAP, OAP.ID ELSE
         RELEASE OAP, OAP.ID
         GOTO 2999
      END
      IF OAP.DIV<1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
         RELEASE OAP, OAP.ID
         GOTO 2999
      END
      IF OAP.GRS.AMT - OAP.PAID.AMT - OAP.DSC.PAID = 0 THEN
*T28949  IF OAP.PAID.DATE > APDFISCAL<6> THEN
         IF OAP.PAID.DATE > APDFISCAL<6,POS> THEN
            RELEASE OAP, OAP.ID
            GOTO 2999
         END
*CSF 24317 v
         IX.ID = OAP.ID[1,3]:STR("0",10-LEN(OAP.INV)):OAP.INV:OAP.VEND<1,1>
*CSF 24317 ^
*T22561 v
*            READU IX.REC FROM INV.XREF, IX.ID ELSE IX.REC = ""
*            IF IX.REC<2> < (TODAY - 90) THEN
*               DELETE INV.XREF, IX.ID
*            END ELSE
*               RELEASE INV.XREF, IX.ID
*            END
*T22561 ^
         DELETE OAP, OAP.ID
      END ELSE
         RELEASE OAP, OAP.ID
      END
2999 REPEAT
*
**** INV.XREF ****
   WRITE "INV.XREF" ON CONTROL , PROG.ID
*T22561 v
*      ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING INV.XREF FILE"
*      ECD.ACTION = 5; CALL SCRN.EDIT
*      PERIOD = FR.CURR.PER[5,2] + 6
*      SELECT INV.XREF
*      DATA = 1
*      LOOP
*         READNEXT ID ELSE DATA = 0
*      WHILE DATA DO
*         IF ID[1,3] # CONO THEN GOTO 3999
*         READV IX.REC FROM INV.XREF, ID, 1 ELSE GOTO 3999
*         XPERIOD = IX.REC[1,2]
** THE FOLLOWING LINE COMPARES PERIOD (WHICH IS SET TO PERIOD + 6) 
** TO XPERIOD + 6 WHICH IS ALMOST ALWAYS TRUE.  THIS ELEMINATES
** THE ENTRY FROM THE CROSS REFERENCE.  THIS IS WHY WE NEVER BUILD UP
** THE 6 MONTH HISTORICAL DATA IN THE CROSS REFERENCE WHICH WAS THE
** ORIGINAL INTENT BUT NEVER ACTUALIZED.  SARA HAS DECIDED TO LEAVE THIS
** SINCE AT THIS POINT ALL THE TRAINERS TEACH THAT THIS CROSS REFERENCE
** IS JUST THE CURRENT MONTH.  WE MAY CHANGE THIS AT A LATER DATE.
** TO FIX MAKE PERIOD = FR.CURR.PERIOD [5,2] AND LEAVE OFF THE +6
**
*         IF PERIOD + 6 = XPERIOD OR PERIOD = XPERIOD + 6 THEN
**
*            DELETE INV.XREF, ID
*         END
*3999  REPEAT
*T22561 ^
6000 IF CO.GLS = "N" THEN GOTO 8000
   IF CO.GL.LINK<1,2> # "Y" THEN GOTO 8000
**** GLA ****
7000 ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING GLA FILE"
   ECD.ACTION = 5; CALL SCRN.EDIT
   MATREAD CSY.REC FROM CONTROL, CONO : "COA.SYS" ELSE MAT CSY.REC = ""
   LOCATE "VR" IN CSY.SOURCE<1>,1 SETTING FND ELSE NULL
   L.GLA = CSY.LEVEL<1,FND>
   ACCOUNTS = ""; AMOUNTS = ""
   MAT GLA.REC = ""
   GLA.DATE = TODAY
   GLA.SRC = "VR"
*      GLA.MON = FR.CURR.PER;* T23278
   GLA.MON = FR.CURR.PER<1,POS>;* T23278
*      ODATE = " " : OCONV(FR.CURR.DATE,"D2/");* T23278
   ODATE = " " : OCONV(FR.CURR.DATE<1,POS>,"D2/");* T23278
   SELECT EOM.TRANS
   READU COUNTER FROM CONTROL, CONO : "GLACOUNTER" ELSE COUNTER = 0
   WRITE "GLA " : COUNTER ON CONTROL , PROG.ID
   DATA = 1
   BEGIN CASE
      CASE L.GLA = "R"
         LOOP
            READNEXT ETR.ID ELSE DATA = 0
         WHILE DATA DO
            IF ETR.ID[1,3] # CONO THEN GOTO 7199
            MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7180
            ACCT = ETR.ID[1,ACCT.LEN]
            VEND.NO = FIELD(ETR.ID,"!",1)[ACCT.LEN + 1,99]
            TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
            IF EQV.FLG = 1 THEN
               READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
               ACCT = N.ACCT
            END
            COA.ID = ACCT[11,IN.ACCT.LEN]
            MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
               MAT COA.REC = ""
               COA.DESC = UNKNOWN
            END
            BEGIN CASE
               CASE COA.DETAIL = "R" OR COA.DETAIL = "D"
                  LOOP
                     COUNTER = COUNTER + 1
                     IF COUNTER > 999998 THEN COUNTER = 1
                     GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                     READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                  WHILE DUMMY # "" DO REPEAT
                  GLA.REF = VEND.NO
                  GLA.DESC = ETR.RDATE "L#25" : ODATE
                  GLA.AMT = ETR.AMT
                  MATWRITE GLA.REC ON GLA, GLA.ID
               CASE 1
                  LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
                     ACCOUNTS<FND> = ACCT
                     AMOUNTS<FND> = ""
                  END
                  IF TYPE = "D" THEN
                     AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
                  END ELSE
                     AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
                  END
            END CASE
7180        RELEASE EOM.TRANS, ETR.ID
7199     REPEAT
      CASE L.GLA = "D"
         LOOP
            READNEXT ETR.ID ELSE DATA = 0
         WHILE DATA DO
            IF ETR.ID[1,3] # CONO THEN GOTO 7299
            MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7280
            ACCT = ETR.ID[1,ACCT.LEN]
            VEND.NO = FIELD(ETR.ID,"!",1)[ACCT.LEN + 1,99]
            TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
            IF EQV.FLG = 1 THEN
               READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
               ACCT = N.ACCT
            END
            COA.ID = ACCT[11,IN.ACCT.LEN]
            MATREAD COA.REC FROM COA, CONO : COA.ID ELSE
               MAT COA.REC = ""
               COA.DESC = UNKNOWN
            END
            BEGIN CASE
               CASE COA.DETAIL = "D"
                  TCNT = COUNT(ETR.TRAN,VM) + (ETR.TRAN # "")
                  FOR I = 1 TO TCNT
                     LOOP
                        COUNTER = COUNTER + 1
                        IF COUNTER > 999998 THEN COUNTER = 1
                        GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                        READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                     WHILE DUMMY # "" DO REPEAT
                     GLA.REF = VEND.NO
                     GLA.DESC = ETR.TRAN<1,I>[1,6] : " " : ETR.RDATE
                     GLA.DESC = GLA.DESC "L#25" : " " : OCONV(ETR.DATE<1,I>,"D2/")
                     GLA.AMT = ETR.TAMT<1,I>
                     MATWRITE GLA.REC ON GLA, GLA.ID
                  NEXT I
               CASE COA.DETAIL = "R"
                  LOOP
                     COUNTER = COUNTER + 1
                     IF COUNTER > 999998 THEN COUNTER = 1
                     GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
                     READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
                  WHILE DUMMY # "" DO REPEAT
                  GLA.REF = VEND.NO
                  GLA.DESC = ETR.RDATE "L#25" : ODATE
                  GLA.AMT = ETR.AMT
                  MATWRITE GLA.REC ON GLA, GLA.ID
               CASE 1
                  LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
                     ACCOUNTS<FND> = ACCT
                     AMOUNTS<FND> = ""
                  END
                  IF TYPE = "D" THEN
                     AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
                  END ELSE
                     AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
                  END
            END CASE
7280        RELEASE EOM.TRANS, ETR.ID
7299     REPEAT
      CASE 1
         LOOP
            READNEXT ETR.ID ELSE DATA = 0
         WHILE DATA DO
            IF ETR.ID[1,3] # CONO THEN GOTO 7350
            MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 7330
            ACCT = ETR.ID[1,ACCT.LEN]
            TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
            IF EQV.FLG = 1 THEN
               READ N.ACCT FROM COA.EQUIV, ACCT ELSE N.ACCT = ACCT
               ACCT = N.ACCT
            END
            LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
               ACCOUNTS<FND> = ACCT
               AMOUNTS<FND> = ""
            END
            IF TYPE = "D" THEN
               AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
            END ELSE
               AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
            END
7330        RELEASE EOM.TRANS, ETR.ID
7350     REPEAT
   END CASE
   GLA.REF = ""
   ACNT = COUNT(ACCOUNTS,AM) + (ACCOUNTS # "")
   FOR I = 1 TO ACNT
      ACCT = ACCOUNTS<I>
      COA.ID = ACCT[11,IN.ACCT.LEN]
      MATREAD COA.REC FROM COA, CONO : COA.ID ELSE COA.DESC = UNKNOWN
      FOR J = 1 TO 2
         IF AMOUNTS<I,J> = "" THEN GOTO 7360
         LOOP
            COUNTER = COUNTER + 1
            IF COUNTER > 999998 THEN COUNTER = 1
            GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
            READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
         WHILE DUMMY # "" DO REPEAT
         GLA.AMT = AMOUNTS<I,J>
         GLA.DESC = COA.DESC "L#25" : ODATE
         MATWRITE GLA.REC ON GLA, GLA.ID
7360  NEXT J
   NEXT I
   WRITE COUNTER ON CONTROL, CONO : "GLACOUNTER"
   ACCOUNTS = ""
   AMOUNTS = ""
**** EOM.TRANS (1) ****
8000 ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING EOM.TRANS (1) FILE"
   ECD.ACTION = 5; CALL SCRN.EDIT
   SELECT EOM.TRANS
   WRITE "EOM.TRANS (1)" ON CONTROL , PROG.ID
   DATA = 1
   LOOP
      READNEXT ETR.ID ELSE DATA = 0
   WHILE DATA DO
      IF CONO # ETR.ID[1,3] THEN GOTO 8999
      MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE
         RELEASE EOM.TRANS, ETR.ID
         GOTO 8999
      END
*T25975 v
      CURR.PER = FR.CURR.PER<1,POS>
      MAT ETH.REC = MAT ETR.REC
      DONE = 0
      SEQ = 0
      ETH.ID = FIELD(ETR.ID,"*",1):"*":CURR.PER
      LOOP UNTIL DONE = 1 DO
         SEQ = SEQ + 1
         ETH.KEY = ETH.ID:"*":SEQ
         READ CHECKIT FROM APV.EOM.TRANS.HIST, ETH.KEY ELSE DONE = 1
      REPEAT
      MATWRITE ETH.REC ON APV.EOM.TRANS.HIST, ETH.KEY
*T25975 ^
      DELETE EOM.TRANS, ETR.ID
8999 REPEAT
   WRITE "EOM.TRANS (2)" ON CONTROL, PROG.ID
   EOD.HIST.REC = INSERT(EOD.HIST.REC,-1,0,0,TODAY)
*      WRITE EOD.HIST.REC ON EOD.HIST,CONO:SYS.FISCAL:FR.CURR.PER;* T23278
* T26685 v
*  WRITE EOD.HIST.REC ON EOD.HIST,CONO:SYS.FISCAL:FR.CURR.PER<1,POS>;* T23278
   WRITE EOD.HIST.REC ON EOD.HIST,CONO:SYS.FISCAL:FR.CURR.PER<1,POS>:EOD.HID;* T23278
* T26685 ^
   SELECT EOM.TRANS
   READNEXT ETR.ID ELSE
      IF D.M.FLG = "PERIOD" THEN
*            FR.CURR.PER = FR.NEXT.PER;* T23278
         FR.CURR.PER<1,POS> = FR.NEXT.PER<1,POS>;* T23278
*            FR.CURR.DATE = FR.NEXT.DATE;* T23278
         FR.CURR.DATE<1,POS> = FR.NEXT.DATE<1,POS>;* T23278
*            FR.CLOSE.DATE = TODAY;* T23278
         FR.CLOSE.DATE<1,POS> = TODAY;* T23278
      END
*         FR.NEXT.PER = ""; FR.NEXT.DATE = "";* T23278
      FR.NEXT.PER<1,POS> = ""; FR.NEXT.DATE<1,POS> = "";* T23278
*         FR.PRINT = "";FR.D.M.FLG = "";* T23278
      FR.PRINT<1,POS> = "";FR.D.M.FLG<1,POS> = "";* T23278
      MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
      DELETE CONTROL, PROG.ID
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      GOTO 99999
   END
*      FR.PRINT = "X";* T23278
   FR.PRINT<1,POS> = "X";* T23278
   MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
   ERRMSG = "E R R O R !!! CANNOT LOCATE ALL TRANSACTIONS"
   ECD.ACTION = 99 ; CALL SCRN.EDIT
   GOTO 93000
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 WRITEV ERRMSG ON CONTROL, PROG.ID,2
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99000 INBUFF<5> = "END"
   PROCWRITE INBUFF
*** END OF JOB ***
99999*ECD.ACTION = 99;CALL SCRN.EDIT
END
