*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - APV.EOM.POST
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
* MODIFIED    - 05/20/88 BY KERRY WILSON
* DESCRIPTION -
* 10/19/94 CLW TASK 17919 - Accrual accounting
*T21478 rick 01/15/1997 * MODIFY PGM SO THAT THE DEPT THAT IS USED IN
*                         THE GL ACCT NUMBER IS ONLY TWO DIGITS LONG
*T21177 diane 01/21/1997 * REV11 UPG ADD CLOSE
*T23278 markt 11/25/1998 * Make fiscal data multi-value by division.
*T26229 gat 10/17/2001 * FIX DISPLAY OF PROCESSING ITEMS
*T26685 lhelms 07/02/2002 * REV12 DIVISION SECURITY
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>DIVISION ;* T23278 
*COPY>APS.CPYLIB>VOUCHERS
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>POST.REJECTS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** INTITIALIZATION
*
  MAT FILE.VARS=''
  MAT EDIT.COM.DRIVER=''
*
***** SETUP ERRMSG ROUTINE
*
  SYS.TYPE=1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- PROCREAD
*
  PROCREAD INBUFF ELSE
      ERRMSG = "MUST RUN FROM PROC"
      GOSUB 91000
      GOTO 99999
  END
  CONO = INBUFF<1>
  DIV.CODE = INBUFF<7>;* T23278
*  FR.NEXT.PER = ""
*
***** OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
  OPEN '','DIVISION' TO DIVISION ELSE ERRMSG='DIVISION FILE MISSING';GOTO 93000;* T23278
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG='PMC.SCREENS FILE IS MISSING';GOTO 93000
  OPEN '','VOUCHERS' TO VOUCHERS ELSE ERRMSG='VOUCHERS FILE IS MISSING';GOTO 93000
  OPEN '','VEND' TO VEND ELSE ERRMSG='VEND FILE IS MISSING';GOTO 93000
  OPEN '','COA' TO COA ELSE ERRMSG='COA FILE IS MISSING';GOTO 93000
  OPEN '',INBUFF<3> TO EOM.TRANS ELSE ERRMSG=INBUFF<3>:' FILE MISSING';GOTO 93000
  OPEN '',INBUFF<4> TO POST.REJECTS ELSE ERRMSG=INBUFF<4>:' FILE IS MISSING';GOTO 93000
  OPEN '','EOD.HIST' TO EOD.HIST ELSE ERRMSG='EOD.HIST FILE IS MISSING';GOTO 93000
*
***** GET CONO
*
  MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "CANNOT LOCATE COMPANY " : CONO
      GOTO 93000
  END
  SYS.FISCAL = 'APVFISCAL'
  STMT.HEAD = 'VOUCHERS'
*COPY>PMCBP>EOD.POST
  IF ECD.RET.VALUE = 'END' THEN
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      GOTO 99000
  END
  CLEARFILE EOM.TRANS
  CLEARFILE POST.REJECTS
  PRR.SEQ = 10000
  OLD.VEND = "!@#$%^&*"
  OLD.VEND.NAME = "!@#$%^&*"
  DATA = 1
  LOOP
      READNEXT VCH.ID ELSE DATA = 0
  WHILE DATA DO
      IF CONO # VCH.ID[1,3] THEN GOTO 999
      VCH.NO = VCH.ID[4,99]
      MATREADU VCH.REC FROM VOUCHERS, VCH.ID ELSE
          MAT PRR.REC = ''
          PRR.FILE = 'VOUCHERS'
          PRR.ERR = 'CANNOT LOCATE'
          PRR.ID = VCH.NO
          PRR.SEQ = PRR.SEQ + 1
          MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
          RELEASE VOUCHERS, VCH.ID
          GOTO 999
      END
*      IF VCH.MON <> FR.CURR.PER THEN;* T23278
      IF VCH.MON <> FR.CURR.PER<1,POS> THEN;* T23278
          RELEASE VOUCHERS, VCH.ID
          GOTO 999
      END
      IF VCH.GLA.DATE # "" AND VCH.GLA.DATE # "P" THEN
          RELEASE VOUCHERS, VCH.ID
          GOTO 999
      END
***** T23278 v
       IF VCH.DIV<1,1> # DIV.CODE AND DIV.CODE # "ALL" THEN
           RELEASE VOUCHERS, VCH.ID
           GOTO 999
       END
***** T23278 ^
      VCNT = DCOUNT(VCH.VEND,VM)
      VCH.VEND.NAME = VCH.VEND<1,2>
      IF TRIM(VCH.DESC)[1,7] = "DELETED" THEN
          VCH.INV = "DELETED"
      END
      IF OLD.VEND # VCH.VEND THEN
          OLD.VEND = VCH.VEND
          NEW.REC = 0; NEW.PTR = 0
          IF VCH.VEND.NAME = "" THEN
              MATREAD VEND.REC FROM VEND , CONO:OLD.VEND<1,1> ELSE MAT VEND.REC = ""
              OLD.VEND.NAME = VEND.DESC
          END ELSE
              OLD.VEND.NAME = VCH.VEND.NAME
          END
      END
      P_X  = 5 ; P_Y = 19 ; P_VALUE = 'NOW PROCESSING VOUCHER NUMBER - ':VCH.NO "L#10" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = COUNT(VCH.DIST.AMT,VM) + (VCH.DIST.AMT # '')
      FOR I = 1 TO CNT
          T.ACCT = VCH.ACCT<1,I>
          DIST.AMT = VCH.DIST.AMT<1,I>
          BEGIN CASE
              CASE VCH.DIV<1,I> = ""
                  CASH.DIV = GEN.DIV
                  CASH.DEPT = GEN.DEPT
                  CASH.CCTR = GEN.CCTR
              CASE VCH.DEPT<1,I> = ""
                  CASH.DIV = VCH.DIV<1,I>
                  CASH.DEPT = GEN.DEPT
                  CASH.CCTR = GEN.CCTR
              CASE VCH.CCTR<1,I> = ""
                  CASH.DIV = VCH.DIV<1,I>
                  CASH.DEPT = VCH.DEPT<1,I>[1,2] ;*T21478
                  CASH.CCTR = GEN.CCTR
              CASE 1
                  CASH.DIV = VCH.DIV<1,I>
                  CASH.DEPT = VCH.DEPT<1,I>[1,2] ;*T21478
                  CASH.CCTR = VCH.CCTR<1,I>
          END CASE
          MATREAD COA.REC FROM COA, CONO : T.ACCT ELSE COA.LEVEL = 0
          BEGIN CASE
              CASE COA.LEVEL = 0
                  T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : T.ACCT
              CASE COA.LEVEL = 1
                  T.ACCT = CASH.DIV : GEN.DEPT : GEN.CCTR : T.ACCT
              CASE COA.LEVEL = 2
                  T.ACCT = CASH.DIV : CASH.DEPT : GEN.CCTR : T.ACCT
              CASE 1
                  T.ACCT = CASH.DIV : CASH.DEPT : CASH.CCTR : T.ACCT
          END CASE
          GOSUB 1000
      NEXT I
      VCH.GLA.DATE = 'P'
      MATWRITE VCH.REC ON VOUCHERS, VCH.ID
999 REPEAT
  ECD.ACTION = 99 ; CALL SCRN.EDIT
  GOTO 99999
1000 *
  ETR.ID = CONO : T.ACCT : OLD.VEND<1,1>
  BEGIN CASE
      CASE DIST.AMT > 0
          ETR.ID = ETR.ID : "!D*"
      CASE DIST.AMT + 0 < 0
          ETR.ID = ETR.ID : "!C*"
      CASE 1
          GOTO 1999
  END CASE
  IF VCNT > 1 THEN
      NEW.REC = 0
      FND = 0
      LOOP
          MATREAD ETR.REC FROM EOM.TRANS, ETR.ID:NEW.REC ELSE
              MAT ETR.REC = ''
              ETR.RDATE = OLD.VEND.NAME
          END
          IF ETR.RDATE # OLD.VEND.NAME THEN
              NEW.REC = NEW.REC + 1
          END ELSE
              ETR.ID = ETR.ID:NEW.REC
              FND = 1
          END
      UNTIL FND DO REPEAT
  END ELSE
      ETR.ID = ETR.ID:NEW.REC
      MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
          MAT ETR.REC = ''
      END
  END
  ETR.AMT = ETR.AMT + DIST.AMT
  LOCATE VCH.NO IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
  INS VCH.NO BEFORE ETR.TRAN<1,PTR>
  INS VCH.INV.DATE BEFORE ETR.DATE<1,PTR>
  INS DIST.AMT BEFORE ETR.TAMT<1,PTR>
  INS VCH.INV BEFORE ETR.REF<1,PTR>
  ETR.RDATE = OLD.VEND.NAME
  IF PTR > 99 THEN NEW.PTR = 1
  MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
1999 RETURN
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99000 
* IF FR.NEXT.PER = "" THEN;* T23278
  IF FR.NEXT.PER<1,POS> = "" THEN;* T23278
      INBUFF<5> = "END"
      PROCWRITE INBUFF
  END
*** END OF JOB ***
99999 END
