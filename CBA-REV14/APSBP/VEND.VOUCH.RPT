***************************************
* REVISION - [08.0]                   *
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*                                     *
* PROGRAM  - APSBP VEND.VOUCH.RPT     *
*                                     *
* AUTHOR   - CURTIS WILLIAMS, CBA     *
*                                     *
* DATE     - MAR. 02, 1987            *
***************************************
*
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>COM1
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>VEND
*
*----PROCS VALUES
*
   PROCREAD PROC.VALUES ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC" ; GOTO 93000
   END
   CONO = PROC.VALUES<1>
*T26493 v
*  CONO.NAME = PROC.VALUES<2>
   REPORT.NUMBER = PROC.VALUES<2>
   CONO.NAME = ""
*T26493 ^
   ST.PERIOD = PROC.VALUES<3>
   ED.PERIOD = PROC.VALUES<4>
   PO.TYPE = PROC.VALUES<7>
*
*
*----OPEN FILES
*
   OPEN '','INVENTORY' TO INVENTORY ELSE ERR.MSG = "CANNOT OPEN INVENTORYFILE";GOTO 93000
   OPEN '','VEND.VOUCH.STATS' TO VEND.VOUCH.STATS ELSE ERR.MSG = "CANNOT OPEN VEND.VOUCH.STATS";GOTO 93000
   OPEN '','VEND' TO VEND ELSE ERR.MSG = "CANNOT OPEN VEND";GOTO 93000
*
*----INIT
*
   LN.CNT = 99; PGNO = 0
   VOUCH.TOT = 0
   VEN.VOU.TOT = 0
   GRD.VOU.TOT = 0
   PREV.VOUCH = ''; PREV.VEND = ''
   VOU.QTY = ''
   SP = " "
   MAT INV.CNV.REC = ''
   FST.TIME = 1
*
*
*----SETUP HEADING
*
REPORT.NAME=''
   REPORT.NUMBER = PROC.VALUES<2>
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   BEGIN CASE
      CASE PO.TYPE = "R"
         HD3 = SPACE(43):"FROM ":ST.PERIOD[1,4]:SP:ST.PERIOD[5,2]:" THROUGH ":ED.PERIOD[1,4]:SP:ED.PERIOD[5,2]:" FOR REGULAR P/O's"
      CASE PO.TYPE = "M"
         HD3 = SPACE(39):"FROM ":ST.PERIOD[1,4]:SP:ST.PERIOD[5,2]:" THROUGH ":ED.PERIOD[1,4]:SP:ED.PERIOD[5,2]:" FOR MISCELLANEOUS P/O's"
      CASE 1
         HD3 = SPACE(43):"FROM ":ST.PERIOD[1,4]:SP:ST.PERIOD[5,2]:" THROUGH ":ED.PERIOD[1,4]:SP:ED.PERIOD[5,2]:" FOR OUTSIDE P/O's"
   END CASE
   HD4 = " <--- VOUCHERED --->"
   HD5 = "VOUCH#  INV NUM   INV DATE PERIOD PO NUMBER  "
   HD6 = "------ ---------- -------- ------ ---------- "
   IF PO.TYPE # "O" THEN
      HD4 = SPACE(45):"PRODUCT NUMBER":SPACE(37):HD4
      HD5 = HD5:"  PRODUCT DESCRIPTION                      WHSE U/M"
      HD6 = HD6:"------------------------------------------ ---- ---"
   END ELSE
      HD4 = SPACE(45):"JOB NUMBER":SPACE(41):HD4
      HD5 = HD5:"  JOB DESCRIPTION                          CATG OSP"
      HD6 = HD6:"------------------------------------------ --------"
   END
   HD5 = HD5:" QUANTITY   AMOUNT"
   HD6 = HD6:" -------- ----------"
*
*----MAINT PROCESS
*
   PRINTER ON
1*
   READNEXT ID ELSE GOTO 99999
   MATREAD VVOS.REC FROM VEND.VOUCH.STATS, ID ELSE GOTO 111
   GOSUB 1000
*
*----START PROCESSING
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
      MATREAD VVOS.REC FROM VEND.VOUCH.STATS, ID ELSE GOTO 111
      GOSUB 1000
111 REPEAT
   GOSUB 8000
   GOTO 99999
*
1000*--TEST KEY
*
   VEND.NUM = FIELD(ID,"!",1)[4,10]
   VOUCH.NUM = FIELD(ID,"!",2)
   BEGIN CASE
      CASE VEND.NUM # PREV.VEND
         IF FST.TIME THEN
            GOSUB 9000
            FST.TIME = 0
         END ELSE
            GOSUB 7000
            GOSUB 9000
         END
         PREV.VEND = VEND.NUM
         PREV.VOUCH = VOUCH.NUM
      CASE VOUCH.NUM # PREV.VOUCH
         GOSUB 6000
         PREV.VOUCH = VOUCH.NUM
   END CASE
   IF PO.TYPE = "M" THEN
      PROD.CNT = COUNT(VVOS.PROD.DESC,VM) + (VVOS.PROD.DESC # "")
   END ELSE
      PROD.CNT = COUNT(VVOS.PROD,VM) + (VVOS.PROD # "")
   END
*
*--- PRINT DATA
*
   FOR PCNT = 1 TO PROD.CNT
      GOSUB 10000
      P.LINE = ''
      IF PCNT = 1 THEN
         P.LINE = PREV.VOUCH"L#6":SP:VVOS.INV.NO"L#10":SP:OCONV(VVOS.INV.DATE,"D2/")"L#8":SP:VVOS.VOU.PERIOD"L#6":SP
      END ELSE
         P.LINE = SPACE(34)
      END
      P.LINE = P.LINE:VVOS.PO.NO<1,PCNT>"L#10":SP:VVOS.PROD<1,PCNT>"L#15":SPACE(27):SP
      IF PO.TYPE = "O" THEN
         P.LINE = P.LINE:VVOS.WHSE<1,PCNT>"L#8":SP
      END ELSE
         P.LINE = P.LINE:VVOS.WHSE<1,PCNT>"L#4":SP:VVOS.U.M<1,PCNT>"L#3":SP
      END
      P.LINE = P.LINE:OCONV(VOU.QTY<1,PCNT>,ICR.CNV<PCNT>)"R#8":SP:OCONV(VVOS.PROD.COST<1,PCNT>,"MD2")"R#10":SP
      PRINT P.LINE; LN.CNT = LN.CNT + 1
      IF LN.CNT > 55 THEN GOSUB 9000
      P.LINE = ''
      P.LINE = SPACE(47):VVOS.PROD.DESC<1,PCNT>"L#44"
      PRINT P.LINE; LN.CNT = LN.CNT + 1
      IF LN.CNT > 55 THEN GOSUB 9000
      VOUCH.TOT = VOUCH.TOT + VVOS.PROD.COST<1,PCNT>
   NEXT PCNT
   RETURN
*
*----TOTAL FOR VOUCHER BREAK
*
6000 PRINT SPACE(106):"----------"
   PRINT SPACE(90):"VOUCHER TOTAL - ":OCONV(VOUCH.TOT,"MD2")"R#10"
   PRINT
   LN.CNT = LN.CNT + 3
   IF LN.CNT > 55 THEN GOSUB 9000
   VEN.VOU.TOT = VEN.VOU.TOT + VOUCH.TOT
   VOUCH.TOT = 0
   RETURN
*
*----TOTAL FOR VENDOR BREAK
*
7000 GOSUB 6000
   PRINT SPACE(106):"----------"
   PRINT SPACE(91):"VENDOR TOTAL - ":OCONV(VEN.VOU.TOT,"MD2")"R#10"
   PRINT
   LN.CNT = LN.CNT + 3
   IF LN.CNT > 55 THEN GOSUB 9000
   GRD.VOU.TOT = GRD.VOU.TOT + VEN.VOU.TOT
   VEN.VOU.TOT = 0
   RETURN
*
*----GRAND TOTAL
*
8000 GOSUB 7000
   PRINT SPACE(106):"----------"
   PRINT SPACE(92):"GRAND TOTAL - ":OCONV(GRD.VOU.TOT,"MD2")"R#10"
   RETURN
*
*----PRINT HEADING
*
9000 MATREAD VEND.REC FROM VEND, CONO:VEND.NUM ELSE VEND.DESC = "UNKOWN"
   PRINT CHAR(12)
   PGNO = PGNO + 1
   PRINT HD1:PGNO
   PRINT HD2
   PRINT HD3
   PRINT "VENDOR :":SP:VEND.NUM"L#8":SP:VEND.DESC"L#30"
   PRINT
   PRINT HD4
   PRINT HD5
   PRINT HD6
   LN.CNT = 8
   RETURN
*
*----CONVERT
*
10000 BEGIN CASE
      CASE PO.TYPE = "R"
         MATREAD INV.REC FROM INVENTORY, CONO:VVOS.PROD<1,PCNT> ELSE
            MAT INV.REC = ""; INV.FULL.DESC = "UNKNOWN"
         END
      CASE 1
         MAT INV.REC = ''
   END CASE
   IF INV.SBR + 0 = 0 THEN INV.SBR = 1
   BEGIN CASE
      CASE VVOS.U.M<1,PCNT> = "SHT" AND INV.UNIT<1,3> = "LBS" AND PO.TYPE = "R"
         ICR.CNV<PCNT> = "MD0"; ICR.DV2<PCNT> = 1
         ICR.DV1<PCNT> = INV.M.WT; ICR.MT1<PCNT> = 10
      CASE VVOS.U.M<1,PCNT> = "PC" AND INV.UNIT<1,3> = "MSI" AND PO.TYPE = "R"
         ICR.CNV<PCNT> = "MD0"; ICR.DV2<PCNT> = 1
         ICR.DV1<PCNT> = INV.PAP.WIDTH/100; ICR.MT1<PCNT> = 100
      CASE VVOS.U.M<1,PCNT> = "FT" AND INV.UNIT<1,3> = "MSI" AND PO.TYPE = "R"
         ICR.CNV<PCNT> = "MD0"; ICR.DV2<PCNT> = 12
         ICR.DV1<PCNT> = INV.PAP.WIDTH/100; ICR.MT1<PCNT> = 1000
      CASE 1
         ICR.CNV<PCNT> = "MD2"; ICR.DV2<PCNT> = 1
         ICR.DV1<PCNT> = 1; ICR.MT1<PCNT> = 1
   END CASE
   VOU.QTY<1,PCNT> = INT(((VVOS.VOU.QTY<1,PCNT>/ICR.DV1<PCNT>)*ICR.MT1<PCNT>)/ICR.DV2<PCNT> + .5)
   RETURN
**** CALLS FOR SYSCOM
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER OFF
END
