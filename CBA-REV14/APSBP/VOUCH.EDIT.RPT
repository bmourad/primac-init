*********************************************************************
* REVISION    - [10.2]
* Copyright 1996 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - VOUCH.EDIT.RPT
* DATE        - 01/31/96
* BY          - RICK BROWN (VERCOM)
* DESCRIPTION - This program prints the voucher edit listing.
*T21008 gat 09/23/96 * FIX PRINT PROBLEM ON NON PAID ITEMS.
*T26493 cmykleb 05/08/2002 * Change rpt to get the rpt # from the proc
*                            and use GET.PROG.HEAD for the heading.
*ENDDOC
*********************************************************************
*
* File equates
*
*COPY>APS.CPYLIB>TEMP.VOUCHERS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
* Initialization
*
   PAGE.NO = 0
   FIRST.TIME = 1
   TOT.GROS = 0 ; TOT.DISC = 0 ; TOT.ACCT = 0 ; TOT.AP.ACCT = 0
   TOT.PAID = 0
*
* SYSCOM setup
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
* Open files
*
   EM=''
   BLANK.PRINTED=0
   OPEN '','TEMP.VOUCHERS' TO TEMP.VOUCHERS ELSE EM=EM:'TEMP.VOUCHERS '
   OPEN '','COMPANY' TO COMPANY ELSE EM=EM:'COMPANY '
   IF EM#'' THEN
      ERRMSG=EM:'FILE(S) MISSING'
      GOTO 93000
   END
*
* Company
*
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT EXECUTE PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
*T26493 v
*  HD1 = ID<2>
   CONO.NAME = ""
   RPT.NAME = ""
   RPT.NO = ID<2>
   HEAD1 = ""
   HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NO,"",HEAD1,HEAD2)
   HD1 = HEAD1:" 'PL'"
   HD1 = HD1:HEAD2
*T26493 ^
   IF INDEX(HD1,"(PAID)",1) THEN
      PAID.RPT=1
   END ELSE
      PAID.RPT=0
   END
   HDG.DIV = ID<3>
   PRINTER ON
   HD1 = HD1:"'L'"
   HD1 = HD1:"DIVISION : ":HDG.DIV:"'L'"
   IF NOT(PAID.RPT) THEN
      HD1 = HD1:"VOU NO  VENDOR  PO NUMBER  INVOICE NO INV DATE    GROSS AMT DISCOUNT AMT     DIST AMT ACCT CODE DV DP CTR     A/P AMT       PRT DATE'L'"
      HD1 = HD1:"                                      DUE DATE                                                                          NO POST STAT'L'"
      HD1 = HD1:"------ -------- ---------- ---------- -------- ------------ ------------ ------------ --------- -- -- --- ------------ -------------'L'"
   END ELSE
      HD1=HD1:"VOU NO  VENDOR   INVOICE NO PAID DATE  GROSS AMT     PAID AMT   CKDI ACCT   DIST AMT    ACCOUNT  DV DP CTR    AP AMT    PRINT DATE'L'"
      HD1=HD1:"                            CHECK #                                                                                     STATUS'L'"
      HD1=HD1:"------ --------- ---------- --------- ------------ ------------ --------- ------------ --------- -- -- --- ------------ ------------'L'"
   END
   HEADING HD1
*
100*
*
* Heading lines
*
   IF NOT(PAID.RPT) THEN
      DATA.UL=SPACE(47):'------------ ------------ ------------':SPACE(21):'------------'
      GTOT.UL=SPACE(47):'============ ============ ============':SPACE(21):'============'
   END ELSE
      DATA.UL=SPACE(38):'------------ ------------':SPACE(11):'------------':SPACE(21):'------------'
      GTOT.UL=SPACE(38):'============ ============':SPACE(11):'============':SPACE(21):'============'
   END     
*
* Process file
*
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      MATREAD TVO.REC FROM TEMP.VOUCHERS,KEY THEN
         GOSUB 1000
      END
   REPEAT
*
* End of processing
*
   GOSUB 81000
   PRINTER CLOSE
   PRINTER OFF
   STOP
*
* Process record
*
1000*
   BLANK.PRINTED=0
   VOU.TOT.ACCT=0;VOU.TOT.AP.ACCT=0
   VOU.TOT.PAID=0
   VOU.NO = KEY[4,99]
   * print line one
   PRINT VOU.NO "L#6":" ":TVO.VEND<1,1> "L#9":
   IF PAID.RPT THEN
      PRINT " ":TVO.INV"L#10":" ":OCONV(TVO.PAID.DATE,'D2/')"L#10":
   END ELSE
      PRINT TVO.PO "L#11":TVO.INV "L#11":
      PRINT OCONV(TVO.INV.DATE,"D2/") "L#9":
   END
   PRINT OCONV(TVO.GRS.AMT,"MD2") "R#12":" ":
   IF PAID.RPT THEN
      PRINT OCONV(TVO.PAID.AMT,"MD2")"R#12":" ":
      PRINT TVO.DISB.ACCT"L#9":" ":
   END ELSE
      PRINT OCONV(TVO.DSC.AMT,"MD2") "R#12":" ":
   END
   PRINT OCONV(TVO.DIST.AMT<1,1>,"MD2")"R#12":" ":
   PRINT TVO.ACCT<1,1>"L#9":" ":
   PRINT TVO.DIV<1,1> "L#3":TVO.DEPT<1,1> "L#3":TVO.CCTR<1,1> "L#4":
   PRINT SPACE(12):" ":
   IF PAID.RPT THEN
      LAST.FMT = "L#12"
   END ELSE
      LAST.FMT="L#13"
   END
   PRINT OCONV(TVO.PRT.DATE,"D2/") LAST.FMT
   TOT.ACCT = TOT.ACCT + TVO.DIST.AMT<1,1>
   VOU.TOT.ACCT = VOU.TOT.ACCT + TVO.DIST.AMT<1,1>
   TOT.PAID = TOT.PAID + TVO.PAID.AMT
   VOU.TOT.PAID = VOU.TOT.PAID + TVO.PAID.AMT
*   print line 2   
   IF PAID.RPT THEN
      PRINT SPACE(28):TVO.CHK.NO"L#9":SPACE(37):
   END ELSE
      PRINT SPACE(38):OCONV(TVO.DUE.DATE,"D2/") "L#9":SPACE(26):
   END
   ACCT.PTR=2
   AP.PTR = 1
   IF TVO.DIST.AMT<1,2> # "" THEN
      PRINT OCONV(TVO.DIST.AMT<1,2>,"MD2")"R#12":" ":TVO.ACCT<1,2>"L#9":" ":
      PRINT TVO.DIV<1,2> "L#3":TVO.DEPT<1,2> "L#3":TVO.CCTR<1,2> "L#4":
      PRINT SPACE(12):" ":
      PRINT TVO.STATUS[1,13] LAST.FMT
      ACCT.PTR=3
      TOT.ACCT = TOT.ACCT + TVO.DIST.AMT<1,2>
      VOU.TOT.ACCT = VOU.TOT.ACCT + TVO.DIST.AMT<1,2>
   END ELSE
      PRINT """L#12":" ":TVO.AP.ACCT<1,1>"L#9":" ":
      PRINT TVO.AP.DIV<1,1>"L#3":TVO.AP.DEPT<1,1>"L#3":TVO.AP.CCTR<1,1>"L#4":
      PRINT OCONV(TVO.AP.AMT<1,1>,"MD2")"R#12":" ":
      PRINT TVO.STATUS[1,13] LAST.FMT
      AP.PTR=2
      TOT.AP.ACCT = TOT.AP.ACCT + TVO.AP.AMT<1,1>
      VOU.TOT.AP.ACCT = VOU.TOT.AP.ACCT + TVO.AP.AMT<1,1>
   END
   NUM.LN = DCOUNT(TVO.DIST.AMT,@VM)
   FOR II = ACCT.PTR TO NUM.LN
      IF PAID.RPT THEN
         LSPACE=74
      END ELSE
         LSPACE=73
      END
      PRINT SPACE(LSPACE):
      PRINT OCONV(TVO.DIST.AMT<1,II>,"MD2")"R#12":" ":TVO.ACCT<1,II>"L#9":" ":
      PRINT TVO.DIV<1,II> "L#3":TVO.DEPT<1,II> "L#3":TVO.CCTR<1,II> "L#4":
      IF II = 2 THEN
         PRINT TVO.STATUS[1,13] LAST.FMT
      END ELSE
         PRINT
      END
      TOT.ACCT = TOT.ACCT + TVO.DIST.AMT<1,II>
      VOU.TOT.ACCT = VOU.TOT.ACCT + TVO.DIST.AMT<1,II>
   NEXT II
   NUM.AP.LN = DCOUNT(TVO.AP.AMT,@VM)
   IF PAID.RPT THEN
      LSPACE=74
   END ELSE
      LSPACE=73
   END
   FOR II = AP.PTR TO NUM.AP.LN
      PRINT SPACE(LSPACE):
      PRINT """R#12":" ":TVO.AP.ACCT<1,II>"L#9":" ":
      PRINT TVO.AP.DIV<1,II> "L#3":TVO.AP.DEPT<1,II> "L#3":TVO.AP.CCTR<1,II> "L#4":
      PRINT OCONV(TVO.AP.AMT<1,II>,"MD2")"R#12":
      IF II = 1 AND NUM.LN = 1 THEN
         PRINT TVO.STATUS[1,13] LAST.FMT
      END ELSE
         PRINT
      END
      TOT.AP.ACCT = TOT.AP.ACCT + TVO.AP.AMT<1,II>
      VOU.TOT.AP.ACCT = VOU.TOT.AP.ACCT + TVO.AP.AMT<1,II>
   NEXT II
   TOT.GROS = TOT.GROS + TVO.GRS.AMT + 0
   TOT.DISC = TOT.DISC + TVO.DSC.AMT + 0
   DB.CT = COUNT(TVO.ACCT,VM) + (TVO.ACCT#"")
*
* Print voucher totals
*
   IF NUM.AP.LN > 1 OR NUM.LN > 1 THEN
      PRINT DATA.UL
      IF PAID.RPT THEN
         LSPACE=38
         AMT = TVO.PAID.AMT
      END ELSE
         LSPACE=47
         AMT = TVO.DSC.AMT
      END
      PRINT SPACE(LSPACE):OCONV(TVO.GRS.AMT,"MD2") "R#12":" ":
      PRINT OCONV(AMT,"MD2") "R#12":" ":
      IF PAID.RPT THEN
         PRINT SPACE(10):
      END
      PRINT OCONV(VOU.TOT.ACCT,"MD2") "R#12":SPACE(21):
      PRINT OCONV(VOU.TOT.AP.ACCT,"MD2")"R#12"
      PRINT
      BLANK.PRINTED=1
   END ELSE
      PRINT
      BLANK.PRINTED=1
   END
   RETURN
*
* Print grand totals
*
81000*
   IF NOT(BLANK.PRINTED) THEN
      PRINT
   END
   PRINT GTOT.UL
* T21008
   IF PAID.RPT THEN
      LFMT = "L#38"
      AMT1 = TOT.PAID
   END ELSE
      LFMT = "L#47"
      AMT1 = TOT.DISC
   END
*T21008
   PRINT ' *** GRAND TOTALS *** ' LFMT :OCONV(TOT.GROS,"MD2") "R#12":" ":
   PRINT OCONV(AMT1,"MD2") "R#12":" ":
   IF PAID.RPT THEN
      PRINT SPACE(10):
   END
   PRINT OCONV(TOT.ACCT,"MD2")"R#12":
   PRINT SPACE(21):OCONV(TOT.AP.ACCT,"MD2") "R#12"
   RETURN
*
* SYSCOM calls
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINTER CLOSE
   PRINTER OFF
END
