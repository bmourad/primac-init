*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* PROGRAM     - HDREG
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 06/01/86
* DESCRIPTION -
*            This program is the account payable hand check register.
*T20016 01/31/96 RKB * ADD DIV TO HEADING
*C31215 renee 06/23/1998 * Add PRINTER CLOSE statements so report will
*                          print before the PROC prompts if the report
*                          printed satisfactorily.
*T26493 cmykleb 03/27/2002 * Change report to get rpt # from proc.
*T26556 adelgado 05/24/2002 * Change how error are handle.
*ENDDOC
*********************************************************************
*COPY>APS.CPYLIB>HDCH
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
**** INITIALZATION
  LINE.COUNT = 99
  PAGE.NO = 0
  TOT.GRS = 0
  TOT.DSC = 0
  TOT.NET = 0
  ATOT.GRS = 0
  ATOT.DSC = 0
  ATOT.NET = 0
  GTOT.GRS = 0
  GTOT.DSC = 0
  GTOT.NET = 0
  SP1 = " "
  PREV.VEND = ""
  PREV.ACCT = ""
  PREV.CHECK = ""
  PREV.DATE = ""
**** SETUP FOR SYSTEM ERRMSGS
  SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
**** GET PROC VALUES
  PROCREAD PROC.VALUES ELSE
    ERRMSG = "THIS REPORT MUST RUN FROM A PROC." ; GOTO 93000
  END
  CONO = PROC.VALUES<1>
*T20016 v
  DRIVER.DIV = PROC.VALUES<7>
*T20016 ^        
**** OPEN ALL FILES
  OPEN "","HDCH" TO HDCH ELSE ERRMSG = "HDCH FILE IS MISSING" ; GOTO 93000
  OPEN "","COA" TO COA ELSE ERRMSG = "COA FILE IS MISSING" ; GOTO 93000
  OPEN "","VEND" TO VEND ELSE ERRMSG = "VENDOR FILE IS MISSING" ; GOTO 93000
  OPEN "DICT","HDCH" TO D.HDCH ELSE ERRMSG = "HDCH FILE IS MISSING" ; GOTO 93000
* T26556 v
* READV PRINT.DATE FROM D.HDCH, CONO:"HDDATE",1 ELSE ERRMSG = "REPORT DATE IS MISSING" ; GOTO 93000
  READV PRINT.DATE FROM D.HDCH, CONO:"HDDATE",1 ELSE ERRMSG = "REPORT DATE IS MISSING" ; GOSUB 91000 ; GOTO 99999
* T26556 ^
  PRINT.DATE = ICONV(PRINT.DATE , "D2")
  OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING";GOTO 93000
  OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY NOT ON FILE" ; GOTO 93000
**** GET VALUES FROM CONTROL FILES
*
  MATREAD GLTABLE.REC FROM CONTROL , CONO:"GLTABLE" ELSE ERRMSG = "GLTABLE IS MISSING"; GOTO 93000
  MATREAD COMP.REC FROM COMPANY , CONO ELSE ERRMSG = "COMPANY RECORD IS MISSING" ; GOTO 93000
**** SETUP HEADING
*
  CONO.NAME = PROC.VALUES<2>
  IF PROC.VALUES<6> = "R" THEN
    REPORT.NAME = "ACCOUNTS PAYABLE HAND CHECK REGISTER"
  END ELSE
    REPORT.NAME = "ACCOUNTS PAYABLE HAND CHECK REGISTER (UPDATE)"
  END
*T26493 v
*  REPORT.NUMBER = "XXXX"
  CONO.NAME = ""
  REPORT.NAME = ""
  REPORT.NUMBER = PROC.VALUES<2>
*T26493 ^
  REPORT.DATE = DATE()
  HD1 = "" ; HD2 = ""
  CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
**** OPEN ALL FILES
  HD3 = "CHECK# CHK DATE VENDOR #           VENDOR NAME         INVOICE NO  INV DATE VOUCH#   GROSS AMT     DISC AMT     NET AMOUNT  "
  HD4 = "------ -------- -------- ----------------------------- ---------- --------- ------ ------------- ------------- -------------"
  HD5 = "                                                                                   ------------- ------------- -------------"
  HD6 = "                                                                                   ============= ============= ============="
  PRINTER ON
*
**** READ AND PROCESS FIRST REPORD
*
100*
  READNEXT ID ELSE
    PRINTER OFF
    PRINTER CLOSE  ; * C31215
*T26493 v
*     ERRMSG = "NO ITEMS SELECTED" ; GOSUB 91000 ; GOTO 99999
    PROC.VALUES = ""
    PROC.VALUES<1> = "END"
    PROCWRITE PROC.VALUES
    GOTO 99999
*T26493 ^
  END
  MATREAD HDCK.REC FROM HDCH, ID ELSE GOTO 100
  PREV.ACCOUNT = HDCK.DISB.ACCT
  MATREAD COA.REC FROM COA , CONO:PREV.ACCOUNT ELSE COA.DESC = "????????"
  PREV.CHECK = HDCK.NUM
*CSF 25936 v
  PREV.DATE = HDCK.PAID.DATE
  PREV.VEND = HDCK.VEND<1,1>
*CSF 25936 ^
  GOSUB 1000
**** PROCESS ALL RECORDS
200*
  DATA = 1
  LOOP
    READNEXT ID ELSE DATA = 0
  WHILE DATA = 1 DO
    MATREAD HDCK.REC FROM HDCH , ID ELSE GOTO 299
    GOSUB 900
    GOSUB 1000
299 REPEAT
  GOSUB 2000
  GOSUB 3000
  GOSUB 4000
  GOTO 99999
  RETURN
*
**** FIND BREAK
*
900*
  BEGIN CASE
    CASE PREV.ACCOUNT # HDCK.DISB.ACCT
      PRT.FLG = 1
      GOSUB 2000
      TOT.GRS = 0 ; TOT.DSC = 0 ; TOT.NET = 0
      GOSUB 3000
      ATOT.GRS = 0 ; ATOT.DSC = 0 ; ATOT.NET = 0
      PREV.ACCOUNT = HDCK.DISB.ACCT
      MATREAD COA.REC FROM COA , CONO:PREV.ACCOUNT ELSE COA.DESC = "????????"
      PREV.CHECK = HDCK.NUM
      PREV.DATE = HDCK.PAID.DATE
      PREV.VEND = HDCK.VEND<1,1>
      LINE.COUNT = 99
    CASE PREV.CHECK # HDCK.NUM
      GOSUB 2000
      PRT.FLG = 1
      TOT.GRS = 0 ; TOT.DSC = 0 ; TOT.NET = 0
      PREV.CHECK = HDCK.NUM
      PREV.DATE = HDCK.PAID.DATE
      PREV.VEND = HDCK.VEND<1,1>
  END CASE
  RETURN
*
**** PRINT VOUCHER LINES AND TOTAL CHECKS
*
1000*
  IF LINE.COUNT > 55 THEN
    GOSUB 8000
    PRT.FLG = 1
  END
  P.LINE = ""
  IF PRT.FLG = 1 THEN
    P.LINE = P.LINE: HDCK.NUM "R#6":SP1
    P.LINE = P.LINE: OCONV(HDCK.PAID.DATE, "D2/") "R#8":SP1
    P.LINE = P.LINE: HDCK.VEND<1,1> "L#8":SP1
*CSF 25936 v
    VFND = 1
    IF HDCK.VEND<1,2> # "" THEN
      VEND.DESC = HDCK.VEND<1,2>
      VFND = 0
    END ELSE
*         MATREAD VEND.REC FROM VEND , CONO:HDCK.VEND<1,1> ELSE VEND.DESC = "VENDOR NOT ON FILE"
      MATREAD VEND.REC FROM VEND , CONO:HDCK.VEND<1,1> ELSE VEND.DESC = "VENDOR NOT ON FILE"; VFND = 0
    END
*CSF 25936 ^
    P.LINE = P.LINE: VEND.DESC "L#30":SP1
    PRT.FLG = 0
  END ELSE
    P.LINE = SPACE(56)
  END
  P.LINE = P.LINE:HDCK.INV "L#10":SP1
  P.LINE = P.LINE:OCONV(HDCK.INV.DATE, "D2/") "R#8":SP1
  P.LINE = P.LINE:ID[4,6] "L#6":SP1
  P.LINE = P.LINE:OCONV(HDCK.GRS.AMT , "MD2,") "R#13":SP1
  P.LINE = P.LINE:OCONV(HDCK.DSC.AMT , "MD2,") "R#13":SP1
  P.LINE = P.LINE:OCONV(HDCK.GRS.AMT - HDCK.DSC.AMT, "MD2,") "R#13"
  TOT.GRS = TOT.GRS + HDCK.GRS.AMT
  TOT.DSC = TOT.DSC + HDCK.DSC.AMT
  TOT.NET = TOT.NET + (HDCK.GRS.AMT - HDCK.DSC.AMT)
  PRINT P.LINE
  LINE.COUNT = LINE.COUNT + 1
1999 RETURN
*
**** PRINT CHECK TOTAL LINE
*
2000*
  IF LINE.COUNT + 3 > 55 THEN
    GOSUB 8000
  END
  PRINT HD5
  T.LINE = ""
  T.LINE = T.LINE:SPACE(62):"CHECK# "
  T.LINE = T.LINE:PREV.CHECK "L#6"
  T.LINE = T.LINE:"  TOTAL "
  T.LINE = T.LINE:OCONV(TOT.GRS, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(TOT.DSC, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(TOT.NET, "MD2,") "R#13"
  PRINT T.LINE
  PRINT
  ATOT.GRS = ATOT.GRS + TOT.GRS
  ATOT.DSC = ATOT.DSC + TOT.DSC
  ATOT.NET = ATOT.NET + TOT.NET
  LINE.COUNT = LINE.COUNT + 3
*CSF 25936 v
  IF VFND THEN
    MATREADU VEND.REC FROM VEND , CONO:PREV.VEND THEN
      IF PREV.DATE >= VEND.DATE.PD THEN
        VEND.CK.NO.PD = PREV.CHECK
        VEND.PA.AMT = TOT.NET
        VEND.DATE.PD = PREV.DATE
      END
      MATWRITE VEND.REC ON VEND, CONO:PREV.VEND
    END ELSE RELEASE VEND,CONO:PREV.VEND
  END
  VFND = 0
*CSF 25936 ^
  TOT.GRS = 0 ; TOT.DSC = 0 ; TOT.NET = 0
  RETURN
*
**** PRINT ACCOUNT TOTAL
*
3000*
  IF LINE.COUNT + 3 > 55 THEN
    GOSUB 8000
  END
  PRINT HD5
  T.LINE = ""
  T.LINE = T.LINE:SPACE(40):"DISBURSEMENT ACCOUNT "
  T.LINE = T.LINE:PREV.ACCOUNT CO.ACCT.MASK
  T.LINE = T.LINE:"  TOTAL "
  T.LINE = T.LINE:OCONV(ATOT.GRS, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(ATOT.DSC, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(ATOT.NET, "MD2,") "R#13"
  PRINT T.LINE
  PRINT
  GTOT.GRS = GTOT.GRS + ATOT.GRS
  GTOT.DSC = GTOT.DSC + ATOT.DSC
  GTOT.NET = GTOT.NET + ATOT.NET
  LINE.COUNT = LINE.COUNT + 3
  ATOT.GRS = 0 ; ATOT.DSC = 0 ; ATOT.NET = 0
  RETURN
*
***** PRINT GRAND TOTAL
*
4000*
  IF LINE.COUNT + 2 >  55 THEN
    GOSUB 8000
  END
  PRINT HD6
  T.LINE = ""
  T.LINE = T.LINE :SPACE(71):"GRAND TOTAL "
  T.LINE = T.LINE:OCONV(GTOT.GRS, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(GTOT.DSC, "MD2,") "R#13":SP1
  T.LINE = T.LINE:OCONV(GTOT.NET, "MD2,") "R#13"
  PRINT T.LINE
  RETURN
*
**** PRINT HEADING
*
8000*
  PRINT CHAR(12)
  PAGE.NO = PAGE.NO + 1
  PRINT HD1:PAGE.NO
  PRINT HD2
  PRINT SPACE(66):"REPORT DATE : ":OCONV(PRINT.DATE , "D2/")
  PRINT "DIVISION : ":DRIVER.DIV ;*T20016
  PRINT "DISBURSEMENT ACCOUNT : ":PREV.ACCOUNT CO.ACCT.MASK:"  ":COA.DESC
  PRINT
  PRINT HD3
  PRINT HD4
  LINE.COUNT = 7
  RETURN
***** CALLS FOR SYSCOM
91000*
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
  PRINTER OFF
  PRINTER CLOSE  ; * C31215
END
