*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*T28973 v
**COPY>PMC.CPYLIB>COM.VEND
*COPY>POS.CPYLIB>COM.MPO
*COPY>APS.CPYLIB>COM.APS.FILE.VARS
*COPY>APS.CPYLIB>COM.TEMP.VOUCHERS
*COPY>APS.CPYLIB>COM.VOUCHER.MAINT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* PROGRAM     - PURGE.VEND.STATS
* BY          - CURTIS WILLIAMS, C.B.A
* DATE        - 03/03/87
* DESCRIPTION
* This Program is used to dispays all products for a vendor po .
*T21177 diane 01/21/1997 * REV11 UPG CLOSE FORM
*T23278 markt 10/28/1998 * Add check for divisional security
*T25626 lanny 04/12/2001 * Need to check correct type of PO for DIV
*                          SECURITY.
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST)
*T27646 thompson 08/19/2003 * MODIFY FOR MULTI PLATFORM ISSUE
*T28779 lross 02/08/2006 * Retain identity of multiple same Prod/Whse
*                          lines.
*T28973 lross 08/17/2006 * Incorrect COMMONS for call to
*                          PO.PROD.REC.ACT.
********************************************************************
*
*----INSERT FILE EQUATES
*
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>APS.CPYLIB>VEND.STATS
*COPY>APS.CPYLIB>APS.FILE.VARS
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>VEND
*****T23278 v
*COPY>PMC.CPYLIB>PO
*COPY>POS.CPYLIB>MISC.PO
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>PMC.CPYLIB>SECURITY
*****T23278 ^
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*----SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN "","VEND.PO.STATS" TO VEND.PO.STATS ELSE ERRMSG = "CANNOT OPEN VEND.PO.STATS FILE"; GOTO 93000
  OPEN "","VEND.PROD.STATS" TO VEND.PROD.STATS ELSE ERRMSG = "CANNOT OPEN VEND.PROD.STATS FILE"; GOTO 93000
  OPEN "","VEND.VOUCH.STATS" TO VEND.VOUCH.STATS ELSE ERRMSG = "CANNOT OPEN VEND.VOUCH.STATS FILE"; GOTO 93000
  OPEN "","VEND.STATS" TO VEND.STATS ELSE ERRMSG = "CANNOT OPEN VEND.STATS FILE"; GOTO 93000
  OPEN "","PMC.SCREENS" TO M.SCREENS ELSE ERRMSG = "CANNOT OPEN PMC.SCREENS FILE"; GOTO 93000
  OPEN "","VEND" TO VEND ELSE ERRMSG = "CANNOT OPEN VEND FILE"; GOTO 93000
  OPEN "","INVENTORY" TO INVENTORY ELSE ERRMSG = "CANNOT OPEN INVENTORY FILE"; GOTO 93000
  OPEN "","PO" TO  PO ELSE ERRMSG = "CANNOT OPEN PO FILE"; GOTO 93000;* T23278
  OPEN "","MISC.PO" TO  MISC.PO ELSE ERRMSG = "CANNOT OPEN MISC.PO FILE"; GOTO 93000;* T25626
  OPEN "","OUTSIDE.PO" TO  OUTSIDE.PO ELSE ERRMSG = "CANNOT OPEN OUTSIDE.PO FILE"; GOTO 93000;* T25626
  OPEN "","SECURITY" TO SECURITY ELSE ERRMSG = "CANNOT OPEN SECURITY FILE"; GOTO 93000;* T23278
  OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "CANNOT OPEN COMPANY FILE"; GOTO 93000
  OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CANNOT OPEN CONTROL FILE"; GOTO 93000
*
*----PROC VALUES
*
  PROCREAD PROC.VALUES ELSE
    ERRMSG = "THIS REPORT MUST RUN FROM A PROC" ; GOTO 93000
  END
  CONO = PROC.VALUES<1>
  PO.TYPE = PROC.VALUES<7>
  SEL.DIV = PROC.VALUES<8> ;*T25626
  MATREAD COMP.REC FROM COMPANY,CONO ELSE
    ERRMSG = "CANNOT LOCATE COMPANY NAME"
    GOSUB 93000
  END
*
*---INT
*
  LOGNAME = 'LOGNAME'; CALL SYSVARS.SUB(LOGNAME) ; *T27646
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 2
  ECD.SCRN.NAME<1> = "PO.ACT.INQ"
  ECD.SCRN.NAME<2> = "PO.PROD.REC.ACT"
  ECD.ACTION = 1; CALL SCRN.EDIT
*    MAT EDIT.COM = ""
*    TYP = 0
*    CALL EDIT.SUB
  FILL = "#"
  BEGIN.PAGE = 8
  PAGE.SIZE = 5
  LINE.SPACE = 2
  LINES = 0
  OLD.START.LINE=0
  MAT INV.CNV.REC = ""
  LINE.FLAG = ""
  LINE.COST.WT = ""
*
*****T23278 v
  MATREAD SEC.REC FROM SECURITY, CONO:UPCASE(LOGNAME) ELSE
    ERRMSG = "SECURITY RECORD FOR ":UPCASE(LOGNAME):" NOT FOUND"; GOTO 93000
  END
  DIV.FOUND = ''
*****T23278 ^
*
*--- PRINT HEADINGS
*
100*
  MAT SCV.REC = ""
  ECD.SCRN.NO = 1
  ECD.ACTION = 2; CALL SCRN.EDIT
  BEGIN CASE
    CASE PO.TYPE = "O"
      SCV.REC(21)<ECD.SCRN.NO> = "Job number"
      SCV.REC(22)<ECD.SCRN.NO> = "Category"
      SCV.REC(23)<ECD.SCRN.NO> = "--------"
      PO.DESC = "OUTSIDE"
    CASE 1
      SCV.REC(21)<ECD.SCRN.NO> = "Product no"
      SCV.REC(22)<ECD.SCRN.NO> = "Whse U/M"
      SCV.REC(23)<ECD.SCRN.NO> = "---- ---"
      IF PO.TYPE = "M" THEN
        PO.DESC = "MISCELLANEOUS"
      END ELSE
        PO.DESC = "REGULAR"
      END
  END CASE
  SCV.REC(20)<ECD.SCRN.NO> = PO.DESC
  SCV.REC(19)<ECD.SCRN.NO> = PO.TYPE
  SCV.REC(11)<ECD.SCRN.NO> = 0
  SCV.REC(12)<ECD.SCRN.NO> = 0
  SCV.REC(13)<ECD.SCRN.NO> = 0
  ECD.ACTION = 3; CALL SCRN.EDIT
*
*----MAIN PROCESS
*
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 99990
    CASE ECD.RET.VALUE = "N"
200   READNEXT ID ELSE
        X = 0; Y = 23; TYP = 18; MAXL = 1
        PMSG = "End of selected vendors, Do you want to continue (Y/N) : "
        CALL EDIT.SUB
        IF VALUE = "Y" THEN GOTO 100 ELSE GOTO 99990
      END
      IF CONO # ID[1,3] THEN GOTO 200
      MATREADU VPS.REC FROM VEND.PO.STATS, ID ELSE
        RELEASE VEND.PO.STATS, ID; GOTO 200
      END
      VEND.NUM = FIELD(ID,"!",1)[4,99]
      PO.NUM = FIELD(ID,"!",3)
***** T23278 v 25626 v
      GOSUB 80000
      IF ERRMSG # '' THEN GOTO 200
***** T23278 ^ 25626 ^
      MATREAD VEND.REC FROM VEND,CONO:VEND.NUM ELSE VEND.DESC = "UNKOWN"
      ECD.NUM = 1
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = VEND.NUM
      ECD.ACTION = 5; CALL SCRN.EDIT
      ECD.NUM = 2
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = VEND.DESC
      ECD.ACTION = 5; CALL SCRN.EDIT
      ECD.NUM = 3
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = PO.NUM
      ECD.ACTION = 5; CALL SCRN.EDIT
      MATREADU VSTAT.REC FROM VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE ELSE
        ERRMSG = "Vendor ( ":VEND.NUM:" )is not active"
        RELEASE VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE
        GOSUB 91000; GOTO 100
      END
    CASE 1
      MATREADU VSTAT.REC FROM VEND.STATS, CONO:ECD.RET.VALUE:"!":PO.TYPE ELSE
        ERRMSG = "Vendor ( ":ECD.RET.VALUE:" )is not active"
        RELEASE VEND.STATS, CONO:ECD.RET.VALUE:"!":PO.TYPE
        GOSUB 91000; GOTO 100
      END
      MATREAD VEND.REC FROM VEND, CONO:ECD.RET.VALUE ELSE VEND.DESC = "UNKOWN"
      VEND.NUM = ECD.RET.VALUE
      ECD.NUM = 2
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = VEND.DESC
      ECD.ACTION = 5;CALL SCRN.EDIT
300   ECD.NUM = 3
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO  100
      FND = 0
      LOCATE ECD.RET.VALUE IN VSTAT.PO<1> SETTING FND ELSE
        ERRMSG = "Cannot locate PO ( ":ECD.RET.VALUE:" ) for this vendor"
        GOSUB 91000; GOTO 300
      END
      PO.NUM = ECD.RET.VALUE
      ID = CONO:VEND.NUM:"!":PO.TYPE:"!":PO.NUM
      MATREADU VPS.REC FROM VEND.PO.STATS, ID ELSE
        RELEASE VEND.PO.STATS, ID
        ERRMSG = "This Vendor PO is not active"
        GOSUB 91000; GOTO 300
      END
***** T23278 v 25626 v
      GOSUB 80000
      IF ERRMSG # '' THEN GOTO 300
***** T23278 ^ 25626 ^
  END CASE
*
*----QUANITIY CONVERT
*
  IF PO.TYPE = "M" THEN
    LINES = COUNT(VPS.PROD.DESC,VM) + (VPS.PROD.DESC # "")
  END ELSE
    LINES = COUNT(VPS.PROD,VM) + (VPS.PROD # "")
  END
  MAT INV.CNV.REC = ""
  MAT INV.REC = ""
  FOR LN = 1 TO LINES
    BEGIN CASE
      CASE PO.TYPE = "R"
        MATREAD INV.REC FROM INVENTORY, CONO:VPS.PROD<1,LN> ELSE
          MAT INV.REC = ""; INV.FULL.DESC = "UNKNOWN"
        END
      CASE 1
        MAT INV.REC = ''
    END CASE
    IF INV.SBR + 0 = 0 THEN INV.SBR = 1
    IF INV.COST.WT + 0 = 0 THEN INV.COST.WT = 100
    BEGIN CASE
      CASE VPS.U.M<1,LN> = "SHT" AND INV.UNIT<1,3> = "LBS" AND PO.TYPE = "R"
        ICR.CNV<LN> = "MD0"; ICR.DV2<LN> = 1
        ICR.DV1<LN> = INV.M.WT; ICR.MT1<LN> = 10
      CASE VPS.U.M<1,LN> = "PC" AND INV.UNIT<1,3> = "MSI" AND PO.TYPE = "R"
        ICR.CNV<LN> = "MD0"; ICR.DV2<LN> = 1
        ICR.DV1<LN> = INV.PAP.WIDTH/100; ICR.MT1<LN> = 100
      CASE VPS.U.M<1,LN> = "FT" AND INV.UNIT<1,3> = "MSI" AND PO.TYPE = "R"
        ICR.CNV<LN> = "MD0"; ICR.DV2<LN> = 12
        ICR.DV1<LN> = INV.PAP.WIDTH/100; ICR.MT1<LN> = 1000
      CASE 1
        ICR.CNV<LN> = "MD2"; ICR.DV2<LN> = 1
        ICR.DV1<LN> = 1; ICR.MT1<LN> = 1
    END CASE
    SCV.REC(11)<ECD.SCRN.NO> = SCV.REC(11)<ECD.SCRN.NO> + VPS.ORD.AMT<1,LN>
    SCV.REC(12)<ECD.SCRN.NO> = SCV.REC(12)<ECD.SCRN.NO> + VPS.REC.AMT<1,LN>
    SCV.REC(13)<ECD.SCRN.NO> = SCV.REC(13)<ECD.SCRN.NO> + VPS.VOU.AMT<1,LN>
    IF VPS.FLAG<1,LN> = "Y" THEN
      LINE.FLAG<LN> = "<="
    END ELSE
      LINE.FLAG<LN> = ""
    END
    LINE.COST.WT<LN> = INV.COST.WT
  NEXT LN
  LN = 1
  OLD.START.LINE = 0
  IF LINES > 0 THEN
    GOSUB 10000
  END
  ECD.NUM = 11 ; ECD.ACTION = 5 ; CALL SCRN.EDIT
  ECD.NUM = 12 ; ECD.ACTION = 5 ; CALL SCRN.EDIT
  ECD.NUM = 13 ; ECD.ACTION = 5 ; CALL SCRN.EDIT
*
*--- OPTIONS
*
  MORE = 1
  LOOP
    ECD.NUM = 26
    GOSUB 10000
    FOR XXX = START.LINE TO LAST.LINE
      ECD.VALDAT = ECD.VALDAT:",":XXX
    NEXT XXX
    ECD.VALDAT = ECD.VALDAT:",R,S,P,E,F"
    ECD.VALDAT := ',SR,SB,ST'        ;* T25978
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
    ECD.ACTION = 4 ; CALL SCRN.EDIT
    ECD.VALDAT = ""
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'E' OR OPTION = 'END' OR OPTION = ""
        RELEASE
        MORE = 0
      CASE OPTION = "F"
        IF PO.TYPE = "M" THEN
          PD = COUNT(VPS.PROD.DESC,VM) + (VPS.PROD.DESC # "")
        END ELSE
          PD = COUNT(VPS.PROD,VM) + (VPS.PROD # "")
        END
        PL = 0
        LOCATE PO.NUM IN VSTAT.PO<1>,1 SETTING PL ELSE PL = 0
        FOR DL = PD TO 1 STEP -1
          IF LINE.FLAG<DL> # "" THEN
            IF PO.TYPE # "M" THEN
              DELETE VEND.PROD.STATS, ID:"!":VPS.PROD<1,DL>:"!":VPS.WHSE<1,DL>
            END ELSE
              DELETE VEND.PROD.STATS, ID:"!":VPS.SEQ.NO<1,DL>:"!":VPS.WHSE<1,DL>
            END
            IF PL # 0 THEN
              VSTAT.PO.ORDERD<1,PL> = VSTAT.PO.ORDERD<1,PL> - VPS.ORD.AMT<1,DL>
              VSTAT.PO.RECV<1,PL> = VSTAT.PO.RECV<1,PL> - VPS.REC.AMT<1,DL>
              VSTAT.PO.VOUCH<1,PL> = VSTAT.PO.VOUCH<1,PL> - VPS.VOU.AMT<1,DL>
            END
            VPS.PROD = DELETE(VPS.PROD,1,DL,0)
            VPS.PROD.DESC = DELETE(VPS.PROD.DESC,1,DL,0)
            VPS.U.M = DELETE(VPS.U.M,1,DL,0)
            VPS.WHSE = DELETE(VPS.WHSE,1,DL,0)
            VPS.ORD.QTY = DELETE(VPS.ORD.QTY,1,DL,0)
            VPS.ORD.AMT = DELETE(VPS.ORD.AMT,1,DL,0)
            VPS.REC.QTY = DELETE(VPS.REC.QTY,1,DL,0)
            VPS.REC.AMT = DELETE(VPS.REC.AMT,1,DL,0)
            VPS.VOU.QTY = DELETE(VPS.VOU.QTY,1,DL,0)
            VPS.VOU.AMT = DELETE(VPS.VOU.AMT,1,DL,0)
            VPS.SEQ.NO = DELETE(VPS.SEQ.NO,1,DL,0)
            VPS.VOU.NO = DELETE(VPS.VOU.NO,1,DL,0)
          END
        NEXT DL
        IF PO.TYPE = "M" THEN
          IF VPS.SEQ.NO = '' THEN
            IF PL # 0 THEN
              VSTAT.PO = DELETE(VSTAT.PO,1,PL,0)
              VSTAT.PO.ORDERD = DELETE(VSTAT.PO.ORDERD,1,PL,0)
              VSTAT.PO.RECV = DELETE(VSTAT.PO.RECV,1,PL,0)
              VSTAT.PO.VOUCH = DELETE(VSTAT.PO.VOUCH,1,PL,0)
              VSTAT.VOUCH.NOS = DELETE(VSTAT.VOUCH.NOS,1,PL,0)
            END
            DELETE VEND.PO.STATS,ID
          END ELSE
            MATWRITE VPS.REC ON VEND.PO.STATS,ID
          END
        END ELSE
          IF VPS.PROD = '' THEN
            IF PL # 0 THEN
              VSTAT.PO = DELETE(VSTAT.PO,1,PL,0)
              VSTAT.PO.ORDERD = DELETE(VSTAT.PO.ORDERD,1,PL,0)
              VSTAT.PO.RECV = DELETE(VSTAT.PO.RECV,1,PL,0)
              VSTAT.PO.VOUCH = DELETE(VSTAT.PO.VOUCH,1,PL,0)
              VSTAT.VOUCH.NOS = DELETE(VSTAT.VOUCH.NOS,1,PL,0)
            END
            DELETE VEND.PO.STATS,ID
          END ELSE
            MATWRITE VPS.REC ON VEND.PO.STATS,ID
          END
        END
        IF VSTAT.PO = '' THEN
          DELETE VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE
        END ELSE
          MATWRITE VSTAT.REC ON VEND.STATS, CONO:VEND.NUM:"!":PO.TYPE
        END
        MORE = 0
      CASE OPTION = 'S'
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 10000
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 10000
      CASE OPTION = 'ST'
        LN = 1
        GOSUB 10000
      CASE OPTION = 'SB'
        LN = LINES
        GOSUB 10000
      * T25978 ^
      CASE OPTION = "P"
        GOSUB 70000
        IF LNO THEN
          LN = LNO
          LINE.FLAG<LN>= "<="
          VPS.FLAG<LN> = "Y"
          SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          P_X  = 77 ; P_Y = SLN ; P_VALUE = "<=" ; P_OPT = ""
          CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        END
      CASE OPTION = "R"
        GOSUB 70000
        IF LNO THEN
          LN = LNO
          IF LINE.FLAG<LN>= "" THEN
            ERRMSG = "This product has not been purged"
            GOSUB 91000
          END ELSE
            LINE.FLAG<LN>= ""
            VPS.FLAG<LN> = ""
            SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
            P_X  = 77 ; P_Y = SLN ; P_VALUE = CL ; P_OPT = ""
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
          END
        END
      CASE NUM(OPTION)
        LN = OPTION
        IF PO.TYPE # "M" THEN
          MATREAD VPDS.REC FROM VEND.PROD.STATS, ID:"!":VPS.PROD<1,LN>:"!":VPS.WHSE<1,LN> ELSE
            MAT VPDS.REC = ""
          END
        END ELSE
          MATREAD VPDS.REC FROM VEND.PROD.STATS, ID:"!":VPS.SEQ.NO<1,LN>:"!":VPS.WHSE<1,LN> ELSE
            MAT VPDS.REC = ""
          END
        END
        IF VPDS.REC.DATE = "" AND VPDS.VOU.DATE = "" AND VPDS.ORD.DATE = "" THEN
          ERRMSG = "There is no activity for this product" ; GOSUB 91000
        END ELSE
          ECD.SCRN.NO = 2
          SCV.REC(1)<ECD.SCRN.NO> = PO.NUM
          SCV.REC(2)<ECD.SCRN.NO> = PO.TYPE
          SCV.REC(3)<ECD.SCRN.NO> = PO.DESC
          SCV.REC(16)<ECD.SCRN.NO> = VPS.PROD<1,LN>
          SCV.REC(17)<ECD.SCRN.NO> = VPS.PROD.DESC<1,LN>
          SCV.REC(19)<ECD.SCRN.NO> = VPS.U.M<1,LN>
*T28779   SCV.REC(29)<ECD.SCRN.NO> = VPS.WHSE<1,LN>
          SCV.REC(29)<ECD.SCRN.NO> = FIELD(VPS.WHSE<1,LN>,"@",1)
          IF PO.TYPE = "O" THEN
            SCV.REC(30)<ECD.SCRN.NO> = "Job Number"
            SCV.REC(31)<ECD.SCRN.NO> = "Category"
          END ELSE
            SCV.REC(30)<ECD.SCRN.NO> = "Product #"
            SCV.REC(31)<ECD.SCRN.NO> = "Warehouse #"
          END
          ECD.ACTION = 3 ; CALL SCRN.EDIT
          CALL PO.PROD.REC.ACT(OPTION,PO.TYPE,LINE.COST.WT<LN>)
          ECD.SCRN.NO = 1
          ECD.ACTION = 3 ; CALL SCRN.EDIT
          OLD.START.LINE = 0 ; GOSUB 10000
        END
    END CASE
  WHILE MORE DO
  REPEAT
  GOTO 100
*
*---- SCROLL ROUTINE
*
10000*
  START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE=OLD.START.LINE THEN GOTO 10001
  OLD.START.LINE=START.LINE
  CNT=1
  FOR N=START.LINE TO LAST.LINE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = N "L#3" ; P_OPT = ""
    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:VPS.PROD<1,N> "L#15"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    IF PO.TYPE = "O" THEN
*T28779  P_X  = 36 ; P_Y = SLN ; P_VALUE = VPS.WHSE<1,N> "L#8" ; P_OPT = ""
      P_X  = 36 ; P_Y = SLN ; P_VALUE = FIELD(VPS.WHSE<1,N>,"@",1) "L#8" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END ELSE
      P_X  = 36 ; P_Y = SLN ; P_VALUE = VPS.WHSE<1,N> "L#4" ; P_OPT = ""
      P_X  := AM:41 ; P_Y := AM:SLN ; P_VALUE := AM:VPS.U.M<1,N> "L#3"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    IF ICR.CNV<N> = "MD0" THEN
      TEMP = INT(((VPS.ORD.QTY<1,N>/ICR.DV1<N>)*ICR.MT1<N>)/ICR.DV2<N> + .5)
      P_X  = 45 ; P_Y = SLN ; P_VALUE = TEMP "R#10" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      TEMP = INT(((VPS.REC.QTY<1,N>/ICR.DV1<N>)*ICR.MT1<N>)/ICR.DV2<N> + .5)
      P_X  = 56 ; P_Y = SLN ; P_VALUE = TEMP "R#10" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      TEMP = INT(((VPS.VOU.QTY<1,N>/ICR.DV1<N>)*ICR.MT1<N>)/ICR.DV2<N> + .5)
      P_X  = 67 ; P_Y = SLN ; P_VALUE = TEMP "R#10" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END ELSE
      P_X  = 45 ; P_Y = SLN ; P_VALUE = OCONV(VPS.ORD.QTY<1,N>, "MD2") "R#10" ; P_OPT = ""
      P_X  := AM:56 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VPS.REC.QTY<1,N>, "MD2") "R#10"
      P_X  := AM:67 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(VPS.VOU.QTY<1,N>, "MD2") "R#10"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    P_X  = 77 ; P_Y = SLN ; P_VALUE = LINE.FLAG<N> "L#3" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    P_X  = 4 ; P_Y = SLN+1 ; P_VALUE = VPS.PROD.DESC<1,N> "L#40" ; P_OPT = ""
    P_X  := AM:45 ; P_Y := AM:SLN+1 ; P_VALUE := AM:OCONV(VPS.ORD.AMT<1,N>, "MD2") "R#10"
    P_X  := AM:56 ; P_Y := AM:SLN+1 ; P_VALUE := AM:OCONV(VPS.REC.AMT<1,N>, "MD2") "R#10"
    P_X  := AM:67 ; P_Y := AM:SLN+1 ; P_VALUE := AM:OCONV(VPS.VOU.AMT<1,N>, "MD2") "R#10"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT=CNT + 1
  NEXT N
  FOR M=CNT TO PAGE.SIZE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
10001*
  RETURN
*
*** ENTER LINE NUMBER
*
70000*
  GOSUB 10000
  ECD.MINV = OLD.START.LINE
  ECD.MAXV = LAST.LINE
  ECD.NUM = 17; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
  ECD.ACTION = 4; CALL SCRN.EDIT
  LNO = ECD.RET.VALUE
  IF LNO = "" OR LNO = "END" THEN LNO = 0
  RETURN
*
80000 *
***** T23278 v 25626 v
  PODIV=''; ERRMSG=''
  IF SEL.DIV # 'ALL' THEN
    BEGIN CASE
      CASE PO.TYPE = 'R'
        MATREAD PO.REC FROM PO, CONO:PO.NUM ELSE ERRMSG = 'X'
        PODIV = PO.DIV.OWNER
      CASE PO.TYPE = 'O'
        MATREAD OPO.REC FROM OUTSIDE.PO, CONO:PO.NUM ELSE ERRMSG = 'X'
        PODIV = OPO.DIV.OWNER
      CASE PO.TYPE = 'M'
        MATREAD MPO.REC FROM MISC.PO, CONO:PO.NUM ELSE ERRMSG = 'X'
        PODIV = MPO.DIV.OWNER
    END CASE
    IF ERRMSG = '' THEN
      CALL CK.DIV.SEC.SUB(CONO,PODIV,USER.ID,ERRMSG)
    END
  END
  RETURN
***** T23278 ^ 25626 ^
*---- CALLS FOR SYSCOM
91000*
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
  GOTO 99999
99990 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
99999*
END
