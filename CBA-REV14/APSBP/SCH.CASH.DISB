*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM     - SCH.CASH.DISB
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 09/26/83
* DESCRIPTION
*T21177 diane 12/03/1996 * REV11 UPG
* REV11 UPG diane 2/20/97 Remove CHAIN
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST)
*T26685 cmykeb 09/16/2002 * Rev12 division security.
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
   DIM SVEND.REC(10)
   EQU SVEND.NO TO SVEND.REC(1)
   EQU SVEND.DS TO SVEND.REC(2)
   EQU SVEND.PT TO SVEND.REC(3)
   MAT SVEND.REC = ''
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
**************
* OPEN FILES *
**************
   MAT FILE.VARS = ''
   OPEN '', 'APS.SCREENS' TO M.SCREENS ELSE
      ERRMSG = 'APS.SCREENS FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'VEND' TO VEND ELSE
      ERRMSG = 'VEND FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'VENDOR.XREF' TO VENDOR.XREF ELSE
      ERRMSG = 'VENDOR.XREF FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'PREFIX' TO PREFIX ELSE
      ERRMSG = 'PREFIX FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'CONTROL' TO CONTROL ELSE
      ERRMSG = 'CONTROL FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'COMPANY' TO COMPANY ELSE
      ERRMSG = 'COMPANY FILE IS MISSING'
      GOTO 93000
   END
   MAT COMP.REC = ''
   PROCREAD XX ELSE  ERRMSG = "THIS PROCESS MUST RUN FROM PROC" ; GOTO 93000
   CONO = XX<1>
*T26685 v
*  DIV.OWNER = XX<6>
   DIV.OWNER = XX<3>
*T26685 ^
   MATREAD COMP.REC FROM COMPANY ,CONO ELSE ERRMSG = 'COMPANY NOT ON FILE '; GOTO 93000
******************
* INITIALIZATION *
******************
   FILL = "#"
   MAT EDIT.COM.DRIVER = ''
   MAT GEN.XREF.REC = ''
   DASHES = STR('-',80)
   UNKNOWN = STR('?',30)
   PAGE.SIZE = 14
   BEGIN.PAGE = 6
   LINE.SPACE = 1
   LINES = 0
   OLD.START.LINE = 0
*
   GXR.CO = CONO
   GXR.XREF = VENDOR.XREF
   GXR.FILE = VEND
   GXR.TOP.LINE = 'VENDOR XREF SEARCH'
   GXR.HEADING<1,1> = ' VENDOR '
   GXR.HEADING<1,2> = 'DESCRIPTION'
   GXR.ATT<1,1> = 0
   GXR.ATT<1,2> = 1
   GXR.LEN<1,1> = 4
   GXR.LEN<1,2> = 30
*******************
* MAIN PROCESSING *
*******************
100*
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'SCH.CASH.DISB'
   ECD.ACTION=1;CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION=2;CALL SCRN.EDIT
   MATREAD SVEND.REC FROM CONTROL , CONO:"VENDORS":DIV.OWNER ELSE
      MAT SVEND.REC = ''
   END
   IF XX<7> = 'S' THEN
      DELETE CONTROL ,CONO:"VENDORS":DIV.OWNER
      MAT SVEND.REC = ''
   END
200 IF SVEND.PT = "" THEN
      ECD.NUM = 1 ; ECD.ACTION = 4 ; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" AND ECD.RET.VALUE # "E" THEN
         SVEND.PT = ECD.RET.VALUE
      END ELSE
         GOTO 99990
      END
   END
   IF SVEND.PT = 'S' THEN
      P_X  = 25 ; P_Y = 2 ; P_VALUE = "VENDORS TO BE SUPPRESSED" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   END ELSE
      P_X  = 25 ; P_Y = 2 ; P_VALUE = " VENDORS TO BE PAID " ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   END
   IF SVEND.NO # '' THEN
      LINES = COUNT(SVEND.NO, VM) + (SVEND.NO # "")
      LN = 1
   END ELSE
      REQUEST = 'A'
      LOOP
         LN = LINES + 1
         OLD.LINES = LINES
         GOSUB 10010
      WHILE LINES > OLD.LINES DO REPEAT
      LN = 1
   END
   GOSUB 11000
   MORE = 1
   LOOP
      ECD.NUM = 2
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
      ECD.ACTION=4;CALL SCRN.EDIT
      REQUEST = ECD.RET.VALUE
      BEGIN CASE
         CASE REQUEST = 'END' OR REQUEST = 'E'
            MORE = 0
         CASE REQUEST = 'A'
            LOOP
               LN = LINES + 1
               OLD.LINES = LINES
               GOSUB 10010
            WHILE LINES > OLD.LINES DO REPEAT
            LN = LINES
            GOSUB 11000
         CASE REQUEST = 'D'
            GOSUB 10900
            IF LN.NO THEN
               LN = LN.NO
               SVEND.NO = DELETE(SVEND.NO,1,LN,0)
               SVEND.DS = DELETE(SVEND.DS,1,LN,0)
               LINES = COUNT(SVEND.NO, VM) + (SVEND.NO # "")
               IF LN > 1 AND LN > LINES THEN LN = LN - 1
               IF LN < 1 THEN LN = 1
               OLD.START.LINE = 0
            END
            GOSUB 11000
         CASE REQUEST = 'S'
            LN = LN + PAGE.SIZE
            IF LN > LINES THEN LN = 1
            GOSUB 11000
      * T25978 v
         CASE REQUEST = 'SR'
            LN -= PAGE.SIZE
            IF LN < 1 THEN LN = 1
            GOSUB 11000
         CASE REQUEST = 'ST'
            LN = 1
            GOSUB 11000
         CASE REQUEST = 'SB'
            LN = LINES
            GOSUB 11000
      * T25978 ^
         CASE REQUEST = 'F'
            MATWRITE SVEND.REC ON CONTROL , CONO:'VENDORS':DIV.OWNER
            MORE = 0
      END CASE
   WHILE MORE DO REPEAT
   GOTO 99990
10010 GOSUB 11000
   SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
   P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10100 X = 6
   Y = SLN
   TYP = 1
   MAXL = 8
   O.R = 'R'
   CALL EDIT.SUB
   IF VALUE = 'END' OR VALUE = '' THEN
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 10999
   END
   LOCATE VALUE IN SVEND.NO<1>,1 SETTING FND ELSE FND = 0
   IF FND THEN
      ERRMSG = "VENDOR (":VALUE:") ALREADY USED"
      GOSUB 91000
      GOTO 10100
   END
   MATREAD VEND.REC FROM VEND , CONO:VALUE ELSE
      ERRMSG = "VENDOR (":VALUE:") IS NOT ON FILE."
      GOSUB 91000
      GOTO 10100
   END
   SVEND.NO<1,LN> = VALUE
   SVEND.DS<1,LN> = VEND.DESC
   P_X  = 17 ; P_Y = SLN ; P_VALUE = VEND.DESC "L#30" ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   LINES = COUNT(SVEND.NO,VM) + (SVEND.NO # "")
10999 RETURN
10900 GOSUB 11000
   X = 0
   Y = 21
   PMSG = 'ENTER LINE NUMBER :'
   TYP = 3
   MINV = OLD.START.LINE
   IF LAST.LINE < LINES THEN
      MAXV = LAST.LINE
   END ELSE
      MAXV = LINES
   END
   O.R = 'O'
   MAXL = 3
   CALL EDIT.SUB
   P_X  = 0 ; P_Y = 21 ; P_VALUE = "" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   LN.NO = VALUE
   IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
   RETURN
11000 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
   IF START.LINE = OLD.START.LINE THEN GOTO 11999
   OLD.START.LINE = START.LINE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   CNT = 1
   FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
      P_X  := AM:6 ; P_Y := AM:SLN ; P_VALUE := AM:SVEND.NO<1,N> "L#8"
      P_X  := AM:17 ; P_Y := AM:SLN ; P_VALUE := AM:SVEND.DS<1,N> "L#30"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
11999 RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
   GOTO 99999
*
99990 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
*
99999 *
END
