*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.DETAIL.RPT.MISC
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 07/25/84
* DESCRIPTION -
* This program produces a Job Detail Report by Miscellaneous.
**************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM A MENU";GOSUB 91000;GOTO 99999
     CS.FLG = ID<5>
*
*---- DIMENSION RECORDS
*
     DIM SUB.TOTAL.REC(5)
     DIM DEPT.TOTAL.REC(5)
     DIM GRAND.TOTAL.REC(5)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     RPT.NO="XXXX"
     UNKNOWN=STR("?",30)
     DASHES=STR("-",54)
     MAT SUB.TOTAL.REC=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     PREVIOUS.DEPT.NO=''
     PREVIOUS.DEPT=''
     PREVIOUS.CCTR=''
     PREVIOUS.OPER=''
     FIRST.FLAG=1
     NODATA=1
     MAT JCR.REC=''
     MAT JOB.REC=''
     DATE.LAST.WORKED=''
     DATE.ITEMS.THROUGH=''
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     TYPE=FIELD(JCR.KEY,"*",4)
     IF TYPE="EST" THEN GOTO 10
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'MSC' THEN GOTO 10
     IF JCR.LINE.TYPE="P" OR JCR.LINE.TYPE="M" THEN GOTO 10
     NODATA=0
     DEPT.NO=JCR.DEPT
     JOB.NO=JCR.JOB
     IF FIRST.FLAG=1 THEN GOSUB 15
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     I=0
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        OPER.NO=JCR.OPER<1,I>
        TRANS.DATE=JCR.DATE<1,I>
        IF CS.FLG = "S" THEN
           REG.COST=JCR.SALE<1,I,1>
           OT.COST=JCR.SALE<1,I,2>
           DT.COST=JCR.SALE<1,I,3>
        END ELSE
           REG.COST=JCR.COST<1,I,1>
           OT.COST=JCR.COST<1,I,2>
           DT.COST=JCR.COST<1,I,3>
        END
        TOTAL=REG.COST + OT.COST + DT.COST
        IF TOTAL = 0 THEN TOTAL=''
        DESC=JCR.DESC<1,I>
        SREASON=JCR.SPOIL.CODE<1,I>
        REASON=JCR.REASON<1,I>
        TRAN.JOB=JCR.RC.JOB<1,I>
        IF FIRST.FLAG=1 THEN GOTO 11
        IF PREVIOUS.DEPT # DEPT.NO THEN
           GOSUB 50
           GOSUB 60
           IF LINE.CNT >= 50 THEN GOSUB 20
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.CCTR # CCTR.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=''
              H.LINE=H.LINE:DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.OPER # OPER.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.OPER=OPER.NO
        END
11      GOSUB 30
        IF FIRST.FLAG=1 THEN
           FIRST.FLAG=0
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
        END
     NEXT I
     GOTO 10
12*
     GOSUB 50
     GOSUB 60
     GOSUB 70
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFORMATION FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     D.CNT=COUNT(JOB.MS.DATE,VM) + (JOB.MS.DATE # '')
     FOR T=1 TO D.CNT
        IF JOB.MS.DATE<1,T,1> > DATE.LAST.WORKED THEN DATE.LAST.WORKED=JOB.MS.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     T=0;D.CNT=0
     D.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR T=1 TO D.CNT
        IF DATE.TBL<1,T,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,T,1>
     NEXT T
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     PG.NO=PG.NO + 1
     H.LINE='';HLINE1='';HLINE2='';HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     K=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=K:CO.NAME:K
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):'PAGE: ':PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     PRINT JOB.CUST"L#8":SPACE(37):'D E T A I L   R E P O R T (MISCELLANEOUS)':SPACE(24):'#':JOB.NO "L#8":" ":CONSOL
     PRINT CUST.NAME"L#30":SPACE(72):CUST.NAME
     PRINT JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
     PRINT JOB.DESC<1,1>"L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.WORKED,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>"L#30"
     PRINT 'SR: ':SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT
     PRINT 'DEPT  CCTR  OPER':SPACE(16):'MISCELLANEOUS DESCRIPTION':SPACE(18):'REASON':SPACE(17):'DATE      TOTAL   REASON  XFER JOB'
     PRINT '----  ----  ----  ':DASHES:'  --------------------  --------  -------- -------- --------'
     LINE.CNT=9
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF LINE.CNT >= 50 THEN
        GOSUB 20
        H.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT H.LINE
        PRINT STR('-',LEN(H.LINE))
        LINE.CNT=LINE.CNT + 2
     END
     IF DEPT.NO # PREVIOUS.DEPT.NO THEN
        MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
        P.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT P.LINE
        PRINT STR('-',LEN(P.LINE))
        P.LINE=''
        P.LINE=P.LINE:SPACE(18):DESC"L#54":'  ':REASON"L#20":'  ':DATE"R#8":'  ':TOTAL"R#8":' ':SREASON 'L#8':' ':TRAN.JOB"L#8"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 3
     END ELSE
        P.LINE=''
        P.LINE=P.LINE:SPACE(18):DESC"L#54":'  ':REASON"L#20":'  ':DATE"R#8":'  ':TOTAL"R#8":' ':SREASON 'L#8':' ':TRAN.JOB"L#8"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 1
     END
     PREVIOUS.DEPT.NO=DEPT.NO
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     DATE=OCONV(TRANS.DATE,"D2-")
     SUB.TOTAL.REC(1)=SUB.TOTAL.REC(1) + TOTAL
     DEPT.TOTAL.REC(1)=DEPT.TOTAL.REC(1) + TOTAL
     GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + TOTAL
     IF TOTAL # '' THEN TOTAL=OCONV(TOTAL,"MD2")
     RETURN
*
*---- PRINT SUB TOTALS
*
50*
     GOSUB 55
     S.LINE=''
     S.LINE=S.LINE:SPACE(106):'--------'
     PRINT S.LINE
     S.LINE=''
     S.LINE=S.LINE:SPACE(4):'* ':PREVIOUS.CCTR"L#4":'  ':PREVIOUS.OPER"L#4":'  ':OPER.DESC"L#30":SPACE(58):SUB.TOTAL.REC(1)"R#8"
     PRINT S.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT SUB.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- GET VALUES FOR SUB TOTALS
*
55*
     MATREAD OPER.REC FROM OPERATION,CONO:PREVIOUS.OPER ELSE OPER.DESC=UNKNOWN
     IF SUB.TOTAL.REC(1) # '' THEN SUB.TOTAL.REC(1)=OCONV(SUB.TOTAL.REC(1),"MD2")
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     IF DEPT.TOTAL.REC(1) # '' THEN DEPT.TOTAL.REC(1)=OCONV(DEPT.TOTAL.REC(1),"MD2")
     D.LINE=''
     D.LINE=D.LINE:SPACE(106):'--------'
     PRINT D.LINE
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
     D.LINE=D.LINE:SPACE(66):DEPT.TOTAL.REC(1)"R#8"
     PRINT D.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT GRAND TOTALS
*
70*
     IF GRAND.TOTAL.REC(1) # '' THEN GRAND.TOTAL.REC(1)=OCONV(GRAND.TOTAL.REC(1),"MD2")
     G.LINE=''
     G.LINE=G.LINE:SPACE(106):'--------'
     PRINT G.LINE
     G.LINE=''
     G.LINE=G.LINE:"*** GRAND TOTALS ***":SPACE(86):GRAND.TOTAL.REC(1)"R#8"
     PRINT G.LINE
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
