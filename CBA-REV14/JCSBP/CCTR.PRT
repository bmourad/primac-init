*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - CCTR.PERFORM.PRT
* BY          - JIHAD YAMOUT, CBA
* DATE        - 10/04/83
* MODIFIED    - 07/01/88 BY KERRY WILSON
* DESCRIPTION
* This program prints the Detail Cost Center Performance report.
*********************************************************************
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      SYS.TYPE=1
      CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
      OPEN 'JOB.TIME' TO JOB.TIME ELSE
         ERRMSG ='JOB.FILE IS MISSING'
         GOSUB 91000;GOTO 99999
      END
      OPEN 'COST.CNTR' TO COST.CNTR ELSE
         ERRMSG = 'COST.CNTR FILE IS MISSING'
         GOSUB 91000;GOTO 99999
      END
      OPEN 'EMPLOYEE' TO EMPLOYEE ELSE
         ERRMSG = 'EMPLOYEE FILE IS MISSING'
         GOSUB 91000;GOTO 99999
      END
*
**** READ PROC VALUES
*
      PROCREAD ID ELSE
         ERRMSG = 'CANNOT PERFORM PROCREAD'
         GOSUB 91000;GOTO 99999
      END
      CONO = ID<1>
      CONO.NAME = ID<2>
      REPORT.NAME = "COST.CENTER.PERFORMANCE.DETAIL"
      REPORT.NUMBER = "XXXX"
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
      ST.DATE = ID<4>
      EN.DATE = ID<5>
      ST.PER = ID<6>
      EN.PER = ID<7>
      STD.FLG = ID<8>
      FILE.FLG = ID<9>
*
**** INITIALIZATION
*
      LINE.COUNT = 99
      PAGE.NO = 0
      PRT.FLG = 1
      HH.LINE1 = "                                                     DATE     JOB   "
      HH.LINE2 = "DV DEPT  CTR OPER FC         EMPLOYEE NAME          WORKED   NUMBER "
      UH.LINE  = "-- ----- --- ---- -- ----------------------------- -------- --------"
      UT.LINE  = "                                                                    "
      UT.LINE1 = "                                                                    "
      HH.LINE1 = HH.LINE1:"   ACT     STD      %    CNCR    ACTUAL    ACT    STD        "
      HH.LINE2 = HH.LINE2:"  HOURS   HOURS    EFF   HOURS  QUANTITY  QTY/HR QTY/HR COUNT"
      UH.LINE  = UH.LINE :" ------- ------- ------ ------ ---------- ------ ------ -----"
      UT.LINE  = UT.LINE :" ------- ------- ------ ------                          -----"
      UT.LINE1 = UT.LINE1:" ------- ------- ------ ------ ---------- ------        -----"
*
***** TOTAL TABLE FOR DIV
*
      DIM TOT.DIV(7)
      EQU DIV.TIME        TO TOT.DIV(1)
      EQU DIV.CNCR.TIME   TO TOT.DIV(2)
      EQU DIV.IMP         TO TOT.DIV(3)
      EQU DIV.CNT         TO TOT.DIV(4)
      EQU DIV.STD.HRS     TO TOT.DIV(5)
      EQU DIV.STD.IMP     TO TOT.DIV(6)
      EQU DIV.IMP.HRS     TO TOT.DIV(7)
      MAT TOT.DIV = 0
*
***** TOTAL TABLE FOR DEPT
*
      DIM TOT.DEPT(7)
      EQU DEPT.TIME        TO TOT.DEPT(1)
      EQU DEPT.CNCR.TIME   TO TOT.DEPT(2)
      EQU DEPT.IMP         TO TOT.DEPT(3)
      EQU DEPT.CNT         TO TOT.DEPT(4)
      EQU DEPT.STD.HRS     TO TOT.DEPT(5)
      EQU DEPT.STD.IMP     TO TOT.DEPT(6)
      EQU DEPT.IMP.HRS     TO TOT.DEPT(7)
      MAT TOT.DEPT = 0
*
***** TOTAL TABLE FOR CCTR
*
      DIM TOT.CCTR(7)
      EQU CCTR.TIME        TO TOT.CCTR(1)
      EQU CCTR.CNCR.TIME   TO TOT.CCTR(2)
      EQU CCTR.IMP         TO TOT.CCTR(3)
      EQU CCTR.CNT         TO TOT.CCTR(4)
      EQU CCTR.STD.HRS     TO TOT.CCTR(5)
      EQU CCTR.STD.IMP     TO TOT.CCTR(6)
      EQU CCTR.IMP.HRS     TO TOT.CCTR(7)
      MAT TOT.CCTR = 0
*
***** TOTAL TABLE FOR OPER
*
      DIM TOT.OPER(7)
      EQU OPER.TIME        TO TOT.OPER(1)
      EQU OPER.CNCR.TIME   TO TOT.OPER(2)
      EQU OPER.IMP         TO TOT.OPER(3)
      EQU OPER.CNT         TO TOT.OPER(4)
      EQU OPER.STD.HRS     TO TOT.OPER(5)
      EQU OPER.STD.IMP     TO TOT.OPER(6)
      EQU OPER.IMP.HRS     TO TOT.OPER(7)
      MAT TOT.OPER = 0
*
***** READ AND PROCESS FIRST RECORD
*
      PRINTER ON
100*
      READNEXT KEY ELSE
         ERRMSG = "NO RECORDS SELECTED. TRY AGAIN"
         GOSUB 91000;GOTO 99999
      END
      MATREAD JLB.REC FROM JOB.TIME,KEY ELSE GOTO 100
      MATREAD EMP.REC FROM EMPLOYEE, CONO:JLB.EMP ELSE
         EMP.LAST.NAME = '??????????'
         EMP.FRST.NAME = '??????????'
      END
      EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
      PREV.DIV = JLB.DIV
      PREV.DEPT = FIELD(KEY,"!",2)
      PREV.CCTR = FIELD(KEY,"!",3)
      PREV.OPER = JLB.OPER
      DPNO = PREV.DEPT
      CCNO = PREV.CCTR
      JOBNO = FIELD(KEY,"!",1)[4,99]
      CNT = 0
      CONO = KEY[1,3]
      MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
         ERRMSG = "COST CENTER RECORD IS NOT ON FILE"
         GOSUB 91000;GOTO 100
      END
      CCTR.NAME = CCNO:"-":CCTR.DESC
      LOCATE JLB.OPER IN CCTR.OPER<1>,1 SETTING PP ELSE
         ERRMSG ='OPER CODE CAN NOT BE LOCATED IN COST.CENTER FILE'
         GOSUB 91000;GOTO 100
      END
      IF FILE.FLG = 'C' THEN
         IF STD.FLG = 'N' THEN
            IF CCTR.OPER.STD.HRS<1,PP>+0 = 0 AND CCTR.OPER.STD.IMP<1,PP>+0 = 0 THEN GOTO 100
         END
         HRS.STD = CCTR.OPER.STD.HRS<1,PP>
         IMP.STD = CCTR.OPER.STD.IMP<1,PP>
      END ELSE
         IF STD.FLG = 'N' THEN
            IF JLB.HRS.STD+0 = 0 AND JLB.IMP.STD+0 = 0 THEN GOTO 100
         END
         HRS.STD = JLB.HRS.STD
         IMP.STD = JLB.IMP.STD
      END
      GOSUB 1000
      GOSUB 2000
*
***** READ AND PROCESS ALL RECORDS
*
      DATA = 0
      LOOP
         READNEXT KEY ELSE DATA = 1
      WHILE DATA = 0 DO
         MATREAD JLB.REC FROM JOB.TIME,KEY ELSE GOTO 111
         DPNO = FIELD(KEY,"!",2)
         CCNO = FIELD(KEY,"!",3)
         JOBNO = FIELD(KEY,"!",1)[4,99]
         CONO = KEY[1,3]
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
            ERRMSG = "COST CENTER RECORD IS NOT ON FILE"
            GOSUB 91000;GOTO 111
         END
         CCTR.NAME = CCNO:"-":CCTR.DESC
         LOCATE JLB.OPER IN CCTR.OPER<1>,1 SETTING PP ELSE
            ERRMSG ='OPER CODE CAN NOT BE LOCATED IN COST.CENTER FILE'
            GOSUB 91000;GOTO 111
         END
      IF FILE.FLG = 'C' THEN
         IF STD.FLG = 'N' THEN
         IF CCTR.OPER.STD.HRS<1,PP>+0 = 0 AND CCTR.OPER.STD.IMP<1,PP>+0 = 0 THEN GOTO 111
         END
         HRS.STD = CCTR.OPER.STD.HRS<1,PP>
         IMP.STD = CCTR.OPER.STD.IMP<1,PP>
      END ELSE
         IF STD.FLG = 'N' THEN
            IF JLB.HRS.STD+0 = 0 AND JLB.IMP.STD+0 = 0 THEN GOTO 111
         END
         HRS.STD = JLB.HRS.STD
         IMP.STD = JLB.IMP.STD
      END
         GOSUB 400
         GOSUB 1000
         GOSUB 2000
111*
      REPEAT
      GOSUB 4002
      GOSUB 4003
      GOSUB 4004
      GOSUB 4005
      GOTO 99999
*
**** FIND BREAK AND PRINT TOTALS
*
400*
      BEGIN CASE
      CASE PREV.DIV # JLB.DIV
         GOSUB 4002
         GOSUB 4003
         GOSUB 4004
         GOSUB 4005
         PRINT
         LINE.COUNT = LINE.COUNT + 1
         PRT.FLG = 1
      CASE PREV.DEPT # DPNO
         GOSUB 4002
         GOSUB 4003
         GOSUB 4004
         PRINT
         LINE.COUNT = LINE.COUNT + 1
         PRT.FLG = 1
      CASE PREV.CCTR # CCNO
         GOSUB 4002
         GOSUB 4003
         PRINT
         LINE.COUNT = LINE.COUNT + 1
         PRT.FLG = 1
      CASE PREV.OPER # JLB.OPER
         GOSUB 4002
         PRINT
         LINE.COUNT = LINE.COUNT + 1
         PRT.FLG = 1
      END CASE
      RETURN
*
**** ADD UP ALL TOTALS
*
1000*
      MATREAD EMP.REC FROM EMPLOYEE, CONO:JLB.EMP ELSE
         EMP.LAST.NAME = '??????????'
         EMP.FRST.NAME = '??????????'
      END
      EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
      CNT = CNT + 1
      STD.HRS = ""
      EFF.HRS = ""
      CNCR.TIME = ""
      PRT.IMP = ""
      IMP.HRS = ""
      STD.IMP = ""
      EFF.IMP = ""
*
      IF JLB.HRS+0 >= 0 THEN SIGN = 1 ELSE SIGN = (-1)
      IF JLB.CNCR = 'Y' THEN
         CNCR.TIME = JLB.HRS
      END
      IF CCTR.OPER.TYPE<1,PP> = 'V' OR CCTR.OPER.TYPE<1,PP> = 'U' THEN
         PRT.IMP = JLB.IMP
         IF JLB.HRS+0 # 0 THEN
            IMP.HRS = INT((PRT.IMP / JLB.HRS) + 0.5)
            IMP.HRS = IMP.HRS * SIGN
         END
         IF CCTR.OPER.TYPE<1,PP> = 'U' THEN
*           STD.IMP = (CCTR.OPER.STD.HRS<1,PP> * 100) + CCTR.OPER.STD.IMP<1,PP>
            IF HRS.STD + 0 # 0 THEN
               STD.IMP = INT(100/HRS.STD)
               STD.HRS = HRS.STD
            END ELSE
               STD.IMP = IMP.STD * SIGN
               IF STD.IMP+0 # 0 THEN
                  STD.HRS = INT(PRT.IMP / STD.IMP + 0.5)
               END
            END
         END ELSE
            STD.IMP = IMP.STD * SIGN
            IF STD.IMP+0 # 0 THEN
               STD.HRS = INT(PRT.IMP / STD.IMP + 0.5)
            END
         END
         STD.HRS = STD.HRS * SIGN
         IF STD.IMP+0 > 0 THEN
            EFF.IMP = INT(((IMP.HRS / STD.IMP) * 100) + 0.5)
         END ELSE
            EFF.IMP = 0
         END
      END ELSE
         STD.HRS = HRS.STD
         STD.HRS = STD.HRS * SIGN
      END
      IF JLB.HRS+0 # 0 THEN
         EFF.HRS = INT(STD.HRS * 100 / JLB.HRS * 100 + 0.5)
      END ELSE
         EFF.HRS = 0
      END
*
***** ADD TOTAL FOR OPER
*
      OPER.TIME = OPER.TIME + JLB.HRS
      OPER.STD.HRS = OPER.STD.HRS + STD.HRS
      OPER.CNCR.TIME = OPER.CNCR.TIME + CNCR.TIME
      OPER.IMP = OPER.IMP + PRT.IMP
      OPER.CNT = OPER.CNT + 1
      OPER.STD.IMP = OPER.STD.IMP + STD.IMP
      OPER.IMP.HRS = OPER.IMP.HRS + IMP.HRS
*
**** ADD TOTAL FOR CCTR
*
      CCTR.TIME = CCTR.TIME + JLB.HRS
      CCTR.STD.HRS = CCTR.STD.HRS + STD.HRS
      CCTR.CNCR.TIME = CCTR.CNCR.TIME + CNCR.TIME
      CCTR.IMP = CCTR.IMP + PRT.IMP
      CCTR.CNT = CCTR.CNT + 1
      CCTR.STD.IMP = CCTR.STD.IMP + STD.IMP
      CCTR.IMP.HRS = CCTR.IMP.HRS + IMP.HRS
*
**** ADD TOTAL FOR DEPT
*
      DEPT.TIME = DEPT.TIME + JLB.HRS
      DEPT.STD.HRS = DEPT.STD.HRS + STD.HRS
      DEPT.CNCR.TIME = DEPT.CNCR.TIME + CNCR.TIME
      DEPT.IMP = DEPT.IMP + PRT.IMP
      DEPT.CNT = DEPT.CNT + 1
      DEPT.STD.IMP = DEPT.STD.IMP + STD.IMP
      DEPT.IMP.HRS = DEPT.IMP.HRS + IMP.HRS
*
**** ADD TOTAL FOR DIV
*
      DIV.TIME = DIV.TIME + JLB.HRS
      DIV.STD.HRS = DIV.STD.HRS + STD.HRS
      DIV.CNCR.TIME = DIV.CNCR.TIME + CNCR.TIME
      DIV.IMP = DIV.IMP + PRT.IMP
      DIV.CNT = DIV.CNT + 1
      DIV.STD.IMP = DIV.STD.IMP + STD.IMP
      DIV.IMP.HRS = DIV.IMP.HRS + IMP.HRS
1001*
      RETURN
*
***** PRINT DETAIL LINE
*
2000*
      IF LINE.COUNT > 50 THEN
         GOSUB 9000
         PRT.FLG = 1
      END
      IF PRT.FLG = 1 THEN
         PRT.FLG = 0
         DET.LINE = ''
         DET.LINE = DET.LINE :JLB.DIV "L#2":' ':DPNO "L#5":' '
         DET.LINE = DET.LINE :CCNO "L#3":' ':JLB.OPER "L#4":' '
      END ELSE
         DET.LINE = SPACE(18)
      END
      DET.LINE = DET.LINE :JLB.FCTR "L#2":' '
      DET.LINE = DET.LINE :EMP.NAME "L#29":' '
      DET.LINE = DET.LINE :OCONV(JLB.DATE,"D2/") "L#8":' '
      DET.LINE = DET.LINE :JOBNO "L#8"
      DET.LINE = DET.LINE :OCONV(JLB.HRS,"MD2") "R#8"
      DET.LINE = DET.LINE :OCONV(STD.HRS,"MD2") "R#8"
      DET.LINE = DET.LINE :OCONV(EFF.HRS,"MD2") "R#7"
      DET.LINE = DET.LINE :OCONV(CNCR.TIME,"MD2") "R#7"
      DET.LINE = DET.LINE :OCONV(PRT.IMP,"MD2") "R#11"
      DET.LINE = DET.LINE :IMP.HRS "R#7"
      DET.LINE = DET.LINE :STD.IMP "R#7"
      DET.LINE = DET.LINE :CNT "R#6"
      PRINT DET.LINE
      LINE.COUNT = LINE.COUNT + 1
      RETURN
*
**** PRINT TOTAL FOR OPER
*
4002*
      IF LINE.COUNT > 50 THEN
         GOSUB 9000
      END
      GOSUB 4008
      OP.LINE = ''
      OPER.KEY = CONO:PREV.OPER
      OP.LINE = OP.LINE :SPACE(19):OCONV(OPER.KEY,"TOPERATION;X;1;1") "R#30":' OPERATION TOTAL     ' "L#19"
      OP.LINE = OP.LINE :OCONV(OPER.TIME,"MD2") "R#8"
      OP.LINE = OP.LINE :OCONV(OPER.STD.HRS,"MD2") "R#8"
      IF OPER.TIME > 0 THEN
         OPER.EFF.HRS = INT(OPER.STD.HRS * 100 / OPER.TIME * 100 + 0.5)
      END ELSE
         OPER.EFF.HRS = 0
      END
      OP.LINE = OP.LINE :OCONV(OPER.EFF.HRS,"MD2") "R#7"
      OP.LINE = OP.LINE :OCONV(OPER.CNCR.TIME,"MD2") "R#7"
      OP.LINE = OP.LINE :OCONV(OPER.IMP,"MD2Z") "R#11"
      IF OPER.TIME+0 > 0 THEN
         OP.LINE = OP.LINE :INT(OPER.IMP/OPER.TIME+0.5) "R#7"
      END ELSE
         OP.LINE = SPACE(7)
      END
      OP.LINE = OP.LINE :SPACE(7)
      OP.LINE = OP.LINE :OPER.CNT "R#6"
      PRINT OP.LINE
      LINE.COUNT = LINE.COUNT + 1
      OPER.CNT = 0
      CNT = 0
      PREV.OPER = JLB.OPER
      MAT TOT.OPER = ''
      RETURN
*
**** PRINT TOTAL FOR CCTR
*
4003*
      IF LINE.COUNT > 50 THEN
         GOSUB 9000
      END
      GOSUB 4009
      CCTR.KEY = CONO:PREV.CCTR
      CC.LINE = ''
      CC.LINE = CC.LINE :SPACE(19):OCONV(CCTR.KEY,"TCOST.CNTR;X;1;1") "R#30":' COST CENTER TOTAL   ' "L#19"
      CC.LINE = CC.LINE :OCONV(CCTR.TIME,"MD2") "R#8"
      CC.LINE = CC.LINE :OCONV(CCTR.STD.HRS,"MD2") "R#8"
      IF CCTR.TIME > 0 THEN
         CCTR.EFF.HRS = INT(CCTR.STD.HRS * 100 / CCTR.TIME * 100 + 0.5)
      END ELSE
         CCTR.EFF.HRS = 0
      END
      CC.LINE = CC.LINE :OCONV(CCTR.EFF.HRS,"MD2") "R#7"
      CC.LINE = CC.LINE :OCONV(CCTR.CNCR.TIME,"MD2") "R#7"
      CC.LINE = CC.LINE :SPACE(25)
      CC.LINE = CC.LINE :CCTR.CNT "R#6"
      PRINT CC.LINE
      CCTR.CNT = 0
      LINE.COUNT = LINE.COUNT + 1
      PREV.CCTR = CCNO
      MAT TOT.CCTR = ''
      RETURN
*
**** PRINT TOTAL FOR DEPT
*
4004*
      IF LINE.COUNT > 50 THEN
         GOSUB 9000
      END
      GOSUB 4009
      DEPT.KEY = CONO:PREV.DEPT
      DE.LINE = ''
      DE.LINE = DE.LINE :SPACE(19):OCONV(DEPT.KEY,"TDEPARTMENT;X;2;2") "R#30":' DEPARTMENT TOTAL    ' "L#19"
      DE.LINE = DE.LINE :OCONV(DEPT.TIME,"MD2") "R#8"
      DE.LINE = DE.LINE :OCONV(DEPT.STD.HRS,"MD2") "R#8"
      IF DEPT.TIME > 0 THEN
         DEPT.EFF.HRS = INT(DEPT.STD.HRS * 100 / DEPT.TIME * 100 + 0.5)
      END ELSE
         DEPT.EFF.HRS = 0
      END
      DE.LINE = DE.LINE :OCONV(DEPT.EFF.HRS,"MD2") "R#7"
      DE.LINE = DE.LINE :OCONV(DEPT.CNCR.TIME,"MD2") "R#7"
      DE.LINE = DE.LINE :SPACE(25)
      DE.LINE = DE.LINE :DEPT.CNT "R#6"
      PRINT DE.LINE
      DEPT.CNT = 0
      LINE.COUNT = LINE.COUNT + 1
      PREV.DEPT = DPNO
      MAT TOT.DEPT = ''
      RETURN
*
**** PRINT TOTAL FOR DIV
*
4005*
      IF LINE.COUNT > 50 THEN
         GOSUB 9000
      END
      GOSUB 4009
      DIV.KEY = CONO:PREV.DIV
      DI.LINE = ''
      DI.LINE = DI.LINE :SPACE(19):OCONV(DIV.KEY,"TDIVISION;X;1;1") "R#30":' DIVISION TOTAL      ' "L#19"
      DI.LINE = DI.LINE :OCONV(DIV.TIME,"MD2") "R#8"
      DI.LINE = DI.LINE :OCONV(DIV.STD.HRS,"MD2") "R#8"
      IF DIV.TIME > 0 THEN
         DIV.EFF.HRS = INT(DIV.STD.HRS * 100 / DIV.TIME * 100 + 0.5)
      END ELSE
         DIV.EFF.HRS = 0
      END
      DI.LINE = DI.LINE :OCONV(DIV.EFF.HRS,"MD2") "R#7"
      DI.LINE = DI.LINE :OCONV(DIV.CNCR.TIME,"MD2") "R#7"
      DI.LINE = DI.LINE :SPACE(25)
      DI.LINE = DI.LINE :DIV.CNT "R#6"
      PRINT DI.LINE
      DIV.CNT = 0
      LINE.COUNT = LINE.COUNT + 1
      PREV.DIV = JLB.DIV
      MAT TOT.DIV = ''
      RETURN
*
****** DASH LINE FOR TOTAL
*
4008*
      PRINT UT.LINE1
      LINE.COUNT = LINE.COUNT + 1
      RETURN
*
****** DASH LINE FOR TOTAL
*
4009*
      PRINT UT.LINE
      LINE.COUNT = LINE.COUNT + 1
      RETURN
*
***** PRINT HEADINGS
*
9000*
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      H.LINE = ''
      H.LINE = H.LINE :SPACE(60):"FOR PERIOD ":ST.PER:" THROUGH ":EN.PER
      PRINT H.LINE
      PRINT SPACE(63):"FROM ":OCONV(ST.DATE,"D2/"):" TO ":OCONV(EN.DATE,"D2/")
      PRINT
      PRINT HH.LINE1
      PRINT HH.LINE2
      PRINT UH.LINE
      LINE.COUNT = 8
      RETURN
*
***** ERRMSG ROUTINE
*
91000*
      ERR.TYPE=1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
*
**** END OF JOB
*
99999*
      PRINTER OFF
   END
