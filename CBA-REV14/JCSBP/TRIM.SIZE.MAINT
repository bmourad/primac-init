*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1999 by Computer Business Associates (Vercom Software, Inc.)
*
* SOURCE         - JCSBP
*
* PROGRAM        - TRIM.SIZE.MAINT
*
* DATE           - 03/29/99
*
* DESCRIPTION    -
*                  This program will be used for trim size maintenance.
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN 'COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN 'CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN 'PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
  OPEN 'JOB.TRIM.SIZE' TO JOB.TRIM.SIZE ELSE ERRMSG = 'JOB.TRIM.SIZE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  MAT EDIT.COM.DRIVER = ""
  ERRMSG = ''
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'TRIM.SIZE.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
  SCV.REC(5)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=5
  ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  TRIM.SIZE.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.TRIM.SIZE = 'NO'
      * T26126 v
      READU SIZE.DESC FROM JOB.TRIM.SIZE,CONO:TRIM.SIZE.KEY LOCKED
        ERRMSG = 'TRIM SIZE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE 
      * T26126 ^
        SIZE.DESC=''
        NEW.TRIM.SIZE = 'YES'
      END
  END CASE
  IF NEW.TRIM.SIZE = 'YES' THEN 
    GOSUB 20
    IF ECD.RET.VALUE = 'END' THEN GOTO 10
  END ELSE
    SCV.REC(2)<ECD.SCRN.NO,1>=SIZE.DESC
    ECD.NUM=2;ECD.ACTION = 5; CALL SCRN.EDIT
  END
  GOSUB 100
  GOSUB 200
  GOTO 10
*
*---- ENTER TRIM SIZE DESCRIPTION
*
20*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SIZE.DESC = ECD.RET.VALUE
  END ELSE
    RELEASE JOB.TRIM.SIZE,CONO:TRIM.SIZE.KEY
    GOSUB 200
    GOTO 25
  END
25*
  RETURN
*
*---- ENTER OPTIONS
*
100*
  MORE = 0
  LOOP
    ECD.NUM = 4
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE JOB.TRIM.SIZE,CONO:TRIM.SIZE.KEY     ;* T26126
        MORE = 1
      CASE OPTION = '1'
        GOSUB 20
      CASE OPTION = 'F'
        GOSUB 300
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
200*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE TRIM.SIZE FILE
*
300*
  WRITE SIZE.DESC ON JOB.TRIM.SIZE,CONO:TRIM.SIZE.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
