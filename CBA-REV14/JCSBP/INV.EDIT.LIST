*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - INV.EDIT.LIST
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 02/03/86
* DESCRIPTION -
* This program produces an Invoice edit listing.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>JCS.CPYLIB>DAILY.INVOICE
*COPY>PMC.CPYLIB>COMPANY
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD XX ELSE
        ERRMSG = "MUST BE READ FROM A PROC"
        GOSUB 91000;GOTO 99999
     END
     TYPE.FLAG = XX<3>
     IF TYPE.FLAG # "I" AND TYPE.FLAG # "C" AND TYPE.FLAG # "D" THEN
        ERRMSG = "INVALID INFORMATION PASSED TO PROGRAM"
        GOSUB 91000
        GOTO 99999
     END
*
*---- DIMENTION VARIABLES
*
     DIM PL(300)
     DIM PR(300)
*
*---- OPEN FILES
*
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','DAILY.INVOICE' TO DAILY.INVOICE ELSE ERRMSG='DAILY.INVOICE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     SP=" "
     SP2="  "
     SP10=STR(" ",10)
     SP42=STR(" ",42)
     DSH=STR("-",44)
     DSH40=DSH[1,40]
     DSH13=DSH[1,13]
     EQL13=STR("=",13)
     DSH8=DSH[1,8]
     DSH5=DSH[1,5]
     DSH4=DSH[1,4]
     UNKNOWN=STR("?",30)
     MAT PL = ""
     MAT PR = ""
     INV.TTL=0
     GRAND.INV.TTL=0
     G.PRINT.TOT=0
     G.SHIP.TOT=0
     G.TAX.TOT=0
     NODATA=1
     LL = 5
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
*
*---- GET REPORT HEADING (FIRST TWO LINES)
*
     IF TYPE.FLAG = "I" THEN RNAME="INVOICE EDIT LISTING"
     IF TYPE.FLAG = "C" THEN RNAME="CREDIT MEMO EDIT LISTING"
     IF TYPE.FLAG = "D" THEN RNAME="DEBIT MEMO EDIT LISTING"
     RPT.NO="XXXX"
     RDATE=DATE()
     CALL GET.PROG.HEAD(CONO,CO.NAME,RNAME,RPT.NO,RDATE,HLINE1,HLINE2)
*
*---- MAIN PROCESSING
*
     GOSUB 20
10*
     READNEXT INV.ID ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     MATREAD DI.REC FROM DAILY.INVOICE,INV.ID ELSE GOTO 10
     NODATA=0
     INV.NUM = INV.ID[4,99] "L#8"
     INV.DATE = OCONV(DI.DATE,"D2/") "R#8"
     CUST.NUM = DI.CUST.NO "L#6"
     GOSUB 30
     RR = 0
     CODE.CNT=COUNT(DI.CHG.CODE,VM) + (DI.CHG.CODE # '')
     FOR I=1 TO CODE.CNT
        CODE = DI.CHG.CODE<1,I> "L#4"
        CATG = DI.CHG.CAT<1,I>
        J.S = DI.TAX.JURS<1,I> "L#5"
        IF DI.CHG.JOB = "" THEN
           SUB.JOB = DI.JOB.NO "L#8"
        END ELSE
           SUB.JOB = DI.CHG.JOB<1,I> "L#8"
        END
        QTY = DI.QUANTITY<1,I> "R#8"
        DESC = DI.DESC<1,I> "L#44"
        IF TYPE.FLAG = "C" THEN
           AMOUNT = DI.AMOUNT<1,I> * (-1)
           IF DI.CHG.CODE<1,I> # "SUB" AND DI.CHG.CODE<1,I> # "TOT" AND DI.CHG.CODE<1,I> # "SUBT" THEN
              INV.TTL = INV.TTL + AMOUNT
              GRAND.INV.TTL = GRAND.INV.TTL + AMOUNT
           END
           IF AMOUNT + 0 = 0 THEN AMOUNT = ""
           AMT = OCONV(AMOUNT,"MD2,-") "R#12"
        END ELSE
           IF DI.CHG.CODE<1,I> # "SUB" AND DI.CHG.CODE<1,I> # "TOT" AND DI.CHG.CODE<1,I> # "SUBT" THEN
              INV.TTL = INV.TTL + DI.AMOUNT<1,I>
              GRAND.INV.TTL = GRAND.INV.TTL + DI.AMOUNT<1,I>
           END
           IF DI.AMOUNT<1,I> + 0 = 0 THEN DI.AMOUNT<1,I> = ""
           AMT = OCONV(DI.AMOUNT<1,I>,"MD2,-") "R#12"
        END
        IF DI.CHG.CODE<1,I> # "SUB" AND DI.CHG.CODE<1,I> # "TOT" AND DI.CHG.CODE<1,I> # "SUBT" THEN
           BEGIN CASE
              CASE CATG = "SHP"
                 IF TYPE.FLAG = "C" THEN
                    G.SHIP.TOT = G.SHIP.TOT + AMOUNT
                 END ELSE
                    G.SHIP.TOT = G.SHIP.TOT + DI.AMOUNT<1,I>
                 END
              CASE CATG = "TAX"
                 IF TYPE.FLAG = "C" THEN
                    G.TAX.TOT = G.TAX.TOT + AMOUNT
                 END ELSE
                    G.TAX.TOT = G.TAX.TOT + DI.AMOUNT<1,I>
                 END
              CASE 1
                 IF TYPE.FLAG = "C" THEN
                    G.PRINT.TOT = G.PRINT.TOT + AMOUNT
                 END ELSE
                    G.PRINT.TOT = G.PRINT.TOT + DI.AMOUNT<1,I>
                 END
           END CASE
        END
        GOSUB 40
     NEXT I
     GOSUB 50
     GOTO 10
12*
     GOSUB 60
     PRINTER OFF
     GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     PG.NO=PG.NO + 1
     PRINT HLINE1:PG.NO
     PRINT HLINE2
     PRINT
     PRINT SPACE(11):"INVOICE / CUSTOMER":SPACE(13):"CODE   J/S     JOB#    QUANTITY":SPACE(19):"DESCRIPTION":SPACE(21):"AMOUNT"
     PRINT DSH40:SP2:DSH4:SP2:DSH5:SP2:DSH8:SP2:DSH8:SP2:DSH:SP:DSH13
     PRINT
     LINE.CNT=6
     RETURN
*
*---- FORMAT THE LEFT SIDE OF REPORT
*
30*
     WIP.LINE = ""
     WIP.LINE = "LAST: ": DI.LAST "L#1" : "   WIP(TYPE/DATE/%): "
     WCNT = COUNT(DI.WIP.DATE,VM) + (DI.WIP.DATE # "")
     IF WCNT > 6 THEN WCNT = 6
     FOR WW = 1 TO WCNT
        IF WW < 4 THEN
           IF WW > 1 THEN
              WIP.LINE = WIP.LINE : "   "
           END
           WIP.LINE = WIP.LINE : "(" : DI.WIP.TYPE<1,WW> :"/"
           WIP.LINE = WIP.LINE : OCONV(DI.WIP.DATE<1,WW>, "D2-") "R#8" : "/"
           WIP.LINE = WIP.LINE : OCONV(DI.WIP.PRCNT<1,WW>, "MD2") "R#6" : ")"
        END ELSE
           IF WW = 4 THEN
              WIP.LINE<1,2> = SPACE(28)
           END ELSE
              WIP.LINE<1,2> = WIP.LINE<1,2> : "   "
           END
           WIP.LINE<1,2> = WIP.LINE<1,2> : "(" : DI.WIP.TYPE<1,WW> :"/"
           WIP.LINE<1,2> = WIP.LINE<1,2> : OCONV(DI.WIP.DATE<1,WW>, "D2-") "R#8" : "/"
           WIP.LINE<1,2> = WIP.LINE<1,2> : OCONV(DI.WIP.PRCNT<1,WW>, "MD2") "R#6" : ")"
        END
     NEXT WW
     PL(1) = "INVOICE#: ":INV.NUM:"  ":INV.DATE
     PL(2) = "CUST #  : ":CUST.NUM
     PL(3) = SP10:DI.CUST.NAME "L#30"
     PL(4) = SP10:DI.ADDR1 "L#30"
     LL = 4
     IF DI.ADDR2 # "" THEN
        LL = LL + 1
        PL(LL) = SP10:DI.ADDR2 "L#30"
     END
     IF DI.ADDR3 # "" THEN
        LL = LL + 1
        PL(LL) = SP10:DI.ADDR3 "L#30"
     END
     IF DI.ADDR4 # "" THEN
        LL = LL + 1
        PL(LL) = SP10:DI.ADDR4 "L#35"
     END
     RETURN
*
*---- FORMAT THE RIGHT SIDE OF REPORT
*
40*
     RR=RR + 1
     PR(RR) = CODE:SP2:J.S:SP2:SUB.JOB:SP2:QTY:SP2:DESC:SP:AMT
     AMOUNT = "";AMT = ""
     RETURN
*
*---- PRINT INVOICE AND INVOICE TOTAL
*
50*
     IF LL > RR THEN
        END.LINE = LL
     END ELSE
        END.LINE = RR
     END
     FOR GG = 1 TO END.LINE
        IF LINE.CNT >= 50 THEN GOSUB 20
        PRINT PL(GG) "L#42":PR(GG)
        LINE.CNT = LINE.CNT + 1
     NEXT GG
     PRINT WIP.LINE<1,1>
     LINE.CNT = LINE.CNT + 1
     IF WIP.LINE<1,2> # "" THEN
        PRINT WIP.LINE<1,2>
        LINE.CNT = LINE.CNT + 1
     END
     INV.TOTAL = OCONV(INV.TTL,"MD2,-") "R#13"
     PRINT SPACE(119):DSH13
     PRINT SPACE(110):"TOTAL :  ":INV.TOTAL 
     PRINT
     INV.TOTAL = 0
     INV.TTL = 0
     LINE.CNT=LINE.CNT + 3
     IF LINE.CNT >= 50 THEN GOSUB 20
     MAT PL = ""
     MAT PR = ""
     RETURN
*
*---- PRINT GRAND TOTAL
*
60*
     IF LINE.CNT + 6 >= 54 THEN GOSUB 20
     GRAND.PRINT = OCONV(G.PRINT.TOT,"MD2,-") "R#13"
     GRAND.SHIP = OCONV(G.SHIP.TOT,"MD2,-") "R#13"
     GRAND.TAX = OCONV(G.TAX.TOT,"MD2,-") "R#13"
     GRAND.INV.TOTAL = OCONV(GRAND.INV.TTL,"MD2,-") "R#13"
     PRINT SPACE(119):EQL13
     PRINT SPACE(101):"PRINTING TOTAL :  ":GRAND.PRINT
     PRINT SPACE(95):"TRANSPORTATION TOTAL :  ":GRAND.SHIP
     PRINT SPACE(106):"TAX TOTAL :  ":GRAND.TAX
     PRINT SPACE(119):EQL13
     PRINT SPACE(104):"GRAND TOTAL :  ":GRAND.INV.TOTAL 
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
