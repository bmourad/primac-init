      SUBROUTINE JRS.LB.COST.DET
*COPY>JCS.CPYLIB>COM.JRS
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JRS.LB.COST.DET
* BY          - W.R. MCKENZIE, COMPUTER BUSINESS ASSOCIATES
* DATE        - 05/25/88
* DESCRIPTION -
* This program produces a Labor COST DETAIL Report
*T26556 ajibaly 05/09/2002 * USE RPT NO AND GET.PROG.HEAD
*T28964 wvaughan 08/07/2006 * Extend JOB.QTY field to display 8 numbers.
*************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>JOB.RPT.SET
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>JCS.CPYLIB>JOB.RPT.SUM
*COPY>JCS.CPYLIB>FACTOR
*COPY>JCS.CPYLIB>JOB.TIME
*
*---- INITIALIZATION
*
      PG.NO=0
      ERRMSG=''
*T26556 v
*     RPT.NO="XXXX"
      HD1 = ""; HD2 = ""
      RPT.NAME = ""
      RPT.DATE = DATE()
      CALL GET.PROG.HEAD(CONO,C.CO.NAME,RPT.NAME,RPT.NO,RPT.DATE,HD1,HD2)
      PRINTER ON
*T26556 ^
      LINE.CNT = 75
      DIM R.REC(10)
      DIM C.REC(10)
      DIM S.REC(10)
      DIM D.REC(10)
      DIM G.REC(10)
      DIM PRT.REC(10)
      MAT PRT.REC=''
      MAT R.REC=0
      MAT C.REC=0
      MAT S.REC=0
      MAT D.REC=0
      MAT G.REC=0
      ST.LINE=SPACE(60):'-------- ------- ------- -------- ---------- -------'
      UNKNOWN=STR("?",30)
*
*---- MAIN PROCESSING
*
      IF JRSD.CONS ="Y" THEN
         CONSOL="CONSOLIDATED"
      END ELSE
         CONSOL="            "
      END
      JOB.CNT = DCOUNT(JRS.IDS,AM)
      PTR=0
      FOR I = 1 TO JOB.CNT WHILE PTR=0
         IF FIELD(JRS.IDS<I>,'&',5) # 'L' THEN GOTO 10
         DEPT.NO = FIELD(JRS.IDS<I>,'&',2)
         IF DP.SEL # 'ALL' AND DP.SEL # DEPT.NO THEN GOTO 10
         MATREAD JRS.REC FROM JOB.RPT.SET, CONO:JOBNO:'&':JRS.IDS<I> ELSE GOTO 10
         PTR=I
10    NEXT I
      IF PTR = 0 THEN GOTO 99999
      CCTR.NO = FIELD(JRS.IDS<PTR>,'&',3)
      OPER.NO = FIELD(JRS.IDS<PTR>,'&',4)
      P.DP = DEPT.NO
      P.CCTR=CCTR.NO
      P.OPER=OPER.NO
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE
         MAT DEPT.REC = ""; DEPT.DESC = UNKNOWN
      END
      MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR.NO ELSE
         MAT CCTR.REC = ""
      END
      MATREAD OPER.REC FROM OPERATION,CONO:OPER.NO ELSE
         MAT OPER.REC = ""; OPER.DESC=UNKNOWN
      END
      F.CODE = "01"
      MATREAD FCTR.REC FROM FACTOR, CONO: F.CODE ELSE
         FCTR.PRCNT = 100
      END
      F.PCT = FCTR.PRCNT
      GOSUB 20
      P.LINE=DEPT.DESC:'  ':DEPT.NO
      PRINT P.LINE
      PRINT STR('-',LEN(P.LINE))
      LINE.CNT=LINE.CNT + 2
      GOSUB 1000
      PTR = PTR+ 1
      FOR I = PTR TO JOB.CNT
         IF FIELD(JRS.IDS<I,1>,"&",5) # "L" THEN GOTO 17
         MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<I,1> ELSE GOTO 17
         DEPT.NO = FIELD(JRS.IDS<I,1>,"&",2)
         IF DP.SEL # 'ALL' THEN
            IF DP.SEL # DEPT.NO THEN GOTO 17
         END
         CCTR.NO = FIELD(JRS.IDS<I,1>,"&",3)
         OPER.NO = FIELD(JRS.IDS<I,1>,"&",4)
         IF LINE.CNT >= 50 THEN GOSUB 20
         BEGIN CASE
         CASE DEPT.NO # P.DP
            GOSUB 50
            GOSUB 60
            MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE
               MAT DEPT.REC = ""; DEPT.DESC = UNKNOWN
            END
            MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR.NO ELSE
               MAT CCTR.REC = ""
            END
            MATREAD OPER.REC FROM OPERATION,CONO:OPER.NO ELSE
               MAT OPER.REC = ""; OPER.DESC=UNKNOWN
            END
            P.LINE=DEPT.DESC:'  ':DEPT.NO
            PRINT P.LINE
            PRINT STR('-',LEN(P.LINE))
            LINE.CNT=LINE.CNT + 2
            P.DP = DEPT.NO
            P.CCTR=CCTR.NO
            P.OPER=OPER.NO
         CASE P.CCTR # CCTR.NO
            GOSUB 50
            MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR.NO ELSE
               MAT CCTR.REC = ""
            END
            MATREAD OPER.REC FROM OPERATION,CONO:OPER.NO ELSE
               MAT OPER.REC = ""; OPER.DESC=UNKNOWN
            END
            P.CCTR=CCTR.NO
            P.OPER=OPER.NO
         CASE P.OPER # OPER.NO
            GOSUB 50
            MATREAD OPER.REC FROM OPERATION,CONO:OPER.NO ELSE
               MAT OPER.REC = ""; OPER.DESC=UNKNOWN
            END
            P.OPER=OPER.NO
         CASE 1
         END CASE
         GOSUB 1000
17 *
      NEXT I
      GOSUB 50
      GOSUB 60
      PRINT ST.LINE
      G.LINE="*** GRAND TOTALS ***":SPACE(40):OCONV(G.REC(1),'MD2')"R#8":' ':OCONV(G.REC(2),'MD2')"R#7":' ':OCONV(G.REC(3),'MD2')"R#7":' ':OCONV(G.REC(4),'MD2')"R#8":' ':OCONV(G.REC(5),'MD2')"R#10":' ':OCONV(G.REC(6),'MD2')"R#7"
      PRINT G.LINE
      GOTO 99999
*
*---- PRINT THE HEADINGS
*
20 *
      PRINT CHAR(12)
      PG.NO=PG.NO + 1
      H.LINE='';HLINE1='';HLINE2='';HLINE3=''
      HLINE1="#":JOBNO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
      K=SPACE((50 - LEN(C.CO.NAME)) / 2)
      HLINE2=K:C.CO.NAME:K
      HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):'PAGE: ':PG.NO
      H.LINE=HLINE1:HLINE2:HLINE3
*T26556 v
*     PRINT H.LINE
*     PRINT JOB.CUST"L#8":SPACE(33):'J O B   D E T A I L   R E P O R T (LABOR DOLLARS)':SPACE(20):'#':JOBNO "L#8":" ":CONSOL
      PRINT HD1:PG.NO
      PRINT HD2
      PRINT "Job Number: ":JOBNO "L#8":SPACE(1):CONSOL
      PRINT JOB.CUST "L#8"
*T26556 ^
      PRINT C.CUST.NAME"L#30":SPACE(72):C.CUST.NAME
      *T28964   PRINT JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(JRSD.LB.DATE,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
      PRINT JOB.QTY<1,1>"L#8":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(JRSD.LB.DATE,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>;*T28964
      PRINT JOB.DESC<1,1>"L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(JRSD.DATE,"D2-")"R#8":SPACE(23):JOB.DESC<1,1> "L#30"
      PRINT 'SM: ':C.SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
      PRINT
      PRINT SPACE(60):'--------- ACTUAL COSTS ----------'
      PRINT 'DEPT  CCTR  OPER      EMPLOYEE NAME/NUMBER        DATE         REG     O/T     D/T     TOTAL   QUANTITY   SPOIL   REASON  XFER JOB'
      PRINT '----  ----  ---- ------------------------------ --------    -------- ------- ------- -------- ---------- ------- -------- --------'
*T26556 v
*     LINE.CNT=10
      LINE.CNT=12
*T26556 ^
      RETURN
*
*-----PRINT SUBTOTALS
*
50 *
      D.REC(1)=D.REC(1) + S.REC(1)
      D.REC(2)=D.REC(2) + S.REC(2)
      D.REC(3)=D.REC(3) + S.REC(3)
      D.REC(4)=D.REC(4) + S.REC(4)
      D.REC(5)=D.REC(5) + S.REC(5)
      D.REC(6)=D.REC(6) + S.REC(6)
      PRINT ST.LINE
      LINE.CNT=LINE.CNT + 1
      IF R.REC(1)+R.REC(2)+R.REC(3)+R.REC(4)+R.REC(5)+R.REC(7)+R.REC(6) = 0 THEN REG.FLG = 0 ELSE REG.FLG = 1
      IF C.REC(1)+C.REC(2)+C.REC(3)+C.REC(4)+C.REC(5)+C.REC(7)+C.REC(6) = 0 THEN CO.FLG = 0 ELSE CO.FLG = 1
      IF REG.FLG THEN
         S.LINE=SPACE(40):'*   REG COST TOTAL  ':OCONV(R.REC(1),'MD2')"R#8":' ':OCONV(R.REC(2),'MD2')"R#7":' ':OCONV(R.REC(3),'MD2')"R#7":' ':OCONV(R.REC(4),'MD2')"R#8":' ':OCONV(R.REC(5),'MD2')"R#10":' ':OCONV(R.REC(6),'MD2')"R#7"
         PRINT S.LINE
         LINE.CNT=LINE.CNT + 1
      END
      IF CO.FLG THEN
         S.LINE=SPACE(40):'* CO/AA COST TOTAL  ':OCONV(C.REC(1),'MD2')"R#8":' ':OCONV(C.REC(2),'MD2')"R#7":' ':OCONV(C.REC(3),'MD2')"R#7":' ':OCONV(C.REC(4),'MD2')"R#8":' ':OCONV(C.REC(5),'MD2')"R#10":' ':OCONV(C.REC(6),'MD2')"R#7"
         PRINT S.LINE
         LINE.CNT=LINE.CNT + 1
      END
      PRINT ST.LINE
      S.LINE=SPACE(4):'* ':P.CCTR"L#4":'  '
      S.LINE=S.LINE:P.OPER"L#4":'  '
      S.LINE=S.LINE:OPER.DESC"L#30":SPACE(12):OCONV(S.REC(1),'MD2')"R#8":' ':OCONV(S.REC(2),'MD2')"R#7":' ':OCONV(S.REC(3),'MD2')"R#7"
      S.LINE=S.LINE:' ':OCONV(S.REC(4),'MD2')"R#8":' ':OCONV(S.REC(5),'MD2')"R#10":' ':OCONV(S.REC(6),'MD2')"R#7"
      PRINT S.LINE
      PRINT;PRINT
      LINE.CNT=LINE.CNT + 4
      MAT R.REC=0
      MAT C.REC=0
      MAT S.REC=0
      IF LINE.CNT >= 50 THEN GOSUB 20
      RETURN
*
*---- PRINT DEPT TOTALS
*
60 *
      G.REC(1)=G.REC(1) + D.REC(1)
      G.REC(2)=G.REC(2) + D.REC(2)
      G.REC(3)=G.REC(3) + D.REC(3)
      G.REC(4)=G.REC(4) + D.REC(4)
      G.REC(5)=G.REC(5) + D.REC(5)
      G.REC(6)=G.REC(6) + D.REC(6)
      PRINT ST.LINE
      D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL'
      D.LINE=D.LINE "L#40"
      D.LINE=D.LINE:SPACE(20):OCONV(D.REC(1),'MD2')"R#8":' ':OCONV(D.REC(2),'MD2')"R#7":' ':OCONV(D.REC(3),'MD2')"R#7":' ':OCONV(D.REC(4),'MD2')"R#8":' ':OCONV(D.REC(5),'MD2')"R#10":' ':OCONV(D.REC(6),'MD2')"R#7"
      PRINT D.LINE
      PRINT
      PRINT
      LINE.CNT=LINE.CNT + 4
      MAT D.REC=0
      IF LINE.CNT >= 50 THEN GOSUB 20
      RETURN
*
*-----PRINT LINE
*
1000 *
      MCNT = DCOUNT(JRS.SEQ,VM)
      FOR J = 1 TO MCNT
         SCNT = DCOUNT(JRS.SEQ<1,J>,SVM)
         LB.ID = CONO:JOBNO:"!":DEPT.NO:"!":CCTR.NO:"!"
         EMPL.NO = FIELD(JRS.SORT.DATE<1,J>,"!",1); EMP.NAME=''
         MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPL.NO ELSE
            MAT EMP.REC = ""; EMP.NAME=STR("?",28)
         END
         IF EMP.NAME='' THEN EMP.NAME=EMP.FRST.NAME:' ':EMP.LAST.NAME
         EMP.NAME.NUM=EMP.NAME:'  ':EMPL.NO
         FOR L = 1 TO SCNT
            IF JRS.SUB.JOB<1,J,L> # "" THEN
               LB.ID = CONO:JRS.SUB.JOB<1,J,L>:"!":DEPT.NO:"!":CCTR.NO:"!"
            END
            MATREAD JLB.REC FROM JOB.TIME, LB.ID:JRS.SEQ<1,J,L> ELSE GOTO 1009
            IF JLB.FCTR < 1 OR JLB.FCTR > 3 THEN JLB.FCTR = 1
            IF CS.FLG = "S" THEN JLB.COST = JLB.SALE
            MAT PRT.REC = ''
            IF JLB.TYPE = "N" THEN GOTO 1009
            LOCATE JLB.FCTR IN F.CODE,1 SETTING F.PTR ELSE
               MATREAD FCTR.REC FROM FACTOR, CONO : JLB.FCTR ELSE
                  FCTR.PRCNT = 100
               END
               F.CODE<F.PTR> = JLB.FCTR
               F.PCT<F.PTR> = FCTR.PRCNT
            END
            FCTR = JLB.FCTR
            FCTR.PRCNT = F.PCT<F.PTR>
            BEGIN CASE
            CASE JLB.TYPE ='S'
               TRANS.TYPE = '  '
               PRT.REC(6) =JLB.COST
               R.REC(6)=R.REC(6)+JLB.COST
            CASE JLB.TYPE='C'
               TRANS.TYPE='CO'
               IF FCTR = 1 THEN
                  PRT.REC(1) = JLB.COST
                  C.REC(1) = C.REC(1) + JLB.COST
               END ELSE
                  PRT.REC(1) = INT((JLB.COST / FCTR.PRCNT)*100 +.5)
                  PRT.REC(FCTR) = JLB.COST - PRT.REC(1)
                  C.REC(1) = C.REC(1) + PRT.REC(1)
                  C.REC(FCTR) = C.REC(FCTR) + PRT.REC(FCTR)
               END
               PRT.REC(4)=JLB.COST
               C.REC(4)=C.REC(4)+JLB.COST
               PRT.REC(5)=JLB.IMP
               C.REC(5)=C.REC(5)+JLB.IMP
            CASE 1
               TRANS.TYPE='  '
               IF FCTR = 1 THEN
                  PRT.REC(1) = JLB.COST
                  R.REC(1) = R.REC(1) + JLB.COST
               END ELSE
                  PRT.REC(1) = INT((JLB.COST / FCTR.PRCNT)*100 +.5)
                  PRT.REC(FCTR) = JLB.COST - PRT.REC(1)
                  R.REC(1) = R.REC(1) + PRT.REC(1)
                  R.REC(FCTR) = R.REC(FCTR) + PRT.REC(FCTR)
               END
               PRT.REC(4)=JLB.COST
               R.REC(4)=R.REC(4)+JLB.COST
               PRT.REC(5)=JLB.IMP
               R.REC(5)=R.REC(5)+JLB.IMP
            END CASE
            S.REC(1)=S.REC(1) + PRT.REC(1)
            S.REC(2)=S.REC(2) + PRT.REC(2)
            S.REC(3)=S.REC(3) + PRT.REC(3)
            S.REC(4)=S.REC(4) + PRT.REC(4)
            S.REC(5)=S.REC(5) + PRT.REC(5)
            S.REC(6)=S.REC(6) + PRT.REC(6)
            IF LINE.CNT >= 50 THEN GOSUB 20
            PLINE=SPACE(17):EMP.NAME.NUM'L#30':' ':OCONV(JLB.DATE,"D2/") "R#8":' ':TRANS.TYPE:' ':OCONV(PRT.REC(1),"MD2") "R#8":' '
            PLINE = PLINE:OCONV(PRT.REC(2),"MD2") "R#7":' ':OCONV(PRT.REC(3),"MD2")'R#7':' '
            PLINE=PLINE:OCONV(PRT.REC(4),'MD2')'R#8':' ':OCONV(PRT.REC(5),'MD2')'R#10':' ':OCONV(PRT.REC(6),'MD2')'R#7':' ':JLB.SPOIL.CODE"L#8":' ':JLB.RC.JOB"L#8"
            PRINT PLINE
            LINE.CNT=LINE.CNT+1
1009     NEXT L
      NEXT J
      RETURN
99999 *
      PRINTER OFF
      RETURN
      END
