*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC-JCS
* SOURCE      - JCSBP
* PROGRAM     - CCTR.CHG.MATL.RPT
* DATE        - OCT,29,1983
* BY          - WALID
* DESCRIPTION - BREAK ON COST.CNTR
* SSELECT     - COST.CNTR, DATE, KEY & TYPE # 'N'
*T25901 cmykleb 03/14/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T28139 lross 05/21/2004 * Error in CNV LBS/MSI.
*********************************************************************
*
***   INSERT EQUATE FROM CPYLIB
*
*
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*
***   INITIALIZATION
*
   LINE.COUNT       = 0
   PAGE.NO          = 0
   TOT.REGULAR      = 0
   TOT.CHARGE       = 0
   TOT.SPOLAGE      = 0
   TOT.QTY          = 0
   PREV.VAL         = ''
   REG.QTY          = 0
   SPL.QTY          = 0
   CHG.QTY          = 0
   TOT1.REGULAR     = 0
   TOT1.CHARGE      = 0
   TOT1.SPOLAGE     = 0
   TOT1.REGULAR     = 0
   TOT1.CHARGE      = 0
   TOT1.SPOLAGE     = 0
   TOT1.QTY         = 0
   PREV1.VAL        = ''
   UNKNOWN          = STR("?",30)
*
***   SET UP THE ERRMSG ROUTINE FORM SYSCOM
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
***   OPEN THE FILE
*
   OPEN '','JOB.MATL' TO JOB.MATL ELSE
      ERRMSG = 'JOB.MATL FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG = 'COST.CNTR FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'INVENTORY FILE IS MISSING'
      GOTO 93000
   END
*
***   READ COMPANY NAME
*
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
   CONO.NAME = ID<2>
   S.DATE = ID<3>
   F.DATE = ID<4>
*T25901 v
*T26493 v
*  REPORT.NAME = "CHARGED QTY REPORT BY COST CENTER BY DATE"
*  REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME= ""
   REPORT.NUMBER = ID<2>
*T26493 ^
   HLINE1 = "" ; HLINE2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HLINE1,HLINE2)
*T25901 ^
*
***   PUT PRINTER ON
*
   PRINTER ON
*
***   READ AND PROCESS FIRST RECORD
*
100*
   READNEXT KEY ELSE GOTO 999999
*
*---- BREAK UP KEY
*
   CCTR.NO = FIELD(KEY,"!",3)
   PROD.NO = FIELD(KEY,"!",4)
   DEPT = FIELD(KEY,"!",2)
*
***   READ THE JOB.MATL FILE
*
   MATREAD JMT.REC FROM JOB.MATL,KEY ELSE
      ERRMSG = ' NO RECORD IS READ FROM JOB.MATL'
      GOSUB 92000
      GOTO 100
   END
   MATREAD INV.REC FROM INVENTORY,CONO:PROD.NO ELSE
      MAT INV.REC = ""
   END
   GOSUB 7500
*
***   PRINT HEADINGS
*
   GOSUB 1000
*
***   PROCESS THE FILE
*
   GOSUB 2000
*
***   SET THE COST.CNTR
*
   PREV.VAL = CCTR.NO
   PREV1.VAL = PROD.NO
*
***   GET COST.CNTR DESC
*
   GOSUB 6000
*
***   PRINT THE RECORD
*
   GOSUB 3000
*
***    READ AND PROCESS ALL THE FILES
*
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      MATREAD JMT.REC FROM JOB.MATL,KEY ELSE
         ERRMSG = 'NO RECORD IS READ ...'
         GOSUB 92000
         GOTO 111
      END
*
*---- BREAK UP KEY
*
      CCTR.NO = FIELD(KEY,"!",3)
      PROD.NO = FIELD(KEY,"!",4)
      DEPT = FIELD(KEY,"!",2)
*
***   CHECK FOR NEW COST.CNTR
*
      GOSUB 4000
*
***   PROCESS THE FILE
*
      GOSUB 2000
*
***   PRINT THE RECORD
*
      GOSUB 3000
111*
   REPEAT
*
***   PRINT TOTS FOR THE LAST COST.CNTR
*
   GOSUB 4500
   GOSUB 5000
*
***    END OF JOB
*
   PRINTER OFF
   GOTO 999999
*
***   PRINT HEADINGS
*
1000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
*T25901 v
*  H.LINE = ''
*  H.LINE = H.LINE : 'DATE : ' : OCONV(DATE(), 'D2/')
*  H.LINE = H.LINE : SPACE(47)
*  H.LINE = H.LINE : CONO.NAME "L#30"
*  H.LINE = H.LINE : SPACE(23)
*  H.LINE = H.LINE : 'PAGE ' : PAGE.NO
*  PRINT H.LINE
*  H.LINE = ''
*  H.LINE = H.LINE : SPACE(51)
*  H.LINE = H.LINE : 'CHARGED QTY REPORT BY COST CENTER BY DATE'
*  PRINT H.LINE
   PRINT HLINE1:PAGE.NO"R#3"
   PRINT HLINE2
*T25901 ^
   H.LINE = ''
   H.LINE = H.LINE : SPACE(56)
   H.LINE = H.LINE : 'FOR THE PERIOD '
   H.LINE = H.LINE : OCONV(S.DATE, 'D2/') "R#8"
   H.LINE = H.LINE : ' THROUGH '
   H.LINE = H.LINE : OCONV(F.DATE,'D2/') "R#8"
   PRINT H.LINE
   PRINT
   H.LINE = ''
   H.LINE = H.LINE : SPACE(85)
   H.LINE = H.LINE : '<------------------'
   H.LINE = H.LINE : ' QTY '
   H.LINE = H.LINE : '------------------->'
   PRINT H.LINE
   H.LINE = ''
   D.LINE = ''
   H.LINE = H.LINE : 'CTR'
   D.LINE = D.LINE : '---'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : SPACE(10) : 'DESCRIPTION' : SPACE(9)
   D.LINE = D.LINE : '------------------------------'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : SPACE(2) : 'DATE' : SPACE(2)
   D.LINE = D.LINE : "--------"
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : "PRODUCT NUMBER "
   D.LINE = D.LINE : "---------------"
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : 'DV'
   D.LINE = D.LINE : '--'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : 'DEPT '
   D.LINE = D.LINE : '-----'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : 'TYP'
   D.LINE = D.LINE : '---'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : 'U/M'
   D.LINE = D.LINE : '---'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : ' REGULAR '
   D.LINE = D.LINE : '---------'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : ' SPOILG  '
   D.LINE = D.LINE : '---------'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : ' CS.ORD  '
   D.LINE = D.LINE : '---------'
   H.LINE = H.LINE : SPACE(2)
   D.LINE = D.LINE : SPACE(2)
   H.LINE = H.LINE : '  TOTAL   '
   D.LINE = D.LINE : '----------'
   PRINT H.LINE
   PRINT D.LINE
   LINE.COUNT = LINE.COUNT + 7
   RETURN
*
***   PROCESS THE RECORD
*
2000*
*
***   CASE FOR QTY TYPE
*
   IF JMT.QTY > 0 THEN
      JMT.QTY = INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
   END ELSE
      JMT.QTY = INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
   END
   BEGIN CASE
      CASE JMT.TYPE = 'R'
         REG.QTY = JMT.QTY
         SPL.QTY = 0
         CHG.QTY = 0
         TOT.REGULAR = TOT.REGULAR + JMT.QTY
         TOT1.REGULAR = TOT1.REGULAR + JMT.QTY
      CASE JMT.TYPE = 'C'
         CHG.QTY = JMT.QTY
         REG.QTY = 0
         SPL.QTY = 0
         TOT.CHARGE = TOT.CHARGE + JMT.QTY
         TOT1.CHARGE = TOT1.CHARGE + JMT.QTY
      CASE JMT.TYPE = 'S'
         SPL.QTY = JMT.QTY
         REG.QTY = 0
         CHG.QTY = 0
         TOT.SPOLAGE = TOT.SPOLAGE + JMT.QTY
         TOT1.SPOLAGE = TOT1.SPOLAGE + JMT.QTY
   END CASE
   TOT.QTY = TOT.QTY + JMT.QTY
   TOT1.QTY = TOT1.QTY + JMT.QTY
   RETURN
*
***   PRINT THE RECORD
*
3000*
   IF LINE.COUNT > 50 THEN
      LINE.COUNT = 0
      GOSUB 1000
   END
   LINE.COUNT = LINE.COUNT + 1
   H.LINE = ''
   H.LINE = H.LINE : CCTR.NO "L#3"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : CCTR.DESC "L#30"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(JMT.DATE, 'D2-') "R#8"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : PROD.NO "L#15"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : JMT.DIV "L#2"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : DEPT "L#5"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : JMT.TYPE "L#3"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : INV.UNIT<1,2> "L#3"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(REG.QTY , ICR.CNV) "R#9"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(SPL.QTY , ICR.CNV) "R#9"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(CHG.QTY , ICR.CNV) "R#9"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(JMT.QTY , ICR.CNV) "R#10"
   PRINT H.LINE
   RETURN
*
***   CHECK FOR NEW COST.CNTR
*
4000*
   IF CCTR.NO # PREV.VAL THEN
      GOSUB 4500
      PREV1.VAL = PROD.NO
      GOSUB 5000
      PREV.VAL = CCTR.NO
      GOSUB 6000
      GOSUB 7000
      LINE.COUNT = 0
      GOSUB 1000
   END
   IF PROD.NO # PREV1.VAL THEN
      GOSUB 4500
      PREV1.VAL = PROD.NO
      GOSUB 7000
   END
   RETURN
*
*** PRINT PROD TOT
*
4500*
*
***   INITIALIZE THE PCT AND CALCULATE IT
*
   REG1.PCT = 0
   SPL1.PCT = 0
   CUS1.PCT = 0
   IF TOT1.QTY # 0 THEN
      CUS1.PCT = INT((TOT1.CHARGE/TOT1.QTY)*100)
      REG1.PCT = INT((TOT1.REGULAR/TOT1.QTY)*100)
      SPL1.PCT = INT((TOT1.SPOLAGE/TOT1.QTY)*100)
   END
*
*** CHECK FOR END OF PAGE
*
   IF LINE.COUNT + 5 > 50 THEN
      LINE.COUNT = 0
      GOSUB 1000
   END
   LINE.COUNT = LINE.COUNT + 5
   D.LINE = ''
   D.LINE = D.LINE : SPACE(85)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '----------'
   PRINT D.LINE
   H.LINE = ''
   H.LINE = H.LINE : SPACE(17) : INV.FULL.DESC "L#41" : SPACE(3)
   H.LINE = H.LINE : 'PRODUCT TOTALS : '
   H.LINE = H.LINE : SPACE(7)
   H.LINE = H.LINE : OCONV(TOT1.REGULAR , ICR.CNV) "R#9"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(TOT1.SPOLAGE , ICR.CNV) "R#9"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(TOT1.CHARGE , ICR.CNV) "R#9"
   H.LINE = H.LINE : SPACE(2)
   H.LINE = H.LINE : OCONV(TOT1.QTY , ICR.CNV) "R#10"
   PRINT H.LINE
   PRINT D.LINE
   H.LINE = ''
   H.LINE = H.LINE : SPACE(60)
   H.LINE = H.LINE : 'PRODUCT PERCENT :'
   H.LINE = H.LINE : SPACE(7)
   H.LINE = H.LINE : OCONV(REG1.PCT, 'MD0') "R#9"
   H.LINE = H.LINE : '%'
   H.LINE = H.LINE : SPACE(1)
   H.LINE = H.LINE :OCONV(SPL1.PCT, "MD0") "R#9"
   H.LINE = H.LINE : '%'
   H.LINE = H.LINE : SPACE(1)
   H.LINE = H.LINE : OCONV(CUS1.PCT, 'MD0') "R#9"
   H.LINE = H.LINE : '%'
   PRINT H.LINE
   D.LINE = ''
   D.LINE = D.LINE : SPACE(85)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '---------'
   PRINT D.LINE
*
***   CLEARING THE TOTAL VALUES
*
   TOT1.CHARGE    = 0
   TOT1.REGULAR   = 0
   TOT1.SPOLAGE   = 0
   TOT1.QTY       = 0
   RETURN
*
***   PRINT THE TOTS
*
5000*
*
***   INITIALIZE THE PCT AND CALCULATE IT
*
   REG.PCT = 0
   SPL.PCT = 0
   CUS.PCT = 0
   IF TOT.QTY # 0 THEN
      CUS.PCT = INT((TOT.CHARGE/TOT.QTY)*100)
      REG.PCT = INT((TOT.REGULAR/TOT.QTY)*100)
      SPL.PCT = INT((TOT.SPOLAGE/TOT.QTY)*100)
   END
*
*** CHECK FOR END OF PAGE
*
   IF LINE.COUNT + 4 > 50 THEN
      LINE.COUNT = 0
      GOSUB 1000
   END
   LINE.COUNT = LINE.COUNT + 4
   H.LINE = ''
   H.LINE = H.LINE : SPACE(56)
   H.LINE = H.LINE : 'COST CENTER PERCENT :'
   H.LINE = H.LINE : SPACE(7)
   H.LINE = H.LINE : OCONV(REG.PCT, 'MD0') "R#9"
   H.LINE = H.LINE : '%'
   H.LINE = H.LINE : SPACE(1)
   H.LINE = H.LINE : OCONV(SPL.PCT, 'MD0') "R#9"
   H.LINE = H.LINE : '%'
   H.LINE = H.LINE : SPACE(1)
   H.LINE = H.LINE : OCONV(CUS.PCT, 'MD0') "R#9"
   H.LINE = H.LINE : '%'
   PRINT H.LINE
   D.LINE = ''
   D.LINE = D.LINE : SPACE(85)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '---------'
   D.LINE = D.LINE : SPACE(2)
   D.LINE = D.LINE : '---------'
   PRINT D.LINE
*
***   CLEARING THE TOTAL VALUES
*
   TOT.CHARGE    = 0
   TOT.REGULAR   = 0
   TOT.SPOLAGE   = 0
   TOT.QTY       = 0
   RETURN
*
***   GET COST.CNTR DESC
*
6000*
*
***   READ THE COST.CNTR FILE
*
   VAL.KEY = CONO:CCTR.NO
   MATREAD CCTR.REC FROM COST.CNTR,VAL.KEY ELSE
      MAT CCTR.REC = ''
   END
   RETURN
*
***   GET INVENTORY RECORD AND CONVERSION
*
7000*
   PROD.KEY = CONO:PREV1.VAL
   MATREAD INV.REC FROM INVENTORY, PROD.KEY ELSE
      MAT INV.REC = ''
   END
7500*
   MAT INV.CNV.REC = ''
   BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
*T28139  ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 1000; ICR.DV2 = 12
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2"
         ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
   END CASE
   RETURN
*
***   CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINTER OFF
END
