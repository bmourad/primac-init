      SUBROUTINE JRS.OS.INQ
*COPY>JCS.CPYLIB>COM.JRS
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JRS.OS.INQ
* BY          - KERRY WILSON, COMPUTER BUSINESS ASSOCIATES
* DATE        - 03/19/87
* MODIFIED    - 08/01/88 BY KERRY WILSON
* DESCRIPTION -
*T21346 cba 12/18/1996 * FOUND POINTER PROBLEM IN CALCULATING AMOUNT
*T26556 ajibaly 05/08/2002 * USE REPORT NUMBERS
*ENDDOC
*********************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>VEND
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>JCS.CPYLIB>JOB.STATS
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      DSH = STR('-',30)
      DSH20 = DSH[1,20]
      DSH12 = DSH[1,12]
      DSH10 = DSH[1,10]
      DSH8 = DSH[1,8]
      DSH7 = DSH[1,7]
      PNO = 0
      SP2 = SPACE(2)
      HD1 = "";HD2 = ""
*T26556 v
*     RPT.NAME = "OUTSIDE.PURCHASE.INQUIRY.REPORT"
*     RPT.NO = "XXXX"
      RPT.NAME = ""
      RPT.DATE = DATE()
*T26556 ^
      CALL GET.PROG.HEAD(CONO,C.CO.NAME,RPT.NAME,RPT.NO,RPT.DATE,HD1,HD2)
      POS = ""
      PRINTER ON
*
*---- MAIN PROCESSING
*
100*
      MATREAD JSTAT.REC FROM JOB.STATS,CONO:JOBNO ELSE GOTO 99999
      IF JSTAT.OPO.NO='' THEN GOTO 99999
      GOSUB 2000
      PO.CNT = DCOUNT(JSTAT.OPO.NO,VM)
      FOR D = 1 TO PO.CNT
         LOCATE JSTAT.OPO.NO<1,D> IN POS,1 SETTING FND THEN GOTO 999
         POS<-1> = JSTAT.OPO.NO<1,D>
         MATREAD OPO.REC FROM OUTSIDE.PO,CONO:JSTAT.OPO.NO<1,D> ELSE GOTO 999
         MATREAD VEND.REC FROM VEND,CONO:OPO.VEND.NO ELSE MAT VEND.REC = ""
         JOB.CNT = DCOUNT(OPO.JOB.NO,VM)
         PTR = 1
200*
         IF PTR > JOB.CNT THEN GOTO 999
         LOCATE JOBNO IN OPO.JOB.NO<1>,PTR SETTING JFND THEN
            MATREAD CAOS.REC FROM CATEGORY.OSP,CONO:OPO.PROD.LINE<1,JFND> ELSE CAOS.DESC = "CATEGORY UNKNOWN"
            IF VEND.DESC = "" THEN
               V.NAME = "VENDOR UNKNOWN"
               V.NAME = V.NAME "L#30"
            END ELSE
               IF VEND.DESC<1,2> # "" THEN
                  V.NAME = VEND.DESC<1,2> "L#30"
               END ELSE
                  V.NAME = VEND.DESC "L#30"
               END
            END
            OPO.NUM = JSTAT.OPO.NO<1,D> "L#8"
            VEND.NO = OPO.VEND.NO "L#8"
            CATG = OPO.PROD.LINE<1,JFND> "L#7"
            C.DESC = CAOS.DESC "L#20"
            DATE.EXP = OCONV(OPO.EXP.DATE<1,JFND>,"D2/") "L#8"
            DATE.RECV = OCONV(OPO.DATE.RECVD<1,JFND>,"D2/") "L#8"
            OPEN.QTY = OPO.QTY<1,JFND> - OPO.QTY.RECVD<1,JFND> - OPO.CANCEL.QTY<1,JFND>
            IF OPEN.QTY < 0 THEN OPEN.QTY = 0
            IF OPEN.QTY # 0 THEN
*T21346
               OPEN.AMT = OPO.EST.COST<1,JFND> - OPO.AMT.RECVD<1,JFND> - OPO.CANCEL.COST<1,JFND>
*T21346
               IF OPEN.AMT < 0 THEN OPEN.AMT = 0
            END ELSE
               OPEN.AMT = 0
            END
            OPEN.QTY = OCONV(OPEN.QTY,"MD2") "R#10"
            OPEN.AMT = OCONV(OPEN.AMT,"MD2,") "R#12"
            GOSUB 1000
         END
         PTR = JFND + 1
         GOTO 200
999*
      NEXT D
      GOTO 99999
*
*---- PRINT DETAIL LINE
*
1000*
      IF LINE.CNT >= 55 THEN GOSUB 2000
      PRINT OPO.NUM:SP2:VEND.NO:SP2:V.NAME:SP2:CATG:SP2:C.DESC:SP2:
      PRINT DATE.EXP:SP2:DATE.RECV:SP2:OPEN.QTY:SP2:OPEN.AMT
      LINE.CNT = LINE.CNT + 1
      RETURN
*
*---- PRINT HEADING
*
2000*
      PRINT CHAR(12)
      PNO = PNO + 1
      PRINT HD1:PNO
      PRINT HD2
      PRINT SPACE(57):"FOR JOB : ":JOBNO
      PRINT
      PRINT " OUTSIDE   VENDOR":SPACE(50):"CATEGORY          DATE    LST DATE     OPEN         OPEN"
      PRINT "  PO #     NUMBER             VENDOR NAME            CATEG        DESCRIPTION      EXPECTED  RECEIVED   QUANTITY      AMOUNT"
      PRINT DSH8:SP2:DSH8:SP2:DSH:SP2:DSH7:SP2:DSH20:SP2:
      PRINT DSH8:SP2:DSH8:SP2:DSH10:SP2:DSH12
      PRINT
      LINE.CNT = 8
      RETURN
99999*
      PRINTER OFF
      RETURN
   END
