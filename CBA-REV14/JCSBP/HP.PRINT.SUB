      SUBROUTINE HP.PRINT.SUB (CONO, TARG.KEY, MAT TARG.REC, INTERPRET)
*********************************************************************
*
* PROGRAM  - HP.PRINT.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 08/01/90
*
* DESCRIPTION
*
* This subroutine is used to print the shop-floor bar-code templates
* on a HEWLETT-PACKARD LaserJet printer with bar code font cartridge.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JCS.CPYLIB>TARGET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      EQU ESC TO CHAR(27)
      COLW = 28
      LM = SPACE(2)
      RESET = ESC:"E":ESC:"&l0O"
      COMPRESS.ON  = ESC:"(8U":ESC:"(s16.66h9.5V"
      COMPRESS.OFF = ESC:"(s10h12V"
      BOLD.ON      = ESC:"(8U":ESC:"(s12h14v7B"
      BOLD.OFF     = ESC:"(s10h12v0B"
      EXPAND.ON    = ESC:"(8U":ESC:"(s5h12v7B"
      EXPAND.OFF   = ESC:"(s10h12v0B"
      BARCODE.ON   = ESC:"(0Y":ESC:"(s0p4.6h12v0s0b0T"
      BARCODE.OFF  = ESC:"(0U":ESC:"(s0p10h12v0s0b3T"
      BARCODE.ON   = ESC:"(0Y":ESC:"(s0p4.6h0T"
      BARCODE.OFF  = ESC:"(0U":ESC:"(s0p10h3T"
      PRINTER ON
*     PRINT RESET
*
*---- MAIN PROCESSING
*
100*
      PRINT LM:EXPAND.ON:TARG.NAME"L#68":EXPAND.OFF:OCONV(DATE(),"D2-")
      PRINT
      PRINT
      FOR RP = 1 TO 10
         PL1=""; PL2=""; PL3=""
         FOR CP = 1 TO 3
            PL1C=""; PL2C=""; PL3C=""
            DEFS = TARG.DEF<1,CP,RP>
            DSCS = TARG.DSC<1,CP,RP>
            CNT = COUNT(DEFS,"\")+1
            CSZ = INT(COLW/CNT)
            FMT = "L#":CSZ
            FOR PTR = 1 TO CNT
               DEF = TRIM(FIELD(DEFS,"\",PTR))
               PFX = DEF[1,1]
               DSC = SPACE(1):TRIM(FIELD(DSCS,"\",PTR))
               BEGIN CASE
               CASE PFX = "F"
                  BC = DEF[2,99]
               CASE PFX = "C"
                  BC = ""
               CASE PFX = "1" OR PFX = "4" OR PFX = "5" OR PFX = "7"
                  BC = PFX
               CASE PFX = "2" OR PFX = "8"
                  GOSUB 60000
                  OPER = DEF[2,99]
                  BC = PFX:CCTR:OPER
               CASE PFX = "3" OR PFX = "6"
                  GOSUB 60000
                  BC = PFX:CCTR
               CASE 1
                  BC = ""
               END CASE
               IF BC = "" THEN BCX = BC ELSE BCX = "*":BC:"*"
               INDNT = INT((LEN(BCX)*16.66/4.6-LEN(BC))/2)
               IF CP = 1 AND PTR = 1 THEN
                  COL = 1
                  PL1C = COMPRESS.ON:DSC:COMPRESS.OFF
                  PL2C = BARCODE.ON:BCX:BARCODE.OFF
                  PL3C = COMPRESS.ON:SPACE(INDNT):BC:COMPRESS.OFF
               END ELSE
                  COL = (CP-1)*COLW + (PTR-1)*CSZ + 1
                  HT = ESC:"&a":COL:"C"
                  PL1C = PL1C:HT:COMPRESS.ON:DSC:COMPRESS.OFF
                  PL2C = PL2C:HT:BARCODE.ON:BCX:BARCODE.OFF
                  PL3C = PL3C:HT:COMPRESS.ON:SPACE(INDNT):BC:COMPRESS.OFF
               END
            NEXT PTR
            PL1 = PL1:PL1C
            PL2 = PL2:PL2C
            PL3 = PL3:PL3C
         NEXT CP
         PRINT ESC:"=":                    ;* HALF-LINE SPACE
         PRINT LM:PL1
         PRINT ESC:"=":                    ;* HALF-LINE SPACE
         PRINT LM:PL2
         PRINT LM:PL2
         IF INTERPRET = "Y" THEN
            PRINT LM:PL3
         END ELSE
            PRINT
         END
      NEXT RP
      PRINT RESET
      GOTO 99999
*
*---- GET PREVIOUS COST CENTER
*
60000*
      CCTR = ""
      SDONE = 0
      FOR CP1 = CP TO 1 STEP -1 UNTIL SDONE
         FOR RP1 = 10 TO 1 STEP -1 UNTIL SDONE
            IF CP1 < CP OR RP1 < RP THEN
               SDEF = TARG.DEF<1,CP1,RP1>
               SPFX = SDEF[1,1]
               IF SPFX = "C" THEN
                  CCTR = SDEF[2,3]
                  SDONE = 1
               END
            END
         NEXT RP1
      NEXT CP1
      IF CCTR = "" THEN CCTR = FIELD(TARG.KEY,"-",1)
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER CLOSE
      PRINTER OFF
      RETURN
   END
