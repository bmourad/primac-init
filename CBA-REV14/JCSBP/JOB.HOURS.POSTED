*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.HOURS.POSTED
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 06/15/84
* DESCRIPTION -
* This program produces a Job Hours Posted report.
*************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- DIMENSION RECORDS
*
     DIM HOURS.TBL(25)
     DIM DEPT.TOTAL.REC(20)
     DIM GRAND.TOTAL.REC(20)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     RPT.NO="XXXX"
     MAT HOURS.TBL=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     PREVIOUS.DEPT.NO=''
     PREVIOUS.DEPT=''
     CCTR.NUMBER=''
     UNKNOWN=STR("?",30)
     K=''
     DATE.LAST.ENTRY=''
     DATE.ITEMS.THROUGH=''
     FIRST.FLAG=1
     NODATA=1
     FOUND.CG=0
     MAT JCR.REC=''
     MAT JOB.REC=''
     TLINE=SPACE(62):'------- ------ -------  ------- ------ ------ -------    ------ ------'
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'LAB' THEN GOTO 10
     IF JCR.LINE.TYPE = "P" OR JCR.LINE.TYPE = "M" THEN GOTO 10
     NODATA=0
     TYPE=FIELD(JCR.KEY,"*",4)
     JOB.NO=JCR.JOB
     DEPT.NO=JCR.DEPT
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     I=0
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        OPER.NO=JCR.OPER<1,I>
        SORT.KEY=CCTR.NO:'*':OPER.NO
        TRANS.DATE=JCR.DATE<1,I>
        TRANS.TYPE=JCR.TYPE<1,I>
        IF TYPE="EST" THEN
           E.REG.HRS=JCR.HRS<1,I,1>
           E.OT.HRS=JCR.HRS<1,I,2>
           E.TTL.HRS=E.REG.HRS + E.OT.HRS
           A.REG.HRS=''
           A.OT.HRS=''
           A.DT.HRS=''
           A.TTL.HRS=''
           A.SPOIL.HRS=''
        END ELSE
           E.REG.HRS=''
           E.OT.HRS=''
           E.TTL.HRS=''
           A.REG.HRS=JCR.HRS<1,I,1>
           A.OT.HRS=JCR.HRS<1,I,2>
           A.DT.HRS=JCR.HRS<1,I,3>
           A.TTL.HRS=A.REG.HRS + A.OT.HRS + A.DT.HRS
           A.SPOIL.HRS=JCR.SPOIL.HRS<1,I>
        END
        GOSUB 30
     NEXT I
     GOTO 10
12*
     PRINTER ON
     GOSUB 15
     D.CNT=COUNT(HOURS.TBL(1),VM) + (HOURS.TBL(1) # '')
     Y=0
     FOR Y=1 TO D.CNT
        DEPT.NO=HOURS.TBL(1)<1,Y>
        CCTR.CNT=COUNT(HOURS.TBL(2)<1,Y>,SVM) + (HOURS.TBL(2)<1,Y> # '')
        Z=0
        FOR Z=1 TO CCTR.CNT
           FOUND.R=0;FOUND.C=0
           FOR E=4 TO 12 UNTIL FOUND.R
              IF HOURS.TBL(E)<1,Y,Z> # '' THEN FOUND.R=1
           NEXT E
           FOR F=13 TO 21 UNTIL FOUND.C
              IF HOURS.TBL(F)<1,Y,Z> # '' THEN FOUND.C=1
           NEXT F
           IF FOUND.C=1 THEN FOUND.CG=1
           SORT.KEY=HOURS.TBL(2)<1,Y,Z>
           CCTR.NO=FIELD(SORT.KEY,"*",1)
           OPER.NO=FIELD(SORT.KEY,"*",2)
           R.DATE=HOURS.TBL(4)<1,Y,Z>
           R.E.REG=HOURS.TBL(5)<1,Y,Z>
           R.E.OT=HOURS.TBL(6)<1,Y,Z>
           R.E.TTL=HOURS.TBL(7)<1,Y,Z>
           R.A.REG=HOURS.TBL(8)<1,Y,Z>
           R.A.OT=HOURS.TBL(9)<1,Y,Z>
           R.A.DT=HOURS.TBL(10)<1,Y,Z>
           R.A.TTL=HOURS.TBL(11)<1,Y,Z>
           R.S.HRS=HOURS.TBL(12)<1,Y,Z>
           C.DATE=HOURS.TBL(13)<1,Y,Z>
           C.E.REG=HOURS.TBL(14)<1,Y,Z>
           C.E.OT=HOURS.TBL(15)<1,Y,Z>
           C.E.TTL=HOURS.TBL(16)<1,Y,Z>
           C.A.REG=HOURS.TBL(17)<1,Y,Z>
           C.A.OT=HOURS.TBL(18)<1,Y,Z>
           C.A.DT=HOURS.TBL(19)<1,Y,Z>
           C.A.TTL=HOURS.TBL(20)<1,Y,Z>
           C.S.HRS=HOURS.TBL(21)<1,Y,Z>
           GOSUB 40
        NEXT Z
        GOSUB 60
     NEXT Y
     GOSUB 80
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFO FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     D.CNT=COUNT(JOB.LB.DATE,VM) + (JOB.LB.DATE # '')
     FOR T=1 TO D.CNT
        IF JOB.LB.DATE<1,T,1> > DATE.LAST.ENTRY THEN DATE.LAST.ENTRY=JOB.LB.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     T=0;D.CNT=0
     D.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR T=1 TO D.CNT
        IF DATE.TBL<1,T,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,T,1>
     NEXT T
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     PG.NO=PG.NO + 1
     H.LINE=''; HLINE1=''; HLINE2=''; HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     N=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=N:CO.NAME:N
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):"PAGE: ":PG.NO
     PRINT HLINE1:HLINE2:HLINE3
     PRINT JOB.CUST "L#8":SPACE(33):'L A B O R   H O U R S   P O S T E D   R E P O R T'
     PRINT CUST.NAME
     PRINT JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")
     PRINT JOB.DESC<1,1> "L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.ENTRY,"D2-")
     PRINT 'SR: ':SALS.NAME "L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT SPACE(55):'LAST   ----- EST. HOURS -----  -------- ACTUAL HOURS -------'
     PRINT 'DEPT  CCTR  OPER  DESCRIPTION':SPACE(26):'ENTRY    REG     O/T   TOTAL     REG     O/T    D/T   TOTAL       VAR   SPOIL'
     PRINT '----  ----  ----  -------------------------------    -------- ------- ------ -------  ------- ------ ------ -------    ------ ------'
     LINE.CNT= 9
     RETURN
*
*---- CREATE TABLE
*
30*
     NEW.DEPT=0
     LOCATE DEPT.NO IN HOURS.TBL(1)<1>,1 SETTING D ELSE
        NEW.DEPT=1
        D=COUNT(HOURS.TBL(1),VM) + (HOURS.TBL(1) # '') + 1
        HOURS.TBL(1)<1,D>=DEPT.NO
     END
     IF NEW.DEPT THEN
        C=1
     END ELSE
        LOCATE SORT.KEY IN HOURS.TBL(2)<1,D>,1 BY "AL" SETTING C ELSE
           IF C <= COUNT(HOURS.TBL(2)<1,D>,SVM)+(HOURS.TBL(2)<1,D> # '') THEN
              FOR A = 2 TO 25
                 HOURS.TBL(A)=INSERT(HOURS.TBL(A),1,D,C,"")
              NEXT A
           END
        END
     END
     HOURS.TBL(2)<1,D,C>=SORT.KEY
     IF TRANS.TYPE="R" THEN
        IF TRANS.DATE > HOURS.TBL(4)<1,D,C> THEN HOURS.TBL(4)<1,D,C>=TRANS.DATE
        HOURS.TBL(5)<1,D,C>=HOURS.TBL(5)<1,D,C> + E.REG.HRS
        HOURS.TBL(6)<1,D,C>=HOURS.TBL(6)<1,D,C> + E.OT.HRS
        HOURS.TBL(7)<1,D,C>=HOURS.TBL(7)<1,D,C> + E.TTL.HRS
        HOURS.TBL(8)<1,D,C>=HOURS.TBL(8)<1,D,C> + A.REG.HRS
        HOURS.TBL(9)<1,D,C>=HOURS.TBL(9)<1,D,C> + A.OT.HRS
        HOURS.TBL(10)<1,D,C>=HOURS.TBL(10)<1,D,C> + A.DT.HRS
        HOURS.TBL(11)<1,D,C>=HOURS.TBL(11)<1,D,C> + A.TTL.HRS
        HOURS.TBL(12)<1,D,C>=HOURS.TBL(12)<1,D,C> + A.SPOIL.HRS
     END ELSE
        IF TRANS.DATE > HOURS.TBL(13)<1,D,C> THEN HOURS.TBL(13)<1,D,C>=TRANS.DATE
        HOURS.TBL(14)<1,D,C>=HOURS.TBL(14)<1,D,C> + E.REG.HRS
        HOURS.TBL(15)<1,D,C>=HOURS.TBL(15)<1,D,C> + E.OT.HRS
        HOURS.TBL(16)<1,D,C>=HOURS.TBL(16)<1,D,C> + E.TTL.HRS
        HOURS.TBL(17)<1,D,C>=HOURS.TBL(17)<1,D,C> + A.REG.HRS
        HOURS.TBL(18)<1,D,C>=HOURS.TBL(18)<1,D,C> + A.OT.HRS
        HOURS.TBL(19)<1,D,C>=HOURS.TBL(19)<1,D,C> + A.DT.HRS
        HOURS.TBL(20)<1,D,C>=HOURS.TBL(20)<1,D,C> + A.TTL.HRS
        HOURS.TBL(21)<1,D,C>=HOURS.TBL(21)<1,D,C> + A.SPOIL.HRS
     END
     FOR A=4 TO 25
        IF HOURS.TBL(A)<1,D,C>=0 THEN HOURS.TBL(A)<1,D,C>=''
     NEXT A
     RETURN
*
*---- PRINT THE VALUES
*
40*
     IF FOUND.R=0 AND FOUND.C=0 THEN GOTO 45
     GOSUB 50
     IF LINE.CNT >= 50 THEN GOSUB 20
     IF DEPT.NO # PREVIOUS.DEPT.NO THEN
        MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
        P.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT P.LINE
        PRINT STR('-',LEN(P.LINE))
        LINE.CNT=LINE.CNT + 2
        P.LINE='';PLINE1='';PLINE2='';PLINE3=''
        P.LINE=P.LINE:SPACE(6):CCTR.NO "L#4":'  ':OPER.NO "L#4":'  ':OPER.DESC "L#31"
        PLINE1='    ':R.DATE "R#8":' ':R.E.REG "R#7":' ':R.E.OT "R#6":' ':R.E.TTL "R#7":'  ':R.A.REG "R#7":' ':R.A.OT "R#6":' ':R.A.DT "R#6":' ':R.A.TTL "R#7":'    ':OCONV(R.VAR,"MD2")"R#6":' ':R.S.HRS "R#6"
        PLINE2=SPACE(49):' CO ':C.DATE "R#8":' ':C.E.REG "R#7":' ':C.E.OT "R#6":' ':C.E.TTL "R#7":'  ':C.A.REG "R#7":' ':C.A.OT "R#6":' ':C.A.DT "R#6":' ':C.A.TTL "R#7":' CO ':OCONV(C.VAR,"MD2")"R#7":' ':C.S.HRS "R#6"
        PLINE3=' CO ':C.DATE "R#8":' ':C.E.REG "R#7":' ':C.E.OT "R#6":' ':C.E.TTL "R#7":'  ':C.A.REG "R#7":' ':C.A.OT "R#6":' ':C.A.DT "R#6":' ':C.A.TTL "R#7":' CO ':OCONV(C.VAR,"MD2")"R#6":' ':C.S.HRS "R#6"
        IF FOUND.R # 0 THEN
           P.LINE=P.LINE:PLINE1
           PRINT P.LINE;LINE.CNT=LINE.CNT + 1
           IF FOUND.C # 0 THEN
              P.LINE=''
              P.LINE=PLINE2
              PRINT P.LINE;LINE.CNT=LINE.CNT + 1
           END
        END ELSE
           IF FOUND.C # 0 THEN
              P.LINE=P.LINE:PLINE3
              PRINT P.LINE;LINE.CNT=LINE.CNT + 1
           END
        END
     END ELSE
        P.LINE='';PLINE4='';PLINE5='';PLINE6=''
        P.LINE=P.LINE:SPACE(6):CCTR.NO "L#4":'  ':OPER.NO "L#4":'  ':OPER.DESC "L#31"
        PLINE4='    ':R.DATE "R#8":' ':R.E.REG "R#7":' ':R.E.OT "R#6":' ':R.E.TTL "R#7":'  ':R.A.REG "R#7":' ':R.A.OT "R#6":' ':R.A.DT "R#6":' ':R.A.TTL "R#7":'    ':OCONV(R.VAR,"MD2")"R#6":' ':R.S.HRS "R#6"
        PLINE5=SPACE(49):' CO ':C.DATE "R#8":' ':C.E.REG "R#7":' ':C.E.OT "R#6":' ':C.E.TTL "R#7":'  ':C.A.REG "R#7":' ':C.A.OT "R#6":' ':C.A.DT "R#6":' ':C.A.TTL "R#7":' CO ':OCONV(C.VAR,"MD2")"R#6":' ':C.S.HRS "R#6"
        PLINE6=' CO ':C.DATE "R#8":' ':C.E.REG "R#7":' ':C.E.OT "R#6":' ':C.E.TTL "R#7":'  ':C.A.REG "R#7":' ':C.A.OT "R#6":' ':C.A.DT "R#6":' ':C.A.TTL "R#7":' CO ':OCONV(C.VAR,"MD2")"R#6":' ':C.S.HRS "R#6"
        IF FOUND.R # 0 THEN
           P.LINE=P.LINE:PLINE4
           PRINT P.LINE;LINE.CNT=LINE.CNT + 1
           IF FOUND.C # 0 THEN
              P.LINE=''
              P.LINE=PLINE5
              PRINT P.LINE;LINE.CNT=LINE.CNT + 1
           END
        END ELSE
           IF FOUND.C # 0 THEN
              P.LINE=P.LINE:PLINE6
              PRINT P.LINE;LINE.CNT=LINE.CNT + 1
           END
        END
     END
45*
     IF LINE.CNT >= 50 THEN GOSUB 20
     PREVIOUS.DEPT.NO=DEPT.NO
     RETURN
*
*---- CALCULATE FIELD VALUES
*
50*
     MATREAD OPER.REC FROM OPERATION,CONO:OPER.NO ELSE OPER.DESC=UNKNOWN
     R.VAR=R.A.TTL - R.E.TTL
     IF R.VAR=0 THEN R.VAR=''
     C.VAR=C.A.TTL - C.E.TTL
     IF C.VAR=0 THEN C.VAR=''
     IF R.DATE > DEPT.TOTAL.REC(1) THEN DEPT.TOTAL.REC(1)=R.DATE
     DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + R.E.REG
     DEPT.TOTAL.REC(3)=DEPT.TOTAL.REC(3) + R.E.OT
     DEPT.TOTAL.REC(4)=DEPT.TOTAL.REC(4) + R.E.TTL
     DEPT.TOTAL.REC(5)=DEPT.TOTAL.REC(5) + R.A.REG
     DEPT.TOTAL.REC(6)=DEPT.TOTAL.REC(6) + R.A.OT
     DEPT.TOTAL.REC(7)=DEPT.TOTAL.REC(7) + R.A.DT
     DEPT.TOTAL.REC(8)=DEPT.TOTAL.REC(8) + R.A.TTL
     DEPT.TOTAL.REC(9)=DEPT.TOTAL.REC(9) + R.S.HRS
     IF C.DATE > DEPT.TOTAL.REC(10) THEN DEPT.TOTAL.REC(10)=C.DATE
     DEPT.TOTAL.REC(11)=DEPT.TOTAL.REC(11) + C.E.REG
     DEPT.TOTAL.REC(12)=DEPT.TOTAL.REC(12) + C.E.OT
     DEPT.TOTAL.REC(13)=DEPT.TOTAL.REC(13) + C.E.TTL
     DEPT.TOTAL.REC(14)=DEPT.TOTAL.REC(14) + C.A.REG
     DEPT.TOTAL.REC(15)=DEPT.TOTAL.REC(15) + C.A.OT
     DEPT.TOTAL.REC(16)=DEPT.TOTAL.REC(16) + C.A.DT
     DEPT.TOTAL.REC(17)=DEPT.TOTAL.REC(17) + C.A.TTL
     DEPT.TOTAL.REC(18)=DEPT.TOTAL.REC(18) + C.S.HRS
     R.DATE=OCONV(R.DATE,"D2/")
     C.DATE=OCONV(C.DATE,"D2/")
     IF R.E.REG # '' THEN R.E.REG=OCONV(R.E.REG,"MD2")
     IF R.E.OT # '' THEN R.E.OT=OCONV(R.E.OT,"MD2")
     IF R.E.TTL # '' THEN R.E.TTL=OCONV(R.E.TTL,"MD2")
     IF R.A.REG # '' THEN R.A.REG=OCONV(R.A.REG,"MD2")
     IF R.A.OT # '' THEN R.A.OT=OCONV(R.A.OT,"MD2")
     IF R.A.DT # '' THEN R.A.DT=OCONV(R.A.DT,"MD2")
     IF R.A.TTL # '' THEN R.A.TTL=OCONV(R.A.TTL,"MD2")
     IF R.S.HRS # '' THEN R.S.HRS=OCONV(R.S.HRS,"MD2")
     IF C.E.REG # '' THEN C.E.REG=OCONV(C.E.REG,"MD2")
     IF C.E.OT # '' THEN C.E.OT=OCONV(C.E.OT,"MD2")
     IF C.E.TTL # '' THEN C.E.TTL=OCONV(C.E.TTL,"MD2")
     IF C.A.REG # '' THEN C.A.REG=OCONV(C.A.REG,"MD2")
     IF C.A.OT # '' THEN C.A.OT=OCONV(C.A.OT,"MD2")
     IF C.A.DT # '' THEN C.A.DT=OCONV(C.A.DT,"MD2")
     IF C.A.TTL # '' THEN C.A.TTL=OCONV(C.A.TTL,"MD2")
     IF C.S.HRS # '' THEN C.S.HRS=OCONV(C.S.HRS,"MD2")
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     GOSUB 70
     IF LINE.CNT >= 50 THEN GOSUB 20
     IF FOUND.DR=0 AND FOUND.DC=0 THEN GOTO 65
     PRINT TLINE
     LINE.CNT = LINE.CNT + 1
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE"L#40"
     DLINE1=SPACE(13):DEPT.DATE "R#8":' ':DEPT.E.REG "R#7":' ':DEPT.E.OT "R#6":' ':DEPT.E.TTL "R#7":'  ':DEPT.A.REG "R#7":' ':DEPT.A.OT "R#6":' ':DEPT.A.DT "R#6":' ':DEPT.A.TTL "R#7":'    ':OCONV(DEPT.VAR,"MD2")"R#6":' ':DEPT.S.HRS "R#6"
     DLINE2=SPACE(50):'CO ':DEPT.C.DATE "R#8":' ':DEPT.CE.REG "R#7":' ':DEPT.CE.OT "R#6":' ':DEPT.CE.TTL "R#7":'  ':DEPT.CA.REG "R#7":' ':DEPT.CA.OT "R#6":' ':DEPT.CA.DT "R#6":' ':DEPT.CA.TTL "R#7":' CO ':OCONV(DEPT.C.VAR,"MD2")"R#6":' ':DEPT.CS.HRS "R#6"
     DLINE3=SPACE(9):' CO ':DEPT.C.DATE "R#8":' ':DEPT.CE.REG "R#7":' ':DEPT.CE.OT "R#6":' ':DEPT.CE.TTL "R#7":'  ':DEPT.CA.REG "R#7":' ':DEPT.CA.OT "R#6":' ':DEPT.CA.DT "R#6":' ':DEPT.CA.TTL "R#7":' CO ':OCONV(DEPT.C.VAR,"MD2")"R#6":' ':DEPT.CS.HRS "R#6"
     IF FOUND.DR # 0 THEN
        D.LINE=D.LINE:DLINE1
        PRINT D.LINE
        IF FOUND.DC=0 THEN
           PRINT;LINE.CNT=LINE.CNT + 2
        END ELSE
           D.LINE=''
           D.LINE=DLINE2
           PRINT D.LINE;PRINT
           LINE.CNT=LINE.CNT + 3
        END
     END ELSE
        IF FOUND.DC # 0 THEN
           D.LINE=D.LINE:DLINE3
           PRINT D.LINE;PRINT;LINE.CNT=LINE.CNT + 2
        END
     END
65*
     IF LINE.CNT >= 50 THEN GOSUB 20
     MAT DEPT.TOTAL.REC=''
     RETURN
*
*---- OCONV HOURS FOR DEPT TOTALS
*
70*
     FOUND.DR=0;FOUND.DC=0
     FOR G=1 TO 9
        IF DEPT.TOTAL.REC(G) > 0 THEN FOUND.DR=1
     NEXT G
     FOR H=10 TO 18
        IF DEPT.TOTAL.REC(H) > 0 THEN FOUND.DC=1
     NEXT H
     DEPT.DATE=DEPT.TOTAL.REC(1)
     DEPT.E.REG=OCONV(DEPT.TOTAL.REC(2),"MD2")
     DEPT.E.OT=OCONV(DEPT.TOTAL.REC(3),"MD2")
     DEPT.E.TTL=OCONV(DEPT.TOTAL.REC(4),"MD2")
     DEPT.A.REG=OCONV(DEPT.TOTAL.REC(5),"MD2")
     DEPT.A.OT=OCONV(DEPT.TOTAL.REC(6),"MD2")
     DEPT.A.DT=OCONV(DEPT.TOTAL.REC(7),"MD2")
     DEPT.A.TTL=OCONV(DEPT.TOTAL.REC(8),"MD2")
     DEPT.S.HRS=OCONV(DEPT.TOTAL.REC(9),"MD2")
     DEPT.C.DATE=DEPT.TOTAL.REC(10)
     DEPT.CE.REG=OCONV(DEPT.TOTAL.REC(11),"MD2")
     DEPT.CE.OT=OCONV(DEPT.TOTAL.REC(12),"MD2")
     DEPT.CE.TTL=OCONV(DEPT.TOTAL.REC(13),"MD2")
     DEPT.CA.REG=OCONV(DEPT.TOTAL.REC(14),"MD2")
     DEPT.CA.OT=OCONV(DEPT.TOTAL.REC(15),"MD2")
     DEPT.CA.DT=OCONV(DEPT.TOTAL.REC(16),"MD2")
     DEPT.CA.TTL=OCONV(DEPT.TOTAL.REC(17),"MD2")
     DEPT.CS.HRS=OCONV(DEPT.TOTAL.REC(18),"MD2")
     DEPT.VAR=DEPT.TOTAL.REC(8) - DEPT.TOTAL.REC(4)
     DEPT.C.VAR=DEPT.TOTAL.REC(17) - DEPT.TOTAL.REC(13)
     IF DEPT.TOTAL.REC(1) > GRAND.TOTAL.REC(1) THEN GRAND.TOTAL.REC(1)=DEPT.TOTAL.REC(1)
     GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + DEPT.TOTAL.REC(2)
     GRAND.TOTAL.REC(3)=GRAND.TOTAL.REC(3) + DEPT.TOTAL.REC(3)
     GRAND.TOTAL.REC(4)=GRAND.TOTAL.REC(4) + DEPT.TOTAL.REC(4)
     GRAND.TOTAL.REC(5)=GRAND.TOTAL.REC(5) + DEPT.TOTAL.REC(5)
     GRAND.TOTAL.REC(6)=GRAND.TOTAL.REC(6) + DEPT.TOTAL.REC(6)
     GRAND.TOTAL.REC(7)=GRAND.TOTAL.REC(7) + DEPT.TOTAL.REC(7)
     GRAND.TOTAL.REC(8)=GRAND.TOTAL.REC(8) + DEPT.TOTAL.REC(8)
     GRAND.TOTAL.REC(9)=GRAND.TOTAL.REC(9) + DEPT.TOTAL.REC(9)
     IF DEPT.TOTAL.REC(10) > GRAND.TOTAL.REC(10) THEN GRAND.TOTAL.REC(10)=DEPT.TOTAL.REC(10)
     GRAND.TOTAL.REC(11)=GRAND.TOTAL.REC(11) + DEPT.TOTAL.REC(11)
     GRAND.TOTAL.REC(12)=GRAND.TOTAL.REC(12) + DEPT.TOTAL.REC(12)
     GRAND.TOTAL.REC(13)=GRAND.TOTAL.REC(13) + DEPT.TOTAL.REC(13)
     GRAND.TOTAL.REC(14)=GRAND.TOTAL.REC(14) + DEPT.TOTAL.REC(14)
     GRAND.TOTAL.REC(15)=GRAND.TOTAL.REC(15) + DEPT.TOTAL.REC(15)
     GRAND.TOTAL.REC(16)=GRAND.TOTAL.REC(16) + DEPT.TOTAL.REC(16)
     GRAND.TOTAL.REC(17)=GRAND.TOTAL.REC(17) + DEPT.TOTAL.REC(17)
     GRAND.TOTAL.REC(18)=GRAND.TOTAL.REC(18) + DEPT.TOTAL.REC(18)
     DEPT.DATE=OCONV(DEPT.DATE,"D2/")
     DEPT.C.DATE=OCONV(DEPT.C.DATE,"D2/")
     RETURN
*
*---- PRINT GRAND TOTALS
*
80*
     GOSUB 90
     IF LINE.CNT + 3 >= 50 THEN GOSUB 20
     PRINT TLINE
     G.LINE='*** GRAND TOTALS ***':SPACE(33):GRND.DATE "R#8":' ':GRND.E.REG "R#7":' ':GRND.E.OT "R#6":' ':GRND.E.TTL "R#7":'  ':GRND.A.REG "R#7":' ':GRND.A.OT "R#6":' ':GRND.A.DT "R#6":' ':GRND.A.TTL "R#7":'    ':GRND.VAR "R#6":' ':GRND.S.HRS "R#6"
     PRINT G.LINE
     IF FOUND.CG THEN
        G.LINE=SPACE(50):'CO ':GRND.C.DATE "R#8":' ':GRND.CE.REG "R#7":' ':GRND.CE.OT "R#6":' ':GRND.CE.TTL "R#7":'  ':GRND.CA.REG "R#7":' ':GRND.CA.OT "R#6":' ':GRND.CA.DT "R#6":' ':GRND.CA.TTL "R#7":'    ':GRND.C.VAR "R#6":' ':GRND.CS.HRS "R#6"
        PRINT G.LINE
     END
     RETURN
*
*---- OCONV HOURS FOR GRAND TOTALS
*
90*
     GRND.DATE=GRAND.TOTAL.REC(1)
     GRND.E.REG=OCONV(GRAND.TOTAL.REC(2),"MD2")
     GRND.E.OT=OCONV(GRAND.TOTAL.REC(3),"MD2")
     GRND.E.TTL=OCONV(GRAND.TOTAL.REC(4),"MD2")
     GRND.A.REG=OCONV(GRAND.TOTAL.REC(5),"MD2")
     GRND.A.OT=OCONV(GRAND.TOTAL.REC(6),"MD2")
     GRND.A.DT=OCONV(GRAND.TOTAL.REC(7),"MD2")
     GRND.A.TTL=OCONV(GRAND.TOTAL.REC(8),"MD2")
     GRND.S.HRS=OCONV(GRAND.TOTAL.REC(9),"MD2")
     GRND.C.DATE=GRAND.TOTAL.REC(10)
     GRND.CE.REG=OCONV(GRAND.TOTAL.REC(11),"MD2")
     GRND.CE.OT=OCONV(GRAND.TOTAL.REC(12),"MD2")
     GRND.CE.TTL=OCONV(GRAND.TOTAL.REC(13),"MD2")
     GRND.CA.REG=OCONV(GRAND.TOTAL.REC(14),"MD2")
     GRND.CA.OT=OCONV(GRAND.TOTAL.REC(15),"MD2")
     GRND.CA.DT=OCONV(GRAND.TOTAL.REC(16),"MD2")
     GRND.CA.TTL=OCONV(GRAND.TOTAL.REC(17),"MD2")
     GRND.CS.HRS=OCONV(GRAND.TOTAL.REC(18),"MD2")
     GRND.DATE=OCONV(GRND.DATE,"D2/")
     GRND.C.DATE=OCONV(GRND.C.DATE,"D2/")
     GRND.VAR=GRAND.TOTAL.REC(8) - GRAND.TOTAL.REC(4)
     GRND.C.VAR=GRAND.TOTAL.REC(17) - GRAND.TOTAL.REC(13)
     GRND.VAR=OCONV(GRND.VAR,"MD2")
     GRND.C.VAR=OCONV(GRND.C.VAR,"MD2")
     RETURN
*
*---- CALLS FOR SYSCOM
*
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
