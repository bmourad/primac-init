   SUBROUTINE JOB.TRACK.MAINT(PROG.FLAG,INVOICED.FLAG,CONO,CO.POS)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.TRACK.MAINT
* BY          - WALID YAMOUT, CBA
* DATE        - 04/05/84
* DESCRIPTION -
* COMMENT     - REVISED TO ADD JOB.STATUS "5" AS REOPENED
* - TASK #12907 (RRG)
*T26334 epitka 12/19/2001 * REV12
*T26908 cmykleb 10/26/2002 * RTN on Complete date forced status to
*                            Complete with no corresponding date.
*T26909 cmykleb 10/14/2002 * Include logic from POST.INVOICE for
*                            completing a job.
*T27915 lross 03/01/2004 * Argument list in CALL JOB.RESV.SUB.
*********************************************************************
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM ;*T26909
*COPY>CPYLIB>FILE.VARS ;*T26909
*COPY>JCS.CPYLIB>JOB
*COPY>ICS.CPYLIB>INV.JOB.STATS ;*T26909
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   SP = SPACE(25)
   BEGIN.PAGE = 11; JBEGIN.PAGE = 4
   PAGE.SIZE = 9; JPAGE.SIZE = 4
   LINE.SPACE = 1
   FIELDS.ACCESS = ""
   FIELDS.ACCESS<1,1> = 6
   FIELDS.ACCESS<1,2> = 7
   FIELDS.ACCESS<1,3> = 8
   FIELDS.ACCESS<1,4> = 9
*
*--- DISPLAY THE DATES
*
   SCV.REC(6)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,1>
   SCV.REC(7)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,2>
   SCV.REC(17)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,3>
   SCV.REC(8)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,4>
   SCV.REC(18)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,5>
   SCV.REC(9)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,6>
   SCV.REC(10)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,7>
   SCV.REC(11)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,8>
   SCV.REC(12)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,9>
   SCV.REC(13)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,10>
   JOBNO = SCV.REC(1)<ECD.SCRN.NO> ;*T26909
   WIP.HEAD = "Job Total"; WIP.LINE = JOB.WIP
   ECD.NUM = 30; SCV.REC(ECD.NUM)<ECD.SCRN.NO> =  "Work In Process"
   ECD.ACTION = 5; CALL SCRN.EDIT
   GOSUB 4000
   ECD.ACTION = 3; CALL SCRN.EDIT
   JLINES = COUNT(JOB.SUBS,VM) + (JOB.SUBS # "")
   JLN = 1; JOLD.START.LINE = 0; GOSUB 6000
   SLINES = COUNT(JOB.STATUS,VM) + (JOB.STATUS # "")
   STAT = ""
   FOR I = 1 TO SLINES
      BEGIN CASE
         CASE JOB.STATUS<1,I> = ""
            STAT<I> = "BOOKED"
         CASE JOB.STATUS<1,I> = "1"
            STAT<I> = "IN PROCESS"
         CASE JOB.STATUS<1,I> = "2"
            STAT<I> = "SHIPPED"
         CASE JOB.STATUS<1,I> = "3"
            STAT<I> = "INVOICED"
         CASE JOB.STATUS<1,I> = "4"
            STAT<I> = "COMPLETED"
         CASE JOB.STATUS<1,I> = "5"
            STAT<I> = "REOPENED"
         CASE JOB.STATUS<1,I> = "9"
            STAT<I> = "CANCELLED"
         CASE JOB.STATUS<1,I> = "7"
            STAT<I> = "READY TO PURGE"
         CASE JOB.STATUS<1,I> = "8"
            STAT<I> = "WAS PURGED"
         CASE 1
            STAT<I> = "UNKNOWN"
      END CASE
   NEXT I
   SLN = 1; SOLD.START.LINE = 0
   GOSUB 3000
   MORE = 1
   LOOP
      IF PROG.FLAG = "I" THEN ECD.NUM = 19 ELSE ECD.NUM = 14
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""; ECD.ACTION = 4; CALL SCRN.EDIT
      REQUEST = ECD.RET.VALUE
      BEGIN CASE
         CASE REQUEST = "END" OR REQUEST = "E" OR REQUEST = ""
            MORE = 0
         CASE REQUEST = "SS"
            SLN = SLN + PAGE.SIZE
            IF SLN > SLINES THEN SLN = 1
            GOSUB 3000
         CASE REQUEST = "SJ"
            JLN = JLN + JPAGE.SIZE
            IF JLN > JLINES THEN JLN = 1
            GOSUB 6000
         CASE REQUEST = "W"
            ECD.NUM = 30; SCV.REC(ECD.NUM)<ECD.SCRN.NO> =  "Work In Process"
            ECD.ACTION = 5; CALL SCRN.EDIT
            WIP.HEAD = "Job Total"; WIP.LINE = JOB.WIP
            GOSUB 4000
            FOR I = 21 TO 28
               ECD.NUM = I; ECD.ACTION = 5; CALL SCRN.EDIT
            NEXT I
            LOOP
               ECD.NUM = 20; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
               ECD.ACTION = 4; CALL SCRN.EDIT
               BEGIN CASE
                  CASE ECD.RET.VALUE = "J"
                     WIP.HEAD = "Job Total"
                     WIP.LINE = JOB.WIP
                  CASE ECD.RET.VALUE = "L"
                     WIP.HEAD = "Labor"
                     WIP.LINE = JOB.LB.WIP
                  CASE ECD.RET.VALUE = "M"
                     WIP.HEAD = "Material"
                     WIP.LINE = JOB.MT.WIP
                  CASE ECD.RET.VALUE = "P"
                     WIP.HEAD = "O. Purchases"
                     WIP.LINE = JOB.OS.WIP
                  CASE ECD.RET.VALUE = "S"
                     WIP.HEAD = "Shipping"
                     WIP.LINE = JOB.SP.WIP
                  CASE ECD.RET.VALUE = "O"
                     WIP.HEAD = "Misc / Other"
                     WIP.LINE = JOB.MS.WIP
                  CASE 1
                     ECD.RET.VALUE = ""
               END CASE
            WHILE ECD.RET.VALUE # "" DO
               GOSUB 4000
               FOR I = 21 TO 28
                  ECD.NUM = I; ECD.ACTION = 5; CALL SCRN.EDIT
               NEXT I
            REPEAT
         CASE REQUEST = "R"
            ECD.NUM = 30; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = "  Reverse WIP  "
            ECD.ACTION = 5; CALL SCRN.EDIT
            REV.HEAD = "Job Total"; REV.LINE = JOB.WIP
            GOSUB 5000
            FOR I = 21 TO 28
               ECD.NUM = I; ECD.ACTION = 5; CALL SCRN.EDIT
            NEXT I
            LOOP
               ECD.NUM = 29; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
               ECD.ACTION = 4; CALL SCRN.EDIT
               BEGIN CASE
                  CASE ECD.RET.VALUE = "J"
                     REV.HEAD = "Job Total"
                     REV.LINE = JOB.WIP
                  CASE ECD.RET.VALUE = "L"
                     REV.HEAD = "Labor"
                     REV.LINE = JOB.LB.WIP
                  CASE ECD.RET.VALUE = "M"
                     REV.HEAD = "Material"
                     REV.LINE = JOB.MT.WIP
                  CASE ECD.RET.VALUE = "P"
                     REV.HEAD = "O. Purchases"
                     REV.LINE = JOB.OS.WIP
                  CASE ECD.RET.VALUE = "S"
                     REV.HEAD = "Shipping"
                     REV.LINE = JOB.SP.WIP
                  CASE ECD.RET.VALUE = "O"
                     REV.HEAD = "Misc / Other"
                     REV.LINE = JOB.MS.WIP
                  CASE 1
                     ECD.RET.VALUE = ""
               END CASE
            WHILE ECD.RET.VALUE # "" DO
               GOSUB 5000
               FOR I = 21 TO 28
                  ECD.NUM = I; ECD.ACTION = 5; CALL SCRN.EDIT
               NEXT I
            REPEAT
         CASE NUM(REQUEST) AND REQUEST > 0
            IF INVOICED.FLAG OR JOB.STATUS<1,1> > 2 THEN
               LOCATE REQUEST IN FIELDS.ACCESS<1>,1 SETTING FF ELSE
                  ERRMSG="YOU CANNOT CHANGE THIS FIELD BECAUSE OF THE JOB STATUS"
                  GOSUB 91000
                  GOTO 110
               END
            END
            ON REQUEST GOSUB 1100,1200,1300,1400,1500,1600,1700,1800,1900
110 *
      END CASE
   WHILE MORE DO REPEAT
   GOTO 999999
*
*--- ESTIMATE DATE
*
1100 *
   ECD.NUM = 6; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN JOB.TRACK.DATE<1,1> = ECD.RET.VALUE
   RETURN
*
*--- QUOTED DATE
*
1200 *
   ECD.NUM = 7; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN JOB.TRACK.DATE<1,2> = ECD.RET.VALUE
   RETURN
*
*--- PROOF DATE
*
1300 *
   ECD.NUM = 17; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN JOB.TRACK.DATE<1,3> = ECD.RET.VALUE
   RETURN
*
*--- DUE DATE
*
1400 *
   ECD.NUM = 8; ECD.MINV = JOB.TRACK.DATE<1,2>
   ECD.MAXV = ECD.MINV + 365; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN JOB.TRACK.DATE<1,4> = ECD.RET.VALUE
   RETURN
*
*--- PRODUCTION START DATE
*
1500 *
   IF JOB.STATUS<1,1> > 0 THEN
      IF JOB.STATUS<1,1> = 9 THEN
         ERRMSG = "THIS JOB IS CANCELLED";GOSUB 91000
      END ELSE
         ECD.NUM = 18; ECD.ACTION = 4; CALL SCRN.EDIT
         IF ECD.RET.VALUE # "END" THEN
            IF JOB.TRACK.DATE<1,7> # "" AND ECD.RET.VALUE > JOB.TRACK.DATE<1,7> THEN
               ERRMSG = "PRODUCTION DATE SHOULD BE LESS THAN PASS TO BILL DATE"; GOSUB 91000; GOTO 1500
            END
            JOB.TRACK.DATE<1,5> = ECD.RET.VALUE
         END ELSE
            ECD.NUM = 18; SCV.REC(18)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,5>; ECD.ACTION = 5; CALL SCRN.EDIT
         END
      END
   END
   RETURN
*
*--- FINAL DELIVERY DATE
*
1600 *
   ECD.NUM = 9; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN JOB.TRACK.DATE<1,6> = ECD.RET.VALUE
   RETURN
*
*--- PASSED TO BILLING DATE
*
1700 *
   IF JOB.STATUS<1,1> > 0 THEN
      IF JOB.STATUS<1,1> = 9 THEN
         ERRMSG = "THIS JOB IS CANCELLED";GOSUB 91000; GOTO 1799
      END
      IF JOB.TRACK.DATE<1,5> = "" THEN
         ERRMSG = "YOU NEED TO ENTER PROD. START DATE FIRST"; GOSUB 91000; GOTO 1799
      END
      ECD.NUM = 10; ECD.ACTION = 4; CALL SCRN.EDIT
      BEGIN CASE
         CASE ECD.RET.VALUE = "END"
            ECD.NUM = 10; SCV.REC(10)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,7>; ECD.ACTION = 5; CALL SCRN.EDIT
         CASE ECD.RET.VALUE = ""
            JOB.TRACK.DATE<1,7> = ""
            ECD.NUM = 10; SCV.REC(10)<ECD.SCRN.NO> = JOB.TRACK.DATE<1,7>; ECD.ACTION = 5; CALL SCRN.EDIT
         CASE ECD.RET.VALUE # ""
            IF JOB.TRACK.DATE<1,5> > ECD.RET.VALUE THEN
               ERRMSG = "PASS TO BILL DATE SHOULD BE GREATER THAN PROD. START DATE"; GOSUB 91000; GOTO 1700
            END
            JOB.TRACK.DATE<1,7> = ECD.RET.VALUE
      END CASE
   END
1799 *
   RETURN
*
*--- COSTED DATE
*
1800 *
   ECD.NUM = 11; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN JOB.TRACK.DATE<1,8> = ECD.RET.VALUE
   RETURN
*
*--- COMPLETED DATE
*
1900 *
   IF JOB.STATUS<1,1> # 4 AND JOB.STATUS<1,1> <= 5 THEN
      IF SUM(JOB.WIP<1,2>) = 0 THEN
         ECD.NUM = 13; ECD.ACTION = 4; CALL SCRN.EDIT
*T26908 & T26909 v
*        IF ECD.RET.VALUE # "END" THEN
         IF ECD.RET.VALUE # "END" AND ECD.RET.VALUE # '' THEN
            X = 0
            Y = 23
            TYP = 8
            MAXL = 1
            PMSG = "Do you want to clear JOB's INV allocations/reserves (Y/N) ?"
            CALL EDIT.SUB
            P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            IF VALUE = "Y" THEN
               IF SUM(JOB.ALOC.QTY) # 0 AND CO.POS = 'Y' THEN
                  CALL CLEAR.JOB.ALLOC(CONO,JOBNO)
               END
               RESV.ACTION = "D"; DPTR = 0
*T27915 v      CALL JOB.RESV.SUB(RESV.ACTION,DPTR,CONO,JOBNO)
               CALL JOB.RESV.SUB(CONO,RESV.ACTION,DPTR,JOBNO)
               IF RESV.ACTION # "D" AND RESV.ACTION # "" THEN
                  ERRMSG = RESV.ACTION; GOSUB 91000; GOTO 1999
               END
               MCNT = COUNT(JOB.RESV.MATL,VM) + (JOB.RESV.MATL # "")
               FOR M = MCNT TO 1 STEP -1
                  IF JOB.RESV.QTY<1,M> + JOB.ALOC.QTY<1,M> + JOB.USED.QTY<1,M> = 0 THEN
                     JSTAT.ID = JOB.RESV.MATL<1,M>:"!":JOB.RESV.WHSE<1,M>:"!":JOBNO
                     MATREAD INV.JS.REC FROM INV.JOB.STATS, CONO : JSTAT.ID ELSE
                        JOB.RESV.MATL = DELETE(JOB.RESV.MATL,1,M,0)
                        JOB.RESV.WHSE = DELETE(JOB.RESV.WHSE,1,M,0)
                        JOB.RESV.DATE = DELETE(JOB.RESV.DATE,1,M,0)
                        JOB.ALOC.QTY = DELETE(JOB.ALOC.QTY,1,M,0)
                        JOB.RESV.QTY = DELETE(JOB.RESV.QTY,1,M,0)
                        JOB.USED.QTY = DELETE(JOB.USED.QTY,1,M,0)
                        JOB.ALOC.AMT = DELETE(JOB.ALOC.AMT,1,M,0)
                        JOB.RESV.AMT = DELETE(JOB.RESV.AMT,1,M,0)
                        JOB.USED.AMT = DELETE(JOB.USED.AMT,1,M,0)
                     END
                  END
               NEXT M
            END
*T26909 ^
            JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"4")
            JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,ECD.RET.VALUE)
            JOB.TRACK.DATE<1,10> = ECD.RET.VALUE
            STAT = INSERT(STAT,1,0,0,"COMPLETED")
            SLINES = SLINES + 1
            SLN = 1; SOLD.START.LINE = 0
            GOSUB 3000
         END
      END ELSE
         ERRMSG="ALL WIP MUST BE REVERSED BEFORE THE JOB CAN BE COMPLETED"
         GOSUB 91000
      END
   END ELSE
      ERRMSG="YOU CANNOT CHANGE THIS FIELD BECAUSE OF THE JOB STATUS"
      GOSUB 91000
   END
1999 RETURN
3000 START.LINE = 1 + INT((SLN-1)/PAGE.SIZE)*PAGE.SIZE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   IF LAST.LINE > SLINES THEN LAST.LINE = SLINES
   IF START.LINE = SOLD.START.LINE THEN GOTO 3999
   SOLD.START.LINE = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      PLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 55 ; P_Y = PLN ; P_VALUE = STAT<N> "L#15" ; P_OPT = ""
      P_X  := AM:71 ; P_Y := AM:PLN ; P_VALUE := AM:OCONV(JOB.STAT.DATE<1,N>,"D2/") "L#8"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE
      PLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 55 ; P_Y = PLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
3999 RETURN
4000 SCV.REC(21)<ECD.SCRN.NO> = WIP.HEAD
   SCV.REC(22)<ECD.SCRN.NO> = WIP.LINE<1,4>
   SCV.REC(23)<ECD.SCRN.NO> = WIP.LINE<1,5>
   SCV.REC(24)<ECD.SCRN.NO> = OCONV(WIP.LINE<1,2,1>,"MD2,")
   SCV.REC(25)<ECD.SCRN.NO> = OCONV(WIP.LINE<1,2,3>,"MD2,")
   SCV.REC(26)<ECD.SCRN.NO> = OCONV(WIP.LINE<1,2,2>,"MD2,")
   SCV.REC(27)<ECD.SCRN.NO> = OCONV(WIP.LINE<1,2,4>,"MD2,")
   TOT.WIP = WIP.LINE<1,2,1> + WIP.LINE<1,2,2> + WIP.LINE<1,2,3> + WIP.LINE<1,2,4>
   SCV.REC(28)<ECD.SCRN.NO> = OCONV(TOT.WIP,"MD2,")
   RETURN
5000 SCV.REC(21)<ECD.SCRN.NO> = REV.HEAD
   SCV.REC(22)<ECD.SCRN.NO> = REV.LINE<1,4>
   SCV.REC(23)<ECD.SCRN.NO> = REV.LINE<1,5>
   SCV.REC(24)<ECD.SCRN.NO> = OCONV(REV.LINE<1,3,1>,"MD2,")
   SCV.REC(25)<ECD.SCRN.NO> = OCONV(REV.LINE<1,3,3>,"MD2,")
   SCV.REC(26)<ECD.SCRN.NO> = OCONV(REV.LINE<1,3,2>,"MD2,")
   SCV.REC(27)<ECD.SCRN.NO> = OCONV(REV.LINE<1,3,4>,"MD2,")
   TOT.REV = REV.LINE<1,3,1> + REV.LINE<1,3,2> + REV.LINE<1,3,3> + REV.LINE<1,3,4>
   SCV.REC(28)<ECD.SCRN.NO> = OCONV(TOT.REV,"MD2,")
   RETURN
6000 START.LINE = 1 + INT((JLN-1)/JPAGE.SIZE)*JPAGE.SIZE
   LAST.LINE = START.LINE + JPAGE.SIZE - 1
   IF LAST.LINE > JLINES THEN LAST.LINE = JLINES
   IF START.LINE = JOLD.START.LINE THEN GOTO 6999
   JOLD.START.LINE = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      PLN = JBEGIN.PAGE + LINE.SPACE * MOD(N-1,JPAGE.SIZE)
      P_X  = 71 ; P_Y = PLN ; P_VALUE = JOB.SUBS<1,N> "L#8" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO JPAGE.SIZE
      PLN = JBEGIN.PAGE + LINE.SPACE * MOD(M-1,JPAGE.SIZE)
      P_X  = 71 ; P_Y = PLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
6999 RETURN
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
999999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
