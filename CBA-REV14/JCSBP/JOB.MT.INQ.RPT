   SUBROUTINE JOB.MT.INQ.RPT(CONO,CO.NAME,JOB.NUM,SUB.CNT,HD1,HD2)
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.MT.INQ.RPT
* DATE        - 11/21/83
* DESCRIPTION -
*T26334 epitka 12/19/2001 * REV12
*T26493 cmykleb 04/03/2002 * Change pgm to use headings from calling pgm.
*ENDDOC
*********************************************************************
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>JCS.CPYLIB>COM.JOB.INQ
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>JCS.CPYLIB>JOB.TOT.REC
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
   DASHES = STR('-',80)
   PNO = 0
   LINE.COUNT = 99
   SP1 = SPACE(1)
*T26493 v
   HD3 = "JOB # : ":JOB.NUM"L#8":SPACE(49)
   HD3 = HD3 : "MATERIAL POSTED REPORT"
*T26493 ^
**** PRINT DETAIL LINES AND TOTALS
   DEPT.CNT = COUNT(MT.DEPT.NO,VM) + (MT.DEPT.NO # '')
   FOR D = 1 TO DEPT.CNT
      PTR.CNT = COUNT(MT.DEPT.PTR<1,D>,SVM)+(MT.DEPT.PTR<1,D> # '')
      DPNO = MT.DEPT.NO<1,D>
      FOR PT = 1 TO PTR.CNT
         PTR.NO = MT.DEPT.PTR<1,D,PT>; CCNO = JOB.MT.CCTR<1,PTR.NO>
         PDNO = JOB.MT.PROD<1,PTR.NO>; WHNO = JOB.MT.WHSE<1,PTR.NO>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
            MAT CCTR.REC = '????????????'
         END
         MT.ID = CONO:JOB.NUM:"!":DPNO:"!":CCNO:"!":PDNO:"!":WHNO:"!"
         LINES = JOB.MT.SEQ<1,PTR.NO,1>
         MATREAD INV.REC FROM INVENTORY ,CONO:PDNO ELSE GOTO 120
         MAT INV.CNV.REC = ''
         BEGIN CASE
            CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
               ICR.CNV = "MD0"
               ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
            CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
               ICR.CNV = "MD0"
               ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
            CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
               ICR.CNV = "MD0"
               ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
            CASE 1
               ICR.CNV = "MD2"
               ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
         END CASE
         FOR SE = 1 TO LINES
            MATREAD JMT.REC FROM JOB.MATL, MT.ID:SE ELSE GOTO 111
            GOSUB 1000
111      NEXT SE
         FOR K = 2 TO SUB.CNT
            OLN = LINES + 1; LINES = LINES + JOB.MT.SEQ<1,PTR.NO,K>
            MT.ID = CONO:JOB.SUBS<1,K-1>:"!":DPNO:"!":CCNO:"!":PDNO:"!":WHNO:"!"
            FOR SE = OLN TO LINES
               MATREAD JMT.REC FROM JOB.MATL, MT.ID:(SE-OLN+1) ELSE GOTO 112
               GOSUB 1000
112         NEXT SE
         NEXT K
120      GOSUB 2000
      NEXT PT
      GOSUB 3000
   NEXT D
   GOTO 99999
**** PRINT DETAIL LINE
1000 IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   LINE = ''
   LINE = LINE : PDNO "L#15" :SP1
   LINE = LINE : WHNO "L#4": SP1: JMT.SEQ "L#6":SP1
   LINE = LINE : OCONV(JMT.DATE , "D2/") "L#8": SP1
   LINE = LINE : JMT.MON<1,1> "R#6" :SP1
   IF JMT.QTY > 0 THEN
      RND = 0.5
   END ELSE
      RND = 0 - 0.5
   END
   BEGIN CASE
      CASE JMT.TYPE = 'R'
         LINE = LINE:OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + RND),ICR.CNV)"R#10":SPACE(25)
      CASE JMT.TYPE = 'N'
         LINE = LINE:SPACE(11):OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + RND),ICR.CNV)"R#7":SPACE(17)
      CASE JMT.TYPE = 'C'
         LINE = LINE:SPACE(19):OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + RND),ICR.CNV)"R#7":SPACE(9)
      CASE JMT.TYPE = 'S'
         LINE = LINE:SPACE(27):OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + RND),ICR.CNV)"R#7":SP1
      CASE 1
         LINE = LINE:OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + RND),ICR.CNV)"R#10":SPACE(25)
   END CASE
   LINE = LINE:INV.UNIT<1,2>"L#3":SP1
   LINE = LINE:OCONV(INT((((JMT.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + RND),ICR.CNV)"R#10":SP1
   LINE = LINE :OCONV(JMT.COST , "MD2") "R#10":SP1
   LINE = LINE : OCONV(JMT.SALE, "MD2") "R#10":SP1
   LINE = LINE : JMT.RC.JOB "L#8":SP1:JMT.RC.TRAN "L#6"
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** PRINT TOTALS FOR CCTR
2000 IF LINE.COUNT + 2 > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   GOSUB 9000
   LINE = ''
   LINE = LINE : "** CCTR - ":CCNO "L#3":" ":CCTR.DESC "L#32":" **"
   LINE = LINE : SPACE(44)
   LINE = LINE : SP1:OCONV(JOB.MT.COST<1,PTR.NO>, "MD2") "R#10"
   LINE = LINE : SP1:OCONV(JOB.MT.SALE<1,PTR.NO>, "MD2") "R#10"
   PRINT LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 2
   RETURN
**** PRINT TOTALS FOR DEPT
3000 IF LINE.COUNT + 2 > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   GOSUB 9100
   LINE = ''
   LINE = LINE : "** DEPT - ":DPNO "L#5":" ":MT.DEPT.DESC<1,D> "L#30":" **"
   LINE = LINE :SPACE(44)
   LINE = LINE :SP1:OCONV(MT.DEPT.COST<1,D>, "MD2") "R#10"
   LINE = LINE :SP1:OCONV(MT.DEPT.SALE<1,D>, "MD2") "R#10"
   PRINT LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 2
   RETURN
**** UNDER LINE FOR TOTALS
9000 IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   LINE = ''
   LINE = SPACE(94):'---------- ----------'
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** UNDER LINE FOR TOTALS
9100 IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   LINE = ''
   LINE = SPACE(94):'========== =========='
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** PRINT HEADING
9999 PRINT CHAR(12)
   PNO = PNO + 1
*T26493 v
*  LINE = SPACE(2) : 'DATE - ' : OCONV(DATE(),"D2/") "L#8"
*  LINE = LINE : SPACE(5) : 'TIME - ' : OCONV(TIME(),"MTS") "L#8"
*  LINE = LINE : SPACE(22) : CO.NAME "L#30"
*  LINE = LINE : SPACE(15) : 'PAGE - ' : PNO "R#3"
*  PRINT LINE
*  PRINT SPACE(52) :'M A T L   P O S T E D   R E P O R T'
*  PRINT SPACE(52) :'       FOR JOB : ':JOB.NUM "L#8"
*  PRINT SPACE(52) :DASHES[1,35]
   PRINT HD1:PNO"R#3"
   PRINT HD2
   PRINT HD3
*T26493 ^
   PRINT
   LINE = 'PRODUCT NUMBER  WHSE TRANS# MTL DATE PERIOD  REGULAR   NO CHRG CST ORD SPOILAG U/M  TOT  QTY  TOTAL COST TOTAL SALE RCLS JOB RCLTRN'
   PRINT LINE
   LINE = '--------------- ---- ------ -------- ------ ---------- ------- ------- ------- --- ---------- ---------- ---------- -------- ------'
   PRINT LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 7
   RETURN
**** PRINT TOTALS FOR DEPT
99999 IF LINE.COUNT + 2 > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   GOSUB 9100
   LINE = ''
   LINE = LINE : "** TOTAL FOR ALL MATL DEPARTMENTS              **"
   LINE = LINE :SPACE(44)
   LINE = LINE :SP1:OCONV(MT.TOT.COST, "MD2") "R#10"
   LINE = LINE :SP1:OCONV(MT.TOT.SALE, "MD2") "R#10"
   PRINT LINE
   RETURN
END
