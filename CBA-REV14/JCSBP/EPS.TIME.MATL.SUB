   SUBROUTINE TIME.MATL.SUB (CONO, REF.NO, SLN, MAT TEMP.REC, SHEET.OK, DEPT.WHSE, ESTAT, JOB.CUST.PASS)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>TMCOM
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - JCSBP
* PROGRAM  - TIME.MATL.SUB
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 01/04/87
* MODIFIED - NLB
* DATE MOD - 10/20/88
* COMMENTS - ADDED CUST INVENTORY CHECK
* MODIFIED - NA
* DATE MOD - 12/04/90
* COMMENTS - ADDED BAR CODE INVENTORY FUNCTIONALITY
* DESCRIPTION
* This program is used to maintain employee material usage.
*T20109 RKB 02/20/96 * MATCH WHSE DIV TO PRODUCT OR ROLL SKID
*T26334 epitka 12/17/2001 * REV12
*T26556 cmykleb 05/10/2002 * Correct problem with serial check.
*T28214 lross 05/11/2005 * Add Mill-to-Serial XREF.
*ENDDOC
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>JCS.CPYLIB>DAILY.TIME.MATL
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>MILL_TO_SERIAL ;*T28214
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>JCS.CPYLIB>JOB
*
*---- SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- DEFINE DIMENSIONED VARIABLES
*
   DIM TEMP.REC(35)
   EQU TYPE        TO TEMP.REC(1)
   EQU JOB.NO      TO TEMP.REC(2)
   EQU OSJ.FLAG    TO TEMP.REC(3)
   EQU JINIT       TO TEMP.REC(4)
   EQU CCTR        TO TEMP.REC(5)
   EQU OPER        TO TEMP.REC(6)
   EQU TCODE       TO TEMP.REC(7)
   EQU JTYPE       TO TEMP.REC(8)
   EQU SPOIL.CODE  TO TEMP.REC(9)
   EQU STRT        TO TEMP.REC(10)
   EQU STPT        TO TEMP.REC(11)
   EQU IMP         TO TEMP.REC(12)
   EQU PROD        TO TEMP.REC(13)
   EQU WHSE        TO TEMP.REC(14)
   EQU LOCAT       TO TEMP.REC(15)
   EQU SERIAL      TO TEMP.REC(16)
   EQU ED.LS.FLG   TO TEMP.REC(17)
   EQU STATUS      TO TEMP.REC(18)
   EQU QTY         TO TEMP.REC(19)
   EQU STK.QTY       TO TEMP.REC(20)
   EQU PMT.FLG     TO TEMP.REC(21)
   EQU QTYPE       TO TEMP.REC(22)
   EQU DATE.USED   TO TEMP.REC(23)
   EQU TIME.USED   TO TEMP.REC(24)
   EQU JOBCL       TO TEMP.REC(25)
   EQU JOBRATE     TO TEMP.REC(26)
*
*---- INITIALIZATION
*
   ESTAT=""
*
   OPEN "","INV_SERIAL" TO INV_SERIAL ELSE
      ERRMSG="CANNOT OPEN INV_SERIAL FILE"
      GOSUB 91000
      GOTO 10990
   END
*T28214 v
   OPEN "","MILL_TO_SERIAL" TO MILL_TO_SERIAL ELSE
      ERRMSG="CANNOT OPEN MILL_TO_SERIAL FILE"
      GOSUB 91000
      GOTO 10990
   END
   OPEN "WAREHOUSE" TO WAREHOUSE ELSE
      ERRMSG="CANNOT OPEN WAREHOUSE FILE"
      GOSUB 91000
      GOTO 10990
   END
   OPEN "JOB" TO JOB ELSE
      ERRMSG="CANNOT OPEN JOB FILE"
      GOSUB 91000
      GOTO 10990
   END

*
*---- GET MATERIAL ID
*
10100 *
   PROD=""
   WHSE=""
   LOCAT=""
   SERIAL=""
   DATE.USED=""
   TIME.USED=""
   ED.LS.FLG=""
   STATUS=""
   SCV.REC(44)<1>=""
   SCV.REC(45)<1>=""
   SCV.REC(46)<1>=""
   SCV.REC(47)<1>=""
   SCV.REC(48)<1>=""
   SCV.REC(49)<1>=""
   SCV.REC(44)<1>=DTM.SERIAL<1,REF.NO>
10110 *
   ECD.NUM=44; ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE="END" THEN GOTO 10990
   IF ECD.RET.VALUE # "" THEN
*T28214 v
      IF ECD.RET.VALUE = "M" THEN
         ECD.NUM=50; ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE # "END" THEN
            MATREAD MTS.REC FROM MILL_TO_SERIAL,CONO:ECD.RET.VALUE THEN
               SCV.REC(44)<1> = MTS.SERIAL
               GOTO 10110
            END ELSE
               ERRMSG = "Mill Roll # ":ECD.RET.VALUE:" is not on file"
               GOSUB 91000
               GOTO 10100
            END
         END ELSE
            GOTO 10100
         END
      END
*T28214 ^
      MATREAD ISTK.REC FROM INV_SERIAL,CONO:ECD.RET.VALUE THEN
         IF ISTK.POST.DATE="" THEN
            ERRMSG="Serial has not been received"
            GOSUB 91000
            GOTO 10100
         END ELSE
            IF DTM.WHSE<1,REF.NO>#ISTK.WHSE OR DTM.LOC<1,REF.NO>#ISTK.LOC THEN
            END
         END
         MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE MAT JOB.REC=''
         MATREAD WHSE.REC FROM WAREHOUSE,CONO:ISTK.WHSE ELSE MAT WHSE.REC=''
         IF WHS.DIV # JOB.DIV AND WHS.DIV # "00" THEN
            ERRMSG="Serial Warehouse Div. ":WHS.DIV:" does not match Job Div ":JOB.DIV
            GOSUB 91000
            GOTO 10100
         END
         SERIAL=ECD.RET.VALUE
         PROD=ISTK.PROD
         P_X  = 63 ; P_Y = SLN ; P_VALUE = PROD"L#15" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         WHSE=ISTK.WHSE
         LOCAT=ISTK.LOC
      END ELSE
         IF DTM.PROD<1,REF.NO> = "" THEN
            ERRMSG="Invalid Serial ID"
            GOSUB 91000
            GOTO 10100
         END
      END
   END
   IF PROD = "" THEN
      X=63;Y=SLN;TYP=1;MAXL=15;O.R='O';HMSG="Enter product ID of material used."
      IF DTM.PROD<1,REF.NO> # "" THEN
         DEFAULT=DTM.PROD<1,REF.NO>
      END
      CALL EDIT.SUB
      IF VALUE = 'END' THEN GOTO 10990
      PROD=VALUE
   END
   BEGIN CASE
      CASE TYPE="M" AND PROD=""
         ED.LS.FLG="Y"
         STATUS="MATERIAL REQUIRED"
      CASE OPER.MATL.REQ="Y" AND PROD="" AND TYPE # "C"
         ED.LS.FLG="Y"
         STATUS="MATERIAL REQUIRED"
      CASE TYPE = "C" AND PROD = ""
      CASE OPER.MATL.REQ="Y" AND PROD="0"
      CASE OPER.MATL.REQ="" AND PROD=""
      CASE 1
         MATREAD INV.REC FROM INVENTORY,CONO:PROD ELSE
            ERRMSG="Invalid product ID. Try again! "
            GOSUB 91000
            GOTO 10100
         END
         IF INV.CUST # "" THEN
            IF "X":INV.CUST # "X":JOB.CUST.PASS THEN
               ERRMSG="CUSTOMER ":INV.CUST:" OWNS THIS INVENTORY"
               GOSUB 91000
               GOTO 10100
            END
         END
         BEGIN CASE
            CASE INV.PAP.TYPE="REGULAR"
            CASE INV.PAP.TYPE="SHEET" AND SHEET.OK
            CASE INV.PAP.TYPE="ROLL"
            CASE INV.PAP.TYPE="PCOAT" AND SHEET.OK
            CASE INV.PAP.TYPE="LROLL" AND SHEET.OK
            CASE 1
               ERRMSG="Invalid product type. Try again! "
               GOSUB 91000
               GOTO 10100
         END CASE
         IF INV.M.LINE="FNGD" THEN
            ERRMSG="Product is part of finished goods inventory. Try again! "
            GOSUB 91000
            GOTO 10100
         END
         IF OPER.MATL.REQ="Y" AND OPER.PLINE # "" THEN
            LOCATE INV.LINE IN OPER.PLINE<1>,1 SETTING PL ELSE PL=0
            IF PL=0 THEN
               ERRMSG="Invalid product for this operation. Try again! "
               GOSUB 91000
               GOTO 10100
            END
         END
         MATREAD CATG.REC FROM CATEGORY,CONO:INV.LINE ELSE
            ERRMSG="Cannot locate product line. Try again! "
            GOSUB 91000
            GOTO 10100
         END
         IF CATG.TRACK.QOH # "Y" AND (CATG.COST.TYPE="FI" OR CATG.COST.TYPE="LC") THEN
            ERRMSG=CATG.COST.TYPE:" is not a valid costing method when PLINE does not track QOH"
            GOSUB 91000
            GOTO 10100
         END
         SCV.REC(29)<1>="Product:"
         SCV.REC(30)<1>=INV.FULL.DESC
         ECD.NUM=29;ECD.ACTION=5;CALL SCRN.EDIT
         ECD.NUM=30;ECD.ACTION=5;CALL SCRN.EDIT
10130 *
         IF WHSE="" THEN
            MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE MAT JOB.REC=''
            WH.CNT=COUNT(INV.WHSE.CODE,VM)+(INV.WHSE.CODE # "")
            BEGIN CASE
               CASE WH.CNT=1
                  WHSE=INV.WHSE.CODE
               CASE INV.PAP.TYPE="SHEET" AND SHEET.OK AND DEPT.WHSE # ""
                  LOCATE CCTR.DEPT IN DEPT.WHSE<1>,1 SETTING W ELSE
                     ERRMSG="Cannot locate warehouse for specified department. Try again! "
                     GOSUB 91000
                     GOTO 10100
                  END
                  WHSE=DEPT.WHSE<2,W>
               CASE INV.PAP.TYPE="PCOAT" AND SHEET.OK AND DEPT.WHSE # ""
                  LOCATE CCTR.DEPT IN DEPT.WHSE<1>,1 SETTING W ELSE
                     ERRMSG="Cannot locate warehouse for specified department. Try again! "
                     GOSUB 91000
                     GOTO 10100
                  END
                  WHSE=DEPT.WHSE<2,W>
               CASE INV.PAP.TYPE="LROLL" AND SHEET.OK AND DEPT.WHSE # ""
                  LOCATE CCTR.DEPT IN DEPT.WHSE<1>,1 SETTING W ELSE
                     ERRMSG="Cannot locate warehouse for specified department. Try again! "
                     GOSUB 91000
                     GOTO 10100
                  END
                  WHSE=DEPT.WHSE<2,W>
               CASE 1
                  SCV.REC(45)<1>=DTM.WHSE<1,REF.NO>
                  ECD.NUM=45;ECD.ACTION=4;CALL SCRN.EDIT
                  IF ECD.RET.VALUE="END" THEN GOTO 10990
                  LOCATE ECD.RET.VALUE IN INV.WHSE.CODE<1>,1 SETTING FND ELSE
                     ERRMSG="Invalid Warehouse. Try again! "
                     GOSUB 91000
                     GOTO 10130
                  END
                  WHSE=ECD.RET.VALUE
            END CASE
         END
         MATREAD WHSE.REC FROM WAREHOUSE,CONO:WHSE ELSE MAT WHSE.REC=''
         IF WHS.DIV # JOB.DIV AND WHS.DIV # "00" THEN
            ERRMSG="Warehouse Div ":WHS.DIV:" does not match Job Div ":JOB.DIV
            GOSUB 91000
            GOTO 10100
         END
         MATREAD IWH.REC FROM INV.WHSE,CONO:PROD:"!":WHSE ELSE
            MAT IWH.REC=""
            ERRMSG="Cannot locate warehouse item. Try again! "
            GOSUB 91000
            WHSE=""
            LOCAT=""
            GOTO 10130
         END
         IF IWH.ON.HAND+0=0 AND CATG.TRACK.QOH="Y" THEN
            ERRMSG="Quantity on-hand is zero. Try again! "
            GOSUB 91000
            GOTO 10100
         END
10140 *
         IF LOCAT="" THEN
            LO.CNT=COUNT(IWH.LOC,VM)+(IWH.LOC # "")
            BEGIN CASE
               CASE LO.CNT=1
                  LOCAT=IWH.LOC
               CASE 1
                  SCV.REC(46)<1>=DTM.LOC<1,REF.NO>
                  ECD.NUM=46;ECD.ACTION=4;CALL SCRN.EDIT
                  IF ECD.RET.VALUE="END" THEN GOTO 10990
                  LOCATE ECD.RET.VALUE IN IWH.LOC<1>,1 SETTING FND ELSE
                     ERRMSG="Invalid Location. Try again! "
                     GOSUB 91000
                     GOTO 10140
                  END
                  LOCAT=ECD.RET.VALUE
            END CASE
         END
         MATREAD IWLO.REC FROM INV.WHSE.LOC,CONO:PROD:"!":WHSE:"!":LOCAT ELSE
            MAT IWLO.REC=""
         END
10150 *
         RFND=0
         IF SERIAL="" AND CATG.TRK.LVL='S' THEN
            RS.CNT=DCOUNT(IWLO.SERIAL,VM)
            BEGIN CASE
               CASE RS.CNT=0
                  RFND=0
               CASE RS.CNT=1
                  SERIAL=IWLO.SERIAL
                  RFND=1
               CASE 1
                  SCV.REC(44)<1>=DTM.SERIAL<1,REF.NO>
                  ECD.NUM=44;ECD.ACTION=4;CALL SCRN.EDIT
                  IF ECD.RET.VALUE="END" THEN GOTO 10990
                  LOCATE ECD.RET.VALUE IN IWLO.SERIAL<1>,1 SETTING RFND ELSE
                     ERRMSG='Invalid Serial ID'
                     GOSUB 91000
                     GOTO 10100
                  END
                  SERIAL=ECD.RET.VALUE
            END CASE
*T26556 v
*        END ELSE
*           LOCATE SERIAL IN IWLO.SERIAL<1>,1 SETTING RFND ELSE
*              ERRMSG='Invalid Serial ID'
*              SERIAL=""
*              GOSUB 91000
*              GOTO 10100
*           END
*T26556 ^
         END
   END CASE
*----- GET QTYPE
10200 *
   BEGIN CASE
      CASE PROD="" OR PROD="0"
      CASE INV.PAP.TYPE='ROLL'
         IF DTM.RS.QTYPE<1,REF.NO>[1,1]="W" OR DTM.RS.QTYPE<1,REF.NO>="DR" THEN
            SCV.REC(47)<1>=DTM.RS.QTYPE<1,REF.NO>
         END
         ECD.NUM=47; ECD.VALDAT="WU,WR,DR"; ECD.DEFAULT="WU"
         ECD.HMSG='WU = Weight used, WR = Weight remaining, DR = Diameter remaining'
         ECD.ACTION=4; CALL SCRN.EDIT
         IF ECD.RET.VALUE='END' THEN  GOTO 10990
         QTYPE=ECD.RET.VALUE
      CASE INV.PAP.TYPE='LROLL' OR INV.PAP.TYPE='PCOAT'
         IF DTM.RS.QTYPE<1,REF.NO>[1,1]="S" OR DTM.RS.QTYPE<1,REF.NO>="DR" THEN
            SCV.REC(47)<1>=DTM.RS.QTYPE<1,REF.NO>
         END
         ECD.NUM=47; ECD.VALDAT="SU,DR"; ECD.DEFAULT="SU"
         ECD.HMSG='SU = Quantity used, DR = Diameter remaining' 
         ECD.ACTION=4; CALL SCRN.EDIT
         IF ECD.RET.VALUE='END' THEN  GOTO 10990
         QTYPE=ECD.RET.VALUE
      CASE INV.PAP.TYPE='SHEET'
         IF DTM.RS.QTYPE<1,REF.NO>[1,1] = "S" THEN
            SCV.REC(47)<1>=DTM.RS.QTYPE<1,REF.NO>
         END
         ECD.NUM=47; ECD.VALDAT="SU,SR"; ECD.DEFAULT="SU"
         ECD.HMSG='SU = Sheets used, SR = Sheets remaining' 
         ECD.ACTION=4; CALL SCRN.EDIT
         IF ECD.RET.VALUE='END' THEN GOTO 10990
         QTYPE=ECD.RET.VALUE
   END CASE
*---- GET MATERIAL QUANTITY
10300 *
   BEGIN CASE
      CASE PROD="" OR PROD="0"
         QTY=""
         STK.QTY=""
         P_X  = 63 ; P_Y = SLN+1 ; P_VALUE = QTY "R#8" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CASE 1
         QTY=""
         STK.QTY=""
         X=63;Y=SLN+1;TYP=3;MAXL=8;HMSG="Enter material quantity used."
         BEGIN CASE
            CASE INV.UNIT<1,2>='SHT' AND INV.UNIT<1,3>='LBS'
               ICR.DV1=INV.M.WT; ICR.MT1=1; ICR.DV2=1; ICR.CNV="MD0"
               MAXV=99999999
            CASE INV.UNIT<1,2>='PC' AND INV.UNIT<1,3>='MSI'
               ICR.DV1=INV.PAP.WIDTH/100; ICR.MT1=10; ICR.DV2=1; ICR.CNV="MD0"
               IF QTYPE="DR" THEN TYP=4; SCALER=2
               MAXV=99999999
            CASE INV.UNIT<1,2>='FT' AND INV.UNIT<1,3>='MSI'
               ICR.DV1=INV.PAP.WIDTH/100; ICR.MT1=100; ICR.DV2=12; ICR.CNV="MD0"
               IF QTYPE="DR" THEN TYP=4; SCALER=2
               MAXV=99999999
            CASE 1
               ICR.DV1=10; ICR.MT1=1; ICR.DV2=1; ICR.CNV="MD2"
               TYP=4;SCALER=2
         END CASE
         IF DTM.QTY<1,REF.NO> # "" THEN
            O.R="O"
            IF ICR.CNV="MD0" AND QTYPE # "DR" THEN
               DEFAULT=INT(((DTM.QTY<1,REF.NO>/ICR.DV1)*ICR.MT1)/ICR.DV2+0.5)
            END ELSE
               DEFAULT=OCONV(INT(DTM.QTY<1,REF.NO> / 10),"MD2")
            END
         END
         CALL EDIT.SUB
         IF VALUE='END' THEN GOTO 10990
         IF ICR.CNV="MD0" AND QTYPE # "DR" THEN
            QTY=INT(((VALUE/ICR.MT1)*ICR.DV1)*ICR.DV2+0.5)
         END ELSE
            QTY=VALUE * 10
         END
         IF CATG.TRACK.QOH="Y" THEN
            BEGIN CASE
               CASE RFND AND QTY <= ISTK.CUR.QTY
               CASE NOT(RFND) AND QTY <= IWLO.LOC.ON.HAND
               CASE 1
                  ERRMSG="Quantity exceeds on-hand balance. Try again! "
                  GOSUB 91000
                  GOTO 10300
            END CASE
         END
         BEGIN CASE
            CASE INV.UNIT<1,2>="SHT" AND INV.UNIT<1,3>="LBS"
               STK.QTY=VALUE
            CASE DTM.RS.QTYPE<1,REF.NO>="DR"
               STK.QTY=""
            CASE INV.UNIT<1,2>='PC' AND INV.UNIT<1,3>='MSI'
               STK.QTY=VALUE
            CASE INV.UNIT<1,2>='FT' AND INV.UNIT<1,3>='MSI'
               STK.QTY=VALUE
            CASE 1
               STK.QTY=""
         END CASE
   END CASE
*---- GET TRANSACTION DATE AND TIME
   BARCODE.FLAG = 0
   BEGIN CASE
      CASE PROD="" OR PROD="0"
         TS=DTM.TIME.STAMP<1,REF.NO>
         IF TS # "" THEN
            DATE.USED=INT(TS/100000)
            TIME.USED=MOD(TS,100000)
         END
      CASE INV.PAP.TYPE='ROLL'
         BARCODE.FLAG=1
      CASE INV.PAP.TYPE='LROLL'
         BARCODE.FLAG=1
      CASE INV.PAP.TYPE='PCOAT'
         BARCODE.FLAG=1
      CASE INV.PAP.TYPE='SHEET'
         BARCODE.FLAG=1
   END CASE
   IF BARCODE.FLAG THEN
*---- GET DATE USED  
10400 *
      TS=DTM.TIME.STAMP<1,REF.NO>
      IF TS = "" THEN
         SCV.REC(48)<1> = ""
      END ELSE
         SCV.REC(48)<1>=INT(TS/100000)
      END
      ECD.NUM=48; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 10990
      IF ECD.RET.VALUE > DATE() THEN
         ERRMSG = "Date out of range. Try again! "
         GOSUB 91000
         GOTO 10400
      END
      DATE.USED=ECD.RET.VALUE
*---- GET TIME USED
10500 *
      TS=DTM.TIME.STAMP<1,REF.NO>
      IF TS = "" THEN
         SCV.REC(49)<1> = STPT
      END ELSE
         TSTIME=OCONV(MOD(TS,100000),"MTS")
         SCV.REC(49)<1>=TSTIME[1,2]:TSTIME[4,2]
      END
      ECD.NUM=49; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 10990
      HH=ECD.RET.VALUE[1,2]; MM=ECD.RET.VALUE[3,4]
      IF HH < 0 OR HH > 23 OR MM < 0 OR MM > 59 THEN
         ERRMSG = 'Invalid time. Enter "HHMM" '
         GOSUB 91000
         GOTO 10500
      END
      TIME.USED=ICONV((HH:":":MM:":00"),"MTS")
   END
   GOTO 99999
*---- BACKOUT ENTERED DATA
10990 *
   ESTAT="END"
   VALUE="END"
   GOTO 99999
*
*---- ERROR ROUTINE
*
91000 *
   ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
*
*---- END OF PROGRAM
*
99999 *
   RETURN
END
