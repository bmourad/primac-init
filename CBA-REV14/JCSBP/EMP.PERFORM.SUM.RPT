*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [11.0]
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - EMP.PERFORM.SUM.RPT
* AUTHOR      - LANNY ROSS
* DATE        - 02/20/03
* DESCRIPTION - Prints the summary employee performance report.
*
*T27307 lross 02/20/2003 * Create summary report from detail version.
*T27307 adelgado 03/24/2003 * Assist in the development of summary rpt.
*T26928 cmykleb 10/21/2002 * Concurrent time entries are being zeroed
*T26493 lross 05/02/2006 * Change report to use rpt # from proc.
*********************************************************************
*
***** INSERT FILES EQUEATES
*
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
  OPEN '','JOB.TIME' TO JOB.TIME ELSE
    ERRMSG ='JOB.FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','COST.CNTR' TO COST.CNTR ELSE
    ERRMSG = 'COST.CNTR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','EMPLOYEE' TO EMPLOYEE ELSE
    ERRMSG = 'EMPLOYEE FILE IS MISSING'
    GOTO 93000
  END
*
**** READ PROC VALUES
*
  PROCREAD ID ELSE
    ERRMSG = 'CANNOT PERFORM PROCREAD'
    GOTO 93000
  END
  CONO = ID<1>
*T26493 v
*     CONO.NAME = ID<2>
*     REPORT.NAME = "EMPLOYEE.PERFORMANCE.DETAIL"
*     REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
*T26493 ^
  REPORT.DATE = DATE()
  HD1 = "" ; HD2 = ""
  CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
  ST.DATE = ID<4>
  EN.DATE = ID<5>
  ST.PER = ID<6>
  EN.PER = ID<7>
  STD.FLG = ID<8>
  FILE.FLG = ID<9>
  DIVISION = ID<10>
  ST.PER  = ST.PER[5,2]:"/":ST.PER[1,4]
  EN.PER  = EN.PER[5,2]:"/":EN.PER[1,4]
*
**** INITIALIZATION
*
  LINE.COUNT = 99
  PAGE.NO = 0
  PRT.FLG = 1
  UNDER.LINE1 = "-------------------------------------- "
  UNDER.LINE2 = "-------- -------- ------- -------- ------- ------- ------- ------- ------- ------ ----- -----"
  UNDER.LINE3 = "======== ======== ======= ========                 ======= ======= ======= ====== ===== ====="
  UNDER.LINE4 = "-------- -------- ------- --------                 ------- ------- ------- ------ ----- -----"
  P.OPER = ""
  MAT CCTR.REC = ""
*
***** TOTAL TABLE FOR EMPLOYEE
*
  DIM TOT.EMP(11)
  EQU EMP.ACT.HRS     TO TOT.EMP(1)
  EQU EMP.STD.HRS     TO TOT.EMP(2)
  EQU EMP.EFF.HRS     TO TOT.EMP(3)
  EQU EMP.CNC.HRS     TO TOT.EMP(4)
  EQU EMP.ACT.IMP     TO TOT.EMP(5)
  EQU EMP.STD.IMP     TO TOT.EMP(6)
  EQU EMP.CHG.HRS     TO TOT.EMP(7)
  EQU EMP.N.C.HRS     TO TOT.EMP(8)
  EQU EMP.O.T.HRS     TO TOT.EMP(9)
  EQU EMP.D.T.HRS     TO TOT.EMP(10)
  EQU EMP.OPER.CNT    TO TOT.EMP(11)
  MAT TOT.EMP = ''
*
***** TOTAL TABLE FOR CCTR
*
  DIM TOT.CCTR(11)
  EQU CCTR.ACT.HRS     TO TOT.CCTR(1)
  EQU CCTR.STD.HRS     TO TOT.CCTR(2)
  EQU CCTR.EFF.HRS     TO TOT.CCTR(3)
  EQU CCTR.CNC.HRS     TO TOT.CCTR(4)
  EQU CCTR.ACT.IMP     TO TOT.CCTR(5)
  EQU CCTR.STD.IMP     TO TOT.CCTR(6)
  EQU CCTR.CHG.HRS     TO TOT.CCTR(7)
  EQU CCTR.N.C.HRS     TO TOT.CCTR(8)
  EQU CCTR.O.T.HRS     TO TOT.CCTR(9)
  EQU CCTR.D.T.HRS     TO TOT.CCTR(10)
  EQU CCTR.OPER.CNT    TO TOT.CCTR(11)
  MAT TOT.CCTR = ''
*
*---- TOTAL TABLE FOR OPER
*
  DIM TOT.OPER(11)
  EQU OPER.ACT.HRS     TO TOT.OPER(1)
  EQU OPER.STD.HRS     TO TOT.OPER(2)
  EQU OPER.EFF.HRS     TO TOT.OPER(3)
  EQU OPER.CNC.HRS     TO TOT.OPER(4)
  EQU OPER.ACT.IMP     TO TOT.OPER(5)
  EQU OPER.STD.IMP     TO TOT.OPER(6)
  EQU OPER.CHG.HRS     TO TOT.OPER(7)
  EQU OPER.N.C.HRS     TO TOT.OPER(8)
  EQU OPER.O.T.HRS     TO TOT.OPER(9)
  EQU OPER.D.T.HRS     TO TOT.OPER(10)
  EQU OPER.CNT         TO TOT.OPER(11)
  MAT TOT.OPER = ''
*
***** READ AND PROCESS FIRST RECORD
*
  PRINTER ON
100 *
  READNEXT KEY ELSE GOTO 99999
  MATREAD JLB.REC FROM JOB.TIME,KEY ELSE GOTO 100
  MATREAD EMP.REC FROM EMPLOYEE, CONO:JLB.EMP ELSE
    EMP.LAST.NAME = '??????????'
    EMP.FRST.NAME = '??????????'
  END
  EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
  EMP = JLB.EMP
  PREV.EMP = JLB.EMP
  PREV.DIV = JLB.DIV
  PREV.DEPT = FIELD(KEY,"!",2)
  PREV.CCTR = FIELD(KEY,"!",3)
  PREV.OPER = JLB.OPER
  OPER=JLB.OPER
  CCTR=PREV.CCTR
  PREV.DATA = OCONV(KEY,'G!3'):"!":JLB.OPER
  PREV.FCTR = JLB.FCTR
  DPNO = PREV.DEPT
  CCNO = PREV.CCTR
  JOBNO = FIELD(KEY,"!",1)[4,99]
  CNT = 0
  CONO = KEY[1,3]
  MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
    MAT CCTR.REC = ""
    GOTO 100
  END
  GOSUB 500
  IF EXIT.FLG THEN GOTO 100
  GOSUB 1000
*
***** READ AND PROCESS ALL RECORDS
*
  DATA = 0
  LOOP
    READNEXT KEY ELSE DATA = 1
  WHILE DATA = 0 DO
    MATREAD JLB.REC FROM JOB.TIME,KEY ELSE GOTO 111
    DPNO = FIELD(KEY,"!",2)
    CCNO = FIELD(KEY,"!",3)
    JOBNO = FIELD(KEY,"!",1)[4,99]
    CONO = KEY[1,3]
    MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
      MAT CCTR.REC = ""
      GOTO 111
    END
    EMP = JLB.EMP
    OPER=JLB.OPER
    CCTR=CCNO
    GOSUB 500
    IF EXIT.FLG THEN GOTO 111
    GOSUB 400
    GOSUB 1000
111 *
  REPEAT
  GOSUB 4002
  GOSUB 4003
  GOSUB 4004
  GOTO 99999
*
**** FIND BREAK AND PRINT TOTALS
*
400 *
  IF PREV.EMP # JLB.EMP THEN
    GOSUB 4002
    GOSUB 4003
    GOSUB 4004
    IF LINE.COUNT+1 > 53 THEN
      LINE.COUNT = 0
      GOSUB 9000
    END ELSE
      PRINT
      LINE.COUNT = LINE.COUNT + 1
    END
    PRT.FLG = 1
  END
  IF PREV.CCTR # CCNO THEN
    GOSUB 4002
    GOSUB 4003
    IF LINE.COUNT+1 > 53 THEN
      LINE.COUNT = 0
      GOSUB 9000
    END ELSE
      PRINT
      LINE.COUNT = LINE.COUNT + 1
    END
    PRT.FLG = 1
  END
  IF PREV.OPER # OPER THEN
    GOSUB 4002
    PRT.FLG = 1
  END
  RETURN
*
500 *
*
  EXIT.FLG = 0
  LOCATE JLB.OPER IN CCTR.OPER<1>,1 SETTING SI ELSE
    EXIT.FLG = 1; GOTO 599
  END
*T26928 v
*  IF JLB.CNCR + 0 # 0 THEN
   IF JLB.CNCR + 0 # 0 AND JLB.CNCR = JLB.HRS AND JLB.RC.DESC = 'GANG ALLOC' THEN
*T26928 ^
    JLB.HRS = 0
    JLB.IMP = 0
    JLB.HRS.STD = 0
    JLB.IMP.STD = 0
    CCTR.OPER.STD.HRS<1,SI> = 0
    CCTR.OPER.STD.IMP<1,SI> = 0
  END
  IF OCONV(KEY,'G!3'):"!":JLB.OPER = PREV.DATA AND JLB.HRS + 0 = 0 THEN JLB.HRS.STD = 0; JLB.IMP.STD = 0; CCTR.OPER.STD.HRS<1,SI> = 0; CCTR.OPER.STD.IMP<1,SI> = 0
  PREV.DATA = OCONV(KEY,'G!3'):"!":JLB.OPER
*
  IF FILE.FLG = 'C' THEN
    IF STD.FLG = 'N' THEN
      IF CCTR.OPER.STD.HRS<1,SI>+0 = 0 AND CCTR.OPER.STD.IMP<1,SI>+0 = 0 THEN EXIT.FLG=1; GOTO 599
    END
    HRS.STD = CCTR.OPER.STD.HRS<1,SI> + 0
    IMP.STD = CCTR.OPER.STD.IMP<1,SI> + 0
  END ELSE
    IF STD.FLG = 'N' THEN
      IF JLB.HRS.STD+0 = 0 AND JLB.IMP.STD+0 = 0 THEN EXIT.FLG=1; GOTO 599
    END
    HRS.STD = JLB.HRS.STD + 0
    IMP.STD = CCTR.OPER.STD.IMP<1,SI> + 0
  END
599 *
  RETURN
*
**** ADD UP ALL TOTALS
*
1000 *
  CNT = CNT + 1
  STD.HRS = 0
  EFF.HRS = 0
  IMP.HRS = 0
  STD.IMP = 0
  N.C.HRS=0
  CHG.HRS=0
  O.T.HRS=0
  D.T.HRS=0
*
  JLB.HRS = JLB.HRS + 0
  JLB.IMP = JLB.IMP + 0
  IF NOT(NUM(JLB.CNCR)) THEN JLB.CNCR = 0
  JLB.CNCR = JLB.CNCR + 0
  IF JLB.HRS + 0 = 0 THEN
    HRS.STD = 0
    IMP.STD = 0
  END
  IF JLB.HRS+0 >= 0 THEN SIGN = 1 ELSE SIGN = (-1)
  IF CCTR.OPER.TYPE<1,SI> = 'V' OR CCTR.OPER.TYPE<1,SI> = 'U' THEN
    IF JLB.HRS+0 # 0 THEN
      IMP.HRS = INT(JLB.IMP / JLB.HRS)
      IMP.HRS = IMP.HRS * SIGN
    END
  END
  IF FILE.FLG = "C" THEN
    BEGIN CASE
      CASE CCTR.OPER.TYPE<1,SI> = "U"
        BEGIN CASE
          CASE HRS.STD+0 # 0
            STD.HRS = INT(JLB.IMP * HRS.STD / 100)
            STD.IMP = INT(100 / HRS.STD)
          CASE IMP.STD+0 # 0
            STD.HRS = INT(JLB.IMP / IMP.STD)
            STD.IMP = IMP.STD
          CASE 1
            STD.HRS = 0
            STD.IMP = 0
        END CASE
      CASE CCTR.OPER.TYPE<1,SI> = "V"
        IF IMP.STD+0 # 0 THEN
          STD.HRS = INT(JLB.IMP / IMP.STD)
          STD.IMP = IMP.STD
        END ELSE
          STD.HRS = 0
          STD.IMP = 0
        END
      CASE 1
        STD.HRS = HRS.STD + 0
        STD.IMP = 0
    END CASE
  END ELSE
    STD.HRS = HRS.STD + 0
    STD.IMP = IMP.STD + 0
  END
*
  IF JLB.CNCR + 0 = 0 THEN
    IF JLB.TYPE = "N" OR JLB.TYPE = "S" THEN
      N.C.HRS = N.C.HRS + JLB.HRS
    END ELSE 
      CHG.HRS = CHG.HRS + JLB.HRS
    END
    BEGIN CASE
      CASE JLB.FCTR = "02"
        O.T.HRS = O.T.HRS + JLB.HRS
      CASE JLB.FCTR = "03"
        D.T.HRS = D.T.HRS + JLB.HRS
    END CASE
  END
*
  OPER.ACT.HRS += JLB.HRS
  OPER.STD.HRS += STD.HRS
  OPER.CNC.HRS += JLB.CNCR
* OPER.ACT.IMP += IMP.HRS
  OPER.ACT.IMP += JLB.IMP
  OPER.STD.IMP += STD.IMP
  OPER.CHG.HRS += CHG.HRS
  OPER.N.C.HRS += N.C.HRS
  OPER.O.T.HRS += O.T.HRS
  OPER.D.T.HRS += D.T.HRS
  P.OPER = OPER
  IF CCTR.OPER.TYPE<1,SI> = "V" OR CCTR.OPER.TYPE<1,SI> = "U" THEN
    OPER.CNT = OPER.CNT + 1
  END
  RETURN
*
*---- ADD TOTAL FOR CCTR
*
1001*
  CCTR.ACT.HRS = CCTR.ACT.HRS + OPER.ACT.HRS
  CCTR.STD.HRS = CCTR.STD.HRS + OPER.STD.HRS
  CCTR.CNC.HRS = CCTR.CNC.HRS + OPER.CNC.HRS
  CCTR.ACT.IMP = CCTR.ACT.IMP + OPER.ACT.IMP
  CCTR.STD.IMP = CCTR.STD.IMP + OPER.STD.IMP
  CCTR.CHG.HRS = CCTR.CHG.HRS + OPER.CHG.HRS
  CCTR.N.C.HRS = CCTR.N.C.HRS + OPER.N.C.HRS
  CCTR.O.T.HRS = CCTR.O.T.HRS + OPER.O.T.HRS
  CCTR.D.T.HRS = CCTR.D.T.HRS + OPER.D.T.HRS
  CCTR.OPER.CNT = CCTR.OPER.CNT + OPER.CNT
  RETURN
*
*---- ADD TOTAL FOR EMP
*
1002*
  EMP.ACT.HRS = EMP.ACT.HRS + CCTR.ACT.HRS
  EMP.STD.HRS = EMP.STD.HRS + CCTR.STD.HRS
  EMP.CNC.HRS = EMP.CNC.HRS + CCTR.CNC.HRS
  EMP.ACT.IMP = EMP.ACT.IMP + CCTR.ACT.IMP
  EMP.STD.IMP = EMP.STD.IMP + CCTR.STD.IMP
  EMP.CHG.HRS = EMP.CHG.HRS + CCTR.CHG.HRS
  EMP.N.C.HRS = EMP.N.C.HRS + CCTR.N.C.HRS
  EMP.O.T.HRS = EMP.O.T.HRS + CCTR.O.T.HRS
  EMP.D.T.HRS = EMP.D.T.HRS + CCTR.D.T.HRS
  EMP.OPER.CNT = EMP.OPER.CNT + CCTR.OPER.CNT
  RETURN
*---- PRINT TOTAL FOR OPER
*
4002*
  IF LINE.COUNT+1 > 53 THEN
    LINE.COUNT = 0
    GOSUB 9000
    CCTR.PRT.FLG = 1
  END
  IF CCTR.PRT.FLG = 1 THEN
    IF LINE.COUNT+2 > 53 THEN
      LINE.COUNT = 0
      GOSUB 9000
    END
    CCTR.PRT.FLG = 0
    CCTR.KEY = CONO:PREV.CCTR
    CCTR.PRT.DESC = PREV.CCTR:"  ":OCONV(CCTR.KEY,"TCOST.CNTR;X;1;1")
    CD.LINE = ""
    CD.LINE = CCTR.PRT.DESC
    PRINT CD.LINE
    PRINT
    LINE.COUNT = LINE.COUNT + 2
  END
  OPER.KEY = CONO:PREV.OPER
  OPER.PRT.DESC = "   ": PREV.OPER:"  ":OCONV(OPER.KEY,"TOPERATION;X;1;1")
  OP.LINE = ''
  OP.LINE = OP.LINE :OPER.PRT.DESC "L#38":" "
  OP.LINE = OP.LINE :OCONV(OPER.ACT.HRS, "MD2") "R#8":' '
  OP.LINE = OP.LINE :OCONV(OPER.STD.HRS, "MD2") "R#8":' '
  IF OPER.ACT.HRS > 0 THEN
    OPER.EFF.HRS = INT(OPER.STD.HRS * 100/OPER.ACT.HRS * 100)
  END ELSE
    OPER.EFF.HRS = 0
  END
  OP.LINE = OP.LINE :OCONV(OPER.EFF.HRS, "MD2") "R#7":' '
  OP.LINE = OP.LINE :OCONV(OPER.CNC.HRS, "MD2") "R#8":' '
  IF OPER.ACT.HRS+0 >= 0 THEN SIGN = 1 ELSE SIGN = (-1)
  IF OPER.ACT.HRS+0 # 0 THEN
    IMP.HRS = INT(OPER.ACT.IMP/OPER.ACT.HRS)
    IMP.HRS = IMP.HRS * SIGN
  END ELSE
    IMP.HRS = 0
  END
  OP.LINE = OP.LINE :OCONV(IMP.HRS, "MD0") "R#7":' '
  IF OPER.CNT + 0 = 0 THEN
    IMP.HR = INT(OPER.STD.IMP/1)
  END ELSE
    IMP.HR = INT(OPER.STD.IMP/OPER.CNT)
  END
  OP.LINE = OP.LINE :OCONV(IMP.HR, "MD0") "R#7":' '
  HRS = INT(OPER.CHG.HRS/10)
  OP.LINE = OP.LINE :OCONV(HRS, "MD1") "R#7":' '
  HRS = INT(OPER.N.C.HRS/10)
  OP.LINE = OP.LINE :OCONV(HRS, "MD1") "R#7":' '
  OPER.TOT.HRS = OPER.CHG.HRS + OPER.N.C.HRS
  HRS = INT(OPER.TOT.HRS/10)
  OP.LINE = OP.LINE :OCONV(HRS, "MD1") "R#7":' '
  IF OPER.CHG.HRS * 1 # 0 AND OPER.TOT.HRS * 1 # 0 THEN
    OPER.CHG.PER = INT(OPER.CHG.HRS / OPER.TOT.HRS * 1000)
  END ELSE
    OPER.CHG.PER = 0
  END
  OP.LINE = OP.LINE :OCONV(OPER.CHG.PER, "MD1") "R#6":' '
  HRS = INT(OPER.O.T.HRS/10)
  OP.LINE = OP.LINE :OCONV(HRS, "MD1") "R#5":' '
  HRS = INT(OPER.D.T.HRS/10)
  OP.LINE = OP.LINE :OCONV(HRS, "MD1") "R#5"
  PRINT OP.LINE
  LINE.COUNT = LINE.COUNT + 1
  PREV.OPER = OPER
  CNT = 0
  GOSUB 1001
  MAT TOT.OPER = ''
  RETURN
*
*---- PRINT TOTAL FOR CCTR
*
4003*
  IF LINE.COUNT+2 > 53 THEN
    LINE.COUNT = 0
    GOSUB 9000
  END
  U.LINE = ''
  U.LINE = SPACE(39):UNDER.LINE4
  PRINT U.LINE
  CCTR.PRT.DESC = "COST CENTER TOTAL"
  CC.LINE = ''
  CC.LINE = CC.LINE :CCTR.PRT.DESC "R#38":" "
  CC.LINE = CC.LINE :OCONV(CCTR.ACT.HRS, "MD2") "R#8":' '
  CC.LINE = CC.LINE :OCONV(CCTR.STD.HRS, "MD2") "R#8":' '
  IF CCTR.ACT.HRS > 0 THEN
    CCTR.EFF.HRS = INT((CCTR.STD.HRS/CCTR.ACT.HRS) * 10000)
  END ELSE
    CCTR.EFF.HRS = 0
  END
  CC.LINE = CC.LINE :OCONV(CCTR.EFF.HRS, "MD2") "R#7":' '
  CC.LINE = CC.LINE :OCONV(CCTR.CNC.HRS, "MD2") "R#8":' '
  CC.LINE = CC.LINE :SPACE(8)
  CC.LINE = CC.LINE :SPACE(8)
  HRS = INT(CCTR.CHG.HRS/10)
  CC.LINE = CC.LINE :OCONV(HRS, "MD1") "R#7":' '
  HRS = INT(CCTR.N.C.HRS/10)
  CC.LINE = CC.LINE :OCONV(HRS, "MD1") "R#7":' '
  CCTR.TOT.HRS = CCTR.CHG.HRS + CCTR.N.C.HRS
  HRS = INT(CCTR.TOT.HRS/10)
  CC.LINE = CC.LINE :OCONV(HRS, "MD1") "R#7":' '
  IF CCTR.CHG.HRS * 1 # 0 AND CCTR.TOT.HRS * 1 # 0 THEN
    CCTR.CHG.PER = INT(CCTR.CHG.HRS / CCTR.TOT.HRS * 1000)
  END ELSE
    CCTR.CHG.PER = 0
  END
  CC.LINE = CC.LINE :OCONV(CCTR.CHG.PER, "MD1") "R#6":' '
  HRS = INT(CCTR.O.T.HRS/10)
  CC.LINE = CC.LINE :OCONV(HRS, "MD1") "R#5":' '
  HRS = INT(CCTR.D.T.HRS/10)
  CC.LINE = CC.LINE :OCONV(HRS, "MD1") "R#5":' '
  PRINT CC.LINE
  LINE.COUNT = LINE.COUNT + 2
  PREV.CCTR = CCTR
  CNT = 0
  GOSUB 1002
  MAT TOT.CCTR = ''
  CCTR.PRT.FLG = 1
  RETURN
*
*---- PRINT TOTAL FOR EMP
*
4004*
  IF LINE.COUNT+2 > 53 THEN
    LINE.COUNT = 0
    GOSUB 9000
  END
  U.LINE = ""
  U.LINE = U.LINE :SPACE(39)
  U.LINE = U.LINE :UNDER.LINE3
  PRINT U.LINE
  EMP.PRT.DESC = "TOTAL FOR ":EMP.NAME
  EM.LINE = ''
  EM.LINE = EM.LINE :EMP.PRT.DESC "R#38":" "
  EM.LINE = EM.LINE :OCONV(EMP.ACT.HRS, "MD2") "R#8":' '
  EM.LINE = EM.LINE :OCONV(EMP.STD.HRS, "MD2") "R#8":' '
  IF EMP.ACT.HRS > 0 THEN
    EMP.EFF.HRS = INT((EMP.STD.HRS/EMP.ACT.HRS) * 10000)
  END ELSE
    EMP.EFF.HRS = 0
  END
  EM.LINE = EM.LINE :OCONV(EMP.EFF.HRS, "MD2") "R#7":' '
  EM.LINE = EM.LINE :OCONV(EMP.CNC.HRS, "MD2") "R#8":' '
  EM.LINE = EM.LINE : SPACE(16)
  HRS = INT(EMP.CHG.HRS/10)
  EM.LINE = EM.LINE :OCONV(HRS, "MD1") "R#7":' '
  HRS = INT(EMP.N.C.HRS/10)
  EM.LINE = EM.LINE :OCONV(HRS, "MD1") "R#7":' '
  EMP.TOT.HRS = EMP.CHG.HRS + EMP.N.C.HRS
  HRS = INT(EMP.TOT.HRS/10)
  EM.LINE = EM.LINE :OCONV(HRS, "MD1") "R#7":' '
  IF EMP.CHG.HRS * 1 # 0 AND EMP.TOT.HRS * 1 # 0 THEN
    EMP.CHG.PER = INT(EMP.CHG.HRS / EMP.TOT.HRS * 1000)
  END ELSE
    EMP.CHG.PER = 0
  END
  EM.LINE = EM.LINE :OCONV(EMP.CHG.PER, "MD1") "R#6":' '
  HRS = INT(EMP.O.T.HRS/10)
  EM.LINE = EM.LINE :OCONV(HRS, "MD1") "R#5":' '
  HRS = INT(EMP.D.T.HRS/10)
  EM.LINE = EM.LINE :OCONV(HRS, "MD1") "R#5":' '
  PRINT EM.LINE
  EMP.CNT = 0
  CNT = 0
  LINE.COUNT = 99
  PREV.EMP = EMP
  MAT TOT.EMP = ''
  MATREAD EMP.REC FROM EMPLOYEE, CONO:EMP ELSE
    EMP.LAST.NAME = '??????????'
    EMP.FRST.NAME = '??????????'
  END
  EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
  RETURN
*
*---- PRINT HEADINGS
*
9000*
  PRINT CHAR(12):
  PAGE.NO = PAGE.NO + 1
  PRINT HD1:PAGE.NO
  PRINT HD2
  H.LINE = 'DIVISION : ':DIVISION
  H.LINE = H.LINE :SPACE(45):"FOR PERIOD ":ST.PER:" THROUGH ":EN.PER
  PRINT H.LINE
  PRINT SPACE(63):"FROM ":OCONV(ST.DATE,"D2/"):" TO ":OCONV(EN.DATE,"D2/")
  PRINT
  H.LINE = ""
  H.LINE = H.LINE:"EMPLOYEE : ":PREV.EMP:"  ":EMP.NAME
  PRINT H.LINE
  PRINT
  H.LINE = ''
  H.LINE = H.LINE :"COST CENTER                           "
  H.LINE = H.LINE :"   ACT      STD       %     CNCR      ACT     STD     CHG     N/C    TOTAL    %     O-T   D-T "
  PRINT H.LINE
  H.LINE = ""
  H.LINE = H.LINE :"   OPERATION                          "
  H.LINE = H.LINE :"  HOURS    HOURS     EFF    HOURS    QTY/HR  QTY/HR  HOURS   HOURS   HOURS  CH/HRS HOURS HOURS"
  PRINT H.LINE
  H.LINE = ''
  H.LINE = H.LINE :UNDER.LINE1:UNDER.LINE2
  PRINT H.LINE
  LINE.COUNT = LINE.COUNT + 10
  RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 *
  ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 *
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
  PRINTER OFF
END
