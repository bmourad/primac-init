      SUBROUTINE TARGET.PRINT.SUB (CONO, TARG.KEY, MAT TARG.REC, INTERPRET)
***************************************************************************
*
* PROGRAM  - TARGET.PRINT.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/25/86
*
* DESCRIPTION
*
* This subroutine is used to print the shop-floor bar-code templates on a
* GENICOM bar code printer.
*
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JCS.CPYLIB>TARGET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      EQU HT TO CHAR(9)
      EQU CR TO CHAR(13)
      EQU ESC TO CHAR(27)
      SET.TAB = ESC:"H"
      RTABS = ESC:"[3g"
      COLW = 28
      CFMT = "L#":COLW
      SPL = 5
      NW = 2                           ;* NARROW WIDTH IN 1/2 POINTS
      WW = 5                           ;* WIDE WIDTH IN 1/2 POINTS
      IW = 3                           ;* INTERCHAR WIDTH IN 1/2 POINTS
      BARCODE.PAR = ESC:"[4;5;0;":NW:";":WW:";":NW:";":WW:";":IW:"}"
      BARCODE.ON = ESC:"[3t"
      BARCODE.OFF = ESC:"[0t"
      PRINTER ON
      PRINT CHAR(12):
      PRINT BARCODE.PAR:
*
*---- MAIN PROCESSING
*
100*
      PRINT ESC:"[13m":ESC:"[5m":TARG.NAME"L#35":ESC:"[0m":OCONV(DATE(),"D2-")
      PRINT
      FOR RP = 1 TO 10
         PL0="";PL1=""; PL2=""; PL3=""
         FOR CP = 1 TO 3
            PL0C="";PL1C=""; PL2C=""; PL3C=""
            DEFS = TARG.DEF<1,CP,RP>
            DSCS = TARG.DSC<1,CP,RP>
            CNT = COUNT(DEFS,"\")+1
            CSZ = INT(COLW/CNT)
            FMT = "L#":CSZ
            FOR PTR = 1 TO CNT
               DEF = TRIM(FIELD(DEFS,"\",PTR))
               PFX = DEF[1,1]
               DSC = SPACE(5):TRIM(FIELD(DSCS,"\",PTR))
               BEGIN CASE
               CASE PFX = "F"
                  BC = DEF[2,99]
               CASE PFX = "C"
                  BC = ""
               CASE PFX = "1" OR PFX = "4" OR PFX = "7"
                  BC = PFX
               CASE PFX = "2" OR PFX = "8"
                  GOSUB 60000
                  OPER = DEF[2,99]
                  BC = PFX:CCTR:OPER
               CASE PFX = "3" OR PFX = "6"
                  GOSUB 60000
                  BC = PFX:CCTR
               CASE 1
                  BC = ""
               END CASE
               IF CP = 1 AND PTR = 1 THEN
                  PL0C = SPACE(CSZ)
                  PL1C = DSC
                  PL2C = BARCODE.ON:BC:BARCODE.OFF
                  PL3C = SPACE(13):BC
               END ELSE
                  PL0C = PL0C:SET.TAB:SPACE(CSZ)
                  PL1C = PL1C:HT:DSC
                  PL2C = PL2C:HT:BARCODE.ON:BC:BARCODE.OFF
                  PL3C = PL3C:HT:SPACE(13):BC
               END
            NEXT PTR
            PL0 = PL0:PL0C
            PL1 = PL1:PL1C
            PL2 = PL2:PL2C
            PL3 = PL3:PL3C
         NEXT CP
         PRINT RTABS:
         PRINT ESC:"[11m":PL0
         PRINT ESC:"[14m":PL1
         PRINT ESC:"[11m":PL2
         PRINT
         PRINT
         IF INTERPRET = "Y" THEN
            PRINT ESC:"[12m":PL3
         END ELSE
            PRINT
         END
      NEXT RP
      PRINT ESC:"[11m"
      GOTO 99999
*
*---- GET PREVIOUS COST CENTER
*
60000*
      CCTR = ""
      SDONE = 0
      FOR CP1 = CP TO 1 STEP -1 UNTIL SDONE
         FOR RP1 = 10 TO 1 STEP -1 UNTIL SDONE
            IF CP1 < CP OR RP1 < RP THEN
               SDEF = TARG.DEF<1,CP1,RP1>
               SPFX = SDEF[1,1]
               IF SPFX = "C" THEN
                  CCTR = SDEF[2,3]
                  SDONE = 1
               END
            END
         NEXT RP1
      NEXT CP1
      IF CCTR = "" THEN CCTR = FIELD(TARG.KEY,"-",1)
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER OFF
      RETURN
   END
