*COPY>CPYLIB>COM1
**************************************************************
*
* REVISION      - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - SPOIL.STATS.RPT
*
* SYSTEM        - JCSBP
*
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 12/08/86
*
* DESCRIPTION   -
* This program prints the DCOST dollars for SPOIL.STATS file
* by labor,matl,osp,ship & misc.  Breaks (and totals) on
* division and company, by period.
*
* T26493 cmykleb 05/08/2002 * Change rpt to get the rpt # from the proc
*                             and use GET.PROG.HEAD for the heading.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>SPOIL.STATS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD XX ELSE
      ERRMSG="MUST BE RUN FROM A PROC"
      GOSUB 91000
      GOTO 99999
   END
   CONO = XX<1>
   CO.NAME = XX<2>
   PERIOD=XX<3>
   SP = SPACE(1); SP2 = SPACE(2); SP3 = SPACE(3)
*T26493 v
*  HLINE3 = SPACE(57): "FOR PERIOD " : PERIOD : " 'LL'"
*  HLINE1 = "REPORT NO: XXXX" : SPACE(26)
*  N=SPACE((50 - LEN(CO.NAME)) / 2)
*  HLINE1 = HLINE1 : N : CO.NAME : N
*  HLINE1 = HLINE1 : SPACE(10) : " 'TL'"
*  HLINE2 = SPACE(40) : "S P O I L A G E   S T A T I S T I C S   R E P O R T" : SPACE(31)
*  HLINE2 = HLINE2 : "PAGE : 'PL'"
   CO.NAME = ""
   RPT.NAME = ""
   RPT.NO = XX<2>
   HLINE1 = ""
   HLINE2 = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,RPT.NAME,RPT.NO,"",HLINE1,HLINE2)
   HLINE1 = HLINE1:" 'PL'"
   HLINE3 = SPACE(66): "FOR PERIOD " : PERIOD : " 'LL'"
*T26493 ^
   HLINE4 = "SPL CODE       SPOIL CODE DESCRIPTION           LABOR"
   HLINE4 = HLINE4 : "       MATERIAL      O/S PURCH     SHIPPING"
   HLINE4 = HLINE4 : "        MISC          TOTAL"
   HLINE4 = HLINE4 : "'L'"
   HLINE5 = STR('-',8) : SP3 : STR('-',30) : SP3 : STR('-',12) : SP2
   HLINE5 = HLINE5 : STR('-',12) : SP2 : STR('-',12) : SP2 : STR('-',12) : SP2
   HLINE5 = HLINE5 : STR('-',12) : SP2 : STR('-',12)
*---- DIMENTION VARIABLES
*
   DIM SUB(11)
   DIM DIV(11)
   DIM TOT(11)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','SPOIL.STATS' TO SPOIL.STATS ELSE ERRMSG='SPOIL.STATS FILE MISSING';GOTO 93000
   OPEN '','SPOILAGE.CODES' TO SPOILAGE.CODES ELSE ERRMSG='SPOILAGE.CODES FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
   READNEXT SSR.ID ELSE GOTO 99999
   MATREAD SSR.REC FROM SPOIL.STATS, SSR.ID ELSE GOTO 99999
   SP.CODE = FIELD(SSR.ID,"!",2)[10,8]
   SSR.DIV = FIELD(SSR.ID,"!",1)[4,2]
   MAT SUB = 0
   MAT DIV = 0
   MAT TOT = 0
   SLINE = SPACE(44) : STR('-',12) : SP2 : STR('-',12) : SP2  : STR('-',12) : SP2
   SLINE = SLINE : STR('-',12) : SP2 : STR('-',12) : SP2 : STR('-',12)
   GLINE = SPACE(44) : STR('=',12) : SP2 : STR('=',12) : SP2  : STR('=',12) : SP2
   GLINE = GLINE : STR('=',12) : SP2 : STR('=',12) : SP2 : STR('=',12)
   PRINTER ON
   HEADING HLINE1 : HLINE2 : HLINE3 : HLINE4 : HLINE5
*
*---- MAIN PROCESSING
*
   PREV.SP.CODE = SP.CODE
   PREV.DIV = SSR.DIV 
   READ SPOIL.CD.DESC FROM SPOILAGE.CODES, CONO : PREV.SP.CODE ELSE
      SPOIL.CD.DESC = "SPOILAGE CODE UNKNOWN"
   END
   DATA = 1
   LOOP
      BEGIN CASE
         CASE PREV.DIV <> SSR.DIV 
            GOSUB 1000
            GOSUB 2000
            READ SPOIL.CD.DESC FROM SPOILAGE.CODES, CONO : PREV.SP.CODE ELSE
               SPOIL.CD.DESC = "SPOILAGE CODE UNKNOWN"
            END
         CASE PREV.SP.CODE <> SP.CODE
            GOSUB 1000
            READ SPOIL.CD.DESC FROM SPOILAGE.CODES, CONO : PREV.SP.CODE ELSE
               SPOIL.CD.DESC = "SPOILAGE CODE UNKNOWN"
            END
      END CASE
      LAB = SSR.LB<1,1> + SSR.LB<1,2> + SSR.LB<1,3> + SSR.LB<1,4>
      MATL = SSR.MT<1,1> + SSR.MT<1,2> + SSR.MT<1,3>
      OSP = SSR.OS<1,1> + SSR.OS<1,2>
      SHP = SSR.SP<1,1> + SSR.SP<1,2>
      MSC = SSR.MS<1,1> + SSR.MS<1,2>
      TTL = LAB + MATL + OSP + SHP + MSC
      SUB(1) = SUB(1) + LAB
      SUB(2) = SUB(2) + MATL
      SUB(3) = SUB(3) + OSP
      SUB(4) = SUB(4) + SHP
      SUB(5) = SUB(5) + MSC
      SUB(6) = SUB(6) + TTL
      READNEXT SSR.ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD SSR.REC FROM SPOIL.STATS, SSR.ID ELSE
         MAT SSR.REC = ''
      END
      SP.CODE = FIELD(SSR.ID,"!",2)[10,8]
      SSR.DIV = FIELD(SSR.ID,"!",1)[4,2]
   REPEAT
   GOSUB 1000
   GOSUB 2000
   PRINT GLINE
   PNAME = "** COMPANY " : CONO : " TOTALS   "
   PLINE = PNAME "R#44": OCONV(TOT(1),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(TOT(2),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(TOT(3),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(TOT(4),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(TOT(5),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(TOT(6),"MD2,") "R#12"
   PRINT PLINE
   GOTO 99999
1000*
   PLINE = PREV.SP.CODE "L#8" : SP3 : SPOIL.CD.DESC "L#30"
   PLINE = PLINE : SP3 : OCONV(SUB(1),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(SUB(2),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(SUB(3),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(SUB(4),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(SUB(5),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(SUB(6),"MD2,") "R#12"
   PRINT PLINE
   FOR I = 1 TO 6
      DIV(I) = DIV(I) + SUB(I)
      SUB(I) = 0
   NEXT I
   PREV.SP.CODE = SP.CODE
   RETURN
2000 *
   PRINT SLINE
   PNAME = "* DIVISION " : PREV.DIV : " TOTALS   "
   PLINE = PNAME "R#44" : OCONV(DIV(1),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(DIV(2),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(DIV(3),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(DIV(4),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(DIV(5),"MD2,") "R#12"
   PLINE = PLINE : SP2 : OCONV(DIV(6),"MD2,") "R#12"
   PRINT PLINE
   PRINT
2100 FOR I = 1 TO 6 
      TOT(I) = TOT(I) + DIV(I)
      DIV(I) = 0
   NEXT I
   PREV.DIV = SSR.DIV 
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   PRINTER OFF
   PRINTER CLOSE
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER OFF
   PRINTER CLOSE
END
