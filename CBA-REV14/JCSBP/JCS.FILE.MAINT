*COPY>CPYLIB>COM1
*    SUBROUTINE JCS.FILE.MAINT.SUB(ACTION,MAT FILE.VARS)
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JCS.FILE.MAINT
* BY          - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 06/20/83
* DESCRIPTION -
*ENDOC
*T21407 gat 05/27/1997 * MOD TO ALERT IF OPS AND NO DEFAULT
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
   DIM FILE.RECORD(20)
   MAT FILE.RECORD = ''
   DIM CONTROLS(9,20)
   MAT CONTROLS = ''
   DIM FILE.VAR(20)
   MAT FILE.VAR = ''
   DIM DUMMY.REC(20)
   MAT DUMMY.REC = ''
*
******************
* INITIALIZATION *
******************
   MAT FILE.VARS = ''
   DASHES = STR('-',80)
   ERRMSG = ''
   OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = "COMPANY FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN '','JCS.SCREENS' TO M.SCREENS ELSE
      ERRMSG = "JCS.SCREENS FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   MAT COMP.REC = ""
   CONO = ""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
   MAT EDIT.COM = ''
   TYP = 0
   CALL EDIT.SUB
   FILL = '#'
*
   PROCREAD ACTION ELSE 
      ACTION = 100
   END
   BEGIN CASE
      CASE ACTION<1> = '1'
         FILE.NAME = "SPOILAGE.CODES"
         HEAD = "SPOILAGE CODE MAINTENANCE"
         CODE.LEN = 8
         CODE.TYP = 1
         CONTROL.CNT = 1
         CONTROLS(1,1) = 'DESCRIPTION'
         CONTROLS(2,1) = 1
         CONTROLS(3,1) = 30
         CONTROLS(4,1) = 'O'
      CASE ACTION<1> = '2'
         FILE.NAME = "JOB.STAT.CODE"
         HEAD = "JOB STATUS ACCESS CODE"
         CODE.LEN = 3
         CODE.TYP = 1
         CONTROL.CNT = 1
         CONTROLS(1,1) = 'NAME'
         CONTROLS(2,1) = 1
         CONTROLS(3,1) = 30
         CONTROLS(4,1) = 'O'
      CASE ACTION<1> = '4'
         FILE.NAME = "JOB.CATEGORY"
         HEAD = "JOB CATEGORY CODE MAINTENANCE"
         CODE.LEN = 5
         CODE.TYP = 1
         CONTROL.CNT = 1
         CONTROLS(1,1) = 'DESCRIPTION'
         CONTROLS(2,1) = 1
         CONTROLS(3,1) = 15
         CONTROLS(4,1) = 'O'
      CASE 1
         ERRMSG = 'INVALID ACTION CODE'
         GOSUB 90000
         GOTO 99999
   END CASE
   OPEN '',FILE.NAME TO FILE ELSE
      ERRMSG = FILE.NAME : ' FILE IS MISSING'
      GOSUB 90000
      GOTO 99999
   END
   FOR I = 1 TO 20
      IF CONTROLS(5,I) # '' THEN
         OPEN '', CONTROLS(5,I) TO FILE.VAR(I) ELSE
            ERRMSG = CONTROLS(5,I) : ' FILE IS MISSING'
            GOSUB 90000
            GOTO 99999
         END
      END
   NEXT I
*
*--- PRINT SCREEN TEMPLATE
10*
*      PRINT @(-1) :
   COL = 40 - INT(LEN(HEAD)/2)
   P_X  = COL ; P_Y = 1 ; P_VALUE = HEAD ; P_OPT = ""
   P_X  := AM:0 ; P_Y := AM:2 ; P_VALUE := AM:STR('-',80)
   P_X  := AM:15 ; P_Y := AM:4 ; P_VALUE := AM:'CODE:'
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   FOR I = 1 TO CONTROL.CNT
      P_X  = 15 ; P_Y = I+5 ; P_VALUE = CONTROLS(1,I):SPACE(23-LEN(CONTROLS(1,I))) ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      IF CONTROLS(2,I) # 0 THEN PRINT ':' :
   NEXT I
   P_X  = 0 ; P_Y = 20 ; P_VALUE = STR('-',80) ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
*--- GET KEY VALUE
20*
   FOR I = 1 TO CONTROL.CNT
      IF CONTROLS(2,I) # '0' THEN
         P_X  = 40 ; P_Y = I+5 ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
   NEXT I
   NEW = 0
   TYP = CODE.TYP
   X = 33
   Y = 4
   MAXL = CODE.LEN
   O.R = 'O'
   CALL EDIT.SUB
   IF VALUE = '' OR VALUE = 'END' THEN GOTO 99999
   KEY = VALUE
   MATREAD FILE.RECORD FROM FILE,CONO:KEY ELSE
      TYP = 8
      X = 0
      Y = 23
      PMSG = 'THIS CODE IS NOT ON FILE. DO YOU WANT TO SET A NEW CODE (Y/N)'
      O.R = 'R'
      CALL EDIT.SUB
      IF VALUE # 'Y' THEN
         GOTO 10
      END
      MAT FILE.RECORD = ''
      NEW = 1
   END
   IF NOT(NEW) THEN
      FOR I = 1 TO CONTROL.CNT
         IF CONTROLS(2,I) # '6' THEN
            P_X  = 40 ; P_Y = I+5 ; P_VALUE = FILE.RECORD(I) ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         END ELSE
            P_X  = 40 ; P_Y = I+5 ; P_VALUE = OCONV(FILE.RECORD(I),"D2/") "R#8" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         END
      NEXT I
      GOTO 40
   END
*
*--- GET ATTRIBUTES
30*
   FOR I = 1 TO CONTROL.CNT
      IF CONTROLS(2,I) # '0' THEN
         TYP = CONTROLS(2,I)
         MAXL = CONTROLS(3,I)
         MINV = CONTROLS(6,I)
         MAXV = CONTROLS(7,I)
         IF FILE.RECORD(I) = '' THEN
            O.R = CONTROLS(4,I)
         END ELSE
            O.R = 'O'
            BEGIN CASE
               CASE TYP = '4'
                  DEFAULT = OCONV(FILE.RECORD(I),"MD":SCALER)
               CASE TYP = '6'
                  DEFAULT = OCONV(FILE.RECORD(I),"D2/")
               CASE 1
                  DEFAULT = FILE.RECORD(I)
            END CASE
         END
         X = 40
         Y = I + 5
         CALL EDIT.SUB
         IF VALUE = 'END' THEN GOTO 40
         IF CONTROLS(5,I) # '' THEN
            MATREAD DUMMY.REC FROM FILE.VAR(I), VALUE ELSE
               ERRMSG = 'THIS RECORD DOES NOT EXIST ON FILE'
               GOSUB 90000
               GOTO 30
            END
         END
         FILE.RECORD(I) = VALUE
      END
   NEXT I
*
*--- WHAT TO DO NEXT
40*
   TYP = 1
   X = 0
   Y = 23
   MAXL = 1
   O.R = 'R'
   VALDAT = 'F' :VM: 'C' :VM: 'E' :VM: 'N' :VM: 'D'
   PMSG = CL : 'ACTION? (F)ILE, (C)HANGE, (D)ELETE, (E)XIT, (N)EXT: '
   CALL EDIT.SUB
   RESPONSE = VALUE
   BEGIN CASE
      CASE RESPONSE = 'F'
* T21407
         IF CO.OPS = "Y"  AND FILE.NAME = "JOB.CATEGORY" THEN
            MATREAD OPCO.REC FROM CONTROL, CONO:"OPS" ELSE
               MAT OPCO.REC = ""
            END
            IF OPCO.JOB.CAT = "" THEN
               ERRMSG = " NO DEFAULT SETUP FOR OPS JOB CATEGORY "
               GOSUB 90000
            END
         END
* T21407
         MATWRITE FILE.RECORD ON FILE,CONO:KEY
         GOTO 99999
      CASE RESPONSE = 'N'
         MATWRITE FILE.RECORD ON FILE,CONO:KEY
      CASE RESPONSE = 'D'
         DELETE FILE,CONO:KEY
      CASE RESPONSE = 'C'
         GOTO 30
      CASE RESPONSE = 'E' OR RESPONSE = 'END'
         GOTO 99999
   END CASE
   GOTO 20
*
*--- ERRMSG ROUTINE
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*      PRINT @(0,23) : CL : ERRMSG :
*      INPUT X :
*      PRINT @(0,23) : CL :
*      RETURN
99999*
*      PRINT @(-1)
*    RETURN
END
