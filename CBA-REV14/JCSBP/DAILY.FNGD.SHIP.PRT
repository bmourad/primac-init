*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - DAILY.FNGD.SHIP.PRT
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 04/09/86
* DESCRIPTION - This report transaction information for a
*                 finish goods shipping.
*ENDDOC
********************************************************************
*
*-- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INVENTORY
*COPY>JCS.CPYLIB>DAILY.FNGD.SHIP
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>TAX
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*
*-- SETUP FOR SYSTEM ERRMSGS
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*-- GET PROC VALUES
      PROCREAD ID ELSE
         ERRMSG = ' CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      CONO = ID<1>
      CONO.NAME = ID<2>
      LIST.TYPE = ID<5>
      ST.DATE = ID<3>
      ED.DATE = ID<4>
      BEGIN CASE
      CASE LIST.TYPE = "N"
         LIST.TYPE = "(NEW)"
      CASE LIST.TYPE = "R"
         LIST.TYPE = "(REJECT)"
      CASE 1
         LIST.TYPE = "(ALL)"
      END CASE
*
*-- OPEN FILES
*
      OPEN '','INVENTORY' TO INVENTORY ELSE
         ERRMSG = 'INVENTORY FILE IS MISSING'; GOTO 93000
      END
      OPEN '','CUSTOMER' TO CUSTOMER ELSE
         ERRMSG = 'CUSTOMER FILE IS MISSING'; GOTO 93000
      END
      OPEN '','CATEGORY' TO CATEGORY ELSE
         ERRMSG = 'CATEGORY FILE IS MISSING'; GOTO 93000
      END
      OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
         ERRMSG = 'DEPARTMENT FILE IS MISSING'; GOTO 93000
      END
      OPEN '','DIVISION' TO DIVISION ELSE
         ERRMSG = 'DIVISION FILE IS MISSING'; GOTO 93000
      END
      OPEN '','OPERATION' TO OPERATION ELSE
         ERRMSG = 'OPERATION FILE IS MISSING'; GOTO 93000
      END
      OPEN '','COST.CNTR' TO COST.CNTR ELSE
         ERRMSG = 'COST.CNTR FILE IS MISSING'; GOTO 93000
      END
      OPEN '','DAILY.FNGD.SHIP' TO DAILY.FNGD.SHIP ELSE
         ERRMSG = 'DAILY.FNGD.SHIP FILE IS MISSING' ; GOTO 93000
      END
      OPEN '','TAX' TO TAX ELSE
         ERRMSG = 'TAX IS MISSING'; GOTO 93000
      END
      OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
         ERRMSG = 'SHIP.VIA IS MISSING'; GOTO 93000
      END
*
*-- INT
*
      LINE.COUNT = 99
      NO.DATA = 1
      SP1 = " " ; PAGE.NO = 0
      UNKNOWN = "????????????????????"
      LINES = 0 ; TOTAL.LINES = 0
      SHD1 = "<------ SOLD - TO INFORMATION ------> <------ SHIP - TO INFORMATION ------> <-------------- OTHER INFORMATION -------------->"
      SHD2 = "<------------------------------------------------ PRODUCT / WAREHOUSE INFORMATION ------------------------------------------------->"
      SHD3 = " PRODUCT NUMBER                  DESCRIPTION                   WHSE U/M LOC# ROLL / SKID#   QUANTITY    DIAMETER"
      SHD4 = " --------------- --------------------------------------------- ---- --- ---- ------------ ------------- --------"
      PRINTER ON
      PRINT CHAR(12)
*
*-- MAIN PROCESS
*
      DATA = 1
      LOOP
         READNEXT KEY ELSE
            DATA = 0
            IF NO.DATA = 1 THEN GOSUB 1100
         END
      WHILE DATA = 1 DO
         NO.DATA = 0
         MATREAD DFGS.REC FROM DAILY.FNGD.SHIP , KEY ELSE GOTO 111
         CONO = KEY[1,3]
         MATREAD CUST.REC FROM CUSTOMER , CONO:DFGS.SOLD.TO ELSE
            MAT CUST.REC = UNKNOWN
         END
         MATREAD SHIP.VIA.REC FROM SHIP.VIA , CONO:DFGS.SHIP.VIA ELSE
            SHPV.DESC = UNKNOWN
         END
         MATREAD TAX.REC FROM TAX , CONO:DFGS.TAX.JUR ELSE
            TAX.JUR = UNKNOWN
         END
         MATREAD DEPT.REC FROM DEPARTMENT, CONO:DFGS.DEPT ELSE DEPT.DESC = UNKNOWN
         MATREAD DIV.REC FROM DIVISION, CONO:DFGS.DIV ELSE DIV.DESC = UNKNOWN
         MATREAD CCTR.REC FROM COST.CNTR, CONO:DFGS.CCTR ELSE CCTR.DESC=UNKNOWN
         MATREAD OPER.REC FROM OPERATION, CONO:DFGS.OPER ELSE OPER.DESC=UNKNOWN
         BEGIN CASE
         CASE DFGS.SHIP.TYPE = 'N'
            SHP.TYP.DESC = "NON CHARGEABLE"
         CASE DFGS.SHIP.TYPE = 'S'
            SHP.TYP.DESC = "SPOILAGE"
         CASE DFGS.SHIP.TYPE = 'C'
            SHP.TYP.DESC = "CHANGE ORDER"
         CASE 1
            SHP.TYP.DESC = "REGULAR"
         END CASE
         ST.CNT = COUNT(DFGS.STATUS, VM) + (DFGS.STATUS # "")
         FND = 0; STATUS = ""
         FOR I = 1 TO ST.CNT UNTIL FND
            IF DFGS.STATUS<1,I> # "" THEN
               STATUS = DFGS.STATUS<1,I>
               FND = 1
            END
         NEXT I
         LOC.CNT = COUNT(DFGS.LOC,VM) + (DFGS.LOC # "")
*   FOR PL = 1 TO PROD.CNT
*      LINES = LINES + COUNT(DFGS.LOC<1,PL>, SVM) + (DFGS.LOC<1,PL> # "")
*   NEXT PL
         LINES = LOC.CNT
         TOTAL.LINES = LINES
         NO.PAGES = INT(TOTAL.LINES / 40 + 1)
         GOSUB 1000
         GOSUB 2000
111   REPEAT
      GOTO 99999
*
*-- PRINT TOP PART OF REPORT
*
1000 *
      IF LINE.COUNT + 15 > 55 THEN
         PRINT CHAR(12)
         PAGE.NO = PAGE.NO + 1
         LINE.COUNT = 0
      END
      H.LINE = ""
      H.LINE = "TRAN - DATE   : ":OCONV(DFGS.SHIP.DATE, "D2/") "L#8"
      H.LINE = H.LINE : SPACE(8)
      H.LINE = H.LINE : "F I N I S H E D   G O O D S   S H I P P I N G   E D I T   L I S T I N G  ":LIST.TYPE
      PRINT H.LINE
      H.LINE = ""
      H.LINE = "TRANSACTION # : ":KEY[4,99] "L#8":SPACE(14)
*     H.LINE = H.LINE : SPACE(65):PAGE.NO "R#3":" OF ":NO.PAGES "L#3"
      PRINT H.LINE
      H.LINE = "JOB NUMBER    : ":DFGS.JOB
      PRINT H.LINE
      PRINT
      PRINT SHD1
      H.LINE = " ":DFGS.SOLD.TO "L#6":SPACE(32):DFGS.SHIP.TO "L#6":SPACE(32):"SHIP-VIA : ":DFGS.SHIP.VIA "L#2":"    ":SHPV.DESC
      PRINT H.LINE
      H.LINE = " ":CUST.NAME "L#35":"   ":DFGS.SHIP.NAME "L#35":"   TAX JURS : ":DFGS.TAX.JUR "L#5":"  ":TAX.JUR
      PRINT H.LINE
      H.LINE = " ":CUST.ADDR1 "L#35":"   ":DFGS.SHIP.ADDR1 "L#35":"   FREIGHT #: ":DFGS.FREIGHT.NO
      PRINT H.LINE
      H.LINE = " ":CUST.ADDR2 "L#35":"   ":DFGS.SHIP.ADDR2 "L#35":"   INVOICE #: ":DFGS.INV.NO
      PRINT H.LINE
      H.LINE = " ":CUST.ADDR3 "L#35":"   ":DFGS.SHIP.ADDR3 "L#35":"   INV DATE : ":OCONV(DFGS.INV.DATE,"D2/") "L#8"
      PRINT H.LINE
      H.LINE = " ":CUST.ADDR4 "L#35":"   ":DFGS.SHIP.CI.ST "L#35":"   INV AMT  : ":OCONV(DFGS.INV.AMT,"MD2") "R#9"
      PRINT H.LINE
      H.LINE = " ":CUST.ZIP "L#35":"   ":DFGS.SHIP.ZIP "L#35":"   DIV      : ":DFGS.DIV "L#2":"    ":DIV.DESC "L#30"
      PRINT H.LINE
      H.LINE = SPACE(77):"DEPT     : ":DFGS.DEPT "L#4":"  ":DEPT.DESC "L#30"
      PRINT H.LINE
      H.LINE = SPACE(77):"COST CTR : ":DFGS.CCTR "L#3":"   ":CCTR.DESC "L#30"
      PRINT H.LINE
      H.LINE = SPACE(77):"OPER     : ":DFGS.OPER "L#4":"  ":OPER.DESC "L#30"
      PRINT H.LINE
      H.LINE = SPACE(77):"SHP TYPE : ":DFGS.SHIP.TYPE:SPACE(5):SHP.TYP.DESC "L#20"
      PRINT H.LINE
      H.LINE = 'PRINT DATE :':OCONV(DFGS.PRT.DATE,"D2/") "R#8":SP1
      H.LINE = H.LINE:'  STATUS : ':STATUS "L#50"
      PRINT H.LINE
      PRINT
      PRINT SHD2
      PRINT SHD3
      PRINT SHD4
      PRT.FLG = 1
      LINE.COUNT = LINE.COUNT + 21
      RETURN
1100 *
      H.LINE = SPACE(20):"F I N I S H E D   G O O D S   S H I P P I N G   E D I T   L I S T I N G  ":LIST.TYPE
      PRINT H.LINE
      PRINT;PRINT
      PRINT "NO ITEMS PRESENT"
      RETURN
*
*-- PRINT PRODUCT WAREHOUSE INFORMATION
*
2000 *
*  PROD.CNT = COUNT(DFGS.PROD,VM) + (DFGS.PROD # "")
*  FOR PR = 1 TO PROD.CNT
      MATREAD INV.REC FROM INVENTORY, CONO:DFGS.PROD ELSE MAT INV.REC = ""
      GOSUB 6000
      LOC.CNT = COUNT(DFGS.LOC,VM) + (DFGS.LOC # "")
      FOR PR1 = 1 TO LOC.CNT
         IF LINE.COUNT > 55 THEN
            PRT.FLG = 1 ; LINE.COUNT = 99 ; GOSUB 1000
         END
         IF PRT.FLG = 1 OR PR1 = 1 THEN
            H.LINE =" ":DFGS.PROD "L#15":SP1
            H.LINE = H.LINE :INV.FULL.DESC "L#45":SP1
            H.LINE = H.LINE :DFGS.WHSE "L#4":SP1
            H.LINE = H.LINE :INV.UNIT<1,2> "L#3":SP1
            H.LINE = H.LINE :DFGS.LOC<1,PR1> "L#4":SP1
            PRT.FLG = 0
         END ELSE
            H.LINE = SPACE(72)
            IF DFGS.LOC<1,PR1> # DFGS.LOC<1,PR1-1> THEN
               H.LINE = H.LINE :DFGS.LOC<1,PR1> "L#4":SP1
            END ELSE
               H.LINE = H.LINE :SPACE(5)
            END
         END
         H.LINE = H.LINE :DFGS.R.S.ID<1,PR1> "L#12":SP1
         IF DFGS.QTY<1,PR1> < 0 THEN
            ADDQ = "-.5"
         END ELSE
            ADDQ = .5
         END
         PRT.QTY = INT(((DFGS.QTY<1,PR1> / ICR.DV1) * ICR.MT1) / ICR.DV2 + ADDQ)
         H.LINE = H.LINE :OCONV(PRT.QTY, ICR.CNV) "R#13":SP1
         H.LINE = H.LINE:OCONV(DFGS.DIAM<1,PR1>,"MD2") "R#8"
         PRINT H.LINE
         LINE.COUNT = LINE.COUNT + 1
      NEXT PR1
*  NEXT PR
      IF DFGS.PRT.DATE = "" THEN
         DFGS.PRT.DATE = DATE()
         MATWRITE DFGS.REC ON DAILY.FNGD.SHIP , KEY
      END ELSE
         RELEASE DAILY.FNGD.SHIP, KEY
      END
      IF LINE.COUNT + 1 < 53 THEN
         PRINT STR("*",132)
         PRINT
         LINE.COUNT = LINE.COUNT + 2
      END
      RETURN
*-- CONVERT SHEET/WGHT/QTY
6000 *
      MAT INV.CNV.REC = ''
      BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2,"
         ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
      END CASE
      RETURN
*-- ERROR ROUTINE
91000 *
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 *
      ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999 *
      PRINTER OFF
      END
