*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - CATEGORY.OSP.MAINT
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 02/12/85
* DESCRIPTION -
*This program creates and updates CATEGORY.OSP file.
*
* Mod CLW 09/24/94 Task 17919 Add accrual accounts
*
*T26126 adelgado 03/01/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*********************************************************************
*
***** FILE EQUATES
*
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>JCS.CPYLIB>FOH.TABLE
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SETUP SYSTEM ERRMSGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
  OPEN '','CATEGORY.OSP' TO CATEGORY.OSP ELSE
    ERRMSG = 'CATEGORY.OSP FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','JCS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'JCS.SCREENS FILES IS MISSING'
    GOSUB 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','COA' TO COA ELSE
    ERRMSG = 'COA FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','FOH.TABLE' TO FOH.TABLE ELSE
    ERRMSG = 'FOH.TABLE FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOSUB 93000
  END
*
***** GET COMPANY NAME
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
  IN.ACCT.LEN = LEN(CO.ACCT.PIC)
  UNKNOWN = STR('?',30)
*
***** MAIN PROCESSING
*
  MAT EDIT.COM.DRIVER = ''
  MAT CAOS.REC = ''
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME = 'CATEGORY.OSP.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
*
**** PRINT SCREEN
*
100*
  RELEASE              ;* T26126
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
*
**** ENTER PROD.LINE
*
200*
  ECD.NUM = 1
  ECD.VALDAT.CODE = 4
  ECD.VALDAT.FILE = CATEGORY.OSP
  ECD.PREFIX.ID = CONO
  ECD.ACTION=4;CALL SCRN.EDIT
  PROD.LINE = ECD.RET.VALUE
  MATREAD GLTABLE.REC FROM CONTROL,CONO:"GLTABLE" ELSE MAT GLTABLE.REC=""
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
      * T26126 v
      READU DUMMY FROM CATEGORY.OSP, CONO:ECD.RET.VALUE LOCKED
        ERRMSG = 'CATEGORY record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 200 
      END ELSE NULL
      * T26126 ^
      FOR SAVE = 1 TO CAOS.REC.SIZE
        CAOS.REC(SAVE) = ECD.VALDAT.ITEM<SAVE>
      NEXT SAVE
      SCV.REC(2)<ECD.SCRN.NO> = CAOS.DESC
      SCV.REC(3)<ECD.SCRN.NO> = CAOS.MAJ.LINE
      SCV.REC(4)<ECD.SCRN.NO> = CAOS.LEAD.TIME
      SCV.REC(5)<ECD.SCRN.NO>= OCONV(CAOS.FOH.PCT,"MD2")
      SCV.REC(6)<ECD.SCRN.NO>= CAOS.MARKUP
      MATREAD COA.REC FROM COA, CONO:CAOS.WIP ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(7)<ECD.SCRN.NO> = CAOS.WIP CO.ACCT.MASK
      SCV.REC(8)<ECD.SCRN.NO> = COA.DESC
      MATREAD COA.REC FROM COA, CONO:CAOS.COG ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(9)<ECD.SCRN.NO> = CAOS.COG CO.ACCT.MASK
      SCV.REC(10)<ECD.SCRN.NO> = COA.DESC
      MATREAD COA.REC FROM COA, CONO:CAOS.APL ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(11)<ECD.SCRN.NO> = CAOS.APL CO.ACCT.MASK
      SCV.REC(12)<ECD.SCRN.NO> = COA.DESC
      MATREAD COA.REC FROM COA, CONO:CAOS.FOH.WIP ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(13)<ECD.SCRN.NO> = CAOS.FOH.WIP CO.ACCT.MASK
      SCV.REC(14)<ECD.SCRN.NO> = COA.DESC
      MATREAD COA.REC FROM COA, CONO:CAOS.FOH ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(15)<ECD.SCRN.NO> = CAOS.FOH CO.ACCT.MASK
      SCV.REC(16)<ECD.SCRN.NO> = COA.DESC
      MATREAD COA.REC FROM COA, CONO:CAOS.FOH.APL ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(17)<ECD.SCRN.NO> = CAOS.FOH.APL CO.ACCT.MASK
      SCV.REC(18)<ECD.SCRN.NO> = COA.DESC
*
*----- ACCRUED LIABILIY and ACCOUNTS PAYABLE ACCOUNTS
* Task 17919
      MATREAD COA.REC FROM COA, CONO:CAOS.ACCRU.LIAB ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(20)<ECD.SCRN.NO> = CAOS.ACCRU.LIAB CO.ACCT.MASK
      SCV.REC(21)<ECD.SCRN.NO> = COA.DESC
      MATREAD COA.REC FROM COA, CONO:CAOS.AP.ACCT ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(22)<ECD.SCRN.NO> = CAOS.AP.ACCT CO.ACCT.MASK
      SCV.REC(23)<ECD.SCRN.NO> = COA.DESC
*
      ECD.ACTION=3;CALL SCRN.EDIT
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
      FOR II = 1 TO 13
        ON II GOSUB 1100,1200,1300,1400,1500,1550,1560,1600,1700,1800,1900,2000,2100
        IF ECD.RET.VALUE = 'END' THEN GOTO 100
      NEXT II
    CASE 1
      GOTO 100
  END CASE
  LOOP
    MORE = 0
    ECD.NUM = 19; SCV.REC(ECD.NUM)<1> = ''
    ECD.ACTION=4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'E' OR OPTION = 'END'
      CASE NUM(OPTION)
        ON OPTION GOSUB 1100,1200,1300,1400,1500,1550,1560,1600,1700,1800,1900,2000,2100
        MORE = 1
      CASE OPTION = 'F'
        MATWRITE CAOS.REC ON CATEGORY.OSP , CONO:PROD.LINE
    END CASE
  WHILE MORE DO REPEAT
  GOTO 100
*
***** ENTER CATG DESC
*
1100*
  ECD.NUM = 2
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    CAOS.DESC = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER CAOS.MAJOR.LINE
*
1200*
  ECD.NUM = 3
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    CAOS.MAJ.LINE = ECD.RET.VALUE
  END
  RETURN
*
***** ENTER ORDER.CYCLE.DAYS
*
1300*
  ECD.NUM = 4
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    CAOS.LEAD.TIME = ECD.RET.VALUE
  END
  RETURN
*
***** ENTER INDIRECT PERCENT
*
1400*
  ECD.NUM = 5
  ECD.ACTION=4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
    CASE NUM(ECD.RET.VALUE)
      ECD.RET.VALUE = ICONV(ECD.RET.VALUE,"MD2")
      IF ECD.RET.VALUE > "10000" THEN
        ERRMSG = "CANNOT EXCEED 100 %";GOSUB 91000;GOTO 1400
      END
      CAOS.FOH.PCT = ECD.RET.VALUE
      SCV.REC(ECD.NUM)<1> = OCONV(CAOS.FOH.PCT,"MD2")
      ECD.ACTION = 5; CALL SCRN.EDIT
    CASE 1
      MATREAD FTR.REC FROM FOH.TABLE, CONO : ECD.RET.VALUE ELSE
        ERRMSG = 'CANNOT LOCATE INDIRECT CODE - ' : ECD.RET.VALUE
        GOSUB 91000; GOTO 1400
      END
      CAOS.FOH.PCT = ECD.RET.VALUE
  END CASE
  RETURN
*
***** ENTER MARKUP PERCENT
*
1500*
  ECD.NUM = 6
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    CAOS.MARKUP = ECD.RET.VALUE
  END
  RETURN
*
*---- ENTER ACCRUED LIABILITY ACCOUNT     Task - 17919
*
1550*
  ECD.NUM = 20
  GL.DEFAULT= GLTB.OS.ACCRU.LIAB
  ACCT.NO = CAOS.ACCRU.LIAB; GOSUB 5000
  CAOS.ACCRU.LIAB = ACCT.NO
  RETURN
*
*---- ENTER ACCOUNTS PAYABLE ACCOUNT     Task - 17919
*
1560*
  ECD.NUM = 22
  GL.DEFAULT= GLTB.OS.AP.ACCT
  ACCT.NO = CAOS.AP.ACCT; GOSUB 5000
  CAOS.AP.ACCT = ACCT.NO
  RETURN
1600 ECD.NUM = 7
  GL.DEFAULT = GLTB.OS.DIR.WIP
  ACCT.NO = CAOS.WIP; GOSUB 5000
  CAOS.WIP = ACCT.NO
  RETURN
1700 ECD.NUM = 9
  GL.DEFAULT = GLTB.OS.DIR.COG
  ACCT.NO = CAOS.COG; GOSUB 5000
  CAOS.COG = ACCT.NO
  RETURN
1800 ECD.NUM = 11
  GL.DEFAULT = GLTB.OS.DIR.APL
  ACCT.NO = CAOS.APL; GOSUB 5000
  CAOS.APL = ACCT.NO
  RETURN
1900 ECD.NUM = 13
  GL.DEFAULT = GLTB.OS.FOH.WIP
  ACCT.NO = CAOS.FOH.WIP; GOSUB 5000
  CAOS.FOH.WIP = ACCT.NO
  RETURN
2000 ECD.NUM = 15
  GL.DEFAULT = GLTB.OS.FOH.COG
  ACCT.NO = CAOS.FOH; GOSUB 5000
  CAOS.FOH = ACCT.NO
  RETURN
2100 ECD.NUM = 17
  GL.DEFAULT = GLTB.OS.FOH.APL
  ACCT.NO = CAOS.FOH.APL; GOSUB 5000
  CAOS.FOH.APL = ACCT.NO
  RETURN
5000 SCV.REC(ECD.NUM)<1> = ''; ECD.ACTION=5;CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN
  ECD.DEFAULT = GL.DEFAULT
  ECD.VALDAT.CODE = '2'; ECD.VALDAT.FILE = COA; ECD.PREFIX.ID = CONO
  SCV.REC(ECD.NUM)<1> = ACCT.NO
  ECD.PATRN = CO.ACCT.MATCH
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    ACCT.NO = ECD.RET.VALUE
    FOR L = 1 TO COA.REC.SIZE; COA.REC(L) = ECD.VALDAT.ITEM<L>; NEXT L
    SCV.REC(ECD.NUM)<1> = ACCT.NO CO.ACCT.MASK; ECD.ACTION=5;CALL SCRN.EDIT
    ECD.NUM = ECD.NUM + 1; SCV.REC(ECD.NUM)<1> = COA.DESC; ECD.ACTION=5;CALL SCRN.EDIT
  END
  RETURN
*
***** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
