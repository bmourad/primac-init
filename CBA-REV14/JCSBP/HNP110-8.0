      SUBROUTINE (FILE.NO, FILE.KEY, PTR, MSG, STATUS)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - HNP110
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/27/87
*
* DESCRIPTION
*
* This subroutine will read the specified file for specified key,
* format and transmit the data to the Primac Network Processor.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JCS.CPYLIB>COM.PNP
*COPY>JCS.CPYLIB>PNP.LOAD
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- DEFINE CONSTANTS
*
      SOH$ = CHAR(1)
      STX$ = CHAR(2)
      ETX$ = CHAR(3)
      EOT$ = CHAR(4)
      ENQ$ = CHAR(5)
      ACK$ = CHAR(6)
      NAK$ = CHAR(21)
*
*---- INITIALIZATION
*
      STATUS = ""
*
*---- MAIN PROCESSING
*
100*
      BEGIN CASE
      CASE FILE.NO = "1"
         CMP.KEY = FILE.KEY
         MATREAD COMP.REC FROM COMPANY, CMP.KEY ELSE GOTO 90000
         REC1.ID = CMP.KEY"L#3"
         REC1.DESC = CO.NAME"L#30"
         TRN = "01"
         CNT = 2
      CASE FILE.NO = "2"
         DIV.KEY = FILE.KEY
         MATREAD DIV.REC FROM DIVISION, DIV.KEY ELSE GOTO 90000
         CONO = DIV.KEY[1,3]
         DIV = DIV.KEY[4,99]
         REC2.ID = (CONO:DIV)"L#5"
         REC2.DESC = DIV.DESC"L#30"
         TRN = "02"
         CNT = 2
      CASE FILE.NO = "3"
         DEPT.KEY = FILE.KEY
         MATREAD DEPT.REC FROM DEPARTMENT, DEPT.KEY ELSE GOTO 90000
         CONO = DEPT.KEY[1,3]
         DIV = DEPT.DIV<1,PTR>
         IF DIV = "" THEN GOTO 90000
         DEPT = DEPT.KEY[4,99]
         IF DEPT = "" THEN GOTO 90000
         REC3.ID = (CONO:DIV:DEPT)"L#10"
         REC3.DESC = DEPT.DESC"L#20"
         TRN = "03"
         CNT = 2
      CASE FILE.NO = "4"
         CCTR.KEY = FILE.KEY
         MATREAD CCTR.REC FROM COST.CNTR, CCTR.KEY ELSE GOTO 90000
         CONO = CCTR.KEY[1,3]
         CCTR = CCTR.KEY[4,99]
         IF CCTR = "" THEN GOTO 90000
         IF CCTR.TYPE = "F" THEN GOTO 90000
         REC4.ID = (CONO:CCTR)"L#6"
         REC4.DESC = CCTR.DESC"L#30"
         REC4.DEPT = CCTR.DEPT"L#5"
         TRN = "04"
         CNT = 3
      CASE FILE.NO = "5"
         OPER.KEY = FILE.KEY
         MATREAD OPER.REC FROM OPERATION, OPER.KEY ELSE GOTO 90000
         CONO = OPER.KEY[1,3]
         OPER = OPER.KEY[4,99]
         IF OPER = "" THEN GOTO 90000
         REC5.ID = (CONO:OPER)"L#7"
         REC5.DESC = OPER.DESC"L#30"
         REC5.MR = OPER.MATL.REQ"L#1"
         REC5.CTYPE = OPER.CTYPE"L#1"
         REC5.PROMPT = OPER.PROMPT"L#15"
         TRN = "05"
         CNT = 5
      CASE FILE.NO = "6"
         CCOP.KEY = FILE.KEY
         MATREAD CCTR.REC FROM COST.CNTR, CCOP.KEY ELSE GOTO 90000
         CONO = CCOP.KEY[1,3]
         CCTR = CCOP.KEY[4,99]
         IF CCTR = "" THEN GOTO 90000
         OPER = CCTR.OPER<1,PTR>
         IF OPER = "" THEN GOTO 90000
         TYPE = CCTR.OPER.TYPE<1,PTR>
         REC6.ID = (CONO:CCTR:OPER)"L#10"
         REC6.TYPE = TYPE"L#1"
         REC6.PROMPT = """L#15"
         TRN = "06"
         CNT = 3
      CASE FILE.NO = "7"
         EMP.KEY = FILE.KEY
         MATREAD EMP.REC FROM EMPLOYEE, EMP.KEY ELSE GOTO 90000
         IF EMP.TERM.DATE # "" THEN GOTO 90000
         CONO = EMP.KEY[1,3]
         EMP = EMP.KEY[4,99]
         IF EMP = "" THEN GOTO 90000
         REC7.ID = (CONO:EMP)"L#9"
         REC7.DESC = (TRIM(EMP.FRST.NAME:" ":EMP.LAST.NAME))"L#30"
         REC7.SFT = EMP.SHIFT"L#1"
         REC7.DEPT = EMP.DEPT"L#5"
         BDAY = OCONV(EMP.BIRTHDAY,"D2-")
         BDAY = BDAY[7,2]:BDAY[1,2]:BDAY[4,2]
         REC7.BDAY = BDAY"L#6"
         REC7.PWD = """L#8"
         REC7.AUTH = "111000"
         TRN = "07"
         CNT = 7
      CASE FILE.NO = "8"
         SHPV.KEY = FILE.KEY
         MATREAD SHIP.VIA.REC FROM SHIP.VIA, SHPV.KEY ELSE GOTO 90000
         CONO = SHPV.KEY[1,3]
         SHPV = SHPV.KEY[4,99]
         IF SHPV = "" THEN GOTO 90000
         REC8.ID = (CONO:SHPV)"L#9"
         REC8.DESC = SHPV.DESC"L#30"
         TRN = "08"
         CNT = 2
      CASE FILE.NO = "9"
         JOB.KEY = FILE.KEY
         MATREAD JOB.REC FROM JOB, JOB.KEY ELSE GOTO 90000
         IF JOB.STATUS<1,1> > 4 THEN GOTO 90000
         MATREAD COMP.REC FROM COMPANY, JOB.KEY[1,3] ELSE
            MAT COMP.REC = ""
         END
         IF CO.JOB.CUST.FLG = "C" THEN
            MATREAD CUST.REC FROM CUSTOMER, JOB.KEY[1,3]:JOB.CUST ELSE GOTO 90000
            JDESC = CUST.NAME
         END ELSE
            JDESC = JOB.DESC<1,1>
         END
         CONO = JOB.KEY[1,3]
         JOB.NO = JOB.KEY[4,99]
         IF JOB.NO = "" THEN GOTO 90000
         REC9.ID = (CONO:JOB.NO)"L#11"
         REC9.DESC = JDESC"L#40"
         REC9.DIV = JOB.DIV"L#2"
         REC9.TYPE = JOB.TYPE"L#1"
         TRN = "09"
         CNT = 4
      CASE FILE.NO = "10"
         INV.KEY = FILE.KEY
         MATREAD INV.REC FROM INVENTORY, INV.KEY ELSE GOTO 90000
         CONO = INV.KEY[1,3]
         PROD = INV.KEY[4,99]
         REC10.ID = (CONO:PROD)"L#18"
         REC10.DESC = INV.DESC"L#30"
         REC10.TYPE = INV.PAP.TYPE"L#8"
         TRN = "10"
         CNT = 3
      CASE FILE.NO = "11"
         SPL.KEY = FILE.KEY
         READ SPL.DESC FROM SPOILAGE.CODES, SPL.KEY ELSE GOTO 90000
         CONO = SPL.KEY[1,3]
         SPCODE = SPL.KEY[4,99]
         REC11.ID = (CONO:SPCODE)"L#11"
         REC11.DESC = SPL.DESC"L#30"
         TRN = "11"
         CNT = 2
      END CASE
*
*---- FORMAT TRANSMISSION DATA
*
20000*
      MSG = STX$:TRN
      FOR N = 1 TO CNT
         MSG = MSG:REC(N)
      NEXT N
      MSG = MSG:ETX$
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000*
      STATUS = "E"
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
