*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - DRSBP
* PROGRAM     - SHIPTO.MASTER.LIST
* AUTHOR      - L. ROSS, C.B.A.
* DATE        - 02/11/92
* DESCRIPTION
* PRINT A LISTING OF ALL SHIPTO MASTER RECORDS ON FILE
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*
***** INSERT FILES EQUATES
*
*COPY>JCS.CPYLIB>SHIPTO.MASTER
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
   OPEN '','SHIPTO.MASTER' TO SHIPTO.MASTER ELSE
      ERRMSG = 'SHIPTO.MASTER FILE IS MISSING'
      GOTO 93000
   END
*
**** READ PROC VALUES
*
   PROCREAD ID ELSE
      ERRMSG = 'CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
*T26493 v
*  CONO.NAME = ID<2>
*  REPORT.NAME = 'SHIPTO MASTER LIST'
*  REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** INITIALIZATION
*
   LINE.COUNT = 99
   PAGE.NO = 0
   HH.LINE1 = 'SHIPTO#  STORE  SHIPTO NAME':SPACE(20):'DESTINATION NAME'
   HH.LINE2 = '-------  -----  ':STR('-',30):' ':STR('-',30)
*
***** READ AND PROCESS FIRST RECORD
*
100 *
   READNEXT KEY ELSE GOTO 99999
   MATREAD SHM.REC FROM SHIPTO.MASTER, KEY ELSE MAT SHM.REC = "???"
   PRINTER ON
   GOSUB 9000
   GOSUB 1000
*
***** READ AND PROCESS ALL RECORDS
*
   DATA = 1
   LOOP
      READNEXT KEY ELSE DATA = 0
   WHILE DATA = 1 DO
      MATREAD SHM.REC FROM SHIPTO.MASTER, KEY THEN GOSUB 1000
   REPEAT
   GOTO 99999
*
**** DETAIL ROUTINE
*
1000 *
*
   PLINE = KEY[4,7] 'R#7':'  ':SHM.SP.STORE 'R#5':'  ':SHM.SP.NAME 'L#30':' ':SHM.DT.NAME 'L#30'
   IF LINE.COUNT > 52 THEN GOSUB 9000
   PRINT PLINE
   LINE.COUNT = LINE.COUNT + 1
   PLINE = ''
   RETURN
*
***** PRINT HEADINGS
*
9000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT HH.LINE1
   PRINT HH.LINE2
   PRINT
   LINE.COUNT = 0
   RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
   PRINTER OFF
END
