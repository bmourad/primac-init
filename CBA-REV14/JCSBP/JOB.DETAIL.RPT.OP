*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.DETAIL.RPT.OP
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 07/25/84
* DESCRIPTION -
* This program produces a Job Detail Report by Outside 
* Purchases.
**************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>VEND
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM A MENU";GOSUB 91000;GOTO 99999
     CS.FLG = ID<5>
*
*---- DIMENSION RECORDS
*
     DIM SUB.TOTAL.REC(5)
     DIM DEPT.TOTAL.REC(5)
     DIM GRAND.TOTAL.REC(5)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','VEND' TO VEND ELSE ERRMSG='VEND FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     UNKNOWN=STR("?",30)
     MAT SUB.TOTAL.REC=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     PREVIOUS.DEPT.NO=''
     PREVIOUS.DEPT=''
     PREVIOUS.CCTR=''
     PREVIOUS.OPER=''
     FIRST.FLAG=1
     NODATA=1
     MAT JCR.REC=''
     MAT JOB.REC=''
     VEND.NAME=''
     DATE.LAST.WORKED=''
     DATE.ITEMS.THROUGH=''
     RPT.NO="XXXX"
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     TYPE=FIELD(JCR.KEY,"*",4)
     IF TYPE="EST" THEN GOTO 10
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'OSP' THEN GOTO 10
     IF JCR.LINE.TYPE="P" OR JCR.LINE.TYPE="M" THEN GOTO 10
     NODATA=0
     DEPT.NO=JCR.DEPT
     JOB.NO=JCR.JOB
     IF FIRST.FLAG=1 THEN GOSUB 15
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     I=0
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        OPER.NO=JCR.OPER<1,I>
        VEND.NUM=JCR.VEND<1,I>
        DESC=JCR.DESC<1,I>
        TRANS.DATE=JCR.DATE<1,I>
        TRANS.TYPE=JCR.TYPE<1,I>
        IF TRANS.TYPE="R" THEN
           TRANS.TYPE='  '
        END ELSE
           TRANS.TYPE='CO'
        END
        IF CS.FLG = "S" THEN
           REG.COST=JCR.SALE<1,I,1>
           OT.COST=JCR.SALE<1,I,2>
           DT.COST=JCR.SALE<1,I,3>
        END ELSE
           REG.COST=JCR.COST<1,I,1>
           OT.COST=JCR.COST<1,I,2>
           DT.COST=JCR.COST<1,I,3>
        END
        TOTAL=REG.COST + OT.COST + DT.COST
        IF TOTAL = 0 THEN TOTAL=''
        QTY=JCR.QTY<1,I>
        S.QTY=JCR.SPOIL.QTY<1,I>
        S.COST=JCR.SPOIL.COST<1,I>
        S.CODE=JCR.SPOIL.CODE<1,I>
        PO=JCR.PO<1,I>
        PLINE=JCR.PLINE<1,I>
        INV=JCR.INV<1,I>
        INV.DATE=JCR.INV.DATE<1,I>
        TRAN.JOB=JCR.RC.JOB<1,I>
        IF FIRST.FLAG=1 THEN GOTO 11
        IF PREVIOUS.DEPT # DEPT.NO THEN
           GOSUB 50
           GOSUB 60
           IF LINE.CNT >= 50 THEN GOSUB 20
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.CCTR # CCTR.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.OPER # OPER.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.OPER=OPER.NO
        END
11      VEND.NAME=''
        GOSUB 30
        IF FIRST.FLAG=1 THEN
           FIRST.FLAG=0
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
        END
     NEXT I
     GOTO 10
12*
     GOSUB 50
     GOSUB 60
     GOSUB 70
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFORMATION FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     D.CNT=COUNT(JOB.OS.DATE,VM) + (JOB.OS.DATE # '')
     FOR T=1 TO D.CNT
        IF JOB.OS.DATE<1,T,1> > DATE.LAST.WORKED THEN DATE.LAST.WORKED=JOB.OS.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     T=0;D.CNT=0
     D.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR T=1 TO D.CNT
        IF DATE.TBL<1,T,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,T,1>
     NEXT T
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     PG.NO=PG.NO + 1
     H.LINE='';HLINE1='';HLINE2='';HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     K=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=K:CO.NAME:K
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):'PAGE: ':PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     PRINT JOB.CUST"L#8":SPACE(35):'D E T A I L   R E P O R T (OUTSIDE PURCHASES)':SPACE(22):'#':JOB.NO "L#8":" ":CONSOL
     PRINT CUST.NAME"L#30":SPACE(72):CUST.NAME
     PRINT JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
     PRINT JOB.DESC<1,1>"L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.WORKED,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>"L#30"
     PRINT 'SR: ':SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT
     PRINT SPACE(83):'ACTUAL   ACTUAL   SPOIL    SPOIL'
     PRINT 'DEPT CCTR OPER VENDOR/DESCRIPTION       DATE    PO NUM  PROD LN INVOICE  INV DATE   QTY      COST     QTY     COST   REASON XFER JOB'
     PRINT '---- ---- ---- ---------------------  -------- -------- ------- -------- -------- -------- -------- ------- -------- ------ --------'
     LINE.CNT=9
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF LINE.CNT >= 50 THEN
        GOSUB 20
        H.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT H.LINE
        PRINT STR('-',LEN(H.LINE))
        LINE.CNT=LINE.CNT + 2
     END
     IF DEPT.NO # PREVIOUS.DEPT.NO THEN
        MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
        P.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT P.LINE
        PRINT STR('-',LEN(P.LINE))
        PRINT SPACE(15):VEND.NAME:'  ':VEND.NUM
        PRINT SPACE(15):DESC
        PRINT SPACE(35):TRANS.TYPE"L#2":' ':DATE"R#8":' ':PO"L#8":' ':PLINE"L#7":' ':INV"L#8":' ':INV.DATE"R#8":' ':QTY"R#8":' ':TOTAL"R#8":' ':S.QTY"R#7":' ':S.COST"R#8":' ':S.CODE"L#6":' ':TRAN.JOB"L#8"
        LINE.CNT=LINE.CNT + 5
     END ELSE
        PRINT SPACE(15):VEND.NAME:'  ':VEND.NUM
        PRINT SPACE(15):DESC
        PRINT SPACE(35):TRANS.TYPE"L#2":' ':DATE"R#8":' ':PO"L#8":' ':PLINE"L#7":' ':INV"L#8":' ':INV.DATE"L#8":' ':QTY"R#8":' ':TOTAL"R#8":' ':S.QTY"R#7":' ':S.COST"R#8":' ':S.CODE"L#6":' ':TRAN.JOB"L#8"
        LINE.CNT=LINE.CNT + 3
     END
     PREVIOUS.DEPT.NO=DEPT.NO
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     MATREAD VEND.REC FROM VEND,CONO:VEND.NUM ELSE VEND.NAME=UNKNOWN
     IF VEND.NAME='' THEN VEND.NAME=VEND.DESC
     DATE=OCONV(TRANS.DATE,"D2-")
     INV.DATE=OCONV(INV.DATE,"D2-")
     SUB.TOTAL.REC(1)=SUB.TOTAL.REC(1) + QTY
     SUB.TOTAL.REC(2)=SUB.TOTAL.REC(2) + TOTAL
     SUB.TOTAL.REC(3)=SUB.TOTAL.REC(3) + S.QTY
     SUB.TOTAL.REC(4)=SUB.TOTAL.REC(4) + S.COST
     DEPT.TOTAL.REC(1)=DEPT.TOTAL.REC(1) + QTY
     DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + TOTAL
     DEPT.TOTAL.REC(3)=DEPT.TOTAL.REC(3) + S.QTY
     DEPT.TOTAL.REC(4)=DEPT.TOTAL.REC(4) + S.COST
     GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + QTY
     GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + TOTAL
     GRAND.TOTAL.REC(3)=GRAND.TOTAL.REC(3) + S.QTY
     GRAND.TOTAL.REC(4)=GRAND.TOTAL.REC(4) + S.COST
     IF TOTAL # '' THEN TOTAL=OCONV(TOTAL,"MD2")
     IF S.COST # '' THEN S.COST=OCONV(S.COST,"MD2")
     IF QTY # "" THEN QTY=OCONV(QTY,"MD2")
     IF S.QTY # "" THEN S.QTY=OCONV(S.QTY,"MD2")
     RETURN
*
*---- PRINT SUB TOTALS
*
50*
     GOSUB 55
     S.LINE=''
     S.LINE=S.LINE:SPACE(82):'-------- -------- ------- --------'
     PRINT S.LINE
     S.LINE=''
     S.LINE=S.LINE:SPACE(4):'* ':PREVIOUS.CCTR"L#4":'  ':PREVIOUS.OPER"L#4":'  ':OPER.DESC"L#30":SPACE(34):SUB.TOTAL.REC(1)"R#8":' ':SUB.TOTAL.REC(2)"R#8":' ':SUB.TOTAL.REC(3)"R#7":' ':SUB.TOTAL.REC(4)"R#8"
     PRINT S.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT SUB.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- GET VALUES FOR SUB TOTALS
*
55*
     MATREAD OPER.REC FROM OPERATION,CONO:PREVIOUS.OPER ELSE OPER.DESC=UNKNOWN
     IF SUB.TOTAL.REC(1) # "" THEN SUB.TOTAL.REC(1)=OCONV(SUB.TOTAL.REC(1),"MD2")
     IF SUB.TOTAL.REC(2) # '' THEN SUB.TOTAL.REC(2)=OCONV(SUB.TOTAL.REC(2),"MD2")
     IF SUB.TOTAL.REC(3) # "" THEN SUB.TOTAL.REC(3)=OCONV(SUB.TOTAL.REC(3),"MD2")
     IF SUB.TOTAL.REC(4) # '' THEN SUB.TOTAL.REC(4)=OCONV(SUB.TOTAL.REC(4),"MD2")
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     IF DEPT.TOTAL.REC(1) # "" THEN DEPT.TOTAL.REC(1)=OCONV(DEPT.TOTAL.REC(1),"MD2")
     IF DEPT.TOTAL.REC(2) # '' THEN DEPT.TOTAL.REC(2)=OCONV(DEPT.TOTAL.REC(2),"MD2")
     IF DEPT.TOTAL.REC(3) # "" THEN DEPT.TOTAL.REC(3)=OCONV(DEPT.TOTAL.REC(3),"MD2")
     IF DEPT.TOTAL.REC(4) # '' THEN DEPT.TOTAL.REC(4)=OCONV(DEPT.TOTAL.REC(4),"MD2")
     D.LINE=''
     D.LINE=D.LINE:SPACE(82):'-------- -------- ------- --------'
     PRINT D.LINE
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
     D.LINE=D.LINE:SPACE(42):DEPT.TOTAL.REC(1)"R#8":' ':DEPT.TOTAL.REC(2)"R#8":' ':DEPT.TOTAL.REC(3)"R#7":' ':DEPT.TOTAL.REC(4)"R#8"
     PRINT D.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT GRAND TOTALS
*
70*
     GRAND.TOTAL.REC(1)=OCONV(GRAND.TOTAL.REC(1),"MD2")
     GRAND.TOTAL.REC(2)=OCONV(GRAND.TOTAL.REC(2),"MD2")
     GRAND.TOTAL.REC(3)=OCONV(GRAND.TOTAL.REC(3),"MD2")
     GRAND.TOTAL.REC(4)=OCONV(GRAND.TOTAL.REC(4),"MD2")
     G.LINE=''
     G.LINE=G.LINE:SPACE(82):'-------- -------- ------- --------'
     PRINT G.LINE
     G.LINE=''
     G.LINE=G.LINE:"*** GRAND TOTALS ***":SPACE(62):GRAND.TOTAL.REC(1)"R#8":' ':GRAND.TOTAL.REC(2)"R#8":' ':GRAND.TOTAL.REC(3)"R#7":' ':GRAND.TOTAL.REC(4)"R#8"
     PRINT G.LINE
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
