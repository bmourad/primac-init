   SUBROUTINE JOB.STATS.INQ.RPT.SUB(CONO,CO.NAME,JOB.NUM,ADD.SUB,HD1,HD2)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>JCS.CPYLIB>COM.JOB.INQ
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.STATS.INQ.RPT.SUB
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 03/19/87
* DESCRIPTION -
* MODIFIED    - 8.8.89 CK CSF 9843
* T21346 gat 02/06/96 * FIX SUB JOB PRINT OUT
*T26334 epitka 12/19/2001 * REV12
*********************************************************************
*
*COPY>PMC.CPYLIB>VEND
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.STATS
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
* T21346
   SUB.STAR = ""
* T21346
   DSH = STR('-',30)
   DSH20 = DSH[1,20]
   DSH12 = DSH[1,12]
   DSH10 = DSH[1,10]
   DSH8 = DSH[1,8]
   DSH7 = DSH[1,7]
   PNO = 0
   SP2 = SPACE(2)
*T26493 v
*  HD1 = "";HD2 = ""
*  REPORT.NAME = "OUTSIDE.PURCHASE.INQUIRY.REPORT"
*  REPORT.NUMBER = "XXXX"
*  REPORT.DATE = DATE()
*  CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*T26493 ^
*
*---- MAIN PROCESSING
*
100*
   IF ADD.SUB THEN
      SUBS = DCOUNT(JOB.SUBS,VM)
      VAR = ""
      SUB.STAR = "***" ; * T21346
      FOR X = 1 TO SUBS
         MATREAD JSTAT.REC FROM JOB.STATS, CONO:JOB.SUBS<1,X> ELSE
            MAT JSTAT.REC = ''
         END
         IF JSTAT.OPO.NO # "" THEN
            VAR = VAR:VM:JSTAT.OPO.NO
         END
      NEXT X
      VAR = DELETE(VAR,1,1,0)
      MATREAD JSTAT.REC FROM JOB.STATS, CONO:JOB.NUM ELSE
         MAT JSTAT.REC = ""
      END
      BEGIN CASE
         CASE JSTAT.OPO.NO = "" AND VAR = ""
            GOTO 99999
         CASE JSTAT.OPO.NO = ""
            JSTAT.OPO.NO = VAR
         CASE VAR = ""
         CASE 1
            JSTAT.OPO.NO = JSTAT.OPO.NO:VM:VAR
      END CASE
   END ELSE
      MATREAD JSTAT.REC FROM JOB.STATS, CONO:JOB.NUM ELSE
         GOTO 99999
      END
      IF JSTAT.OPO.NO = "" THEN GOTO 99999
   END
*T26493 v
   HD3 = "JOB # : ":JOB.NUM"L#8":SPACE(2):SUB.STAR"L#3"
   HD3 = HD3:SPACE(39):"OUTSIDE PURCHASE INQUIRY REPORT"
*T26493 ^
   GOSUB 2000
*CSF 25687 v
   DUMMY=JSTAT.OPO.NO
   FOR I = DCOUNT(DUMMY,VM) TO 1 STEP -1
      LOCATE DUMMY<1,I> IN DUMMY<1>,1 SETTING PFND THEN
         IF PFND # I THEN DEL DUMMY<1,PFND>
      END
   NEXT I
   JSTAT.OPO.NO = DUMMY
*CSF 25687 ^
   PO.CNT = DCOUNT(JSTAT.OPO.NO,VM)
* T21346
   IF JOB.SUBS # "" THEN 
      SJOBS = JOB.SUBS:VM:JOB.NUM
   END ELSE
      SJOBS = JOB.NUM
   END
   SJOB.CNT = DCOUNT(SJOBS,VM)
   FOR X = 1 TO SJOB.CNT
* T21346
      FOR D = 1 TO PO.CNT
         MATREAD OPO.REC FROM OUTSIDE.PO,CONO:JSTAT.OPO.NO<1,D> ELSE GOTO 999
         MATREAD VEND.REC FROM VEND,CONO:OPO.VEND.NO ELSE MAT VEND.REC = ""
         JOB.CNT = DCOUNT(OPO.JOB.NO,VM)
         PTR = 1
200*
* T21346
*   IF PTR > JOB.CNT THEN GOTO 999
*CSF 23859
*   LOCATE JOB.NUM IN OPO.JOB.NO<1>,PTR SETTING JFND THEN
         LOCATE SJOBS<1,X> IN OPO.JOB.NO<1>,PTR SETTING JFND THEN
*
* T21346
            MATREAD CAOS.REC FROM CATEGORY.OSP,CONO:OPO.PROD.LINE<1,JFND> ELSE CAOS.DESC = "CATEGORY UNKNOWN"
            IF VEND.DESC = "" THEN
               V.NAME = "VENDOR UNKNOWN"
               V.NAME = V.NAME "L#30"
            END ELSE
               IF VEND.DESC<1,2> # "" THEN
                  V.NAME = VEND.DESC<1,2> "L#30"
               END ELSE
                  V.NAME = VEND.DESC "L#30"
               END
            END
            OPO.NUM = JSTAT.OPO.NO<1,D> "L#8"
            VEND.NO = OPO.VEND.NO "L#8"
            CATG = OPO.PROD.LINE<1,JFND> "L#7"
            C.DESC = CAOS.DESC "L#20"
            DATE.EXP = OCONV(OPO.EXP.DATE<1,JFND>,"D2/") "L#8"
            DATE.RECV = OCONV(OPO.DATE.RECVD<1,JFND>,"D2/") "L#8"
            O.QTY=OPO.QTY<1,JFND> - OPO.QTY.RECVD<1,JFND> - OPO.CANCEL.QTY<1,JFND>
            IF O.QTY < 0 THEN O.QTY = 0
            IF O.QTY # 0 THEN
               O.AMT = OPO.EST.COST<1,JFND> - OPO.AMT.RECVD<1,JFND> - OPO.CANCEL.COST<1,JFND>
               IF O.AMT < 0 THEN O.AMT = 0
            END ELSE
               O.AMT = 0
            END
            O.QTY=OCONV(O.QTY,'MD2') 'R#10'
            O.AMT=OCONV(O.AMT,'MD2') 'R#12'
            GOSUB 1000
         END
*   PTR = JFND + 1
*   GOTO 200
999*
      NEXT D
* T21346
   NEXT X
* T21346
   GOTO 99999
*
*---- PRINT DETAIL LINE
*
1000*
   IF LINE.CNT >= 55 THEN GOSUB 2000
   PRINT OPO.NUM:SP2:VEND.NO:SP2:V.NAME:SP2:CATG:SP2:C.DESC:SP2:
   PRINT DATE.EXP:SP2:DATE.RECV:SP2:O.QTY:SP2:O.AMT
   LINE.CNT = LINE.CNT + 1
   RETURN
*
*---- PRINT HEADING
*
2000*
   PRINT CHAR(12)
   PNO = PNO + 1
   PRINT HD1:PNO
   PRINT HD2
*T26493 v
*  PRINT SPACE(57):"FOR JOB : ":JOB.NUM:SP2:SUB.STAR
   PRINT HD3
*T26493 ^
   PRINT
   PRINT " OUTSIDE   VENDOR":SPACE(50):"CATEGORY          DATE    LST DATE     OPEN         OPEN"
   PRINT "  PO #     NUMBER             VENDOR NAME            CATEG        DESCRIPTION      EXPECTED  RECEIVED   QUANTITY      AMOUNT"
   PRINT DSH8:SP2:DSH8:SP2:DSH:SP2:DSH7:SP2:DSH20:SP2:
   PRINT DSH8:SP2:DSH8:SP2:DSH10:SP2:DSH12
   PRINT
   LINE.CNT = 8
   RETURN
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
