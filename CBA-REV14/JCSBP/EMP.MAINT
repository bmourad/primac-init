*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM      - EMP.MAINT
* BY           - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE         - 10/03/83
* DESCRIPTION
*T23278 markt 10/15/1998 * Check for divisional security
*T25697 cm 11/01/2001 * Add RF password to screen.
*T26126 adelgado 03/07/2002 * Implement the LOCKED clause for READU.
*T28243 cmykleb 09/08/2004 * Allow for update of BHR file.
*T29032 cmyklebu 12/28/2006 * Move pgm from rev12 to rev14.
*********************************************************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
   MAT FILE.VARS = ''
   OPEN '', 'EMPLOYEE' TO EMPLOYEE ELSE
      ERRMSG = 'EMPLOYEE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'EMPLOYEE.XREF' TO EMPLOYEE.XREF ELSE
      ERRMSG = 'EMPLOYEE.XREF FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'DIVISION' TO DIVISION ELSE
      ERRMSG = 'DIVISION FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG = 'DEPARTMENT FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'JCS.SCREENS' TO M.SCREENS ELSE
      ERRMSG = 'JCS.SCREENS FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = 'COMPANY FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','PREFIX' TO PREFIX ELSE
      ERRMSG = 'PREFIX FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'CONTROL FILE IS MISSING'
      GOTO 93000
   END
*
**** GET COMPANY
*
   MAT COMP.REC = ''
   CONO = ''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = 'END' THEN GOTO 99999
*
* INITIALIZATION
*
   MAT EDIT.COM = ''
   MAT EDIT.COM.DRIVER = ''
   MAT GEN.XREF.REC = ''
   DASHES = STR('-',80)
   ERRMSG = ''
   GXR.CO = CONO
   GXR.HEADING<1,2> = 'DESCRIPTION'
   GXR.ATT<1,1> = 0
   GXR.LEN<1,1> = 10
   GXR.LEN<1,2> = 30
*
* MAIN PROCESSING
*
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'EMPLOYEE'
   ECD.ACTION=1;CALL SCRN.EDIT
   ECD.SCRN.NO = 1
100*
   MAT EMP.REC = ''
   MAT DEPT.REC = ''
   MAT SCV.REC = ""
   OLD.NAME = ''
   ECD.ACTION=2;CALL SCRN.EDIT
   ECD.NUM = 5
   ECD.VALDAT.CODE = '4'
   ECD.VALDAT.FILE = EMPLOYEE
   ECD.PREFIX.ID = CONO
   ECD.ACTION=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = 'END'
         GOTO 99999
      CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
         EMP.NUM = ECD.RET.VALUE
*T25697 v
*         FOR REQUEST = 1 TO 7
*             ON REQUEST GOSUB 1000,2000,2500,3000,4000,5000,6000
         FOR REQUEST = 1 TO 8
            ON REQUEST GOSUB 1000,2000,2500,3000,4000,5000,6000,7000
*T25697 ^
            IF ECD.RET.VALUE = 'END' THEN GOTO 100
         NEXT REQUEST
      CASE ECD.RET.VALUE = ''
         EMP.NUM = ''
         GOSUB 1000
         IF EMP.NUM = '' THEN GOTO 100
         GOSUB 10000
         ECD.ACTION=3;CALL SCRN.EDIT
         OLD.NAME = EMP.LAST.NAME
      CASE ECD.VALDAT.ITEM # ''
      * T26126 v
         READU DUMMY FROM EMPLOYEE, CONO:ECD.RET.VALUE LOCKED
            ERRMSG = 'EMPLOYEE record is locked by user - ':GETUSERNAME(STATUS())
            GOSUB 91000;GOTO 100 
         END ELSE NULL
      * T26126 ^
         FOR I = 1 TO EMP.REC.SIZE
            EMP.REC(I) = ECD.VALDAT.ITEM<I>
         NEXT I
         EMP.NUM = ECD.RET.VALUE
*T23278 v
         DIV.CODE = EMP.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
         CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
         IF ERRMSG # '' THEN
            GOSUB 91000; GOTO 100
         END
*T23278 ^
         GOSUB 10000
         ECD.ACTION=3;CALL SCRN.EDIT
         OLD.NAME = EMP.LAST.NAME
      CASE 1
         ERRMSG = '* * INVALID EMPLOYEE # * *'
         GOSUB 91000
         GOTO 100
   END CASE
500*
   ECD.NUM = 15
   SCV.REC(ECD.NUM)<1> = ''
   ECD.ACTION=4;CALL SCRN.EDIT
   REQUEST = ECD.RET.VALUE
   BEGIN CASE
      CASE REQUEST = 'END' OR REQUEST = 'E'
         RELEASE EMPLOYEE, CONO:EMP.NUM       ;* T26126
         GOTO 100
      CASE NUM(REQUEST) AND REQUEST > 0
*T25697 v
*         ON REQUEST GOSUB 1000,2000,2500,3000,4000,5000,6000
         ON REQUEST GOSUB 1000,2000,2500,3000,4000,5000,6000,7000
*T25697 ^
      CASE REQUEST = 'F'
         CALL GEN.XREF.MAINT(CONO,EMP.NUM,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,PREFIX)
*T28243 v
         IF CO.GLS.HIST<1,3> = 'Y' THEN
            EMP.BHR.FLAG = DATE()
         END
*T28243 ^
         MATWRITE EMP.REC ON EMPLOYEE, CONO : EMP.NUM
         GOTO 100
   END CASE
   GOTO 500
1000*
   ECD.NUM = 6
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # '' AND ECD.RET.VALUE # 'END' THEN
      IF EMP.NUM = '' THEN
         GXR.XREF = EMPLOYEE.XREF
         GXR.FILE = EMPLOYEE
         GXR.LOC = 1
         GXR.TOP.LINE = 'EMPLOYEE XREF SEARCH'
         GXR.HEADING<1,1> = 'EMPLOYEE'
         GXR.HEADING<1,2> = 'LAST NAME'
         GXR.HEADING<1,3> = 'FIRST NAME'
         GXR.LEN<1,2> = 20
         GXR.LEN<1,3> = 20
         GXR.ATT<1,2> = 4
         GXR.ATT<1,3> = 5
         GXR.ID = ''
         GXR.SRCH.ID = ECD.RET.VALUE
         CALL GEN.XREF(MAT GEN.XREF.REC,PREFIX)
         GXR.HEADING<1,2> = 'DESCRIPTION'
         GXR.LEN<1,2> = 30
         GXR.HEADING = DELETE(GXR.HEADING,1,3,0)
         GXR.LEN = DELETE(GXR.LEN,1,3,0)
         GXR.ATT = DELETE(GXR.ATT,1,3,0)
         ECD.ACTION=2;CALL SCRN.EDIT
         IF GXR.ID # '' THEN
            MATREAD EMP.REC FROM EMPLOYEE, CONO:GXR.ID ELSE
               ERRMSG = 'EMPLOYEE ':GXR.ID:' IS MISSING'
               GOSUB 91000
               GOTO 1000
            END
            EMP.NUM = GXR.ID
         END
      END ELSE
         EMP.LAST.NAME = ECD.RET.VALUE
      END
   END
   RETURN
2000*
   ECD.NUM = 7
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # '' THEN EMP.FRST.NAME = ECD.RET.VALUE
   RETURN
2500*
   ECD.NUM = 3
   ECD.VALDAT.CODE = 2
   ECD.VALDAT.FILE = DIVISION
   ECD.PREFIX.ID = CONO
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE = '' THEN
      GXR.XREF=CONTROL
      GXR.FILE=DIVISION
      GXR.LOC=1
      GXR.TOP.LINE='DIVISION XREF SEARCH'
      GXR.HEADING<1,1>='DIVISION'
      GXR.ATT<1,2>=1
      GXR.ID=''
      GXR.SRCH.ID="DIVISIONS"
      CALL GEN.XREF(MAT GEN.XREF.REC,PREFIX)
      ECD.ACTION=2;CALL SCRN.EDIT
      IF GXR.ID='' THEN
         IF EMP.DIV # "" THEN SCV.REC(3)<1> = EMP.DIV
         ECD.ACTION=3;CALL SCRN.EDIT
         GOTO 2500
      END ELSE
         MATREAD DIV.REC FROM DIVISION, CONO:GXR.ID ELSE
            IF EMP.DIV # "" THEN SCV.REC(3)<1> = EMP.DIV
            ECD.ACTION=3;CALL SCRN.EDIT
            ERRMSG='DIVISION ':GXR.ID:' IS MISSING';GOSUB 91000
            GOSUB 2500
         END
*T23278 v
         DIV.CODE = GXR.ID; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
         CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
         IF ERRMSG # '' THEN
            GOSUB 91000; GOTO 2500
         END
*T23278 ^
         EMP.DIV = GXR.ID
         SCV.REC(3)<1> = EMP.DIV
         SCV.REC(4)<1> = DIV.DESC
         ECD.ACTION=3;CALL SCRN.EDIT
      END
   END ELSE
      IF ECD.RET.VALUE # 'END' THEN
*T23278 v
         DIV.CODE = ECD.RET.VALUE; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
         CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
         IF ERRMSG # '' THEN
            GOSUB 91000; GOTO 2500
         END
*T23278 ^
         EMP.DIV = ECD.RET.VALUE
         FOR I = 1 TO DIV.REC.SIZE
            DIV.REC(I) = ECD.VALDAT.ITEM<I>
         NEXT I
         ECD.NUM = 4
         SCV.REC(ECD.NUM)<1> = DIV.DESC
         ECD.ACTION=5;CALL SCRN.EDIT
      END
   END
   RETURN
3000*
   ECD.NUM = 8
   ECD.VALDAT.CODE = 2
   ECD.VALDAT.FILE = DEPARTMENT
   ECD.PREFIX.ID = CONO
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE = '' THEN
      GXR.XREF = DIVISION
      GXR.FILE = DEPARTMENT
      GXR.LOC = 2
      GXR.TOP.LINE = 'DEPARTMENT XREF SEARCH'
      GXR.HEADING<1,1> = 'DEPARTMENT'
      GXR.ATT<1,2> = 2
      GXR.ID = ''
      GXR.SRCH.ID = EMP.DIV
      CALL GEN.XREF(MAT GEN.XREF.REC,PREFIX)
      ECD.ACTION=2;CALL SCRN.EDIT
      IF GXR.ID = '' THEN
         IF EMP.DEPT # "" THEN
            SCV.REC(8)<1> = EMP.DEPT
         END ELSE
            SCV.REC(9)<1> = ''
         END
         ECD.ACTION=3;CALL SCRN.EDIT
         GOTO 3000
      END ELSE
         MATREAD DEPT.REC FROM DEPARTMENT, CONO:GXR.ID ELSE
            ERRMSG = 'DEPARTMENT ':GXR.ID:' IS MISSING'
            GOSUB 91000
            IF EMP.DEPT # "" THEN
               SCV.REC(8)<1> = EMP.DEPT
            END ELSE
               SCV.REC(9)<1> = ''
            END
            ECD.ACTION=3;CALL SCRN.EDIT
            GOTO 3000
         END
         EMP.DEPT = GXR.ID
         SCV.REC(8)<1> = EMP.DEPT
         SCV.REC(9)<1> = DEPT.DESC
         ECD.ACTION=3;CALL SCRN.EDIT
      END
   END ELSE
      IF ECD.RET.VALUE # 'END' THEN
         EMP.DEPT = ECD.RET.VALUE
         FOR I = 1 TO DEPT.REC.SIZE
            DEPT.REC(I) = ECD.VALDAT.ITEM<I>
         NEXT I
         ECD.NUM = 9
         SCV.REC(ECD.NUM)<1> = DEPT.DESC
         ECD.ACTION=5;CALL SCRN.EDIT
      END
   END
   RETURN
4000*
   ECD.NUM = 11
   ECD.DEFAULT = OCONV(DATE() , "D2/")
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN EMP.STRT.DATE = ECD.RET.VALUE
   RETURN
5000*
   ECD.NUM = 12
   ECD.MINV = EMP.STRT.DATE
   ECD.MAXV = EMP.STRT.DATE + 99999
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN EMP.TERM.DATE = ECD.RET.VALUE
   RETURN
6000*
   ECD.NUM = 10
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN EMP.LANGUAGE = ECD.RET.VALUE
   RETURN
*T25697 v
7000*
   ECD.NUM = 16
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN EMP.PASSWORD = ECD.RET.VALUE
   RETURN
*T25697 ^
10000*
   MATREAD DIV.REC FROM DIVISION, CONO : EMP.DIV ELSE
      MAT DIV.REC = ''
      DIV.DESC = '????????????????????'
   END
   MATREAD DEPT.REC FROM DEPARTMENT, CONO : EMP.DEPT ELSE
      MAT DEPT.REC = ''
      DEPT.DESC = '????????????????????'
   END
   SCV.REC(3)<1> = EMP.DIV
   SCV.REC(4)<1> = DIV.DESC
   SCV.REC(5)<1> = EMP.NUM
   SCV.REC(6)<1> = EMP.LAST.NAME
   SCV.REC(7)<1> = EMP.FRST.NAME
   SCV.REC(8)<1> = EMP.DEPT
   SCV.REC(9)<1> = DEPT.DESC
   SCV.REC(11)<1> = EMP.STRT.DATE
   SCV.REC(12)<1> = EMP.TERM.DATE
   SCV.REC(16)<1> = EMP.PASSWORD ; * T25697
   SCV.REC(10)<1> = EMP.LANGUAGE
   RETURN
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
* PRINT @(-1)
END
