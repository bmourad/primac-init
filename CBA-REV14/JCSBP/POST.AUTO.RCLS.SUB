   SUBROUTINE POST.AUTO.RCLS.SUB(CONO,POSTING.MON,DRC.ID)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JOB.RCLS
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - POST.AUTO.RCLS.SUB
* BY          - ZIAD A. YAMOUT , C.B.A
* DESCRIPTION -
*********************************************************************
*
***** INSERT FILE EQUETE
*
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>DAILY.RCLS
*COPY>JCS.CPYLIB>JOB.RCLS
*COPY>PMC.CPYLIB>DIVISION
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
**** INITALIZATION
   MAT JOB.REC = ""
   MAT FROM.JOB.REC = ""
   MAT TO.JOB.REC = ""
   MAT DRC.REC = ""
   MAT JRC.REC = ""
   SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
***** MAIN PROCESSING
*
  MATREADU DRC.REC FROM DAILY.RCLS, DRC.ID ELSE
     RELEASE DAILY.RCLS, DRC.ID
     GOTO 199999
  END
  MATREADU JOB.REC FROM JOB, CONO : DRC.F.JOB ELSE
     DRC.STATUS = 'JOB # ':DRC.F.JOB:' IS MISSING'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  IF DRC.F.JOB = DRC.T.JOB THEN
     DRC.STATUS = 'CANNOT RECLASSIFY TO THE SAME JOB'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  IF DRC.F.TYPE # DRC.T.TYPE THEN
     DRC.STATUS = 'CANNOT RECLASSIFY TO A DIFFERENT TYPE'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  S.DIV = JOB.DIV
  S.DEPT = JOB.DEPT
  S.TYPE = JOB.TYPE
  MAT FROM.JOB.REC = MAT JOB.REC
  MATREADU JOB.REC FROM JOB, CONO : DRC.T.JOB ELSE
     DRC.STATUS = 'JOB # ':DRC.T.JOB:' IS MISSING'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  IF JOB.DIV # S.DIV THEN
     DRC.STATUS = 'JOBS MUST HAVE THE SAME DIVISION'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  IF JOB.DEPT # S.DEPT[1,LEN(JOB.DEPT)] THEN
     DRC.STATUS = 'JOBS MUST HAVE THE SAME DEPARTMENT'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  IF JOB.TYPE # "R" AND JOB.TYPE # S.TYPE THEN
     DRC.STATUS = 'JOBS MUST HAVE THE SAME TYPE'
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END
  MAT TO.JOB.REC = MAT JOB.REC
  MAT JRC.REC = ''
  JRC.F.JOB = DRC.F.JOB
  JRC.F.TYPE = DRC.F.TYPE
  JRC.T.JOB = DRC.T.JOB
  JRC.T.CUST = JOB.CUST
  JRC.T.TYPE = DRC.T.TYPE
  JRC.DATE = DRC.DATE
  JRC.MON = POSTING.MON
  MAT JOB.REC = MAT FROM.JOB.REC
  JRC.F.CUST = JOB.CUST
  BEGIN CASE
  CASE DRC.F.TYPE = 'LB'
    CALL POST.LB.RCLS.SUB(CONO)
  CASE DRC.F.TYPE = 'MT'
    CALL POST.MT.RCLS.SUB(CONO)
  CASE DRC.F.TYPE = 'OS'
    CALL POST.OS.RCLS.SUB(CONO)
  CASE DRC.F.TYPE = 'SP'
    CALL POST.SP.RCLS.SUB(CONO)
  CASE DRC.F.TYPE = 'MS'
  CASE 1
    DRC.STATUS = 'INVALID FROM TYPE'
    MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
    RELEASE
    GOTO 199999
  END CASE
  IF JRC.TRAN = '' THEN
     MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
     RELEASE
     GOTO 199999
  END ELSE
     MATWRITE FROM.JOB.REC ON JOB, CONO:DRC.F.JOB
     MATWRITE TO.JOB.REC ON JOB, CONO:DRC.T.JOB
     IF DRC.TRAN = '' THEN
        DELETE DAILY.RCLS, DRC.ID
     END ELSE
        MATWRITE DRC.REC ON DAILY.RCLS, DRC.ID
        ERRMSG = "CHECK RECLASSIFICATION EDIT LISTING"
        ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
     END
     READU NEW.SEQ FROM CONTROL, CONO:"JOB.RCLS" ELSE NEW.SEQ = 10001
     NEXT.SEQ = NEW.SEQ + 1
     WRITE NEXT.SEQ ON CONTROL, CONO:"JOB.RCLS"
     MATWRITE JRC.REC ON JOB.RCLS, CONO:NEW.SEQ
  END
****** END OF JOB
  GOTO 999999
199999*
   ERRMSG = "CHECK RECLASSIFICATION EDIT LISTING"
   ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
999999 RETURN
   END
