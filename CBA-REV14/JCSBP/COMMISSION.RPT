***************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - COMMISSION.RPT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/06/85
* DESCRIPTION -
* This program produces a Commission report sorted by
* Salesman and by Customer.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*ENDDOC
***************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>COMMISSION
*COPY>PMC.CPYLIB>CUSTOMER
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD XX ELSE
        ERRMSG="MUST BE RUN FROM A PROC"
        GOSUB 91000
        GOTO 99999
     END
     CONO = XX<1>
     CO.NAME = XX<2>
     PERIOD=XX<3>
*
*---- DIMENSION VARIABLES
*
     DIM SUBTOTAL.REC(10)
     DIM TOTAL.REC(10)
     DIM SP(132)
     DIM DSH(30)
     DIM COMM.TBL(10)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     MATREAD COMP.REC FROM COMPANY, CONO ELSE
        ERRMSG = "COMPANY ":CONO:" IS MISSING"
        GOTO 93000
     END
     IF CO.COMMISSION # "Y" THEN GOTO 99999
     OPEN '','COMMISSION' TO COMMISSION ELSE ERRMSG='COMMISSION FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
*T26493 v
*    RNAME = "COMMISSION REPORT"
*    RPT.NO = "XXXX"
     CO.NAME = ""
     RNAME = ""
     RPT.NO = XX<2>
*T26943 ^
     RDATE = DATE()
     PER = PERIOD[5,2]
     ATT = PER+1
     READV REC FROM CONTROL, CONO:"SALESDATES",ATT ELSE REC = ""
     PDATE = REC<1,1>
     CALL GET.PROG.HEAD(CONO,CO.NAME,RNAME,RPT.NO,RDATE,HLINE1,HLINE2)
     PG.NO=0
     LINE.CNT=0
     ERRMSG=""
     UNKNOWN=STR("?",25)
     PREV.JOB = ""
     PREV.SLSMN = ""
     FOR S = 1 TO 132
        SP(S) = SPACE(S)
     NEXT S
     FOR D = 1 TO 30
        DSH(D) = STR("-",D)
     NEXT D
     EQL10 = STR("=",10)
     MAT COMM.TBL = ""
     MAT SUBTOTAL.REC=""
     MAT TOTAL.REC=""
     FIRST.FLAG=1
     NEW.PAGE=0
     MM.TDATE = OCONV(DATE(),"D2/")[1,2]
     PRINTER ON
*
*---- MAIN PROCESSING
*
10*
     READNEXT COMM.KEY ELSE GOTO 15
     MATREAD COMM.REC FROM COMMISSION,COMM.KEY ELSE GOTO 10
     MAT COMM.TBL = ""
     I.CNT = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "")
     CP = 0
     FOR I = 1 TO I.CNT
        IF COMM.INV.AMT<1,I>+0 # 0 THEN
           CP = CP + 1
           COMM.TBL(1)<1,CP> = COMM.INV.NO<1,I>
           COMM.TBL(2)<1,CP> = COMM.INV.DATE<1,I>
           COMM.TBL(3)<1,CP> = COMM.INV.AMT<1,I>
           COMM.TBL(4)<1,CP> = COMM.PMT.DATE<1,I>
           COMM.TBL(5)<1,CP> = COMM.INV.BAL<1,I>
        END
     NEXT I
     C.CNT = COUNT(COMM.TBL(1),VM) + (COMM.TBL(1) # "")
     IF C.CNT = 0 THEN GOTO 10
     COMM.AMT = COMM.CALC + COMM.ADJ
     DUE = COMM.AMT - COMM.PAID
     PAID.DATE = OCONV(COMM.PAID.DATE,"D2/")
     MM.PDATE = PAID.DATE[1,2]
*    IF DUE + 0 = 0 AND MM.TDATE # MM.PDATE THEN GOTO 10
     JOB.NO = COMM.KEY[4,99]
     CUST.NO = COMM.CUST
     SLSMN = COMM.SLSM
     PAID = COMM.PAID
     IF PREV.SLSMN # SLSMN THEN
        IF FIRST.FLAG = 0 THEN GOSUB 50
        MATREAD SALESMAN.REC FROM SALESMAN,CONO:SLSMN ELSE SALS.NAME = UNKNOWN
        GOSUB 20
     END
     FIRST.LINE = 1
     GOSUB 30
     PREV.JOB = JOB.NO
     IF C.CNT = 1 THEN GOTO 14
     FOR CC = 2 TO C.CNT
        FIRST.LINE = 0
        GOSUB 30
     NEXT CC
14*
     PREV.SLSMN = SLSMN
     GOTO 10
15*
     GOSUB 50
     PRINTER OFF
     GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
     IF FIRST.FLAG=1 THEN FIRST.FLAG=0
     PRINT CHAR(12)
     PRINT
     PG.NO=PG.NO + 1
     PRINT HLINE1:PG.NO
     PRINT HLINE2
     PRINT SP(55):"JOBS COMPLETED THROUGH ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
     PRINT "SALESREP - ":SLSMN "L#3":" ":SALS.NAME "L#25"
     PRINT
*    PRINT SP(33):"JOB     INVOICE  INVOICE   INVOICE  LAST PAY   INVOICE  <-------------- COMMISSION -------------->"
     PRINT SP(33):"JOB     INVOICE  INVOICE   INVOICE  LAST PAY   INVOICE  <--------- COMMISSION --------->"
*    PRINT SP(11):"CUSTOMER":SP(13):"NUMBER   NUMBER    DATE     AMOUNT     DATE     BALANCE    AMOUNT      PAID        DUE    PAID DATE"
     PRINT SP(11):"CUSTOMER":SP(13):"NUMBER   NUMBER    DATE     AMOUNT     DATE     BALANCE    AMOUNT      PAID      BALANCE"
*    PRINT DSH(30):' ':DSH(8):' ':DSH(8):' ':DSH(8):' ':DSH(10):' ':DSH(8):' ':DSH(10):' ':DSH(10):' ':DSH(10):' ':DSH(10):' ':DSH(9)
     PRINT DSH(30):' ':DSH(8):' ':DSH(8):' ':DSH(8):' ':DSH(10):' ':DSH(8):' ':DSH(10):' ':DSH(10):' ':DSH(10):' ':DSH(10)
     LINE.CNT = 8
     NEW.PAGE = 1
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF LINE.CNT >= 55 THEN GOSUB 20
     P.LINE=""
     IF JOB.NO # PREV.JOB OR NEW.PAGE=1 THEN
        MATREAD CUST.REC FROM CUSTOMER,CONO:CUST.NO ELSE CUST.NAME = UNKNOWN
        IF NEW.PAGE=1 THEN NEW.PAGE=0
        PRINT
        LINE.CNT = LINE.CNT + 1
        P.LINE = CUST.NAME "L#30":' ':JOB.NO "L#8":' '
     END ELSE
        P.LINE = SP(40)
     END
     P.LINE = P.LINE:INV.NUM "L#8":' ':INV.DATE "L#8":' ':INV.AMT "R#10":' ':LAST.PAY.DATE "L#8":' ':INV.BAL "R#10":' '
     IF FIRST.LINE THEN
*       P.LINE = P.LINE:C.AMT "R#10":' ':C.PAID "R#10":' ':C.DUE "R#10":' ':PAID.DATE"R#8"
        P.LINE = P.LINE:C.AMT "R#10":' ':C.PAID "R#10":' ':C.DUE "R#10"
        FIRST.LINE = 0
     END
     PRINT P.LINE
     LINE.CNT=LINE.CNT + 1
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     FOR SS = 1 TO 10
        FOR II = 1 TO C.CNT
           IF COMM.TBL(SS)<1,II> = 0 THEN COMM.TBL(SS)<1,II> = ""
        NEXT II
     NEXT SS
     IF FIRST.LINE THEN
        INV.NUM = COMM.TBL(1)<1,1>
        INV.DATE = OCONV(COMM.TBL(2)<1,1>,"D2-")
        INV.AMT = COMM.TBL(3)<1,1>
        LAST.PAY.DATE = OCONV(COMM.TBL(4)<1,1>,"D2-")
        INV.BAL = COMM.TBL(5)<1,1>
        SUBTOTAL.REC(1)=SUBTOTAL.REC(1) + COMM.AMT
        SUBTOTAL.REC(2)=SUBTOTAL.REC(2) + PAID
        SUBTOTAL.REC(3)=SUBTOTAL.REC(3) + DUE
        IF COMM.AMT = 0 OR COMM.AMT = "" THEN
           C.AMT = ""
        END ELSE
           C.AMT = OCONV(COMM.AMT,"MD2-")
        END
        IF PAID = 0 OR PAID = "" THEN
           C.PAID = ""
        END ELSE
           C.PAID = OCONV(PAID,"MD2-")
        END
        IF DUE = 0 OR DUE = "" THEN
           C.DUE = ""
        END ELSE
           C.DUE = OCONV(DUE,"MD2-")
        END
     END ELSE
        INV.NUM = COMM.TBL(1)<1,CC>
        INV.DATE = OCONV(COMM.TBL(2)<1,CC>,"D2-")
        INV.AMT = COMM.TBL(3)<1,CC>
        LAST.PAY.DATE = OCONV(COMM.TBL(4)<1,CC>,"D2-")
        INV.BAL = COMM.TBL(5)<1,CC>
     END
     IF INV.AMT = 0 OR INV.AMT = "" THEN
        INV.AMT = ""
     END ELSE
        INV.AMT = OCONV(INV.AMT,"MD2-")
     END
     IF INV.BAL = 0 OR INV.BAL = "" THEN
        INV.BAL = ""
     END ELSE
        INV.BAL = OCONV(INV.BAL,"MD2-")
     END
     RETURN
*
*---- PRINT SUB TOTALS
*
50*
     GOSUB 60
     IF (LINE.CNT + 6) >= 55 THEN GOSUB 20
     PRINT SP(89):EQL10:' ':EQL10:' ':EQL10
     PRINT "** SALESREP TOTALS":SP(71):SC.AMT "R#10":' ':SC.PAID "R#10":' ':SC.DUE "R#10"
     PRINT
*    PRINT SP(71):"SALESREP DRAW":SP(27):COMM.DRAW "R#10"
     PRINT SP(71):"SALESREP DRAW":SP(16):COMM.DRAW "R#10"
*    PRINT SP(111):EQL10
     PRINT SP(100):EQL10
*    PRINT SP(111):G.TOTAL "R#10"
     PRINT SP(100):G.TOTAL "R#10"
     LINE.CNT=LINE.CNT + 6
     MAT SUBTOTAL.REC=""
     RETURN
*
*---- VALUES FOR SUB TOTALS
*
60*
     SC.AMT = OCONV(SUBTOTAL.REC(1),"MD2-")
     SC.PAID = OCONV(SUBTOTAL.REC(2),"MD2-")
     SC.DUE = OCONV(SUBTOTAL.REC(3),"MD2-")
     MATREAD COMM.DRAW.REC FROM COMMISSION,CONO:"!":PREV.SLSMN ELSE COMM.DRAW = ""
     G.TOTAL = SUBTOTAL.REC(2) - COMM.DRAW
     COMM.DRAW = OCONV(COMM.DRAW * (-1),"MD2-")
     G.TOTAL = OCONV(G.TOTAL,"MD2-")
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
