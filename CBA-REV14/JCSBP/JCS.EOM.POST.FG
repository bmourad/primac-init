   SUBROUTINE JCS.EOM.POST.FG(FNGD.WIP.HIST,FNGD_WIP_HIST_TAG,INV_RECEIPTS)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>JCS.CPYLIB>COM.JCS.EOM.POST
*COPY>PMC.CPYLIB>COM.COMPANY
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JCS.EOM.POST.FG
* DATE        - 03/28/93
* DESCRIPTION -
* TASK
*    19526 LLH 10/09/94 - EOD/EOM POSTING
*T21319 lanny 12/12/1996 * FNGD VAR ACCT NEEDS TO COME FROM CATEGORY
*                          FILE IF AVAILABLE.
*T21177 diane 12/19/1996 * Move message for GUI
*T22367 renee 11/25/1997 * Taking out the transaction that debits the  
*                          WIP clearing account and credits the FNGD   
*                          variance account when the job is left open.
*T26334 epitka 03/11/2002 * REV12
*T26925 cmykleb 10/18/2002 * JOB.FNGD.STATS may have multiple products
*                            and/or multiple catagories.
*T28691 lross 10/17/2005 * Incorrect use of "SUM" verb.
*T28943 lross 07/12/2006 * Processing multiple times without closing causes
*                          prior processed amount to be excluded.
*ENDDOC
*********************************************************************
*
*---- INSERT FILE EQUATE
*
*COPY>JCS.CPYLIB>EOM.ACCT
*COPY>JCS.CPYLIB>JOB
*COPY>OPS.CPYLIB>FNGD.WIP.HIST
*COPY>OPS.CPYLIB>JOB.FNGD.STATS
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>PMC.CPYLIB>POST.REJECTS
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- MAIN
*
   NEW.REC = 0; JNEW.REC = 0
   SELECT FNGD_WIP_HIST_TAG
   DATA = 1
   LOOP
      READNEXT FWH.ID ELSE DATA = 0
   WHILE DATA DO
      IF CONO # FWH.ID[1,3] THEN GOTO 999
      JOBNO=FWH.ID[4,99]
      P_X  = 3 ; P_Y = 23 ; P_VALUE = 'NOW PROCESSING JOB - ':JOBNO ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      MATREADU FWH.REC FROM FNGD.WIP.HIST, FWH.ID ELSE
         MAT PRR.REC = ''
         PRR.JOB = JOBNO
         PRR.FILE = 'FNGD.WIP.HIST'
         PRR.ID = JOBNO
         PRR.ERR = 'CANNOT LOCATE'
         PRR.SEQ = PRR.SEQ + 1
         MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
         GOTO 999
      END
      MATREAD JFS.REC FROM JOB.FNGD.STATS, FWH.ID ELSE MAT JFS.REC = "" 
      MATREAD JOB.REC FROM JOB, FWH.ID ELSE
         MAT PRR.REC = ''
         PRR.JOB = JOBNO
         PRR.FILE = 'JOB'
         PRR.ID = JOBNO
         PRR.ERR = 'CANNOT LOCATE'
         PRR.SEQ = PRR.SEQ + 1
         MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
         GOTO 999
      END
      FNGD.WIP = 0; FNGD.AMT = 0
*T26925 v
      HOLD.CATG = ""
      HOLD.FNGD = ""
*T26925 ^
      LOCATE PERIOD IN FWH.WIP.PER<1>,1 SETTING MPTR THEN
         FNGD.WIP = FWH.WIP.AMT<1,MPTR>
      END
      PCNT = DCOUNT(JFS.PROD,VM)            
      FOR P = 1 TO PCNT
         PROD = JFS.PROD<1,P> 
         MATREAD INV.REC FROM INVENTORY, CONO:PROD ELSE
            MAT PRR.REC = ""
            PRR.JOB = JOBNO
            PRR.FILE = "INVENTORY"
            PRR.ID = PROD
            PRR.ERR = "CANNOT LOCATE"
            PRR.SEQ = PRR.SEQ + 1
            MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
            GOTO 299
         END
         MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
            MAT PRR.REC = ""
            PRR.JOB = JOBNO
            PRR.FILE = "CATEGORY"
            PRR.ID = INV.LINE
            PRR.ERR = "CANNOT LOCATE"
            PRR.SEQ = PRR.SEQ + 1
            MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
            GOTO 299
         END
*T26925 v
         LOCATE INV.LINE IN HOLD.CATG<1>,1 SETTING CFND ELSE
            HOLD.CATG<1,CFND> = INV.LINE
            HOLD.FNGD<1,CFND> = 0
         END
*T26925 ^
         SCNT = DCOUNT(JFS.RECP<1,P>,SM)
         FOR S = 1 TO SCNT
            RECP = JFS.RECP<1,P,S>   ; * T22367 Changes to JFS.PROD      
            MATREAD INVR.REC FROM INV_RECEIPTS, CONO:RECP ELSE
               MAT PRR.REC = ""
               PRR.JOB = JOBNO
               PRR.FILE = "INV_RECEIPTS"
               PRR.ID = RECP
               PRR.ERR = "CANNOT LOCATE"
               PRR.SEQ = PRR.SEQ + 1
               MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
               GOTO 199
            END
*T28943     LOCATE "EOM" IN INVR.STATUS<1>,1 SETTING XX ELSE 
               IF PERIOD=INVR.PERIOD THEN
*T26925 v
*                 FNGD.AMT = FNGD.AMT + INVR.TOT.COST
                  HOLD.FNGD<1,CFND> += INVR.TOT.COST
*T26925 ^
               END
*T28943     END
199 *
         NEXT S
299 *
      NEXT P
*T26925 v
      FDONE = 0
      TEMP.WIP = FNGD.WIP
      PCT = 1
      BEGIN CASE
         CASE DCOUNT(HOLD.CATG,VM) = 1
            FNGD.AMT = HOLD.FNGD
            FDONE = 1
            GOSUB 300
         CASE DCOUNT(HOLD.CATG,VM) = 0
            MAT CATG.REC = ''
            FNGD.AMT = 0
            FDONE = 1
            GOSUB 300
         CASE 1
            FOR F = 1 TO DCOUNT(HOLD.CATG,VM)
*T28691        IF SUM(HOLD.FNGD,VM) # 0 THEN
               IF SUM(HOLD.FNGD) # 0 THEN
                  PCT = HOLD.FNGD<1,F> / SUM(HOLD.FNGD)
                  TEMP.WIP = ICONV(FNGD.WIP * PCT,'MD0')
                  FNGD.AMT = HOLD.FNGD<1,F>
                  MATREAD CATG.REC FROM CATEGORY,CONO:HOLD.CATG<1,F> ELSE MAT CATG.REC = ''
               END ELSE
                  FNGD.AMT = 0
                  MATREAD CATG.REC FROM CATEGORY,CONO:HOLD.CATG<1,1> ELSE MAT CATG.REC = ''
                  F = DCOUNT(HOLD.CATG,VM)
               END
               IF F = DCOUNT(HOLD.CATG,VM) THEN FDONE = 1
               GOSUB 300
            NEXT F
      END CASE
      GOTO 999
*T26925 ^
*
*---- WIP DIFFERENTIAL
*
300 *
*T26925 v
*     IF FNGD.AMT+0 # 0 THEN
*T26925 ^
      BEGIN CASE
         CASE CATG.INV.VAR = FNGD.VAR.ACCT
         CASE CATG.INV.VAR # ""
            CATG.INV.VAR = CATG.INV.VAR : STR("0",IN.ACCT.LEN-LEN(CATG.INV.VAR))
            CATG.INV.VAR = CATG.INV.VAR[1,IN.ACCT.LEN]
            MATREAD COA.REC FROM COA, CONO : CATG.INV.VAR ELSE COA.LEVEL = 0
            FNGD.VAR.ACCT = CATG.INV.VAR
            FNGD.VAR.LEVEL = COA.LEVEL
         CASE GLTB.FNGD.VAR = FNGD.VAR.ACCT
         CASE 1
            MATREAD COA.REC FROM COA, CONO : GLTB.FNGD.VAR ELSE COA.LEVEL = 0
            FNGD.VAR.ACCT = GLTB.FNGD.VAR
            FNGD.VAR.LEVEL = COA.LEVEL
      END CASE
*T26925 v
*     END
*T26925 ^
      BEGIN CASE
         CASE FNGD.VAR.LEVEL < 1
            T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : FNGD.VAR.ACCT
         CASE 1
            T.ACCT = JOB.DIV : GEN.DEPT : GEN.CCTR : FNGD.VAR.ACCT
      END CASE
*T26925 v
*     AMT = FNGD.WIP - FNGD.AMT
      AMT = TEMP.WIP - FNGD.AMT
*T26925 ^
      LOCATE PERIOD IN FWH.GLA.PER<1>,1 BY "AR" SETTING PINDX THEN
*T26925 v
*        AMT = AMT - FWH.GLA.AMT<1,PINDX>
*        FWH.GLA.EOD<1,PINDX> = AMT
         AMT = AMT - ICONV(FWH.GLA.AMT<1,PINDX> * PCT,'MD0')
*T28943 v  FWH.GLA.EOD<1,PINDX> += AMT
         FWH.GLA.EOD<1,PINDX> = AMT
*T26925 ^
      END ELSE
         FWH.GLA.PER = INSERT(FWH.GLA.PER,1,PINDX,1,PERIOD)
         FWH.GLA.EOD = INSERT(FWH.GLA.EOD,1,PINDX,1,AMT)
         FWH.GLA.AMT = INSERT(FWH.GLA.AMT,1,PINDX,1,"0")
      END
      BEGIN CASE
         CASE AMT + 0 > 0
            TRAN.AMT = AMT
            GOSUB 3000
         CASE AMT + 0 < 0
            TRAN.AMT = 0 - AMT
            GOSUB 4000
      END CASE
      BEGIN CASE
         CASE WIP.CLR.LEVEL < 1
            T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : WIP.CLR.ACCT
         CASE 1
            T.ACCT = JOB.DIV : GEN.DEPT : GEN.CCTR : WIP.CLR.ACCT
      END CASE
      BEGIN CASE
         CASE AMT + 0 > 0
            TRAN.AMT = AMT
            GOSUB 4000
         CASE AMT + 0 < 0
            TRAN.AMT = 0 - AMT
            GOSUB 3000
      END CASE
*T26925 v
*     MATWRITE FWH.REC ON FNGD.WIP.HIST, FWH.ID
      IF FDONE THEN MATWRITE FWH.REC ON FNGD.WIP.HIST, FWH.ID
      RETURN
*T26925 ^
999 *
      RELEASE FNGD.WIP.HIST, FWH.ID
   REPEAT
   GOTO 99999
*
*---- DEBIT (JOB)
*
3000 *
   ETR.ID = CONO : T.ACCT : JOBNO : "!FG-D*" : JNEW.REC
   MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
      MAT ETR.REC = ""
      ETR.REF = JOB.CUST
   END
   ETR.AMT = ETR.AMT + TRAN.AMT
   LOCATE JOBNO IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
   INS JOBNO BEFORE ETR.TRAN<1,PTR>
   INS JOB.STAT.DATE<1,1> BEFORE ETR.DATE<1,PTR>
   INS TRAN.AMT BEFORE ETR.TAMT<1,PTR>
   IF PTR > 99 THEN JNEW.REC = JNEW.REC+1
   MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
   RETURN
*
*---- CREDIT (JOB)
*
4000 *
   ETR.ID = CONO : T.ACCT : JOBNO : "!FG-C*" : JNEW.REC
   MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
      MAT ETR.REC = ""
      ETR.REF = JOB.CUST
   END
   ETR.AMT = ETR.AMT - TRAN.AMT
   LOCATE JOBNO IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
   INS JOBNO BEFORE ETR.TRAN<1,PTR>
   INS JOB.STAT.DATE<1,1> BEFORE ETR.DATE<1,PTR>
   INS (0 - TRAN.AMT) BEFORE ETR.TAMT<1,PTR>
   IF PTR > 99 THEN JNEW.REC = JNEW.REC+1
   MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
   RETURN
*
*---- END
*
99999 *
   RETURN
END
