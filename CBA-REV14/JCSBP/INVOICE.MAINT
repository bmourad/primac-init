*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.INVOICE
*******************************************************************
* REVISION    - [08.1]
* COPYRIGHT   - 1982 by C.B.A     (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - INVOICE.MAINT
* AUTHOR      - WALID YAMOUT, CBA
* DATE        - 06/05/84
* DESCRIPTION
* This program is used to create or maintain invoices, credit memos
* or debit memos.
* COMMENT     - ADDED JOB.STATUS '5' - "REOPENED"
* - TASK #12907 (RRG)
*T28826 wvaughan 05/09/2006 * x
*******************************************************************
*
*-- FILE COPY STATEMENTS
*
*COPY>JCS.CPYLIB>INVOICE
*COPY>PMC.CPYLIB>INVOICE.CODE
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>PMC.CPYLIB>DIVISION
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>JOB.SALES.STATS
*
*-- DIMENSIONED VARIABLES
*
* T21796      DIM SAVE.CUST(75)
      DIM SAVE.CUST(CUST.REC.SIZE)
      DIM SAVE.REC(60)
*
*-- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*-- OPEN ALL FILES
*
      OPEN "","JCS.SCREENS" TO M.SCREENS ELSE ERRMSG="JCS.SCREENS FILE IS MISSING"; GOTO 93000
      OPEN "","CONTROL" TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING"; GOTO 93000
      OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING"; GOTO 93000
      OPEN "","JOB" TO JOB ELSE ERRMSG="JOB FILE IS MISSING"; GOTO 93000
      OPEN "","INVOICE" TO INVOICE ELSE ERRMSG="INVOICE FILE IS MISSING"; GOTO 93000
      OPEN "","DAILY.INVOICE" TO DAILY.INVOICE ELSE ERRMSG="DAILY.INVOICE FILE IS MISSING"; GOTO 93000
      OPEN "","INVOICE.CODE" TO INVOICE.CODE ELSE ERRMSG="INVOICE.CODE FILE IS MISSING"; GOTO 93000
      OPEN "","VOID.INVOICES" TO VOID.INVOICES ELSE ERRMSG="VOID.INVOICES FILE IS MISSING"; GOTO 93000
      OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG="CUSTOMER FILE IS MISSING"; GOTO 93000
      OPEN "","SALESMAN" TO SALESMAN ELSE ERRMSG="SALESMAN FILE IS MISSING"; GOTO 93000
      OPEN "","DIVISION" TO DIVISION ELSE ERRMSG="DIVISION FILE IS MISSING"; GOTO 93000
      OPEN "","JOB.CATEGORY" TO JOB.CATEGORY ELSE ERRMSG="JOB.CATEGORY FILE IS MISSING"; GOTO 93000
      OPEN "","JOB.SHIP" TO JOB.SHIP ELSE ERRMSG="JOB.SHIP FILE IS MISSING"; GOTO 93000
      OPEN "","SALES.CODE" TO SALES.CODE ELSE ERRMSG="SALES.CODE FILE IS MISSING"; GOTO 93000
      OPEN "","TAX" TO TAX ELSE ERRMSG="TAX FILE IS MISSING"; GOTO 93000
      OPEN "","SHIP.VIA" TO SHIP.VIA ELSE ERRMSG="SHIP-VIA FILE IS MISSING"; GOTO 93000
      OPEN "","PREFIX" TO PREFIX ELSE ERRMSG = "PREFIX FILE IS MISSING" ; GOSUB 93000
      OPEN "","CUSTOMER.XREF" TO CUSTOMER.XREF ELSE ERRMSG = "CUSTOMER.XREF FILE IS MISSING" ; GOSUB 93000
*
*-- GET COMPANY NUMBER
*
      MAT COMP.REC = ""
      CONO = ""
      CALL GET.CONO(CONO,MAT COMP.REC)
      IF CONO = "END" THEN GOTO 999999
      IF CO.ARS = "Y" THEN OPEN "","OPEN.RECV" TO OPEN.RECV ELSE ERRMSG="OPEN.RECV FILE IS MISSING"; GOTO 93000
      IF CO.SAS = "Y" AND CO.SAS.INVOICE # "N" THEN OPEN "","JOB.SALES.STATS" TO JOB.SALES.STATS ELSE ERRMSG="JOB.SALES.STATS FILE IS MISSING";GOTO 93000
*
*-- GET MENU SELECTION
*
      PROCREAD ID ELSE ERRMSG = "MUST RUN FROM MENU"; GOTO 93000
      MENU = ID<1>
*
*-- INITIALIZATION
*
      UNKNOWN = "????????"
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.CNT = 2
      BEGIN CASE
      CASE MENU = "INVOICE"
         ECD.SCRN.NAME<1> = "INVOICE.MAINT"
         ECD.SCRN.NAME<2> = "INVOICE.MAINT.SUB"
         ECD.SCRN.NAME<3> = "INVOICE.WIP.SUB"
         ECD.SCRN.CNT = 3
         IF CO.SAS.INVOICE # "N" THEN
            ECD.SCRN.NAME<4> = "JOB.SALES.MAINT"
            ECD.SCRN.CNT = 4
         END
      CASE MENU = "DEBIT"
         ECD.SCRN.NAME<1> = "DEBIT.MEMO"
         ECD.SCRN.NAME<2> = "DEBIT.MEMO.SUB"
      CASE MENU = "CREDIT"
         ECD.SCRN.NAME<1> = "CREDIT.MEMO"
         ECD.SCRN.NAME<2> = "CREDIT.MEMO.SUB"
      CASE 1
         ERRMSG = "INVALID SCREEN"; GOTO 93000
      END CASE
      ECD.ACTION=1;CALL SCRN.EDIT
      MAT EDIT.COM = ""
      TYP = 0; CALL EDIT.SUB; FILL = "#"
      LINE.COUNT.C = 2
      LINE.COUNT.D = 4
*
*-- MAIN PROCESSING
*
100*
      ECD.SCRN.NO = 1
      MAT SCV.REC = ""
      ECD.ACTION=2;CALL SCRN.EDIT
      GOTO 150
120*
      MAT SCV.REC = ""
      ECD.ACTION=6;CALL SCRN.EDIT
150*
      SCV.REC(23)<1> = DATE()
      ECD.NUM=23;ECD.ACTION=5;CALL SCRN.EDIT
      MAT JOB.REC = ""
      MAT CUST.REC = ""
      MAT SAVE.CUST = ""
      MAT SAVE.REC = ""
      MAST.CUST = ""
      JOB.NO = ""
      ECD.NUM=1;ECD.ACTION = 4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO 999999
      JOB.NO = ECD.RET.VALUE
      MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE
         ERRMSG = "Cannot locate Job record - ":JOB.NO; GOSUB 91000
         GOTO 120
      END
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
         CUST.NAME = UNKNOWN
         CUST.ACCT = ""
      END
      MATREAD DIV.REC FROM DIVISION, CONO:JOB.DIV ELSE
         MAT DIV.REC = ""
         DIV.DESC = "??????????"
      END
      MATREAD SALESMAN.REC FROM SALESMAN, CONO:JOB.SLSMN ELSE
         MAT SALESMAN.REC = ""
         SALS.NAME = "??????????"
      END
      READV CATG.DESC FROM JOB.CATEGORY, CONO:JOB.CATG,1 ELSE
         CATG.DESC = "??????????"
      END
      SCV.REC(3)<1>=CUST.NAME
      SCV.REC(4)<1>=JOB.DIV
      SCV.REC(5)<1>=DIV.DESC
      SCV.REC(6)<1>=JOB.SLSMN
      SCV.REC(7)<1>=SALS.NAME
      SCV.REC(8)<1>=JOB.TYPE
      BEGIN CASE
      CASE JOB.TYPE = "R"
         SCV.REC(9)<1>="REGULAR"
      CASE JOB.TYPE = "S"
         SCV.REC(9)<1>="SPOILAGE"
      CASE JOB.TYPE = "N"
         SCV.REC(9)<1>="NON-CHARGEABLE"
      CASE JOB.TYPE = "C"
         SCV.REC(9)<1>="CHANGE ORDER"
      CASE 1
         SCV.REC(9)<1>="UNKNOWN"
      END CASE
      SCV.REC(10)<1>=JOB.SALES.CODE
      SCV.REC(12)<1>=JOB.CATG
      SCV.REC(13)<1>=CATG.DESC
      SCV.REC(14)<1>=JOB.MASTER
      SCV.REC(25)<1> = ""
      JCNT = COUNT(CUST.TAX.JUR,VM) + (CUST.TAX.JUR # "")
      FOR J = 1 TO JCNT
         IF CUST.TAXABLE<1,J> = "Y" THEN
            IF SCV.REC(25)<1> = "" THEN
               SCV.REC(25)<1> = CUST.TAX.JUR<1,J>
            END ELSE
               SCV.REC(25)<1> = SCV.REC(25)<1>:", ":CUST.TAX.JUR<1,J>
            END
         END
      NEXT J
      SCV.REC(15)<1>=JOB.COLORS
      SCV.REC(16)<1>=JOB.QTY<1,1>
      SCV.REC(17)<1>=JOB.QTY<1,3>
      CNT = COUNT(JOB.COMMENTS,VM)+(JOB.COMMENTS # "")
      FOR C = 1 TO CNT
         SCV.REC(20)<1,C>=JOB.COMMENTS<1,C>
      NEXT C
      CNT = COUNT(JOB.DESC,VM)+(JOB.DESC # "")
      FOR C = 1 TO CNT
         SCV.REC(21)<1,C>=JOB.DESC<1,C>
      NEXT C
      BEGIN CASE
      CASE JOB.STATUS<1,1>=""
         SCV.REC(24)<1>="BOOKED"
      CASE JOB.STATUS<1,1>="9"
         SCV.REC(24)<1>="CANCELLED"
      CASE JOB.STATUS<1,1>="7"
         SCV.REC(24)<1>="READY TO PURGE"
      CASE JOB.STATUS<1,1>="8"
         SCV.REC(24)<1>="PURGED"
      CASE JOB.TRACK.DATE<1,10> # ""
         BEGIN CASE
         CASE JOB.STATUS<1,1> = "1"
            SCV.REC(24)<1> = "IN PROCESS"
         CASE JOB.STATUS<1,1> = "5"
            SCV.REC(24)<1>="REOPENED"
         CASE 1
            SCV.REC(24)<1> = "COMPLETED"
         END CASE
      CASE JOB.TRACK.DATE<1,9> # ""
         BEGIN CASE
         CASE JOB.STATUS<1,1> = "1"
            SCV.REC(24)<1> = "IN PROCESS"
         CASE JOB.STATUS<1,1> = "5"
            SCV.REC(24)<1>="REOPENED"
         CASE 1
            SCV.REC(24)<1> = "INVOICED"
         END CASE
      CASE JOB.TRACK.DATE<1,8> # ""
         SCV.REC(24)<1>="COSTED"
      CASE JOB.TRACK.DATE<1,7> # ""
         SCV.REC(24)<1>="RDY-TO-BILL"
      CASE JOB.TRACK.DATE<1,6> # ""
         SCV.REC(24)<1>="DELIVERED"
      CASE JOB.TRACK.DATE<1,5> # ""
         SCV.REC(24)<1>="IN PROCESS"
      CASE JOB.TRACK.DATE<1,3> # ""
         SCV.REC(24)<1>="IN PROCESS"
      CASE JOB.STATUS<1,1>="1"
         SCV.REC(24)<1>="IN PROCESS"
      CASE JOB.STATUS<1,1>="3"
         SCV.REC(24)<1>="INVOICED"
      CASE 1
         SCV.REC(24)<1>="UNKNOWN"
      END CASE
      ECD.ACTION=3;CALL SCRN.EDIT
      CREF.NO=1;DREF.NO=1
*
*-- GET OPERATOR INPUT
*
200*
      MAT IVC.REC = ""
      IVC.NO = ""
      MODE = ""
      IF MENU = "INVOICE" THEN
         ECD.NUM=18;ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE = "END" THEN GOTO 120
         PRE.BILL = ECD.RET.VALUE
      END ELSE
         PRE.BILL = ""
      END
      ECD.NUM=2;ECD.ACTION = 4;CALL SCRN.EDIT
      BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 120
      CASE ECD.RET.VALUE = "N"
         IF JOB.STATUS<1,1> > 6 THEN
            ERRMSG = "Cannot add new invoices to job because of status"
            GOSUB 91000; GOTO 200
         END
         GOSUB 1000
         MODE = "NEW"
         IVC.NO = ECD.RET.VALUE
      CASE ECD.RET.VALUE = "D"
         ECD.NUM=26;ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "" THEN GOTO 200
         DUP.INVOICE = ECD.RET.VALUE
         MATREAD IVC.REC FROM INVOICE, CONO:DUP.INVOICE ELSE
            ERRMSG = "Cannot locate specified item"
            GOSUB 91000; GOTO 200
         END
*PO.NO=IVC.PO.NO
         IF IVC.JOB.NO # JOB.NO THEN
            ERRMSG = "Cannot duplicate item from another job"
            GOSUB 91000; GOTO 200
         END
         IF DUP.INVOICE[LEN(DUP.INVOICE)-1,2] = "CM" THEN
            N2 = COUNT(IVC.CHG.CODE,VM) + (IVC.CHG.CODE # "")
            FOR N = 1 TO N2
               IF IVC.AMOUNT<1,N>+0 # 0 THEN
                  IVC.AMOUNT<1,N> = IVC.AMOUNT<1,N> * (-1)
               END
               IF IVC.TAXABLE<1,N>+0 # 0 THEN
                  IVC.TAXABLE<1,N> = IVC.TAXABLE<1,N> * (-1)
               END
            NEXT N
         END
         MATREAD CUST.REC FROM CUSTOMER, CONO:IVC.CUST.NO ELSE
            ERRMSG = "Cannot locate customer record - ":IVC.CUST.NO
            GOSUB 91000; GOTO 200
         END
         ECD.NUM=27;ECD.ACTION=4;CALL SCRN.EDIT
         BEGIN CASE
         CASE ECD.RET.VALUE = "END" OR ECD.RET.VALUE = ""
            GOTO 200
         CASE ECD.RET.VALUE = "N"
            SCV.REC(2)<1> = ECD.RET.VALUE; ECD.NUM = 2; ECD.ACTION = 5; CALL SCRN.EDIT
         CASE ECD.RET.VALUE # ""
            ECD.RET.VALUE = STR("0",6-LEN(ECD.RET.VALUE)):ECD.RET.VALUE
            SCV.REC(2)<1> = ECD.RET.VALUE; ECD.NUM = 2; ECD.ACTION = 5; CALL SCRN.EDIT
            BEGIN CASE
            CASE MENU = "CREDIT"
               ECD.RET.VALUE = ECD.RET.VALUE:"CM"
            CASE MENU = "DEBIT"
               ECD.RET.VALUE = ECD.RET.VALUE:"DM"
            CASE PRE.BILL = "Y"
               ECD.RET.VALUE = ECD.RET.VALUE:"PB"
            END CASE
            IF CO.ARS = "Y" THEN
               FND = 1
               READ REC FROM OPEN.RECV, CONO : ECD.RET.VALUE ELSE FND = 0
               IF FND THEN
                  ERRMSG = "Item exists on OPEN.RECV file"; GOSUB 91000
                  GOTO 200
               END
            END
            FND = 1
            READ REC FROM INVOICE, CONO : ECD.RET.VALUE ELSE FND = 0
            IF FND THEN
               ERRMSG = "Item exists on INVOICE file"; GOSUB 91000
               GOTO 200
            END
            FND = 1
            READ REC FROM VOID.INVOICES, CONO : ECD.RET.VALUE ELSE FND = 0
            IF FND THEN
               ERRMSG = "Item exists on VOID INVOICES file"; GOSUB 91000
               GOTO 200
            END
            FND = 1
            READU REC FROM DAILY.INVOICE, CONO : ECD.RET.VALUE ELSE FND = 0
            IF FND THEN
               RELEASE DAILY.INVOICE, CONO : ECD.RET.VALUE
               ERRMSG = "Item exists on DAILY.INVOICE file"; GOSUB 91000
               GOTO 200
            END
         END CASE
         IVC.NO = ECD.RET.VALUE
         IVC.DATE = DATE()
         IVC.PROC.DATE = ""
         IVC.PRT.DATE = ""
         IVC.PROC.MON = ""
         IVC.STATUS = ""
         IVC.LAST = ""
         IVC.WIP.DATE = ""
         IVC.WIP.PRCNT = ""
         IVC.PRE.BILL = ""
         IVC.PRE.BILL.MON = ""
         MODE = "NEW"
      CASE ECD.RET.VALUE # ""
         ECD.RET.VALUE = STR("0",6-LEN(ECD.RET.VALUE)):ECD.RET.VALUE
         SCV.REC(2)<1> = ECD.RET.VALUE; ECD.NUM = 2; ECD.ACTION = 5;CALL SCRN.EDIT
         BEGIN CASE
         CASE MENU = "CREDIT"
            ECD.RET.VALUE = ECD.RET.VALUE:"CM"
         CASE MENU = "DEBIT"
            ECD.RET.VALUE = ECD.RET.VALUE:"DM"
         CASE PRE.BILL = "Y"
            ECD.RET.VALUE = ECD.RET.VALUE:"PB"
         END CASE
         IF CO.ARS = "Y" THEN GOSUB 2000 ELSE GOSUB 3000
         IF MODE = "ERROR" OR MODE = "END" THEN RELEASE DAILY.INVOICE, CONO:ECD.RET.VALUE
         IF MODE = "ERROR" THEN GOTO 200
         IF MODE = "END" THEN GOTO 120
         IF JOB.NO # IVC.JOB.NO THEN
            RELEASE DAILY.INVOICE, CONO:ECD.RET.VALUE
            ERRMSG = "Invoice # ":ECD.RET.VALUE:" is for job # ":IVC.JOB.NO
            GOSUB 91000; GOTO 200
         END
         IVC.NO = ECD.RET.VALUE
      CASE 1
         ERRMSG = "*** Invalid invoice number ***";GOSUB 91000; GOTO 200
      END CASE
*
*-- GET OPERATOR REQUEST
*
300*
      ECD.NUM=22;ECD.ACTION=4;CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
      CASE OPTION = "SC"
         CREF.NO = CREF.NO + LINE.COUNT.C
         IF CREF.NO > COUNT(JOB.COMMENTS,VM)+1 THEN CREF.NO = 1
         ECD.NUM=20;ECD.SUB.NUM=CREF.NO;ECD.ACTION=7;CALL SCRN.EDIT
         GOTO 300
      CASE OPTION = "SD"
         DREF.NO = DREF.NO + LINE.COUNT.D
         IF DREF.NO > COUNT(JOB.DESC,VM)+1 THEN DREF.NO = 1
         ECD.NUM=21;ECD.SUB.NUM=DREF.NO;ECD.ACTION=7;CALL SCRN.EDIT
         GOTO 300
      CASE OPTION = "E" OR OPTION = "END"
         GOTO 120
      END CASE
      SAVE.IVC.LAST=IVC.LAST
      IF PRE.BILL = "Y" THEN
         CALL INVOICE.MAINT.SUB(CONO,JOB.NO,IVC.NO,"PREBILL","")
      END ELSE
         CALL INVOICE.MAINT.SUB(CONO,JOB.NO,IVC.NO,MENU,"")
      END
      RELEASE DAILY.INVOICE, CONO:IVC.NO
      IF CO.SAS = "Y" AND CO.SAS.INVOICE # "N" AND SAVE.IVC.LAST # IVC.LAST AND IVC.LAST = "Y" AND MENU = "INVOICE" THEN
         MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE MAT JOB.REC = ""
         CALL JOB.SALES.MAINT(CONO,JOB.NO,IVC.NO,4,MAT JSS.REC)
      END
      GOTO 100
*
*-- LOAD CUSTOMER INFORMATION
*
1000*
      IVC.JOB.NO = JOB.NO
      IVC.DATE = DATE()
      IF CUST.ACCT = "" THEN
         IVC.CUST.NO = JOB.CUST
      END ELSE
         MAT SAVE.CUST = MAT CUST.REC
         MAST.CUST = CUST.ACCT
         MATREAD CUST.REC FROM CUSTOMER, CONO:MAST.CUST ELSE
            MAT CUST.REC = MAT SAVE.CUST
            IVC.CUST.NO = JOB.CUST
            GOTO 1100
         END
         IVC.CUST.NO = MAST.CUST
      END
1100*
      IVC.CUST.NAME = CUST.NAME
      IVC.ADDR1 = CUST.ADDR1
      IVC.ADDR2 = CUST.ADDR2
      IVC.ADDR3 = CUST.ADDR3
      IVC.ADDR4 = CUST.ADDR4:"  ":CUST.ZIP
      IVC.ATTENTION = CUST.ATTENTION
      RETURN
*
*-- EDIT INVOICE NUMBER ENTERED
*
2000*
      FLAG = ""
      REC = ""
      MATREAD IVC.REC FROM INVOICE, CONO:ECD.RET.VALUE ELSE FLAG<1,1> = "N"
      READ REC FROM DAILY.INVOICE, CONO:ECD.RET.VALUE ELSE FLAG<1,2> = "N"
      READ REC FROM OPEN.RECV, CONO:ECD.RET.VALUE ELSE FLAG<1,3> = "N"
      READ REC FROM VOID.INVOICES, CONO:ECD.RET.VALUE ELSE FLAG<1,4> = "N"
      BEGIN CASE
      CASE FLAG<1,4> # "N"
         ERRMSG = "This is a voided invoice"; GOSUB 91000; MODE = "ERROR"
      CASE FLAG<1,3> # "N" AND FLAG<1,2> = "N" AND FLAG<1,1> = "N"
         ERRMSG = "This is a manual invoice"; GOSUB 91000; MODE = "ERROR"
      CASE FLAG<1,1> # "N" AND FLAG<1,2> = "N" AND FLAG<1,3> # "N"
         MODE = "OLD"
      CASE FLAG<1,1> = "N" AND FLAG<1,3> = "N"
         MODE = ""
         MATREADU IVC.REC FROM DAILY.INVOICE, CONO:ECD.RET.VALUE ELSE
            IF JOB.STATUS<1,1> > 6 THEN
               ERRMSG = "Cannot add new invoices to job because of status"
               GOSUB 91000; MODE = "ERROR"; GOTO 2999
            END
            X = 0; Y = 23; TYP = 18; MAXL = 1; O.R = "R"
            PMSG = "Record is not on file, do you wish to add (Y/N)"
            CALL EDIT.SUB
            IF VALUE = "END" THEN MODE = "END"; GOTO 2999
            IF VALUE = "Y" THEN
               GOSUB 1000
               MODE = "NEW"
            END ELSE
P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               MODE = "ERROR"; GOTO 2999
            END
         END
      CASE 1
         ERRMSG = "*** ERROR ***"; MODE = "ERROR"; GOSUB 91000
      END CASE
2999*
      RETURN
*
*-- ENTER INVOICE NUMBER ENTERED
*
3000*
      MATREAD IVC.REC FROM INVOICE, CONO:ECD.RET.VALUE ELSE
         MATREADU IVC.REC FROM DAILY.INVOICE, CONO:ECD.RET.VALUE ELSE
            IF JOB.STATUS<1,1> > 6 THEN
               ERRMSG = "Cannot add new invoices to job because of status"
               GOSUB 91000; MODE = "ERROR"; GOTO 3999
            END
            X = 0; Y = 23; TYP = 18; MAXL = 1; O.R = "R"
            PMSG = "Record is not on file, do you wish to add (Y/N)"
            CALL EDIT.SUB
            IF VALUE = "END" THEN MODE = "END"; GOTO 3999
            IF VALUE = "Y" THEN
               GOSUB 1000
               MODE = "NEW"
            END ELSE
P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               MODE = "ERROR"; GOTO 3999
            END
         END
      END
3999*
      RETURN
*
*-- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
999999*
*       PRINT @(-1)
   END
