*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - HIST.FNGD.PRT
* DATE        - 03/20/86
* BY          - JIHAD YAMOUT , C.B.A
* DESCRIPTION - This program list history finish goods transaction.
*********************************************************************
*** INSERT EQUATE FROM CPYLIB
*COPY>JCS.CPYLIB>JOB.FNGD
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*** INITIALIZATION
      LINE.COUNT = 99 ; PAGE.NO = 0
      SP1 = SPACE(1)
      T.LINE = ""
      T.LINE = "------ -------- -------- --------------- --------- --- ---- ---- ----------- ----------- -------- "
      HD.LINE = ""
      HD.LINE = "TRAN # JOB NUM  REC DATE PRODUCT NUMBER  UN-PRICE  U/M WHSE LOC# ROLL/SKID #  QTY/ U-M   DIAMETER "
*** SET UP FOR SYSCOM
      SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
*** OPEN FILES
      OPEN '', 'INVENTORY' TO INVENTORY ELSE
         ERRMSG = 'INVENTORY FILE IS MISSING'
         GOTO 93000
      END
      OPEN '', 'JOB.FNGD' TO JOB.FNGD ELSE
         ERRMSG = 'JOB.FNGD FILE IS MISSING'
         GOTO 93000
      END
*** GET PROC VALUES
      PROCREAD ID ELSE
         ERRMSG = ' CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      CONO = ID<1>
      CONO.NAME = ID<2>
      ST.DATE = ID<3>
      EN.DATE = ID<4>
      REPORT.NAME = "FINISHED GOODS RECEIPTS HISTORY REPORT"
      REPORT.NUMBER = "XXXX"
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
*** GET HEADING
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*** PRINTER ON
      PRINTER ON
*** READ AND PROCESS ALL THE FILES
      DATA = 0
      LOOP
         READNEXT KEY ELSE DATA = 1
      WHILE DATA = 0 DO
         MATREAD JFG.REC FROM JOB.FNGD , KEY ELSE
            MAT JFG.REC = "" ; GOTO 111
         END
         GOSUB 3000
111*
      REPEAT
*** END OF JOB
      PRINTER OFF
      GOTO 999999
*** PRINT HEADINGS
1000*
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT SPACE(53):"FROM ":ST.DATE "L#8":" TO ":EN.DATE "L#8"
      PRINT
      PRINT HD.LINE
      PRINT T.LINE
      LINE.COUNT = 5
      RETURN
***   PRINT THE RECORD
3000*
      MATREAD INV.REC FROM INVENTORY, CONO:JFG.PROD ELSE MAT INV.REC = ""
      GOSUB 6000
      LOC.CNT = COUNT(JFG.LOC, VM) + (JFG.LOC # "")
      FOR PR1 = 1 TO LOC.CNT
         IF LINE.COUNT > 53 THEN
            PRT.FLG = 1 ; GOSUB 1000
         END
         IF PRT.FLG = 1 OR PR1 = 1 THEN
            H.LINE = KEY[4,6] "R#6":SP1
            H.LINE = H.LINE :JFG.JOB "L#8":SP1
            H.LINE = H.LINE :OCONV(JFG.DATE, "D2/") "R#8":SP1
            H.LINE = H.LINE :JFG.PROD "L#15":SP1
            H.LINE = H.LINE :OCONV(JFG.UN.PRICE , "MD4") "R#9":SP1
            H.LINE = H.LINE :INV.UNIT<1,2> "L#3":SP1
            H.LINE = H.LINE :JFG.WHSE "L#4":SP1
            H.LINE = H.LINE :JFG.LOC<1,PR1> "L#4":SP1
            PRT.FLG = 0
         END ELSE
            H.LINE = SPACE(60)
            IF JFG.LOC<1,PR1> # JFG.LOC<1,PR1-1> THEN
               H.LINE = H.LINE :JFG.LOC<1,PR1> "L#4":SP1
            END ELSE
               H.LINE = H.LINE :SPACE(5)
            END
         END
         H.LINE = H.LINE :JFG.R.S.ID<1,PR1> "L#11":SP1
         IF JFG.QTY<1,PR1> < 0 THEN
            ADDQ = "-.5"
         END ELSE
            ADDQ = .5
         END
         PRT.QTY = INT(((JFG.QTY<1,PR1> / ICR.DV1) * ICR.MT1) / ICR.DV2 + ADDQ)
         H.LINE = H.LINE :OCONV(PRT.QTY, ICR.CNV) "R#11":SP1
         H.LINE = H.LINE :OCONV(JFG.DIAM<1,PR1> , "MD2") "R#8":SP1
         PRINT H.LINE
         LINE.COUNT = LINE.COUNT + 1
      NEXT PR1
      IF LINE.COUNT + 1 < 53 THEN
         PRINT
         LINE.COUNT = LINE.COUNT + 1
      END
      RETURN
**** PRINT SUB LINE
4000*
      RETURN
***        CONVERT SHEET/WGHT/QTY
6000*
      MAT INV.CNV.REC = ''
      BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2"
         ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
      END CASE
      RETURN
***   CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      PRINTER OFF
      ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999*
   END
