      SUBROUTINE JOB_GNXRF_TU(MAT GEN.XREF.REC,PREFIX)
*COPY>CPYLIB>COM3
*******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC-JCS
* SOURCE      - JCSBP
* PROGRAM     - JOB.GEN.XREF
* BY          - WALID YAMOUT, C.B.A
* DATE        - 08/06/86
* DESCRIPTION -
* COMMENT     - REVISED JOB.STATUS TO ACCEPT "5" - "REOPENED"
* TASK #12907 (RRG)
*T25024 diane 05/02/2000 * Update with version
*ENDDOC
*******************************************************
*COPY>CPYLIB>GEN.XREF
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>TCC
*COPY>CPYLIB>ROC.H
*COPY>CPYLIB>SPECIAL.H
*
*
      GXR.ACTION = ""
      DESC.LEN = 50
      IF GXR.SRCH.ID = '' THEN
         GXR.ACTION = 'X'
         GOTO 99999
      END ELSE
         DISP.CNT = 3
      END
      TOP.CNT = COUNT(GXR.TOP.LINE, VM) + (GXR.TOP.LINE # "")
      SCRN.SIZE = 17 - TOP.CNT
      BEG.SCRN = 3 + TOP.CNT
      END.SCRN = 20
*
      MATREAD CUST.REC FROM GXR.XREF, GXR.CO : GXR.SRCH.ID ELSE MAT CUST.REC = ""
      IF CUST.JOB = "" THEN
         GXR.ACTION = 'X'
         GOTO 99999
      END
      REC.CNT = COUNT(CUST.JOB,VM) + 1
*
      TITLE = GXR.TOP.LINE<1,1>
      WIDTH = 8 / 2
      WIDTH<1,2> = DESC.LEN / 2
      WIDTH<1,3> = 14 / 2
      IF REC.CNT > SCRN.SIZE THEN
         DEPTH = SCRN.SIZE
      END ELSE
         DEPTH = REC.CNT
      END
      XYPOS = 0; XYPOS<1,2> = 0
      ILIST = ""
      FOR PL = 1 TO REC.CNT
         JOBNO = CUST.JOB<1,PL>
         MATREAD JOB.REC FROM GXR.FILE, GXR.CO : JOBNO ELSE
            MAT JOB.REC = ""
            JOB.STATUS = STR("?",13)
         END
         ILIST<1,PL> = JOBNO
         ILIST<1,PL,2> = JOB.DESC<1,1>
         STATUS = ""
         BEGIN CASE
            CASE JOB.STATUS<1,1> = ""
               STATUS = "BOOKED"
            CASE JOB.STATUS<1,1> = "7"
               STATUS = "READY TO PURGE"
            CASE JOB.STATUS<1,1> = "8"
               STATUS = "WAS PURGED"
            CASE JOB.STATUS<1,1> = "9"
               STATUS = "CANCELLED"
            CASE JOB.TRACK.DATE<1,10> # ""
               BEGIN CASE
                  CASE JOB.STATUS<1,1> = "1"
                     STATUS = "IN PROCESS"
                  CASE JOB.STATUS<1,1> = "5"
                     STATUS="REOPENED"
                  CASE 1
                     STATUS = "COMPLETED"
               END CASE
            CASE JOB.TRACK.DATE<1,9> # ""
               BEGIN CASE
                  CASE JOB.STATUS<1,1> = "1"
                     STATUS = "IN PROCESS"
                  CASE JOB.STATUS<1,1> = "5"
                     STATUS="REOPENED"
                  CASE 1
                     STATUS = "INVOICED"
               END CASE
            CASE JOB.TRACK.DATE<1,8> # ""
               STATUS = "COSTED"
            CASE JOB.TRACK.DATE<1,7> # ""
               STATUS = "RDY-TO-BILL"
            CASE JOB.TRACK.DATE<1,6> # ""
               STATUS = "DELIVERED"
            CASE JOB.TRACK.DATE<1,5> # ""
               STATUS = "IN PROCESS"
            CASE JOB.TRACK.DATE<1,3> # ""
               STATUS = "IN PROCESS"
            CASE JOB.STATUS<1,1> = "1"
               STATUS = "IN PROCESS"
            CASE 1
               STATUS = JOB.STATUS<1,1>
         END CASE
         ILIST<1,PL,3> = STATUS
      NEXT PL
      BKGRND = ""; FRGRND = ""
      OPT = "M"; VALUE = ""; ERROR = ""
      TU_FUNC = "TU.FORM.SELECTLIST":TU_VERNO     ;*T25024
      CALL @TU_FUNC(TITLE,ILIST,WIDTH,DEPTH,XYPOS,BKGRND,FRGRND,OPT,VALUE,ERROR)     ;*T25024
      OPT = VALUE<1,1>
      IF NUM(OPT) THEN
         IF OPT > 0 AND OPT <= REC.CNT THEN
            GXR.ID = CUST.JOB<1,OPT>
         END ELSE
            GXR.ID = ""
         END
      END ELSE
         GXR.ID = ""
      END
99999*
      RETURN
   END
