SUBROUTINE ADC.SCROLL(MAT DT.REC,RET.VALUE)
*COPY>CPYLIB>COM1
********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - ADC.SCROLL
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 11/16/84
* DESCRIPTION - 
* This subroutine displays all the Division, Department, Cost Center
* or Operation numbers and descriptions then returns the choice to
* ADC.TIME.ENTRY.
*ENDDOC
********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- DIMENTION DISPLAY TABLE
*
 DIM DT.REC(2)
 EQU DT.NO   TO DT.REC(1)
 EQU DT.DESC TO DT.REC(2)
*
*---- INITIALIZATION
*
BEGIN.PAGE = 4
PAGE.SIZE = 16
LINE.SPACE = 1
LINES = 0
OLD.START.LINE=0
*MAT EDIT.COM=''
*
*---- SETUP FOR SYSTEM ERRMSGS
*
     SYS.TYPE = 1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- MAIN PROCESSING
*
100*
     LINES = COUNT(DT.NO ,VM) + (DT.NO # "")
     LN = 1
     IF LINES > 0 THEN
        GOSUB 200
     END
MORE = 1
LOOP
     X=18;Y=21;MAXL=3;O.R='O';TYP=1;VALDAT="S":VM:"E"
     FOR L=1 TO LINES
        VALDAT=VALDAT:VM:L
     NEXT L
     CALL EDIT.SUB
     SELECT=VALUE
     BEGIN CASE
        CASE SELECT = '' OR SELECT = 'END' OR SELECT = 'E'
           MORE = 0
           RET.VALUE<1,1>='';RET.VALUE<1,2>=''
        CASE SELECT = 'S'
           LN = LN + (PAGE.SIZE * 2)
           IF LN > LINES THEN LN = 1
           GOSUB 200
        CASE NUM(SELECT)
           RET.VALUE<1,1>=DT.NO<1,SELECT>
           RET.VALUE<1,2>=DT.DESC<1,SELECT>
           MORE=0
        CASE 1
           ERRMSG="INVALID SELECTION";GOSUB 91000
     END CASE
     WHILE MORE DO REPEAT
     FOR M=4 TO 19
P_X  = 0 ; P_Y = M ; P_VALUE = "" ; P_OPT = "CL"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
     NEXT M
     GOTO 99999
*
*---- SCROLL ROUTINE
*
200*
      START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
      IF START.LINE=OLD.START.LINE THEN GOTO 201
      OLD.START.LINE=START.LINE
      FOR M=4 TO 19
P_X  = 0 ; P_Y = M ; P_VALUE = "" ; P_OPT = "CL"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT M
      LAST.LINE=START.LINE + PAGE.SIZE - 1
      FOR N=START.LINE TO LAST.LINE UNTIL N > LINES
         SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:DT.NO<1,N> "L#5"
P_X  := AM:10 ; P_Y := AM:SLN ; P_VALUE := AM:DT.DESC<1,N> "L#29"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT N
      IF LINES > 16 THEN
         START.LINE=LAST.LINE + 1 ; LAST.LINE=START.LINE + PAGE.SIZE - 1
         FOR N=START.LINE TO LAST.LINE UNTIL N > LINES
            SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
P_X  = 40 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
P_X  := AM:44 ; P_Y := AM:SLN ; P_VALUE := AM:DT.NO<1,N>"L#5"
P_X  := AM:50 ; P_Y := AM:SLN ; P_VALUE := AM:DT.DESC<1,N>"L#29"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         NEXT N
      END
201   RETURN
*
*---- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 RETURN
      END
