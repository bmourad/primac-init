*COPY>CPYLIB>COM1
****************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.DETAIL.RPT.SHIP
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 10/19/84
* DESCRIPTION -
* This program produces a Job Shipping Detail report.
****************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>SHIP.VIA
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM A MENU";GOSUB 91000;GOTO 99999
     CS.FLG = ID<5>
*
*---- DIMENSION RECORDS
*
     DIM SUB.TOTAL.REC(5)
     DIM DEPT.TOTAL.REC(5)
     DIM GRAND.TOTAL.REC(5)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','SHIP.VIA' TO SHIP.VIA ELSE ERRMSG='SHIP.VIA FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     RPT.NO="XXXX"
     UNKNOWN=STR("?",30)
     MAT SUB.TOTAL.REC=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     PREVIOUS.DEPT=''
     PREVIOUS.CCTR=''
     PREVIOUS.OPER=''
     DATE.LAST.ENTRY=''
     DATE.ITEMS.THROUGH=''
     FIRST.FLAG=1
     NODATA=1
     REC.CNT=0
     NEW.DESC=''
     NEW.LEN=30
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE 
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.LINE.TYPE="P" OR JCR.LINE.TYPE="M" THEN GOTO 10
     IF FIELD(JCR.KEY,"*",4) = "EST" THEN GOTO 10
     NODATA=0
     JOB.NO=JCR.JOB
     DEPT.NO=JCR.DEPT
     IF FIRST.FLAG=1 THEN GOSUB 15
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        OPER.NO=JCR.OPER<1,I>
        VIA=JCR.VIA<1,I>
        NAME=JCR.NAME<1,I>
        ADDR=JCR.ADDR<1,I>
        FRT.NO=JCR.FRT.NO<1,I>
        INV.NO=JCR.INV<1,I>
        INV.DATE=JCR.INV.DATE<1,I>
        DATE=JCR.DATE<1,I>
        OLD.DESC=JCR.DESC<1,I>
        QTY=JCR.QTY<1,I>
        IF CS.FLG = "S" THEN
           AMT=JCR.SALE<1,I>
        END ELSE
           AMT=JCR.COST<1,I>
        END
        IF FIRST.FLAG=1 THEN GOTO 11
        IF PREVIOUS.DEPT # DEPT.NO THEN
           GOSUB 50
           GOSUB 60
           GOSUB 20
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.CCTR # CCTR.NO THEN
           GOSUB 50
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.OPER # OPER.NO THEN
           GOSUB 50
           PREVIOUS.OPER=OPER.NO
        END
11*
        GOSUB 30
        GOSUB 40
        IF FIRST.FLAG=1 THEN
           FIRST.FLAG=0
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
        END
     NEXT I
     GOTO 10
12*
     IF REC.CNT <=1 THEN GOTO 13
     GOSUB 50
     GOSUB 60
     GOSUB 70
13*
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFO FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     D.CNT=COUNT(JOB.SP.DATE,VM) + (JOB.SP.DATE # '')
     FOR T=1 TO D.CNT
        IF JOB.SP.DATE<1,T,1> > DATE.LAST.ENTRY THEN DATE.LAST.ENTRY=JOB.SP.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     T=0;D.CNT=0
     D.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR T=1 TO D.CNT
        IF DATE.TBL<1,T,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,T,1>
     NEXT T
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     LINE.CNT=0
     PG.NO=PG.NO + 1
     H.LINE=''; HLINE1=''; HLINE2=''; HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     N=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=N:CO.NAME:N
     HLINE3=SPACE(13):OCONV(DATE(),"D2/")"R#8":SPACE(12):"PAGE: ":PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.CUST"L#8":SPACE(40):'D E T A I L   R E P O R T (SHIPPING)':SPACE(26):'#':JOB.NO "L#8":" ":CONSOL
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:CUST.NAME"L#30":SPACE(72):CUST.NAME
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.QTY<1,1> "L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.DESC<1,1> "L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.ENTRY,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>"L#30"
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'SM: ':SALS.NAME "L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT H.LINE
     PRINT
     MATREAD DEPT.REC FROM DEPARTMENT,CONO:JCR.DEPT ELSE DEPT.DESC=UNKNOWN
     H.LINE=''
     H.LINE=H.LINE:"DEPARTMENT: ":JCR.DEPT"L#4":"  ":DEPT.DESC"L#20"
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:SPACE(74):"SHIP"
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:"CCTR OPER":SPACE(12):"SHIP-TO":SPACE(22):"DESCRIPTION":SPACE(13):"DATE       CARRIER/FRT #":SPACE(6):"INVOICE #  QUANTITY  AMOUNT"
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:"---- ---- ------------------------------ ------------------------------ -------- ---------------------- ---------- -------- --------"
     PRINT H.LINE
     LINE.CNT=LINE.CNT + 11
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 35
     P.LINE=''
     P.LINE=CCTR.NO"L#4":' ':OPER.NO"L#4":' ':NAME"L#30":' ':NEW.DESC<1,1>"L#30":' ':DATE"R#8":' ':SHPV.DESC"L#22":' ':INV.NO"L#8":'   ':QTY"R#8":' ':AMT"R#8"
     PRINT P.LINE
     LINE.CNT=LINE.CNT + 1
     RETURN
*
*---- INCREMENT VALUES
*
35*
     CALL DESC.REFORMAT(OLD.DESC,NEW.LEN,NEW.DESC)
     MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:VIA ELSE SHPV.DESC=UNKNOWN
     SUB.TOTAL.REC(1)=SUB.TOTAL.REC(1) + QTY
     SUB.TOTAL.REC(2)=SUB.TOTAL.REC(2) + AMT
     DEPT.TOTAL.REC(1)=DEPT.TOTAL.REC(1) + QTY
     DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + AMT
     GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + QTY
     GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + AMT
     DATE=OCONV(DATE,"D2-")
     AMT=OCONV(AMT,"MD2")
     QTY=OCONV(QTY,"MD0")
     RETURN
*
*---- PRINT 2ND LINE
*
40*
     INV.DATE=OCONV(INV.DATE,"D2-")
     P.LINE=''
     P.LINE=SPACE(10):ADDR"L#30":' ':NEW.DESC<1,2>"L#30":SPACE(10):FRT.NO"L#22":'  ':INV.DATE"R#8"
     PRINT P.LINE
     LINE.CNT=LINE.CNT + 1
     REC.CNT=REC.CNT + 1
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT SUB.TOTALS
*
50*
     MATREAD OPER.REC FROM OPERATION,CONO:PREVIOUS.OPER ELSE OPER.DESC=UNKNOWN
     S.LINE=''
     S.LINE=SPACE(115):'-------- --------'
     PRINT S.LINE
     S.LINE=''
     S.LINE='* ':OPER.DESC"L#22":' TOTAL'
     S.LINE=S.LINE:SPACE(85):OCONV(SUB.TOTAL.REC(1),"MD0")"R#8":' ':OCONV(SUB.TOTAL.REC(2),"MD2")"R#8"
     PRINT S.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT SUB.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     D.LINE=''
     D.LINE=SPACE(115):'-------- --------'
     PRINT D.LINE
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE"L#40"
     D.LINE=D.LINE:SPACE(75):OCONV(DEPT.TOTAL.REC(1),"MD0")"R#8":' ':OCONV(DEPT.TOTAL.REC(2),"MD2")"R#8"
     PRINT D.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT GRAND TOTALS
*
70*
     G.LINE=''
     G.LINE=SPACE(115):'-------- --------'
     PRINT G.LINE
     G.LINE=''
     G.LINE="*** GRAND TOTALS ***":SPACE(95):OCONV(GRAND.TOTAL.REC(1),"MD0")"R#8":' ':OCONV(GRAND.TOTAL.REC(2),"MD2")"R#8"
     PRINT G.LINE
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
