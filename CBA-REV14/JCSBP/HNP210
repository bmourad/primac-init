      SUBROUTINE HNP210 (FILE.NO, FILE.KEY, PTR, MSG, STATUS, SF.PROMPT)
*********************************************************************
*
* REVISION - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - HNP210
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/27/87
*
* REVISED  - 11/30/89
*
* DESCRIPTION
*
* This subroutine will read the specified file for specified key,
* format and transmit the data to the Primac Network Processor.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JCS.CPYLIB>COM.PNP
*COPY>JCS.CPYLIB>PNP.LOAD
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.CUTOFF.NO
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INVENTORY
*COPY>JCS.CPYLIB>SF.PROMPT
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      STATUS = ""
*
*---- MAIN PROCESSING
*
100*
      BEGIN CASE
         CASE FILE.NO = "1"
            CMP.KEY = FILE.KEY
            MATREAD COMP.REC FROM COMPANY, CMP.KEY ELSE GOTO 90000
            REC1.ID = CMP.KEY"L#3"
            REC1.DESC = CO.NAME"L#30"
            TRN = "01"
            CNT = 2
         CASE FILE.NO = "2"
            DIV.KEY = FILE.KEY
            MATREAD DIV.REC FROM DIVISION, DIV.KEY ELSE GOTO 90000
            CONO = DIV.KEY[1,3]
            DIV = DIV.KEY[4,99]
            REC2.ID = (CONO:DIV)"L#5"
            REC2.DESC = DIV.DESC"L#30"
            TRN = "02"
            CNT = 2
         CASE FILE.NO = "3"
            DEPT.KEY = FILE.KEY
            MATREAD DEPT.REC FROM DEPARTMENT, DEPT.KEY ELSE GOTO 90000
            CONO = DEPT.KEY[1,3]
            DIV = DEPT.DIV<1,PTR>
            IF DIV = "" THEN GOTO 90000
            DEPT = DEPT.KEY[4,99]
            IF DEPT = "" THEN GOTO 90000
            REC3.ID = (CONO:DIV:DEPT)"L#10"
            REC3.DESC = DEPT.DESC"L#20"
            TRN = "03"
            CNT = 2
         CASE FILE.NO = "4"
            CCTR.KEY = FILE.KEY
            MATREAD CCTR.REC FROM COST.CNTR, CCTR.KEY ELSE GOTO 90000
            CONO = CCTR.KEY[1,3]
            CCTR = CCTR.KEY[4,99]
            IF CCTR = "" THEN GOTO 90000
            IF CCTR.TYPE = "F" THEN GOTO 90000
            REC4.ID = (CONO:CCTR)"L#6"
            REC4.DESC = CCTR.DESC"L#30"
            REC4.DEPT = CCTR.DEPT"L#5"
            TRN = "04"
            CNT = 3
         CASE FILE.NO = "5"
            OPER.KEY = FILE.KEY
            MATREAD OPER.REC FROM OPERATION, OPER.KEY ELSE GOTO 90000
            CONO = OPER.KEY[1,3]
            OPER = OPER.KEY[4,99]
            IF OPER = "" THEN GOTO 90000
            REC5.ID = (CONO:OPER)"L#7"
            REC5.DESC = OPER.DESC"L#30"
            REC5.MR = OPER.MATL.REQ"L#1"
            REC5.CTYPE = OPER.CTYPE"L#1"
            REC5.PROMPT = OPER.PROMPT"L#15"
            REC5.SUB1 = """L#4"
            REC5.SUB2 = """L#4"
            REC5.SUB3 = """L#4"
            REC5.SUB4 = """L#4"
            REC5.SUB5 = """L#4"
            REC5.CPROMPT = """L#15"
            REC5.SFP = ""
            IF NUM(OPER.SF.PROMPT<1,1>) THEN
               PCODES = OPER.SF.PROMPT
            END ELSE
               PCODES = ""
            END
            FOR N = 1 TO 10
               REC5.SFP = REC5.SFP : PCODES<1,N>"L#2"
            NEXT N
            TRN = "05"
            CNT = 12
         CASE FILE.NO = "6"
            CCOP.KEY = FILE.KEY
            MATREAD CCTR.REC FROM COST.CNTR, CCOP.KEY ELSE GOTO 90000
            CONO = CCOP.KEY[1,3]
            CCTR = CCOP.KEY[4,99]
            IF CCTR = "" THEN GOTO 90000
            OPER = CCTR.OPER<1,PTR>
            IF OPER = "" THEN GOTO 90000
            TYPE = CCTR.OPER.TYPE<1,PTR>
            REC6.ID = (CONO:CCTR:OPER)"L#10"
            REC6.TYPE = TYPE"L#1"
            REC6.PROMPT = """L#15"
            TRN = "06"
            CNT = 3
         CASE FILE.NO = "7"
            EMP.KEY = FILE.KEY
            MATREAD EMP.REC FROM EMPLOYEE, EMP.KEY ELSE GOTO 90000
            IF EMP.TERM.DATE # "" THEN GOTO 90000
            CONO = EMP.KEY[1,3]
            EMP = EMP.KEY[4,99]
            IF EMP = "" THEN GOTO 90000
            REC7.ID = (CONO:EMP)"L#9"
            REC7.DESC = (TRIM(EMP.FRST.NAME:" ":EMP.LAST.NAME))"L#30"
            REC7.SFT = EMP.SHIFT"L#1"
            REC7.DEPT = EMP.DEPT"L#5"
            BDAY = OCONV(EMP.BIRTHDAY,"D2-")
            BDAY = BDAY[7,2]:BDAY[1,2]:BDAY[4,2]
            REC7.BDAY = BDAY"L#6"
            REC7.PWD = """L#8"
            REC7.AUTH = "111000"
            TRN = "07"
            CNT = 7
         CASE FILE.NO = "8"
            JOB.KEY = FILE.KEY
            CONO = JOB.KEY[1,3]
            JOB.NO = JOB.KEY[4,99]
            IF JOB.NO = "" THEN GOTO 90000
            OUTJOB = 0
            MATREAD JOB.REC FROM JOB, JOB.KEY ELSE
               MAT JOB.REC = ""
               GOSUB 30000
               IF NOT(OUTJOB) THEN GOTO 90000
            END
            IF OUTJOB THEN
               JDESC = JOB.NO
               JOB.TYPE = "R"
            END ELSE
               IF JOB.STATUS<1,1> > "5" THEN GOTO 90000
               MATREAD COMP.REC FROM COMPANY, CONO ELSE
                  MAT COMP.REC = ""
               END
               IF CO.JOB.CUST.FLG = "C" THEN
                  MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE GOTO 90000
                  JDESC = CUST.NAME
               END ELSE
                  JDESC = JOB.DESC<1,1>
               END
            END
            IF JDESC = "" THEN JDESC = JOB.NO
            REC8.ID = (CONO:JOB.NO)"L#11"
            REC8.DESC = JDESC"L#40"
            REC8.DIV = JOB.DIV"L#2"
            REC8.TYPE = JOB.TYPE"L#1"
            TRN = "08"
            CNT = 4
         CASE FILE.NO = "9"
            INV.KEY = FILE.KEY
            MATREAD INV.REC FROM INVENTORY, INV.KEY ELSE GOTO 90000
            CONO = INV.KEY[1,3]
            PROD = INV.KEY[4,99]
            REC9.ID = (CONO:PROD)"L#18"
            REC9.DESC = INV.DESC"L#30"
            REC9.TYPE = INV.PAP.TYPE"L#8"
            TRN = "09"
            CNT = 3
         CASE FILE.NO = "10"
            SPL.KEY = FILE.KEY
            READ SPL.DESC FROM SPOILAGE.CODES, SPL.KEY ELSE GOTO 90000
            CONO = SPL.KEY[1,3]
            SPCODE = SPL.KEY[4,99]
            REC10.ID = (CONO:SPCODE)"L#11"
            REC10.DESC = SPL.DESC"L#30"
            TRN = "10"
            CNT = 2
         CASE FILE.NO = "11"
            SFP.KEY = FILE.KEY
            MATREAD SFP.REC FROM SF.PROMPT,SFP.KEY ELSE GOTO 90000
            CONO = SFP.KEY[1,3]
            SFPCODE = SFP.KEY[4,99]
            REC11.ID = (CONO:SFPCODE)"L#5"
            REC11.DESC = SFP.PROMPT"L#20"
            TRN = "11"
            CNT = 2
      END CASE
*
*---- FORMAT TRANSMISSION DATA
*
20000*
      MSG = TRN
      FOR N = 1 TO CNT
         MSG = MSG:REC(N)
      NEXT N
      GOTO 99999
*
*---- CHECK JOB CUTOFF
*
30000*
      MATREAD JOB.CUTOFF.REC FROM CONTROL, CONO:"JOB.CUTOFF.NO" ELSE
         MAT JOB.CUTOFF.REC = ""
         J.CUTOFF.NUM = 0
      END
      IF JOB.NO < J.CUTOFF.NUM THEN
         OUTJOB = 1
      END ELSE
         JCCNT = DCOUNT(J.CUTOFF.PREFIX,VM)
         FOR JCPTR = 1 TO JCCNT WHILE OUTJOB = 0
            PLEN = LEN(J.CUTOFF.PREFIX<1,JCPTR>)
            IF J.CUTOFF.PREFIX<1,L> = JOB.NO[1,PLEN] THEN
               JSFX = JOB.NO[PLEN+1,LEN(JOB.NO)-PLEN]
               IF NUM(JSFX) THEN
                  IF JSFX < J.CUTOFF.PRE.NO<1,JCPTR> THEN OUTJOB = 1
               END
            END
         NEXT JCPTR
      END
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      STATUS = "E"
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
