  SUBROUTINE OUTSIDE.PO.XREF(CONO,JOB.NO,DESC,ID.LIST,PO.NUM,PO.LINE.NO)
*COPY>CPYLIB>COM1
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ADPXBP
* PROGRAM     - OUTSIDE.PO.XREF
* BY          - cls; CBA
* DATE        - 04/11/1997
* DESCRIPTION -
*
*T21692 cls 04/11/1997 * NEW PROG TO DISPLAY ALL THE LINE ITEMS ON A PO
*                        WITH TH ASSOCIATED JOB #
*T21177 diane 06/05/1998 * CSF 31102 CLOSE FORM
*T25978 adelgado 02/14/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
**********************************************
*   CPYLIBS
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*
*   FILE OPENS
*
  OPEN "OUTSIDE.PO" TO OUTSIDE.PO ELSE 
    ERRMSG = "CANNOT OPEN OUTSIDE.PO FILE"
    GOSUB 91000
  END
*
*   INITIALIZATION SECTION
  ECD.SCRN.NO = 2
  ECD.ACTION = 2; CALL SCRN.EDIT
*
  ECD.NUM = 9
  SCV.REC(9)<ECD.SCRN.NO> = JOB.NO
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.NUM = 10
  SCV.REC(10)<ECD.SCRN.NO> = DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
  CURR.REF.NO = ''
  REF.NO = ''
  LINE.COUNT = 14
*
  PONUM = ''
  VENDNUM = ''
  QTY = ''
  CATG = ''
  ESTCOST = ''
  DATEREC = ''
  LINENO = ''
  REF = ''
*
*
  REF.CNT = DCOUNT(ID.LIST,VM)
  PTR = 0
  FOR X = 1 TO DCOUNT(ID.LIST,VM)
    MATREAD OPO.REC FROM OUTSIDE.PO,CONO:ID.LIST<1,X> THEN
      FOR Z = 1 TO DCOUNT(OPO.JOB.NO,VM)
        IF OPO.JOB.NO<1,Z> = JOB.NO THEN
          PTR += 1
          PONUM<1,PTR> = ID.LIST<1,X>
          VENDNUM<1,PTR> = OPO.VEND.NO
          QTY<1,PTR> = OPO.QTY<1,Z>
          CATG<1,PTR> = OPO.PROD.LINE<1,Z>
          ESTCOST<1,PTR> = OPO.EST.COST<1,Z>
          DATEREC<1,PTR> = OPO.DATE.RECVD<1,Z>
          LINENO<1,PTR> = Z
          REF<1,PTR> = PTR
        END
      NEXT Z
    END
  NEXT X
*
  SCV.REC(1)<ECD.SCRN.NO> = REF
  SCV.REC(2)<ECD.SCRN.NO> = PONUM
  SCV.REC(3)<ECD.SCRN.NO> = VENDNUM
  SCV.REC(4)<ECD.SCRN.NO> = QTY
  SCV.REC(6)<ECD.SCRN.NO> = CATG
  SCV.REC(5)<ECD.SCRN.NO> = ESTCOST
  SCV.REC(7)<ECD.SCRN.NO> = DATEREC
*
*
  GOSUB 10000
  GOSUB 5000
  GOTO 99999
*
*
10000*
  START.REF.NO=1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO=CURR.REF.NO THEN RETURN
  CURR.REF.NO=START.REF.NO
  ECD.NUM=1;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=2;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=3;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=4;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=5;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=6;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=7;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
  RETURN
*
5000*
  ECD.NUM = 8; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "E"
      GOTO 99999
    CASE ECD.RET.VALUE = "S"
      REF.NO=CURR.REF.NO + LINE.COUNT
      IF REF.NO > REF.CNT THEN REF.NO=1
      GOSUB 10000
    * T25978 v
    CASE ECD.RET.VALUE = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 10000
    CASE ECD.RET.VALUE = 'ST'
      REF.NO = 1
      GOSUB 10000
    CASE ECD.RET.VALUE = 'SB'
      REF.NO = REF.CNT
      GOSUB 10000
    * T25978 ^
    CASE NUM(ECD.RET.VALUE) AND ECD.RET.VALUE LE DCOUNT(PONUM,VM)
      PO.NUM = PONUM<1,ECD.RET.VALUE>
      PO.LINE.NO = LINENO<1,ECD.RET.VALUE>
      GOTO 99999
  END CASE
  GOTO 5000
*
  RETURN
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23):ERRMSG:CL:
* INPUT XX:
* PRINT @(0,23):CL:
* RETURN
99999 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
  RETURN
END
