*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.MISC.POSTED
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 07/17/84
* DESCRIPTION -
* This program produces a Job Miscellaneous Posted report.
*************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM A MENU";GOSUB 91000;GOTO 99999
     CS.FLG = ID<5>
*
*---- DIMENSION RECORDS
*
     DIM MISC.TBL(25)
     DIM DEPT.TOTAL.REC(20)
     DIM GRAND.TOTAL.REC(20)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     RPT.NO="XXXX"
     UNKNOWN=STR("?",30)
     MAT MISC.TBL=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     PREVIOUS.DEPT.NO=''
     PREVIOUS.DEPT=''
     PREVIOUS.CCTR=''
     PREVIOUS.OPER=''
     CCTR.NUMBER=''
     K=''
     DATE.LAST.ENTRY=''
     DATE.ITEMS.THROUGH=''
     FIRST.FLAG=1
     NODATA=1
     MAT JCR.REC=''
     MAT JOB.REC=''
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'MSC' THEN GOTO 10
     IF JCR.LINE.TYPE # "D" THEN GOTO 10
     NODATA=0
     TYPE=FIELD(JCR.KEY,"*",4)
     JOB.NO=JCR.JOB
     DEPT.NO=JCR.DEPT
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     I=0
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        CCTR.NUMBER=CCTR.NO
        OPER.NO=JCR.OPER<1,I>
        IF PREVIOUS.CCTR # CCTR.NUMBER THEN
           CCTR.NO="1*":CCTR.NO
           K=1
        END ELSE
           IF PREVIOUS.OPER # OPER.NO THEN
              K=K + 1
              CCTR.NO=K:"*":CCTR.NO
           END ELSE
              CCTR.NO=K:"*":CCTR.NO
           END
        END
        TRANS.DATE=JCR.DATE<1,I>
        IF TYPE="EST" THEN
           IF CS.FLG = "S" THEN
              E.REG.COST=JCR.SALE<1,I,1>
              E.OT.COST=JCR.SALE<1,I,2>
           END ELSE
              E.REG.COST=JCR.COST<1,I,1>
              E.OT.COST=JCR.COST<1,I,2>
           END
           E.TTL.COST=E.REG.COST + E.OT.COST
           A.REG.COST=''
           A.OT.COST=''
           A.DT.COST=''
           A.TTL.COST=''
        END ELSE
           E.REG.COST=''
           E.OT.COST=''
           E.TTL.COST=''
           IF CS.FLG = "S" THEN
              A.REG.COST=JCR.SALE<1,I,1>
              A.OT.COST=JCR.SALE<1,I,2>
              A.DT.COST=JCR.SALE<1,I,3>
           END ELSE
              A.REG.COST=JCR.COST<1,I,1>
              A.OT.COST=JCR.COST<1,I,2>
              A.DT.COST=JCR.COST<1,I,3>
           END
           A.TTL.COST=A.REG.COST + A.OT.COST + A.DT.COST
        END
        GOSUB 30
        PREVIOUS.CCTR=CCTR.NUMBER
        PREVIOUS.OPER=OPER.NO
     NEXT I
     GOTO 10
12*
     PRINTER ON
     GOSUB 15
     D.CNT=COUNT(MISC.TBL(1),VM) + (MISC.TBL(1) # '')
     FOR Y=1 TO D.CNT
        DEPT.NO=MISC.TBL(1)<1,Y>
        CCTR.CNT=COUNT(MISC.TBL(2)<1,Y>,SVM) + (MISC.TBL(2)<1,Y> # '')
        FOR Z=1 TO CCTR.CNT
           DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + MISC.TBL(5)<1,Y,Z>
           DEPT.TOTAL.REC(3)=DEPT.TOTAL.REC(3) + MISC.TBL(6)<1,Y,Z>
           GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + MISC.TBL(5)<1,Y,Z>
           GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + MISC.TBL(6)<1,Y,Z>
           CCTR.NUM=MISC.TBL(2)<1,Y,Z>
           CCTR.NO=FIELD(CCTR.NUM,"*",2)
           OPER.NO=MISC.TBL(3)<1,Y,Z>
           DATE=MISC.TBL(4)<1,Y,Z>
           IF MISC.TBL(5)<1,Y,Z> <= 0 THEN
              E.TTL=INT((MISC.TBL(5)<1,Y,Z> / 100) - .5)
           END ELSE
              E.TTL=INT((MISC.TBL(5)<1,Y,Z> / 100) + .5)
           END
           IF E.TTL=0 THEN E.TTL=''
           IF MISC.TBL(6)<1,Y,Z> <= 0 THEN
              A.TTL=INT((MISC.TBL(6)<1,Y,Z> / 100) - .5)
           END ELSE
              A.TTL=INT((MISC.TBL(6)<1,Y,Z> / 100) + .5)
           END
           IF A.TTL=0 THEN A.TTL=''
           GOSUB 40
           IF LINE.CNT >= 50 THEN GOSUB 20
        NEXT Z
        GOSUB 60
        IF LINE.CNT >= 50 THEN GOSUB 20
     NEXT Y
     GOSUB 80
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFO FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     D.CNT=COUNT(JOB.MS.DATE,VM) + (JOB.MS.DATE # '')
     FOR T=1 TO D.CNT
        IF JOB.MS.DATE<1,T,1> > DATE.LAST.ENTRY THEN DATE.LAST.ENTRY=JOB.MS.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     T=0;D.CNT=0
     D.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR T=1 TO D.CNT
        IF DATE.TBL<1,T,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,T,1>
     NEXT T
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     LINE.CNT=0
     PG.NO=PG.NO + 1
     H.LINE=''; HLINE1=''; HLINE2=''; HLINE3=''
     HLINE1="#":JOB.NO"L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO"L#4":SPACE(3)
     N=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=N:CO.NAME:N
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):"PAGE: ":PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.CUST "L#8":SPACE(38):'M I S C E L L A N E O U S   P O S T E D'
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:CUST.NAME
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.QTY<1,1> "L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.DESC<1,1> "L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.ENTRY,"D2-")
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'SR: ':SALS.NAME "L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:SPACE(53):'LAST     EST      ACT'
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'DEPT  CCTR  OPER  DESCRIPTION':SPACE(24):'ENTRY    COST     COST    VAR($)'
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'----  ----  ----  -------------------------------    -----   ------   ------   ------'
     PRINT H.LINE
     LINE.CNT=LINE.CNT + 9
     RETURN
*
*---- CREATE TABLE
*
30*
     LOCATE DEPT.NO IN MISC.TBL(1)<1>,1 SETTING D ELSE
        D=COUNT(MISC.TBL(1),VM) + (MISC.TBL(1) # '') + 1
        MISC.TBL(1)<1,D>=DEPT.NO
     END
     LOCATE CCTR.NO IN MISC.TBL(2)<1,D>,1 SETTING C ELSE
        C=COUNT(MISC.TBL(2)<1,D>,SVM) + (MISC.TBL(2)<1,D> # '') + 1
        MISC.TBL(2)<1,D,C>=CCTR.NO
     END
     MISC.TBL(3)<1,D,C>=OPER.NO
     IF TRANS.DATE > MISC.TBL(4)<1,D,C> THEN MISC.TBL(4)<1,D,C>=TRANS.DATE
     MISC.TBL(5)<1,D,C>=MISC.TBL(5)<1,D,C> + E.TTL.COST
     MISC.TBL(6)<1,D,C>=MISC.TBL(6)<1,D,C> + A.TTL.COST
     FOR A=4 TO 6
        IF MISC.TBL(A)<1,D,C>=0 THEN MISC.TBL(A)<1,D,C>=''
     NEXT A
     RETURN
*
*---- PRINT THE VALUES
*
40*
     GOSUB 50
     IF LINE.CNT >= 50 THEN GOSUB 20
     IF DEPT.NO # PREVIOUS.DEPT.NO THEN
        MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
        P.LINE=''
        P.LINE=P.LINE:DEPT.DESC:'  ':DEPT.NO 
        PRINT P.LINE
        DEPT.LEN=LEN(DEPT.DESC) + LEN(DEPT.NO) + 2
        UNDERLINE.DEPT=STR("-",DEPT.LEN)
        P.LINE=''
        P.LINE=UNDERLINE.DEPT
        PRINT P.LINE
        P.LINE=''
        P.LINE=P.LINE:SPACE(6):CCTR.NO "L#4":'  ':OPER.NO "L#4":'  ':OPER.DESC "L#31"
        P.LINE=P.LINE:'    ':DATE"R#5":'   ':E.TTL"R#6":'   ':A.TTL"R#6":'   ':VAR"R#7"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 3
     END ELSE
        P.LINE=''
        P.LINE=P.LINE:SPACE(6):CCTR.NO "L#4":'  ':OPER.NO "L#4":'  ':OPER.DESC "L#31"
        P.LINE=P.LINE:'    ':DATE"R#5":'   ':E.TTL"R#6":'   ':A.TTL"R#6":'   ':VAR"R#7"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 1
     END
     PREVIOUS.DEPT.NO=DEPT.NO
     RETURN
*
*---- CALCULATE FIELD VALUES
*
50*
     MATREAD OPER.REC FROM OPERATION,CONO:OPER.NO ELSE OPER.DESC=UNKNOWN
     VAR=A.TTL - E.TTL
     IF VAR=0 THEN VAR=''
     IF VAR # '' THEN VAR=OCONV(VAR,"MD0-")
     IF DATE > DEPT.TOTAL.REC(1) THEN DEPT.TOTAL.REC(1)=DATE
     DATE=OCONV(DATE,"D2-")
     DATE=DATE[1,5]
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     IF LINE.CNT >= 50 THEN GOSUB 20
     GOSUB 70
     D.LINE=''
     D.LINE=D.LINE:SPACE(53):'-----   ------   ------   ------'
     PRINT D.LINE
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
     D.LINE=D.LINE:SPACE(13):DEPT.DATE"R#5":'   ':DEPT.E.TTL"R#6":'   ':DEPT.A.TTL"R#6":'   ':DEPT.VAR"R#7"
     PRINT D.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- OCONV HOURS FOR DEPT TOTALS
*
70*
     DEPT.TOTAL.REC(2)=INT((DEPT.TOTAL.REC(2) / 100) + .5)
     DEPT.TOTAL.REC(3)=INT((DEPT.TOTAL.REC(3) / 100) + .5)
     DEPT.DATE=DEPT.TOTAL.REC(1)
     DEPT.E.TTL=DEPT.TOTAL.REC(2)
     DEPT.A.TTL=DEPT.TOTAL.REC(3)
     DEPT.VAR=DEPT.A.TTL - DEPT.E.TTL
     DEPT.VAR=OCONV(DEPT.VAR,"MD0-")
     DEPT.DATE=OCONV(DEPT.DATE,"D2-")
     DEPT.DATE=DEPT.DATE[1,5]
     RETURN
*
*---- PRINT GRAND TOTALS
*
80*
     IF LINE.CNT + 2 >= 50 THEN GOSUB 20
     GOSUB 90
     G.LINE=''
     G.LINE=G.LINE:SPACE(61):'------   ------   ------'
     PRINT G.LINE
     G.LINE=''
     G.LINE=G.LINE:'*** GRAND TOTALS ***':SPACE(41):GRND.E.TTL"R#6":'   ':GRND.A.TTL"R#6":'   ':GRND.VAR"R#7"
     PRINT G.LINE
     RETURN
*
*---- OCONV HOURS FOR GRAND TOTALS
*
90*
     GRAND.TOTAL.REC(1)=INT((GRAND.TOTAL.REC(1) / 100) + .5)
     GRAND.TOTAL.REC(2)=INT((GRAND.TOTAL.REC(2) / 100) + .5)
     GRND.E.TTL=GRAND.TOTAL.REC(1)
     GRND.A.TTL=GRAND.TOTAL.REC(2)
     GRND.VAR=GRND.A.TTL - GRND.E.TTL
     GRND.VAR=OCONV(GRND.VAR,"MD0-")
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
