      SUBROUTINE KYOCERA.PRINT.SUB (CONO,TARG.KEY,MAT TARG.REC,INTERPRET)
***************************************************************************
*
* PROGRAM  - KYOCERA.PRINT.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/25/86
*
* DESCRIPTION
*
* This subroutine is used to print the shop-floor barcode templates on a
* KYOCERA LASER bar code printer.
*
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JCS.CPYLIB>TARGET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
* * * * * * * * *  N O T E :  1 INCH = 300 DOTS  * * * * * * * * *
*
      TOPM = 150                ;* Dots from top margin to 1st barcode
      LFTM = 75                 ;* Dots from left margin to 1st barcode
      COLW = 810                ;* Column size in dots
      ROWW = 300                ;* Row size in dots
*
      PAGE.PAR = "!R! RES; UNIT D; SPO P; SPW 2550; SPL 3300; SLM 60; STM 60;"
      BARC.PAR = "120, 120, 5, 10, 10, 10, 5, 10, 10, 10"
      PRINTER ON
*
*---- MAIN PROCESSING
*
100*
      PRINT "!R! PAGE; EXIT;"
      PRINT PAGE.PAR
      PRINT "MAP 0, 0; BOX 2325,60;"            ;* Draw header box
      PRINT "MAP 50, 50; FONT 10; SCPI 0; TEXT '":TARG.NAME:"';"
      PRINT "MAP 1800, 0; FONT 1; SCPI 0; TEXT '":OCONV(DATE(),"D2-"):"';"
*
      FOR RP = 1 TO 10
         FOR CP = 1 TO 3
            DEFS = TARG.DEF<1,CP,RP>
            DSCS = TARG.DSC<1,CP,RP>
            CNT = COUNT(DEFS,"\")+1
            SUBW = INT(COLW/CNT)
            FOR PTR = 1 TO CNT
               DEF = TRIM(FIELD(DEFS,"\",PTR))
               PFX = DEF[1,1]
               DSC = TRIM(FIELD(DSCS,"\",PTR))
               BEGIN CASE
               CASE PFX = "F"
                  BC = DEF[2,99]
               CASE PFX = "C"
                  BC = ""
               CASE PFX = "1" OR PFX = "4" OR PFX = "7"
                  BC = PFX
               CASE PFX = "2" OR PFX = "8"
                  GOSUB 60000
                  OPER = DEF[2,99]
                  BC = PFX:CCTR:OPER
               CASE PFX = "3" OR PFX = "6"
                  GOSUB 60000
                  BC = PFX:CCTR
               CASE 1
                  BC = ""
               END CASE
               X2 = LFTM +(CP-1)*COLW + (PTR-1)*SUBW     ;* Define X-coord for barcode
               Y2 = TOPM +(RP-1)*ROWW                    ;* Define Y-coord for barcode
               X1 = X2                                   ;* Define X-coord for desc
               Y1 = Y2 - 25                              ;* Define Y-coord for desc
               PRINT "MAP ":X1:", ":Y1:"; FONT 9; SCPI 0; TEXT '":DSC:"';"
               IF BC # "" THEN
                  PRINT "MAP ":X2:", ":Y2:"; FONT 6; BARC 19, ":INTERPRET:", '":BC:"', ":BARC.PAR:";"
               END
            NEXT PTR
         NEXT CP
      NEXT RP
      PRINT "RES; EXIT;"
      GOTO 99999
*
*---- GET PREVIOUS COST CENTER
*
60000*
      CCTR = ""
      SDONE = 0
      FOR CP1 = CP TO 1 STEP -1 UNTIL SDONE
         FOR RP1 = 10 TO 1 STEP -1 UNTIL SDONE
            IF CP1 < CP OR RP1 < RP THEN
               SDEF = TARG.DEF<1,CP1,RP1>
               SPFX = SDEF[1,1]
               IF SPFX = "C" THEN
                  CCTR = SDEF[2,3]
                  SDONE = 1
               END
            END
         NEXT RP1
      NEXT CP1
      IF CCTR = "" THEN CCTR = FIELD(TARG.KEY,"-",1)
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER OFF
      RETURN
   END
