  SUBROUTINE JOB.SALES.MAINT(CONO,JOB.NO,IVC.NO,SCREEN.NUMBER,MAT JSS.REC)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.INVOICE
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.SALES.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 02/11/84
* DESCRIPTION -
*This subroutine updates and maintaines then JOB.SALES.STATS.
*
* Called by INVOICE.MAINT.F4 with a JOB.NO supplied
* Called by JOB.SALES.DRIVER without a JOB.NO
* Uses screen JOB.SALES.MAINT
*
* T20782 UPDATE INVOICE.SALES.STATS FILE
*T26126 adelgado 03/08/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*********************************************************************
*
*--- FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>INVOICE
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>JOB.SALES.STATS
*COPY>PMC.CPYLIB>INVOICE.SALES.STATS
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE=1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- INITIALIZATION
*
  ADJ.FACT.COST = 0
*
*--- MAIN PROCESSING
*
  ECD.SCRN.NO=SCREEN.NUMBER
  FOR SCV = 1 TO SCV.REC.SIZE
    SCV.REC(SCV)<ECD.SCRN.NO>=""
  NEXT SCV
  ECD.ACTION=2;CALL SCRN.EDIT
5 *
  IF JOB.NO = '' THEN
    ECD.NUM = 20 ; ECD.ACTION = 4 ; CALL SCRN.EDIT
    JOB.NO = ECD.RET.VALUE
    IF JOB.NO = 'END' THEN RETURN
    MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
      ERRMSG = "JOB NUMBER ":JOB.NO:" NOT ON FILE"
      GOSUB 91000
      JOB.NO = ''
      GOTO 5
    END
    MAT IVC.REC = ""
    IVC.NO = ""
    ECD.NUM = 21 ; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = JOB.DESC
    ECD.ACTION = 5 ; CALL SCRN.EDIT
    KILL.FORM = 'N'
  END ELSE
    ECD.NUM = 20 ; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = JOB.NO
    ECD.ACTION = 5 ; CALL SCRN.EDIT
    ECD.NUM = 21 ; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = JOB.DESC
    ECD.ACTION = 5 ; CALL SCRN.EDIT
    KILL.FORM = 'Y'
  END
  NEW.JSS = "N"
      * T26126 v
  MATREADU JSS.REC FROM JOB.SALES.STATS,CONO:JOB.NO LOCKED
    ERRMSG = 'JOB SALES STATS record is locked by user - ':GETUSERNAME(STATUS())
    JOB.NO = ''
    GOSUB 91000;GOTO 5 
  END ELSE
      * T26126 ^
    MAT JSS.REC = ""
    NEW.JSS = "Y"
  END
  JSS.COST.TOTAL = JOB.TOT.COST
  IF NEW.JSS = "Y" THEN
    SCV.REC(1)<ECD.SCRN.NO> = JSS.COST.TOTAL
    ECD.NUM=1;ECD.ACTION=5;CALL SCRN.EDIT
    FOR JJ = 1 TO 7
      ON JJ GOSUB 10,20,30,40,50,60,70
      IF ECD.RET.VALUE = "END" THEN
        RELEASE JOB.SALES.STATS,CONO:JOB.NO
        GOSUB 300           ;* CLEAR SCREEN
        GOTO 99999
      END
    NEXT JJ
    GOSUB 400
  END ELSE
    GOSUB 400
    GOSUB 100                 ;* PRINT ALL VALUES
    ECD.ACTION = 3;CALL SCRN.EDIT
  END
  GOSUB 200                    ;* GET OPTION
  GOTO 99999
*
*--- ENTER ADJUSTED O/T
*
10*
  ECD.NUM = 2
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.ADJUST.AMT<1,1> = ECD.RET.VALUE
  END
  RETURN
*
*--- ENTER SPOILAGE
*
20*
  ECD.NUM = 3
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.ADJUST.AMT<1,2> = ECD.RET.VALUE
  END
  RETURN
*
*--- ENTER PLANT CONVENIENCE
*
30*
  ECD.NUM = 4
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.ADJUST.AMT<1,3> = ECD.RET.VALUE
  END
  RETURN
*
*--- ENTER OTHER COST
*
40*
  ECD.NUM = 5
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.ADJUST.AMT<1,4> = ECD.RET.VALUE
  END
  RETURN
*
*--- ENTER MARKETABLE COST
*
50*
  ECD.NUM = 7
  IF JOB.CONF.AMT # "" THEN
    ECD.O.R = "O"; ECD.DEFAULT = JOB.CONF.AMT
  END
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.MARKET.COST = ECD.RET.VALUE
  END
  RETURN
*
*--- ENTER ADJUSTED MARKET COST
*
60*
  ECD.NUM = 8
  ECD.O.R = "O"; ECD.DEFAULT = JSS.MARKET.COST
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.ADJUST.COST<1,2> = ECD.RET.VALUE
  END
  RETURN
*
*--- ENTER O/S COMMISSIONS
*
70*
  ECD.NUM = 9
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    JSS.COMMISSION<1,1> = ECD.RET.VALUE
  END
  RETURN
*
*--- PRINT SCREEN
*
100*
  SCV.REC(1)<ECD.SCRN.NO> = JSS.COST.TOTAL
  SCV.REC(2)<ECD.SCRN.NO> = JSS.ADJUST.AMT<1,1>
  SCV.REC(3)<ECD.SCRN.NO> = JSS.ADJUST.AMT<1,2>
  SCV.REC(4)<ECD.SCRN.NO> = JSS.ADJUST.AMT<1,3>
  SCV.REC(5)<ECD.SCRN.NO> = JSS.ADJUST.AMT<1,4>
  SCV.REC(6)<ECD.SCRN.NO> = JSS.ADJUST.COST<1,1>
  SCV.REC(7)<ECD.SCRN.NO> = JSS.MARKET.COST
  SCV.REC(8)<ECD.SCRN.NO> = JSS.ADJUST.COST<1,2>
  SCV.REC(9)<ECD.SCRN.NO> = JSS.COMMISSION<1,1>
  RETURN
*
*--- ENTER OPTION
*
200*
  MORE=1
  LOOP
    IF SCREEN.NUMBER = 1 THEN
      ECD.NUM = 11
    END ELSE
      ECD.NUM = 10
    END
    SCV.REC(ECD.NUM)<ECD.SCRN.NO>=''
    ECD.ACTION=4;CALL SCRN.EDIT;OPTION=ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION="E" OR OPTION="END"
        RELEASE JOB.SALES.STATS,CONO:JOB.NO
        MORE=0
      CASE OPTION = "" OR OPTION = "F"
        JSS.CUST.ID = JOB.CUST
        JSS.SLSM.ID = JOB.SLSMN
        JSS.SALES.CODE = JOB.SALES.CODE
        JSS.JOB.CAT = JOB.CATG
        JSS.NO.COLORS = JOB.COLORS
        MATWRITE JSS.REC ON JOB.SALES.STATS,CONO:JOB.NO
* T20782 v
        MATREAD JOB.REC FROM JOB, CONO:JOB.NO THEN
          FND = 0
          ICNT = DCOUNT(JOB.INV.NO,VM)
          FOR I = 1 TO ICNT UNTIL FND
            MATREAD IVC.REC FROM INVOICE, CONO:JOB.INV.NO<1,I> THEN
              IF IVC.LAST = "Y" THEN
                ISS.KEY = CONO:"_J_":JOB.NO:"_":JOB.INV.NO<1,I>:"_0_0"
                MATREAD ISS.REC FROM INVOICE.SALES.STATS, ISS.KEY THEN
                  ISS.ADJUST.AMT = JSS.ADJUST.AMT
                  ADJ.COST = ISS.ADJUST.AMT<1,1>
                  SPOIL = ISS.ADJUST.AMT<1,2>
                  PLANT.CON = ISS.ADJUST.AMT<1,3>
                  OTH = ISS.ADJUST.AMT<1,4>
                  ADJ.FACT.COST = ISS.COST.TOTAL + ADJ.COST + SPOIL + PLANT.CON + OTH
                  ISS.ADJUST.COST<1,1> = ADJ.FACT.COST
                  ISS.MARKET.COST = JSS.MARKET.COST
                  ISS.ADJUST.COST<1,2> = JSS.ADJUST.COST<1,2>
                  ISS.COMMISSION<1,1> = JSS.COMMISSION<1,1>
                  MATWRITE ISS.REC ON INVOICE.SALES.STATS, ISS.KEY
                  FND = 1
                END
              END
            END
          NEXT I
        END
* T20782 ^
        MORE=0
      CASE NUM(OPTION) AND OPTION > 0
        ON OPTION GOSUB 10,20,30,40,50,60,70
        IF OPTION > 0 AND OPTION < 5 THEN
          GOSUB 400
        END
    END CASE
  WHILE MORE = 1 DO REPEAT
  RETURN
*
*--- CLEAR SCREEN
*
300*
  FOR SCV = 1 TO SCV.REC.SIZE
    SCV.REC(SCV)<ECD.SCRN.NO> = ""
  NEXT SCV
  ECD.ACTION = 6 ;CALL SCRN.EDIT
  RETURN
*
*--- TOTAL
*
400*
  TOT.COST = JSS.COST.TOTAL
  ADJ.COST = JSS.ADJUST.AMT<1,1>
  SPOIL = JSS.ADJUST.AMT<1,2>
  PLANT.CON = JSS.ADJUST.AMT<1,3>
  OTH = JSS.ADJUST.AMT<1,4>
  ADJ.FACT.COST = TOT.COST + ADJ.COST + SPOIL + PLANT.CON + OTH
  REM  SCV.REC(6)<ECD.SCRN.NO> = OCONV(ADJ.FACT.COST,"MD2")
  SCV.REC(6)<ECD.SCRN.NO> = ADJ.FACT.COST
  ECD.NUM = 6;ECD.ACTION = 5;CALL SCRN.EDIT
  JSS.ADJUST.COST<1,1> = ADJ.FACT.COST
  RETURN
*
*--- CALLS FOR SYSCOM
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
99999*
  IF KILL.FORM = 'Y' THEN
    ECD.ACTION=99;CALL SCRN.EDIT
  END
  RETURN
END
