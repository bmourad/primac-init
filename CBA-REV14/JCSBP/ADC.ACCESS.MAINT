*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - ADC.ACCESS.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 11/12/84
* DESCRIPTION -
* This program will be used to update and maintain ADC.ACCESS 
* file.
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>ADC.ACCESS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE = 1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
     OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
     OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
     OPEN "","JCS.SCREENS" TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE MISSING";GOTO 93000
     OPEN "","ADC.ACCESS" TO ADC.ACCESS ELSE ERRMSG = "ADC.ACCESS FILE MISSING";GOTO 93000
     OPEN "","EMPLOYEE" TO EMPLOYEE ELSE ERRMSG = "EMPLOYEE FILE MISSING";GOTO 93000
*
*---- INITIALIZATION
*
     MAT EDIT.COM = ""
     MAT EDIT.COM.DRIVER = ""
     MAT ADA.REC = ""
     ERRMSG = ""
*
*---- GET COMPANY NUMBER
*
     CONO = ""
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO = "END" THEN GOTO 99999
*
*---- PRINT SCREEN
*
     ECD.SCRN.CNT = 1
     ECD.SCRN.NAME<1> = "ADC.ACCESS.MAINT"
     ECD.ACTION = 1; CALL SCRN.EDIT
     ECD.SCRN.NO = 1
     MAT SCV.REC = ""
     ECD.ACTION = 2; CALL SCRN.EDIT
     ECD.NUM=8;ECD.ACTION=4;CALL SCRN.EDIT
     IF ECD.RET.VALUE="END" THEN GOTO 99999
*
*---- MAIN PROCESSING
*
10*
     SCV.REC(7)<ECD.SCRN.NO,1>=DATE()
     ECD.NUM=7
     ECD.ACTION=5;CALL SCRN.EDIT
     ECD.NUM=1;ECD.ACTION=4;CALL SCRN.EDIT
     BEGIN CASE
        CASE ECD.RET.VALUE = "END"
           GOTO 99999
        CASE ECD.RET.VALUE # ""
           EMP.NUM = ECD.RET.VALUE
           MATREAD EMP.REC FROM EMPLOYEE,CONO:EMP.NUM ELSE
              ERRMSG = "INVALID EMPLOYEE NUMBER, RE-ENTER"
              GOSUB 91000;GOSUB 300;GOTO 10
           END
           EMP.NAME = EMP.FRST.NAME:" ":EMP.LAST.NAME
           SCV.REC(2)<ECD.SCRN.NO,1> = EMP.NAME
           ECD.NUM = 2;ECD.ACTION=5;CALL SCRN.EDIT
           NEW.ADC.ACCESS = "NO"
           MATREADU ADA.REC FROM ADC.ACCESS,EMP.NUM ELSE
              MAT ADA.REC = ""
              NEW.ADC.ACCESS = "YES"
           END
     END CASE
     IF NEW.ADC.ACCESS = "YES" THEN 
        FOR S = 1 TO 3
           ON S GOSUB 20,30,40
           IF ECD.RET.VALUE = "END" THEN 
              GOSUB 300;GOTO 10
           END
        NEXT S
     END ELSE
        GOSUB 100
        ECD.ACTION = 3; CALL SCRN.EDIT
     END
     GOSUB 200
     GOSUB 300
     GOTO 10
*
*---- ENTER EMPLOYEE PASSWORD
*
20*
     ECD.NUM=3;ECD.ACTION=4;CALL SCRN.EDIT
     ADA.PWD=ECD.RET.VALUE
     RETURN
*
*---- ENTER Y / N FOR TIME ENTRY FACILITY
*
30*
     ECD.NUM=4;ECD.ACTION=4;CALL SCRN.EDIT
     ADA.TIME=ECD.RET.VALUE
     RETURN
*
*---- ENTER Y / N FOR MATERIAL ENTRY FACILITY
*
40*
     ECD.NUM=5;ECD.ACTION=4;CALL SCRN.EDIT
     ADA.MATL=ECD.RET.VALUE
     RETURN
*
*---- GET ALL VALUES FROM FILE
*
100*
     SCV.REC(3)<ECD.SCRN.NO,1> = ADA.PWD
     SCV.REC(4)<ECD.SCRN.NO,1> = ADA.TIME
     SCV.REC(5)<ECD.SCRN.NO,1> = ADA.MATL
     RETURN
*
*---- ENTER OPTIONS
*
200*
     MORE = 1
     LOOP
     ECD.NUM = 6
     ECD.ACTION = 4; CALL SCRN.EDIT
     OPTION = ECD.RET.VALUE
     BEGIN CASE
        CASE OPTION = "END" OR OPTION = "E"
           MORE = 1
        CASE NUM(OPTION)
           ON OPTION GOSUB 20,30,40
           MORE = 0
        CASE OPTION = "F"
           GOSUB 400
           MORE = 1
     END CASE
     WHILE MORE = 0 DO REPEAT
     RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
     ECD.ACTION = 6; CALL SCRN.EDIT
     MAT SCV.REC = ""
     RETURN
*
*---- UPDATE ADC.ACCESSS FILE
*
400*
     MATWRITE ADA.REC ON ADC.ACCESS,EMP.NUM
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE = 1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
92000*
     ERR.TYPE = 2
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     ERR.TYPE = 3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
     END
