*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - DRSBP
* PROGRAM     - BOL.PRINT.DRS
* AUTHOR      - STRAKA
* DATE        - 04/10/96
* DESCRIPTION
* This program prints the bills of lading for a job.
*T23947 cm 06/28/1999 * When printing bol's with multiple copies getting
*                       syntax error message.
*T24172 cm 07/21/1999 When printing bol's if flagged not to print it
*                       still prints
*********************************************************************
*
***** INSERT FILES EQUATES
*
*COPY>JCS.CPYLIB>DRS.JOB.SPEC
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>CUST.TEMPLATE.LIST
*COPY>JCS.CPYLIB>SHIPTO.MASTER
*COPY>JCS.CPYLIB>TEMPLATE.DETAIL
*COPY>JCS.CPYLIB>FOLD.CODE
*COPY>JCS.CPYLIB>PACK.CODE
*COPY>JCS.CPYLIB>DISTRIBUTION.CODE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
OPEN '','JOB' TO JOB ELSE
  ERRMSG ='JOB FILE IS MISSING'
  GOTO 93000
END
OPEN '','DRS.JOB.SPEC' TO DRS.JOB.SPEC ELSE
  ERRMSG ='DRS.JOB.SPEC FILE IS MISSING'
  GOTO 93000
END
OPEN '','CUST.TEMPLATE.LIST' TO CUST.TEMPLATE.LIST ELSE
  ERRMSG = 'CUST.TEMPLATE.LIST FILE IS MISSING'
  GOTO 93000
END
OPEN '','SHIPTO.MASTER' TO SHIPTO.MASTER ELSE
  ERRMSG = 'SHIPTO.MASTER FILE IS MISSING'
  GOTO 93000
END
OPEN '','TEMPLATE.DETAIL' TO TEMPLATE.DETAIL ELSE
  ERRMSG = 'TEMPLATE.DETAIL FILE IS MISSING'
  GOTO 93000
END
OPEN '','CUSTOMER' TO CUSTOMER ELSE
  ERRMSG = 'CUSTOMER FILE IS MISSING'
  GOTO 93000
END
OPEN '','FOLD.CODE' TO FOLD.CODE ELSE
  ERRMSG = 'FOLD.CODE FILE IS MISSING'
  GOTO 93000
END
OPEN '','PACK.CODE' TO PACK.CODE ELSE
  ERRMSG = 'PACK.CODE FILE IS MISSING'
  GOTO 93000
END
OPEN '','DISTRIBUTION.CODE' TO DISTRIBUTION.CODE ELSE
  ERRMSG = 'DISTRIBUTION.CODE FILE IS MISSING'
  GOTO 93000
END
OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
  ERRMSG = 'SHIP.VIA FILE IS MISSING'
  GOTO 93000
END
OPEN '','COMPANY' TO COMPANY ELSE
  ERRMSG = 'COMPANY FILE IS MISSING'
  GOTO 93000
END
OPEN '','CONTROL' TO CONTROL ELSE
  ERRMSG = 'CONTROL FILE IS MISSING'
  GOTO 93000
END
*
**** READ PROC VALUES
*
PROCREAD ID ELSE
  ERRMSG = 'CANNOT PERFORM PROCREAD'
  GOTO 93000
END
CONO = ID<1>
CONO.NAME = ID<2>
JOB.NO = ID<3>
CUST.NO = ID<4>
TEMP.NO = ID<3>
COPIES = ID<5>
STRT = ID<6>
LST = ID<7>
IF STRT = 'A' THEN STRT = 1
IF (NOT(NUM(STRT))) THEN STRT = 1
IF LST = 'A' THEN LST = 9999
IF (NOT(NUM(LST))) THEN LST = 9999
*
**** INITIALIZATION
*
MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC =''
MATREAD OPCO.REC FROM CONTROL, CONO:"OPS" ELSE
  MAT OPCO.REC = ""
END
LN.CNT = 0
CO.CS = CO.CITY:', ':CO.STATE
SHIPPER.ID = ''
*
PROMPT''
CRT @(-1)
  LOOP
    CRT @(0,10):CL:@(5,10):'Print (M)ask or (B)ills : ':
    INPUT X:
  WHILE X # 'M' AND X # 'B' DO REPEAT
  IF X = 'M' THEN GOSUB 9000
*T23947 v
* IF COPIES > 1 THEN
*   PERFORM "SP-ASSIGN =STANDARD ":COPIES
* END
*T23947 ^
PRINTER ON
*
***** READ AND PROCESS FIRST RECORD
*
100 *
READNEXT KEY ELSE GOTO 99999
MATREAD DJS.REC FROM DRS.JOB.SPEC,KEY ELSE GOTO 99999
MATREAD CUST.REC FROM CUSTOMER, CONO:DJS.CUST ELSE
  CUST.NAME = '??????????'
END
MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE
  JOB.DESC = '??????????'
END
MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, CONO:CUST.NO:"!":TEMP.NO:"!001" ELSE
  ERRMSG = 'The CUST.TEMPLATE.LIST record for this job is missing'
  GOSUB 91000
  GOTO 99999
END
SHIPTO = CTL.SHIPS<1,1>
IF SHIPTO = '' THEN
  ERRMSG = 'The first record of CUST.TEMPLATE.LIST has no shipto# - run resequencing'
  GOSUB 91000
  GOTO 99999
END
SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
  MAT TDT.REC = ''
END
RSEQ = '001'
KEY = CONO:CUST.NO:"!":TEMP.NO:"!"
*
***** READ AND PROCESS ALL RECORDS
*
DATA = 1
RUN.NO = 0
LOOP
  IF SHIP.CNT < 1 THEN
    RSEQ =RSEQ + 1
    IF LEN(RSEQ) < 3 THEN RSEQ = STR('0',3-LEN(RSEQ)):RSEQ
    MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, KEY:RSEQ THEN
      SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
    END ELSE DATA = 0
    IF SHIP.CNT < 1 THEN DATA = 0
  END
  IF RUN.NO >= LST THEN DATA = 0
WHILE DATA = 1 DO
  FOR I = 1 TO SHIP.CNT
    SHIPTO = CTL.SHIPS<1,I>
    MATREAD SHM.REC FROM SHIPTO.MASTER, CONO:SHIPTO ELSE
      MAT SHM.REC = ''
    END
    MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
      MAT TDT.REC = ''
    END
    IF TDT.SHIP.VIA # "" THEN SHM.DT.SHPVIA = TDT.SHIP.VIA
    IF SHM.DT.SHPVIA # '' THEN
      MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:SHM.DT.SHPVIA ELSE MAT SHIP.VIA.REC = ''
    END
    RUN.NO = RUN.NO + 1
*T24172 v
*   IF TDT.QTY > 0 AND RUN.NO >= STRT AND RUN.NO <= LST THEN GOSUB 1000
    IF TDT.QTY > 0 AND TDT.BOL = "Y" AND RUN.NO >= STRT AND RUN.NO <= LST THEN GOSUB 1000
*T24172 ^
  NEXT I
  SHIP.CNT = 0
REPEAT
GOTO 99999
*
*---- PRINT FORM
*
1000*
FOR C = 1 TO COPIES; *T23947
   LINE.CNT = 0
   PRINT OCONV(DATE(),"D2/"):SPACE(3):OCONV(TIME(),"MT")
   PRINT SPACE(32):"JOB # : ":JOB.NO "L#8  ":"RUN # : ":RUN.NO "L#4"
   PRINT
   PRINT
   SHIP.NUM = ""
   PRINT SPACE(15):SHPV.DESC "L#30":SPACE(5):SHIPPER.ID "R#7":SPACE(2):OCONV(DATE(),"D2/"):SHIP.NUM "R#8"
   R1 = 1; R2 = 1; R4 = 1; R5 = 1
   PRINT SPACE(3):CO.NAME
   FOR N = 1 TO 4
     PRT.LN = SPACE(3)
     BEGIN CASE
     CASE CO.ADDRESS # "" AND R1
       PRT.LN = SPACE(3):CO.ADDRESS
       R1 = 0
     CASE CO.CITY # "" AND R2
       PRT.LN = SPACE(3):CO.CITY
       R2 = 0
     CASE CO.STATE # "" AND R4
       PRT.LN = SPACE(3):CO.STATE
       R4 = 0
     CASE CO.ZIP # "" AND R5
       PRT.LN = SPACE(3):CO.ZIP:SPACE(2):OPCO.CNTY
       R5 = 0
     END CASE
     PRINT PRT.LN
   NEXT N
   PRINT
   PRINT SPACE(4):SHM.DT.NAME"L#32 ":CUST.NO"L#10"
   R1 = 1; R2 = 1; R3 = 1; R4 = 1
   FOR N = 1 TO 4
     PRT.LN = SPACE(35)
     BEGIN CASE
     CASE SHM.DT.ADDR1 # "" AND R1
       PRT.LN = SPACE(4):SHM.DT.ADDR1"L#30":SPACE(3):SHIPTO "L#3"
       R1 = 0
     CASE SHM.DT.ADDR2 # "" AND R2
       PRT.LN = SPACE(4):SHM.DT.ADDR2
       R2 = 0
     CASE SHM.DT.CITY # "" AND R3
       PRT.LN = SPACE(4):SHM.DT.CITY:" , ":SHM.DT.STATE"L#3"
       R3 = 0
     CASE SHM.DT.ZIP # "" AND R4
       PRT.LN = SPACE(4):SHM.DT.ZIP"L#10":SPACE(2):SHM.DT.CNTY
       R4 = 0
     END CASE
     BEGIN CASE
     CASE N = 3
       PRT.LN = PRT.LN "L#31"
     END CASE
     PRINT PRT.LN
   NEXT LN
   PRINT
   PRINT SPACE(4):"ATTN: ":SHM.DT.ATTN
   PRINT
   PRINT
   PRT.LN = ""
   BEGIN CASE
   CASE TDT.SHP.TERMS = "COD"
     PRT.LN = SPACE(60):"X":SPACE(10):TDT.SHP.TERMS
   CASE TDT.SHP.TERMS = "PPA"
     PRT.LN = SPACE(51):"X":SPACE(18):TDT.SHP.TERMS
   CASE TDT.SHP.TERMS = "PPD"
     PRT.LN = SPACE(42):"X":SPACE(27):TDT.SHP.TERMS
   END CASE
   PRINT PRT.LN
   PRINT;PRINT
   *
   SKIDS = 0
   BOXES = 0
   MATREAD PCK.REC FROM PACK.CODE, CONO:TDT.PACK ELSE MAT PCK.REC = ''
   IF TDT.BOL = 'Y' THEN
     IF DJS.SKD.MAX > 0 AND TDT.SKID.LAB = 'Y' THEN
       SKIDS = INT(TDT.QTY / DJS.SKD.MAX)
       IF REM(TDT.QTY,DJS.SKD.MAX) >= DJS.SKD.MIN THEN SKIDS = SKIDS + 1
     END
     IF DJS.BOX.MAX > 0 AND TDT.CART.LAB ='Y' THEN
       BOXES = INT(TDT.QTY / DJS.BOX.MAX)
       IF REM(TDT.QTY,DJS.BOX.MAX) >= DJS.BOX.MIN THEN BOXES = BOXES + 1
     END
     IF DJS.PPL > 0 THEN LBS = OCONV(ICONV(TDT.QTY / DJS.PPL, 'MD0'), 'MD0') ELSE LBS = 0
     INST.LINES = ''
     LN = 1
     IF TDT.SP.INST.1.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.1;LN=LN+1
     IF TDT.SP.INST.2.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.2;LN=LN+1
     IF TDT.SP.INST.3.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.3;LN=LN+1
     IF TDT.SP.INST.4.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.4;LN=LN+1
     IF TDT.SP.INST.5.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.5;LN=LN+1
     IF TDT.SP.INST.6.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.6;LN=LN+1
     IF TDT.SP.INST.7.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.7;LN=LN+1
     IF TDT.SP.INST.8.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.8;LN=LN+1
     IF TDT.SP.INST.9.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.9;LN=LN+1
     IF TDT.SP.INST.10.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.10
     LINE2 = SPACE(19)
     LINE2 = LINE2:OCONV(TDT.QTY,"MD0,")"R#9 "
     LINE2 = LINE2:JOB.DESC<1,1> "L#45"
     PRINT LINE2
     LINE.CNT = LINE.CNT + 1
     FOR J = 1 TO 3
       PRINT SPACE(29):INST.LINES<J> 'L#45'
       LINE.CNT = LINE.CNT + 1
     NEXT J
   END
   FOR R = LINE.CNT + 1 TO 30
     PRINT
   NEXT R
   PRINT;PRINT
   LINE2 = OCONV(SKIDS,'MD0') 'R#4': OCONV(BOXES,'MD0') 'R#4'
   PRINT LINE2:SPACE(25):LBS 'R#8'
   BEGIN CASE
   CASE TDT.FRT.PAY = "O"
     R1 = 1; R2 = 1; R4 = 1; R5 = 1
     PRINT SPACE(50):CO.NAME
     FOR N = 1 TO 4
       PRT.LN = SPACE(3)
       BEGIN CASE
       CASE CO.ADDRESS # "" AND R1
         PRT.LN = SPACE(50):CO.ADDRESS
         R1 = 0
       CASE CO.CITY # "" AND R2
         PRT.LN = SPACE(50):CO.CITY
         R2 = 0
       CASE CO.STATE # "" AND R4
         PRT.LN = SPACE(50):CO.STATE
         R4 = 0
       CASE CO.ZIP # "" AND R5
         PRT.LN = SPACE(50):CO.ZIP:SPACE(2):OPCO.CNTY
         R5 = 0
       END CASE
       PRINT PRT.LN
     NEXT N
     PRINT
   CASE TDT.FRT.PAY = "C"
     R1 = 1; R2 = 1; R4 = 1; R5 = 1
     PRINT SPACE(50):CUST.NAME
     FOR N = 1 TO 4
       PRT.LN = SPACE(3)
       BEGIN CASE
       CASE CUST.ADDR1 # "" AND R1
         PRT.LN = SPACE(50):CUST.ADDR1
         R1 = 0
       CASE CUST.ADDR2 # "" AND R2
         PRT.LN = SPACE(50):CUST.ADDR2
         R2 = 0
       CASE CUST.ADDR4 # "" AND R4
         PRT.LN = SPACE(50):CUST.ADDR4
         R4 = 0
       CASE CUST.ZIP # "" AND R5
         IF CUST.ADDL.OPS<1,4> = "" THEN 
           PRT.CNTY = ""
         END ELSE
           PRT.CNTY = CUST.ADDL.OPS<1,4>
         END
         PRT.LN = SPACE(50):CUST.ZIP:SPACE(2):PRT.CNTY
         R5 = 0
       END CASE
       PRINT PRT.LN
     NEXT N
     PRINT
   *      IF OSD.SHP.TERMS = "COD" THEN
   CASE 1
     PRINT SPACE(50):SHM.DT.NAME
     R1 = 1; R2 = 1; R4 = 1; R5 = 1
     FOR N = 1 TO 4
       PRT.LN = SPACE(50)
       BEGIN CASE
       CASE SHM.DT.ADDR1 # "" AND R1
         PRT.LN = SPACE(50):SHM.DT.ADDR1
         R1 = 0
       CASE SHM.DT.ADDR2 # "" AND R2
         PRT.LN = SPACE(50):SHM.DT.ADDR2
         R2 = 0
       CASE SHM.DT.CITY # "" AND R4
         PRT.LN = SPACE(50):SHM.DT.CITY:", ":SHM.DT.STATE "L#3"
         R4 = 0
       CASE SHM.DT.ZIP # "" AND R5
         PRT.LN = SPACE(50):SHM.DT.ZIP:SPACE(2):SHM.DT.CNTY
         R5 = 0
       END CASE
       PRINT PRT.LN
     NEXT N
     PRINT
   END CASE
NEXT C; *T23947
RETURN
*
*---- PRINTER LINE-UP
*
9000 *
X = 'N'
LOOP
  PRINTER ON
  PRINT "MM/DD/YY   99:99:99"
  PRINT SPACE(32):"JOB # : 99999999  RUN # : 9999"
  PRINT
  PRINT
  PRINT SPACE(15):STR('X',30):SPACE(5):STR('X',7):SPACE(2):"MM/DD/YY"
  FOR N = 1 TO 5
    PRINT SPACE(3):STR('X',30)
  NEXT N
  PRINT
  PRINT SPACE(4):STR('X',30):"   ":STR('X',10)
  PRINT SPACE(4):STR('X',30):"   XXX"
  FOR N = 1 TO 3
    PRINT SPACE(4):STR('X',30)
  NEXT LN
  PRINT
  PRINT SPACE(4):"ATTN: ":STR('X',20)
  PRINT
  PRINT
  PRINT SPACE(42):'X':SPACE(27):'XXX'
  PRINT;PRINT
  PRINT SPACE(19):"9,999,999 ":STR('X',45)
  FOR J = 1 TO 3
    PRINT SPACE(29):STR('X',45)
  NEXT J
  FOR R = 5 TO 30
    PRINT
  NEXT R
  PRINT;PRINT
  PRINT " 999 999":SPACE(25):"99999999"
  FOR N = 1 TO 5
    PRINT SPACE(50):STR('X',30)
  NEXT N
  PRINT
  PRINTER OFF
  PRINTER CLOSE
  LOOP
    PRINT @(0,10):CL:@(5,10):'Are the forms aligned? (RET/N) ':
    INPUT X:
  WHILE X # '' AND X # 'N' DO REPEAT
WHILE X = 'N' DO
  SLEEP 5
REPEAT
PRINT @(0,12):CL:@(5,12):'Bills-of-Lading now printing'
SLEEP 10
RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000 *
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000 *
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
PRINTER OFF
END
