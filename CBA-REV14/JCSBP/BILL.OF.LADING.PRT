*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - DRSBP
* PROGRAM     - BILL.OF.LADING.PRT
* AUTHOR      - L. ROSS, C.B.A.
* DATE        - 09/25/91
* DESCRIPTION
* This program prints the bills of lading for a job.
*********************************************************************
*
***** INSERT FILES EQUATES
*
*COPY>JCS.CPYLIB>DRS.JOB.SPEC
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>CUST.TEMPLATE.LIST
*COPY>JCS.CPYLIB>SHIPTO.MASTER
*COPY>JCS.CPYLIB>TEMPLATE.DETAIL
*COPY>JCS.CPYLIB>FOLD.CODE
*COPY>JCS.CPYLIB>PACK.CODE
*COPY>JCS.CPYLIB>DISTRIBUTION.CODE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
      OPEN '','JOB' TO JOB ELSE
         ERRMSG ='JOB FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','DRS.JOB.SPEC' TO DRS.JOB.SPEC ELSE
         ERRMSG ='DRS.JOB.SPEC FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CUST.TEMPLATE.LIST' TO CUST.TEMPLATE.LIST ELSE
         ERRMSG = 'CUST.TEMPLATE.LIST FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','SHIPTO.MASTER' TO SHIPTO.MASTER ELSE
         ERRMSG = 'SHIPTO.MASTER FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','TEMPLATE.DETAIL' TO TEMPLATE.DETAIL ELSE
         ERRMSG = 'TEMPLATE.DETAIL FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CUSTOMER' TO CUSTOMER ELSE
         ERRMSG = 'CUSTOMER FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','FOLD.CODE' TO FOLD.CODE ELSE
         ERRMSG = 'FOLD.CODE FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','PACK.CODE' TO PACK.CODE ELSE
         ERRMSG = 'PACK.CODE FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','DISTRIBUTION.CODE' TO DISTRIBUTION.CODE ELSE
         ERRMSG = 'DISTRIBUTION.CODE FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
         ERRMSG = 'SHIP.VIA FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','COMPANY' TO COMPANY ELSE
         ERRMSG = 'COMPANY FILE IS MISSING'
         GOTO 93000
      END
*
**** READ PROC VALUES
*
      PROCREAD ID ELSE
         ERRMSG = 'CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      CONO = ID<1>
      CONO.NAME = ID<2>
      JOB.NO = ID<3>
      CUST.NO = ID<4>
      TEMP.NO = ID<3>
      COPIES = ID<5>
      STRT = ID<6>
      LST = ID<7>
      IF STRT = 'A' THEN STRT = 1
      IF (NOT(NUM(STRT))) THEN STRT = 1
      IF LST = 'A' THEN LST = 9999
      IF (NOT(NUM(LST))) THEN LST = 9999
*
**** INITIALIZATION
*
      MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC =''
      LN.CNT = 0
      CO.CS = CO.CITY:', ':CO.STATE
      SHIPPER.ID = '0327-1438001'
*
      PROMPT''
      CRT @(-1)
      LOOP
        CRT @(0,10):CL:@(5,10):'Print (M)ask or (B)ills : ':
        INPUT X:
      WHILE X # 'M' AND X # 'B' DO REPEAT
      IF X = 'M' THEN GOSUB 9000
      IF COPIES > 1 THEN
         PERFORM "SP-ASSIGN =STANDARD ":COPIES
      END
      PRINTER ON
*
***** READ AND PROCESS FIRST RECORD
*
100 *
      READNEXT KEY ELSE GOTO 99999
      MATREAD DJS.REC FROM DRS.JOB.SPEC,KEY ELSE GOTO 99999
      MATREAD CUST.REC FROM CUSTOMER, CONO:DJS.CUST ELSE
         CUST.NAME = '??????????'
      END
      MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE
         JOB.DESC = '??????????'
      END
      MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, CONO:CUST.NO:"!":TEMP.NO:"!001" ELSE
         ERRMSG = 'The CUST.TEMPLATE.LIST record for this job is missing'
         GOSUB 91000
         GOTO 99999
      END
      SHIPTO = CTL.SHIPS<1,1>
      IF SHIPTO = '' THEN
         ERRMSG = 'The first record of CUST.TEMPLATE.LIST has no shipto# - run resequencing'
         GOSUB 91000
         GOTO 99999
      END
      SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
      MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
         MAT TDT.REC = ''
      END
      RSEQ = '001'
      KEY = CONO:CUST.NO:"!":TEMP.NO:"!"
*
***** READ AND PROCESS ALL RECORDS
*
      DATA = 1
      RUN.NO = 0
      LOOP
         IF SHIP.CNT < 1 THEN
            RSEQ =RSEQ + 1
            IF LEN(RSEQ) < 3 THEN RSEQ = STR('0',3-LEN(RSEQ)):RSEQ
            MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, KEY:RSEQ THEN
               SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
            END ELSE DATA = 0
            IF SHIP.CNT < 1 THEN DATA = 0
         END
         IF RUN.NO >= LST THEN DATA = 0
      WHILE DATA = 1 DO
         FOR I = 1 TO SHIP.CNT
             SHIPTO = CTL.SHIPS<1,I>
             MATREAD SHM.REC FROM SHIPTO.MASTER, CONO:SHIPTO ELSE
                MAT SHM.REC = ''
             END
             MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
                MAT TDT.REC = ''
             END
             IF TDT.SHIP.VIA # "" THEN SHM.DT.SHPVIA = TDT.SHIP.VIA
             IF SHM.DT.SHPVIA # '' THEN
                MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:SHM.DT.SHPVIA ELSE MAT SHIP.VIA.REC = ''
             END
             RUN.NO = RUN.NO + 1
             HH.LN1 = SPACE(3):CO.NAME 'L#34':'RUN #':RUN.NO 'R#4':SPACE(7):JOB.NO 'L#30'
             HH.LN2 = SPACE(3):CO.CS 'L#30':SPACE(8):OCONV(DATE(), 'DY')[3,2]:SPACE(7):'X':SPACE(20):SHIPPER.ID 'L#12'
             IF TDT.QTY > 0 AND RUN.NO >= STRT AND RUN.NO <= LST THEN GOSUB 1000
          NEXT I
          SHIP.CNT = 0
       REPEAT
       GOTO 99999
*
**** DETAIL ROUTINE
*
1000 *
*
      SKIDS = 0
      BOXES = 0
      MATREAD PCK.REC FROM PACK.CODE, CONO:TDT.PACK ELSE MAT PCK.REC = ''
      IF TDT.BOL = 'Y' THEN
         IF DJS.SKD.MAX > 0 AND TDT.SKID.LAB = 'Y' THEN
            SKIDS = INT(TDT.QTY / DJS.SKD.MAX)
            IF REM(TDT.QTY,DJS.SKD.MAX) >= DJS.SKD.MIN THEN SKIDS = SKIDS + 1
         END
         IF DJS.BOX.MAX > 0 AND TDT.CART.LAB ='Y' THEN
            BOXES = INT(TDT.QTY / DJS.BOX.MAX)
            IF REM(TDT.QTY,DJS.BOX.MAX) >= DJS.BOX.MIN THEN BOXES = BOXES + 1
         END
         IF DJS.PPL > 0 THEN LBS = OCONV(ICONV(TDT.QTY / DJS.PPL, 'MD0'), 'MD0') ELSE LBS = 0
         INST.LINES = ''
         LN = 1
         IF TDT.SP.INST.1.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.1;LN=LN+1
         IF TDT.SP.INST.2.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.2;LN=LN+1
         IF TDT.SP.INST.3.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.3;LN=LN+1
         IF TDT.SP.INST.4.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.4;LN=LN+1
         IF TDT.SP.INST.5.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.5;LN=LN+1
         IF TDT.SP.INST.6.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.6;LN=LN+1
         IF TDT.SP.INST.7.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.7;LN=LN+1
         IF TDT.SP.INST.8.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.8;LN=LN+1
         IF TDT.SP.INST.9.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.9;LN=LN+1
         IF TDT.SP.INST.10.USE<1,4> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.10
         PRINT HH.LN1
         PRINT HH.LN2
         PRINT
         PRINT SPACE(6):SHPV.DESC 'L#30'
         PRINT
         PRINT SPACE(8):SHM.DT.NAME 'L#30':
         IF SHM.DT.ATTN # '' THEN
            IF LEN(SHM.DT.ATTN) > 23 THEN SHM.DT.ATTN = SHM.DT.ATTN[1,23]
            PRINT '  (':SHM.DT.ATTN:')'
         END ELSE PRINT
         PRINT
         PRINT SPACE(8):SHM.DT.CITY 'L#27':'    ':SHM.DT.STATE 'L#2':' ':SHM.DT.ZIP
         PRINT
         PRINT SPACE(38):SHM.DT.ADDR1 'L#27'
         PRINT; PRINT
         PRINT SPACE(67):'TO BE PAID BY'
         PRINT SPACE(66):CO.NAME 'L#18'
         PRINT SPACE(8):'NEWSPAPER SUPPLEMENTS ITEM 161900, CLASS 60'
         PRINT
         IF SKIDS > 0 THEN
            PRINT OCONV(SKIDS,'MD0') 'R#5':'  PALLETS':
         END ELSE
            PRINT OCONV(BOXES,'MD0') 'R#5':'    BOXES':
         END
         PRINT ' OF INSERTS,':OCONV(TDT.QTY,'MD0,') 'R#9':' PIECES ':OCONV(LBS,'MD0,') 'R#6':' LBS'
         PRINT
         PRINT SPACE(8):'FOR  ':SHM.SP.NAME 'L#30'
         PRINT SPACE(13):SHM.SP.ADDR1 'L#30'
         IF SHM.SP.ADDR2 # '' THEN
            PRINT SPACE(13):SHM.SP.ADDR2 'L#30'
            PRINT SPACE(13):SHM.SP.CITY:' ':SHM.SP.STATE:'  ':SHM.SP.ZIP
         END ELSE
            PRINT SPACE(13):SHM.SP.CITY:' ':SHM.SP.STATE:'  ':SHM.SP.ZIP
            PRINT
         END
         PRINT
         PRINT SPACE(9):'DATED MATERIAL - DO NOT DELAY'
         FOR J = 1 TO 3
           PRINT SPACE(7):INST.LINES<J> 'L#59'
         NEXT J
         PRINT;PRINT; PRINT
         PRINT '  ':CO.NAME
         PRINT '  ':CO.ADDRESS
         PRINT '  ':(CO.CS:'  ':CO.ZIP) 'L#30':'PHONE ':'402-453-1402'
         FOR X = 1 TO 9
            PRINT
         NEXT X
      END
      RETURN
*
**** SETUP C/S/Z LINE
*
1500 *
      DUMMY = ''
      DUMMY2 = DUMMY2[1,2]
      LC = MXL - LEN(DUMMY3) - LEN(DUMMY2) - 3
      DUMMY1 = DUMMY1[1,LC]
      DUMMY = (DUMMY1 : ' ':DUMMY2 : '  ' : DUMMY3)
      RETURN
*
*---- PRINTER LINE-UP
*
9000 *
      X = 'N'
      LOOP
         PRINTER ON
         FOR X = 1 TO 2
            PRINT SPACE(3):STR('X',30)
         NEXT X
         PRINT
         PRINT SPACE(6):STR('X',30)
         PRINT
         PRINT SPACE(8):STR('X',30)
         PRINT
         PRINT SPACE(8):STR('X',30)
         PRINT
         PRINT SPACE(38):STR('X',27)
         PRINT; PRINT; PRINT; PRINT
         PRINT SPACE(8):STR('X',36)
         PRINT
         PRINT '99999  ':STR('X',46)
         PRINT
         PRINT SPACE(8):'XXX  ':STR('X',30)
         PRINT SPACE(13):STR('X',30)
         PRINT SPACE(13):STR('X',30)
         PRINT SPACE(13):STR('X',30)
         PRINT
         PRINT SPACE(9):STR('X',29)
         FOR X = 1 TO 3
            PRINT SPACE(7):STR('X',58)
         NEXT X
         PRINT; PRINT; PRINT
         PRINT SPACE(2):STR('X',28)
         PRINT SPACE(2):STR('X',28)
         PRINT SPACE(2):STR('X',48)
         FOR X = 1 TO 9
            PRINT
         NEXT X
         PRINTER OFF
         PRINTER CLOSE
         LOOP
            PRINT @(0,10):CL:@(5,10):'Are the forms aligned? (RET/N) ':
            INPUT X:
         WHILE X # '' AND X # 'N' DO REPEAT
      WHILE X = 'N' DO
         SLEEP 5
      REPEAT
      PRINT @(0,12):CL:@(5,12):'Bills-of-Lading now printing'
      SLEEP 10
      RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 *
      ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
      PRINTER OFF
      END
