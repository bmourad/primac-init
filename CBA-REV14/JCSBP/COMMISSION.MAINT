*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - COMMISSION.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/05/85
* DESCRIPTION -
* This program updates and maintains the COMMISSION records.
*T23278 markt 01/05/1999 * Add check for divisional security.
*T25978 adelgado 02/14/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*T25740 thompson 05/10/2002 * FIX DISPLAY PROBLEM OVER 99 LINES
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>COMMISSION
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>SALESMAN
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- DIMENSION TABLES
*
   DIM COMM.TBL(10)
   DIM SAVE.COMM.REC(5)
*
*---- OPEN FILES
*
   OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE MISSING";GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
   OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG = "SALESMAN FILE MISSING";GOTO 93000
   OPEN '','JOB' TO JOB ELSE ERRMSG = "JOB FILE MISSING";GOTO 93000
*
*---- GET COMPANY NUMBER
*
   CONO = ''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
   IF CO.COMMISSION # "Y" THEN GOTO 99999
   OPEN '','COMMISSION' TO COMMISSION ELSE ERRMSG = "COMMISSION FILE MISSING";GOTO 93000
*
*---- INITIALIZATION
*
   MAT EDIT.COM = ''
   TYP = 0
   CALL EDIT.SUB
   MAT EDIT.COM.DRIVER = ''
   LINE.SPACE = 1
   PAGE.SIZE = 13
   BEGIN.PAGE = 7
   LINES = 0
   START.LINE = 0
   OLD.LINES = 0
   OPTION = ''
   UNKNOWN = STR('?',30)
   MAT COMM.REC = ""
   MAT COMM.DRAW.REC = ""
   MAT COMM.TBL = ""
   MAT SAVE.COMM.REC = ""
   OLD.COMM.DRAW = ""
*
*---- GET SCREEN
*
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = "COMMISSION.MAINT"
   ECD.ACTION = 1;CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
   RELEASE        ;* T26126
   LN  =  1
   OLD.START.LINE  =  0
   LINES  =  0
   MAT COMM.TBL = ""
   SCV.REC(1)<ECD.SCRN.NO,1> = DATE()
   ECD.NUM = 1;ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 2;ECD.ACTION = 4;CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = 'END'
         GOTO 99999
      CASE ECD.RET.VALUE # ''
         SLSMN.NO = ECD.RET.VALUE
         MATREAD SALESMAN.REC FROM SALESMAN,CONO:SLSMN.NO ELSE
            ERRMSG = "INVALID SALESREP NUMBER"
            GOSUB 91000
            GOTO 5
         END
         SCV.REC(14)<ECD.SCRN.NO> = SALS.NAME
         ECD.NUM = 14;ECD.ACTION = 5;CALL SCRN.EDIT
   END CASE
  * T26126 v
   MATREADU COMM.DRAW.REC FROM COMMISSION,CONO:"!":SLSMN.NO LOCKED
      ERRMSG = 'COMMISSION record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 5 
   END ELSE
      MAT COMM.DRAW.REC = ""
   END
  * T26126 ^
   IF COMM.DRAW = "" THEN
      GOSUB 10
   END ELSE
      OLD.COMM.DRAW = COMM.DRAW
      SCV.REC(15)<ECD.SCRN.NO> = COMM.DRAW
      ECD.NUM = 15;ECD.ACTION = 5;CALL SCRN.EDIT
   END
   COM.JOB = ""
   I = 1
   J.CNT = COUNT(COMM.JOBS,VM) + (COMM.JOBS # "")
   FOR JJ = 1 TO J.CNT
      MATREAD JOB.REC FROM JOB,CONO:COMM.JOBS<1,JJ> ELSE MAT JOB.REC = ""
*T23278 v
      DIV.CODE = JOB.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
      CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
      IF ERRMSG # '' THEN GOTO 9
*T23278 ^
      BEGIN CASE
         CASE JOB.STATUS<1,1> = "9"
            GOTO 9
         CASE JOB.STATUS<1,1> = "8"
            GOTO 9
      END CASE
      COM.JOB<1,I> = COMM.JOBS<1,JJ>
      I = I + 1
9  NEXT JJ
   J.CNT = COUNT(COM.JOB,VM) + (COM.JOB # "")
   FOR JJ = 1 TO J.CNT
      MATREAD COMM.REC FROM COMMISSION,CONO:COM.JOB<1,JJ> ELSE MAT COMM.REC = ""
      COMM.TBL(1)<1,JJ> = COM.JOB<1,JJ>
      I.CNT = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "")
      COMM.TBL(2)<1,JJ> = COMM.INV.NO<1,I.CNT>
      COMM.TBL(3)<1,JJ> = COMM.INV.LAST.DATE
      COMM.TBL(4)<1,JJ> = COMM.INV.TOT.AMT
      COMM.TBL(5)<1,JJ> = COMM.LAST.PMT.DATE
      COMM.TBL(6)<1,JJ> = COMM.INV.TOT.BAL
      COMM.TBL(7)<1,JJ> = COMM.CALC + COMM.ADJ
      COMM.TBL(8)<1,JJ> = COMM.PAID
   NEXT JJ
   LINES = COUNT(COMM.TBL(1),VM) + (COMM.TBL(1) # "")
   GOSUB 160
   GOSUB 1000
   GOSUB 2000
   GOTO 5
*
*---- ENTER SALESMAN DRAW
*
10*
   ECD.NUM = 15
   ECD.ACTION = 4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      COMM.DRAW = ECD.RET.VALUE
   END
   RETURN
*
*---- ENTER SCREEN
*
140*
   GOSUB 160
   SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
   P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
   P_X  := AM:0 ; P_Y := AM:SLN ; P_VALUE := AM:LN "R#2"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
141*
   X = 62;Y = SLN;TYP = 4;MAXL = 8;O.R = "O";MAXV = 99999999;MINV = "";SCALER = 2; *T25740
   DEFAULT = OCONV(COMM.TBL(7)<1,LN>,"MD2");HMSG = "ENTER COMMISSION DUE"
   CALL EDIT.SUB
   BEGIN CASE
      CASE VALUE = ''
         IF OPTION = "C" THEN GOTO 141
         GOTO 149
      CASE VALUE = "END"
         P_X  = 62 ; P_Y = SLN ; P_VALUE = COMM.TBL(7)<1,LN>"MD2""R#8" ; P_OPT = ""; *T25740
         P_X  := AM:72 ; P_Y := AM:SLN ; P_VALUE := AM:COMM.TBL(8)<1,LN>"MD2""R#8"; *T25740
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 149
      CASE VALUE # ''
         COMM.TBL(7)<1,LN> = VALUE
   END CASE
142*
   X = 72;Y = SLN;TYP = 4;MAXL = 9;O.R = "O";MAXV = 99999999;MINV = "";SCALER = 2; * T25740
   DEFAULT = OCONV(COMM.TBL(8)<1,LN>,"MD2");HMSG = "ENTER COMMISSION PAID"
   CALL EDIT.SUB
   IF VALUE = "END" THEN GOTO 141
   IF VALUE = '' THEN GOTO 142
   COMM.TBL(8)<1,LN> = VALUE
149*
   JOB.NUM = COMM.TBL(1)<1,LN>
   LOCATE JOB.NUM IN SAVE.COMM.REC(1)<1>,1 SETTING C ELSE
      C = COUNT(SAVE.COMM.REC(1),VM) + (SAVE.COMM.REC(1) # "") + 1
      SAVE.COMM.REC(1)<1,C> = JOB.NUM
   END
   SAVE.COMM.REC(2)<1,C> = COMM.TBL(7)<1,LN>
   SAVE.COMM.REC(3)<1,C> = COMM.TBL(8)<1,LN>
   LINES = COUNT(COMM.TBL(1),VM) + (COMM.TBL(1) # '')
   RETURN
*
*---- SCROLL ROUTINE
*
160*
   START.LINE = 1 + INT((LN-1) / PAGE.SIZE) * PAGE.SIZE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   IF LAST.LINE > LINES THEN LAST.LINE = LINES
   IF START.LINE = OLD.START.LINE THEN GOTO 169
   OLD.START.LINE = START.LINE
   CNT = 1
   FOR L = START.LINE TO LAST.LINE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(L-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = L "R#3" ; P_OPT = ""; *T25740
      P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:COMM.TBL(1)<1,L>"L#8"; *T25740
      P_X  := AM:13 ; P_Y := AM:SLN ; P_VALUE := AM:COMM.TBL(2)<1,L>"L#8"; *T25740
      P_X  := AM:22 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(COMM.TBL(3)<1,L>,"D2/")"R#8"; *T25740
      P_X  := AM:31 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(COMM.TBL(4)<1,L>,"MD2")"R#10"; *T25740
      P_X  := AM:42 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(COMM.TBL(5)<1,L>,"D2/")"R#8"; *T25740
      P_X  := AM:51 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(COMM.TBL(6)<1,L>,"MD2")"R#10"; *T25740
      P_X  := AM:62 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(COMM.TBL(7)<1,L>,"MD2")"R#8"; *T25740
      P_X  := AM:72 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(COMM.TBL(8)<1,L>,"MD2")"R#8"; *T25740
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT L 
   FOR M = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
169*
   RETURN
*
*---- GET LINE NUMBER
*
170*
   GOSUB 160
   ECD.NUM = 12; SCV.REC(ECD.NUM) = ""
   ECD.MINV = START.LINE; ECD.MAXV = LAST.LINE
   ECD.ACTION = 4;CALL SCRN.EDIT
   IF ECD.RET.VALUE = "" OR ECD.RET.VALUE = "END" THEN
      LNO = 0
   END ELSE
      LNO = ECD.RET.VALUE
   END
   RETURN
*
*---- OPTIONS FOR SCREEN
*
1000*
   MORE = 0
   LOOP
      SCV.REC(11)<ECD.SCRN.NO,1> = ''
      ECD.NUM = 11;ECD.ACTION = 4;CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "END" OR OPTION = "E"
            MORE = 1
         CASE NUM(OPTION) = "1"
            GOSUB 10
         CASE OPTION = "S"
            LN = LN + PAGE.SIZE
            IF LN > LINES THEN LN = 1
            GOSUB 160
      * T25978 v
         CASE OPTION = 'SR'
            LN -= PAGE.SIZE
            IF LN < 1 THEN LN = 1
            GOSUB 160
         CASE OPTION = 'ST'
            LN = 1
            GOSUB 160
         CASE OPTION = 'SB'
            LN = LINES
            GOSUB 160
      * T25978 ^
         CASE OPTION = "C"
            GOSUB 170
            IF LNO THEN
               LN = LNO
               SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
               GOSUB 141
            END
            GOSUB 160
         CASE OPTION  =  "F"
            GOSUB 3000
            MORE = 1
         CASE 1
            ERRMSG = "INVALID OPTION";GOSUB 91000
      END CASE
   WHILE MORE = 0 DO REPEAT
   RETURN
*
*---- CLEAR SCREEN
*
2000*
   ECD.ACTION = 6;CALL SCRN.EDIT
   MAT SCV.REC = ""
   RETURN
*
*---- UPDATE COMMISSION FILES
*
3000*
   IF OLD.COMM.DRAW # COMM.DRAW THEN
      MATWRITE COMM.DRAW.REC ON COMMISSION,CONO:"!":SLSMN.NO
   END ELSE
      RELEASE COMMISSION,CONO:SLSMN.NO
   END
   CR.CNT = COUNT(SAVE.COMM.REC(1),VM) + (SAVE.COMM.REC(1) # "")
   FOR W = 1 TO CR.CNT
      JOB.NO = SAVE.COMM.REC(1)<1,W>
      MATREADU COMM.REC FROM COMMISSION,CONO:JOB.NO ELSE
         RELEASE COMMISSION,CONO:JOB.NO
         GOTO 3009
      END
      COMM.DUE = SAVE.COMM.REC(2)<1,W>
      COMM.ADJ = COMM.DUE - COMM.CALC
      COMM.PAID = SAVE.COMM.REC(3)<1,W>
      COMM.PAID.DATE = DATE()
***** T23278 v
      IF COMM.DIV = "" THEN
         MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE MAT JOB.REC = ""
         COMM.DIV = JOB.DIV
      END
***** T23278 ^
      MATWRITE COMM.REC ON COMMISSION,CONO:JOB.NO
3009*
   NEXT W
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000*
   ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000*
   ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
END
