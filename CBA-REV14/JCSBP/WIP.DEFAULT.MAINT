*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - WIP.DEFAULT.MAINT
* BY          - WALID YAMOUT, CBA
* DATE        - 06/23/86
* DESCRIPTION -
*T25978 adelgado 02/15/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*ENDDOC
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*COPY>PMC.CPYLIB>COMPANY
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  ERRMSG = ""
*
*--- OPEN THE FILES
*
  OPEN '','JCS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = "JCS.SCREENS FILE IS MISSING"
    GOSUB 91000
    GOTO 999999
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = "CONTROL FILE IS MISSING"
    GOSUB 91000
    GOTO 999999
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = "COMPANY FILE IS MISSING"
    GOSUB 91000
    GOTO 999999
  END
*
*--- GET COMPANY NUMBER
*
  MAT COMP.REC = ""
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 999999
*
*--- INITIALIZATION
*
*   MAT EDIT.COM = ""
*   TYP = 0
*   CALL EDIT.SUB
*   FILL = "#"
  MAT EDIT.COM.DRIVER = ""
  LINE.SPACE = 1
  PAGE.SIZE = 5
  BEGIN.PAGE = 4
  LINES = 0
  LN = 1
  OLD.START.LINE = 0
  START.LINE = 0
  OLD.LINES = 0
  OPTION = ""
  WIP.DESC = ""
  MAIN.DESC = "OTHER - "
  ALL.TYPE = "LB"
  ALL.TYPE<1,2> = "MT"
  ALL.TYPE<1,3> = "OS"
  ALL.TYPE<1,4> = "SP"
  ALL.TYPE<1,5> = "MS"
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "WIP.DEFAULT.MAINT"
  ECD.ACTION = 1
  CALL SCRN.EDIT
100*
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2
  CALL SCRN.EDIT
  * T26126 v
  READU WIP.TYPE FROM CONTROL, CONO : "WIP.DEFAULT" LOCKED
    ERRMSG = 'WIP record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 99990 
  END ELSE
    WIP.TYPE = ""
  END
  * T26126 6
  IF WIP.TYPE = "" THEN
    OPTION = "A"
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 1000
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END ELSE
    LINES = COUNT(WIP.TYPE,VM) + (WIP.TYPE # "")
    FOR LN = 1 TO LINES
      BEGIN CASE
        CASE WIP.TYPE<1,LN> = "LB"
          WIP.DESC<1,LN> = "LABOR"
        CASE WIP.TYPE<1,LN> = "MT"
          WIP.DESC<1,LN> = "MATERIAL"
        CASE WIP.TYPE<1,LN> = "OS"
          WIP.DESC<1,LN> = "OUTSIDE PURCHASE"
        CASE WIP.TYPE<1,LN> = "SP"
          WIP.DESC<1,LN> = "SHIPPING"
        CASE WIP.TYPE<1,LN> = "MS"
          WIP.DESC<1,LN> = "MISCELLANEOUS"
        CASE WIP.TYPE<1,LN> = "OTH"
          WIP.DESC<1,LN> = MAIN.DESC
        CASE 1
          WIP.DESC<1,LN> = "ALL"
      END CASE
    NEXT LN
    GOSUB 8000
    LN = 1
  END
  GOSUB 10000
*
*--- ENTER OPTION
*
500*
  ECD.NUM = 1
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
  ECD.ACTION = 4
  CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "E" OR OPTION = "END"
      RELEASE CONTROL, CONO : "WIP.DEFAULT"
      GOTO 99990
    CASE OPTION = "A"
      LOOP
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 1000
      WHILE LINES > OLD.LINES DO REPEAT
      GOSUB 8000
      LN = LINES
    CASE OPTION = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
    * T25978 v
    CASE OPTION = 'SR'
      LN -= PAGE.SIZE
      IF LN < 1 THEN LN = 1
    CASE OPTION = 'ST'
      LN = 1
    CASE OPTION = 'SB'
      LN = LINES
    * T25978 ^
    CASE OPTION = "C"
      GOSUB 70000
      IF LNO THEN
        LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
        GOSUB 1010
      END
    CASE OPTION = "D"
      GOSUB 70000
      IF LNO THEN
        LN = LNO
        IF LN = 1 AND LINES = 2 AND WIP.TYPE<1,2> = "OTH" THEN
          ERRMSG = "Cannot have (OTH) on first line"
          GOSUB 91000
          GOTO 500
        END
        WIP.TYPE = DELETE(WIP.TYPE,1,LN,0)
        WIP.DESC = DELETE(WIP.DESC,1,LN,0)
        GOSUB 8000
        LINES = COUNT(WIP.TYPE,VM) + (WIP.TYPE # "")
        IF LN > 1 AND LN > LINES THEN LN = LN - 1
        IF LN < 1 THEN LN = 1
        OLD.START.LINE = 0
      END
    CASE OPTION = "F"
      LINES = COUNT(WIP.TYPE,VM) + (WIP.TYPE # "")
      IF LINES THEN
        WRITE WIP.TYPE ON CONTROL, CONO : "WIP.DEFAULT"
      END ELSE
        DELETE CONTROL, CONO : "WIP.DEFAULT"
      END
      GOTO 99990
  END CASE
  GOSUB 10000
  GOTO 500
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- MULTI LINE ENTRY
*
1000*
  IF LINES >= 5 OR WIP.TYPE = "ALL" OR (LINES AND WIP.TYPE<1,LINES> = "OTH") THEN GOTO 1999
  GOSUB 10000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
  P_X  := AM:0 ; P_Y := AM:SLN ; P_VALUE := AM:LN "R#3"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
1010*
  X = 5
  Y = SLN
  TYP = 1
  MAXL = 3
  O.R = "O"
  HMSG = "Enter type (ALL)"
  IF WIP.TYPE<1,LN> # "" THEN
    DEFAULT = WIP.TYPE<1,LN>
  END ELSE
    BEGIN CASE
      CASE LN = 1
        DEFAULT = "ALL"
      CASE LN # 5
        DEFAULT = "OTH"
    END CASE
  END
  VALDAT = "ALL"
  IF WIP.TYPE<1,LN> = "LB" THEN
    VALDAT = VALDAT : VM : "LB"
    HMSG = HMSG : ", (LB)"
  END ELSE
    LOCATE "LB" IN WIP.TYPE<1>,1 SETTING FND ELSE
      VALDAT = VALDAT : VM : "LB"
      HMSG = HMSG : ", (LB)"
    END
  END
  IF WIP.TYPE<1,LN> = "MT" THEN
    VALDAT = VALDAT : VM : "MT"
    HMSG = HMSG : ", (MT)"
  END ELSE
    LOCATE "MT" IN WIP.TYPE<1>,1 SETTING FND ELSE
      VALDAT = VALDAT : VM : "MT"
      HMSG = HMSG : ", (MT)"
    END
  END
  IF WIP.TYPE<1,LN> = "OS" THEN
    VALDAT = VALDAT : VM : "OS"
    HMSG = HMSG : ", (OS)"
  END ELSE
    LOCATE "OS" IN WIP.TYPE<1>,1 SETTING FND ELSE
      VALDAT = VALDAT : VM : "OS"
      HMSG = HMSG : ", (OS)"
    END
  END
  IF WIP.TYPE<1,LN> = "SP" THEN
    VALDAT = VALDAT : VM : "SP"
    HMSG = HMSG : ", (SP)"
  END ELSE
    LOCATE "SP" IN WIP.TYPE<1>,1 SETTING FND ELSE
      VALDAT = VALDAT : VM : "SP"
      HMSG = HMSG : ", (SP)"
    END
  END
  IF WIP.TYPE<1,LN> = "MS" THEN
    VALDAT = VALDAT : VM : "MS"
    HMSG = HMSG : ", (MS)"
  END ELSE
    LOCATE "MS" IN WIP.TYPE<1>,1 SETTING FND ELSE
      VALDAT = VALDAT : VM : "MS"
      HMSG = HMSG : ", (MS)"
    END
  END
  IF LN > 1 AND LN < 5 THEN
    IF WIP.TYPE<1,LN> = "OTH" THEN
      VALDAT = VALDAT : VM : "OTH"
      HMSG = HMSG : ", (OTH)"
    END ELSE
      LOCATE "OTH" IN WIP.TYPE<1>,1 SETTING FND ELSE
        VALDAT = VALDAT : VM : "OTH"
        HMSG = HMSG : ", (OTH)"
      END
    END
  END
  CALL EDIT.SUB
  BEGIN CASE
    CASE (VALUE = "" OR VALUE = "END") AND OPTION = "A"
      WIP.TYPE = DELETE(WIP.TYPE,1,LN,0)
      WIP.DESC = DELETE(WIP.DESC,1,LN,0)
      GOTO 1999
    CASE VALUE = "END"
      OLD.START.LINE = 0
      GOTO 1999
    CASE VALUE = ""
      GOTO 1010
    CASE VALUE = "ALL"
      WIP.TYPE = VALUE
      WIP.DESC = VALUE
      LN = 1
      OLD.START.LINE = 0
    CASE VALUE = "OTH"
      BEGIN CASE
        CASE LN = 1
          ERRMSG = "Cannot enter (OTH) on first line"
          GOSUB 91000
          GOTO 1010
        CASE LN = 5
          ERRMSG = "Cannot enter (OTH) on fifth line"
          GOSUB 91000
          GOTO 1010
        CASE 1
          FOR I = LN TO 5
            WIP.TYPE = DELETE(WIP.TYPE,1,I,0)
            WIP.DESC = DELETE(WIP.DESC,1,I,0)
          NEXT I
          WIP.TYPE<1,LN> = VALUE
          WIP.DESC<1,LN> = MAIN.DESC
          OLD.START.LINE = 0
      END CASE
    CASE VALUE = "LB"
      LOCATE VALUE IN WIP.TYPE<1>,1 SETTING FND ELSE FND = 0
      IF FND AND FND # LN THEN
        ERRMSG = "Type (":VALUE:") was entered on line - ":FND
        GOSUB 91000
        GOTO 1010
      END ELSE
        WIP.TYPE<1,LN> = VALUE
        WIP.DESC<1,LN> = "LABOR"
      END
    CASE VALUE = "MT"
      LOCATE VALUE IN WIP.TYPE<1>,1 SETTING FND ELSE FND = 0
      IF FND AND FND # LN THEN
        ERRMSG = "Type (":VALUE:") was entered on line - ":FND
        GOSUB 91000
        GOTO 1010
      END ELSE
        WIP.TYPE<1,LN> = VALUE
        WIP.DESC<1,LN> = "MATERIAL"
      END
    CASE VALUE = "OS"
      LOCATE VALUE IN WIP.TYPE<1>,1 SETTING FND ELSE FND = 0
      IF FND AND FND # LN THEN
        ERRMSG = "Type (":VALUE:") was entered on line - ":FND
        GOSUB 91000
        GOTO 1010
      END ELSE
        WIP.TYPE<1,LN> = VALUE
        WIP.DESC<1,LN> = "OUTSIDE PURCHASE"
      END
    CASE VALUE = "SP"
      LOCATE VALUE IN WIP.TYPE<1>,1 SETTING FND ELSE FND = 0
      IF FND AND FND # LN THEN
        ERRMSG = "Type (":VALUE:") was entered on line - ":FND
        GOSUB 91000
        GOTO 1010
      END ELSE
        WIP.TYPE<1,LN> = VALUE
        WIP.DESC<1,LN> = "SHIPPING"
      END
    CASE VALUE = "MS"
      LOCATE VALUE IN WIP.TYPE<1>,1 SETTING FND ELSE FND = 0
      IF FND AND FND # LN THEN
        ERRMSG = "Type (":VALUE:") was entered on line - ":FND
        GOSUB 91000
        GOTO 1010
      END ELSE
        WIP.TYPE<1,LN> = VALUE
        WIP.DESC<1,LN> = "MISCELLANEOUS"
      END
    CASE 1
      GOTO 1010
  END CASE
  P_X  = 11 ; P_Y = SLN ; P_VALUE = WIP.DESC<1,LN> "L#23" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
1999*
  LINES = COUNT(WIP.TYPE,VM) + (WIP.TYPE # "")
  GOSUB 8000
  RETURN
*
*--- GET DESCRIPTION FOR OTHER TYPE
*
8000*
  LOCATE "OTH" IN WIP.TYPE<1>,1 SETTING PTR ELSE PTR = 0
  IF PTR THEN
    OLD.START.LINE = 0
    WIP.DESC<1,PTR> = MAIN.DESC
    FOR XX = 1 TO 5
      LOCATE ALL.TYPE<1,XX> IN WIP.TYPE<1>,1 SETTING FND ELSE
        IF WIP.DESC<1,PTR> # MAIN.DESC THEN
          WIP.DESC<1,PTR> = WIP.DESC<1,PTR> : ","
        END
        WIP.DESC<1,PTR> = WIP.DESC<1,PTR> : ALL.TYPE<1,XX>
      END
    NEXT XX
  END
  RETURN
*
*--- SCROLL ROUTINE
*
10000*
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE = OLD.START.LINE THEN RETURN
  OLD.START.LINE = START.LINE
  CNT = 1
  FOR N = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
    P_X  := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:WIP.TYPE<1,N> "L#3"
    P_X  := AM:11 ; P_Y := AM:SLN ; P_VALUE := AM:WIP.DESC<1,N> "L#23"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
  RETURN
*
*--- GET LINE NUMBER
*
70000*
  GOSUB 10000
  ECD.NUM = 2
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
  ECD.MINV = START.LINE
  ECD.MAXV = LAST.LINE
  ECD.ACTION = 4
  CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*--- ERROR ROUTINE
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
*    PRINT @(0,23) : CL : ERRMSG :
*    INPUT REPLY,1 :
*    PRINT @(0,23) : CL :
*    RETURN
  GOTO 999999
99990 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
999999*
*    PRINT @(-1)
END
