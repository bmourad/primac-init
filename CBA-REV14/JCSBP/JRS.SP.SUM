      SUBROUTINE JRS.SP.SUM
*COPY>JCS.CPYLIB>COM.JRS
**************************************************************
* REVISION    - [08.0]
* COPYRIGHT   - 1982 by C.B.A.  (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JRS.SP.SUM
* BY          - BILAL MOGHRABI, C.B.A.
* DATE        - 06/06/88
* DESCRIPTION - Program produces a Job Shipping Posted report.
* T26556 ajibaly 05/09/2002 * Use report # and GET.PROG.HEAD.
* T2896wvaughan 08/07/2006 * Extend JOB.QTY to display 8 numbers.
*ENDDOC
**************************************************************
*
*-- FILE EQUATES
*
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>OPERATION
*COPY>JCS.CPYLIB>JOB.SHIP
*COPY>JCS.CPYLIB>JOB.RPT.SET
*COPY>JCS.CPYLIB>JOB.RPT.SUM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
      DIM CC.TOT.REC(4)
      EQU CC.QTY    TO CC.TOT.REC(1)
      EQU CC.COST   TO CC.TOT.REC(2)
      EQU CC.S.QTY  TO CC.TOT.REC(3)
      EQU CC.S.COST TO CC.TOT.REC(4)
      DIM DP.TOT.REC(2)
      EQU DP.QTY    TO DP.TOT.REC(1)
      EQU DP.COST   TO DP.TOT.REC(2)
      DIM TOT.REC(2)
      EQU TOT.QTY    TO TOT.REC(1)
      EQU TOT.COST   TO TOT.REC(2)
*
*-- INITIALIZATION
*
      PG.NO=0
      LINE.CNT=75
*T26556 v
*     RPT.NO="XXXX"
      HD1 = ""; HD2 = ""
      RPT.NAME = ""
      RPT.DATE = DATE()
      CALL GET.PROG.HEAD(CONO,C.CO.NAME,RPT.NAME,RPT.NO,RPT.DATE,HD1,HD2)
      IF JRSD.CONS = "Y" THEN
         CONSOL = "CONSOLIDATED"
      END ELSE
         CONSOL = "            "
      END
      PRINTER ON
*T26556 ^
      UNKNOWN=STR("?",30)
      SPC = SPACE(1)
      NEW.LEN = 30
      NEW.DESC = ""
      HLINE1=SPACE(72):"SHIP"
      HLINE2="CCTR OPER":SPACE(12):"SHIP-TO":SPACE(22):"DESCRIPTION":SPACE(11):"DATE      CARRIER/FRT #":SPACE(4):"INVOICE #  QUANTITY   AMOUNT"
      HLINE3="---- ---- ------------------------------ ------------------------------ ----- -------------------- ---------- -------- ----------"
      IF JRSD.CONS="Y" THEN
         HLINE4="#":JOBNO "L#8":SPC:"CONSOLIDATED  REPORT NO: ":RPT.NO "L#4":SPACE(3)
      END ELSE
         HLINE4="#":JOBNO "L#8":SPACE(12):"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
      END
      N=SPACE((50 - LEN(C.CO.NAME)) / 2)
      HLINE5=N:C.CO.NAME:N
*
*-- MAIN PROCESSING
*
      JOB.CNT = DCOUNT(JRS.IDS,AM)
      PTR = 0
      FOR I = 1 TO JOB.CNT WHILE PTR = 0
         IF FIELD(JRS.IDS<I>,'&',5) # "S" THEN GOTO 100
         DPNO = FIELD(JRS.IDS<I>,"&",2)
         IF DP.SEL # "ALL" AND DP.SEL # DPNO THEN GOTO 100
         MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<I> ELSE GOTO 100
         PTR = I
100   NEXT I
      IF PTR = 0 THEN GOTO 99999
      MAT CC.TOT.REC = ""
      MAT DP.TOT.REC = ""
      MAT TOT.REC = ""
      CCNO = FIELD(JRS.IDS<PTR>,"&",3)
      OPNO = FIELD(JRS.IDS<PTR>,"&",4)
      P.DP = DPNO
      P.CC = CCNO
      P.OP = OPNO
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:P.DP ELSE DEPT.DESC=UNKNOWN
      MATREAD OPER.REC FROM OPERATION,CONO:P.OP ELSE OPER.DESC=UNKNOWN
      GOSUB 1000
      PTR = PTR + 1
      FOR I = PTR TO JOB.CNT
         IF FIELD(JRS.IDS<I>,'&',5) # "S" THEN GOTO 200
         DPNO = FIELD(JRS.IDS<I>,"&",2)
         IF DP.SEL # "ALL" AND DP.SEL # DPNO THEN GOTO 200
         MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<I> ELSE GOTO 200
         CCNO = FIELD(JRS.IDS<I>,"&",3)
         OPNO = FIELD(JRS.IDS<I>,"&",4)
         BEGIN CASE
         CASE P.DP # DPNO
            GOSUB 2000
            P.CC = CCNO
            P.OP = OPNO
            MATREAD OPER.REC FROM OPERATION,CONO:P.OP ELSE OPER.DESC=UNKNOWN
            GOSUB 3000
            P.DP = DPNO
            MATREAD DEPT.REC FROM DEPARTMENT,CONO:P.DP ELSE DEPT.DESC=UNKNOWN
            GOSUB 9000
         CASE P.CC # CCNO OR P.OP # OPNO
            GOSUB 2000
            P.CC = CCNO
            P.OP = OPNO
            MATREAD OPER.REC FROM OPERATION,CONO:P.OP ELSE OPER.DESC=UNKNOWN
         END CASE
         GOSUB 1000
200   NEXT I
      GOSUB 2000
      GOSUB 3000
      IF LINE.CNT + 2 >= 55 THEN GOSUB 9000
      PRINT SPACE(110):"-------- ----------"
      PRINT "*** GRAND TOTALS ***":SPACE(90):OCONV(TOT.QTY,"MD0")"R#8":SPC:OCONV(TOT.COST,"MD2")"R#10"
*
      GOTO 99999
*
*-- PRINT THE VALUES
*
1000  MV.CNT = DCOUNT(JRS.SEQ,VM)
      FOR M = 1 TO MV.CNT
         SP.ID = CONO:JOBNO:"!":DPNO:"!":CCNO:"!"
         SMV.CNT = DCOUNT(JRS.SEQ<1,M>,SVM)
         FOR S = 1 TO SMV.CNT
            IF JRS.SUB.JOB<1,M,S> # "" THEN
               SP.ID = CONO:JRS.SUB.JOB<1,M,S>:"!":DPNO:"!":CCNO:"!"
            END
            MATREAD JSP.REC FROM JOB.SHIP, SP.ID:JRS.SEQ<1,M,S> ELSE
               MAT JSP.REC = ""
            END
            OLD.DESC = JSP.DESC<1,1>
            IF LINE.CNT + 2 >= 55 THEN GOSUB 9000
            IF JSP.TYPE # "S" THEN
               CC.QTY = CC.QTY + JSP.QTY
               CC.COST = CC.COST + JSP.COST
            END ELSE
               CC.S.QTY = CC.S.QTY + JSP.QTY
               CC.S.COST = CC.S.COST + JSP.COST
            END
            MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:JSP.VIA ELSE MAT SHIP.VIA.REC = ""
            CALL DESC.REFORMAT(OLD.DESC,NEW.LEN,NEW.DESC)
            PLINE=P.CC"L#4":SPC:P.OP"L#4":SPC:JSP.NAME"L#30":SPC
*           DESC = JSP.DESC<1,1>:" ":JSP.DESC<1,2>
            PLINE = PLINE :NEW.DESC<1,1> "L#30":SPC:OCONV(JSP.DATE, "D2-") "L#5":SPC
            PLINE = PLINE :SHPV.DESC"L#20":SPC:JSP.INV"L#8":"   "
            PLINE = PLINE :OCONV(JSP.QTY,"MD0")"R#8":SPC:OCONV(JSP.COST,"MD2")"R#10"
            IF JSP.TYPE = "S" THEN PLINE = PLINE:" S"
            PRINT PLINE
            ADDR1 = JSP.ADDR3
            IF ADDR1 = "" THEN
               ADDR1 = JSP.ADDR2
            END
            IF ADDR1 = "" THEN
               ADDR1 = JSP.ADDR1
            END
            PLINE=SPACE(10):ADDR1"L#30":SPC:NEW.DESC<1,2> "L#30"
            PLINE = PLINE :SPACE(10):JSP.FRT.NO"L#20":"  ":OCONV(JSP.INV.DATE, "D2-") "R#8"
            PRINT PLINE
            LINE.CNT=LINE.CNT + 2
         NEXT S
      NEXT M
      RETURN
*
*-- PRINT COST CENTER TOTALS
*
2000  IF LINE.CNT >= 55 THEN GOSUB 9000
      PRINT SPACE(110):"-------- ----------"
      PLINE="* ":OPER.DESC"L#22":" TOTAL"
      PLINE=PLINE:SPACE(80):OCONV(CC.QTY,"MD0")"R#8":SPC:OCONV(CC.COST,"MD2")"R#10"
      PRINT PLINE
      IF (CC.S.QTY+0 # 0) OR (CC.S.COST+0 # 0) THEN
         PLINE="* SPOILAGE":SPACE(14):" TOTAL"
         PLINE=PLINE:SPACE(80):OCONV(CC.S.QTY,"MD0")"R#8":SPC:OCONV(CC.S.COST,"MD2")"R#10"
         PRINT PLINE
         LINE.CNT=LINE.CNT + 1
      END
      PRINT;PRINT
      LINE.CNT=LINE.CNT + 4
      FOR T = 1 TO 2
         DP.TOT.REC(T) = DP.TOT.REC(T) + CC.TOT.REC(T) + CC.TOT.REC(T+2)
      NEXT T
      MAT CC.TOT.REC = ""
      IF LINE.CNT >= 55 THEN GOSUB 9000
      RETURN
*
*-- PRINT DEPT TOTALS
*
3000  IF LINE.CNT >= 55 THEN GOSUB 9000
      PRINT SPACE(110):"-------- ----------"
      PLINE="** ":DEPT.DESC:" DEPARTMENT TOTAL";PLINE=PLINE "L#40"
      PLINE=PLINE:SPACE(70):OCONV(DP.QTY,"MD0")"R#8":SPC:OCONV(DP.COST,"MD2")"R#10"
      PRINT PLINE
      PRINT;PRINT
      LINE.CNT=LINE.CNT + 4
      FOR T = 1 TO 2
         TOT.REC(T) = TOT.REC(T) + DP.TOT.REC(T)
      NEXT T
      MAT DP.TOT.REC = ""
      IF LINE.CNT >= 55 THEN GOSUB 9000
      RETURN
*
*-- PRINT THE HEADINGS
*
9000  PRINT CHAR(12)
      LINE.CNT=0
      PG.NO=PG.NO + 1
      HLINE6=SPACE(13):OCONV(DATE(),"D2/")"R#8":SPACE(12):"PAGE: ":PG.NO
*T26556 v
*     PRINT HLINE4:HLINE5:HLINE6
      PRINT HD1:PG.NO
*T26556 ^
      BEGIN CASE
      CASE LEVEL = "S"
*T26556 v
*        PRINT JOB.CUST "L#8":SPACE(43):"S H I P P I N G   P O S T E D"
         PRINT HD2
         PRINT "Job Number: ":JOBNO "L#8":SPACE(1):CONSOL
         PRINT JOB.CUST "L#8"
*T26556 ^
         PRINT C.CUST.NAME
         *T28964   PRINT JOB.QTY<1,1> "L#7":SPACE(45):"FOR ITEMS THROUGH: ":OCONV(JRSD.DATE,"D2-")
         PRINT JOB.QTY<1,1> "L#8":SPACE(45):"FOR ITEMS THROUGH: ":OCONV(JRSD.DATE,"D2-");*T28964
         PRINT JOB.DESC<1,1> "L#30":SPACE(22):"DATE LAST ENTRY  : ":OCONV(JRSD.SP.DATE,"D2-")
         PRINT "SM: ":C.SALS.NAME "L#27":"SALES CODE: ":JOB.SALES.CODE
         PRINT
         PRINT "DEPARTMENT: ":P.DP"L#4":"  ":DEPT.DESC"L#20"
         PRINT HLINE1
         PRINT HLINE2
         PRINT HLINE3
*T26556 v
*        LINE.CNT=LINE.CNT + 11
         LINE.CNT=LINE.CNT + 13
*T26556 ^
      CASE 1
*T26556 v
*        IF JRSD.CONS="Y" THEN
*           PRINT JOB.CUST "L#8":SPACE(39):"D E T A I L   R E P O R T (SHIPPING)":SPACE(26):"#":JOBNO "L#8":" ":SPC:"CONSOLIDATED"
*        END ELSE
*           PRINT JOB.CUST "L#8":SPACE(39):"D E T A I L   R E P O R T (SHIPPING)":SPACE(26):"#":JOBNO "L#8"
*        END
         PRINT HD2
         PRINT "Job Number: ":JOBNO "L#8":SPACE(1):CONSOL
         PRINT JOB.CUST "L#8"
*T26556 ^
         PRINT C.CUST.NAME "L#35":SPACE(67):C.CUST.NAME
         *T28964   PRINT JOB.QTY<1,1> "L#7":SPACE(45):"FOR ITEMS THROUGH: ":OCONV(JRSD.DATE,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
         PRINT JOB.QTY<1,1> "L#8":SPACE(45):"FOR ITEMS THROUGH: ":OCONV(JRSD.DATE,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>;*T28964
         PRINT JOB.DESC<1,1> "L#30":SPACE(22):"DATE LAST ENTRY  : ":OCONV(JRSD.SP.DATE,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>
         PRINT "SM: ":C.SALS.NAME "L#27":"SALES CODE: ":JOB.SALES.CODE
         PRINT
         PRINT "DEPARTMENT: ":P.DP"L#4":"  ":DEPT.DESC"L#20"
         PRINT HLINE1
         PRINT HLINE2
         PRINT HLINE3
*T26556 v
*        LINE.CNT=LINE.CNT + 10
         LINE.CNT=LINE.CNT + 12
*T26556 ^
      END CASE
      RETURN
99999*
      PRINTER OFF
      RETURN
   END
