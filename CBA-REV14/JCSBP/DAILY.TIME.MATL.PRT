*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - DAILY.TIME.MATL.PRT
* SOURCE      - JCSBP
* DATE        - 05/19/87
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DESCRIPTION -
* This report is used for reporting DAILY.TIME.MATL records.
* MOD TASK 19393  CLS 7/95  ADD DIVISION TO TITLE
*
*T21847 lanny 04/25/1997 * Stacked error status print.
*T22378 stefanie 12/19/1997 * Add Additional Prompts & Responses.
*T26493 cmykleb 03/28/2002 * Change pgm to get report # from proc.
*********************************************************************
*----   INSERT EQUATE FROM CPYLIB
*COPY>JCS.CPYLIB>DAILY.TIME.MATL
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PRS.CPYLIB>WORK.SHIFT
***T22378 v Added SF.PROMPT cpylib below
*COPY>JCS.CPYLIB>SF.PROMPT
*COPY>CPYLIB>CHAR
*----   INITIALIZATION
   LINE.COUNT = 99
   PAGE.NO = 0
   PREV.TDATE = "";PREV.SHIFT = "";PREV.EMP = ""
   TOT.TD = ""
   TOT.S = ""
   TOT.E = ""
   GRD.TOT = ""
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   SP4 = SPACE(4)
*---- SET UP THE ERRMSG ROUTINE FOR SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*---- OPEN THE FILE
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'INVENTORY FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','COMPANY' TO COMPANY ELSE
      ERRMSG = 'COMPANY FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','CUSTOMER' TO CUSTOMER ELSE
      ERRMSG = 'CUSTOMER FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','EMPLOYEE' TO EMPLOYEE ELSE
      ERRMSG = 'EMPLOYEE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','DAILY.TIME.MATL' TO DAILY.TIME.MATL ELSE
      ERRMSG = 'DAILY.TIME.MATL FILE IS MISSING'
      GOTO 93000
   END
*T22378 v
   OPEN '','SF.PROMPT' TO SF.PROMPT ELSE
      ERRMSG = 'SF.PROMPT FILE IS MISSING'
      GOTO 93000
   END
*T22378 ^
*---- READ COMPANY NAME 
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
   CONO.NAME = ID<2>
   TYPE.RPT = ID<8>
   ST.DATE = ID<3>
   ED.DATE = ID<4>
   PROC.DIV = ID<5>
   PRINT.FLAG = ID<8> ; * T26493
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG = "CANNOT READ COMPANY RECORD FROM PROGRAM - DAILY.TIME.MATL.PRT"
      GOTO 93000
   END
   PRSFLAG = CO.PRS
   IF PRSFLAG = "E" THEN
      OPEN '','WORK.SHIFT' TO WORK.SHIFT ELSE
         ERRMSG = 'CANNOT OPEN WORK.SHIFT FILE FROM PROGRAM - DAILY.TIME.MATL.PRT'
         GOTO 93000
      END
   END
*T26493 v
*  IF TYPE.RPT = "(REJECT)" THEN
*     REPORT.NAME = "TIME.&.MATERIAL.EDIT.LISTING.(REJECT)"
*  END ELSE
*     REPORT.NAME = "TIME.&.MATERIAL.EDIT.LISTING"
*  END
*  REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*---- SET THE HEADING LINES
   HH.LINE1 = SPACE(107):"PRS UNITS"
   HH.LINE = "T TRANS   JOB NO  CUST # CTR OPER  CL FT J  IMPR/UNIT PRODUCT ID/DESC QUANTITY STRT STOP  REG    OT    DT  PRT DATE   NO POST STATUS"
   DH.LINE = "- ------ -------- ------ --- ---- --- -- - ---------- --------------- -------- ---- ---- ----- ----- ----- -------- ----------------"
   DH1.LINE = "                                                                                         ----- ----- ----- -----"
   DH2.LINE = "                                                                                         ===== ===== ===== ====="
*---- PUT PRINTER ON
   PRINTER ON
*---- READ AND PROCESS FIRST RECORD
100 READNEXT KEY ELSE 
      GOSUB 80000
      PRINT "NO ITEMS PRESENT"
      GOTO 999999
   END
   GOSUB 200
*---- READ THE FILE
   MATREADU DTM.REC FROM DAILY.TIME.MATL,KEY ELSE
      RELEASE DAILY.TIME.MATL, KEY
      GOTO 100
   END
*---- SET THE FIRST PREVIOUS VALUES
   PREV.TDATE = TDATE
   PREV.SHIFT = SHIFT
   PREV.EMP = EMP
*---- PROCESS THE FILE
   GOSUB 1000
*---- READ AND PROCESS ALL THE FILES
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      GOSUB 200
*---- READ THE FILE
      MATREADU DTM.REC FROM DAILY.TIME.MATL,KEY ELSE
         RELEASE DAILY.TIME.MATL, KEY
         GOTO 111
      END
*
*---- PROCESS THE RECORD
*
      GOSUB 1000
111 REPEAT
*---- PRINT THE TOTAL FOR LAST RECORD
   GOSUB 25000 ; GOSUB 26000 ; GOSUB 27000
*---- PRINT THE GRAND TOTAL
   IF LINE.COUNT + 3 >= 54 THEN GOSUB 80000
   PRINT
   PRINT DH2.LINE
   G.LINE = SPACE(56):"GRAND TOTAL :  "
   G.LINE = G.LINE: " JCS UNITS ":OCONV(GRD.TOT<1,5>,"MD2") "R#6"
   G.LINE = G.LINE:OCONV(GRD.TOT<1,1>,"MD2") "R#6"
   G.LINE = G.LINE:OCONV(GRD.TOT<1,2>,"MD2") "R#6"
   G.LINE = G.LINE:OCONV(GRD.TOT<1,3>,"MD2") "R#6"
   G.LINE = G.LINE:OCONV(GRD.TOT<1,4>,"MD2") "R#6"
   PRINT G.LINE
*---- END OF JOB
   GOTO 999999
*---- SEPERATE THE KEY TO PRODUCT, WHSE 
200 EMP.KEY = FIELD(KEY,"*",1)
   EMP = EMP.KEY[4,99]
   TDATE = FIELD(KEY,"*",2)
   SHIFT = FIELD(KEY,"*",3)
   RETURN
*---- PROCESS THE RECORD
1000 BEGIN CASE
      CASE PREV.TDATE # TDATE
         GOSUB 25000
         GOSUB 26000
         GOSUB 27000
         LINE.COUNT = 99
      CASE PREV.SHIFT # SHIFT
         GOSUB 25000
         GOSUB 26000
         LINE.COUNT = 99
      CASE PREV.EMP # EMP
         GOSUB 25000
   END CASE
   EMP.NAME = ""
   MATREAD EMP.REC FROM EMPLOYEE,EMP.KEY ELSE EMP.NAME = "EMPLOYEE UNKNOWN"
   IF EMP.NAME = "" THEN EMP.NAME = EMP.LAST.NAME:", ":EMP.FRST.NAME
   LUNCH=''
   IF PRSFLAG # 'E' THEN GO 1050
   WSS=EMP.WRK.SCHD<1,SHIFT>
   MATREAD WSS.REC FROM WORK.SHIFT,CONO:"!":WSS ELSE MAT WSS.REC = ''
   DD=MOD(TDATE,7);DAY=''
   BEGIN CASE
      CASE DD=0
         DAY='SUN'
      CASE DD=1
         DAY='MON'
      CASE DD=2
         DAY='TUE'
      CASE DD=3
         DAY='WED'
      CASE DD=4
         DAY='THU'
      CASE DD=5
         DAY='FRI'
      CASE DD=6
         DAY='SAT'
   END CASE
   LOCATE DAY IN WSS.DAY<1>,1 SETTING POS ELSE
      BEGIN CASE
         CASE DD=0
            IF WSS.USH.LUN<1,3> = 'Y' THEN LUNCH = 1;GO 1050
         CASE DD=6
            IF WSS.USH.LUN<1,2> = 'Y' THEN LUNCH = 1;GO 1050
         CASE 1
            IF WSS.USH.LUN<1,1> = 'Y' THEN LUNCH = 1;GO 1050
      END CASE
   END
   IF WSS.LUN.PD<1,POS> = 'Y' THEN LUNCH = 1
1050*
   S.PR.TIME = ""
   ESTAT = ""
   CALL CALC.PRS.TIME(DTM.TYPE,DTM.TIME.ST,DTM.TIME,S.PR.TIME,ESTAT,PRSFLAG)
   IF DTM.PRT.DATE = "" THEN CALL ADJUST.EMP.TIME(MAT DTM.REC)
   T.CNT = COUNT(DTM.TYPE,VM) + (DTM.TYPE # "")
   FOR TT = 1 TO T.CNT
      GOSUB 10000
      GOSUB 20000
   NEXT TT
   IF DTM.PRT.DATE = "" THEN DTM.PRT.DATE = REPORT.DATE
   IF PRINT.FLAG = "V" THEN RETURN ; * T26493
   MATWRITE DTM.REC ON DAILY.TIME.MATL, KEY
   RETURN
*---- READ FILES & INV.CNV
10000 MATREAD CUST.REC FROM CUSTOMER,CONO:DTM.CUST<1,TT> ELSE CUST.NAME = "CUSTOMER UNKNOWN"
   IF DTM.CUST<1,TT> = "" THEN CUST.NAME = ""
   IF DTM.PROD<1,TT> # "" THEN
      MATREAD INV.REC FROM INVENTORY,CONO:DTM.PROD<1,TT> ELSE MAT INV.REC = ""
   END ELSE
      MAT INV.REC = ""
   END
   IF INV.COST.WT *1 = 0 THEN INV.COST.WT = 100
   MAT INV.CNV.REC = ""
   BEGIN CASE
      CASE INV.UNIT<1,2> = "SHT" AND INV.UNIT<1,3> = "LBS"
         ICR.CNV = "MD0"; ICR.DV2 = 1
         ICR.MT1 = 1; ICR.DV1 = INV.M.WT
      CASE INV.UNIT<1,2> = "PC" AND INV.UNIT<1,3> = "MSI"
         ICR.CNV = "MD0"; ICR.DV2 = 1
         ICR.MT1 = 10; ICR.DV1 = INV.PAP.WIDTH/100
      CASE INV.UNIT<1,2> = "FT" AND INV.UNIT<1,3> = "MSI"
         ICR.CNV = "MD0"; ICR.DV2 = 12
         ICR.MT1 = 100; ICR.DV1 = INV.PAP.WIDTH/100
      CASE 1
         ICR.CNV = "MD2"; ICR.DV2 = 1
         ICR.MT1 = 1; ICR.DV1 = 10
   END CASE
   RETURN
*---- ROUTINE TO PRINT THE RECORD
20000 IF LINE.COUNT + 3 >= 54 THEN GOSUB 80000
   LINE.COUNT = LINE.COUNT + 3
   PRINT
   H.LINE = DTM.TYPE<1,TT> "L#1":SP1:DTM.SEQ<1,TT> "L#6":SP1
   H.LINE = H.LINE:DTM.JOB<1,TT> "L#8":SP1:DTM.CUST<1,TT> "L#6":SP1
   H.LINE = H.LINE:DTM.CCTR<1,TT>"L#3":SP1:DTM.OPER<1,TT> "L#4":SP1
   H.LINE = H.LINE:DTM.CLASS.CD<1,TT>"L#3":SP1
   H.LINE = H.LINE:DTM.TIME.CODE<1,TT>"L#2":SP1
   H.LINE = H.LINE:DTM.JOB.TYPE<1,TT> "L#1":SP1
   H.LINE = H.LINE:OCONV(DTM.IMP<1,TT>,"MD2") "R#10":SP1:DTM.PROD<1,TT>"L#15":SP1
   BEGIN CASE
      CASE DTM.QTY<1,TT> + 0 = 0 AND DTM.PROD<1,TT> = ""
         QTY = ""
         H.LINE = H.LINE:SPACE(9)
      CASE DTM.RS.QTYPE<1,TT> = "DR"
         QTY = INT(DTM.QTY<1,TT>/10)
         H.LINE = H.LINE:OCONV(QTY,"MD2") "R#8":SP1
      CASE 1
         QTY = INT(((DTM.QTY<1,TT>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5)
         H.LINE = H.LINE:OCONV(QTY,ICR.CNV) "R#8":SP1
   END CASE
   H.LINE = H.LINE:DTM.TIME.ST<1,TT>"R#4":SP1:DTM.TIME<1,TT>"R#4":SP1
   H.LINE = H.LINE:OCONV(DTM.REG.TIME<1,TT>,"MD2") "R#5":SP1
   H.LINE = H.LINE:OCONV(DTM.OVR.TIME<1,TT>,"MD2") "R#5":SP1
   H.LINE = H.LINE:OCONV(DTM.DUB.TIME<1,TT>,"MD2") "R#5":SP1
*     H.LINE = H.LINE:OCONV(DTM.LAPS.TIME<1,TT>,"MD2") "R#5":SP1
   IF DTM.TYPE<1,TT> = "L" AND LUNCH # 1 THEN S.PR.TIME<1,TT> = ''
   H.LINE = H.LINE:OCONV(S.PR.TIME<1,TT>,"MD2") "R#5":SP4
*     H.LINE = H.LINE:OCONV(DTM.PRT.DATE,"D2/")[1,5] "R#5":SP1
   NEW.DESC = ""
   IF DTM.STATUS<1,TT> # "" THEN
*T21847 v
      ORIG.DESC = DTM.STATUS<1,TT>
      CONVERT SVM TO VM IN ORIG.DESC
*        CALL DESC.REFORMAT(DTM.STATUS<1,TT>,"16",NEW.DESC)
      CALL DESC.REFORMAT.2(ORIG.DESC,"16",NEW.DESC)
   END
   H.LINE = H.LINE:NEW.DESC<1,1> "L#16"
   PRINT H.LINE
   IF DTM.SPOIL.CODE<1,TT> # "" THEN
      CUST.SC = CUST.NAME[1,17] "L#17":SP2:DTM.SPOIL.CODE<1,TT> "L#8"
   END ELSE
      CUST.SC = CUST.NAME "L#30"
   END
*     H.LINE = SPACE(18):CUST.SC "L#36":INV.FULL.DESC "L#35":SPACE(18):OCONV(DTM.PRT.DATE,"D2/")[1,5] "R#5":SP4:NEW.DESC<1,2> "L#16"
   H.LINE = SPACE(18):CUST.SC "L#36":INV.FULL.DESC "L#35":SPACE(18):OCONV(DTM.PRT.DATE<1,TT>,"D2/")[1,5] "R#5":SP4:NEW.DESC<1,2> "L#16"
   PRINT H.LINE
   FOR XD = 3 TO DCOUNT(NEW.DESC,VM)
      PRINT SPACE(116):NEW.DESC<1,XD>
      GOSUB 70000
   NEXT XD
*T21847 ^
*T22378 v
   IF DTM.ADD.PRMPT.CD<1,TT> # '' THEN
      PRINT SPACE(9):' ADDITIONAL PROMPTS         RESPONSES'
      GOSUB 70000
      PRINT SPACE(9):'-------------------- ------------------------'
      GOSUB 70000
      AP.CNT = DCOUNT(DTM.ADD.PRMPT.CD<1,TT>,SM)
      FOR AP = 1 TO AP.CNT
         MATREAD SFP.REC FROM SF.PROMPT,CONO:DTM.ADD.PRMPT.CD<1,TT,AP> THEN
            BEGIN CASE
               CASE SFP.RESP.TYPE = '6'
                  PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'D2/')
               CASE SFP.RESP.TYPE = '4'
                  BEGIN CASE
                     CASE SFP.RESP.SCALER = '1'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR1')
                     CASE SFP.RESP.SCALER = '2'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR2')
                     CASE SFP.RESP.SCALER = '3'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR3')
                     CASE SFP.RESP.SCALER = '4'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR4')
                     CASE SFP.RESP.SCALER = '5'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR5')
                     CASE SFP.RESP.SCALER = '6'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR6')
                     CASE SFP.RESP.SCALER = '7'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR7')
                     CASE SFP.RESP.SCALER = '8'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR8')
                     CASE SFP.RESP.SCALER = '9'
                        PRINT.RESP = OCONV(DTM.ADD.RESP<1,TT,AP>,'MR9')
                     CASE 1
                        PRINT.RESP = DTM.ADD.RESP<1,TT,AP>
                  END CASE
               CASE 1
                  PRINT.RESP = DTM.ADD.RESP<1,TT,AP>
            END CASE
            PRINT SPACE(9):SFP.PROMPT "L#20":SPACE(1):PRINT.RESP "L#24"
            GOSUB 70000
         END
      NEXT AP
   END
*T22378 ^
   TOT.E<1,1> = TOT.E<1,1> + DTM.REG.TIME<1,TT>
   TOT.E<1,2> = TOT.E<1,2> + DTM.OVR.TIME<1,TT>
   TOT.E<1,3> = TOT.E<1,3> + DTM.DUB.TIME<1,TT>
   TOT.E<1,4> = TOT.E<1,4> + S.PR.TIME<1,TT>
   TOT.E<1,5> = TOT.E<1,5> + DTM.LAPS.TIME<1,TT>
   RETURN
*---- PRINT TOTAL FOR EMPLOYEE
25000 IF LINE.COUNT + 2 >= 54 THEN GOSUB 80000 
   LINE.COUNT = LINE.COUNT + 2
   PRINT DH1.LINE
   TTL.LINE = PREV.EMP "L#4":SP1:EMP.NAME:" :   "
   T.LINE = TTL.LINE"R#72"
   T.LINE = T.LINE:"JCS UNITS ":OCONV(TOT.E<1,5>,"MD2")"R#6":SP1
   T.LINE = T.LINE:OCONV(TOT.E<1,1>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.E<1,2>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.E<1,3>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.E<1,4>,"MD2")"R#5"
   PRINT T.LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 1
   PREV.EMP = EMP
   FOR XX = 1 TO 5
      TOT.S<1,XX> = TOT.S<1,XX> + TOT.E<1,XX>
   NEXT XX
   TOT.E = ""
   RETURN
*---- PRINT TOTAL FOR SHIFT
26000 IF LINE.COUNT + 2 >= 54 THEN GOSUB 80000 
   LINE.COUNT = LINE.COUNT + 2
   PRINT DH1.LINE
   TTL.LINE = "TOTALS FOR SHIFT ":PREV.SHIFT "L#1":" :   "
   T.LINE = TTL.LINE"R#72"
   T.LINE = T.LINE:"JCS UNITS ":OCONV(TOT.S<1,5>,"MD2")"R#6":SP1
   T.LINE = T.LINE:OCONV(TOT.S<1,1>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.S<1,2>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.S<1,3>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.S<1,4>,"MD2")"R#5"
   PRINT T.LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 1
   PREV.SHIFT = SHIFT
   FOR XX = 1 TO 5
      TOT.TD<1,XX> = TOT.TD<1,XX> + TOT.S<1,XX>
   NEXT XX
   TOT.S = ""
   RETURN
*---- PRINT TOTAL FOR TRANSACTION DATE
27000 IF LINE.COUNT + 2 >= 54 THEN GOSUB 80000 
   LINE.COUNT = LINE.COUNT + 2
   PRINT DH1.LINE
   TTL.LINE = "TOTALS FOR TRANS DATE ":OCONV(PREV.TDATE,"D2/")"L#8":" :   "
   T.LINE = TTL.LINE"R#72"
   T.LINE = T.LINE:"JCS UNITS ":OCONV(TOT.TD<1,5>,"MD2")"R#6":SP1
   T.LINE = T.LINE:OCONV(TOT.TD<1,1>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.TD<1,2>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.TD<1,3>,"MD2")"R#5":SP1
   T.LINE = T.LINE:OCONV(TOT.TD<1,4>,"MD2")"R#5"
   PRINT T.LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 1
   PREV.TDATE = TDATE
   FOR XX = 1 TO 5
      GRD.TOT<1,XX> = GRD.TOT<1,XX> + TOT.TD<1,XX>
   NEXT XX
   TOT.TD = ""
   RETURN
*
70000 LINE.COUNT = LINE.COUNT+1
   IF LINE.COUNT > 54 THEN GOSUB 80000
   RETURN
*
*---- ROUTINE TO PRINT HEADINGS
80000 PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT SPACE(52):ST.DATE:" THROUGH ":ED.DATE:" FOR DIV ":PROC.DIV
   PRINT
   PRINT "TRANS DATE : ":OCONV(PREV.TDATE,"D2/"):SPACE(4):"SHIFT : ":PREV.SHIFT
   PRINT
   PRINT HH.LINE1
   PRINT HH.LINE
   PRINT DH.LINE
   LINE.COUNT = 9
   RETURN
*---- CALLS FOR SYSCOM
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINTER OFF
END
