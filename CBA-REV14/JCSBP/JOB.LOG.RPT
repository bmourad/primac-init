*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.LOG.RPT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 06/08/84
* MOD         - NLB
* DATE        - 1/15/89
* REASON      - 8.1 ENHANCEMENT
* DESCRIPTION - This program produces a Job Log Report.
*
* T22007 stefanie 08/29/1997 * Add Division to the heading.
* T26493 cmykleb 05/08/2002 * Change rpt to use GET.PROG.HEAD and get
*                             the rpt # from the proc.
*************************************************************
*
*---- PROCREAD
   PROCREAD N ELSE PRINT "MUST BE READ FROM PROC";STOP
   BY.SORT  = N<3>
   PROC.DIV = N<4>    ;* T22007 <
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','JOB' TO JOB ELSE ERRMSG = 'JOB FILE MISSING';GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
   OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG = 'CUSTOMER FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
   PG.NO = 0
   LINE.CNT = 0
   ERRMSG = ''
   RPT.NO = "XXXX"
   NUM = ''
   SP = ''
   SPC = ''
   SPD = '' ; * T26493
   SORT.NAME = ''
   JOB.DELIVERED.HEADING = ''
   JOB.D.HEADING = "JOBS DELIVERED BUT NOT COSTED OR INVOICED"
   JOB.C.HEADING = "JOBS COSTED BUT NOT INVOICED"
   CURRENT.NUMBER = ''
   PREVIOUS.NUMBER = ''
   FIRST.FLAG = 1
   FIRST.REC = 1
   SELECT.SLSMN = ""
   VOP.TOTAL = 0
   MAT JOB.REC = ''
*T26493 v
   CONO = N<1>
   CONO.NAME = ""
   RPT.NAME = ""
   RPT.NO = N<2>
   HD1 = ""
   HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NO,"",HD1,HD2)
*T26493 ^
   PRINTER ON
*
*---- GET HEADING INFORMATION
*
   IF BY.SORT = 1 OR BY.SORT = 9 OR BY.SORT = 13 THEN
      SP = SPACE(56)
      SORT.NAME = "BY JOB NUMBER"
   END
   IF BY.SORT = 2 OR BY.SORT = 10 OR BY.SORT = 14 THEN
      SP = SPACE(57)
      SORT.NAME = "BY CUSTOMER"
   END
   IF BY.SORT = 3 OR BY.SORT = 11 OR BY.SORT = 15 THEN
      SP = SPACE(57)
      SORT.NAME = "BY SALESREP"
      SELECT.SLSMN = "SLSRP"
   END
   IF BY.SORT = 6 OR BY.SORT = 8 OR BY.SORT = 12 THEN
      SP = SPACE(55)
      SORT.NAME = "BY DELIVERY DATE"
   END
   IF BY.SORT = 4 THEN
      SP = SPACE(56)
      SORT.NAME = "BY ORDER DATE"
   END
   IF BY.SORT = 5 THEN
      SP = SPACE(56)
      SORT.NAME = "BY SALES CODE"
   END
   IF BY.SORT = 7 THEN
      SP = SPACE(56)
      SORT.NAME = "BY OPEN JOBS"
   END
   IF BY.SORT = 8 OR BY.SORT = 9 OR BY.SORT = 10 OR BY.SORT = 11 THEN
      SPC = SPACE(42)
      JOB.DELIVERED.HEADING = JOB.D.HEADING
   END
   IF BY.SORT = 12 OR BY.SORT = 13 OR BY.SORT = 14 OR BY.SORT = 15 THEN
      SPC = SPACE(49)
      JOB.DELIVERED.HEADING = JOB.C.HEADING
   END
*
*---- MAIN PROCESSING
*
10*
   READNEXT JOB.KEY ELSE GOTO 15
   MATREAD JOB.REC FROM JOB,JOB.KEY ELSE GOTO 10
   IF BY.SORT = 7 THEN
      IF JOB.INV.DATE # "" AND SUM(JOB.WIP<1,2>) <= 0 THEN GOTO 10
   END
   CONO = JOB.KEY[1,3]
   JOB.NO = JOB.KEY[4,8]
   SLM.NO = JOB.SLSMN
   QTY = JOB.QTY<1,1>
   VOP = INT((JOB.CONF.AMT / 100) + .5)
   IF VOP = 0 THEN VOP = ''
   SCC = JOB.SALES.CODE
   EST.DATE = JOB.TRACK.DATE<1,1>
   QUOTED.DATE = JOB.TRACK.DATE<1,2>
   PROOF.DATE = JOB.TRACK.DATE<1,3>
   DUE.DATE = JOB.TRACK.DATE<1,4>
   PROD.START.DATE = JOB.TRACK.DATE<1,5>
   DELIV.DATE = JOB.TRACK.DATE<1,6>
   BILL.DATE = JOB.TRACK.DATE<1,7>
   COSTED.DATE = JOB.TRACK.DATE<1,8>
   LAST.INV.DATE = JOB.TRACK.DATE<1,9>
   COMPLETED.DATE = JOB.TRACK.DATE<1,10>
   IF BY.SORT = 1 OR BY.SORT = 7 OR BY.SORT = 9 OR BY.SORT = 13 THEN CURRENT.NUMBER = JOB.NO
   IF BY.SORT = 2 OR BY.SORT = 10 OR BY.SORT = 14 THEN CURRENT.NUMBER = JOB.CUST
   IF BY.SORT = 3 OR BY.SORT = 11 OR BY.SORT = 15 THEN CURRENT.NUMBER = SLM.NO
   IF BY.SORT = 6 OR BY.SORT = 8 OR BY.SORT = 12 THEN CURRENT.NUMBER = DELIV.DATE
   IF BY.SORT = 4 THEN CURRENT.NUMBER = JOB.TRACK.DATE<1,2>
   IF BY.SORT = 5 THEN CURRENT.NUMBER = SCC
   IF FIRST.FLAG = 1 THEN FIRST.FLAG = 0;GOSUB 20
   INVOICE.CNT = COUNT(JOB.INV.NO,VM) + (JOB.INV.NO # '')
   IF INVOICE.CNT > 0 THEN
      I = 0
      FOR I = 1 TO INVOICE.CNT
         INV.NO = JOB.INV.NO<1,I>
         INV.DATE = JOB.INV.DATE<1,I>
         NUM = I
         GOSUB 30
      NEXT I
   END ELSE
      GOSUB 30
   END
   GOTO 10
15*
   GOSUB 50
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
   PRINT CHAR(12)
   LINE.CNT = 0
   NEW.LINE.CNT = 0
   PG.NO = PG.NO + 1
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG = 'COMPANY MISSING'
      GOTO 93000
   END
   H.LINE = '';HLINE1='';HLINE2='';HLINE3=''
*T26493 v
*  HLINE1 = "DATE : ":OCONV(DATE(),"D2/")"R#8":"     REPORT NO: ":RPT.NO"L#4":SPACE(6)
*  K = SPACE((50 - LEN(CO.NAME)) / 2)
*  HLINE2 = K:CO.NAME:K
*  HLINE3 = SPACE(30):"PAGE : ":PG.NO
*  H.LINE = HLINE1:HLINE2:HLINE3
*  PRINT H.LINE
*  H.LINE = 'DIVISION: ':PROC.DIV
*  PRINT H.LINE
   HLINE1 = HD1:PG.NO
   HLINE2 = HD2
   PRINT HLINE1
   PRINT HLINE2
   H.LINE = "DIVISION: ":PROC.DIV
*T26493 ^
   IF JOB.DELIVERED.HEADING # '' THEN
      H.LINE = H.LINE:SPC:JOB.DELIVERED.HEADING
      PRINT H.LINE
      LINE.CNT = LINE.CNT + 1
      H.LINE = ''
      SPD = SPACE(13); * T26493
   END
*T26493 v
*  H.LINE = H.LINE:SP:SORT.NAME
   H.LINE = H.LINE:SP:SPD:SORT.NAME
*T26493 ^
   PRINT H.LINE
   PRINT
   H.LINE = ''
   H.LINE = H.LINE:'  JOB          CUSTOMER       QUANTITY     DESCRIPTION      SLM   ORDER   SALES    EST   CONF   DATE   PASS   COST   --- INVOICE ---'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'  NO.            NAME':SPACE(39):'NO    DATE    CATG.     $     OF    DELIV   TO    DONE   NUMBER   DATE'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:SPACE(74):'CODE           ORDER         BILL'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'-------- -------------------- -------- -------------------- --- --------  -----  ------  -----  -----  -----  -----  ------ --------'
   PRINT H.LINE
   PRINT
   LINE.CNT = LINE.CNT + 9
   RETURN
*
*---- PRINT THE VALUES (SORTED BY JOB NUMBER)
*
30*
   IF LINE.CNT >= 50 THEN GOSUB 20
   IF CURRENT.NUMBER # JOB.NO THEN
      GOSUB 35 ; GOTO 31
   END
   P.LINE = ''
   IF CURRENT.NUMBER # PREVIOUS.NUMBER THEN
      GOSUB 40
      P.LINE = P.LINE:JOB.NO "L#8":' ':CUST.NAME "L#20":' ':QTY "R#8":' ':JOB.DESC<1,1> "L#20":' ':SLM.NO "L#3":' '
      P.LINE = P.LINE:OCONV(JOB.TRACK.DATE<1,2>,"D2-")"L#8":'  ':SCC "L#5":'  ':VOP "R#6":'  ':CONF.ORD "L#5":'  ':DELIV.DATE "L#5":'  ':BILL.DATE "L#5":'  '
      P.LINE = P.LINE:COSTED.DATE "L#5":'  ':INV.NO "L#6":' ':OCONV(INV.DATE,"D2-")"L#8"
      PRINT P.LINE
      PRINT
   END ELSE
      P.LINE = P.LINE:SPACE(117):INV.NO "L#6":' ':OCONV(INV.DATE,"D2-")"L#8"
      PRINT P.LINE
      PRINT
   END
   LINE.CNT = LINE.CNT + 2
   PREVIOUS.NUMBER = CURRENT.NUMBER
31*
   RETURN
*
*---- PRINT THE VALUES (SORTED BY OTHER THAN JOB.NUMBER)
*
35*
   P.LINE = ''
   IF CURRENT.NUMBER # PREVIOUS.NUMBER AND SELECT.SLSMN = "SLSMN" THEN
      PRINT;PRINT
      LINE.CNT = LINE.CNT + 2
      PREVIOUS.NUMBER = CURRENT.NUMBER
   END
   IF INVOICE.CNT <= 1  OR INVOICE.CNT = '' OR NUM = 1 THEN
      GOSUB 40
      P.LINE = P.LINE:JOB.NO"L#8":' ':CUST.NAME"L#20":' ':QTY"R#8":' ':JOB.DESC<1,1> "L#20":' ':SLM.NO"L#3":' ':OCONV(JOB.TRACK.DATE<1,2>,"D2-")"L#8":'  ':SCC"L#5":'  ':VOP"R#6":'  ':CONF.ORD"L#5":'  ':DELIV.DATE"L#5":'  ':BILL.DATE"L#5":'  ':COSTED.DATE"L#5":'  ':INV.NO"L#6":' ':OCONV(INV.DATE,"D2-")"L#8"
      PRINT P.LINE
      PRINT
   END ELSE
      P.LINE = P.LINE:SPACE(117):INV.NO"L#6":' ':OCONV(INV.DATE,"D2-")"L#8"
      PRINT P.LINE
      PRINT
   END
   LINE.CNT = LINE.CNT + 2
   RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
   MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME="MISSING CUSTOMER"
   CONF.OF.ORDER = OCONV(QUOTED.DATE,"D2-")
   CONF.ORD = CONF.OF.ORDER[1,5]
   DELIV.DATE = OCONV(DELIV.DATE,"D2-")
   DELIV.DATE = DELIV.DATE[1,5]
   BILL.DATE = OCONV(BILL.DATE,"D2-")
   BILL.DATE = BILL.DATE[1,5]
   COSTED.DATE = OCONV(COSTED.DATE,"D2-")
   COSTED.DATE = COSTED.DATE[1,5]
   IF JOB.INV.NO = '' THEN INV.NO = ''
   IF JOB.INV.DATE = '' THEN INV.DATE = ''
   VOP.TOTAL = VOP.TOTAL + VOP
   RETURN
*
*---- PRINT GRAND TOTAL
*
50*
   PRINT
   G.LINE = ''
   G.LINE = G.LINE:SPACE(79):"--------"
   PRINT G.LINE
   G.LINE = ''
   G.LINE = G.LINE:SPACE(58):"*** GRAND TOTAL ***  ":VOP.TOTAL "R#8"
   PRINT G.LINE
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINT @(-1)
END
