*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - DRSBP
* PROGRAM     - CUST.RUN.LIST
* AUTHOR      - L. ROSS, C.B.A.
* DATE        - 09/18/91
* DESCRIPTION
* This program prints the customer run list for a job.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*
***** INSERT FILES EQUATES
*
*COPY>JCS.CPYLIB>DRS.JOB.SPEC
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>CUST.TEMPLATE.LIST
*COPY>JCS.CPYLIB>SHIPTO.MASTER
*COPY>JCS.CPYLIB>TEMPLATE.DETAIL
*COPY>JCS.CPYLIB>FOLD.CODE
*COPY>JCS.CPYLIB>PACK.CODE
*COPY>JCS.CPYLIB>DISTRIBUTION.CODE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>JCS.CPYLIB>FORMAT.CODE
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
   OPEN '','JOB' TO JOB ELSE
      ERRMSG ='JOB FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','DRS.JOB.SPEC' TO DRS.JOB.SPEC ELSE
      ERRMSG ='DRS.JOB.SPEC FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','CUST.TEMPLATE.LIST' TO CUST.TEMPLATE.LIST ELSE
      ERRMSG = 'CUST.TEMPLATE.LIST FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','SHIPTO.MASTER' TO SHIPTO.MASTER ELSE
      ERRMSG = 'SHIPTO.MASTER FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','TEMPLATE.DETAIL' TO TEMPLATE.DETAIL ELSE
      ERRMSG = 'TEMPLATE.DETAIL FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','CUSTOMER' TO CUSTOMER ELSE
      ERRMSG = 'CUSTOMER FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','FOLD.CODE' TO FOLD.CODE ELSE
      ERRMSG = 'FOLD.CODE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','PACK.CODE' TO PACK.CODE ELSE
      ERRMSG = 'PACK.CODE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','DISTRIBUTION.CODE' TO DISTRIBUTION.CODE ELSE
      ERRMSG = 'DISTRIBUTION.CODE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
      ERRMSG = 'SHIP.VIA FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','FORMAT.CODE' TO FORMAT.CODE ELSE
      ERRMSG = 'FORMAT.CODE FILE IS MISSING'
      GOTO 93000
   END
*
**** READ PROC VALUES
*
   PROCREAD ID ELSE
      ERRMSG = 'CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
*T26943 v
*  CONO.NAME = ID<2>
*  REPORT.NAME = 'Customer Run List'
*  REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   JOB.NO = ID<3>
   CUST.NO = ID<4>
   TEMP.NO = ID<3>
   IF ID<6> = 'Y' THEN PRT.SHP = 1 ELSE PRT.SHP = 0
*
**** INITIALIZATION
*
   LINE.COUNT = 99
   PAGE.NO = 0
   IF PRT.SHP THEN SHP.HED = ' Ship#  ' ELSE SHP.HED = SPACE(7)
   HH.LINE1 = 'Cust : ':CUST.NO "L#6":' '
   HH.LINE1.1 = '  Template: ':TEMP.NO "L#10":'  Job: ':JOB.NO "L#8":' '
   ULINE = STR('-',132)
   HH.LINE2 = SHP.HED:'Run#  Store  STCY  Store Name, Address':SPACE(11)
   HH.LINE2 = HH.LINE2:'Dist  Description':SPACE(11):'Shipping Address'
   HH.LINE2 = HH.LINE2:SPACE(10):'Fold Pack      Count'
*
***** TOTAL TABLES
*
   FOLD.CODES = ''
   FOLD.DESCS = ''
   FOLD.TOTLS = ''
   PACK.CODES = ''
   PACK.DESCS = ''
   PACK.TOTLS = ''
   PACK.RUNS = ''
   DIST.CODES = ''
   DIST.DESCS = ''
   DIST.TOTLS = ''
   TOTL.BOXED = ''
   TOTL.BOXES = ''
   TOTL.MAILS = ''
   TOTL.COPYS = ''
   TOTL.SKIDS = ''
*
***** READ AND PROCESS FIRST RECORD
*
100 *
   READNEXT KEY ELSE GOTO 99999
   MATREAD DJS.REC FROM DRS.JOB.SPEC,KEY ELSE GOTO 99999
   MATREAD CUST.REC FROM CUSTOMER, CONO:DJS.CUST ELSE
      CUST.NAME = '??????????'
   END
   CUST.NO = DJS.CUST
   MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE
      JOB.DESC = '??????????'
   END
   MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, CONO:CUST.NO:"!":TEMP.NO:"!001" ELSE
      ERRMSG = 'The CUST.TEMPLATE.LIST record for this job is missing'
      GOSUB 91000
      GOTO 99999
   END
   HH.LINE1 = HH.LINE1:CUST.NAME "L#28":HH.LINE1.1:JOB.DESC "L#30"
   HH.LINE1 = HH.LINE1:"   Format: ":DJS.FORMAT "L#11"
   SHIPTO = CTL.SHIPS<1,1>
   IF SHIPTO = '' THEN
      ERRMSG = 'The first record of CUST.TEMPLATE.LIST has no shipto# - run resequencing'
      GOSUB 91000
      GOTO 99999
   END
   SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
   MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
      ERRMSG = 'The TEMPLATE.DETAIL record is missing for shipto ':SHIPTO
      GOSUB 91000
      GOTO 99999
   END
   PREV.VER = TDT.VER.NO
   PREV.VER.DESC = TDT.VER.DESC
   PRINTER ON
   GOSUB 9000
   RSEQ = '001'
   KEY = CONO:CUST.NO:"!":TEMP.NO:"!"
*
***** READ AND PROCESS ALL RECORDS
*
   DATA = 1
   RUN.NO = 0
   LOOP
      IF SHIP.CNT < 1 THEN
         RSEQ =RSEQ + 1
         IF LEN(RSEQ) < 3 THEN RSEQ = STR('0',3-LEN(RSEQ)):RSEQ
         MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, KEY:RSEQ THEN
            SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
         END ELSE DATA = 0
         IF SHIP.CNT < 1 THEN DATA = 0
      END
   WHILE DATA = 1 DO
      FOR I = 1 TO SHIP.CNT
         SHIPTO = CTL.SHIPS<1,I>
         MATREAD SHM.REC FROM SHIPTO.MASTER, CONO:SHIPTO ELSE
            MAT SHM.REC = ''
         END
         MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
            MAT TDT.REC = ''
         END
         RUN.NO = RUN.NO + 1
         IF TDT.QTY > 0 THEN
            GOSUB 400
            GOSUB 1000
         END
      NEXT I
      SHIP.CNT = 0
   REPEAT
   GOSUB 4000
   GOSUB 4001
   GOTO 99999
*
**** FIND BREAK AND PRINT TOTALS
*
400 *
   IF TDT.VER.NO # PREV.VER THEN
      GOSUB 4000
      PRINT
      LINE.COUNT = LINE.COUNT + 1
   END
   RETURN
*
**** DETAIL ROUTINE
*
1000 *
*
   PLINES = ''
   IF TDT.FOLD = '' THEN TDT.FOLD = '????'
   LOCATE TDT.FOLD IN FOLD.CODES<1>,1 BY 'AL' SETTING L ELSE
      INS TDT.FOLD BEFORE FOLD.CODES<1,L>
      INS 0 BEFORE FOLD.TOTLS<1,L>
      FOLD.TOTLS<1,L,2> = 0
      MATREAD FLD.REC FROM FOLD.CODE, CONO:TDT.FOLD ELSE FLD.DESC = 'UNKNOWN'
      INS FLD.DESC BEFORE FOLD.DESCS<1,L>
   END
   FOLD.TOTLS<1,L,1> = FOLD.TOTLS<1,L,1> + TDT.QTY
   FOLD.TOTLS<1,L,2> = FOLD.TOTLS<1,L,2> + TDT.QTY
   IF TDT.PACK = '' THEN TDT.PACK = '??????'
   LOCATE TDT.PACK IN PACK.CODES<1>,1 BY 'AL' SETTING L ELSE
      INS TDT.PACK BEFORE PACK.CODES<1,L>
      INS 0 BEFORE PACK.TOTLS<1,L>
      PACK.TOTLS<1,L,2> = 0
      MATREAD PCK.REC FROM PACK.CODE, CONO:TDT.PACK ELSE PCK.DESC = 'UNKNOWN'
      INS PCK.DESC BEFORE PACK.DESCS<1,L>
      INS 0 BEFORE PACK.RUNS<1,L>
      PACK.RUNS<1,L,2> = 0
   END
   PACK.TOTLS<1,L,1> = PACK.TOTLS<1,L,1> + TDT.QTY
   PACK.TOTLS<1,L,2> = PACK.TOTLS<1,L,2> + TDT.QTY
   PACK.RUNS<1,L,1> = PACK.RUNS<1,L,1> + 1
   PACK.RUNS<1,L,2> = PACK.RUNS<1,L,2> + 1
   IF TDT.DIST.CODE = '' THEN TDT.DIST.CODE = '????'
   LOCATE TDT.DIST.CODE IN DIST.CODES<1>,1 BY 'AL' SETTING L ELSE
      INS TDT.DIST.CODE BEFORE DIST.CODES<1,L>
      INS 0 BEFORE DIST.TOTLS<1,L>
      DIST.TOTLS<1,L,2> = 0
      MATREAD DRS.REC FROM DISTRIBUTION.CODE, CONO:TDT.DIST.CODE ELSE DRS.DESC = 'UNKNOWN'
      INS DRS.DESC BEFORE DIST.DESCS<1,L>
   END
   DIST.TOTLS<1,L,1> = DIST.TOTLS<1,L,1> + TDT.QTY
   DIST.TOTLS<1,L,2> = DIST.TOTLS<1,L,2> + TDT.QTY
   IF TDT.DIST.CODE = 'MAIL' THEN
      TOTL.MAILS<1,1> = TOTL.MAILS<1,1> + TDT.QTY
      TOTL.MAILS<1,2> = TOTL.MAILS<1,2> + TDT.QTY
   END
   IF TDT.CART.LAB = 'Y' AND DJS.BOX.MAX > 0 THEN
      TOTL.BOXED<1,1> = TOTL.BOXED<1,1> + TDT.QTY
      TOTL.BOXED<1,2> = TOTL.BOXED<1,2> + TDT.QTY
      BOXES = INT(TDT.QTY / DJS.BOX.MAX)
      IF REM(TDT.QTY,DJS.BOX.MAX) >= DJS.BOX.MIN THEN BOXES = BOXES + 1
      TOTL.BOXES<1,1> = TOTL.BOXES<1,1> + BOXES
      TOTL.BOXES<1,2> = TOTL.BOXES<1,2> + BOXES
   END
   IF TDT.SKID.LAB = 'Y' AND DJS.SKD.MAX > 0 THEN
      SKIDS = INT(TDT.QTY / DJS.SKD.MAX)
      IF REM(TDT.QTY,DJS.SKD.MAX) >= DJS.SKD.MIN THEN SKIDS = SKIDS + 1
      TOTL.SKIDS<1,1> = TOTL.SKIDS<1,1> + SKIDS
      TOTL.SKIDS<1,2> = TOTL.SKIDS<1,2> + SKIDS
   END
   TOTL.COPYS<1,1> = TOTL.COPYS<1,1> + TDT.QTY
   TOTL.COPYS<1,2> = TOTL.COPYS<1,2> + TDT.QTY
   INST.LINES = ''
   LN = 1
   IF TDT.SP.INST.1.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.1;LN=LN+1
   IF TDT.SP.INST.2.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.2;LN=LN+1
   IF TDT.SP.INST.3.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.3;LN=LN+1
   IF TDT.SP.INST.4.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.4;LN=LN+1
   IF TDT.SP.INST.5.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.5;LN=LN+1
   IF TDT.SP.INST.6.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.6;LN=LN+1
   IF TDT.SP.INST.7.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.7;LN=LN+1
   IF TDT.SP.INST.8.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.8;LN=LN+1
   IF TDT.SP.INST.9.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.9;LN=LN+1
   IF TDT.SP.INST.10.USE<1,5> = 'Y' THEN INST.LINES<LN> = TDT.SP.INST.10
   LN = 1
   IF PRT.SHP THEN PLINES<LN> = SHIPTO 'R#7':' ' ELSE PLINES<LN> = SPACE(8)
   PLINES<LN> = PLINES<LN>:OCONV(RUN.NO, 'MD0Z') 'R#4'
   PLINES<LN> = PLINES<LN>:'  ':SHM.SP.STORE 'L#7'
   PLINES<LN> = PLINES<LN>:OCONV(TDT.STORE.QTY, 'MD0Z') 'R#4':'  ':SHM.SP.NAME 'L#28':'  '
   PLINES<LN> = PLINES<LN>:TDT.DIST.CODE 'L#6'
   IF INST.LINES<LN> # '' THEN
      PLINES<LN> = PLINES<LN>:INST.LINES<LN> 'L#20'
   END ELSE
      PLINES<LN> = PLINES<LN>:SPACE(20)
   END
   PLINES<LN> = PLINES<LN>:'  ':SHM.DT.NAME 'L#25'
   PLINES<LN> = PLINES<LN>:' ':TDT.FOLD 'L#4':' ':TDT.PACK 'L#6'
   PLINES<LN> = PLINES<LN>:OCONV(TDT.QTY, 'MD0,') 'R#10'
   LN = 2
   PLINES<LN> = SPACE(27)
   PLINES<LN> = PLINES<LN>:SHM.SP.ADDR1 'L#28':SPACE(8)
   IF INST.LINES<LN> # '' THEN
      PLINES<LN> = PLINES<LN>:INST.LINES<LN> 'L#20'
   END ELSE
      PLINES<LN> = PLINES<LN>:SPACE(20)
   END
   PLINES<LN> = PLINES<LN>:'  ':SHM.DT.ADDR1 'L#25':SPACE(22)
   LN = 3
   PLINES<LN> = SPACE(27)
   IF SHM.SP.ADDR2 # '' THEN
      PLINES<LN> = PLINES<LN>:SHM.SP.ADDR2 'L#28'
   END ELSE
      DUMMY1 = SHM.SP.CITY
      DUMMY2 = SHM.SP.STATE
      DUMMY3 = SHM.SP.ZIP
      MXL = 28
      GOSUB 1500
      PLINES<LN> = PLINES<LN>:DUMMY 'L#28'
   END
   PLINES<LN> = PLINES<LN>:SPACE(8)
   IF INST.LINES<LN> # '' THEN
      PLINES<LN> = PLINES<LN>:INST.LINES<LN> 'L#20'
   END ELSE PLINES<LN> = PLINES<LN>:SPACE(20)
   PLINES<LN> = PLINES<LN>:'  '
   IF SHM.DT.ADDR2 # '' THEN
      PLINES<LN> = PLINES<LN>:SHM.DT.ADDR2 'L#25'
   END ELSE
      DUMMY1 = SHM.DT.CITY
      DUMMY2 = SHM.DT.STATE
      DUMMY3 = SHM.DT.ZIP
      MXL = 25
      GOSUB 1500
      PLINES<LN> = PLINES<LN>:DUMMY 'L#25'
   END
   PLINES<LN> = PLINES<LN>:SPACE(22)
   LN = 4
   PLINES<LN> = SPACE(27)
   IF SHM.SP.ADDR2 # '' THEN
      DUMMY1 = SHM.SP.CITY
      DUMMY2 = SHM.SP.STATE
      DUMMY3 = SHM.SP.ZIP
      MXL = 28
      GOSUB 1500
      PLINES<LN> = PLINES<LN>:DUMMY 'L#28'
   END ELSE PLINES<LN> = PLINES<LN>:SPACE(28)
   PLINES<LN> = PLINES<LN>:SPACE(8)
   IF INST.LINES<LN> # '' THEN
      PLINES<LN> = PLINES<LN>:INST.LINES<LN> 'L#20'
   END ELSE PLINES<LN> = PLINES<LN>:SPACE(20)
   PLINES<LN> = PLINES<LN>:'  '
   IF SHM.DT.ADDR2 # '' THEN
      DUMMY1 = SHM.DT.CITY
      DUMMY2 = SHM.DT.STATE
      DUMMY3 = SHM.DT.ZIP
      MXL = 25
      GOSUB 1500
      PLINES<LN> = PLINES<LN>:DUMMY 'L#47'
   END ELSE PLINES<LN> = PLINES<LN>:SPACE(47)
   IF PLINES<LN> = SPACE(LEN(PLINES<LN>)) THEN PLINES<LN> = '' ELSE LN = LN + 1
   IL = 0
   FOR J = LN TO 10
      IF INST.LINES<J> # '' THEN IL = IL + 1
   NEXT J
   IF IL THEN
      FOR K = 1 TO IL
         PLINES<LN> = SPACE(63):INST.LINES<LN> 'L#20':SPACE(49)
         LN = LN + 1
      NEXT K
   END
   PLINES<LN> = SPACE(132)
   LN = LN + 1
   PLINES<LN> = SPACE(75):'Remarks  '
   IF TDT.SHIP.VIA # "" THEN SHM.DT.SHPVIA = TDT.SHIP.VIA
   MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:SHM.DT.SHPVIA ELSE MAT SHIP.VIA.REC = ''
   PLINES<LN> = PLINES<LN>:SHPV.DESC 'L#30'
   IF LINE.COUNT + LN + 2 > 63 THEN GOSUB 9000
   FOR P = 1 TO LN
      PRINT PLINES<P>
   NEXT P
   LINE.COUNT = LINE.COUNT + LN
   PRINT ULINE
   PRINT
   LINE.COUNT = LINE.COUNT + 2
   RETURN
*
**** SETUP C/S/Z LINE
*
1500 *
   DUMMY = ''
   DUMMY2 = DUMMY2[1,2]
   LC = MXL - LEN(DUMMY3) - LEN(DUMMY2) - 3
   DUMMY1 = DUMMY1[1,LC]
   DUMMY = (DUMMY1 : ' ':DUMMY2 : '  ' : DUMMY3)
   RETURN
*
**** PRINT TOTAL FOR VERSION
*
4000 *
   PLINES = ''
   PLINES<1> = ' '
   DUMMY = ' Totals for Version - ':PREV.VER:' ':PREV.VER.DESC:' '
   DUMMY1 = STR('<',INT((132-LEN(DUMMY)) / 2))
   DUMMY2 = STR('>',INT((132-LEN(DUMMY)) / 2))
   PLINES<2> = DUMMY1:DUMMY:DUMMY2
   IF LEN(PLINES<2>) < 132 THEN
      FOR P = LEN(PLINES<2>) TO 132
         PLINES<2> = PLINES<2>:'>'
      NEXT P
   END
   PLINES<3> = ' '
   PLINES<4> = SPACE(11)
   FCNT = DCOUNT(FOLD.CODES,VM)
   F = 0
   PTR = 0
   BEGIN CASE
      CASE FCNT > 0
         LOOP
            F = F + 1
            IF FOLD.TOTLS<1,F,1> > 0 THEN
               PLINES<4> = PLINES<4>:'Total ':FOLD.DESCS<1,F> 'L#20':OCONV(FOLD.TOTLS<1,F,1>, 'MD0,') 'R#10':SPACE(7)
               PTR = PTR + 1
            END
         WHILE PTR < 2 AND F < FCNT DO REPEAT
      CASE 1
   END CASE
   IF LEN(PLINES<4>) < 97 THEN PLINES<4> = PLINES<4>:SPACE(97-LEN(PLINES<4>))
   PLINES<4> = PLINES<4>:'Total Copies    ':OCONV(TOTL.COPYS<1,1>, 'MD0,')
   TOTL.COPYS<1,1> = 0
   LN = 5
   IF F < FCNT THEN
      LOOP
         PTR = 0
         PLINES<LN> = SPACE(11)
         LOOP
            F = F + 1
            IF FOLD.TOTLS<1,F,1> > 0 THEN
               PLINES<LN> = PLINES<LN>:'Total ':FOLD.DESCS<1,F> 'L#20':OCONV(FOLD.TOTLS<1,F,1>, 'MD0,') 'R#10':SPACE(7)
               PTR = PTR + 1
            END
         WHILE PTR < 2 AND F < FCNT DO REPEAT
         IF PLINES<LN> # SPACE(LEN(PLINES<LN>)) THEN LN = LN + 1 ELSE PLINES<LN> = ''
      WHILE F < FCNT DO REPEAT
   END
   FOR F = 1 TO FCNT
      FOLD.TOTLS<1,F,1> = 0
   NEXT F
   PLINES<LN> = ' '
   LN = LN + 1
   PLINES<LN> = SPACE(23)
   IF TOTL.BOXED<1,1> > 0 THEN
      PLINES<LN> = PLINES<LN>:'Total Boxed' 'L#20':OCONV(TOTL.BOXED<1,1>, 'MD0,') 'R#10':SPACE(7)
   END
   IF TOTL.MAILS<1,1> > 0 THEN
      PLINES<LN> = PLINES<LN>:'Total Mailers' 'L#20':OCONV(TOTL.MAILS<1,1>, 'MD0,') 'R#10'
   END
   TOTL.BOXED<1,1> = 0
   TOTL.MAILS<1,1> = 0
   IF PLINES<LN> = SPACE(LEN(PLINES<LN>)) THEN PLINES<LN> = ''; LN = LN - 1
   FOR L = 1 TO 3
      LN = LN + 1
      PLINES<LN> = ' '
   NEXT L
   PCNT = DCOUNT(PACK.CODES,VM)
   FOR P = 1 TO PCNT
      IF PACK.RUNS<1,P,1> > 0 THEN
         PLINES<LN> = SPACE(11):PACK.DESCS<1,P> 'L#20'
         PLINES<LN> = PLINES<LN>:OCONV(PACK.RUNS<1,P,1>, 'MD0Z') 'R#4':' '
         PLINES<LN> = PLINES<LN>:'Runs':OCONV(PACK.TOTLS<1,P,1>, 'MD0,') 'R#12':' Copies  '
         IF TOTL.SKIDS<1,1> > 0 THEN
            PLINES<LN> = PLINES<LN>:OCONV(TOTL.SKIDS<1,1>, 'MD0Z') 'R#6':' Skids'
            TOTL.SKIDS<1,1> = 0
            IF TOTL.BOXES<1,1> > 0 THEN PLINES<LN> = PLINES<LN>:'  '
         END
         IF TOTL.BOXES<1,1> > 0 THEN
            PLINES<LN> = PLINES<LN>:OCONV(TOTL.BOXES<1,1>, 'MD0Z') 'R#6':' Boxes   Code ':DJS.BOX
            TOTL.BOXES<1,1> = 0
         END
         LN = LN + 1
      END
      PACK.RUNS<1,P,1> = 0
      PACK.TOTLS<1,P,1> = 0
   NEXT P
*** THE FOLLOWING DIST TOTALS NOT USED BY RAPID
   PLINES<LN> = ' '
   DCNT = DCOUNT(DIST.CODES,VM)
   IF DCNT THEN
      LN = LN + 1
      PLINES<LN> = 'Distribution Totals :  '
      FOR DD = 1 TO DCNT
         PLINES<LN> = PLINES<LN>:DIST.DESCS<1,DD>'L#20 ':OCONV(DIST.TOTLS<1,DD,1>,'MD0,')'R#12'
         DIST.TOTLS<1,DD,1> = 0
         LN = LN + 1
         PLINES<LN> = SPACE(23)
      NEXT DD
   END
****
   IF LINE.COUNT + LN > 63 THEN
      GOSUB 9000
   END
   FOR P = 1 TO LN
      PRINT PLINES<P>
   NEXT P
   LINE.COUNT = LINE.COUNT + LN
   IF DATA THEN
      GOSUB 9000
   END ELSE
      IF LINE.COUNT + 2 < 63 THEN
         PRINT ULINE; PRINT
         LINE.COUNT = LINE.COUNT + 2
      END
   END
   PREV.VER = TDT.VER.NO
   PREV.VER.DESC = TDT.VER.DESC
   RETURN
*
**** PRINT GRAND TOTALS
*
4001 *
   PLINES = ''
   PLINES<1> = SPACE(132)
   PLINES<2> = 'Run Totals'
   PLINES<3> = SPACE(23)
   PLINES<4> = SPACE(11)
   FCNT = DCOUNT(FOLD.CODES,VM)
   F = 0
   PTR = 0
   BEGIN CASE
      CASE FCNT > 0
         LOOP
            F = F + 1
            IF FOLD.TOTLS<1,F,2> > 0 THEN
               PLINES<4> = PLINES<4>:'Total ':FOLD.DESCS<1,F> 'L#20':OCONV(FOLD.TOTLS<1,F,2>, 'MD0,') 'R#10':SPACE(7)
               PTR = PTR + 1
            END
         WHILE PTR < 2 AND F < FCNT DO REPEAT
      CASE 1
   END CASE
   IF LEN(PLINES<4>) < 97 THEN PLINES<4> = PLINES<4>:SPACE(97-LEN(PLINES<4>))
   PLINES<4> = PLINES<4>:'Total Copies    ':OCONV(TOTL.COPYS<1,2>, 'MD0,')
   LN = 5
   IF F < FCNT THEN
      LOOP
         PTR = 0
         PLINES<LN> = SPACE(11)
         LOOP
            F = F + 1
            IF FOLD.TOTLS<1,F,2> > 0 THEN
               PLINES<LN> = PLINES<LN>:'Total ':FOLD.DESCS<1,F> 'L#20':OCONV(FOLD.TOTLS<1,F,2>, 'MD0,') 'R#10':SPACE(7)
               PTR = PTR + 1
            END
         WHILE PTR < 2 AND F < FCNT DO REPEAT
         IF PLINES<LN> # SPACE(LEN(PLINES<LN>)) THEN LN = LN + 1 ELSE PLINES<LN> = ''
      WHILE F < FCNT DO REPEAT
   END
   PLINES<LN> = ' '
   LN = LN + 1
   PLINES<LN> = SPACE(23):'Total Boxed' 'L#20':OCONV(TOTL.BOXED<1,2>, 'MD0,') 'R#10'
   PLINES<LN> = PLINES<LN>:SPACE(7):'Total Mailers' 'L#20':OCONV(TOTL.MAILS<1,2>, 'MD0,') 'R#10'
   FOR L = 1 TO 2
      LN = LN + 1
      PLINES<LN> = ' '
   NEXT L
   LN = LN + 1
   PCNT = DCOUNT(PACK.CODES,VM)
   FOR P = 1 TO PCNT
      PLINES<LN> = SPACE(11):PACK.DESCS<1,P> 'L#20'
      PLINES<LN> = PLINES<LN>:OCONV(PACK.RUNS<1,P,2>, 'MD0Z') 'R#4':' '
      PLINES<LN> = PLINES<LN>:'Runs':OCONV(PACK.TOTLS<1,P,2>, 'MD0,') 'R#12':' Copies  '
      IF TOTL.SKIDS<1,2> > 0 THEN
         PLINES<LN> = PLINES<LN>:OCONV(TOTL.SKIDS<1,2>, 'MD0Z') 'R#6':' Skids'
         TOTL.SKIDS<1,2> = 0
         IF TOTL.BOXES<1,2> > 0 THEN PLINES<LN> = PLINES<LN>:'  '
      END
      IF TOTL.BOXES<1,2> > 0 THEN
         PLINES<LN> = PLINES<LN>:OCONV(TOTL.BOXES<1,2>, 'MD0Z') 'R#6':' Boxes   Code ':DJS.BOX
         TOTL.BOXES<1,2> = 0
      END
      LN = LN + 1
   NEXT P
*** THE FOLLOWING DIST TOTALS NOT USED BY RAPID
   PLINES<LN> = ' '
   DCNT = DCOUNT(DIST.CODES,VM)
   IF DCNT THEN
      LN = LN + 1
      PLINES<LN> = 'Distribution Totals :  '
      FOR DD = 1 TO DCNT
         PLINES<LN> = PLINES<LN>:DIST.DESCS<1,DD>'L#20 ':OCONV(DIST.TOTLS<1,DD,2>,'MD0,')'R#12'
         LN = LN + 1
         PLINES<LN> = SPACE(23)
      NEXT DD
   END
****
   IF LINE.COUNT + LN > 63 THEN
      GOSUB 9000
   END
   FOR P = 1 TO LN
      PRINT PLINES<P>
   NEXT P
   RETURN
*
***** PRINT HEADINGS
*
9000 *
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT HH.LINE1
   PRINT ULINE
   PRINT HH.LINE2
   PRINT ULINE
   PRINT
   LINE.COUNT = 8
   RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
   PRINTER OFF
END
