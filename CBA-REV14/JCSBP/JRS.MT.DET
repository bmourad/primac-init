      SUBROUTINE JRS.MT.DET
*COPY>JCS.CPYLIB>COM.JRS
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JRS.MT.DET
* BY          - NATALIE BLAKE, COMPUTER BUSINESS ASSOCIATES
* DATE        - 06/15/88
* DESCRIPTION -
* This program produces a Job Material Posted Detail report.
*T26556 ajibaly 05/09/2002 * USE RPT NO AND GET.PROG.HEAD
*T2896wvaughan 08/07/2006 * Extend JOB.QTY field to display 8 numbers.
*********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>JCS.CPYLIB>JOB.RPT.SET
*COPY>JCS.CPYLIB>JOB.RPT.SUM
*COPY>ICS.CPYLIB>CATEGORY
*COPY>JCS.CPYLIB>JOB.MATL
*
*---INITIALIZE VARIABLES
*
      PG.NO = 0
*T26556 v
*     RPT.NO="XXXX"
      HD1 = ""; HD2 = ""
      RPT.NAME = ""
      RPT.DATE = DATE()
      CALL GET.PROG.HEAD(CONO,C.CO.NAME,RPT.NAME,RPT.NO,RPT.DATE,HD1,HD2)
      PRINTER ON
*T26556 ^
      DIM DEPT.TOT(2); MAT DEPT.TOT = 0
      DIM GRAND.TOT(2); MAT GRAND.TOT = 0
      DIM PROD.TOT(3); MAT PROD.TOT = 0
      LINE.CNT = 99
*
*---MAIN PROCESSING
*
      IF JRSD.CONS = "Y" THEN
         CONSOL = "CONSOLIDATED"
      END ELSE
         CONSOL = "            "
      END
      ID.CNT = DCOUNT(JRS.IDS,AM)
      FND = 0
      FOR J = 1 TO ID.CNT WHILE FND = 0
         IF FIELD(JRS.IDS<J>,"&",5)#"M" THEN GOTO 100
         DPNO = FIELD(JRS.IDS<J>,"&",2)
         IF DP.SEL # "ALL" AND DP.SEL # DPNO THEN GOTO 100
         MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<J> ELSE GOTO 100
         FND = J
100   NEXT J
      IF FND = 0 THEN GOTO 99999
      CCTR = FIELD(JRS.IDS<FND>,"&",3)
      PROD = FIELD(JRS.IDS<FND>,"&",4)
      PREV.PROD = PROD
      PREV.DEPT = DPNO
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:DPNO ELSE DEPT.DESC = STR("?",30)
      MATREAD INV.REC FROM INVENTORY,CONO:PROD ELSE MAT INV.REC=""
      GOSUB 4000
      FND = FND + 1
      FOR I = FND TO ID.CNT
         IF FIELD(JRS.IDS<I>,"&",5) # "M" THEN GOTO 1099
         DPNO = FIELD(JRS.IDS<I>,"&",2)
         IF DP.SEL # "ALL" AND DP.SEL # DPNO THEN GOTO 1099
         MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<I> ELSE GOTO 1099
         CCTR = FIELD(JRS.IDS<I>,"&",3)
         PROD = FIELD(JRS.IDS<I>,"&",4)
         BEGIN CASE
         CASE PREV.DEPT # DPNO
            GOSUB 2000
            GOSUB 2500
            PREV.DEPT = DPNO
            PREV.PROD = PROD
            MATREAD DEPT.REC FROM DEPARTMENT,CONO:DPNO ELSE DEPT.DESC=STR("?",30)
            P.LINE = DEPT.DESC:"  ":DPNO
            PRINT P.LINE
            PRINT STR("-",LEN(P.LINE))
            LINE.CNT = LINE.CNT + 2
         CASE PREV.PROD # PROD
            GOSUB 2000
            PRINT
            LINE.CNT = LINE.CNT + 1
            PREV.PROD = PROD
         END CASE
         MATREAD INV.REC FROM INVENTORY,CONO:PROD ELSE MAT INV.REC=""
         GOSUB 4000
1099  NEXT I
      GOSUB 2000
      GOSUB 2500
      PRINT SPACE(89):'--------         --------'
      PRINT "*** GRAND TOTALS ***":SPACE(69):
      PRINT OCONV(GRAND.TOT(1),"MD2") "R#8":
      PRINT SPACE(9):OCONV(GRAND.TOT(2),"MD2") "R#8"
      GOTO 99999
*
*---PRINT PRODUCT TOTALS
*
2000 *
      PRINT SPACE(89):'--------         --------'
      LINE.CNT = LINE.CNT + 1
      BEGIN CASE
      CASE PROD.TOT(1) + PROD.TOT(3) + 0 # 0
         S.LINE=SPACE(62):'*   REG COST TOTAL  '
         S.LINE = S.LINE: SPACE(7):OCONV(PROD.TOT(1),"MD2") "R#8"
         S.LINE = S.LINE: SPACE(9):OCONV(PROD.TOT(3),"MD2") "R#8"
         PRINT S.LINE
         LINE.CNT = LINE.CNT + 1
      CASE PROD.TOT(2) + 0 # 0
         S.LINE=SPACE(62):'*    CO COST TOTAL  '
         S.LINE = S.LINE: SPACE(7): OCONV(PROD.TOT(2),"MD2") "R#8"
         PRINT S.LINE
         LINE.CNT = LINE.CNT + 1
      CASE 1
         S.LINE=SPACE(82):SPACE(11):"0.00"
         S.LINE = S.LINE: SPACE(13):"0.00"
         PRINT S.LINE
         LINE.CNT = LINE.CNT + 1
      END CASE
      DEPT.TOT(1) = DEPT.TOT(1) + PROD.TOT(1) + PROD.TOT(2)
      DEPT.TOT(2) = DEPT.TOT(2) + PROD.TOT(3)
      MAT PROD.TOT = 0
      RETURN
*
*---PRINT DEPARTMENT TOTALS
*
2500 *
      IF LINE.CNT >= 50 THEN GOSUB 3000
      PRINT SPACE(89):'--------         --------'
      D.LINE = '** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
      D.LINE = D.LINE:SPACE(49):OCONV(DEPT.TOT(1),"MD2")"R#8"
      D.LINE = D.LINE:SPACE(9):OCONV(DEPT.TOT(2),"MD2") "R#8"
      PRINT D.LINE
      PRINT
      PRINT
      LINE.CNT=LINE.CNT + 4
      FOR J = 1 TO 2
         GRAND.TOT(J) = GRAND.TOT(J) + DEPT.TOT(J)
      NEXT J
      MAT DEPT.TOT = 0
      RETURN
*
*---- PRINT THE HEADINGS
*
3000 *
      PRINT CHAR(12)
      PG.NO=PG.NO + 1
      SP=SPACE((50 - LEN(C.CO.NAME)) / 2)
*T26556 v
*     PRINT "#":JOBNO "L#8 ":CONSOL:
*     PRINT "  REPORT NO: ":RPT.NO "L#4   ":
*     PRINT SP:C.CO.NAME:SP:
*     PRINT SPACE(8):OCONV(DATE(),"D2/")"R#8":
*     PRINT SPACE(12):'PAGE: ':PG.NO
*     PRINT JOB.CUST"L#8":SPACE(39):
*     PRINT 'D E T A I L   R E P O R T (MATERIAL)':SPACE(27):
*     PRINT '#':JOBNO "L#8":" ":CONSOL
      PRINT HD1:PG.NO
      PRINT HD2
      PRINT "Job Number: ":JOBNO "L#8":SPACE(1):CONSOL
      PRINT JOB.CUST "L#8"
*T26556 ^
      PRINT C.CUST.NAME"L#30":SPACE(72):C.CUST.NAME
      *T28964   PRINT JOB.QTY<1,1>"L#7":SPACE(45):
      PRINT JOB.QTY<1,1>"L#8":SPACE(45):;*T28964
      PRINT 'FOR ITEMS THROUGH: ':OCONV(JRSD.DATE,"D2-")"R#8":
      PRINT SPACE(23):JOB.QTY<1,1>
      PRINT JOB.DESC<1,1>"L#30":SPACE(22):
      PRINT 'DATE LAST ENTRY  : ':OCONV(JRSD.MT.DATE,"D2-")"R#8":
      PRINT SPACE(23):JOB.DESC<1,1>"L#30"
      PRINT 'SM: ':C.SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
      PRINT
      PRINT SPACE(99):'SPOIL    SPOIL'
      PRINT 'DEPT  PRODUCT NUMBER  PRODUCT DESCRIPTION':SPACE(28):'DATE    U/M   QTY     COST     QTY     COST    REASON  XFER JOB'
      PRINT '----  --------------- ---------------------------------------------  -----   --- ------- -------- ------- -------- -------- --------'
*T26556 v
*     LINE.CNT= 10
      LINE.CNT= 12
*T26556 ^
      RETURN
*
*---PRINT DETAIL LINE
*
4000 *
      MCNT = DCOUNT(JRS.SEQ,VM)
      FOR M = 1 TO MCNT
         MAT INV.CNV.REC = ""
         BEGIN CASE
         CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
            ICR.CNV = "MD0"
            ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
         CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
            ICR.CNV = "MD0"
            ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
         CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
            ICR.CNV = "MD0"
            ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
         CASE 1
            ICR.CNV = "MD2"
            ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
         END CASE
         MT.ID = CONO:JOBNO:"!":DPNO:"!":CCTR:"!":PROD:"!"
         SCNT = DCOUNT(JRS.SEQ<1,M>,SVM)
         FOR S = 1 TO SCNT
            IF JRS.SUB.JOB<1,M,S> # "" THEN
               MT.ID = CONO:JRS.SUB.JOB<1,M,S>:"!":DPNO:"!":CCTR:"!":PROD:"!"
            END
            MATREAD JMT.REC FROM JOB.MATL, MT.ID:JRS.SEQ<1,M,S> ELSE GOTO 4099
            IF JMT.COST = 0 THEN JMT.COST = ""
            BEGIN CASE
            CASE JMT.TYPE = "S"
               TRANS.TYPE = "   "
               QTY = ""; COST = ""
               SP.QTY = JMT.QTY
               SP.COST = JMT.COST
               PROD.TOT(3) = PROD.TOT(3) + JMT.COST
            CASE JMT.TYPE = "C"
               PROD.TOT(2) = PROD.TOT(2) + JMT.COST
               TRANS.TYPE = "CO "
               QTY = JMT.QTY
               COST = JMT.COST
               SP.QTY = ""; SP.COST = ""
            CASE JMT.TYPE = "R"
               TRANS.TYPE = "   "
               QTY = JMT.QTY
               COST = JMT.COST
               PROD.TOT(1) = PROD.TOT(1) + JMT.COST
               SP.QTY = ""; SP.COST = ""
            END CASE
            IF LINE.CNT > 50 THEN
               GOSUB 3000
               P.LINE = DEPT.DESC:"  ":DPNO
               PRINT P.LINE
               PRINT STR("-",LEN(P.LINE))
               LINE.CNT = LINE.CNT + 2
            END
            IF QTY > 0 THEN
               QTY = INT((((QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
            END ELSE
               QTY = INT((((QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
            END
            IF SP.QTY > 0 THEN
               SP.QTY = INT((((SP.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
            END ELSE
               SP.QTY = INT((((SP.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
            END
            BEGIN CASE
            CASE QTY = ""
            CASE ICR.CNV = "MD0"
               QTY = OCONV(QTY,ICR.CNV)
            CASE 1
               PQTY = OCONV(QTY,ICR.CNV)
               BEGIN CASE
               CASE FIELD(PQTY,".",2) = 0
                  QTY = FIELD(PQTY,".",1)
               CASE LEN(PQTY) > 7
                  QTY = INT((QTY / 100) + .5)
               CASE 1
                  QTY = PQTY
               END CASE
            END CASE
            BEGIN CASE
            CASE SP.QTY = ""
            CASE ICR.CNV = "MD0"
               SP.QTY = OCONV(SP.QTY,ICR.CNV)
            CASE 1
               PQTY = OCONV(SP.QTY,ICR.CNV)
               BEGIN CASE
               CASE FIELD(PQTY,".",2) = 0
                  SP.QTY = FIELD(PQTY,".",1)
               CASE LEN(PQTY) > 7
                  SP.QTY = INT((SP.QTY / 100) + .5)
               CASE 1
                  SP.QTY = PQTY
               END CASE
            END CASE
            IF QTY = 0 THEN QTY = ""
            IF SP.QTY = 0 THEN SP.QTY = ""
            PRINT SPACE(6):PROD "L#15 ":INV.FULL.DESC"L#45 ":
            PRINT OCONV(JMT.DATE,"D0-") "R#5 ":
            PRINT TRANS.TYPE "L#2": INV.UNIT<1,2> "L#3 ":
            PRINT QTY "R#7 ":
            PRINT OCONV(COST,"MD2Z") "R#8 ":
            PRINT SP.QTY "R#7 ":
            PRINT OCONV(SP.COST,"MD2Z") "R#8 ":
            PRINT JMT.SPOIL.CODE"L#8 ":
            PRINT JMT.RC.JOB "L#7"
            LINE.CNT=LINE.CNT + 1
4099 *
         NEXT S
      NEXT M
      RETURN
99999 *
      PRINTER OFF
      RETURN
      END
