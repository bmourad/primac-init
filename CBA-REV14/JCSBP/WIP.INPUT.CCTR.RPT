*COPY>CPYLIB>COM1
******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - WIP.INPUT.CCTR.RPT
* BY          - RHONDA PERRIN, C.B.A
* DATE        - 05/19/86
* DESCRIPTION -
* This program creates a WIP Input by Cost Cntr report
* T22007 stefanie 08/26/1997 * Add Division to heading.
* T25901 cmykleb 03/26/2002 * Change heading to use GET.PROG.HEAD pgm.
******************************************************
*-- INSERT FILE EQUATES
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>COST.CNTR.WIP
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*-- DIMENTION RECORDS
   DIM COMP.TTL(8)
   DIM DIV.TTL(8)
   DIM DEPT.TTL(8)
   DIM DSUB.TTL(8)
   DIM CCTR.TTL(9)
   EQU CT.CCTR      TO CCTR.TTL(1)
   EQU CT.TOTAL     TO CCTR.TTL(2)
   EQU CT.DR        TO CCTR.TTL(3)
   EQU CT.VF        TO CCTR.TTL(4)
   EQU CT.FF        TO CCTR.TTL(5)
   EQU CT.SV        TO CCTR.TTL(6)
   EQU CT.MT        TO CCTR.TTL(7)
   EQU CT.OS        TO CCTR.TTL(8)
   EQU CT.MS        TO CCTR.TTL(9)
*-- SET UP ERRMSG ROUTINE FOR CPYLIB SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*-- GET COMPANY NAME
   PROCREAD ID ELSE ERRMSG = "MUST RUN FROM MENU"; GOSUB 91000; GOTO 99999
   CONO     = ID<1>
   CO.NAME  = ID<2>
   PERIOD   = ID<3>
   DIVISION = ID<4>  ;* T22007 <
*-- OPEN FILES
   OPEN '','COST.CNTR.WIP' TO COST.CNTR.WIP ELSE ERRMSG = "COST.CNTR.WIP FILE IS MISSING";GOTO 93000
   OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG = "COST.CNTR FILE IS MISSING";GOTO 93000
*-- INITALIZATION
   SP = " "
   SP3 = "   "
   D2 = '--'
   D3 = '---'
   D5 = '-----'
   D11 = STR('-',11)
   D12 = STR('-',12)
   D21 = STR('-',21)
   DSH = SPACE(35):D12:SP:D11:SP:D11:SP:D11:SP:D11:SP:D11:SP:D11:SP:D11
   EQL = SPACE(35):STR('=',12):SP:STR('=',11):SP:STR('=',11):SP:STR('=',11):SP:STR('=',11):SP:STR('=',11):SP:STR('=',11):SP:STR('=',11)
   STRS = SPACE(35):STR('*',12):SP:STR('*',11):SP:STR('*',11):SP:STR('*',11):SP:STR('*',11):SP:STR('*',11):SP:STR('*',11):SP:STR('*',11)
*T25901 v
*  POS = 51 - INT(LEN(CO.NAME)/2)
*  POS2 = 70 - POS
*  HEAD1 = "DATE - ":OCONV(DATE(), 'D2/') "L#8"
*  HEAD1 = HEAD1 :SPACE(POS) :CO.NAME "L#30"
*  HEAD1 = HEAD1 :SPACE(POS2) : "PAGE : 'PL'"
*  HEAD2 = "TIME - " : OCONV(TIME(),"MTS") "L#8"
*  HEAD2 = HEAD2 : SPACE(32):"INPUT TO WIP REPORT BY COST CENTER":"'L'"
*  HEAD3 = SPACE(61):"DIVISION: ":DIVISION:"'L'"
*  HEAD4 = SPACE(57):"FOR PERIOD ":PERIOD[5,2]:"  ":PERIOD[1,4]:"'LL'"
   CO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
   HEAD1 = "" ; HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,"",HEAD1,HEAD2)
   HEAD1 = HEAD1 : " 'PL'"
   HEAD3 = SPACE(70):"DIVISION: ":DIVISION:"'L'"
   HEAD4 = SPACE(66):"FOR PERIOD ":PERIOD[5,2]:"  ":PERIOD[1,4]:"'LL'"
*T25901 ^
   HEAD5 = SPACE(48):"<-------------------------------- COST INFORMATION ------------------------------->":"'L'"
   HEAD6 = SPACE(62):"VAR FACT    FXD FACT     S & A     INVENTORY    OUTSIDE    SHIPPING":"'L'"
   HEAD7 = "DV  DEPT CTR       CCTR DESC          TOTAL $     DIRECT $     OVH $       OVH $       OVH $       MATL $    PURCHASE $  &  MISC $":"'L'"
   HEAD8 = D2:SP:D5:SP:D3:SP:D21:SP:D12:SP:D11:SP:D11:SP:D11:SP:D11:SP:D11:SP:D11:SP:D11:"'L'"
   PRINTER ON
   HEADING HEAD1:HEAD2:HEAD3:HEAD4:HEAD5:HEAD6:HEAD7:HEAD8
*-- READING ALL FILES AND PROCESS ONE AT A TIME
   FIRST = 1
   NO.CCTRS = 0
   NO.SUBS = 0
   NO.DEPT = 0
   MAT CCW.REC = ""
   MAT COMP.TTL = ""
   MAT DIV.TTL = ""
   MAT DEPT.TTL = ""
   MAT DSUB.TTL = ""
   MAT CCTR.TTL = ""
100*
   READNEXT KEY ELSE GOTO 111
   MATREAD CCW.REC FROM COST.CNTR.WIP,KEY ELSE GOTO 100
   DIV.NO = KEY[4,2]
   DEPT.NUM = FIELD(KEY,"!",1)[6,99]
   DEPT.NO = DEPT.NUM[1,2]
   IF LEN(DEPT.NUM) = 5 THEN
      DEPT.SUB = DEPT.NUM
   END ELSE
      DEPT.SUB = ""
   END
   CCTR.NO = FIELD(KEY,"!",2)[1,3]
   IF FIRST THEN
      PREV.DIV = DIV.NO
      PREV.DEPT = DEPT.NO
      PREV.SUB = DEPT.SUB
      PREV.CCTR = CCTR.NO
      FIRST = 0
      NEW.DIV = 1
   END
   BEGIN CASE
      CASE PREV.DIV <> DIV.NO
         IF NO.CCTRS # 0 THEN PRINT DSH
         IF PREV.SUB # "" THEN
            GOSUB 3000          ;* DEPT.SUB TOTAL LINE
         END
         GOSUB 4000             ;* DEPT TOTAL LINE
         GOSUB 5000             ;* DIV TOTAL LINE
         PRINT EQL
         PREV.DIV = DIV.NO
         PREV.DEPT = DEPT.NO
         PREV.SUB = DEPT.SUB
         NEW.DIV = 1
         NO.CCTRS = 0
         NO.SUBS = 0
         NO.DEPT = 0
      CASE PREV.DEPT <> DEPT.NO
         IF NO.CCTRS # 0 THEN PRINT DSH
         IF PREV.SUB # "" THEN
            GOSUB 3000          ;* DEPT.SUB TOTAL LINE
         END
         GOSUB 4000             ;* DEPT TOTAL LINE
         PREV.DEPT = DEPT.NO
         PREV.SUB = DEPT.SUB
         NO.CCTRS = 0
         NO.SUBS = 0
      CASE PREV.SUB <> DEPT.SUB AND PREV.SUB # ""
         IF NO.CCTRS # 0 THEN PRINT DSH
         GOSUB 3000             ;* DEPT.SUB TOTAL LINE
         PREV.SUB = DEPT.SUB
         NO.CCTRS = 0
   END CASE
   GOSUB 1000
   PREV.DIV = DIV.NO
   PREV.DEPT = DEPT.NO
   PREV.SUB = DEPT.SUB
   PREV.CCTR = CCTR.NO
   GOTO 100
111*
   IF FIRST THEN GOTO 115
   IF NO.CCTRS # 0 THEN PRINT DSH
   IF PREV.SUB # "" THEN
      GOSUB 3000          ;* LAST DEPT.SUB TOTAL LINE
   END
   GOSUB 4000             ;* LAST DEPT TOTAL LINE
   GOSUB 5000             ;* LAST DIV TOTAL LINE
*-- PRINTING THE GRAND TOTAL VALUES
   T.LINE = ''
   T.LINE = T.LINE : 'COMPANY ':CONO:' TOTAL':SPACE(18)
   T.LINE = T.LINE : OCONV(COMP.TTL(1), 'MD2') "R#12" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(2), 'MD2') "R#11" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(3), 'MD2') "R#11" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(4), 'MD2') "R#11" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(5), 'MD2') "R#11" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(6), 'MD2') "R#11" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(7), 'MD2') "R#11" :SP
   T.LINE = T.LINE : OCONV(COMP.TTL(8), 'MD2') "R#11" :SP
   PRINT STRS
   PRINT T.LINE
*-- END OF JOB
115*
   PRINTER OFF
   GOTO 99999
*-- PROCESS THE RECORD
1000*
   TOTAL = 0;DR.WIP = 0;FF.WIP = 0;VF.WIP = 0
   SV.WIP = 0;MT.WIP = 0;OS.WIP = 0;MS.WIP = 0
   DR.WIP = CCW.LB.I<1,1>
   FF.WIP = CCW.LB.I<1,2>
   VF.WIP = CCW.LB.I<1,3>
   SV.WIP = CCW.LB.I<1,4>
   MT.WIP = CCW.MT.I<1,1> + CCW.MT.I<1,2> + CCW.MT.I<1,3>
   OS.WIP = CCW.OS.I<1,1> + CCW.OS.I<1,2>
   MS.WIP = CCW.SP.I<1,1> + CCW.SP.I<1,2>
   MS.WIP = MS.WIP + CCW.MS.I<1,1> + CCW.MS.I<1,2>
*-- FINDING THE TOTAL COST
   TOTAL = TOTAL + DR.WIP + VF.WIP + FF.WIP + SV.WIP + MT.WIP + OS.WIP + MS.WIP
   IF TOTAL = 0 THEN GOTO 1999
   CT.CCTR = CCTR.NO
   CT.DR = CT.DR + DR.WIP
   CT.VF = CT.VF + VF.WIP
   CT.FF = CT.FF + FF.WIP
   CT.SV = CT.SV + SV.WIP
   CT.MT = CT.MT + MT.WIP
   CT.OS = CT.OS + OS.WIP
   CT.MS = CT.MS + MS.WIP
   CT.TOTAL = CT.TOTAL + TOTAL
   IF PREV.SUB # "" THEN
      DSUB.TTL(1) = DSUB.TTL(1) + TOTAL
      DSUB.TTL(2) = DSUB.TTL(2) + DR.WIP
      DSUB.TTL(3) = DSUB.TTL(3) + VF.WIP
      DSUB.TTL(4) = DSUB.TTL(4) + FF.WIP
      DSUB.TTL(5) = DSUB.TTL(5) + SV.WIP
      DSUB.TTL(6) = DSUB.TTL(6) + MT.WIP
      DSUB.TTL(7) = DSUB.TTL(7) + OS.WIP
      DSUB.TTL(8) = DSUB.TTL(8) + MS.WIP
   END
   DEPT.TTL(1) = DEPT.TTL(1) + TOTAL
   DEPT.TTL(2) = DEPT.TTL(2) + DR.WIP
   DEPT.TTL(3) = DEPT.TTL(3) + VF.WIP
   DEPT.TTL(4) = DEPT.TTL(4) + FF.WIP
   DEPT.TTL(5) = DEPT.TTL(5) + SV.WIP
   DEPT.TTL(6) = DEPT.TTL(6) + MT.WIP
   DEPT.TTL(7) = DEPT.TTL(7) + OS.WIP
   DEPT.TTL(8) = DEPT.TTL(8) + MS.WIP
*-- PRINT LINE FOR CCTR
   MATREAD CCTR.REC FROM COST.CNTR,CONO:CT.CCTR ELSE CCTR.DESC = "COST CENTER UNKNOWN"
   V.LINE = ""
   IF NEW.DIV THEN
      V.LINE = V.LINE : PREV.DIV "L#2" :SP
      NEW.DIV = 0
   END ELSE
      V.LINE = V.LINE : SP3
   END
   IF NO.CCTRS = 0 THEN
      IF DEPT.SUB # "" THEN
         V.LINE = V.LINE : DEPT.SUB "R#5" :SP
      END ELSE
         V.LINE = V.LINE : DEPT.NO "R#5":SP
      END
   END ELSE
      V.LINE = V.LINE : SPACE(6)
   END
   V.LINE = V.LINE : CT.CCTR "L#3":SP
   V.LINE = V.LINE : CCTR.DESC "L#21" :SP
   V.LINE = V.LINE : OCONV(CT.TOTAL, 'MD2') "R#12" :SP
   V.LINE = V.LINE : OCONV(CT.DR, 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(CT.VF, 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(CT.FF, 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(CT.SV, 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(CT.MT, 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(CT.OS, 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(CT.MS, 'MD2') "R#11"
   PRINT V.LINE
   NO.CCTRS = NO.CCTRS + 1
   NO.SUBS = NO.SUBS + 1
1999*
   MAT CCTR.TTL = ""
   RETURN
*-- PRINT DEPT SUB TOTAL LINE
3000*
   IF NO.CCTRS = 0 THEN GOTO 3999
   V.LINE = ""
   V.LINE = V.LINE : SP3 : PREV.SUB "R#5":" SUBDEPT TOTAL":SPACE(13)
   V.LINE = V.LINE : OCONV(DSUB.TTL(1), 'MD2') "R#12" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(2), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(3), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(4), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(5), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(6), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(7), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DSUB.TTL(8), 'MD2') "R#11" :SP
   PRINT V.LINE
   PRINT DSH
3999*
   MAT DSUB.TTL = ""
   RETURN
*-- PRINT DEPT TOTAL LINE
4000*
   IF NO.CCTRS = 0 AND NO.SUBS = 0 THEN GOTO 4999
   V.LINE = ""
   V.LINE = V.LINE : SP3 : PREV.DEPT "R#5":" DEPT TOTAL":SPACE(16)
   V.LINE = V.LINE : OCONV(DEPT.TTL(1), 'MD2') "R#12" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(2), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(3), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(4), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(5), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(6), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(7), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DEPT.TTL(8), 'MD2') "R#11" :SP
   PRINT V.LINE
   PRINT EQL
   NO.DEPT = NO.DEPT + 1
   FOR JJ = 1 TO 8
      DIV.TTL(JJ) = DIV.TTL(JJ) + DEPT.TTL(JJ)
   NEXT JJ
4999*
   MAT DEPT.TTL = ""
   RETURN
*-- PRINT DIV TOTAL LINE
5000*
   IF NO.DEPT = 0 THEN GOTO 5999
   V.LINE = ""
   V.LINE = V.LINE : PREV.DIV "L#2":" DIVISION TOTAL":SPACE(18)
   V.LINE = V.LINE : OCONV(DIV.TTL(1), 'MD2') "R#12" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(2), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(3), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(4), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(5), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(6), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(7), 'MD2') "R#11" :SP
   V.LINE = V.LINE : OCONV(DIV.TTL(8), 'MD2') "R#11" :SP
   PRINT V.LINE
   FOR JJ = 1 TO 8
      COMP.TTL(JJ) = COMP.TTL(JJ) + DIV.TTL(JJ)
   NEXT JJ
5999*
   MAT DIV.TTL = ""
   RETURN
*-- CALLS FOR SYSCOM
91000ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
