   SUBROUTINE JOB.TRANS.UPD (CONO,ID,MAT DTM.REC,PTR,JOB.TRANS)
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM - JOB.TRANS.UPD
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 10/18/89
*
* DESCRIPTION
*
* This subroutine is called by POST.TIME.MATL to update the JOB.TRANS
* file with item posted.
*
*T25262 edvard 06/27/2000 * Job tracking inquiry was not showing all
*                           of the lines correctly. The are no marks of
*                           the task in the pgm since it significantly
*                           changed from the original.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JCS.CPYLIB>DAILY.TIME.MATL
*COPY>JCS.CPYLIB>JOB.TRANS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   JOB.NUM = DTM.JOB<1,PTR>
   JTYPE = DTM.JOB.TYPE<1,PTR>
   IF (JOB.NUM MATCHES "'X'7N") AND (JTYPE = "N") THEN GOTO 99999
   DEMPL = FIELD(ID,"*",1)[4,99]
   DDATE = FIELD(ID,"*",2)
   DSHFT = FIELD(ID,"*",3)
   TRAN = DTM.SEQ<1,PTR>
*
*---- MAIN PROCESSING
*
100 *
   MATREADU JTRN.REC FROM JOB.TRANS, CONO:JOB.NUM ELSE
      MAT JTRN.REC = ""
   END
   LOCATE TRAN IN JTRN.TRANS<1>,1 SETTING XFND THEN
      JTRN.DEPT<1,XFND> = DTM.DEPT<1,PTR>            
      JTRN.CCTR<1,XFND> = DTM.CCTR<1,PTR>            
      JTRN.OPER<1,XFND> = DTM.OPER<1,PTR>            
      JTRN.EMP<1,XFND> = DEMPL                       
      STIME = DTM.TIME.ST<1,PTR>
      ETIME = DTM.TIME<1,PTR>
      IF ETIME < STIME THEN ETIME = ETIME+2400
      ELAP.MIN = (INT(ETIME/100)*60+MOD(ETIME,100)) - (INT(STIME/100)*60+MOD(STIME,100))
      HRS = INT(ELAP.MIN*100/60+0.5)
      JTRN.HRS<1,XFND> = HRS               
      JTRN.IMP<1,XFND> = DTM.IMP<1,PTR>              
      JTRN.SHIFT<1,XFND> = DSHFT                     
      JTRN.CMP.FLG<1,XFND> = DTM.OPER.COMPLETE<1,PTR>
      JTRN.OPER.TYPE<1,XFND> = ""                    
      JTRN.POST.FLAG<1,XFND> = "Y"                   
   END ELSE
      IF DTM.TIME.STAMP<1,PTR> = "" THEN                       
         SDATE=""                                              
         SPTR = 0                                              
         TCNT = DCOUNT(DTM.TYPE,VM)                            
         FOR TPTR = TCNT TO 1 STEP - 1 UNTIL SDATE # ""        
            TTYPE = DTM.TYPE<1,TPTR>                           
            BEGIN CASE                                         
               CASE TTYPE = "M"                                   
               CASE DTM.TIME.STAMP<1,TPTR> # ""                   
                  SDATE = INT(DTM.TIME.STAMP<1,TPTR>/100000)      
                  SPTR = TPTR                                     
            END CASE                                           
         NEXT TPTR                                             
         BEGIN CASE                                            
            CASE SPTR = 0                                         
               TS = DDATE*100000                                  
            CASE SPTR > PTR AND DTM.TIME<1,SPTR> < DTM.TIME<PTR>  
               TS = (SDATE-1)*100000                              
            CASE SPTR < PTR AND DTM.TIME<1,SPTR> > DTM.TIME<1,PTR>
               TS = (SDATE+1)*100000                              
            CASE 1                
               TS = SDATE*100000  
         END CASE              
         HH = INT(DTM.TIME<1,PTR>/100)
         MM = MOD(DTM.TIME<1,PTR>,100)
         HH = STR("0",2-LEN(HH)) : HH 
         MM = STR("0",2-LEN(MM)) : MM 
         ETIME = HH : ":" : MM        
         ETIME = ICONV(ETIME, "MT")   
         TS = TS + ETIME              
      END ELSE                     
         TS = DTM.TIME.STAMP<1,PTR>
      END                          
      STIME = DTM.TIME.ST<1,PTR>
      ETIME = DTM.TIME<1,PTR>
      IF ETIME < STIME THEN ETIME = ETIME+2400
      ELAP.MIN = (INT(ETIME/100)*60+MOD(ETIME,100)) - (INT(STIME/100)*60+MOD(STIME,100))
      HRS = INT(ELAP.MIN*100/60+0.5)
      JTRN.TIME.STAMP<1,-1> = TS
      JTRN.DEPT<1,-1> = DTM.DEPT<1,PTR>
      JTRN.CCTR<1,-1> = DTM.CCTR<1,PTR>
      JTRN.OPER<1,-1> = DTM.OPER<1,PTR>
      JTRN.EMP<1,-1> = DEMPL
      JTRN.HRS<1,-1> = HRS 
      JTRN.IMP<1,-1> = DTM.IMP<1,PTR>
      JTRN.SHIFT<1,-1> = DSHFT
      JTRN.CMP.FLG<1,-1> = DTM.OPER.COMPLETE<1,PTR>
      JTRN.OPER.TYPE<1,-1> = ""
      JTRN.POST.FLAG<1,-1> = "Y"
   END
   MATWRITE JTRN.REC ON JOB.TRANS, CONO:JOB.NUM
   GOTO 99999
*
*---- END OF PROGRAM
*
99999 *
   RETURN
END
