*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SYSTEM      - JCSBP
* PROGRAM     - JOB.DETAIL.RPT.MATL
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 06/15/84
* DESCRIPTION -
* This program produces a Job Detail Report by Material. 
**************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>CATEGORY
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM MENU";GOSUB 91000;GOTO 99999
     CS.FLG = ID<5>
*
*---- DIMENSION VARIABLES
*
     DIM REG.TOTAL.REC(5)
     DIM CO.TOTAL.REC(5)
     DIM DEPT.TOTAL.REC(5)
     DIM GRAND.TOTAL.REC(5)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','CATEGORY' TO CATEGORY ELSE ERRMSG='CATEGORY FILE MISSING';GOTO 93000
     OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG='INVENTORY FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     UNKNOWN=STR("?",30)
     MAT REG.TOTAL.REC=''
     MAT CO.TOTAL.REC=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     PREVIOUS.DPM.NO=''
     PREVIOUS.DPM=''
     PREVIOUS.PROD=''
     FIRST.FLAG=1
     NODATA=1
     DATE.LAST.WORKED=''
     DATE.ITEMS.THROUGH=''
     RPT.NO="XXXX"
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     TYPE=FIELD(JCR.KEY,"*",4)
     IF TYPE="EST" THEN GOTO 10
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'MAT' THEN GOTO 10
     NODATA=0
     BEGIN CASE
        CASE JCR.LINE.TYPE="D" OR JCR.LINE.TYPE=''
           DPM.LINE="D*":JCR.DEPT
        CASE JCR.LINE.TYPE="P"
           DPM.LINE="P*":JCR.DEPT
        CASE JCR.LINE.TYPE="M"
           DPM.LINE="M*":JCR.DEPT
     END CASE
     IF FIRST.FLAG=1 THEN JOB.NO=JCR.JOB;GOSUB 15
     PROD.CNT=COUNT(JCR.PROD,VM) + (JCR.PROD # '')
     I=0
     FOR I=1 TO PROD.CNT
        PROD.NO=JCR.PROD<1,I>
        UM=JCR.U.M<1,I>
        TRANS.DATE=JCR.DATE<1,I>
        TRANS.TYPE=JCR.TYPE<1,I>
        QTY=JCR.QTY<1,I>
        IF CS.FLG = "S" THEN
           REG.COST=JCR.SALE<1,I,1>
           OT.COST=JCR.SALE<1,I,2>
           DT.COST=JCR.SALE<1,I,3>
        END ELSE
           REG.COST=JCR.COST<1,I,1>
           OT.COST=JCR.COST<1,I,2>
           DT.COST=JCR.COST<1,I,3>
        END
        COST=REG.COST + OT.COST + DT.COST
        IF COST=0 THEN COST=''
        SPOIL.QTY=JCR.SPOIL.QTY<1,I>
        SPOIL.COST=JCR.SPOIL.COST<1,I>
        IF SPOIL.COST=0 THEN SPOIL.COST=''
        REASON=JCR.SPOIL.CODE<1,I>
        TRAN.JOB=JCR.RC.JOB<1,I>
        DPM.NO=FIELD(DPM.LINE,"*",2)
        IF FIRST.FLAG=1 THEN GOTO 11
        IF PREVIOUS.DPM # DPM.NO THEN
           GOSUB 50
           GOSUB 60
           IF LINE.CNT >= 50 THEN GOSUB 20
           PREVIOUS.DPM=DPM.NO
           PREVIOUS.PROD=PROD.NO
           GOTO 11
        END
        IF PREVIOUS.PROD # PROD.NO THEN
           GOSUB 50
           PRINT 
           LINE.CNT=LINE.CNT + 1
           IF LINE.CNT >= 50 THEN GOSUB 20
           PREVIOUS.PROD=PROD.NO
        END
11      DPM.DESC='';PROD.DESC=''
        GOSUB 30
        IF FIRST.FLAG=1 THEN
           FIRST.FLAG=0
           PREVIOUS.DPM=DPM.NO
           PREVIOUS.PROD=PROD.NO
        END
        REG.COST='';OT.COST='';DT.COST='';COST=''
     NEXT I
     GOTO 10
12*
     GOSUB 50
     GOSUB 60
     GOSUB 70
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFO FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     D.CNT=COUNT(JOB.MT.DATE,VM) + (JOB.MT.DATE # '')
     FOR T=1 TO D.CNT
        IF JOB.MT.DATE<1,T,1> > DATE.LAST.WORKED THEN DATE.LAST.WORKED=JOB.MT.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     T=0;D.CNT=0
     D.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR T=1 TO D.CNT
        IF DATE.TBL<1,T,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,T,1>
     NEXT T
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     LINE.CNT=0
     PG.NO=PG.NO + 1
     H.LINE='';HLINE1='';HLINE2='';HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     K=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=K:CO.NAME:K
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):'PAGE: ':PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.CUST"L#8":SPACE(39):'D E T A I L   R E P O R T (MATERIAL)':SPACE(27):'#':JOB.NO "L#8":" ":CONSOL
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:CUST.NAME"L#30":SPACE(72):CUST.NAME
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:JOB.DESC<1,1>"L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.WORKED,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>"L#30"
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'SR: ':SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT H.LINE
     PRINT
     H.LINE=''
     H.LINE=H.LINE:SPACE(100):'SPOIL    SPOIL'
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'DEPT  PRODUCT NUMBER  PRODUCT DESCRIPTION':SPACE(29):'DATE    U/M   QTY     COST     QTY     COST   REASON  XFER JOB'
     PRINT H.LINE
     H.LINE=''
     H.LINE=H.LINE:'----  --------------- ---------------------------------------------  -----    --- ------- -------- -------  ------ -------- --------'
     PRINT H.LINE
     LINE.CNT=LINE.CNT + 10
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF LINE.CNT >= 50 THEN GOSUB 20
     IF DPM.NO # PREVIOUS.DPM.NO THEN
        P.LINE=''
        P.LINE=P.LINE:DPM.DESC:'  ':DPM.NO 
        PRINT P.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
        PRINT STR('-',LEN(P.LINE))
     IF LINE.CNT >= 50 THEN GOSUB 20
        P.LINE=''
        P.LINE=P.LINE:SPACE(6):PROD.NO"L#15":' ':PROD.DESC"L#45":'  ':DATE"R#5":' ':TRANS.TYPE:' ':UM"L#3":' ':QTY"R#7":' ':COST"R#8":' ':SPOIL.QTY"R#7":'  ':SPOIL.COST"R#6":'  ':REASON"L#8":'  ':TRAN.JOB"L#7"
        PRINT P.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
        LINE.CNT=LINE.CNT + 3
     END ELSE
        P.LINE=''
        P.LINE=P.LINE:SPACE(6):PROD.NO"L#15":' ':PROD.DESC"L#45":'  ':DATE"R#5":' ':TRANS.TYPE:' ':UM"L#3":' ':QTY"R#7":' ':COST"R#8":' ':SPOIL.QTY"R#7":'  ':SPOIL.COST"R#6":'  ':REASON"L#8":'  ':TRAN.JOB"L#7"
        PRINT P.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
        LINE.CNT=LINE.CNT + 1
     END
     PREVIOUS.DPM.NO=DPM.NO
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     MATREAD INV.REC FROM INVENTORY,CONO:PROD.NO ELSE PROD.DESC=UNKNOWN
     IF PROD.DESC='' THEN PROD.DESC=INV.FULL.DESC
     MAT INV.CNV.REC = ''
     BEGIN CASE
        CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
           ICR.CNV = "MD0"
        CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
           ICR.CNV = "MD0"
        CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
           ICR.CNV = "MD0"
        CASE 1
           ICR.CNV = "MD2"
     END CASE
     DATE=OCONV(TRANS.DATE,"D2-")
     DATE=DATE[1,5]
     IF TRANS.TYPE='R' THEN
        TRANS.TYPE='  '
     END ELSE
        TRANS.TYPE='CO'
     END
     IF TRANS.TYPE='CO' THEN
        CO.TOTAL.REC(1)=CO.TOTAL.REC(1) + COST
        CO.TOTAL.REC(2)=CO.TOTAL.REC(2) + SPOIL.COST
     END ELSE
        REG.TOTAL.REC(1)=REG.TOTAL.REC(1) + COST
        REG.TOTAL.REC(2)=REG.TOTAL.REC(2) + SPOIL.COST
     END
     DEPT.TOTAL.REC(1)=DEPT.TOTAL.REC(1) + COST
     DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + SPOIL.COST
     GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + COST
     GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + SPOIL.COST
     DPM=FIELD(DPM.LINE,"*",1)
     DPM.NO=FIELD(DPM.LINE,"*",2)
     BEGIN CASE
        CASE DPM="D"
           MATREAD DEPT.REC FROM DEPARTMENT,CONO:DPM.NO ELSE DPM.DESC=UNKNOWN
           IF DPM.DESC='' THEN DPM.DESC=DEPT.DESC
        CASE DPM="P"
           MATREAD CATG.REC FROM CATEGORY,CONO:DPM.NO ELSE DPM.DESC=UNKNOWN
           IF DPM.DESC='' THEN DPM.DESC=CATG.DESC
           IF TRANS.TYPE='CO' THEN
              CO.TOTAL.REC(3)=CO.TOTAL.REC(3) + QTY
           END ELSE
              REG.TOTAL.REC(3)=REG.TOTAL.REC(3) + QTY
           END
           DEPT.TOTAL.REC(3)=DEPT.TOTAL.REC(3) + QTY
           GRAND.TOTAL.REC(3)=GRAND.TOTAL.REC(3) + QTY
        CASE DPM="M"
           IF DPM.DESC='' THEN DPM.DESC=DPM.NO
     END CASE
     IF COST # '' THEN COST=OCONV(COST,"MD2")
     IF SPOIL.COST # '' THEN SPOIL.COST=OCONV(SPOIL.COST,"MD2")
     BEGIN CASE
     CASE QTY = ""
     CASE ICR.CNV = "MD0"
        QTY = OCONV(QTY,ICR.CNV)
     CASE 1
        PQTY = OCONV(QTY,ICR.CNV)
        BEGIN CASE
        CASE FIELD(PQTY,".",2) = 0
           QTY = FIELD(PQTY,".",1)
        CASE LEN(PQTY) > 7
           QTY = INT((QTY / 100) + .5)
        CASE 1
           QTY = PQTY
        END CASE
     END CASE
     BEGIN CASE
     CASE SPOIL.QTY = ""
     CASE ICR.CNV = "MD0"
        SPOIL.QTY = OCONV(SPOIL.QTY,ICR.CNV)
     CASE 1
        PQTY = OCONV(SPOIL.QTY,ICR.CNV)
        BEGIN CASE
        CASE FIELD(PQTY,".",2) = 0
           SPOIL.QTY = FIELD(PQTY,".",1)
        CASE LEN(PQTY) > 7
           SPOIL.QTY = INT((SPOIL.QTY / 100) + .5)
        CASE 1
           SPOIL.QTY = PQTY
        END CASE
     END CASE
     RETURN
*
*---- PRINT SUBTOTALS
*
50*
     IF CO.TOTAL.REC(3) # "" THEN CO.TOTAL.REC(3) = OCONV(CO.TOTAL.REC(3),ICR.CNV)
     IF REG.TOTAL.REC(3) # "" THEN REG.TOTAL.REC(3) = OCONV(REG.TOTAL.REC(3),ICR.CNV)
     IF CO.TOTAL.REC(1) # '' THEN CO.TOTAL.REC(1)=OCONV(CO.TOTAL.REC(1),"MD2")
     IF CO.TOTAL.REC(2) # '' THEN CO.TOTAL.REC(2)=OCONV(CO.TOTAL.REC(2),"MD2")
     IF REG.TOTAL.REC(1) # '' THEN REG.TOTAL.REC(1)=OCONV(REG.TOTAL.REC(1),"MD2")
     IF REG.TOTAL.REC(2) # '' THEN REG.TOTAL.REC(2)=OCONV(REG.TOTAL.REC(2),"MD2")
     S.LINE=''
     IF DPM # "P" THEN
        S.LINE=S.LINE:SPACE(90):'--------          ------'
     END ELSE
        S.LINE=S.LINE:SPACE(82):'------- --------          ------'
     END
     PRINT S.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
     S.LINE=SPACE(62):'*   REG COST TOTAL  ':REG.TOTAL.REC(3)"R#7":' ':REG.TOTAL.REC(1)"R#8":SPACE(10):REG.TOTAL.REC(2)"R#6"
     PRINT S.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
     LINE.CNT=LINE.CNT + 2
     IF CO.TOTAL.REC(3)+0 # 0 OR CO.TOTAL.REC(1)+0 # 0 OR CO.TOTAL.REC(2)+0 # 0 THEN
        S.LINE=SPACE(62):'*    CO COST TOTAL  ':CO.TOTAL.REC(3)"R#7":' ':CO.TOTAL.REC(1)"R#8":SPACE(10):CO.TOTAL.REC(2)"R#6"
        PRINT S.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
        LINE.CNT=LINE.CNT + 1
     END
     MAT REG.TOTAL.REC=''
     MAT CO.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT DEPARTMENT TOTALS
*
60*
     IF DEPT.TOTAL.REC(1) # '' THEN DEPT.TOTAL.REC(1)=OCONV(DEPT.TOTAL.REC(1),"MD2")
     IF DEPT.TOTAL.REC(2) # '' THEN DEPT.TOTAL.REC(2)=OCONV(DEPT.TOTAL.REC(2),"MD2")
     IF DEPT.TOTAL.REC(3) # '' THEN DEPT.TOTAL.REC(3)=OCONV(DEPT.TOTAL.REC(3),"MD0")
     D.LINE=''
     IF DPM # "P" THEN
        D.LINE=D.LINE:SPACE(90):'--------          ------'
     END ELSE
        D.LINE=D.LINE:SPACE(82):'------- --------          ------'
     END
     PRINT D.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
     D.LINE='** ':DPM.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
     D.LINE=D.LINE:SPACE(42):DEPT.TOTAL.REC(3)"R#7":' ':DEPT.TOTAL.REC(1)"R#8":SPACE(10):DEPT.TOTAL.REC(2)"R#6"
     PRINT D.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
     PRINT
     IF LINE.CNT >= 50 THEN GOSUB 20
     PRINT
     IF LINE.CNT >= 50 THEN GOSUB 20
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT GRAND TOTALS
*
70*
     IF GRAND.TOTAL.REC(1) # '' THEN GRAND.TOTAL.REC(1)=OCONV(GRAND.TOTAL.REC(1),"MD2")
     IF GRAND.TOTAL.REC(2) # '' THEN GRAND.TOTAL.REC(2)=OCONV(GRAND.TOTAL.REC(2),"MD2")
     IF GRAND.TOTAL.REC(3) # '' THEN GRAND.TOTAL.REC(3)=OCONV(GRAND.TOTAL.REC(3),"MD0")
     IF GRAND.TOTAL.REC(3)='' THEN
        G.LINE=SPACE(90):'--------          ------'
     END ELSE
        G.LINE=SPACE(82):'------- --------          ------'
     END
     PRINT G.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
     G.LINE="*** GRAND TOTALS ***":SPACE(62):GRAND.TOTAL.REC(3)"R#7":' ':GRAND.TOTAL.REC(1)"R#8":SPACE(10):GRAND.TOTAL.REC(2)"R#6"
     PRINT G.LINE
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
