*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - EMP.PERFORM.PRT
* AUTHOR      - JIHAD YAMOUT, C.B.A
* DATE        - 10/04/83
* MODIFIED    - 06/30/88 BY KERRY WILSON
* MOD         - 10/05/94 TASK 18439 (LMR)
* DESCRIPTION - This program prints the detail employee performance report.
*
*T22007 stefanie 08/26/1997 * Add Division to the heading.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T26928 cmykleb 10/21/2002 * Concurrent time entries are being zeroed
*                            out in error.
*********************************************************************
*
***** INSERT FILES EQUEATES
*
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
   OPEN '','JOB.TIME' TO JOB.TIME ELSE
      ERRMSG ='JOB.FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','COST.CNTR' TO COST.CNTR ELSE
      ERRMSG = 'COST.CNTR FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','EMPLOYEE' TO EMPLOYEE ELSE
      ERRMSG = 'EMPLOYEE FILE IS MISSING'
      GOTO 93000
   END
*
**** READ PROC VALUES
*
   PROCREAD ID ELSE
      ERRMSG = 'CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
*T26493 v
*     CONO.NAME = ID<2>
*     REPORT.NAME = "EMPLOYEE.PERFORMANCE.DETAIL"
*     REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   ST.DATE = ID<4>
   EN.DATE = ID<5>
   ST.PER = ID<6>
   EN.PER = ID<7>
   STD.FLG = ID<8>
   FILE.FLG = ID<9>
   DIVISION = ID<10>
*
**** INITIALIZATION
*
   LINE.COUNT = 99
   PAGE.NO = 0
   PRT.FLG = 1
   HH.LINE1 = "                                                  DATE       "
   HH.LINE2 = "        EMPLOYEE NAME         DV DEPT  CTR OPER  WORKED   FC "
   UH.LINE  = "----------------------------- -- ----- --- ---- --------  -- "
   HH.LINE1 = HH.LINE1:"   JOB      ACT     STD      %    CNCR    ACTUAL    ACT    STD"
   HH.LINE2 = HH.LINE2:"  NUMBER   HOURS   HOURS    EFF   HOURS  QUANTITY  QTY/HR QTY/HR COUNT"
   UH.LINE  = UH.LINE :" -------- ------- ------- ------ ------ ---------- ------ ------ -----"
   UT.LINE  = "                                                             "
   UT.LINE  = UT.LINE :"          ------- ------- ------ ------                          -----"
*
***** TOTAL TABLE FOR EMPLOYEE
*
   DIM TOT.EMP(7)
   EQU EMP.TIME        TO TOT.EMP(1)
   EQU EMP.CNCR.TIME   TO TOT.EMP(2)
   EQU EMP.IMP         TO TOT.EMP(3)
   EQU EMP.CNT         TO TOT.EMP(4)
   EQU EMP.STD.HRS     TO TOT.EMP(5)
   EQU EMP.STD.IMP     TO TOT.EMP(6)
   MAT TOT.EMP = 0
*
***** TOTAL TABLE FOR DIV
*
   DIM TOT.DIV(7)
   EQU DIV.TIME        TO TOT.DIV(1)
   EQU DIV.CNCR.TIME   TO TOT.DIV(2)
   EQU DIV.IMP         TO TOT.DIV(3)
   EQU DIV.CNT         TO TOT.DIV(4)
   EQU DIV.STD.HRS     TO TOT.DIV(5)
   EQU DIV.STD.IMP     TO TOT.DIV(6)
   MAT TOT.DIV = 0
*
***** TOTAL TABLE FOR DEPT
*
   DIM TOT.DEPT(7)
   EQU DEPT.TIME        TO TOT.DEPT(1)
   EQU DEPT.CNCR.TIME   TO TOT.DEPT(2)
   EQU DEPT.IMP         TO TOT.DEPT(3)
   EQU DEPT.CNT         TO TOT.DEPT(4)
   EQU DEPT.STD.HRS     TO TOT.DEPT(5)
   EQU DEPT.STD.IMP     TO TOT.DEPT(6)
   MAT TOT.DEPT = 0
*
***** TOTAL TABLE FOR CCTR
*
   DIM TOT.CCTR(7)
   EQU CCTR.TIME        TO TOT.CCTR(1)
   EQU CCTR.CNCR.TIME   TO TOT.CCTR(2)
   EQU CCTR.IMP         TO TOT.CCTR(3)
   EQU CCTR.CNT         TO TOT.CCTR(4)
   EQU CCTR.STD.HRS     TO TOT.CCTR(5)
   EQU CCTR.STD.IMP     TO TOT.CCTR(6)
   MAT TOT.CCTR = 0
*
***** READ AND PROCESS FIRST RECORD
*
   PRINTER ON
100 *
   READNEXT KEY ELSE GOTO 99999
   MATREAD JLB.REC FROM JOB.TIME,KEY ELSE GOTO 100
   MATREAD EMP.REC FROM EMPLOYEE, CONO:JLB.EMP ELSE
      EMP.LAST.NAME = '??????????'
      EMP.FRST.NAME = '??????????'
   END
   EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
   PREV.EMP = JLB.EMP
   PREV.DIV = JLB.DIV
   PREV.DEPT = FIELD(KEY,"!",2)
   PREV.CCTR = FIELD(KEY,"!",3)
   PREV.OPER = JLB.OPER
   PREV.FCTR = JLB.FCTR
   DPNO = PREV.DEPT
   CCNO = PREV.CCTR
   JOBNO = FIELD(KEY,"!",1)[4,99]
   CNT = 0
   CONO = KEY[1,3]
   MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
      MAT CCTR.REC = ""
      GOTO 100
   END
   LOCATE JLB.OPER IN CCTR.OPER<1>,1 SETTING PP ELSE
      GOTO 100
   END
*T26928 v
*  IF JLB.CNCR + 0 # 0 THEN
   IF JLB.CNCR + 0 # 0 AND JLB.CNCR = JLB.HRS AND JLB.RC.DESC = 'GANG ALLOC' THEN
*T26928 ^
      JLB.HRS = 0
      JLB.IMP = 0
      JLB.HRS.STD = 0
      JLB.IMP.STD = 0
      CCTR.OPER.STD.HRS<1,PP> = 0
      CCTR.OPER.STD.IMP<1,PP> = 0
   END
   PREV.DATA = OCONV(KEY,'G!3'):"!":JLB.OPER
*
   IF FILE.FLG = 'C' THEN
      IF STD.FLG = 'N' THEN
         IF CCTR.OPER.STD.HRS<1,PP>+0 = 0 AND CCTR.OPER.STD.IMP<1,PP>+0 = 0 THEN GOTO 100
      END
      HRS.STD = CCTR.OPER.STD.HRS<1,PP> + 0
      IMP.STD = CCTR.OPER.STD.IMP<1,PP> + 0
   END ELSE
      IF STD.FLG = 'N' THEN
         IF JLB.HRS.STD+0 = 0 AND JLB.IMP.STD+0 = 0 THEN GOTO 100
      END
      HRS.STD = JLB.HRS.STD + 0
      IMP.STD = CCTR.OPER.STD.IMP<1,PP> + 0
*        IMP.STD = JLB.IMP.STD
   END
   GOSUB 1000
   GOSUB 2000
*
***** READ AND PROCESS ALL RECORDS
*
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      MATREAD JLB.REC FROM JOB.TIME,KEY ELSE GOTO 111
      DPNO = FIELD(KEY,"!",2)
      CCNO = FIELD(KEY,"!",3)
      JOBNO = FIELD(KEY,"!",1)[4,99]
      CONO = KEY[1,3]
      MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
         MAT CCTR.REC = ""
         GOTO 111
      END
      LOCATE JLB.OPER IN CCTR.OPER<1>,1 SETTING PP ELSE
         GOTO 111
      END
* T18439
*      IF JLB.CNCR + 0 # 0 THEN
   IF JLB.CNCR + 0 # 0 AND JLB.CNCR = JLB.HRS AND JLB.RC.DESC = 'GANG ALLOC' THEN
         JLB.HRS = 0
         JLB.IMP = 0
         JLB.HRS.STD = 0
         JLB.IMP.STD = 0
         CCTR.OPER.STD.HRS<1,PP> = 0
         CCTR.OPER.STD.IMP<1,PP> = 0
      END
      IF OCONV(KEY,'G!3'):"!":JLB.OPER = PREV.DATA AND JLB.HRS + 0 = 0 THEN JLB.HRS.STD = 0; JLB.IMP.STD = 0; CCTR.OPER.STD.HRS<1,PP> = 0; CCTR.OPER.STD.IMP<1,PP> = 0
      PREV.DATA = OCONV(KEY,'G!3'):"!":JLB.OPER
*
      IF FILE.FLG = 'C' THEN
         IF STD.FLG = 'N' THEN
            IF CCTR.OPER.STD.HRS<1,PP>+0 = 0 AND CCTR.OPER.STD.IMP<1,PP>+0 = 0 THEN GOTO 111
         END
         HRS.STD = CCTR.OPER.STD.HRS<1,PP> + 0
         IMP.STD = CCTR.OPER.STD.IMP<1,PP> + 0
      END ELSE
         IF STD.FLG = 'N' THEN
            IF JLB.HRS.STD+0 = 0 AND JLB.IMP.STD+0 = 0 THEN GOTO 111
         END
         HRS.STD = JLB.HRS.STD + 0
         IMP.STD = CCTR.OPER.STD.IMP<1,PP> + 0
*           IMP.STD = JLB.IMP.STD
      END
      GOSUB 400
      GOSUB 1000
      GOSUB 2000
111 *
   REPEAT
   GOSUB 4003
   GOSUB 4004
   GOSUB 4005
   GOSUB 4006
   GOTO 99999
*
**** FIND BREAK AND PRINT TOTALS
*
400 *
   IF PREV.EMP # JLB.EMP THEN
      GOSUB 4003
      GOSUB 4004
      GOSUB 4005
      GOSUB 4006
      PRINT
      LINE.COUNT = LINE.COUNT + 1
      PRT.FLG = 1
   END
   IF PREV.DIV # JLB.DIV THEN
      GOSUB 4003
      GOSUB 4004
      GOSUB 4005
      PRINT
      LINE.COUNT = LINE.COUNT + 1
      PRT.FLG = 1
   END
   IF PREV.DEPT # DPNO THEN
      GOSUB 4003
      GOSUB 4004
      PRINT
      LINE.COUNT = LINE.COUNT + 1
      PRT.FLG = 1
   END
   IF PREV.CCTR # CCNO THEN
      GOSUB 4003
      PRINT
      LINE.COUNT = LINE.COUNT + 1
      PRT.FLG = 1
   END
   RETURN
*
**** ADD UP ALL TOTALS
*
1000 *
   CNT = CNT + 1
   STD.HRS = 0
   EFF.HRS = 0
   IMP.HRS = 0
   STD.IMP = 0
   EFF.IMP = 0
   JLB.HRS = JLB.HRS + 0
   JLB.IMP = JLB.IMP + 0
   IF NOT(NUM(JLB.CNCR)) THEN JLB.CNCR = 0
   JLB.CNCR = JLB.CNCR + 0
*T18439
   IF JLB.HRS + 0 = 0 THEN
      HRS.STD = 0
      IMP.STD = 0
   END
*
*
   IF JLB.HRS+0 >= 0 THEN SIGN = 1 ELSE SIGN = (-1)
   IF CCTR.OPER.TYPE<1,PP> = 'V' OR CCTR.OPER.TYPE<1,PP> = 'U' THEN
      IF JLB.HRS+0 # 0 THEN
         IMP.HRS = INT(JLB.IMP / JLB.HRS)
         IMP.HRS = IMP.HRS * SIGN
      END
   END
   IF FILE.FLG = "C" THEN
      BEGIN CASE
         CASE CCTR.OPER.TYPE<1,PP> = "U"
            BEGIN CASE
               CASE HRS.STD+0 # 0
                  STD.HRS = INT(JLB.IMP * HRS.STD / 100)
                  STD.IMP = INT(100 / HRS.STD)
               CASE IMP.STD+0 # 0
                  STD.HRS = INT(JLB.IMP / IMP.STD)
                  STD.IMP = IMP.STD
               CASE 1
                  STD.HRS = 0
                  STD.IMP = 0
            END CASE
         CASE CCTR.OPER.TYPE<1,PP> = "V"
            IF IMP.STD+0 # 0 THEN
               STD.HRS = INT(JLB.IMP / IMP.STD)
               STD.IMP = IMP.STD
            END ELSE
               STD.HRS = 0
               STD.IMP = 0
            END
         CASE 1
            STD.HRS = HRS.STD + 0
            STD.IMP = 0
      END CASE
   END ELSE
      STD.HRS = HRS.STD + 0
      STD.IMP = IMP.STD + 0
   END
   IF JLB.HRS+0 # 0 THEN
      EFF.HRS = INT(STD.HRS * 100 / JLB.HRS * 100)
   END ELSE
      EFF.HRS = 0
   END
*
**** ADD TOTAL FOR CCTR
*
   CCTR.TIME = CCTR.TIME + JLB.HRS
   CCTR.CNCR.TIME = CCTR.CNCR.TIME + JLB.CNCR
   CCTR.IMP = CCTR.IMP + JLB.IMP + 0
   CCTR.CNT = CCTR.CNT + 1
   CCTR.STD.HRS = CCTR.STD.HRS + STD.HRS
   CCTR.STD.IMP = CCTR.STD.IMP + STD.IMP
*
**** ADD TOTAL FOR DEPT
*
   DEPT.TIME = DEPT.TIME + JLB.HRS
   DEPT.CNCR.TIME = DEPT.CNCR.TIME + JLB.CNCR
   DEPT.IMP = DEPT.IMP + JLB.IMP
   DEPT.CNT = DEPT.CNT + 1
   DEPT.STD.HRS = DEPT.STD.HRS + STD.HRS
   DEPT.STD.IMP = DEPT.STD.IMP + STD.IMP
*
**** ADD TOTAL FOR DIV
*
   DIV.TIME = DIV.TIME + JLB.HRS
   DIV.CNCR.TIME = DIV.CNCR.TIME + JLB.CNCR
   DIV.IMP = DIV.IMP + JLB.IMP
   DIV.CNT = DIV.CNT + 1
   DIV.STD.HRS = DIV.STD.HRS + STD.HRS
   DIV.STD.IMP = DIV.STD.IMP + STD.IMP
*
**** ADD TOTAL FOR EMPLOYEE
*
   EMP.TIME = EMP.TIME + JLB.HRS
   EMP.CNCR.TIME = EMP.CNCR.TIME + JLB.CNCR
   EMP.IMP = EMP.IMP + JLB.IMP
   EMP.CNT = EMP.CNT + 1
   EMP.STD.HRS = EMP.STD.HRS + STD.HRS
   EMP.STD.IMP = EMP.STD.IMP + STD.IMP
1001 *
   RETURN
*
***** PRINT DETAIL LINE
*
2000 *
   IF LINE.COUNT > 50 THEN
      GOSUB 9000
      PRT.FLG = 1
   END
   IF PRT.FLG = 1 THEN
      PRT.FLG = 0
      DET.LINE = ''
      DET.LINE = DET.LINE : EMP.NAME "L#29":' ':JLB.DIV "L#2":' '
      DET.LINE = DET.LINE : DPNO "L#5":' ':CCNO "L#3":' '
   END ELSE
      DET.LINE = SPACE(43)
   END
   DET.LINE = DET.LINE : JLB.OPER "L#4":' '
   DET.LINE = DET.LINE : OCONV(JLB.DATE,"D2/") "L#8":'  '
   DET.LINE = DET.LINE : JLB.FCTR "L#2":'  '
   DET.LINE = DET.LINE : JOBNO "L#8"
   DET.LINE = DET.LINE : OCONV(JLB.HRS,"MD2") "R#8"
   DET.LINE = DET.LINE : OCONV(STD.HRS,"MD2") "R#8"
   DET.LINE = DET.LINE : OCONV(EFF.HRS,"MD2") "R#7"
   DET.LINE = DET.LINE : OCONV(JLB.CNCR,"MD2") "R#7"
   DET.LINE = DET.LINE : OCONV(JLB.IMP,"MD2") "R#11"
   DET.LINE = DET.LINE : IMP.HRS "R#7"
   DET.LINE = DET.LINE : STD.IMP "R#7"
   DET.LINE = DET.LINE : CNT "R#6"
   PRINT DET.LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
*       GOSUB 9000
**** PRINT TOTAL FOR CCTR
*
4003 *
   IF LINE.COUNT > 50 THEN
      GOSUB 9000
   END
   GOSUB 4009
   CCTR.KEY = CONO:PREV.CCTR
   CC.LINE = ''
   CC.LINE = CC.LINE :SPACE(22):OCONV(CCTR.KEY,"TCOST.CNTR;X;1;1") "R#30":' COST CENTER TOTAL   ' "L#18"
   CC.LINE = CC.LINE :OCONV(CCTR.TIME,"MD2") "R#8"
   CC.LINE = CC.LINE :OCONV(CCTR.STD.HRS,"MD2") "R#8"
   IF CCTR.TIME > 0 THEN
      CCTR.EFF.HRS = INT(CCTR.STD.HRS / CCTR.TIME * 10000)
   END ELSE
      CCTR.EFF.HRS = 0
   END
   CC.LINE = CC.LINE :OCONV(CCTR.EFF.HRS,"MD2") "R#7"
   CC.LINE = CC.LINE :OCONV(CCTR.CNCR.TIME,"MD2") "R#7"
   CC.LINE = CC.LINE :SPACE(25)
   CC.LINE = CC.LINE :CCTR.CNT "R#6"
   PRINT CC.LINE
   LINE.COUNT = LINE.COUNT + 1
   PREV.CCTR = CCNO
   CCTR.CNT = 0
   CNT = 0
   MAT TOT.CCTR = ''
   RETURN
*
**** PRINT TOTAL FOR DEPT
*
4004 *
   IF LINE.COUNT > 50 THEN
      GOSUB 9000
   END
   GOSUB 4009
   DEPT.KEY = CONO:PREV.DEPT
   DE.LINE = ''
   DE.LINE = DE.LINE :SPACE(22):OCONV(DEPT.KEY,"TDEPARTMENT;X;2;2") "R#30":' DEPARTMENT TOTAL    ' "L#18"
   DE.LINE = DE.LINE :OCONV(DEPT.TIME,"MD2") "R#8"
   DE.LINE = DE.LINE :OCONV(DEPT.STD.HRS,"MD2") "R#8"
   IF DEPT.TIME+0 > 0 THEN
      DEPT.EFF.HRS = INT(DEPT.STD.HRS / DEPT.TIME * 10000)
   END ELSE
      DEPT.EFF.HRS = 0
   END
   DE.LINE = DE.LINE :OCONV(DEPT.EFF.HRS,"MD2") "R#7"
   DE.LINE = DE.LINE :OCONV(DEPT.CNCR.TIME,"MD2") "R#7"
   DE.LINE = DE.LINE :SPACE(25)
   DE.LINE = DE.LINE :DEPT.CNT "R#6"
   PRINT DE.LINE
   DEPT.CNT = 0
   CNT = 0
   LINE.COUNT = LINE.COUNT + 1
   PREV.DEPT = DPNO
   MAT TOT.DEPT = ''
   RETURN
*
**** PRINT TOTAL FOR DIV
*
4005 *
   IF LINE.COUNT > 50 THEN
      GOSUB 9000
   END
   GOSUB 4009
   DIV.KEY = CONO:PREV.DIV
   DI.LINE = ''
   DI.LINE = DI.LINE :SPACE(22):OCONV(DIV.KEY,"TDIVISION;X;1;1") "R#30":' DIVISION TOTAL      ' "L#18"
   DI.LINE = DI.LINE :OCONV(DIV.TIME,"MD2") "R#8"
   DI.LINE = DI.LINE :OCONV(DIV.STD.HRS,"MD2") "R#8"
   IF DIV.TIME+0 > 0 THEN
      DIV.EFF.HRS = INT(DIV.STD.HRS / DIV.TIME * 10000)
   END ELSE
      DIV.EFF.HRS = 0
   END
   DI.LINE = DI.LINE :OCONV(DIV.EFF.HRS,"MD2") "R#7"
   DI.LINE = DI.LINE :OCONV(DIV.CNCR.TIME,"MD2") "R#7"
   DI.LINE = DI.LINE :SPACE(25)
   DI.LINE = DI.LINE :DIV.CNT "R#6"
   PRINT DI.LINE
   DIV.CNT = 0
   CNT = 0
   LINE.COUNT = LINE.COUNT + 1
   PREV.DIV = JLB.DIV
   MAT TOT.DIV = ''
   RETURN
*
**** PRINT TOTAL FOR EMP
*
4006 *
   IF LINE.COUNT > 50 THEN
      GOSUB 9000
   END
   GOSUB 4009
   EM.LINE = ''
   EM.LINE = EM.LINE :SPACE(22):PREV.EMP "R#30":' EMPLOYEE TOTAL      ' "L#18"
   EM.LINE = EM.LINE :OCONV(EMP.TIME,"MD2") "R#8"
   EM.LINE = EM.LINE :OCONV(EMP.STD.HRS,"MD2") "R#8"
   IF EMP.TIME+0 > 0 THEN
      EMP.EFF.HRS = INT(EMP.STD.HRS / EMP.TIME * 10000)
   END ELSE
      EMP.EFF.HRS = 0
   END
   EM.LINE = EM.LINE :OCONV(EMP.EFF.HRS,"MD2") "R#7"
   EM.LINE = EM.LINE :OCONV(EMP.CNCR.TIME,"MD2") "R#7"
   EM.LINE = EM.LINE :SPACE(25)
   EM.LINE = EM.LINE :EMP.CNT "R#6"
   PRINT EM.LINE
   EMP.CNT = 0
   CNT = 0
   LINE.COUNT = LINE.COUNT + 1
   PREV.EMP = JLB.EMP
   MAT TOT.EMP = ''
   MATREAD EMP.REC FROM EMPLOYEE, CONO:JLB.EMP ELSE
      EMP.LAST.NAME = '??????????'
      EMP.FRST.NAME = '??????????'
   END
   EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
   RETURN
*
****** DASH LINE FOR TOTAL
*
4009 *
   PRINT UT.LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
*
***** PRINT HEADINGS
*
9000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
* T22007 v
*  H.LINE = H.LINE :SPACE(59):"FOR PERIOD ":ST.PER:" THROUGH ":EN.PER
   H.LINE = 'DIVISION : ':DIVISION
   H.LINE = H.LINE :SPACE(46):"FOR PERIOD ":ST.PER:" THROUGH ":EN.PER
* T22007 ^
   PRINT H.LINE
   PRINT SPACE(63):"FROM ":OCONV(ST.DATE,"D2/"):" TO ":OCONV(EN.DATE,"D2/")
   PRINT
   PRINT HH.LINE1
   PRINT HH.LINE2
   PRINT UH.LINE
   LINE.COUNT = 8
   RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
   PRINTER OFF
END
