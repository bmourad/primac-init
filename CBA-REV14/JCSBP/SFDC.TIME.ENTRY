*****************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM   - SFDC.TIME.ENTRY
*
* AUTHOR    - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE      - 03/09/86
*
* DESCRIPTION
*
* This screen will be used for Shop Floor Data (Time) Collection
* Entry.  It updates ADC.TIME file, and calls a second routine
* to display all Division, Department, Cost Centers and Operation
* numbers and descriptions.
*
*****************************************************************
*
******************************
******************************
******************************
****************************** CHANGE FOR CCTR/OPER UNITS OPER TYPE
******************************
******************************
******************************
*---- FILE EQUATES
*
*COPY>CPYLIB>SCOMMON1
*COPY>CPYLIB>SCREEN.COM
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>ADC.ACCESS
*COPY>JCS.CPYLIB>ADC.TIME
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*COPY>JCS.CPYLIB>DAILY.TIME
*
*---- DIMENSION LB.TBL
*
      DIM LB.TBL(40)
      EQU LB.EMP.NO         TO LB.TBL(1)
      EQU LB.DIV            TO LB.TBL(2)
      EQU LB.DIV.DESC       TO LB.TBL(3)
      EQU LB.DEPT           TO LB.TBL(4)
      EQU LB.DEPT.DESC      TO LB.TBL(5)
      EQU LB.CCTR           TO LB.TBL(6)
      EQU LB.CCTR.DESC      TO LB.TBL(7)
      EQU LB.OPER           TO LB.TBL(8)
      EQU LB.OPER.DESC      TO LB.TBL(9)
      EQU LB.TYPE           TO LB.TBL(10)
      EQU LB.FCTR           TO LB.TBL(11)
      EQU LB.CNCR           TO LB.TBL(12)
      EQU LB.IMP            TO LB.TBL(13)
      EQU LB.JOB.NO         TO LB.TBL(14)
      EQU LB.CUST.NO        TO LB.TBL(15)
      EQU LB.STOP.TIME      TO LB.TBL(16)
      EQU LB.EMP.NAME       TO LB.TBL(17)
      EQU LB.JOB.DESC1      TO LB.TBL(18)
      EQU LB.JOB.DESC2      TO LB.TBL(19)
      EQU LB.JOB.DESC3      TO LB.TBL(20)
      EQU LB.JOB.DESC4      TO LB.TBL(21)
      EQU LB.CUST.NAME      TO LB.TBL(22)
      EQU LB.DLB.KEY        TO LB.TBL(23)
      EQU LB.HRS.ST         TO LB.TBL(24)
      EQU LB.DATE           TO LB.TBL(25)
      EQU LB.HRS.END        TO LB.TBL(26)
      EQU LB.SEQ            TO LB.TBL(27)
      EQU LB.ERR.FLG        TO LB.TBL(28)
      EQU LB.START.TIME     TO LB.TBL(29)
      EQU LB.LOGON.DATE     TO LB.TBL(30)
      EQU LB.COST           TO LB.TBL(31)
      EQU LB.MARKUP         TO LB.TBL(32)
      EQU LB.SP.CD          TO LB.TBL(33)
      EQU LB.CURRENT.TIME   TO LB.TBL(34)
      EQU LB.SHIFT          TO LB.TBL(35)
      DIM DT.REC(2)
      EQU DT.NO   TO DT.REC(1)
      EQU DT.DESC TO DT.REC(2)
*
*---- OPEN ALL FILES
*
      OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','DAILY.TIME' TO DAILY.TIME ELSE ERRMSG='DAILY.TIME FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','ADC.ACCESS' TO ADC.ACCESS ELSE ERRMSG='ADC.ACCESS FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','ADC.TIME' TO ADC.TIME ELSE ERRMSG='ADC.TIME FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','DIVISION' TO DIVISION ELSE ERRMSG='DIVISION FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG='EMPLOYEE FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','SPOILAGE.CODES' TO SPOILAGE.CODES ELSE ERRMSG='SPOILAGE.CODES FILE MISSING';GOSUB 90000;GOTO 99999
      OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='JCS.SCREENS FILE MISSING';GOSUB 90000;GOTO 99999
*
*---- GET COMPANY NUMBER
*
      CONO=''
      CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
      IF CONO='END' THEN GOTO 99999
*
*---- INITIALIZATION
*
      SCREEN INIT;#
      S$SCR = 1
      SCREEN DEFINE;SFDC.TIME.ENTRY
      SCREEN FORMAT;1
      T.DATE=OCONV(DATE(),"D2/")
      TODAY=DATE()
      MAT LB.TBL=""
      SCREEN COUNT;SFDC.TIME.ENTRY;35
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      CURR.REF.NO = ""
      S$DATA(1)<S$SCR> = DATE()
      SCREEN DISPLAY;1;1
      READV ALL.DIV FROM CONTROL,CONO:"DIVISIONS",1 ELSE ALL.DIV = "01"
      DIV.CNT = COUNT(ALL.DIV,VM) + (ALL.DIV # "")
*
*---- MAIN PROCESSING
*
5*
      SCREEN FIELD;1;2
      SCREEN INPUT;1;2
      IF S$VALUE='END' THEN GOTO 99999
      LB.EMP.NO=S$VALUE
      MATREAD EMP.REC FROM EMPLOYEE,CONO:LB.EMP.NO ELSE
         ERRMSG="INVALID EMPLOYEE NUMBER";GOSUB 90000;GOTO 5
      END
      EMP.NAME=EMP.FRST.NAME:' ':EMP.LAST.NAME
      LB.EMP.NAME=EMP.NAME
      S$DATA(3)<S$SCR> = LB.EMP.NAME
      SCREEN DISPLAY;1;3
      MATREAD ADA.REC FROM ADC.ACCESS,LB.EMP.NO ELSE
         ERRMSG="ADC.ACCESS FILE MISSING";GOSUB 90000;GOTO 5
      END
      IF ADA.TIME # "Y" THEN
         ERRMSG="YOU DO NOT HAVE ACCESS TO TIME ENTRY";GOSUB 90000;GOTO 5
      END
6*
      S$DATA(4)<S$SCR> = ""
      SCREEN FIELD;1;4
      SCREEN INPUT;1;4
      IF S$VALUE = "" AND ADA.PWD = "" THEN GOTO 7
      IF S$VALUE='END' THEN GOTO 5
      IF S$VALUE # ADA.PWD THEN
         ERRMSG="INVALID PASSWORD";GOSUB 90000;GOTO 6
      END
7*
      SCREEN FIELD;1;41
      SCREEN INPUT;1;41
      MODE=S$VALUE
      LB.SEQ=10001
      BEGIN CASE
         CASE MODE='E' OR MODE="END"
            GOTO 5
         CASE MODE='1'
            GOSUB 10
         CASE MODE='2'
            GOSUB 20
         CASE MODE='3'
            GOSUB 30
      END CASE
      MAT LB.TBL = ""
      SCREEN CLEAR;1
      SCREEN DISPLAY;1;1
      IF LB.ERR.FLG=1 THEN GOTO 7
      GOTO 5
*
*---- LOGON SELECTION
*
10*
      MATREADU ADT.REC FROM ADC.TIME,CONO:LB.EMP.NO ELSE
         MAT ADT.REC='';GOTO 11
      END
      ERRMSG="YOU HAVE ALREADY LOGGED ON FOR ":OCONV(ADT.DATE.ST,"D2/"):' AT ':OCONV(ADT.LOGON,"MD2");GOSUB 90000;LB.ERR.FLG=1;GOTO 19
11    GOSUB 100
      IF S$VALUE='END' OR S$VALUE='E' THEN GOTO 19
      SCREEN FIELD;1;42
      SCREEN INPUT;1;42
      IF S$VALUE=2 THEN GOTO 11
      IF S$VALUE='END' THEN GOTO 19
      ADT.DIV<1,1>=LB.DIV
      ADT.DIV<1,2>=LB.DIV.DESC
      ADT.DEPT<1,1>=LB.DEPT
      ADT.DEPT<1,2>=LB.DEPT.DESC
      ADT.CCTR<1,1>=LB.CCTR
      ADT.CCTR<1,2>=LB.CCTR.DESC
      ADT.DATE.ST=LB.DATE
      ADT.HRS.ST=INT(TIME() / 3600 * 100 + 0.5)
      ADT.TIME.ST=ADT.HRS.ST * 36
      ADT.LOGON=ADT.HRS.ST
      ADT.SHIFT=LB.SHIFT
      MATWRITE ADT.REC ON ADC.TIME,CONO:LB.EMP.NO
19*
      RETURN
*
*---- TIME ENTRY SELECTION
*
20*
      MATREADU ADT.REC FROM ADC.TIME,CONO:LB.EMP.NO ELSE
         ERRMSG="LOGON FOR EMPLOYEE ":LB.EMP.NO:" NOT COMPLETED";GOSUB 90000;LB.ERR.FLG=1;GOTO 29
      END
      LB.DIV=ADT.DIV<1,1>
      LB.DIV.DESC=ADT.DIV<1,2>
      LB.DEPT=ADT.DEPT<1,1>
      LB.DEPT.DESC=ADT.DEPT<1,2>
      LB.CCTR=ADT.CCTR<1,1>
      LB.CCTR.DESC=ADT.CCTR<1,2>
      LB.OPER=ADT.OPER<1,1>
      LB.OPER.DESC=ADT.OPER<1,2>
      LB.DATE=ADT.DATE.ST
      LB.TYPE="1"
      LB.FCTR="1"
      LB.CNCR="2"
      LB.IMP=''
      LB.JOB.NO=ADT.JOB.NO
      LB.START.TIME=ADT.TIME.ST
      LB.STOP.TIME=''
      LB.HRS.END=''
      LB.HRS.ST=ADT.HRS.ST
      LB.SHIFT=ADT.SHIFT
      MATREAD DIV.REC FROM DIVISION,CONO:LB.DIV ELSE DIV.DESC="DIVISION UNKNOWN"
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:LB.DEPT ELSE DEPT.DESC="DEPARTMENT UNKNOWN"
      GOSUB 1025
      IF LB.JOB.NO # '' THEN GOSUB 181
21    GOSUB 120
      IF S$VALUE="END" OR S$VALUE='E' THEN
         RELEASE ADC.TIME,CONO:LB.EMP.NO
         GOTO 29
      END
      GOSUB 190
      IF S$VALUE="END" OR S$VALUE='E' THEN
         RELEASE ADC.TIME,CONO:LB.EMP.NO
         GOTO 29
      END
      SCREEN FIELD;1;42
      SCREEN INPUT;1;42
      IF S$VALUE=2 THEN GOTO 21
      IF S$VALUE='END' THEN GOTO 29
      GOSUB 4000
      READU SEQ.NO FROM CONTROL,CONO:"DAILY.TIME" ELSE
         SEQ.NO=10001
      END
      LB.SEQ=SEQ.NO
      LB.DLB.KEY=CONO:LB.EMP.NO:"*":LB.DATE
      MATREADU DLB.REC FROM DAILY.TIME,LB.DLB.KEY ELSE
         MAT DLB.REC=''
      END
      DLB.CNT=COUNT(DLB.SEQ,VM) + (DLB.SEQ # '') + 1
      DLB.DIV<1,DLB.CNT>=LB.DIV
      DLB.DEPT<1,DLB.CNT>=LB.DEPT
      DLB.SEQ<1,DLB.CNT>=LB.SEQ
      DLB.CCTR<1,DLB.CNT>=LB.CCTR
      DLB.OPER<1,DLB.CNT>=LB.OPER
      DLB.TYPE<1,DLB.CNT>=LB.TYPE
      DLB.FCTR<1,DLB.CNT>=LB.FCTR
      DLB.HRS.ST<1,DLB.CNT>=LB.HRS.ST
      DLB.TIME.ST<1,DLB.CNT>=LB.START.TIME
      DLB.IMP<1,DLB.CNT>=LB.IMP
      DLB.CNCR<1,DLB.CNT>=LB.CNCR
      DLB.JOB<1,DLB.CNT>=LB.JOB.NO
      DLB.HRS.EN<1,DLB.CNT>=LB.HRS.END
      DLB.TIME.END<1,DLB.CNT>=LB.STOP.TIME
      DLB.COST<1,DLB.CNT>=LB.COST
      DLB.MARKUP<1,DLB.CNT>=LB.MARKUP
      IF LB.SP.CD # '' THEN DLB.SPOIL.CODE<1,DLB.CNT>=LB.SP.CD
      ADT.SEQ=LB.SEQ
      ADT.DIV<1,1>=LB.DIV
      ADT.DIV<1,2>=LB.DIV.DESC
      ADT.DEPT<1,1>=LB.DEPT
      ADT.DEPT<1,2>=LB.DEPT.DESC
      ADT.CCTR<1,1>=LB.CCTR
      ADT.CCTR<1,2>=LB.CCTR.DESC
      ADT.OPER<1,1>=LB.OPER
      ADT.OPER<1,2>=LB.OPER.DESC
      ADT.JOB.NO=LB.JOB.NO
      ADT.FCTR=LB.FCTR
      ADT.DATE.ST=LB.DATE
      ADT.HRS.ST=LB.HRS.END
      ADT.TIME.ST=LB.STOP.TIME
      MATWRITE ADT.REC ON ADC.TIME,CONO:LB.EMP.NO
      MATWRITE DLB.REC ON DAILY.TIME,LB.DLB.KEY
      SEQ.NO=SEQ.NO + 1
      WRITE SEQ.NO ON CONTROL,CONO:"DAILY.TIME"
29*
      RETURN
*
*---- LOGOFF SELECTION
*
30*
      MATREAD ADT.REC FROM ADC.TIME,CONO:LB.EMP.NO ELSE
         ERRMSG="LOGON FOR EMPLOYEE ":LB.EMP.NO:" NOT COMPLETED";GOSUB 90000;LB.ERR.FLG=1;GOTO 39
      END
      SCREEN FIELD;1;43
      SCREEN INPUT;1;43
      IF S$VALUE # 1 THEN GOTO 39
      GOSUB 300
39*
      RETURN
*
*---- ENTER DIVISION NUMBER
*
100*
      IF MODE=1 THEN
         SCREEN FIELD;1;5
         IF LB.DATE='' THEN
            S$DATA(5)<S$SCR>=T.DATE
         END ELSE
            S$DATA(5)<S$SCR>=OCONV(LB.DATE,"D2/")
         END
         SCREEN INPUT;1;5
         IF S$VALUE='.' THEN GOTO 100
         IF S$VALUE='END' THEN GOTO 189
         IF S$VALUE='' THEN GOTO 100
         LB.DATE=S$VALUE
         DAYS=LB.DATE - TODAY
         IF DAYS=0 OR DAYS=1 OR DAYS=-1 THEN GOTO 101 
         ERRMSG="LOGON DATE MUST BE DAY BEFORE, DAY AFTER OR DAY OF CURRENT DATE";GOSUB 90000;GOTO 100
101*
         SCREEN FIELD;1;6
         IF LB.SHIFT='' THEN
            S$DATA(6)<S$SCR>="1"
         END ELSE
            S$DATA(6)<S$SCR>=LB.SHIFT
         END
         SCREEN INPUT;1;6
         IF S$VALUE='.' THEN GOTO 100
         IF S$VALUE='END' OR S$VALUE='E' THEN GOTO 189
         IF S$VALUE='' THEN GOTO 101
         LB.SHIFT=S$VALUE
      END
102*
      FILE = 1
      REF.NO = 1
      CNT = DIV.CNT
      CURR.REF.NO = ""
      GOSUB 2000
103*
      SCREEN FIELD;1;7
      IF MODE=1 THEN
         IF LB.DIV='' THEN
            S$DATA(7)<S$SCR>="01"
         END ELSE
            S$DATA(7)<S$SCR>=LB.DIV
         END
      END ELSE
         IF LB.DIV # "" THEN
            S$DATA(7)<S$SCR>=LB.DIV
         END ELSE
            S$DATA(7)<S$SCR>='01'
         END
      END
      SCREEN INPUT;1;7
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 103
      IF S$VALUE=".." THEN
         REF.NO = CURR.REF.NO + LINE.CNT
         IF REF.NO > CNT THEN REF.NO = 1
         FILE=1;GOSUB 2000;GOTO 103
      END
      LB.DIV=S$VALUE
      MATREAD DIV.REC FROM DIVISION,CONO:LB.DIV ELSE
         ERRMSG="INVALID DIVISION NUMBER";GOSUB 90000;GOTO 103
      END
      LB.DIV.DESC=DIV.DESC
      S$DATA(8)<S$SCR> = LB.DIV.DESC
      SCREEN DISPLAY;1;8
*
*---- ENTER DEPARTMENT NUMBER
*
110*
      FILE = 2
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 2000
111*
      SCREEN FIELD;1;9
      IF MODE=1 THEN
         IF LB.DEPT='' THEN
            S$DATA(9)<S$SCR>=EMP.DEPT
         END ELSE
            S$DATA(9)<S$SCR>=LB.DEPT
         END
      END ELSE
         IF LB.DEPT # '' THEN
            S$DATA(9)<S$SCR>=LB.DEPT
         END ELSE
            S$DATA(9)<S$SCR>=EMP.DEPT
         END
      END
      SCREEN INPUT;1;9
      IF S$VALUE='.' THEN GOTO 102
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 111
      IF S$VALUE=".." THEN
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > CNT THEN REF.NO = 1
         FILE=2;GOSUB 2000;GOTO 111
      END
      LB.DEPT=S$VALUE
      LOCATE LB.DEPT IN DIV.DEPT<1>,1 SETTING D ELSE
         ERRMSG="DEPARTMENT ":LB.DEPT:" NOT IN DIVISION";GOSUB 90000;GOTO 111
      END
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:LB.DEPT ELSE
         ERRMSG="INVALID DEPARTMENT";GOSUB 90000;GOTO 111
      END
      LB.DEPT.DESC=DEPT.DESC
      S$DATA(10)<S$SCR>=LB.DEPT.DESC
      SCREEN DISPLAY;1;10
*
*---- ENTER COST CENTER NUMBER
*
120*
      FILE = 3
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 2000
121*
      SCREEN FIELD;1;11
      IF MODE=1 THEN
         IF LB.CCTR='' THEN
            S$DATA(11)<S$SCR>=''
         END ELSE
            S$DATA(11)<S$SCR>=LB.CCTR
         END
      END ELSE
         IF LB.CCTR='' THEN
            S$DATA(11)<S$SCR>=''
         END ELSE
            S$DATA(11)<S$SCR>=LB.CCTR
         END
      END
      SCREEN INPUT;1;11
      IF S$VALUE='.' THEN GOTO 110
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN
         IF MODE=1 THEN
            FILE = ""
            REF.NO = 1
            CURR.REF.NO = ""
            GOSUB 2000
            LB.CCTR=''
            LB.CCTR.DESC=''
            S$DATA(12)<S$SCR>=""
            SCREEN DISPLAY;1;12
            GOTO 189
         END ELSE
            GOTO 121
         END
      END
      IF S$VALUE=".." THEN
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > CNT THEN REF.NO = 1
         FILE=3;GOSUB 2000;GOTO 121
      END
      LB.CCTR=S$VALUE
      LOCATE LB.CCTR IN DEPT.CCTRS<1>,1 SETTING C ELSE
         ERRMSG="COST CENTER ":LB.CCTR:" NOT IN DEPARTMENT";GOSUB 90000;GOTO 121
      END
      MATREAD CCTR.REC FROM COST.CNTR,CONO:LB.CCTR ELSE
         ERRMSG="INVALID COST.CNTR";GOSUB 90000;GOTO 121
      END
      IF CCTR.DEPT # LB.DEPT THEN
         ERRMSG="INCORRECT DEPARTMENT IN COST CENTER ":LB.CCTR
         GOSUB 90000;GOTO 121
      END
      IF CCTR.TYPE='F' THEN
         ERRMSG='COST CENTER IS FROZEN';GOSUB 90000;GOTO 121
      END
      SAVE.OPER.TYPE=CCTR.OPER.TYPE
      SAVE.HR.RATE=CCTR.OPER.HR.RATE
      SAVE.MARKUP=CCTR.OPER.MARKUP
      LB.CCTR.DESC=CCTR.DESC
      S$DATA(12)<S$SCR>=LB.CCTR.DESC
      SCREEN DISPLAY;1;12
      IF MODE = 1 THEN
         FILE = ""
         REF.NO = 1
         CURR.REF.NO = ""
         GOSUB 2000
         GOTO 189
      END
*
*---- ENTER OPERATION NUMBER
130*
      FILE = 4
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 2000
131*
      SCREEN FIELD;1;13
      IF LB.OPER='' THEN
         S$DATA(13)<S$SCR>=''
      END ELSE
         S$DATA(13)<S$SCR>=LB.OPER
      END
      SCREEN INPUT;1;13
      IF S$VALUE='.' THEN GOTO 120
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 131
      IF S$VALUE=".." THEN
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > CNT THEN REF.NO = 1
         FILE=4;GOSUB 2000;GOTO 131
      END 
      LB.OPER=S$VALUE
      LOCATE LB.OPER IN CCTR.OPER<1>,1 SETTING O ELSE
         ERRMSG="OPERATION ":LB.OPER:" NOT IN COST CENTER";GOSUB 90000;GOTO 131
      END
      S.OPER.TYPE=SAVE.OPER.TYPE<1,O>
      LB.COST=SAVE.HR.RATE<1,O>
      LB.MARKUP=SAVE.MARKUP<1,O>
      MATREAD OPER.REC FROM OPERATION,CONO:LB.OPER ELSE
         ERRMSG="INVALID OPERATION";GOSUB 90000;GOTO 131
      END
      LB.OPER.DESC=OPER.DESC
      S$DATA(14)<S$SCR>=OPER.DESC
      SCREEN DISPLAY;1;14
      FILE = ""
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 2000
*
*---- ENTER TRANSACTION TYPE
*
140*
      SCREEN FIELD;1;15
      IF LB.TYPE='' THEN
         S$DATA(15)<S$SCR>='1'
      END ELSE
         IF LB.TYPE="R" THEN LB.TYPE="1"
         IF LB.TYPE="S" THEN LB.TYPE="2"
         IF LB.TYPE="C" THEN LB.TYPE="3"
         IF LB.TYPE="N" THEN LB.TYPE="4"
         S$DATA(15)<S$SCR>=LB.TYPE
      END
      SCREEN INPUT;1;15
      IF S$VALUE='.' THEN GOTO 130
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 140
      LB.TYPE=S$VALUE
      IF S$VALUE="2" THEN
         SCREEN FIELD;1;16
         IF LB.SP.CD='' THEN
            S$DATA(16)<S$SCR>=''
         END ELSE
            S$DATA(16)<S$SCR>=LB.SP.CD
         END
         SCREEN INPUT;1;16
         IF S$VALUE='END' OR S$VALUE='' OR S$VALUE = '.' THEN 
            S$DATA(16)<S$SCR> = ""
            SCREEN DISPLAY;1;16
            GOTO 140
         END
         READ SPOIL.CODE.DESC FROM SPOILAGE.CODES,CONO:S$VALUE ELSE
            ERRMSG="INVALID SPOILAGE CODE";GOSUB 90000
            S$DATA(16)<S$SCR> = ""
            SCREEN DISPLAY;1;16
            GOTO 140
         END
         LB.SP.CD=S$VALUE
      END ELSE
         LB.SP.CD = ""
      END
*
*---- ENTER TIME CODE
*
150*
      SCREEN FIELD;1;17
      IF LB.FCTR='' THEN
         S$DATA(17)<S$SCR>='1'
      END ELSE
         IF LB.FCTR="01" THEN LB.FCTR="1"
         IF LB.FCTR="02" THEN LB.FCTR="2"
         IF LB.FCTR="03" THEN LB.FCTR="3"
         S$DATA(17)<S$SCR>=LB.FCTR
      END
      SCREEN INPUT;1;17
      IF S$VALUE='.' THEN GOTO 140
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 150
      LB.FCTR=S$VALUE
*
*---- ENTER CONCURRENT
*
160*
      SCREEN FIELD;1;19
      IF LB.CNCR='' THEN
         S$DATA(19)<S$SCR>='2'
      END ELSE
         IF LB.CNCR="Y" THEN LB.CNCR="1"
         IF LB.CNCR="N" THEN LB.CNCR="2"
         S$DATA(19)<S$SCR>=LB.CNCR
      END
      SCREEN INPUT;1;19
      IF S$VALUE='.' THEN GOTO 150
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 160
      LB.CNCR=S$VALUE
*
*---- ENTER IMPRESSIONS
*
170*
      IF LB.OPER='' THEN
         LB.IMP=''
         GOTO 180
      END
      IF S.OPER.TYPE # "V" THEN
         LB.IMP=''
         GOTO 180
      END
      SCREEN FIELD;1;20
      IF LB.IMP # '' THEN
         S$O.R='O';S$DEFAULT=LB.IMP
      END ELSE
         S$O.R='R';S$DEFAULT=''
      END
      SCREEN INPUT;1;20
      IF S$VALUE='.' THEN GOTO 160
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 170
      LB.IMP=S$VALUE
*
*---- ENTER JOB NUMBER (PRINT CUST NAME)
*
180*
      SCREEN FIELD;1;25
      IF LB.JOB.NO # '' THEN
         S$O.R='O';S$DEFAULT=LB.JOB.NO
      END ELSE
         S$O.R='R';S$DEFAULT=''
      END
      SCREEN INPUT;1;25
      IF S$VALUE='.' THEN GOTO 160
      IF S$VALUE='END' THEN GOTO 189
      IF S$VALUE='' THEN GOTO 180
      LB.JOB.NO=S$VALUE
181*
      MATREAD JOB.REC FROM JOB,CONO:LB.JOB.NO ELSE
         ERRMSG="INVALID JOB NUMBER";GOSUB 90000;GOTO 180
      END
      IF JOB.CUST # '' THEN
         LB.CUST.NO=JOB.CUST
         MATREAD CUST.REC FROM CUSTOMER,CONO:LB.CUST.NO ELSE CUST.NAME="CUSTOMER UNKNOWN"
         LB.CUST.NAME=CUST.NAME
         S$DATA(26)<S$SCR>=LB.CUST.NAME
         SCREEN DISPLAY;1;26
      END ELSE
         LB.CUST.NAME=''
      END
189*
      RETURN
*
*---- ENTER STOP TIME
*
190*
      C.TIME=OCONV(TIME(),"MTS")
      S$DATA(44)<S$SCR> = C.TIME
      SCREEN DISPLAY;1;44
      SCREEN FIELD;1;31
      IF LB.HRS.END # '' THEN
         HTIME=LB.HRS.END
         GOSUB 520
      END ELSE
         STIME=TIME()
         GOSUB 510
      END
      S$O.R="O"
      S$DEFAULT=MTIME
      SCREEN INPUT;1;31
      IF S$VALUE='.' THEN
         GOSUB 180;GOTO 190
      END
      IF S$VALUE='' THEN GOTO 190
      IF S$VALUE='END' THEN GOTO 199
      IF MOD(S$VALUE,100) > 59 THEN
         ERRMSG="Invalid Response - Enter HHMM"
         GOSUB 90000
         GOTO 190
      END
      MTIME=S$VALUE
      GOSUB 530
      LB.CURRENT.TIME=INT(TIME() / 3600 * 100 + 0.5)
      IF HTIME > LB.CURRENT.TIME THEN
         ERRMSG="STOP TIME CANNOT BE GREATER THAN CURRENT TIME";GOSUB 90000;GOTO 190
      END
      IF HTIME < LB.HRS.ST THEN
         ERRMSG="STOP TIME MUST BE AFTER START TIME";GOSUB 90000;GOTO 190
      END
      LB.HRS.END=HTIME
      LB.STOP.TIME=LB.HRS.END * 36
199*
      RETURN
*
*---- UPDATE DAILY.TIME / DELETE ADC.TIME
*
300*
      LB.DIV=ADT.DIV<1,1>
      LB.DIV.DESC=ADT.DIV<1,2>
      LB.DEPT=ADT.DEPT<1,1>
      LB.DEPT.DESC=ADT.DEPT<1,2>
      LB.CCTR=ADT.CCTR<1,1>
      LB.CCTR.DESC=ADT.CCTR<1,2>
      LB.OPER=ADT.OPER<1,1>
      LB.OPER.DESC=ADT.OPER<1,2>
      LB.TYPE="1"
      LB.FCTR="1"
      LB.CNCR="2"
      LB.IMP=''
      LB.DATE=ADT.DATE.ST
      LB.JOB.NO=ADT.JOB.NO
      LB.START.TIME=ADT.TIME.ST
      LB.STOP.TIME=''
      LB.HRS.ST=ADT.HRS.ST
      LB.HRS.END=INT(TIME() / 3600 * 100 + 0.5)
      LB.SHIFT=ADT.SHIFT
      IF LB.HRS.END > LB.HRS.ST THEN
         LB.HRS.END='';GOTO 304
      END ELSE
         GOTO 308
      END
304*
      MATREAD DIV.REC FROM DIVISION,CONO:LB.DIV ELSE DIV.DESC="DIVISION UNKNOWN"
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:LB.DEPT ELSE DEPT.DESC="DEPARTMENT UNKNOWN"
      GOSUB 1025
      IF LB.JOB.NO # '' THEN GOSUB 181
305*
      GOSUB 120
      IF S$VALUE="END" OR S$VALUE='E' THEN GOTO 309
      GOSUB 190
      IF S$VALUE='END' OR S$VALUE='E' THEN GOTO 309
      SCREEN FIELD;1;41
      SCREEN INPUT;1;41
      IF S$VALUE=2 THEN GOTO 305
      IF S$VALUE='END' THEN GOTO 309
      GOSUB 4000
      READU SEQ.NO FROM CONTROL,CONO:"DAILY.TIME" ELSE
         SEQ.NO=10001
      END
      LB.SEQ=SEQ.NO
      LB.DLB.KEY=CONO:LB.EMP.NO:"*":LB.DATE
      MATREADU DLB.REC FROM DAILY.TIME,LB.DLB.KEY ELSE
         MAT DLB.REC=''
      END
      DLB.CNT=COUNT(DLB.SEQ,VM) + (DLB.SEQ # '') + 1
      DLB.DIV<1,DLB.CNT>=LB.DIV
      DLB.DEPT<1,DLB.CNT>=LB.DEPT
      DLB.SEQ<1,DLB.CNT>=LB.SEQ
      DLB.CCTR<1,DLB.CNT>=LB.CCTR
      DLB.OPER<1,DLB.CNT>=LB.OPER
      DLB.TYPE<1,DLB.CNT>=LB.TYPE
      DLB.FCTR<1,DLB.CNT>=LB.FCTR
      DLB.HRS.ST<1,DLB.CNT>=LB.HRS.ST
      DLB.TIME.ST<1,DLB.CNT>=LB.START.TIME
      DLB.IMP<1,DLB.CNT>=LB.IMP
      DLB.CNCR<1,DLB.CNT>=LB.CNCR
      DLB.JOB<1,DLB.CNT>=LB.JOB.NO
      DLB.HRS.EN<1,DLB.CNT>=LB.HRS.END
      DLB.TIME.END<1,DLB.CNT>=LB.STOP.TIME
      DLB.COST<1,DLB.CNT>=LB.COST
      DLB.MARKUP<1,DLB.CNT>=LB.MARKUP
      IF LB.SP.CD # '' THEN DLB.SPOIL.CODE<1,DLB.CNT>=LB.SP.CD
      MATWRITE DLB.REC ON DAILY.TIME,LB.DLB.KEY
      SEQ.NO=SEQ.NO + 1
      WRITE SEQ.NO ON CONTROL,CONO:"DAILY.TIME"
308*
      DELETE ADC.TIME,CONO:LB.EMP.NO
309*
      RETURN
*
*---- CONVERT SECONDS TO HHMM
*
510*
*     HTIME=INT(STIME * 100 / 3600 + 0.5)
      HTIME=INT(STIME * 100 / 3600)
      XHR=INT(HTIME / 100)
*     XMIN=INT(MOD(HTIME,100) / 100 * 60 + 0.5)
      XMIN=INT(MOD(HTIME,100) / 100 * 60)
      MTIME=XHR:STR("0",2-LEN(XMIN)):XMIN
      RETURN
*
*---- CONVERT HOURS TO HHMM
*
520*
      XHR=INT(HTIME / 100)
*     XMIN=INT(MOD(HTIME,100) / 100 * 60 + 0.5)
      XMIN=INT(MOD(HTIME,100) / 100 * 60)
      MTIME=XHR:STR("0",2-LEN(XMIN)):XMIN
      RETURN
*
*---- CONVERT HHMM TO HOURS
*
530*
      XHR=INT(MTIME / 100)
*     XMIN=INT(MOD(MTIME,100) * 100 / 60 + 0.5)
      XMIN=INT(MOD(MTIME,100) * 100 / 60)
      HTIME=XHR*100+XMIN
      RETURN
*
*---- SCREEN LAYOUT
*
1000*
      SCREEN FORMAT;1
      RETURN
*
*---- PRINT VALUES
*
1025*
      IF LB.DATE # '' THEN
         LB.LOGON.DATE=OCONV(LB.DATE,"D2/")
      END ELSE
         LB.LOGON.DATE=''
      END
      S$DATA(4)<S$SCR>=""
      S$DATA(5)<S$SCR>=LB.LOGON.DATE
      S$DATA(6)<S$SCR>=LB.SHIFT
      S$DATA(7)<S$SCR>=LB.DIV
      S$DATA(8)<S$SCR>=LB.DIV.DESC
      S$DATA(9)<S$SCR>=LB.DEPT
      S$DATA(10)<S$SCR>=LB.DEPT.DESC
      S$DATA(11)<S$SCR>=LB.CCTR
      S$DATA(12)<S$SCR>=LB.CCTR.DESC
      S$DATA(13)<S$SCR>=LB.OPER
      S$DATA(14)<S$SCR>=LB.OPER.DESC
      S$DATA(15)<S$SCR>=LB.TYPE
      S$DATA(17)<S$SCR>=LB.FCTR
      S$DATA(19)<S$SCR>=LB.CNCR
      S$DATA(20)<S$SCR>=LB.IMP
      S$DATA(25)<S$SCR>=LB.JOB.NO
      S$DATA(26)<S$SCR>=LB.CUST.NAME
      HTIME=LB.HRS.ST
      GOSUB 520
      S$DATA(30)<S$SCR>=MTIME
      HTIME=LB.HRS.END
      IF HTIME = "" THEN
         S$DATA(31)<S$SCR>=""
      END ELSE
         GOSUB 520
         S$DATA(31)<S$SCR>=MTIME
      END
      SCREEN DISPLAY;1;ALL
      RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
1500*
      SCREEN CLEAR;1
      RETURN
*
*---- CLEAR CENTER OF SCREEN
*
1550*
      RETURN
*
*---- LOAD MATRIX FOR DISPLAY
*
2000*
      MAT DT.REC = ""
      BEGIN CASE
         CASE FILE=1
            FOR E=1 TO DIV.CNT
               MATREAD DIV.REC FROM DIVISION,CONO:ALL.DIV<1,E> ELSE DIV.DESC="DIVISION UNKNOWN"
               DT.DESC<1,E>=DIV.DESC
               DT.NO<1,E>=ALL.DIV<1,E>
            NEXT E
         CASE FILE=2
            DT.NO=DIV.DEPT
            D.CNT=COUNT(DT.NO,VM) + (DT.NO # '')
            CNT = D.CNT
            FOR E=1 TO D.CNT
               MATREAD DEPT.REC FROM DEPARTMENT,CONO:DT.NO<1,E> ELSE DEPT.DESC="DEPARTMENT UNKNOWN"
               DT.DESC<1,E>=DEPT.DESC
            NEXT E
         CASE FILE=3
            DT.NO=DEPT.CCTRS
            D.CNT=COUNT(DT.NO,VM) + (DT.NO # "")
            CNT = D.CNT
            FOR E=1 TO D.CNT
               MATREAD CCTR.REC FROM COST.CNTR,CONO:DT.NO<1,E> ELSE CCTR.DESC="COST CENTER UNKNOWN"
               IF CCTR.TYPE='F' THEN CCTR.DESC='COST CENTER IS FROZEN'
               DT.DESC<1,E>=CCTR.DESC
            NEXT E
         CASE FILE=4
            DT.NO=CCTR.OPER
            D.CNT=COUNT(DT.NO,VM) + (DT.NO # '')
            CNT = D.CNT
            FOR E=1 TO D.CNT
               MATREAD OPER.REC FROM OPERATION,CONO:DT.NO<1,E> ELSE OPER.DESC="OPERATION UNKNOWN"
               DT.DESC<1,E>=OPER.DESC
            NEXT E
         CASE 1
            DT.NO = ""
            DT.DESC = ""
      END CASE
      S$DATA(35)<S$SCR> = DT.NO
      S$DATA(36)<S$SCR> = DT.DESC
      GOSUB 2500
      RETURN
*
*---- SCROLL ROUTINE
*
2500*
      START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = COUNT(S$DATA(35)<S$SCR>,VM) + (S$DATA(35)<S$SCR> # "")
      SCREEN MULTI;SFDC.TIME.ENTRY;S;35;36
      RETURN
*
*---- UNIT CONVERSION
*
3000*
      OK=0
      OK1=INDEX(S$VALUE,":",1)
      OK2=INDEX(S$VALUE,".",1)
      BEGIN CASE
         CASE OK1 > 0
            MM=FIELD(S$VALUE,":",2)
            IF MM='' THEN MM=0
            UN=INT((MM + 5) / 6) * .1
            HH=FIELD(S$VALUE,":",1)
            UNITS=HH + UN
            S$VALUE=ICONV(UNITS,"MD2")
         CASE OK2 > 0
            UNITS=S$VALUE
            S$VALUE=ICONV(UNITS,"MD2")
         CASE OK=0 AND NUM(S$VALUE)
            S$VALUE=S$VALUE
      END CASE
      RETURN
*
*---- CHANGE FCTR,TYPE,CNCR
*
4000*
      IF LB.TYPE="1" THEN LB.TYPE="R"
      IF LB.TYPE="2" THEN LB.TYPE="S"
      IF LB.TYPE="3" THEN LB.TYPE="C"
      IF LB.TYPE="4" THEN LB.TYPE="N"
      IF LB.CNCR="1" THEN LB.CNCR="Y"
      IF LB.CNCR="2" THEN LB.CNCR="N"
      IF LB.FCTR="1" THEN LB.FCTR="01"
      IF LB.FCTR="2" THEN LB.FCTR="02"
      IF LB.FCTR="3" THEN LB.FCTR="03"
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      SCREEN CLEAR;1;D
*       PRINT @(-1)
   END
