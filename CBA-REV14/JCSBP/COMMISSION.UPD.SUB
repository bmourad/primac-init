      SUBROUTINE COMMISSION.UPD.SUB(CONO,MAT SAVE.REC,PROG.TYPE)
*COPY>CPYLIB>COM1
*****************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - HERITAGE.BP 
* PROGRAM     - COMMISSION.UPD.SUB
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/09/85
* DESCRIPTION -
* This subroutine updates and maintains the COMMISSION records.
*ENDDOC
*****************************************************************
*
*---- CPYLIB
*
*COPY>JCS.CPYLIB>COMMISSION
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
DIM SAVE.REC(60)
*
*---- INITIALIZATION
*
     WRITE.FLAG = 0
     SLSMN = SAVE.REC(2)
     INV.NO = SAVE.REC(14)
     JOB.NO = SAVE.REC(52)
*
*---- MAIN PROCESSING
*
     MATREADU COMM.REC FROM COMMISSION,CONO:JOB.NO ELSE MAT COMM.REC = ""
     BEGIN CASE
        CASE PROG.TYPE = "POST.INV"
           GOSUB 10
           GOSUB 20
        CASE PROG.TYPE = "JSALES.MAINT"
           GOSUB 20
        CASE PROG.TYPE = "CASH.REC"
           GOSUB 30
        CASE 1
           RELEASE COMMISSION,CONO:JOB.NO
           GOTO 99999
     END CASE
     IF SLSMN # "" THEN
        MATREADU COMM.DRAW.REC FROM COMMISSION,CONO:"!":SLSMN ELSE MAT COMM.DRAW.REC = ""
        GOSUB 40
     END
     GOSUB 60
     GOTO 99999
*
*---- UPDATE RECORD FOR POSTED INVOICES
*
10*
     COMM.CUST = SAVE.REC(1)
     COMM.SLSM = SLSMN
     COMM.SALES.CODE = SAVE.REC(3)
     COMM.COMPL.DATE = SAVE.REC(9)
     LOCATE INV.NO IN COMM.INV.NO<1>,1 SETTING CPTR ELSE
        CPTR = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "") + 1
     END
     COMM.INV.NO<1,CPTR> = INV.NO
     COMM.INV.DATE<1,CPTR> = SAVE.REC(15)
     COMM.INV.PDATE<1,CPTR> = SAVE.REC(16)
     COMM.INV.MON<1,CPTR> = SAVE.REC(17)
     COMM.INV.AMT<1,CPTR> = SAVE.REC(18)
     COMM.INV.OS<1,CPTR> = SAVE.REC(19)
     COMM.INV.TAX<1,CPTR> = SAVE.REC(20)
     COMM.INV.SHP<1,CPTR> = SAVE.REC(21)
     COMM.INV.BAL<1,CPTR> = SAVE.REC(18)
     IF SAVE.REC(15) > COMM.INV.LAST.DATE THEN
        COMM.INV.LAST.DATE = SAVE.REC(15)
     END
     COMM.INV.LAST.PDATE = SAVE.REC(16)
     IF SAVE.REC(17) > COMM.INV.LAST.MON THEN
        COMM.INV.LAST.MON = SAVE.REC(17)
     END
     COMM.INV.TOT.AMT = 0
     COMM.INV.TOT.TAX = 0
     COMM.INV.TOT.SHP = 0
     COMM.INV.TOT.BAL = 0
     CNT = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "")
     FOR C = 1 TO CNT
        COMM.INV.TOT.AMT = COMM.INV.TOT.AMT + COMM.INV.AMT<1,C>
        COMM.INV.TOT.TAX = COMM.INV.TOT.TAX + COMM.INV.TAX<1,C>
        COMM.INV.TOT.SHP = COMM.INV.TOT.SHP + COMM.INV.SHP<1,C>
        COMM.INV.TOT.BAL = COMM.INV.TOT.BAL + COMM.INV.BAL<1,C>
     NEXT C
     IF COMM.MKT.COST # "" THEN GOSUB 50
     RETURN
*
*---- UPDATE RECORD FOR JOB SALES ENTRIES
*
20*
     COMM.ACT.COST = SAVE.REC(4)
     COMM.MKT.COST = SAVE.REC(5)
     COMM.ADJ.COST = SAVE.REC(6)
     COMM.OS.COMM = SAVE.REC(7)
     COMM.VAL.ADDED = SAVE.REC(8)
     IF COMM.MKT.COST # "" AND COMM.SALES.CODE # "" THEN GOSUB 50
     RETURN
*
*---- UPDATE RECORD FOR CASH RECEIPTS
*
30*
     LOCATE INV.NO IN COMM.INV.NO<1>,1 SETTING CPTR ELSE GOTO 39
*       CPTR = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "") + 1
*    END
     COMM.PMT.DATE<1,CPTR> = SAVE.REC(25)
     COMM.PMT.PDATE<1,CPTR> = SAVE.REC(26)
     COMM.INV.BAL<1,CPTR> = COMM.INV.BAL<1,CPTR> - (SAVE.REC(27) + SAVE.REC(28))
     COMM.PMT.AMT<1,CPTR> = SAVE.REC(27)
     COMM.PMT.ALLOW<1,CPTR> = SAVE.REC(28)
     COMM.PMT.TOT.AMT = 0
     COMM.PMT.TOT.ALLOW = 0
     COMM.INV.TOT.BAL = 0
     CNT = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "")
     FOR C = 1 TO CNT
        COMM.PMT.TOT.AMT = COMM.PMT.TOT.AMT + COMM.PMT.AMT<1,C>
        COMM.PMT.TOT.ALLOW = COMM.PMT.TOT.ALLOW + COMM.PMT.ALLOW<1,C>
        COMM.INV.TOT.BAL = COMM.INV.TOT.BAL + COMM.INV.BAL<1,C>
     NEXT C
     IF SAVE.REC(25) > COMM.LAST.PMT.DATE THEN
        COMM.LAST.PMT.DATE = SAVE.REC(25)
     END
     IF SAVE.REC(26) > COMM.LAST.PMT.PDATE THEN
        COMM.LAST.PMT.PDATE = SAVE.REC(26)
     END
39*
     RETURN
*
*---- UPDATE COMM.DRAW.REC
*
40*
     LOCATE JOB.NO IN COMM.JOBS<1>,1 SETTING JPTR ELSE
        JPTR = COUNT(COMM.JOBS,VM) + (COMM.JOBS # "") + 1
        COMM.JOBS<1,JPTR> = JOB.NO
        WRITE.FLAG = 1
     END
     RETURN
*
*---- CALCULATE COMM.CALC & COMM.DUE
*
50*
     IF COMM.SALES.CODE = "" THEN GOTO 59
     TYPE = COMM.SALES.CODE[1,1]
     NET = COMM.INV.TOT.AMT - (COMM.INV.TOT.TAX + COMM.INV.TOT.SHP)
     MKT = COMM.MKT.COST
     MARKUP = NET - MKT
     IF MKT = 0 THEN
        MARGIN = 0
     END ELSE
        MARGIN = INT((NET / MKT) * 1000 + .5) / 10
     END
     BEGIN CASE
        CASE TYPE = "S" OR TYPE = "O"
           BEGIN CASE
              CASE MARGIN < 105
                 COMM.CALC = 0
              CASE MARGIN < 115
                 COMM.CALC = INT(((MARKUP * 40) / 100) + .5)
              CASE MARGIN >= 115 AND MARGIN <= 125
                 COMM.CALC = INT(((MARKUP * 45) / 100) + .5)
              CASE MARGIN > 125
                 MKUP = INT((((125 * MKT) / 100) - MKT) + .5)
                 COMM.CALC = INT(((MKUP * 45) / 100) + .5)
                 MKUP = MARKUP - MKUP
                 COMM.CALC2 = INT(((MKUP * 50) / 100) + .5)
                 COMM.CALC = COMM.CALC + COMM.CALC2
           END CASE
        CASE TYPE = "W"
           BEGIN CASE
              CASE MARGIN < 103
                 COMM.CALC = 0
              CASE MARGIN < 108
                 COMM.CALC = INT(((MARKUP * 33) / 100) + .5)
              CASE MARGIN >= 108 AND MARGIN <= 115
                 COMM.CALC = INT(((MARKUP * 40) / 100) + .5)
              CASE MARGIN > 115
                 MKUP = INT((((115 * MKT) / 100) - MKT) + .5)
                 COMM.CALC = INT(((MKUP * 40) / 100) + .5)
                 MKUP = MARKUP - MKUP
                 COMM.CALC2 = INT(((MKUP * 50) / 100) + .5)
                 COMM.CALC = COMM.CALC + COMM.CALC2
           END CASE
        CASE 1
           COMM.CALC = 0
     END CASE
     COMM.DUE = COMM.CALC + COMM.ADJ
59*
     RETURN
*
*---- WRITE RECORD
*
60*
     MATWRITE COMM.REC ON COMMISSION,CONO:JOB.NO
     IF WRITE.FLAG THEN
        MATWRITE COMM.DRAW.REC ON COMMISSION,CONO:"!":SLSMN
     END ELSE
        RELEASE COMMISSION,CONO:"!":SLSMN
     END
     RETURN
*
*---- CALLS FOR SYSCOM
*
99999*
     RETURN
     END
