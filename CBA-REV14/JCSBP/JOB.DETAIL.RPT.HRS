*COPY>CPYLIB>COM1
****************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SYSTEM      - JCSBP
* PROGRAM     - JOB.DETAIL.RPT.HRS
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 06/28/84
* DESCRIPTION -
* This program produces a Job Detail Report by Hours. 
****************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- DIMENTION RECORDS
*
     DIM REG.TOTAL.REC(10)
     DIM R.REC(10)
     DIM CO.TOTAL.REC(10)
     DIM C.REC(10)
     DIM SUB.TOTAL.REC(10)
     DIM S.REC(10)
     DIM DEPT.TOTAL.REC(10)
     DIM D.REC(10)
     DIM GRAND.TOTAL.REC(10)
     DIM G.REC(10)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG='EMPLOYEE FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     RPT.NO="XXXX"
     UNKNOWN=STR("?",30)
     MAT REG.TOTAL.REC=''
     MAT CO.TOTAL.REC=''
     MAT SUB.TOTAL.REC=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     MAT R.REC=''
     MAT C.REC=''
     MAT S.REC=''
     MAT D.REC=''
     MAT G.REC=''
     PREVIOUS.DEPT.NO=''
     PREVIOUS.DEPT=''
     PREVIOUS.CCTR=''
     PREVIOUS.OPER=''
     FIRST.FLAG=1
     NODATA=1
     MAT JCR.REC=''
     MAT JOB.REC=''
     EMP.NAME=''
     DATE.LAST.WORKED=''
     DATE.ITEMS.THROUGH=''
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     TYPE=FIELD(JCR.KEY,"*",4)
     IF TYPE="EST" THEN GOTO 10
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'LAB' THEN GOTO 10
     IF JCR.LINE.TYPE="P" OR JCR.LINE.TYPE="M" THEN GOTO 10
     NODATA=0
     DEPT.NO=JCR.DEPT
     JOB.NO=JCR.JOB
     IF FIRST.FLAG=1 THEN GOSUB 15
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     I=0
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        OPER.NO=JCR.OPER<1,I>
        EMP.NUM=JCR.EMP<1,I>
        TRANS.DATE=JCR.DATE<1,I>
        TRANS.TYPE=JCR.TYPE<1,I>
        REG.HRS=JCR.HRS<1,I,1>
        OT.HRS=JCR.HRS<1,I,2>
        DT.HRS=JCR.HRS<1,I,3>
        TTL.HRS=REG.HRS + OT.HRS + DT.HRS
        IF TTL.HRS = 0 THEN TTL.HRS=''
        QTY=JCR.QTY<1,I>
        SPOIL.HRS=JCR.SPOIL.HRS<1,I>
        REASON=JCR.SPOIL.CODE<1,I>
        TRAN.JOB=JCR.RC.JOB<1,I>
        IF FIRST.FLAG=1 THEN GOTO 11
        IF PREVIOUS.DEPT # DEPT.NO THEN
           GOSUB 50
           GOSUB 60
           IF LINE.CNT >= 50 THEN GOSUB 20
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.CCTR # CCTR.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.OPER # OPER.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.OPER=OPER.NO
        END
11      EMP.NAME="";EFC.PER="";SAVE.CCTR.OPER.TYPE=""
        GOSUB 30
        IF FIRST.FLAG=1 THEN
           FIRST.FLAG=0
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
        END
     NEXT I
     GOTO 10
12*
     GOSUB 50
     GOSUB 60
     GOSUB 70
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFORMATION FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     Q=0
     DATE.CNT=COUNT(JOB.LB.DATE,VM) + (JOB.LB.DATE # '')
     FOR Q=1 TO DATE.CNT
        IF JOB.LB.DATE<1,Q,1> > DATE.LAST.WORKED THEN DATE.LAST.WORKED=JOB.LB.DATE<1,Q,1>
     NEXT Q
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     Q=0;DATE.CNT=0
     DATE.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR Q=1 TO DATE.CNT
        IF DATE.TBL<1,Q,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,Q,1>
     NEXT Q
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     PG.NO=PG.NO + 1
     H.LINE='';HLINE1='';HLINE2='';HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     K=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=K:CO.NAME:K
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):'PAGE: ':PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     PRINT JOB.CUST"L#8":SPACE(39):'D E T A I L   R E P O R T (LABOR HOURS)':SPACE(24):'#':JOB.NO "L#8":" ":CONSOL
     PRINT CUST.NAME"L#30":SPACE(72):CUST.NAME
     PRINT JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
     PRINT JOB.DESC<1,1>"L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.WORKED,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>"L#30"
     PRINT 'SR: ':SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT
     PRINT SPACE(60):'-------- ACTUAL HOURS -------'
     PRINT 'DEPT  CCTR  OPER      EMPLOYEE NAME/NUMBER       DATE         REG     O/T    D/T   TOTAL   QUANTITY  EFC %  SPOIL   REASON  XFER JOB'
     PRINT '----  ----  ---- ----------------------------- --------     ------- ------ ------ ------- ---------- ----- ------- -------- --------'
     LINE.CNT=10
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF DEPT.NO # PREVIOUS.DEPT.NO THEN
        MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
        P.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT P.LINE
        PRINT STR('-',LEN(P.LINE))
        P.LINE=SPACE(17):EMP.NAME.NUM"L#29":' ':DATE"R#8":'  ':TRANS.TYPE:' ':REG.HRS"R#7":' ':OT.HRS"R#6":' ':DT.HRS"R#6":' ':TTL.HRS"R#7":' ':IQTY"R#10":' ':EFC.PER"R#5":' ':SPOIL.HRS"R#7":' ':REASON"L#8":' ':TRAN.JOB"L#8"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 3
     END ELSE
        P.LINE=SPACE(17):EMP.NAME.NUM"L#29":' ':DATE"R#8":'  ':TRANS.TYPE:' ':REG.HRS"R#7":' ':OT.HRS"R#6":' ':DT.HRS"R#6":' ':TTL.HRS"R#7":' ':IQTY"R#10":' ':EFC.PER"R#5":' ':SPOIL.HRS"R#7":' ':REASON"L#8":' ':TRAN.JOB"L#8"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 1
     END
     PREVIOUS.DEPT.NO=DEPT.NO
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     MATREAD CCTR.REC FROM COST.CNTR,CONO:CCTR.NO ELSE
        STD.HRS="";STD.IMP="";EFC.PER=0;GOTO 45
     END
     LOCATE OPER.NO IN CCTR.OPER<1>,1 SETTING AA ELSE
        STD.HRS="";STD.IMP="";EFC.PER=0;GOTO 45
     END
     IF CCTR.OPER.TYPE<1,AA>='V' OR CCTR.OPER.TYPE<1,AA>='U' THEN
        PRT.IMP=QTY
        IF TTL.HRS=0 OR TTL.HRS="" THEN
           IMP.HRS=0
        END ELSE
           IMP.HRS=INT((PRT.IMP / TTL.HRS) + .5)
        END
        STD.IMP=CCTR.OPER.STD.IMP<1,AA>
        STD.HRS=""
        IF STD.IMP=0 OR STD.IMP="" THEN
           EFC.PER=0
        END ELSE
           IF IMP.HRS = "0" OR IMP.HRS = "" THEN
              EFC.PER=0
           END ELSE
              EFC.PER=INT(((IMP.HRS / STD.IMP) * 100) + .5)
           END 
        END
     END ELSE
        STD.HRS=CCTR.OPER.STD.HRS<1,AA>
        STD.IMP=""
        IF STD.HRS=0 OR STD.HRS="" THEN
           EFC.PER=0
        END ELSE
           IF TTL.HRS = "0" OR TTL.HRS = "" THEN
              EFC.PER=0
           END ELSE
              EFC.PER=INT(((STD.HRS / TTL.HRS) * 100) + .5)
           END 
        END
     END
     SAVE.CCTR.OPER.TYPE=CCTR.OPER.TYPE<1,AA>
45*
     MATREAD EMP.REC FROM EMPLOYEE,CONO:EMP.NUM ELSE EMP.NAME=STR("?",28)
     IF EMP.NAME='' THEN EMP.NAME=EMP.FRST.NAME:' ':EMP.LAST.NAME
     EMP.NAME.NUM=EMP.NAME:'  ':EMP.NUM
     DATE=OCONV(TRANS.DATE,"D2-")
     IF TRANS.TYPE='R' THEN
        TRANS.TYPE='  '
     END ELSE
        TRANS.TYPE='CO'
     END
     IF TRANS.TYPE='CO' THEN
        CO.TOTAL.REC(1)=CO.TOTAL.REC(1) + REG.HRS
        CO.TOTAL.REC(2)=CO.TOTAL.REC(2) + OT.HRS
        CO.TOTAL.REC(3)=CO.TOTAL.REC(3) + DT.HRS
        CO.TOTAL.REC(4)=CO.TOTAL.REC(4) + TTL.HRS
        CO.TOTAL.REC(5)=CO.TOTAL.REC(5) + QTY
        CO.TOTAL.REC(6)=CO.TOTAL.REC(6) + SPOIL.HRS
        CO.TOTAL.REC(7)=CO.TOTAL.REC(7) + STD.HRS
*        CO.TOTAL.REC(8)=CO.TOTAL.REC(8) + STD.IMP
        CO.TOTAL.REC(8)=STD.IMP
        CO.TOTAL.REC(9)<1,-1>=EFC.PER
     END ELSE
        REG.TOTAL.REC(1)=REG.TOTAL.REC(1) + REG.HRS
        REG.TOTAL.REC(2)=REG.TOTAL.REC(2) + OT.HRS
        REG.TOTAL.REC(3)=REG.TOTAL.REC(3) + DT.HRS
        REG.TOTAL.REC(4)=REG.TOTAL.REC(4) + TTL.HRS
        REG.TOTAL.REC(5)=REG.TOTAL.REC(5) + QTY
        REG.TOTAL.REC(6)=REG.TOTAL.REC(6) + SPOIL.HRS
        REG.TOTAL.REC(7)=REG.TOTAL.REC(7) + STD.HRS
*        REG.TOTAL.REC(8)=REG.TOTAL.REC(8) + STD.IMP
        REG.TOTAL.REC(8)=STD.IMP
        REG.TOTAL.REC(9)<1,-1>=EFC.PER
     END
     SUB.TOTAL.REC(1)=SUB.TOTAL.REC(1) + REG.HRS
     SUB.TOTAL.REC(2)=SUB.TOTAL.REC(2) + OT.HRS
     SUB.TOTAL.REC(3)=SUB.TOTAL.REC(3) + DT.HRS
     SUB.TOTAL.REC(4)=SUB.TOTAL.REC(4) + TTL.HRS
     SUB.TOTAL.REC(5)=SUB.TOTAL.REC(5) + QTY
     SUB.TOTAL.REC(6)=SUB.TOTAL.REC(6) + SPOIL.HRS
     SUB.TOTAL.REC(7)=SUB.TOTAL.REC(7) + STD.HRS
*     SUB.TOTAL.REC(8)=SUB.TOTAL.REC(8) + STD.IMP
     SUB.TOTAL.REC(8)=STD.IMP
     DEPT.TOTAL.REC(1)=DEPT.TOTAL.REC(1) + REG.HRS
     DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + OT.HRS
     DEPT.TOTAL.REC(3)=DEPT.TOTAL.REC(3) + DT.HRS
     DEPT.TOTAL.REC(4)=DEPT.TOTAL.REC(4) + TTL.HRS
     DEPT.TOTAL.REC(5)=DEPT.TOTAL.REC(5) + QTY
     DEPT.TOTAL.REC(6)=DEPT.TOTAL.REC(6) + SPOIL.HRS
     GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + REG.HRS
     GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + OT.HRS
     GRAND.TOTAL.REC(3)=GRAND.TOTAL.REC(3) + DT.HRS
     GRAND.TOTAL.REC(4)=GRAND.TOTAL.REC(4) + TTL.HRS
     GRAND.TOTAL.REC(5)=GRAND.TOTAL.REC(5) + QTY
     GRAND.TOTAL.REC(6)=GRAND.TOTAL.REC(6) + SPOIL.HRS
     IF REG.HRS # '' THEN REG.HRS=OCONV(REG.HRS,"MD2")
     IF OT.HRS # '' THEN OT.HRS=OCONV(OT.HRS,"MD2")
     IF DT.HRS # '' THEN DT.HRS=OCONV(DT.HRS,"MD2")
     IF TTL.HRS # '' THEN TTL.HRS=OCONV(TTL.HRS,"MD2")
     IF SPOIL.HRS # '' THEN SPOIL.HRS=OCONV(SPOIL.HRS,"MD2")
     IF EFC.PER=0 THEN EFC.PER=""
     IQTY = OCONV(QTY,"MD2")
     RETURN
*
*---- PRINT SUB TOTALS
*
50*
     GOSUB 55
     S.LINE=SPACE(60):'------- ------ ------ ------- ---------- ----- -------'
     PRINT S.LINE
     LINE.CNT=LINE.CNT + 1
     IF R.REC(1)+R.REC(2)+R.REC(3)+R.REC(4)+R.REC(5)+R.REC(7)+R.REC(6) = 0 THEN REG.FLG = 0 ELSE REG.FLG = 1
     IF C.REC(1)+C.REC(2)+C.REC(3)+C.REC(4)+C.REC(5)+C.REC(7)+C.REC(6) = 0 THEN CO.FLG = 0 ELSE CO.FLG = 1
     IF REG.FLG THEN
        S.LINE=SPACE(40):'*   REG HOURS TOTAL ':R.REC(1)"R#7":' ':R.REC(2)"R#6":' ':R.REC(3)"R#6":' ':R.REC(4)"R#7":' ':R.REC(5)"R#10":' ':R.REC(7)"R#5":' ':R.REC(6)"R#7"
        PRINT S.LINE
        LINE.CNT=LINE.CNT + 1
     END
     IF CO.FLG THEN
        S.LINE=SPACE(40):'*    CO HOURS TOTAL ':C.REC(1)"R#7":' ':C.REC(2)"R#6":' ':C.REC(3)"R#6":' ':C.REC(4)"R#7":' ':C.REC(5)"R#10":' ':C.REC(7)"R#5":' ':C.REC(6)"R#7" 
        PRINT S.LINE
        LINE.CNT=LINE.CNT + 1
     END
     S.LINE=SPACE(60):'------- ------ ------ ------- ---------- ----- -------'
     PRINT S.LINE
     S.LINE=SPACE(4):'* ':PREVIOUS.CCTR"L#4":'  ':PREVIOUS.OPER"L#4":'  ':OPER.DESC"L#30":SPACE(12):S.REC(1)"R#7":' ':S.REC(2)"R#6":' ':S.REC(3)"R#6":' ':S.REC(4)"R#7":' ':S.REC(5)"R#10":' ':S.REC(7)"R#5":' ':S.REC(6)"R#7"
     PRINT S.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT REG.TOTAL.REC=''
     MAT CO.TOTAL.REC=''
     MAT SUB.TOTAL.REC=''
     MAT R.REC=''
     MAT C.REC=''
     MAT S.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- OCONV HOURS FOR SUB TOTALS
*
55*
     MATREAD OPER.REC FROM OPERATION,CONO:PREVIOUS.OPER ELSE OPER.DESC=UNKNOWN
     IF SAVE.CCTR.OPER.TYPE="" THEN
        R.REC(7)="";C.REC(7)="";S.REC(7)="";GOTO 57
     END
     IF SAVE.CCTR.OPER.TYPE='V' OR SAVE.CCTR.OPER.TYPE='U' THEN
        R.TTL.HRS=REG.TOTAL.REC(4)
        R.PRT.IMP=REG.TOTAL.REC(5)
        R.STD.IMP=REG.TOTAL.REC(8)
        C.TTL.HRS=CO.TOTAL.REC(4)
        C.PRT.IMP=CO.TOTAL.REC(5)
        C.STD.IMP=CO.TOTAL.REC(8)
        S.TTL.HRS=SUB.TOTAL.REC(4)
        S.PRT.IMP=SUB.TOTAL.REC(5)
        S.STD.IMP=SUB.TOTAL.REC(8)
        IF R.TTL.HRS=0 OR R.TTL.HRS="" THEN
           R.IMP.HRS=0
        END ELSE
           R.IMP.HRS=INT((R.PRT.IMP / R.TTL.HRS) + .5)
        END
        IF R.STD.IMP=0 OR R.STD.IMP="" THEN
           R.EFC.PER=0
        END ELSE
           IF R.IMP.HRS # 0 THEN
              R.EFC.PER=INT(((R.IMP.HRS / R.STD.IMP) * 100) + .5)
           END ELSE
              R.EFC.PER=0
           END
        END
        IF C.TTL.HRS=0 OR C.TTL.HRS="" THEN
           C.IMP.HRS=0
        END ELSE
           C.IMP.HRS=INT((C.PRT.IMP / C.TTL.HRS) + .5)
        END
        IF C.STD.IMP=0 OR C.STD.IMP="" THEN
           C.EFC.PER=0
        END ELSE
           IF C.IMP.HRS # 0 THEN
              C.EFC.PER=INT(((C.IMP.HRS / C.STD.IMP) * 100) + .5)
           END ELSE
              C.EFC.PER=0
           END
        END
        IF S.TTL.HRS=0 OR S.TTL.HRS="" THEN
           S.IMP.HRS=0
        END ELSE
           S.IMP.HRS=INT((S.PRT.IMP / S.TTL.HRS) + .5)
        END
        IF S.STD.IMP=0 OR S.STD.IMP="" THEN
           S.EFC.PER=0
        END ELSE
           IF S.IMP.HRS # 0 THEN
              S.EFC.PER=INT(((S.IMP.HRS / S.STD.IMP) * 100) + .5)
           END ELSE
              S.EFC.PER=0
           END
        END
     END ELSE
        R.TTL.HRS=REG.TOTAL.REC(4)
        R.STD.HRS=REG.TOTAL.REC(7)
        C.TTL.HRS=CO.TOTAL.REC(4)
        C.STD.HRS=CO.TOTAL.REC(7)
        S.TTL.HRS=SUB.TOTAL.REC(4)
        S.STD.HRS=SUB.TOTAL.REC(7)
        IF R.STD.HRS=0 OR R.STD.HRS="" THEN
           R.EFC.PER=0
        END ELSE
           IF R.TTL.HRS # 0 THEN
              R.EFC.PER=INT(((R.STD.HRS / R.TTL.HRS) * 100) + .5)
           END ELSE
              R.EFC.PER=0
           END
        END
        IF C.STD.HRS=0 OR C.STD.HRS="" THEN
           C.EFC.PER=0
        END ELSE
           IF C.TTL.HRS # 0 THEN
              C.EFC.PER=INT(((C.STD.HRS / C.TTL.HRS) * 100) + .5)
           END ELSE
              C.EFC.PER=0
           END
        END
        IF S.STD.HRS=0 OR S.STD.HRS="" THEN
           S.EFC.PER=0
        END ELSE
           IF S.TTL.HRS # 0 THEN
              S.EFC.PER=INT(((S.STD.HRS / S.TTL.HRS) * 100) + .5)
           END ELSE
              S.EFC.PER=0
           END
        END
     END
     IF R.EFC.PER # 0 THEN R.REC(7)=R.EFC.PER
     IF C.EFC.PER # 0 THEN C.REC(7)=C.EFC.PER
     IF S.EFC.PER # 0 THEN S.REC(7)=S.EFC.PER
57*
     FOR G=1 TO 6
        IF REG.TOTAL.REC(G) # '' THEN R.REC(G)=OCONV(REG.TOTAL.REC(G),"MD2")
        IF CO.TOTAL.REC(G) # '' THEN C.REC(G)=OCONV(CO.TOTAL.REC(G),"MD2")
        IF SUB.TOTAL.REC(G) # '' THEN S.REC(G)=OCONV(SUB.TOTAL.REC(G),"MD2")
     NEXT G
     SAVE.CCTR.OPER.TYPE=""
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     FOR B=1 TO 6
        IF DEPT.TOTAL.REC(B) # '' THEN D.REC(B)=OCONV(DEPT.TOTAL.REC(B),"MD2")
     NEXT B
     D.LINE=SPACE(60):'------- ------ ------ ------- ----------       -------'
     PRINT D.LINE
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
     D.LINE=D.LINE:SPACE(20):D.REC(1)"R#7":' ':D.REC(2)"R#6":' ':D.REC(3)"R#6":' ':D.REC(4)"R#7":' ':D.REC(5)"R#10":'       ':D.REC(6)"R#7"
     PRINT D.LINE
     PRINT
     PRINT
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     MAT D.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT GRAND TOTALS
*
70*
     FOR V=1 TO 6
        IF GRAND.TOTAL.REC(V) # '' THEN G.REC(V)=OCONV(GRAND.TOTAL.REC(V),"MD2")
     NEXT V
     G.LINE=SPACE(60):'------- ------ ------ ------- ----------       -------'
     PRINT G.LINE
     G.LINE="*** GRAND TOTALS ***":SPACE(40):G.REC(1)"R#7":' ':G.REC(2)"R#6":' ':G.REC(3)"R#6":' ':G.REC(4)"R#7":' ':G.REC(5)"R#10":'       ':G.REC(6)"R#7"
     PRINT G.LINE
     RETURN
*
*---- CALLS FOR SYSCOM
*
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
