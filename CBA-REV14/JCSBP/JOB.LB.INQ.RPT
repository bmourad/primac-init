   SUBROUTINE JOB.LB.INQ.RPT(CONO,CO.NAME,JOB.NUM,SUB.CNT,HD1,HD2)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>JCS.CPYLIB>COM.JOB.INQ
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.TIME.INQ.RPT
* DATE        - 11/21/83
* DESCRIPTION -
*T26334 epitka 12/19/2001 * REV12
*T26493 cmykleb 04/03/2002 * Change pgm to use headings from calling pgm.
*ENDDOC
*********************************************************************
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JCS.CPYLIB>JOB.TOT.REC
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
   DASHES = STR('-',80)
   PNO = 0
   SP1 = SPACE(1)
   LINE.COUNT = 99
   TOTAL.TIME = ''
*T26493 v
   HD3 = "JOB # : ":JOB.NUM"L#8":SPACE(50)
   HD3 = HD3 : "TIME POSTED REPORT"
*T26493 ^
**** PRINT DETAIL LINES AND TOTALS
100 DEPT.CNT = COUNT(LB.DEPT.NO,VM) + (LB.DEPT.NO # '')
   FOR D = 1 TO DEPT.CNT
      DEPT.TOTALS = ''
      PTR.CNT = COUNT(LB.DEPT.PTR<1,D>,SVM)+(LB.DEPT.PTR<1,D> # '')
      DPNO = LB.DEPT.NO<1,D>
      FOR PT = 1 TO PTR.CNT
         CCTR.TOTALS = ''
         PTR.NO = LB.DEPT.PTR<1,D,PT>; CCNO = JOB.LB.CCTR<1,PTR.NO>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCNO ELSE
            MAT CCTR.REC = '????????????'
         END
         LB.ID = CONO:JOB.NUM:"!":DPNO:"!":CCNO:"!"
         LINES = JOB.LB.SEQ<1,PTR.NO,1>
         FOR SE = 1 TO LINES
            MATREAD JLB.REC FROM JOB.TIME , LB.ID:SE ELSE GOTO 111
            GOSUB 1000
111      NEXT SE
         FOR K = 2 TO SUB.CNT
            OLN = LINES + 1; LINES = LINES + JOB.LB.SEQ<1,PTR.NO,K>
            LB.ID = CONO:JOB.SUBS<1,K-1>:"!":DPNO:"!":CCNO:"!"
            FOR SE = OLN TO LINES
               MATREAD JLB.REC FROM JOB.TIME , LB.ID:(SE-OLN+1) ELSE GOTO 112
               GOSUB 1000
112         NEXT SE
         NEXT K
         GOSUB 2000
      NEXT PT
      GOSUB 3000
   NEXT D
   GOTO 99999
**** PRINT DETAIL LINE
1000 IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   LINE = ''
   LINE = LINE : JLB.OPER "L#4":SP1
   LINE = LINE : JLB.FCTR "L#2": SP1 : JLB.SEQ "L#8" :SP1
   LINE = LINE : OCONV(JLB.DATE , "D2/") "L#8": SP1
   LINE = LINE : JLB.MON<1,1>[3,4] "R#4" :SP1
   LINE = LINE : JLB.EMP "L#4" : SP1
   BEGIN CASE
      CASE JLB.TYPE = 'R'
         LINE = LINE :OCONV(JLB.HRS, "MD2") "R#7":SPACE(24)
         CCTR.TOTALS<1,1> = CCTR.TOTALS<1,1> + JLB.HRS
      CASE JLB.TYPE = 'N'
         LINE = LINE : SPACE(8):OCONV(JLB.HRS, "MD2") "R#7":SPACE(16)
         CCTR.TOTALS<1,2> = CCTR.TOTALS<1,2> + JLB.HRS
      CASE JLB.TYPE = 'C'
         LINE = LINE : SPACE(16):OCONV(JLB.HRS, "MD2") "R#7":SPACE(8)
         CCTR.TOTALS<1,3> = CCTR.TOTALS<1,3> + JLB.HRS
      CASE JLB.TYPE = 'S'
         LINE = LINE : SPACE(24):OCONV(JLB.HRS, "MD2") "R#6":SP1
         CCTR.TOTALS<1,4> = CCTR.TOTALS<1,4> + JLB.HRS
      CASE 1
         LINE = LINE :OCONV(JLB.HRS, "MD2") "R#7":SPACE(24)
         CCTR.TOTALS<1,1> = CCTR.TOTALS<1,1> + JLB.HRS
   END CASE
   LINE = LINE : OCONV(JLB.CNCR, "MD2Z") "R#7":SP1
   CCTR.TOTALS<1,5> = CCTR.TOTALS<1,5> + JLB.CNCR
   LINE = LINE :OCONV(JLB.HRS, "MD2") "R#7" :SP1
   LINE = LINE : OCONV(JLB.IMP, "MD2") "R#11" :SP1
   LINE = LINE :OCONV(JLB.COST , "MD2") "R#9":SP1
   LINE = LINE : OCONV(JLB.SALE, "MD2") "R#9":SP1
   LINE = LINE : JLB.RC.JOB "L#8":SP1:JLB.RC.TRAN "L#8"
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** PRINT TOTALS FOR CCTR
2000 IF LINE.COUNT + 3 > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   GOSUB 9000
   LINE = ''
   LINE = LINE : "** CCTR - ":CCNO "L#3":" ":CCTR.DESC "L#18":" **"
   LINE = LINE : SP1:OCONV(CCTR.TOTALS<1,1>, "MD2") "R#7"
   LINE = LINE : SP1:OCONV(CCTR.TOTALS<1,2>, "MD2") "R#7"
   LINE = LINE : SP1:OCONV(CCTR.TOTALS<1,3>, "MD2") "R#7"
   LINE = LINE : SP1:OCONV(CCTR.TOTALS<1,4>, "MD2") "R#6"
   LINE = LINE : SP1:OCONV(CCTR.TOTALS<1,5>, "MD2") "R#7"
   LINE = LINE : SP1:OCONV(JOB.LB.HRS<1,PTR.NO>, "MD2") "R#7"
   LINE = LINE : SP1: OCONV(JOB.LB.IMP<1,PTR.NO>, "MD2") "R#11"
   DEPT.TOTALS<1,1> = DEPT.TOTALS<1,1> + CCTR.TOTALS<1,1>
   DEPT.TOTALS<1,2> = DEPT.TOTALS<1,2> + CCTR.TOTALS<1,2>
   DEPT.TOTALS<1,3> = DEPT.TOTALS<1,3> + CCTR.TOTALS<1,3>
   DEPT.TOTALS<1,4> = DEPT.TOTALS<1,4> + CCTR.TOTALS<1,4>
   DEPT.TOTALS<1,5> = DEPT.TOTALS<1,5> + CCTR.TOTALS<1,5>
   DEPT.TOTALS<1,6> = DEPT.TOTALS<1,6> + JOB.LB.HRS<1,PTR.NO>
   DEPT.TOTALS<1,7> = DEPT.TOTALS<1,7> + JOB.LB.IMP<1,PTR.NO>
   LINE = LINE : SP1:OCONV(JOB.LB.COST<1,PTR.NO>, "MD2") "R#9"
   LINE = LINE : SP1:OCONV(JOB.LB.SALE<1,PTR.NO>, "MD2") "R#9"
   PRINT LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 2
   RETURN
**** PRINT TOTALS FOR DEPT
3000 IF LINE.COUNT + 3 > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   GOSUB 9100
   LINE = ''
   LINE = LINE : "** DEPT - ":DPNO "L#5":" ":LB.DEPT.DESC<1,D> "L#16":" **"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,1>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,2>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,3>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,4>, "MD2") "R#6"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,5>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,6>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(DEPT.TOTALS<1,7>, "MD2") "R#11"
   LINE = LINE :SP1:OCONV(LB.DEPT.COST<1,D>, "MD2") "R#9"
   LINE = LINE :SP1:OCONV(LB.DEPT.SALE<1,D>, "MD2") "R#9"
   TOTAL.TIME<1,1> = TOTAL.TIME<1,1> + DEPT.TOTALS<1,1>
   TOTAL.TIME<1,2> = TOTAL.TIME<1,2> + DEPT.TOTALS<1,2>
   TOTAL.TIME<1,3> = TOTAL.TIME<1,3> + DEPT.TOTALS<1,3>
   TOTAL.TIME<1,4> = TOTAL.TIME<1,4> + DEPT.TOTALS<1,4>
   TOTAL.TIME<1,5> = TOTAL.TIME<1,5> + DEPT.TOTALS<1,5>
   TOTAL.TIME<1,6> = TOTAL.TIME<1,6> + DEPT.TOTALS<1,6>
   TOTAL.TIME<1,7> = TOTAL.TIME<1,7> + DEPT.TOTALS<1,7>
   PRINT LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 2
   RETURN
**** UNDER LINE FOR TOTALS
9000 IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   LINE = ''
   LINE = SPACE(36):'------- ------- ------- ------ ------- ------- ----------- --------- ---------'
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** UNDER LINE FOR TOTALS
9100 IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   LINE = ''
   LINE = SPACE(36):'======= ======= ======= ====== ======= ======= =========== ========= ========='
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** PRINT HEADING
9999 PRINT CHAR(12)
   PNO = PNO + 1
*T26493 v
*  LINE = SPACE(2) : 'DATE - ' : OCONV(DATE(),"D2/") "L#8"
*  LINE = LINE : SPACE(5) : 'TIME - ' : OCONV(TIME(),"MTS") "L#8"
*  LINE = LINE : SPACE(22) : CO.NAME "L#30"
*  LINE = LINE : SPACE(15) : 'PAGE - ' : PNO "R#3"
*  PRINT LINE
*  PRINT SPACE(52) :'T I M E   P O S T E D   R E P O R T'
*  PRINT SPACE(52) :'       FOR JOB : ':JOB.NUM "L#8"
*  PRINT SPACE(52) :DASHES[1,35]
   PRINT HD1:PNO"R#3"
   PRINT HD2
   PRINT HD3
*T26493 ^
   PRINT
   LINE = 'OPER FC TRANS  # HRS DATE PER  EMP# REGULAR NO CHRG CST ORD SPOIL  CONCRNT TOT HRS  NUM IMPRS  TOT COST  TOT SALE  RCLS JOB RCL TRAN'
   PRINT LINE
   LINE = '---- -- -------- -------- ---- ---- ------- ------- ------- ------ ------- ------- ----------- --------- --------- -------- --------'
   PRINT LINE
   PRINT
   LINE.COUNT = LINE.COUNT + 7
   RETURN
**** PRINT TOTAL FOR ALL DEPARTMENT AND END OF JOB
99999 IF LINE.COUNT + 3 > 45 THEN
      LINE.COUNT = 0 ; GOSUB 9999
   END
   GOSUB 9100
   LINE = ''
   LINE = LINE : "** TOTAL FOR ALL TIME DEPARTMENTS**"
   LINE = LINE :SP1:OCONV(TOTAL.TIME<1,1>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(TOTAL.TIME<1,2>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(TOTAL.TIME<1,3>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(TOTAL.TIME<1,4>, "MD2") "R#6"
   LINE = LINE :SP1:OCONV(TOTAL.TIME<1,5>, "MD2") "R#7"
   LINE = LINE :SP1:OCONV(TOTAL.TIME<1,6>, "MD2") "R#7"
   LINE = LINE :SP1: OCONV(TOTAL.TIME<1,7>, "MD2") "R#11"
   LINE = LINE :SP1:OCONV(LB.TOT.COST, "MD2") "R#9"
   LINE = LINE :SP1:OCONV(LB.TOT.SALE, "MD2") "R#9"
   PRINT LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
END
