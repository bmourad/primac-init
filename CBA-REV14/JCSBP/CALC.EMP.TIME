      SUBROUTINE CALC.EMP.TIME (TTYPE, STIME, ETIME, ATIME, ITIME, TTIME, OVERLAP)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - CALC.EMP.TIME
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/31/87
*
* DESCRIPTION
*
* This subroutine calculates active, idle and total employee time.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
*
*---- MAKE ALL TIME ENTRIES RELATIVE TO START TIME
*
      OVERLAP = 0
      CNT = COUNT(TTYPE,VM) + (TTYPE # "")
      ET = STIME<1,1>+0
      FOR TP = 1 TO CNT
         IF STIME<1,TP> = "" OR TTYPE<1,TP> = "C" THEN GOTO 40
         ST = STIME<1,TP>+0
         PET = ET
         IF ST < ET AND (PET-ST) > 1600 THEN
            ST = ST + 2400
            STIME<1,TP> = ST
         END
         IF ST < PET THEN OVERLAP = 1
         ET = ETIME<1,TP>+0
         IF ET < ST THEN
            ET = ET + 2400
            ETIME<1,TP> = ET
         END
40    NEXT TP
*
*---- SERIALIZE TIME ENTRIES
*
      TIMEX = ""
      TIMEV = ""
      FOR TP = 1 TO CNT
         IF STIME<1,TP> = "" OR TTYPE<1,TP> = "C" THEN GOTO 90
         ST = STIME<1,TP>+0
         ET = ETIME<1,TP>+0
* CORRECT LOCATE ON UNIDATA - LMR 9/24/93
         STX = ST+1
         STX = STR('0',4-LEN(STX)):STX
*        LOCATE (ST+1) IN TIMEX,1 BY "AR" SETTING P ELSE NULL
         LOCATE STX IN TIMEX,1 BY "AR" SETTING P ELSE NULL
*
         TIMEX = INSERT(TIMEX,P,0,0,ST)
         TIMEV = INSERT(TIMEV,P,0,0,TP)
* CORRECT LOCATE ON UNIDATA - LMR 9/24/93
         ETX = ET+1
         ETX = STR('0',4-LEN(ETX)):ETX
*        LOCATE (ET+1) IN TIMEX,1 BY "AR" SETTING P ELSE NULL
         LOCATE ETX IN TIMEX,1 BY "AR" SETTING P ELSE NULL
*
         TIMEX = INSERT(TIMEX,P,0,0,ET)
         TIMEV = INSERT(TIMEV,P,0,0,TP)
90    NEXT TP
*
*---- CALCULATE ACTIVE AND IDLE TIME
*
      AMIN = 0
      IMIN = 0
      TMIN = 0
      PPX = ""
      PP1 = 0
      TCNT = COUNT(TIMEX,AM) + (TIMEX # "")
      FOR TP = 1 TO TCNT
         V = TIMEV<TP>
         BEGIN CASE
         CASE PPX = ""
            PPX = V
            IF PP1 > 0 THEN
               PP2 = TP
               GOSUB 1000
               IMIN = IMIN + ELAP.MIN
            END
            PP1 = TP
         CASE 1
            LOCATE V IN PPX,1 SETTING P ELSE P = 0
            IF P = 0 THEN
               PPX = INSERT(PPX,-1,0,0,V)
            END ELSE
               PPX = DELETE(PPX,P,0,0)
               IF PPX = "" THEN
                  PP2 = TP
                  GOSUB 1000
                  IF TTYPE<1,V> = "L" OR TTYPE<1,V> = "I" THEN
                     IMIN = IMIN + ELAP.MIN
                  END ELSE
                     AMIN = AMIN + ELAP.MIN
                  END
                  PP1 = TP
               END
            END
         END CASE
      NEXT TP
      TMIN = AMIN + IMIN
      ATIME = INT(AMIN/60)*100+MOD(AMIN,60)
      ITIME = INT(IMIN/60)*100+MOD(IMIN,60)
      TTIME = INT(TMIN/60)*100+MOD(TMIN,60)
      GOTO 99999
*
*---- CALCULATE ELAPSED TIME
*
1000*
      STRTC = TIMEX<PP1>
      STPTC = TIMEX<PP2>
      STRMC = INT(STRTC/100)*60+MOD(STRTC,100)
      STPMC = INT(STPTC/100)*60+MOD(STPTC,100)
      ELAP.MIN = (STPMC - STRMC)
      RETURN
*
*---- RETURN TO CALLING PROGRAM
*
99999*
      RETURN
   END
