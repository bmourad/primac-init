***************************************************************************
*
* PROGRAM   - PNP.LOGON.RPT
*
* AUTHOR    - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE      - 09/01/87
*
* DESCRIPTION
*
* Reports employees who are currently logged on to the shop-floor data
* collection system.
*
* MAINTENANCE:
*
* T26090 ajibaly 07/02/2002 * CONVERT TO R12 REPORT.SCRN
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JCS.CPYLIB>PNP.LOG
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
      OPEN "","PNP.LOG" TO PNP.LOG ELSE
         ERRMSG = "CANNOT OPEN PNP.LOG FILE"
         GOSUB 90000
         RETURN
      END
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE
         ERRMSG = "CANNOT OPEN EMPLOYEE FILE"
         GOSUB 90000
         RETURN
      END
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         RETURN
      END
*
*---- INITIALIZATION
*
      PROCREAD BUFFER ELSE
         ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC."
         GOSUB 90000
         RETURN
      END
      HD1      = ''
      HD2      = ''
      CONO     = BUFFER<1>
      RPT.NUM  = BUFFER<2>
      RPT.NAME = ''
      RPT.DATE = DATE()
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         MAT COMP.REC = ""
      END
      CALL GET.PROG.HEAD(CONO,CO.NAME,RPT.NAME,RPT.NUM,RPT.DATE,HD1,HD2)
      ;* HD1 = ""
      ;* HD2 = SPACE(33):"E M P L O Y E E S   L O G G E D   O N   R E P O R T"
      SH1 = "                                             <--- SIGN-ON ---->  <-- LAST ENTRY -->"
      SH2 = "        EMPLOYEE NAME            EMP#   SFT    DATE      TIME      DATE      TIME  "
      SH3 = "------------------------------  ------  ---  --------  --------  --------  --------"
      PAGE.NO = 0
      LINE.CNT = 99
      PRINTER ON
*
*---- MAIN PROCESSING
*
100*
      READNEXT ID ELSE
         GOTO 99999
      END
      CONO = ID[1,3]
      EMP = ID[4,99]
      MATREAD PNPL.REC FROM PNP.LOG, CONO:EMP ELSE GOTO 100
      IF PNPL.LOGOFF.FLAG # "" THEN GOTO 100
      GOSUB 1000
      GOTO 100
*
*---- PRINT DETAIL
*
1000*
      IF LINE.CNT > 55 THEN GOSUB 10000
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMP ELSE
         MAT EMP.REC = ""
      END
      PLINE = SPACE(132)
      PLINE[1,30] = EMP.LAST.NAME:", ":EMP.FRST.NAME
      PLINE[33,6] = EMP
      PLINE[42,1] = PNPL.SHIFT
      PLINE[46,8] = OCONV(PNPL.LOGON.DATE,"D2-")
      PLINE[56,8] = OCONV(PNPL.LOGON.TIME,"MTS")
      PLINE[66,8] = OCONV(PNPL.LAST.DATE,"D2-")
      PLINE[76,8] = OCONV(PNPL.LAST.TIME,"MTS")
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      RETURN
*
*---- PRINT PAGE HEADINGS
*
10000*
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PRINT SH1
      PRINT SH2
      PRINT SH3
      LINE.CNT = 6
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER OFF
      STOP
   END
