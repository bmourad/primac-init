*COPY>CPYLIB>COM1
*******************************************************
* REVISION    - [08.1]
* SYSTEM   - CAF1BP
* PROGRAM  - PROFITABILITY.RPT
* DATE     - 7.18.91
*            COPIED TO CAF1 
* DESCRIPTION -
*
* TASK
*     18573 5/95 LLH 1-52 ACCOUNTING PERIOD
*
*T22007 stefanie 08/27/1997 * Add Division to the heading.
*T26493 05/09/2002 * Change pgm to get the rpt # from the proc and use
*                    GET.PROG.HEAD for the heading.
*******************************************************
*ENDDOC
*--- INSERT FILE EQUATE
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>JCS.CPYLIB>JOB.OSP
*COPY>JCS.CPYLIB>JOB.SHIP
*COPY>JCS.CPYLIB>JOB.MISC
*COPY>JCS.CPYLIB>INVOICE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*
*--- INITALIZATION
   MAT FILE.VARS = ""
   MAT EDIT.COM  = ""
   MAT EDIT.COM.DRIVER = ""
   MAT COMP.REC  = ""
   MAT SCV.REC   = ""
*
   TOTALS = 0
   TOTAL.LABOR.COST = 0
   TOTAL.LB.OH.COST = 0
   TOTAL.MATL.COST = 0
   TOTAL.OSP.COST = 0
   TOTAL.SHIP.COST = 0
   TOTAL.MISC.COST = 0
   TOTAL.INV.AMT = 0
   SLSMN.TOTALS = 0
   SLSMN.LABOR.COST = 0
   SLSMN.LB.OH.COST = 0
   SLSMN.MATL.COST = 0
   SLSMN.OSP.COST = 0
   SLSMN.SHIP.COST = 0
   SLSMN.MISC.COST = 0
   SLSMN.INV.AMT = 0
   PURGE.STATUS = 7
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   SP4 = SPACE(4)
   SP10 = SPACE(10)
   SP11 = SP10:SP1
   D2 = STR('-',2)
   D3 = STR('-',3)
   D4 = D2:D2
   D5 = D2:D3
   D30 = STR('-',30)
   PREV.CUST = ""
*
*--- SET UP ERRMSG ROUTINE FOR CPYLIB SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---  OPEN FILE
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING";GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING";GOTO 93000
   OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = "PMC.SCREENS FILE IS MISSING"; GOTO 93000
   OPEN '','JOB' TO JOB ELSE ERRMSG = "JOB FILE IS MISSING";GOTO 93000
   OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING";GOTO 93000
   OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG = "SALESMAN FILE IS MISSING";GOTO 93000
   OPEN '','INVOICE' TO INVOICE ELSE ERRMSG = "INVOICE FILE IS MISSING";GOTO 93000
   OPEN '','JOB.TIME' TO JOB.TIME ELSE ERRMSG = "JOB.TIME FILE IS MISSING";GOTO 93000
   OPEN '','JOB.MATL' TO JOB.MATL ELSE ERRMSG = "JOB.MATL FILE IS MISSING";GOTO 93000
   OPEN '','JOB.OSP' TO JOB.OSP ELSE ERRMSG = "JOB.OSP FILE IS MISSING";GOTO 93000
   OPEN '','JOB.SHIP' TO JOB.SHIP ELSE ERRMSG = "JOB.SHIP FILE IS MISSING";GOTO 93000
   OPEN '','JOB.MISC' TO JOB.MISC ELSE ERRMSG = "JOB.MISC FILE IS MISSING";GOTO 93000
*
*--- GET COMPANY
*
   MAT COMP.REC = ""; CONO = ""
   CALL GET.CONO(CONO, MAT COMP.REC)
   IF CONO = "END" THEN GOTO 999999
*
*--- GET FISCAL PERIOD TO REPORT
*
   PROCREAD BUFFER ELSE
      BUFFER = ""
      GOTO 999999
   END
   MENU     = BUFFER<3>  ;* PERIOD TO REPORT
   DIVISION = BUFFER<5>  ;* T22007 <
*
*
*--- SETUP HEADERS
*
*T26493 v
*  POS = 51 - INT(LEN(CO.NAME)/2)
*  POS2 = 70 - POS
*  HEAD1 = "DATE - ":OCONV(DATE(), 'D2/') "L#8"
*  HEAD1 = HEAD1 :SPACE(POS) :CO.NAME "L#30"
*  HEAD1 = HEAD1 :SPACE(POS2) : "PAGE : 'PL'"
*  HEAD2 = "DIVISION - ":DIVISION:SPACE(32): "PERIOD PROFITABILITY REPORT "
*  IF MENU = "CURRENT" THEN
*     HEAD2 = HEAD2 : MENU : " PERIOD" : "'L'"
*  END ELSE
*     HEAD2 = HEAD2 : "PERIOD " : MENU[5,2] : SP2 : MENU[1,4] : "'L'"
*  END
*  HEAD3 = SPACE(60):"BY SALESREP": "'LL'"
   CONO.NAME = ""
   RPT.NAME = ""
   RPT.NO = BUFFER<2>
   HEAD1 = ""
   HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NO,"",HEAD1,HEAD2)
   HEAD1 = HEAD1:"'PL'"
   HEAD3 = "DIVISION - ":DIVISION
   IF MENU = "CURRENT" THEN
      HEAD3 = HEAD3:SPACE(52):"FOR ":MENU:" PERIOD":"'LL'"
   END ELSE
      HEAD3 = HEAD3:SPACE(52):"FOR PERIOD ":MENU[5,2]:SP2:MENU[1,4]:"'LL'"
   END
*T26493 ^
   HEAD4 = SPACE(68) : "<----------------- COST INFORMATION ---------------->":"'L'"
   HEAD5 = "JOB NUM  CUSTOMER  SALESREP    INV DATE INVOICE#  INV. AMT  LABOR DIR LABOR OH  MATERIAL  O/S PURCH SHIPPING    MISC      TOTAL":"'L'"
   HEAD6 = "-------  --------  --------    -------- -------- ---------- --------- --------- --------- --------- --------- --------- ----------":"'L'"
   H.LINE = SPACE(49):"========== ========= ========= ========= ========= ========= ========= =========="
   LINE = SPACE(49):"----------"
   PRINTER ON
   HEADING HEAD1:HEAD2:HEAD3:HEAD4:HEAD5:HEAD6
*---  READING ALL FILES AND PROCESS ONE AT A TIME
   GOSUB 5500
   FIRST.REC=1
100 *
   READNEXT ID ELSE GOTO 111
   KEY = ID[4,99]
   MATREAD JOB.REC FROM JOB , ID ELSE GOTO 100
   LOCATE PURGE.STATUS IN JOB.STATUS<1>,1 SETTING FND ELSE FND = 0
   IF FND THEN GOTO 100
   MATREAD SALESMAN.REC FROM SALESMAN, CONO:JOB.SLSMN ELSE
      MAT SALESMAN.REC = ""
   END
   IF FIRST.REC THEN
      WIP.PRESENT=0
      PREV.SLSMN=JOB.SLSMN
      PREV.SLSNAME=SALS.NAME
      FIRST.REC=0
   END
   IF PREV.SLSMN # JOB.SLSMN THEN
      IF WIP.PRESENT THEN GOSUB 6000
      PREV.SLSMN=JOB.SLSMN
      PREV.SLSNAME=SALS.NAME
   END
   ICNT = COUNT(JOB.INV.NO,VM) + (JOB.INV.NO # "")
   IF MENU = "CURRENT" THEN
      GOSUB 3000
   END ELSE
      GOSUB 4000
   END
   GOTO 100
111 *
*--- PRINTING THE TOTAL VALUES
   IF WIP.PRESENT THEN GOSUB 6000
   GMRGN.AMT = TOTAL.INV.AMT - TOTALS
   IF TOTAL.INV.AMT + 0 = 0 THEN
      GMRGN.PCT=0
      GM.PCT = 0
   END ELSE
      GM.PCT = ((TOTAL.INV.AMT - TOTALS) / TOTAL.INV.AMT) * 10000
      IF GM.PCT > 0 THEN
         GMRGN.PCT = INT(GM.PCT + .5)
      END ELSE
         GMRGN.PCT = INT(GM.PCT - .5)
      END
   END
   PRINT H.LINE
   T.LINE = ''
   T.LINE = T.LINE : "GMRGN$ :":OCONV(GMRGN.AMT,'MD2')"R#10":SP1
   T.LINE = T.LINE : "GMRGN% :":OCONV(GMRGN.PCT,'MD2')"R#7":SP1
   T.LINE = T.LINE : "GRAND TOTAL :":SP1:OCONV(TOTAL.INV.AMT,"MD2")"R#10"
   T.LINE = T.LINE : OCONV(TOTAL.LABOR.COST, 'MD2') "R#10"
   T.LINE = T.LINE : OCONV(TOTAL.LB.OH.COST, 'MD2') "R#10"
   T.LINE = T.LINE : OCONV(TOTAL.MATL.COST, 'MD2') "R#10"
   T.LINE = T.LINE : OCONV(TOTAL.OSP.COST, 'MD2') "R#10"
   T.LINE = T.LINE : OCONV(TOTAL.SHIP.COST, 'MD2') "R#10"
   T.LINE = T.LINE : OCONV(TOTAL.MISC.COST, 'MD2') "R#10":SP1
   T.LINE = T.LINE : OCONV(TOTALS, 'MD2') "R#10"
   PRINT T.LINE
*--- END OF JOB
   PRINTER OFF
   GOTO 999999
*--- PROCESS THE RECORD FOR CURRENT
3000 *
   IF JOB.WIP<1,1> < 1 THEN GOTO 3999
   WIP = JOB.WIP<1,3,1> + JOB.WIP<1,3,2> + JOB.WIP<1,3,3> + JOB.WIP<1,3,4>
   IF WIP = 0 AND ICNT = 0 THEN GOTO 3999
   JJ = 0;INV.NO = "";INV.DATE = "";INV.AMT = ""
   FOR XX = 1 TO ICNT
      IAMT = 0
      C.CNT = COUNT(JOB.INV.CAT<1,XX>,SVM) + (JOB.INV.CAT<1,XX> # "")
      FOR ZZ = 1 TO C.CNT
         CATG = JOB.INV.CAT<1,XX,ZZ>
         AMT = JOB.INV.AMT<1,XX,ZZ>
         IF CATG # "TAX" AND CATG # "CMT" THEN IAMT = IAMT + AMT
      NEXT ZZ
      IF IAMT # 0 THEN
         JJ = JJ + 1
         INV.NO<1,JJ> = JOB.INV.NO<1,XX>
         INV.DATE<1,JJ> = OCONV(JOB.INV.DATE<1,XX>,"D2/")
         INV.AMT<1,JJ> = IAMT
      END
   NEXT XX
   LB.WIP = JOB.LB.WIP<1,3,1>
   LB.OH.WIP = JOB.LB.WIP<1,3,2> + JOB.LB.WIP<1,3,3> + JOB.LB.WIP<1,3,4>
   MT.WIP = JOB.MT.WIP<1,3,1> + JOB.MT.WIP<1,3,2> + JOB.MT.WIP<1,3,3>
   OS.WIP = JOB.OS.WIP<1,3,1> + JOB.OS.WIP<1,3,2>
   SP.WIP = JOB.SP.WIP<1,3,1> + JOB.SP.WIP<1,3,2>
   MS.WIP = JOB.MS.WIP<1,3,1> + JOB.MS.WIP<1,3,2>
*---FINDING THE TOTAL COST
   MAST.INV.AMT = INV.AMT
   MAST.INV.DATE = INV.DATE
   MAST.INV.NO = INV.NO
   MAST.LABOR.COST = LB.WIP
   MAST.LB.OH.COST = LB.OH.WIP
   MAST.MATL.COST = MT.WIP
   MAST.OSP.COST = OS.WIP
   MAST.SHIP.COST = SP.WIP
   MAST.MISC.COST = MS.WIP
   MAST.TOTAL = WIP
   GOSUB 5000
3999 *
   RETURN
*--- PROCESS THE RECORD FOR SPECIFIC YEAR AND PERIOD
4000 *
   IF JOB.WIP<1,4> > MENU THEN GOTO 4999
   IF JOB.WIP<1,3,1> + JOB.WIP<1,3,2> + JOB.WIP<1,3,3> + JOB.WIP<1,3,4> = 0 THEN
      IF JOB.WIP<1,4> = "" OR JOB.WIP<1,5> < MENU THEN GOTO 4999
   END
   TOT.LB.COST = 0; TOT.LB.OH.COST = 0; TOT.MT.COST = 0; TOT.OS.COST = 0; TOT.SP.COST = 0; TOT.MS.COST = 0
   JJ = 0;INV.NO = "";INV.DATE = "";INV.AMT = ""
   IAMT=""
   FOR XX = 1 TO ICNT
      MATREAD IVC.REC FROM INVOICE,CONO:JOB.INV.NO<1,XX> ELSE GOTO 4005
      IF IVC.PROC.MON # MENU THEN GOTO 4005
      IAMT=0
      C.CNT = COUNT(JOB.INV.CAT<1,XX>,SVM) + (JOB.INV.CAT<1,XX> # "")
      FOR ZZ = 1 TO C.CNT
         CATG = JOB.INV.CAT<1,XX,ZZ>
         AMT = JOB.INV.AMT<1,XX,ZZ>
         IF CATG # "TAX" AND CATG # "CMT" THEN IAMT = IAMT + AMT
      NEXT ZZ
*        IF IAMT # 0 THEN
      JJ = JJ + 1
      INV.NO<1,JJ> = JOB.INV.NO<1,XX>
      INV.DATE<1,JJ> = OCONV(JOB.INV.DATE<1,XX>,"D2/")
      INV.AMT<1,JJ> = IAMT
*        END
4005 *
   NEXT XX
   IF JOB.LB.WIP<1,4> > MENU THEN GOTO 4015
   IF JOB.LB.WIP<1,3,1> + JOB.LB.WIP<1,3,2> + JOB.LB.WIP<1,3,3> + JOB.LB.WIP<1,3,4> = 0 THEN
      IF JOB.LB.WIP<1,4> = "" OR JOB.LB.WIP<1,5> < MENU THEN GOTO 4015
   END
   CNT = COUNT(JOB.LB.DEPT,VM) + (JOB.LB.DEPT # "")
   FOR I = 1 TO CNT
      LB.ID = ID:"!":JOB.LB.DEPT<1,I>:"!":JOB.LB.CCTR<1,I>:"!"
      SEQ.CNT = JOB.LB.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JLB.REC FROM JOB.TIME, LB.ID:II ELSE GOTO 4010
         III = 2
         LOOP WHILE JLB.WIP<1,III> # "" DO
            IF JLB.MON<1,III> = MENU THEN
               TOT.LB.COST = TOT.LB.COST - JLB.WIP<1,III,1>
               TOT.LB.OH.COST = TOT.LB.OH.COST - JLB.WIP<1,III,2> - JLB.WIP<1,III,3> - JLB.WIP<1,III,4>
            END
            III = III + 1
         REPEAT
4010  NEXT II
   NEXT I
4015 IF JOB.MT.WIP<1,4> > MENU THEN GOTO 4025
   IF JOB.MT.WIP<1,3,1> + JOB.MT.WIP<1,3,2> + JOB.MT.WIP<1,3,3> = 0 THEN
      IF JOB.MT.WIP<1,4> = "" OR JOB.MT.WIP<1,5> < MENU THEN GOTO 4025
   END
   CNT = COUNT(JOB.MT.DEPT,VM) + (JOB.MT.DEPT # "")
   FOR I = 1 TO CNT
      MT.ID = ID:"!":JOB.MT.DEPT<1,I>:"!":JOB.MT.CCTR<1,I>:"!":JOB.MT.PROD<1,I>:"!":JOB.MT.WHSE<1,I>:"!"
      SEQ.CNT = JOB.MT.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JMT.REC FROM JOB.MATL, MT.ID:II ELSE GOTO 4020
         III = 2
         LOOP WHILE JMT.WIP<1,III> # "" DO
            IF JMT.MON<1,III> = MENU THEN TOT.MT.COST = TOT.MT.COST - JMT.WIP<1,III,1> - JMT.WIP<1,III,2> - JMT.WIP<1,III,3>
            III = III + 1
         REPEAT
4020  NEXT II
   NEXT I
4025 IF JOB.OS.WIP<1,4> > MENU THEN GOTO 4035
   IF JOB.OS.WIP<1,3,1> + JOB.OS.WIP<1,3,2> = 0 THEN
      IF JOB.OS.WIP<1,4> = "" OR JOB.OS.WIP<1,5> < MENU THEN GOTO 4035
   END
   CNT = COUNT(JOB.OS.DEPT,VM) + (JOB.OS.DEPT # "")
   FOR I = 1 TO CNT
      OS.ID = ID:"!":JOB.OS.DEPT<1,I>:"!":JOB.OS.CCTR<1,I>:"!":JOB.OS.PLINE<1,I>:"!"
      SEQ.CNT = JOB.OS.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JOS.REC FROM JOB.OSP, OS.ID:II ELSE GOTO 4030
         III = 2
         LOOP WHILE JOS.WIP<1,III> # "" DO
            IF JOS.MON<1,III> = MENU THEN TOT.OS.COST = TOT.OS.COST - JOS.WIP<1,III,1> - JOS.WIP<1,III,2>
            III = III + 1
         REPEAT
4030  NEXT II
   NEXT I
4035 IF JOB.SP.WIP<1,4> > MENU THEN GOTO 4045
   IF JOB.SP.WIP<1,3,1> + JOB.SP.WIP<1,3,2> = 0 THEN
      IF JOB.SP.WIP<1,4> = "" OR JOB.SP.WIP<1,5> < MENU THEN GOTO 4045
   END
   CNT = COUNT(JOB.SP.DEPT,VM) + (JOB.SP.DEPT # "")
   FOR I = 1 TO CNT
      SP.ID = ID:"!":JOB.SP.DEPT<1,I>:"!":JOB.SP.CCTR<1,I>:"!"
      SEQ.CNT = JOB.SP.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JSP.REC FROM JOB.SHIP, SP.ID:II ELSE GOTO 4040
         III = 2
         LOOP WHILE JSP.WIP<1,III> # ""  DO
            IF JSP.MON<1,III> = MENU THEN TOT.SP.COST = TOT.SP.COST - JSP.WIP<1,III,1> - JSP.WIP<1,III,2>
            III = III + 1
         REPEAT
4040  NEXT II
   NEXT I
4045 IF JOB.MS.WIP<1,4> > MENU THEN GOTO 4055
   IF JOB.MS.WIP<1,3,1> + JOB.MS.WIP<1,3,2> = 0 THEN
      IF JOB.MS.WIP<1,4> = "" OR JOB.MS.WIP<1,5> < MENU THEN GOTO 4055
   END
   CNT = COUNT(JOB.MS.DEPT,VM) + (JOB.MS.DEPT # "")
   FOR I = 1 TO CNT
      MS.ID = ID:"!":JOB.MS.DEPT<1,I>:"!":JOB.MS.CCTR<1,I>:"!"
      SEQ.CNT = JOB.MS.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JMS.REC FROM JOB.MISC, MS.ID:II ELSE GOTO 4050
         III = 2
         LOOP WHILE JMS.WIP<1,III> # "" DO
            IF JMS.MON<1,III> = MENU THEN TOT.MS.COST = TOT.MS.COST - JMS.WIP<1,III,1> - JMS.WIP<1,III,2>
            III = III + 1
         REPEAT
4050  NEXT II
   NEXT I
4055 TOT.COST = TOT.LB.COST + TOT.LB.OH.COST + TOT.MT.COST + TOT.OS.COST + TOT.SP.COST + TOT.MS.COST
   IF TOT.COST = 0 AND IAMT = "" THEN GOTO 4999
*---FINDING THE TOTAL COST
   MAST.INV.DATE = INV.DATE
   MAST.INV.NO = INV.NO
   MAST.INV.AMT = INV.AMT
   MAST.LABOR.COST = TOT.LB.COST
   MAST.LB.OH.COST = TOT.LB.OH.COST
   MAST.MATL.COST = TOT.MT.COST
   MAST.OSP.COST = TOT.OS.COST
   MAST.SHIP.COST = TOT.SP.COST
   MAST.MISC.COST = TOT.MS.COST
   MAST.TOTAL = TOT.COST
   GOSUB 5000
4999 *
   RETURN
*--- PRINT TOTAL FOR JOB
5000 *
   IF PREV.CUST # JOB.CUST THEN
      MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME = "CUSTOMER UNKNOWN"
      PREV.CUST = JOB.CUST
   END
   TTL.INV = 0;MRGN.AMT = 0;MRGN.PCT = 0
   INV.CNT = COUNT(MAST.INV.NO,VM) + (MAST.INV.NO # "")
   FOR K = 1 TO INV.CNT
      TTL.INV = TTL.INV + MAST.INV.AMT<1,K>
   NEXT K
   MRGN.AMT = TTL.INV - MAST.TOTAL
   IF TTL.INV + 0 = 0 THEN
      M.PCT = 0
   END ELSE
      M.PCT = ((TTL.INV - MAST.TOTAL) / TTL.INV) * 10000
      IF M.PCT > 0 THEN
         MRGN.PCT = INT(M.PCT + .5)

      END ELSE
         MRGN.PCT = INT(M.PCT - .5)
      END
   END
   WIP.PRESENT=1
   T.LINE = ""
   T.LINE = T.LINE : KEY "L#8" :SP1: JOB.CUST "L#8" :SP2:JOB.SLSMN "L#8":SP4
   T.LINE = T.LINE : MAST.INV.DATE<1,1> "R#8":SP1
   T.LINE = T.LINE : MAST.INV.NO<1,1> "L#8":SP1
   T.LINE = T.LINE : OCONV(MAST.INV.AMT<1,1>,"MD2") "R#10":SP1
   T.LINE = T.LINE : OCONV(MAST.LABOR.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(MAST.LB.OH.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(MAST.MATL.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(MAST.OSP.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(MAST.SHIP.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(MAST.MISC.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(MAST.TOTAL, 'MD2') "R#10"
   PRINT T.LINE
   FOR SS = 2 TO INV.CNT
      T.LINE = ""
      T.LINE = T.LINE : SPACE(31):MAST.INV.DATE<1,SS> "R#8":SP1
      T.LINE = T.LINE : MAST.INV.NO<1,SS> "L#8":SP1
      T.LINE = T.LINE : OCONV(MAST.INV.AMT<1,SS>,"MD2") "R#10":SP2
      PRINT T.LINE
   NEXT SS
   PRINT LINE
   T.LINE = ""
   T.LINE = T.LINE : "MRGN$ : ":OCONV(MRGN.AMT,'MD2') "R#10":SP2
   T.LINE = T.LINE : "MRGN% :":OCONV(MRGN.PCT,'MD2') "R#7":SPACE(3)
   T.LINE = T.LINE : "INV TOTAL : ":OCONV(TTL.INV,'MD2') "R#10"
   PRINT T.LINE
   PRINT
   PRINT
5100 *
   TOTAL.LABOR.COST = TOTAL.LABOR.COST + MAST.LABOR.COST
   TOTAL.LB.OH.COST = TOTAL.LB.OH.COST + MAST.LB.OH.COST
   TOTAL.MATL.COST = TOTAL.MATL.COST + MAST.MATL.COST
   TOTAL.OSP.COST = TOTAL.OSP.COST + MAST.OSP.COST
   TOTAL.SHIP.COST = TOTAL.SHIP.COST + MAST.SHIP.COST
   TOTAL.MISC.COST = TOTAL.MISC.COST + MAST.MISC.COST
   TOTALS = TOTALS + MAST.TOTAL
   TOTAL.INV.AMT = TOTAL.INV.AMT + TTL.INV
*
   SLSMN.LABOR.COST = SLSMN.LABOR.COST + MAST.LABOR.COST
   SLSMN.LB.OH.COST = SLSMN.LB.OH.COST + MAST.LB.OH.COST
   SLSMN.MATL.COST = SLSMN.MATL.COST + MAST.MATL.COST
   SLSMN.OSP.COST = SLSMN.OSP.COST + MAST.OSP.COST
   SLSMN.SHIP.COST = SLSMN.SHIP.COST + MAST.SHIP.COST
   SLSMN.MISC.COST = SLSMN.MISC.COST + MAST.MISC.COST
   SLSMN.TOTALS = SLSMN.TOTALS + MAST.TOTAL
   SLSMN.INV.AMT = SLSMN.INV.AMT + TTL.INV
*--- CLEAR MASTER TOTALS
5500 *
   MAST.TOTAL = 0
   MAST.LABOR.COST = 0
   MAST.MATL.COST = 0
   MAST.OSP.COST = 0
   MAST.SHIP.COST = 0
   MAST.MISC.COST = 0
   MAST.INV.AMT = ""
   MAST.INV.DATE = ""
   MAST.INV.NO = ""
   RETURN
*------PRINT SALESREP TOTALS
6000 *
   SLSMN.GMRGN.AMT = SLSMN.INV.AMT - SLSMN.TOTALS
   IF SLSMN.INV.AMT + 0 = 0 THEN
      SLSMN.GM.PCT = 0
      SLSMN.GMRGN.PCT= 0
   END ELSE
      SLSMN.GM.PCT = ((SLSMN.INV.AMT - SLSMN.TOTALS) / SLSMN.INV.AMT) * 10000
      IF SLSMN.GM.PCT > 0 THEN
         SLSMN.GMRGN.PCT = INT(SLSMN.GM.PCT + .5)
      END ELSE
         SLSMN.GMRGN.PCT = INT(SLSMN.GM.PCT - .5)
      END
   END
   WIP.PRESENT=0
*PRINT H.LINE
   T.LINE = SPACE(4):"SALESREP TOTALS :":PREV.SLSNAME 'L#20':H.LINE[42,999]
   PRINT T.LINE
   T.LINE ="SMRGN$ :":OCONV(SLSMN.GMRGN.AMT,"MD2")"R#10":SP1
   T.LINE =T.LINE:"SMRGN% :":OCONV(SLSMN.GMRGN.PCT,"MD2")"R#7":SPACE(15)
   T.LINE = T.LINE:OCONV(SLSMN.INV.AMT<1,1>,"MD2") "R#10":SP1
   T.LINE = T.LINE : OCONV(SLSMN.LABOR.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(SLSMN.LB.OH.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(SLSMN.MATL.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(SLSMN.OSP.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(SLSMN.SHIP.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(SLSMN.MISC.COST, 'MD2') "R#9" :SP1
   T.LINE = T.LINE : OCONV(SLSMN.TOTALS, 'MD2') "R#10"
   PRINT T.LINE
   PRINT
   SLSMN.TOTALS = 0
   SLSMN.LABOR.COST = 0
   SLSMN.LB.OH.COST = 0
   SLSMN.MATL.COST = 0
   SLSMN.OSP.COST = 0
   SLSMN.SHIP.COST = 0
   SLSMN.MISC.COST = 0
   SLSMN.INV.AMT = 0
   RETURN
*--- CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999 *
END
