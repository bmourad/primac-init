*COPY>CPYLIB>COM1
***********************************************************************
*   SYSTEM          - CBA
*   PROGRAM         - CREDIT.AUTH.MAINT
*   BY              - EDWIN LEHR
*   DATE            - 07/16/01
*   DESCRIPTION     - CREDIT AUTHORIZATION MAINTENANCE
*T26126 adelgado 03/07/2002 * Implement the LOCKED clause for READU.
*ENDDOC
***********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  MAT FILE.VARS = ''
*
  EQU TRUE  TO 1
  EQU FALSE TO 0
*
*** OPEN FILES
  OPEN '','CREDIT.AUTH.CODE' TO CREDIT.AUTH.CODE ELSE
    ERRMSG = 'CREDIT.AUTH.CODE FILE MISSING'
    GOTO 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE MISSING'
    GOTO 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE MISSING'
    GOTO 93000
  END
  OPEN '','JCS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'JCS.SCREENS FILE MISSING'
    GOTO 93000
  END
*
*---- GET COMPANY NUMBER
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
  EQU CODE.DESC TO CODE.REC<1>
  MAT EDIT.COM.DRIVER = ''
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'CREDIT.AUTH.MAINT'
  ECD.ACTION=1 ; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  LOOP PRIMARY.LOOP = TRUE
    SECONDARY.LOOP = TRUE
    RELEASE    ;* T26126
    CODE.REC = ''
    MAT SCV.REC = ''
    ECD.ACTION = 6 ; CALL SCRN.EDIT
    ECD.NUM = 1 
    ECD.VALDAT.CODE = 4
    ECD.VALDAT.FILE = CREDIT.AUTH.CODE
    ECD.PREFIX.ID = CONO
    ECD.ACTION = 4 ; CALL SCRN.EDIT
    BEGIN CASE
      CASE ECD.RET.VALUE = 'END'
        PRIMARY.LOOP = FALSE ; SECONDARY.LOOP = FALSE
      CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
        * T26126 v
        READU DUMMY FROM CREDIT.AUTH.CODE, CONO:ECD.RET.VALUE LOCKED
          ERRMSG = 'CREDIT AUTHORIZATION record is locked by user - ':GETUSERNAME(STATUS())
          GOSUB 91000;SECONDARY.LOOP = FALSE ; CONTINUE 
        END ELSE NULL
        * T26126 ^
        CREDIT.AUTH.ID = ECD.RET.VALUE
        CODE.REC = ECD.VALDAT.ITEM
        SCV.REC(2) = CODE.DESC
        ECD.ACTION = 3 ; CALL SCRN.EDIT
      CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
        CREDIT.AUTH.ID = ECD.RET.VALUE
        GOSUB 1100
        IF ECD.RET.VALUE = 'END' THEN SECONDARY.LOOP = 2
    END CASE
    LOOP WHILE SECONDARY.LOOP = TRUE DO
      ECD.NUM = 3
      SCV.REC(ECD.NUM) = ''
      ECD.ACTION = 4 ; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
        CASE OPTION = 'E' OR OPTION = 'END'
          SECONDARY.LOOP = FALSE
        CASE OPTION = 1
          GOSUB 1100
        CASE OPTION = 'F'
          WRITE CODE.REC ON CREDIT.AUTH.CODE, CONO:CREDIT.AUTH.ID
          SECONDARY.LOOP = FALSE
        CASE OPTION = 'D'
          DELETE CREDIT.AUTH.CODE, CONO:CREDIT.AUTH.ID
          SECONDARY.LOOP = FALSE
      END CASE
    REPEAT
  WHILE PRIMARY.LOOP DO REPEAT
  GOTO 99999
*
1100 ECD.NUM = 2
  ECD.ACTION = 4 ; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN CODE.DESC = ECD.RET.VALUE
  RETURN
*
*--- SYSTEM ERROR MESSAGE ROUTINES
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- END OF PROGRAM
99999 *
END
