      SUBROUTINE JRS.MS.DET
*COPY>JCS.CPYLIB>COM.JRS
*******************************************************************
* REVISION    - [08.0]
* COPYRIGHT   - 1982 by C.B.A.  (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JRS.MS.DET
* BY          - BILAL MOGHRABI, C.B.A.
* DATE        - 06/15/88
* DESCRIPTION - Program produces a Job Miscellaneois Detail Report.
*T26556 ajibaly 05/09/2002 * USE RPT.NO AND GET.PROG.HEAD
*T28964 wvaughan 08/07/2006 * Extend JOB.QTY field to display 8 numbers.
*ENDDOC
*******************************************************************
*
*-- FILE EQUATES
*
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>OPERATION
*COPY>JCS.CPYLIB>JOB.MISC
*COPY>JCS.CPYLIB>JOB.RPT.SET
*COPY>JCS.CPYLIB>JOB.RPT.SUM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
    DIM CC.TOT.REC(2)
    EQU CC.TOT TO CC.TOT.REC(1)
    EQU CC.S.TOT TO CC.TOT.REC(2)
    DIM DP.TOT.REC(2)
    EQU DP.TOT TO DP.TOT.REC(1)
    EQU DP.S.TOT TO DP.TOT.REC(2)
    DIM TOT.REC(2)
    EQU TOT.TOT TO TOT.REC(1)
    EQU TOT.S.TOT TO TOT.REC(2)
*
*-- INITIALIZATION
*
    PG.NO=0
    LINE.CNT=75
*T26556 v
*   RPT.NO="XXXX"
    HD1 = ""; HD2 = ""
    RPT.NAME = ""
    RPT.DATE = DATE()
    CALL GET.PROG.HEAD(CONO,C.CO.NAME,RPT.NAME,RPT.NO,RPT.DATE,HD1,HD2)
    PRINTER ON
*T26556 ^
    UNKNOWN=STR("?",30)
    SPC = SPACE(1)
    DASHES = STR("-",52)
    PRINT.FLG = 1
    HLINE = SPACE(108):"SPOIL"
    HLINE1="DEPT CCTR OPER":SPACE(13):"MISCELLANEOUS DESCRIPTION":SPACE(23):"REASON":SPACE(10):"DATE     COST    COST    REASON  XFER JOB"
    HLINE2="---- ---- ---- ":DASHES:" -------------------- -------- -------- ------- -------- --------"
    IF JRSD.CONS = "Y" THEN
       CONSOL = "CONSOLIDATED"
    END ELSE
       CONSOL = "            "
    END
    HLINE4="#":JOBNO "L#8":SPC:CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
    N=SPACE((50 - LEN(C.CO.NAME)) / 2)
    HLINE5=N:C.CO.NAME:N
*
*-- MAIN PROCESSING
*
    JOB.CNT = DCOUNT(JRS.IDS,AM)
    PTR = 0
    FOR I = 1 TO JOB.CNT WHILE PTR = 0
       IF FIELD(JRS.IDS<I>,'&',5) # "O" THEN GOTO 100
       DPNO = FIELD(JRS.IDS<I>,"&",2)
       IF DP.SEL # "ALL" AND DP.SEL # DPNO THEN GOTO 100
       MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<I> ELSE GOTO 100
       PTR = I
100 NEXT I
    IF PTR = 0 THEN GOTO 99999
    MAT CC.TOT.REC = ""
    MAT DP.TOT.REC = ""
    MAT TOT.REC = ""
    CCNO = FIELD(JRS.IDS<PTR>,"&",3)
    OPNO = FIELD(JRS.IDS<PTR>,"&",4)
    P.DP = DPNO
    P.CC = CCNO
    P.OP = OPNO
    MATREAD DEPT.REC FROM DEPARTMENT,CONO:P.DP ELSE DEPT.DESC=UNKNOWN
    MATREAD OPER.REC FROM OPERATION,CONO:P.OP ELSE OPER.DESC=UNKNOWN
    GOSUB 1000
    PTR = PTR + 1
    FOR I = PTR TO JOB.CNT
       IF FIELD(JRS.IDS<I>,'&',5) # "O" THEN GOTO 200
       DPNO = FIELD(JRS.IDS<I>,"&",2)
       IF DP.SEL # "ALL" AND DP.SEL # DPNO THEN GOTO 200
       MATREAD JRS.REC FROM JOB.RPT.SET,CONO:JOBNO:"&":JRS.IDS<I> ELSE GOTO 200
       CCNO = FIELD(JRS.IDS<I>,"&",3)
       OPNO = FIELD(JRS.IDS<I>,"&",4)
       BEGIN CASE
       CASE P.DP # DPNO
          PRINT.FLG = 1
          GOSUB 2000
          P.CC = CCNO
          P.OP = OPNO
          MATREAD OPER.REC FROM OPERATION,CONO:P.OP ELSE OPER.DESC=UNKNOWN
          GOSUB 3000
          P.DP = DPNO
          MATREAD DEPT.REC FROM DEPARTMENT,CONO:P.DP ELSE DEPT.DESC=UNKNOWN
       CASE P.CC # CCNO OR P.OP # OPNO
          GOSUB 2000
          P.CC = CCNO
          P.OP = OPNO
          MATREAD OPER.REC FROM OPERATION,CONO:P.OP ELSE OPER.DESC=UNKNOWN
       END CASE
       GOSUB 1000
200 NEXT I
    GOSUB 2000
    GOSUB 3000
    IF LINE.CNT + 2 >= 50 THEN GOSUB 9000
    PRINT SPACE(98):"-------- -------"
    PLINE =  "*** GRAND TOTALS ***":SPACE(78)
    PLINE=PLINE:OCONV(TOT.TOT,"MD2")"R#8":SPC:OCONV(TOT.S.TOT,"MD2")"R#7"
    PRINT PLINE
    GOTO 99999
*
*-- PRINT THE VALUES
*
1000*
    IF LINE.CNT + 3 >= 50 THEN GOSUB 9000
    IF PRINT.FLG THEN
       PRINT.FLG = 0
       PLINE =DEPT.DESC:"  ":P.DP
       PRINT PLINE
       DEPT.LEN=LEN(DEPT.DESC) + LEN(P.DP) + 2
       UNDERLINE.DEPT=STR("-",DEPT.LEN)
       PLINE=UNDERLINE.DEPT
       PRINT PLINE
       LINE.CNT=LINE.CNT + 2
    END
    MV.CNT = DCOUNT(JRS.SEQ,VM)
    FOR M = 1 TO MV.CNT
       SMV.CNT = DCOUNT(JRS.SEQ<1,M>,SVM)
       MS.ID = CONO:JOBNO:"!":DPNO:"!":CCNO:"!"
       FOR S = 1 TO SMV.CNT
          IF JRS.SUB.JOB<1,M,S> # "" THEN
             MS.ID = CONO:JRS.SUB.JOB<1,M,S>:"!":DPNO:"!":CCNO:"!"
          END
          MATREAD JMS.REC FROM JOB.MISC, MS.ID:JRS.SEQ<1,M,S> ELSE
             MAT JMS.REC = ""
          END
          PLINE=SPACE(15):JMS.DESC "L#52":SPC:JMS.REASON"L#20":SPC:OCONV(JMS.DATE,"D2-")"L#8":SPC
          IF JMS.TYPE # "S" THEN
             PLINE = PLINE:OCONV(JMS.COST, "MD2")"R#8":SPACE(8)
             CC.TOT = CC.TOT + JMS.COST
          END ELSE
             PLINE = PLINE:SPACE(9):OCONV(JMS.COST, "MD2")"R#7"
             CC.S.TOT = CC.S.TOT + JMS.COST
          END
          PLINE=PLINE:SPC:JMS.SPOIL.CODE"L#8":SPC:JMS.RC.JOB"L#8"
          PRINT PLINE
          LINE.CNT=LINE.CNT + 1
       NEXT S
    NEXT M
    RETURN
*
*-- PRINT COST CNTR TOTAL
*
2000*
    IF LINE.CNT >= 50 THEN GOSUB 9000
    PRINT SPACE(98):"-------- -------"
    PLINE=SPACE(3):"* ":P.CC"L#4":SPC:P.OP"L#4":SPC:OPER.DESC"L#52":SPACE(31):OCONV(CC.TOT,"MD2")"R#8":SPC:OCONV(CC.S.TOT,"MD2")"R#7"
    PRINT PLINE
    PRINT;PRINT
    LINE.CNT=LINE.CNT + 4
    FOR T = 1 TO 2
       DP.TOT.REC(T) = DP.TOT.REC(T) + CC.TOT.REC(T)
    NEXT T
    MAT CC.TOT.REC = ""
    RETURN
*
*-- PRINT DEPARTMENT TOTAL
*
3000*
    IF LINE.CNT >= 50 THEN GOSUB 9000
    PRINT SPACE(98):"-------- -------"
    PLINE="** ":DEPT.DESC:" DEPARTMENT TOTAL";PLINE=PLINE"L#97"
    PLINE=PLINE:SPC:OCONV(DP.TOT,"MD2")"R#8":SPC:OCONV(DP.S.TOT,"MD2")"R#7"
    PRINT PLINE
    PRINT;PRINT
    LINE.CNT=LINE.CNT + 4
    FOR T = 1 TO 2
       TOT.REC(T) = TOT.REC(T) + DP.TOT.REC(T)
    NEXT T
    MAT DP.TOT.REC = ""
    RETURN
*
*-- PRINT THE HEADINGS
*
9000*
    PRINT CHAR(12)
    LINE.CNT=0
    PG.NO=PG.NO + 1
    HLINE6=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):"PAGE: ":PG.NO
*T26556 v
*   PRINT HLINE4:HLINE5:HLINE6
*   PRINT JOB.CUST"L#8":SPACE(37):'D E T A I L   R E P O R T (MISCELLANEOUS)':SPACE(24):'#':JOBNO "L#8":" ":CONSOL
    PRINT HD1:PG.NO
    PRINT HD2
    PRINT "Job Number: ":JOBNO "L#8":SPACE(1):CONSOL
    PRINT JOB.CUST "L#8"
*T26556 ^
    PRINT C.CUST.NAME "L#35":SPACE(67):C.CUST.NAME
    *T28964   PRINT JOB.QTY<1,1> "L#7":SPACE(45):"FOR ITEMS THROUGH: ":OCONV(JRSD.DATE,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
    PRINT JOB.QTY<1,1> "L#8":SPACE(45):"FOR ITEMS THROUGH: ":OCONV(JRSD.DATE,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>;*T28964
    PRINT JOB.DESC<1,1> "L#30":SPACE(22):"DATE LAST ENTRY  : ":OCONV(JRSD.MS.DATE,"D2-")"R#8":SPACE(23):JOB.DESC<1,1>
    PRINT "SM: ":C.SALS.NAME "L#27":"SALES CODE: ":JOB.SALES.CODE
    PRINT
    PRINT HLINE
    PRINT HLINE1
    PRINT HLINE2
*T26556 v
*   LINE.CNT=LINE.CNT + 10
    LINE.CNT=LINE.CNT + 12
*T26556 ^
    RETURN
99999*
    PRINTER OFF
    RETURN
 END
