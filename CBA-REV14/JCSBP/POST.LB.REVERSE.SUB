      SUBROUTINE POST.LB.REVERSE.SUB(CONO)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>APS.CPYLIB>COM.APS.FILE.VARS
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - POST.LB.REVERSE.SUB
* BY          - WALID A. YAMOUT , C.B.A
* DESCRIPTION -
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUETE
*
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>DAILY.REVERSE
*COPY>JCS.CPYLIB>JOB.REVERSE
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JCS.CPYLIB>PROD.STATS
*COPY>JCS.CPYLIB>PERF.STATS
*COPY>JCS.CPYLIB>SPOIL.STATS
*COPY>JCS.CPYLIB>COST.CNTR.WIP
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
***** MAIN PROCESSING
*
      SEQ.CNT = COUNT(DRV.SEQ,VM) + (DRV.SEQ # "")
1000  FOR MLP = SEQ.CNT TO 1 STEP -1
         SEQ = DRV.SEQ<1,MLP>
         DEPT = DRV.DEPT<1,MLP>; CCTR = DRV.CCTR<1,MLP>
         LB.ID = CONO:DRV.JOB:"!":DEPT:"!":CCTR:"!"
         MATREAD JLB.REC FROM JOB.TIME, LB.ID : SEQ ELSE
            DRV.STATUS<1,MLP> = "CANNOT LOCATE JOB.TIME - ":LB.ID:SEQ
            GOTO 1999
         END
         BEGIN CASE
         CASE JLB.OPER # DRV.OPER<1,MLP>
            DRV.STATUS<1,MLP> = "OPERATION MISMATCH"
            GOTO 1999
         CASE JLB.EMP # DRV.EMP<1,MLP>
            DRV.STATUS<1,MLP> = "EMPLOYEE MISMATCH"
            GOTO 1999
         CASE (JLB.COST-JLB.RC.COST) = DRV.COST<1,MLP>
            REV.ALL = 1
         CASE (JLB.COST-JLB.RC.COST) > DRV.COST<1,MLP>
            REV.ALL = 0
         CASE 1
            DRV.STATUS<1,MLP> = "COST MISMATCH"
            GOTO 1999
         END CASE
         MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR ELSE
            DRV.STATUS<1,MLP> = 'CCTR # ':CCTR:' IS MISSING'
            GOTO 1999
         END
         LOCATE DRV.OPER<1,MLP> IN CCTR.OPER<1>,1 SETTING OLOC ELSE
            DRV.STATUS<1,MLP> = 'OPER # ':DRV.OPER<1,MLP>:' IS NOT IN CCTR # ':CCTR
            GOTO 1999
         END
         PTR = 1
         LOOP
            LOCATE DEPT IN JOB.LB.DEPT<1>,PTR SETTING DFND ELSE
               DFND = 0; PTR = 0
            END
            IF DFND THEN
               IF CCTR = JOB.LB.CCTR<1,DFND> THEN
                  PTR = 0
               END ELSE
                  PTR = DFND + 1
               END
            END
         WHILE PTR DO REPEAT
         IF DFND = 0 THEN
            DRV.STATUS<1,MLP> = "CANNOT LOCATE MAIN DEPT/CCTR/SEQ"
            GOTO 1999
         END
         WCNT = COUNT(JLB.MON,VM) + (JLB.MON # "")
         WPTR = 0; TOT.WIP = 0; INV.WIP = ""
         FOR I = 1 TO 4
            IF JLB.WIP<1,1,I> + 0 <> 0 THEN
               INV.WIP<1,1,I> = 0 - JLB.WIP<1,1,I>
               TOT.WIP = TOT.WIP + JLB.WIP<1,1,I>
               WPTR = I
            END
         NEXT I
         FOR W = 2 TO WCNT
            FOR I = 1 TO WPTR
               INV.WIP<1,1,I> = INV.WIP<1,1,I> - JLB.WIP<1,W,I>
            NEXT I
         NEXT W
         TOT.DCOST = 0; DPTR = 0
         FOR I = 1 TO 4
            IF JLB.DCOST<1,I> + 0 <> 0 THEN
               TOT.DCOST = TOT.DCOST + JLB.DCOST<1,I>
               DPTR = I
            END
         NEXT I
         IF TOT.DCOST <> JLB.COST THEN
            DRV.STATUS<1,MLP> = "COST/DCOST MISMATCH ":OCONV(TOT.DCOST,"MD2")
            GOTO 1999
         END
         TOT.RC.DCOST = ""; TOT.RC.COST = 0; TOT.RC.SALE = 0
         TOT.RC.HRS = 0; TOT.RC.IMP = 0; TOT.RC.WIP = 0
         TOT.RC.CNCR = 0; REV.WIP = ""
         IF JLB.RC.COST + 0 <> 0 THEN
            FOR S = SEQ + 1 TO JOB.LB.SEQ<1,DFND>
               MATREAD JLB.REC FROM JOB.TIME, LB.ID : S ELSE
                  DRV.STATUS<1,MLP> = "CANNOT LOCATE JOB.TIME - " : LB.ID : S
                  GOTO 1999
               END
               IF JLB.RC.TRAN = DRV.TRAN<1,MLP> THEN
                  FOR I = 1 TO WPTR
                     REV.WIP<1,1,I> = REV.WIP<1,1,I> - JLB.WIP<1,1,I>
                     TOT.RC.WIP = TOT.RC.WIP - JLB.WIP<1,1,I>
                  NEXT I
                  FOR I = 1 TO DPTR
                     TOT.RC.DCOST<1,I> = TOT.RC.DCOST<1,I> - JLB.DCOST<1,I>
                  NEXT I
                  TOT.RC.COST = TOT.RC.COST - JLB.COST
                  TOT.RC.SALE = TOT.RC.SALE - JLB.SALE
                  TOT.RC.HRS = TOT.RC.HRS - JLB.HRS
                  TOT.RC.IMP = TOT.RC.IMP - JLB.IMP
                  TOT.RC.CNCR = TOT.RC.CNCR - JLB.CNCR
               END
            NEXT S
            MATREAD JLB.REC FROM JOB.TIME, LB.ID : SEQ ELSE
               DRV.STATUS<1,MLP> = "CANNOT LOCATE JOB.TIME - " : LB.ID : SEQ
               GOTO 1999
            END
            IF TOT.RC.COST <> JLB.RC.COST THEN
               DRV.STATUS<1,MLP> = "REVERSE COST MISMATCH ":OCONV(TOT.RC.COST,"MD2")
               GOTO 1999
            END
         END
         NEW.WIP = ""; NEW.DCOST = ""
         IF REV.ALL THEN
            NEW.SALE = TOT.RC.SALE - JLB.SALE
            NEW.HRS = TOT.RC.HRS - JLB.HRS
            NEW.IMP = TOT.RC.IMP - JLB.IMP
            NEW.CNCR = TOT.RC.CNCR - JLB.CNCR
            STD.HRS = JLB.HRS.STD
            FOR I = 1 TO WPTR
               NEW.WIP<1,1,I> = REV.WIP<1,1,I> - JLB.WIP<1,1,I>
            NEXT I
            FOR I = 1 TO DPTR
               NEW.DCOST<1,I> = TOT.RC.DCOST<1,I> - JLB.DCOST<1,I>
            NEXT I
         END ELSE
            STD.HRS = 0
            PCT = DRV.COST<1,MLP> / (JLB.COST/1000)
            IF JLB.SALE = JLB.COST THEN
               NEW.SALE = 0 - DRV.COST<1,MLP>
            END ELSE
               NEW.SALE = 0 - INT(((JLB.SALE-JLB.COST)/(JLB.COST/1000)) * (DRV.COST<1,MLP>/1000) + DRV.COST<1,MLP> + .5)
               IF TOT.RC.SALE - NEW.SALE > JLB.SALE THEN
                  NEW.SALE = TOT.RC.SALE - JLB.SALE
               END
            END
            NEW.HRS = 0 - INT((JLB.HRS /1000) * PCT + .5)
            IF TOT.RC.HRS - NEW.HRS > JLB.HRS THEN
               NEW.HRS = TOT.RC.HRS - JLB.HRS
            END
            NEW.IMP = 0 - INT((JLB.IMP/1000) * PCT + .5)
            IF TOT.RC.IMP - NEW.IMP > JLB.IMP THEN
               NEW.IMP = TOT.RC.IMP - JLB.IMP
            END
            IF JLB.HRS = JLB.CNCR THEN
               NEW.CNCR = NEW.HRS
            END ELSE
               NEW.CNCR = TOT.RC.HRS - NEW.HRS - JLB.HRS + JLB.CNCR - TOT.RC.CNCR
               IF NEW.CNCR > 0 THEN
                  NEW.CNCR = 0 - NEW.CNCR
                  IF TOT.RC.CNCR - NEW.CNCR > JLB.CNCR THEN
                     NEW.CNCR = TOT.RC.CNCR - JLB.CNCR
                  END
               END ELSE
                  NEW.CNCR = 0
               END
            END
            TOT.DCOST = 0
            FOR I = 1 TO DPTR
               NEW.DCOST<1,I> = 0 - INT((JLB.DCOST<1,I>/1000) * PCT + .5)
               IF TOT.RC.DCOST<1,I> - NEW.DCOST<1,I> > JLB.DCOST<1,I> THEN
                  NEW.DCOST<1,I> = TOT.RC.DCOST<1,I> - JLB.DCOST<1,I>
               END
               TOT.DCOST = TOT.DCOST - NEW.DCOST<1,I>
            NEXT I
            DIFF = DRV.COST<1,MLP> - TOT.DCOST
            BEGIN CASE
            CASE DIFF = 0
            CASE DIFF > 0
               FOR I = DPTR TO 1 STEP -1 WHILE DIFF > 0
                  IF NEW.DCOST<1,I> < 0 - DIFF THEN
                     IF TOT.RC.DCOST<1,I>-NEW.DCOST<1,I>+DIFF <= JLB.DCOST<1,I> THEN
                        NEW.DCOST<1,I> = NEW.DCOST<1,I> - DIFF
                        TOT.DCOST = TOT.DCOST + DIFF
                        DIFF = 0
                     END
                  END
               NEXT I
            CASE 1
               FOR I = DPTR TO 1 STEP -1 WHILE DIFF < 0
                  IF NEW.DCOST<1,I> < DIFF THEN
                     NEW.DCOST<1,I> = NEW.DCOST<1,I> - DIFF
                     TOT.DCOST = TOT.DCOST + DIFF
                     DIFF = 0
                  END
               NEXT I
            END CASE
            IF DRV.COST<1,MLP> <> TOT.DCOST THEN
               DRV.STATUS<1,MLP> = "DCOST MISMATCH ":OCONV(TOT.DCOST,"MD2")
               GOTO 1999
            END
            FOR I = 1 TO WPTR
               IF REV.WIP<1,1,I> - NEW.DCOST<1,I> > JLB.WIP<1,1,I> THEN
                  NEW.WIP<1,1,I> = REV.WIP<1,1,I> - JLB.WIP<1,1,I>
               END ELSE
                  NEW.WIP<1,1,I> = NEW.DCOST<1,I>
               END
            NEXT I
         END
         ADD.MON = 0; WIP.CNTR = 0
         FOR I = 1 TO WPTR
            INV.WIP<1,1,I> = INV.WIP<1,1,I> + 0
            BEGIN CASE
            CASE INV.WIP<1,1,I> < NEW.WIP<1,1,I>
               INV.WIP<1,1,I> = NEW.WIP<1,1,I>
               ADD.MON = 1; WIP.CNTR = 3
            CASE INV.WIP<1,1,I> <> 0
               ADD.MON = 1; WIP.CNTR = 2
            CASE NEW.WIP<1,1,I> <> 0
               WIP.CNTR = 2
            END CASE
         NEXT I
         IF ADD.MON THEN
            IF JLB.MON<1,WCNT> > JRV.MON THEN
               NEW.MON = JLB.MON<1,WCNT>
            END ELSE
               NEW.MON = JRV.MON
            END
            WCNT = WCNT + 1
            JLB.MON<1,WCNT> = NEW.MON
            JLB.WIP<1,WCNT> = INV.WIP
            CCW.ID = CONO : JLB.DIV : DEPT : "!" : CCTR : NEW.MON
            MATREADU CCW.REC FROM COST.CNTR.WIP, CCW.ID ELSE MAT CCW.REC = ""
            FOR I = 1 TO WPTR
               CCW.LB.O<1,I> = CCW.LB.O<1,I> - INV.WIP<1,1,I>
            NEXT I
            MATWRITE CCW.REC ON COST.CNTR.WIP, CCW.ID
         END ELSE
            IF JLB.MON<1,1> > JRV.MON THEN
               NEW.MON = JLB.MON<1,1>
            END ELSE
               NEW.MON = JRV.MON
            END
         END
         IF WIP.CNTR THEN
            FOR I = 1 TO WPTR
               NEW.WIP<1,2,I> = 0 - NEW.WIP<1,1,I>
            NEXT I
            NEW.MON<1,2> = NEW.MON<1,1>
         END
         JLB.RC.COST = JLB.RC.COST + DRV.COST<1,MLP>
         BEGIN CASE
         CASE CCTR.OPER.TYPE<1,OLOC> = "U"
            BEGIN CASE
            CASE CCTR.OPER.STD.HRS<1,OLOC> + 0 # 0
               NEW.HRS.STD = INT(NEW.IMP * CCTR.OPER.STD.HRS<1,OLOC> / 100)
               NEW.IMP.STD = INT(NEW.HRS / CCTR.OPER.STD.HRS<1,OLOC> * 100)
            CASE CCTR.OPER.STD.IMP<1,OLOC> + 0 # 0
               NEW.HRS.STD = INT(NEW.IMP / CCTR.OPER.STD.IMP<1,OLOC>)
               NEW.IMP.STD = NEW.HRS * CCTR.OPER.STD.IMP<1,OLOC>
            CASE 1
               NEW.HRS.STD = 0
               NEW.IMP.STD = 0
            END CASE
         CASE CCTR.OPER.TYPE<1,OLOC> = "V"
            IF CCTR.OPER.STD.IMP<1,OLOC> + 0 # 0 THEN
               NEW.HRS.STD = INT(NEW.IMP / CCTR.OPER.STD.IMP<1,OLOC>)
               NEW.IMP.STD = NEW.HRS * CCTR.OPER.STD.IMP<1,OLOC>
            END ELSE
               NEW.HRS.STD = 0
               NEW.IMP.STD = 0
            END
         CASE 1
            NEW.HRS.STD = CCTR.OPER.STD.HRS<1,OLOC> + 0
            NEW.IMP.STD = 0
         END CASE
         MATWRITE JLB.REC ON JOB.TIME, LB.ID : SEQ
         JLB.RC.COST = 0
         JLB.HRS = NEW.HRS
         JLB.IMP = NEW.IMP
         JLB.CNCR = NEW.CNCR
         JLB.COST = 0 - DRV.COST<1,MLP>
         JLB.DCOST = NEW.DCOST
         JLB.SALE = NEW.SALE
         JLB.DATE = DRV.DATE
         JLB.MON = NEW.MON
         JLB.WIP = NEW.WIP
         JLB.HRS.STD = NEW.HRS.STD
         JLB.IMP.STD = NEW.IMP.STD
         JLB.RC.TRAN = DRV.TRAN<1,MLP>
         JLB.RC.JOB = DRV.JOB
         JLB.RC.DESC = DRV.REASON<1,MLP>
         IF WIP.CNTR THEN
            JOB.LB.WIP<1,1> = JOB.LB.WIP<1,1> + WIP.CNTR
            JOB.WIP<1,1> = JOB.WIP<1,1> + WIP.CNTR
            FOR I = 1 TO WPTR
               JOB.LB.WIP<1,2,I> = JOB.LB.WIP<1,2,I> + INV.WIP<1,1,I>
               JOB.LB.WIP<1,3,I> = JOB.LB.WIP<1,3,I> - INV.WIP<1,1,I> + JLB.WIP<1,1,I>
               JOB.WIP<1,2,I> = JOB.WIP<1,2,I> + INV.WIP<1,1,I>
               JOB.WIP<1,3,I> = JOB.WIP<1,3,I> - INV.WIP<1,1,I> + JLB.WIP<1,1,I>
            NEXT I
         END
         IF JLB.MON<1,1> < JOB.LB.WIP<1,4> OR JOB.LB.WIP<1,4> = "" THEN
            JOB.LB.WIP<1,4> = JLB.MON<1,1>
            IF JLB.MON<1,1> < JOB.WIP<1,4> OR JOB.WIP<1,4> = "" THEN
               JOB.WIP<1,4> = JLB.MON<1,1>
            END
         END
         IF JLB.MON<1,1> > JOB.LB.WIP<1,5> THEN
            JOB.LB.WIP<1,5> = JLB.MON<1,1>
            IF JLB.MON<1,1> > JOB.WIP<1,5> THEN
               JOB.WIP<1,5> = JLB.MON<1,1>
            END
         END
         FOR I = 1 TO DPTR
            JOB.LB.DCOST<1,DFND,I> = JOB.LB.DCOST<1,DFND,I> + JLB.DCOST<1,I>
         NEXT I
         JOB.LB.HRS<1,DFND> = JOB.LB.HRS<1,DFND> + JLB.HRS
         JOB.LB.IMP<1,DFND> = JOB.LB.IMP<1,DFND> + JLB.IMP
         JOB.LB.COST<1,DFND> = JOB.LB.COST<1,DFND> + JLB.COST
         JOB.LB.SALE<1,DFND> = JOB.LB.SALE<1,DFND> + JLB.SALE
         IF JLB.SHIFT + 0 = 0 THEN SHIFT = 1 ELSE SHIFT = JLB.SHIFT
         PRDS.ID = CONO:"*":JLB.DIV:"*":DEPT:"*":CCTR:"*":NEW.MON<1,1>:"*":SHIFT:"*A"
         MATREADU PRDS.REC FROM PROD.STATS, PRDS.ID ELSE MAT PRDS.REC = ""
         PSR.ID = CONO:"!":JLB.DIV:"!":DEPT:"!":CCTR:"!":JLB.OPER:"!":JLB.EMP:"!":NEW.MON<1,1>
         MATREADU PSR.REC FROM PERF.STATS, PSR.ID ELSE MAT PSR.REC = ""
         IF JLB.CNCR <> 0 THEN
            PSR.CNC.HRS = PSR.CNC.HRS + JLB.CNCR
            IF JLB.CNCR = JLB.HRS THEN
               JOB.LB.TCOST<1,DFND,5> = JOB.LB.TCOST<1,DFND,5> + JLB.COST
               PSR.CNC.IMP = PSR.CNC.IMP + JLB.IMP
            END ELSE
               PCT = JLB.CNCR / (JLB.HRS / 1000)
               JOB.LB.TCOST<1,DFND,5> = JOB.LB.TCOST<1,DFND,5> + INT((JLB.COST /1000) * PCT + .5)
               IF JOB.LB.TCOST<1,DFND,5> < 0 THEN JOB.LB.TCOST<1,DFND,5> = 0
               PSR.CNC.IMP = PSR.CNC.IMP + INT((JLB.IMP / 1000) * PCT + .5)
               IF PSR.CNC.IMP < 0 THEN PSR.CNC.IMP = 0
            END
         END
         BEGIN CASE
         CASE JLB.TYPE = "N"
            PTR = 2
            PRDS.LAB.NC.HRS = PRDS.LAB.NC.HRS + JLB.HRS
            PRDS.LAB.NC.COST = PRDS.LAB.NC.COST + JLB.COST
            PSR.N.C.HRS = PSR.N.C.HRS + JLB.HRS
            PSR.N.C.IMP = PSR.N.C.IMP + JLB.IMP
         CASE JLB.TYPE = "C"
            PTR = 3
            PRDS.LAB.HRS = PRDS.LAB.HRS + JLB.HRS
            PRDS.LAB.COST = PRDS.LAB.COST + JLB.COST
            PSR.CHG.HRS = PSR.CHG.HRS + JLB.HRS
            PSR.CHG.IMP = PSR.CHG.IMP + JLB.IMP
         CASE JLB.TYPE = "S"
            PTR = 4
            PRDS.LAB.SP.HRS = PRDS.LAB.SP.HRS + JLB.HRS
            PRDS.LAB.SP.COST = PRDS.LAB.SP.COST + JLB.COST
            PSR.N.C.HRS = PSR.N.C.HRS + JLB.HRS
            PSR.N.C.IMP = PSR.N.C.IMP + JLB.IMP
            IF JLB.SPOIL.CODE # "" THEN
               SSR.ID = CONO:JLB.DIV:DEPT:"!":CCTR:NEW.MON<1,1>:JLB.SPOIL.CODE
               MATREADU SSR.REC FROM SPOIL.STATS,SSR.ID ELSE MAT SSR.REC = ""
               FOR I = 1 TO DPTR
                  SSR.LB<1,I> = SSR.LB<1,I> + JLB.DCOST<1,I>
               NEXT I
               MATWRITE SSR.REC ON SPOIL.STATS,SSR.ID
            END
         CASE 1
            PTR = 1
            PRDS.LAB.HRS = PRDS.LAB.HRS + JLB.HRS
            PRDS.LAB.COST = PRDS.LAB.COST + JLB.COST
            PSR.CHG.HRS = PSR.CHG.HRS + JLB.HRS
            PSR.CHG.IMP = PSR.CHG.IMP + JLB.IMP
         END CASE
         BEGIN CASE
         CASE JLB.FCTR = "02"
            PRDS.LAB.OT.HRS = PRDS.LAB.OT.HRS + JLB.HRS
            PRDS.LAB.OT.COST = PRDS.LAB.OT.COST + JLB.COST
            PSR.O.T.HRS = PSR.O.T.HRS + JLB.HRS
         CASE JLB.FCTR = "03"
            PRDS.LAB.DT.HRS = PRDS.LAB.DT.HRS + JLB.HRS
            PSR.D.T.HRS = PSR.D.T.HRS + JLB.HRS
            PRDS.LAB.DT.COST = PRDS.LAB.DT.COST + JLB.COST
         END CASE
         MATWRITE PRDS.REC ON PROD.STATS, PRDS.ID
         IF JLB.HRS.STD + JLB.IMP.STD <> 0 THEN
            PSR.ACT.HRS = PSR.ACT.HRS + JLB.HRS
            PSR.STD.HRS = PSR.STD.HRS + JLB.HRS.STD
            PSR.ACT.IMP = PSR.ACT.IMP + JLB.IMP
            PSR.STD.IMP = PSR.STD.IMP + JLB.IMP.STD
         END
         PSR.DEPT = DEPT
         PSR.OPER = JLB.OPER
         MATWRITE PSR.REC ON PERF.STATS, PSR.ID
         JOB.LB.TCOST<1,DFND,PTR> = JOB.LB.TCOST<1,DFND,PTR> + JLB.COST
         JOB.TOT.DCOST = JOB.TOT.DCOST + JLB.DCOST<1,1>
         JOB.TOT.COST = JOB.TOT.COST + JLB.COST
         JOB.TOT.SALE = JOB.TOT.SALE + JLB.SALE
         READU JLB.SEQ FROM CONTROL, CONO:"DAILY.TIME" ELSE JLB.SEQ = 10001
         NEXT.SEQ = JLB.SEQ + 1
         WRITE NEXT.SEQ ON CONTROL,CONO:"DAILY.TIME"
         JOB.LB.SEQ<1,DFND> = JOB.LB.SEQ<1,DFND> + 1
         JLB.GLA.DATE = ""
         MATWRITE JLB.REC ON JOB.TIME, LB.ID : JOB.LB.SEQ<1,DFND>
         PPTR = COUNT(JRV.TRAN,VM) + (JRV.TRAN # "") + 1
         JRV.TRAN<1,PPTR> = DRV.TRAN<1,MLP>
         JRV.TTRAN<1,PPTR> = JLB.SEQ
         JRV.REASON<1,PPTR> = DRV.REASON<1,MLP>
         JRV.COST<1,PPTR> = DRV.COST<1,MLP>
         JRV.QTY<1,PPTR> = 0 - JLB.HRS
         JRV.SEQ<1,PPTR> = SEQ
         JRV.TSEQ<1,PPTR> = JOB.LB.SEQ<1,DFND>
         DRV.DEPT = DELETE(DRV.DEPT,1,MLP,0)
         DRV.CCTR = DELETE(DRV.CCTR,1,MLP,0)
         DRV.OPER = DELETE(DRV.OPER,1,MLP,0)
         DRV.EMP = DELETE(DRV.EMP,1,MLP,0)
         DRV.TRAN = DELETE(DRV.TRAN,1,MLP,0)
         DRV.REASON = DELETE(DRV.REASON,1,MLP,0)
         DRV.COST = DELETE(DRV.COST,1,MLP,0)
         DRV.STATUS = DELETE(DRV.STATUS,1,MLP,0)
         DRV.CONV = DELETE(DRV.CONV,1,MLP,0)
         DRV.QTY = DELETE(DRV.QTY,1,MLP,0)
         DRV.SEQ = DELETE(DRV.SEQ,1,MLP,0)
1999  NEXT MLP
99999 RETURN
   END
