*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JOB
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - JCSBP
* PROGRAM      - POST.SHIP
* BY           - JIHAD YAMOUT , C.B.A
* DESCRIPTION  - THIS PROGRAM POST JOBS BY MOVING DAILY.SHIP RECORD
*                TO JOB.FILE AND MOVING DAILY.SHIP RECORD TO JOB.SHIP
*                AND DELETE DAILY.SHIP RECORD.
*T23278 markt 11/20/1998 * Accumulate fiscal data by division.
*ENDDOC
*********************************************************************
*** INSERT FILE EQUETE
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>OPS.CPYLIB>JOB.FNGD.STATS
*COPY>JCS.CPYLIB>DAILY.SHIP
*COPY>JCS.CPYLIB>JOB.SHIP
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>TAX
*COPY>JCS.CPYLIB>JOB.STAT.CODE
*COPY>JCS.CPYLIB>WIP.LEVEL
*COPY>JCS.CPYLIB>COST.CNTR.WIP
*COPY>JCS.CPYLIB>SPOIL.STATS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
**** INTITIALIZATION
  MAT FILE.VARS = ''
  MAT JSI.REC = ""
  MAT EDIT.COM = ''
  MAT EDIT.COM.DRIVER = ''
  MAT JSP.REC = ''
  MAT COMP.REC = ''
*** SETUP ERRMSG ROUTINE
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** T23278 v
  PROCREAD XX ELSE ERRMSG = 'MUST BE RUN FROM A PROC'; GOTO 93000
  DIV.CODE = XX<5>
***** T23278 ^
*
*** OPEN FILES
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING'; GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING'; GOTO 93000
  OPEN '','JOB' TO JOB ELSE ERRMSG = 'JOB FILE MISSING'; GOTO 93000
  OPEN '','JOB.SHIP' TO JOB.SHIP ELSE ERRMSG = 'JOB.SHIP FILE MISSING'; GOTO 93000
  OPEN '','DAILY.SHIP' TO DAILY.SHIP ELSE ERRMSG = 'DAILY.SHIP FILE MISSING'; GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING'; GOTO 93000
  OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG = 'DEPARTMENT FILE MISSING'; GOTO 93000
  OPEN '','DIVISION' TO DIVISION ELSE ERRMSG = 'DIVISION FILE MISSING'; GOTO 93000
  OPEN '','OPERATION' TO OPERATION ELSE ERRMSG = 'OPERATION FILE MISSING'; GOTO 93000
  OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG = 'COST.CNTR FILE MISSING'; GOTO 93000
  OPEN '','SHIP.VIA' TO SHIP.VIA ELSE ERRMSG = 'SHIP-VIA FILE IS MISSING';GOTO 93000
  OPEN '','TAX' TO TAX ELSE ERRMSG = 'TAX FILE IS MISSING';GOTO 93000
  OPEN '','JOB.STAT.CODE' TO JOB.STAT.CODE ELSE ERRMSG = 'JOB.STAT.CODE FILE IS MISSING';GOTO 93000
  OPEN '','COST.CNTR.WIP' TO COST.CNTR.WIP ELSE ERRMSG = 'COST.CNTR.WIP FILE IS MISSING';GOTO 93000
  OPEN '','SPOIL.STATS' TO SPOIL.STATS ELSE ERRMSG = 'SPOIL.STATS FILE MISSING'; GOTO 93000
*** SETUP VALUES FROM PMC.SCREENS
  ECD.ACTION = 1
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME = 'GET.FISCAL.MONTH'
  CALL SCRN.EDIT
  ECD.SCRN.NO = 1
*** INT ECD.RT.VALUE AND PRINT SCREEN
  MAT SCV.REC = ""
  ECD.ACTION = 2
  CALL SCRN.EDIT
*** MAIN PROCESSING
  READNEXT DSP.ID ELSE GOTO 99999
  CONO = DSP.ID[1,3]
  MATREAD COMP.REC FROM COMPANY, CONO ELSE GOTO 99999
  IF CO.OPS = "Y" THEN
      OPEN "","JOB.FNGD.STATS" TO JOB.FNGD.STATS ELSE
          ERRMSG = "Cannot locate the JOB.FNGD.STATS file"
          GOTO 93000
      END
      OPS.ONFILE = 1
  END ELSE
      OPS.ONFILE = 0
  END
  HEADING = "SHIPMENT POSTING PROCESS"
*  CALL GET.FISCAL.MONTH(CONO,POSTING.MON,HEADING,"JCFISCAL",CURR.MON);* T23278
  CALL GET.DIV.FISCAL.MONTH(CONO,POSTING.MON,HEADING,"JCFISCAL",CURR.MON,DIV.CODE);* T23278
  IF POSTING.MON = "END" THEN GOTO 99999
  MATREAD WIP.LEVEL.REC FROM CONTROL, CONO : "WIP.LEVEL" ELSE
      MAT WIP.LEVEL.REC = ""
      WLR.SP.COST = 2; WLR.SP.WIP = 2; WLR.SP.FOH = 0
  END
  GOSUB 1000
  DATA = 1
  LOOP
      READNEXT DSP.ID ELSE DATA = 0
  WHILE DATA DO
      IF CONO = DSP.ID[1,3] THEN GOSUB 1000
  REPEAT
  GOTO 99999
1000 MAT SHIP.VIA.REC = ""
  MATREADU DSP.REC FROM DAILY.SHIP, DSP.ID ELSE
      RELEASE DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  IF DSP.POST = 'N' THEN
      RELEASE DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  MATREADU JOB.REC FROM JOB, CONO : DSP.JOB ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'JOB # ':DSP.JOB:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  IF OPS.ONFILE THEN
      MATREAD JFS.REC FROM JOB.FNGD.STATS, CONO:DSP.JOB THEN
          IF JFS.PROD # "" THEN
              RELEASE JOB, CONO:DSP.JOB
              DSP.STATUS = "Cannot Ship a Finished Goods JOB"
              MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
              GOTO 1999
          END
      END
  END
  BEGIN CASE
      CASE JOB.STATUS<1,1> = 9
          RELEASE JOB, CONO:DSP.JOB
          DSP.STATUS = 'JOB # ':DSP.JOB:' IS CANCELED'
          MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
          GOTO 1999
      CASE JOB.STATUS<1,1> = 7
          RELEASE JOB, CONO:DSP.JOB
          DSP.STATUS = 'JOB # ':DSP.JOB:' IS READY TO PURGE'
          MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
          GOTO 1999
      CASE JOB.STATUS<1,1> = 8
          RELEASE JOB, CONO:DSP.JOB
          DSP.STATUS = 'JOB # ':DSP.JOB:' IS PURGED'
          MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
          GOTO 1999
  END CASE
  IF JOB.TYPE # "R" AND JOB.TYPE # DSP.TYPE THEN
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = "JOB BELONG TO A DIFFERENT TYPE"
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  IF JOB.DIV # DSP.DIV THEN
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = "JOB BELONG TO DIFFERENT DIVISION"
      MATWRITE DSP.REC ON DAILY.SHIP,DSP.ID
      GOTO 1999
  END
  IF JOB.DEPT # DSP.DEPT[1,LEN(JOB.DEPT)] THEN
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = "JOB BELONG TO DIFFERENT DEPARTMENT"
      MATWRITE DSP.REC ON DAILY.SHIP,DSP.ID
      GOTO 1999
  END
  IF (JOB.STATUS<1,1> > 1 AND JOB.STATUS<1,1> # 5) OR JOB.TRACK.DATE<1,7> # "" OR JOB.TRACK.DATE<1,8> # "" THEN
      IF DSP.INIT = "" THEN
          RELEASE JOB, CONO:DSP.JOB
          DSP.STATUS = 'JOB # ':DSP.JOB:' STATUS IS NOT BOOKED, NOR IN PROCESS AND AUTHORIZATION REQUIRED'
          MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
          GOTO 1999
      END ELSE
          MATREAD JSI.REC FROM JOB.STAT.CODE,CONO:DSP.INIT ELSE
              RELEASE JOB, CONO:DSP.JOB
              DSP.STATUS = 'JOB # ':DSP.JOB:' STATUS IS NOT BOOKED, NOR IN PROCESS AND AUTHORIZATION INVALID'
              MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
              GOTO 1999
          END
      END
  END
  MATREAD DIV.REC FROM DIVISION, CONO : DSP.DIV ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'DIV # ':DSP.DIV:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  MATREAD DEPT.REC FROM DEPARTMENT, CONO : DSP.DEPT ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'DEPT # ':DSP.DEPT:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  LOCATE DSP.DEPT IN DIV.DEPT<1>,1 SETTING FND.DEPT ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'DEPT # ':DSP.DEPT:' IS NOT UNDER DIV # ':DSP.DIV
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  MATREAD CCTR.REC FROM COST.CNTR, CONO : DSP.CCTR ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'CCTR # ':DSP.CCTR:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  IF CCTR.DEPT # DSP.DEPT THEN
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'INCORRECT DEPARTMENT FOR CCTR # ':DSP.CCTR
      MATWRITE DSP.REC ON DAILY.SHIP,DSP.ID
      GOTO 1999
  END
  IF CCTR.TYPE='F' THEN
      RELEASE JOB,CONO:DSP.JOB
      DSP.STATUS = 'CCTR # ':DSP.CCTR:' IS FROZEN'
      MATWRITE DSP.REC ON DAILY.SHIP,DSP.ID
      GOTO 1999
  END
  LOCATE DSP.CCTR IN DEPT.CCTRS<1>,1 SETTING FND.CCTR ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'CCTR # ':DSP.CCTR:' IS NOT UNDER DEPT # ':DSP.DEPT
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  MATREAD OPER.REC FROM OPERATION, CONO : DSP.OPER ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'OPER # ':DSP.OPER:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  LOCATE DSP.OPER IN CCTR.OPER<1>,1 SETTING FND.OPER ELSE
      RELEASE JOB, CONO:DSP.JOB
      DSP.STATUS = 'OPER # ':DSP.OPER:' IS NOT IN CCTR # ':DSP.CCTR
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  MATREADU SHIP.VIA.REC FROM SHIP.VIA , CONO:DSP.VIA ELSE
      RELEASE JOB, CONO:DSP.JOB
      RELEASE SHIP.VIA , CONO:DSP.VIA
      DSP.STATUS = 'SHIP VIA # ':DSP.VIA:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  MATREAD TAX.REC FROM TAX , CONO:DSP.JURS ELSE
      RELEASE JOB, CONO:DSP.JOB
      RELEASE SHIP.VIA , CONO:DSP.VIA
      DSP.STATUS = 'TAX JURISTICTION # ':DSP.JURS:' IS MISSING'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  IF DSP.INV.AMT < 0 THEN
      RELEASE JOB, CONO:DSP.JOB
      RELEASE SHIP.VIA , CONO:DSP.VIA
      DSP.STATUS = 'CANNOT POST A NEGATIVE COST'
      MATWRITE DSP.REC ON DAILY.SHIP, DSP.ID
      GOTO 1999
  END
  JSP.DCOST = DSP.INV.AMT
  IF WLR.SP.COST < 2 THEN
      JSP.COST = JSP.DCOST
  END ELSE
      JSP.COST = INT((JSP.DCOST*(1 + WLR.SP.FOH/10000)) + .5)
  END
  IF DSP.TYPE = "S" OR DSP.TYPE = "N" THEN
      JSP.SALE = 0
  END ELSE
      JSP.SALE = INT(JSP.COST + ((JSP.COST * (CCTR.OPER.MARKUP<1,FND.OPER>/100))/100) + .5)
  END
  LOCATE POSTING.MON IN SHPV.PERIOD<1>,1 BY "DR" SETTING FND.PER ELSE
      INS POSTING.MON BEFORE SHPV.PERIOD<1,FND.PER>
      INS "0" BEFORE SHPV.SHPMT<1,FND.PER>
      INS "0" BEFORE SHPV.COST<1,FND.PER>
      INS "0" BEFORE SHPV.SALE<1,FND.PER>
  END
  SHPV.SHPMT<1,FND.PER> = SHPV.SHPMT<1,FND.PER> + 1
  SHPV.COST<1,FND.PER> = SHPV.COST<1,FND.PER> + JSP.DCOST
  JSP.SEQ = DSP.ID[4,99]
  JSP.TYPE = DSP.TYPE
  JSP.SPOIL.CODE = DSP.SPOIL.CODE
  JSP.DIV = DSP.DIV
  JSP.OPER = DSP.OPER
  JSP.SHIP.TO = DSP.SHIP.TO
  JSP.NAME = DSP.NAME
  JSP.ADDR1 = DSP.ADDR1
  JSP.ADDR2 = DSP.ADDR2
  JSP.ADDR3 = DSP.ADDR3:" ":DSP.ADDR4
  JSP.VIA = DSP.VIA
  JSP.JURS = DSP.JURS
  JSP.LAST = DSP.LAST
  JSP.QTY = DSP.QTY
  JSP.INV.DATE = DSP.INV.DATE
  JSP.DATE = DSP.DATE
  JSP.MON = POSTING.MON
  JSP.WIP = ""
  BEGIN CASE
      CASE WLR.SPL = "N" AND JSP.TYPE = "S"
      CASE WLR.NON.CHG = "N" AND JSP.TYPE = "N"
      CASE WLR.SP.WIP > 1
          JSP.WIP<1,1,1> = JSP.DCOST
          JSP.WIP<1,1,2> = JSP.COST - JSP.DCOST
      CASE WLR.SP.WIP > 0
          JSP.WIP<1,1,1> = JSP.DCOST
  END CASE
  JSP.FRT.NO = DSP.FRT.NO
  JSP.INV = DSP.INV
  JSP.DESC = DSP.DESC
  PTR = 1
  LOOP
      LOCATE DSP.DEPT IN JOB.SP.DEPT<1>,PTR SETTING DFND ELSE
          JOB.SP.DEPT<1,DFND> = DSP.DEPT
          JOB.SP.CCTR<1,DFND> = DSP.CCTR
      END
      IF DSP.CCTR = JOB.SP.CCTR<1,DFND> THEN
          PTR = 0
      END ELSE
          PTR = DFND + 1
      END
  WHILE PTR DO REPEAT
  SP.ID = CONO:DSP.JOB:"!":DSP.DEPT:"!":DSP.CCTR:"!"
  LOOP
      JOB.SP.SEQ<1,DFND> = JOB.SP.SEQ<1,DFND> + 1
      JSP.ID = SP.ID:JOB.SP.SEQ<1,DFND>
      READ DUMMY FROM JOB.SHIP, JSP.ID ELSE DUMMY = ""
  WHILE DUMMY # "" DO REPEAT
  IF JSP.DATE > JOB.SP.DATE<1,DFND,1> THEN
      JOB.SP.DATE<1,DFND,1> = JSP.DATE
  END
  IF JSP.SEQ > JOB.SP.DATE<1,DFND,2> THEN
      JOB.SP.OPER<1,DFND> = JSP.OPER
      JOB.SP.VIA<1,DFND> = JSP.VIA
      JOB.SP.DATE<1,DFND,2> = JSP.SEQ
      JOB.SP.DATE<1,DFND,3> = JOB.SP.SEQ<1,DFND>
  END
  JOB.SP.SALE<1,DFND> = JOB.SP.SALE<1,DFND> + JSP.SALE
  JOB.SP.COST<1,DFND> = JOB.SP.COST<1,DFND> + JSP.COST
  JOB.SP.DCOST<1,DFND> = JOB.SP.DCOST<1,DFND> + JSP.DCOST
  IF JSP.WIP # "" THEN
      CCW.ID = CONO:JSP.DIV:DSP.DEPT:"!":DSP.CCTR:JSP.MON
      MATREADU CCW.REC FROM COST.CNTR.WIP, CCW.ID ELSE MAT CCW.REC = ""
      JOB.WIP<1,1> = JOB.WIP<1,1> + 2
      JOB.SP.WIP<1,1> = JOB.SP.WIP<1,1> + 2
      FOR W = 1 TO WLR.SP.WIP
          JOB.WIP<1,2,W> = JOB.WIP<1,2,W> + JSP.WIP<1,1,W>
          JOB.SP.WIP<1,2,W> = JOB.SP.WIP<1,2,W> + JSP.WIP<1,1,W>
          CCW.SP.I<1,W> = CCW.SP.I<1,W> + JSP.WIP<1,1,W>
      NEXT W
      MATWRITE CCW.REC ON COST.CNTR.WIP, CCW.ID
  END
  IF JSP.MON < JOB.SP.WIP<1,4> OR JOB.SP.WIP<1,4> = "" THEN
      JOB.SP.WIP<1,4> = JSP.MON
      IF JSP.MON < JOB.WIP<1,4> OR JOB.WIP<1,4> = "" THEN
          JOB.WIP<1,4> = JSP.MON
      END
  END
  IF JSP.MON > JOB.SP.WIP<1,5> THEN
      JOB.SP.WIP<1,5> = JSP.MON
      IF JSP.MON > JOB.WIP<1,5> THEN
          JOB.WIP<1,5> = JSP.MON
      END
  END
  JOB.SP.QTY<1,DFND> = JOB.SP.QTY<1,DFND> + JSP.QTY
  BEGIN CASE
      CASE JSP.TYPE = 'N'
          PTR = 2
      CASE JSP.TYPE = 'C'
          PTR = 3
      CASE JSP.TYPE = 'S'
          PTR = 4
      CASE 1
          PTR = 1
          JSP.TYPE = "R"
  END CASE
  JOB.SP.TCOST<1,DFND,PTR> = JOB.SP.TCOST<1,DFND,PTR> + JSP.COST
  IF JOB.STATUS = "" THEN
      JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"1")
      JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
  END
  IF JOB.STATUS<1,1> # 1 THEN
      JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"5")
      JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
  END
  IF JSP.LAST = "Y" THEN JOB.TRACK.DATE<1,6> = JSP.DATE
  JOB.TOT.DCOST = JOB.TOT.DCOST + JSP.DCOST
  JOB.TOT.COST = JOB.TOT.COST + JSP.COST
  JOB.TOT.SALE = JOB.TOT.SALE + JSP.SALE
  JOB.QTY<1,2> = JOB.QTY<1,2> + DSP.QTY
  JOB.QTY<1,3> = JOB.QTY<1,3> + DSP.QTY
  IF JSP.SPOIL.CODE # "" THEN
      SSR.ID=CONO:JSP.DIV:DSP.DEPT:"!":DSP.CCTR:JSP.MON:JSP.SPOIL.CODE
      MATREADU SSR.REC FROM SPOIL.STATS,SSR.ID ELSE MAT SSR.REC = ""
      SSR.SP<1,1> = SSR.SP<1,1> + JSP.DCOST
      SSR.SP<1,2> = SSR.SP<1,2> + JSP.COST - JSP.DCOST
      MATWRITE SSR.REC ON SPOIL.STATS,SSR.ID
  END
  MATWRITE JOB.REC ON JOB, CONO : DSP.JOB
  MATWRITE JSP.REC ON JOB.SHIP, JSP.ID
  MATWRITE SHIP.VIA.REC ON SHIP.VIA, CONO:JSP.VIA
  DELETE DAILY.SHIP, DSP.ID
1999 RETURN
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
**** END OF JOB
99999 END
