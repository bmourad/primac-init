*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - DAILY.FNGD.PRT
* DATE        - 03/20/86
* BY          - JIHAD YAMOUT , C.B.A
* DESCRIPTION - This program list reject finish goods transaction.
*********************************************************************
*** INSERT EQUATE FROM CPYLIB
*COPY>JCS.CPYLIB>DAILY.FNGD.REC
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*** INITIALIZATION
      LINE.COUNT = 99 ; PAGE.NO = 0
      SP1 = SPACE(1)
      T.LINE = ""
      T.LINE = "------ -------- -------- --------------- --- ---- ---- ----------- ----------- -------- ------------------------------"
      HD.LINE = ""
      HD.LINE = "TRAN # JOB NUM  REC DATE PRODUCT NUMBER  U/M WHSE LOC# ROLL/SKID #  QTY / U-M  PRT DATE        NO POST STATUS"
*** SET UP FOR SYSCOM
      SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
*** OPEN FILES
      OPEN '', 'INVENTORY' TO INVENTORY ELSE
         ERRMSG = 'INVENTORY FILE IS MISSING'
         GOTO 93000
      END
      OPEN '', 'DAILY.FNGD.REC' TO DAILY.FNGD ELSE
         ERRMSG = 'DAILY.FNGD.REC FILE IS MISSING'
         GOTO 93000
      END
*** GET PROC VALUES
      PROCREAD ID ELSE
         ERRMSG = ' CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      NO.DATA = 1
      CONO = ID<1>
      CONO.NAME = ID<2>
      LIST.TYPE = ID<5>
      ST.DATE = ID<3>
      ED.DATE = ID<4>
      BEGIN CASE
      CASE LIST.TYPE = "N"
         LIST.TYPE = "(NEW)"
      CASE LIST.TYPE = "R"
         LIST.TYPE = "(REJECT)"
      CASE 1
         LIST.TYPE = "(ALL)"
      END CASE
      REPORT.NAME = "FINISHED GOODS RECEIPTS EDIT LISTING ":LIST.TYPE
      REPORT.NUMBER = "XXXX"
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
*** GET HEADING
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*** PRINTER ON
      PRINTER ON
*** READ AND PROCESS ALL THE FILES
      DATA = 0
      LOOP
         READNEXT KEY ELSE
            DATA = 1
            IF NO.DATA = 1 THEN
               GOSUB 1000
               PRINT;PRINT
               PRINT "NO ITEMS PRESENT"
            END
         END
      WHILE DATA = 0 DO
         NO.DATA = 0
         MATREADU DFG.REC FROM DAILY.FNGD , KEY ELSE
            MAT DFG.REC = ""
            RELEASE DAILY.FNGD , KEY
            GOTO 111
         END
         GOSUB 3000
111*
      REPEAT
*** END OF JOB
      PRINTER OFF
      GOTO 999999
*** PRINT HEADINGS
1000*
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT SPACE(63):ST.DATE:" THROUGH ":ED.DATE
      PRINT
      PRINT HD.LINE
      PRINT T.LINE
      LINE.COUNT = 6
      RETURN
***   PRINT THE RECORD
3000*
      MATREAD INV.REC FROM INVENTORY, CONO:DFG.PROD ELSE MAT INV.REC = ""
      GOSUB 6000
      LOC.CNT = COUNT(DFG.LOC, VM) + (DFG.LOC # "")
      FOR PR1 = 1 TO LOC.CNT
         IF LINE.COUNT > 53 THEN
            PRT.FLG = 1 ; GOSUB 1000
         END
         IF PRT.FLG = 1 OR PR1 = 1 THEN
            H.LINE = KEY[4,6] "R#6":SP1
            H.LINE = H.LINE :DFG.JOB "L#8":SP1
            H.LINE = H.LINE :OCONV(DFG.DATE, "D2/") "R#8":SP1
            H.LINE = H.LINE :DFG.PROD "L#15":SP1
            H.LINE = H.LINE :INV.UNIT<1,2> "L#3":SP1
            H.LINE = H.LINE :DFG.WHSE "L#4":SP1
            H.LINE = H.LINE :DFG.LOC<1,PR1> "L#4":SP1
            PRT.FLG = 0
         END ELSE
            H.LINE = SPACE(50)
            IF DFG.LOC<1,PR1> # DFG.LOC<1,PR1-1> THEN
               H.LINE = H.LINE :DFG.LOC<1,PR1> "L#4":SP1
            END ELSE
               H.LINE = H.LINE :SPACE(5)
            END
         END
         H.LINE = H.LINE :DFG.R.S.ID<1,PR1> "L#11":SP1
         IF DFG.QTY<1,PR1> < 0 THEN
            ADDQ = "-.5"
         END ELSE
            ADDQ = .5
         END
         PRT.QTY = INT(((DFG.QTY<1,PR1> / ICR.DV1) * ICR.MT1) / ICR.DV2 + ADDQ)
         H.LINE = H.LINE :OCONV(PRT.QTY, ICR.CNV) "R#11":SP1
         H.LINE = H.LINE :OCONV(DFG.PRT.DATE, "D2/") "R#8":SP1
         H.LINE = H.LINE :DFG.STATUS<1,PR1> "L#30"
         PRINT H.LINE
         LINE.COUNT = LINE.COUNT + 1
      NEXT PR1
      IF DFG.PRT.DATE = "" THEN
         DFG.PRT.DATE = DATE()
         MATWRITE DFG.REC ON DAILY.FNGD , KEY
      END ELSE
         RELEASE DAILY.FNGD, KEY
      END
      IF LINE.COUNT + 1 < 53 THEN
         PRINT
         LINE.COUNT = LINE.COUNT + 1
      END
      RETURN
**** PRINT SUB LINE
***        CONVERT SHEET/WGHT/QTY
6000*
      MAT INV.CNV.REC = ''
      BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2"
         ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
      END CASE
      RETURN
***   CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999*
      PRINTER OFF
   END
