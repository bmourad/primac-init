*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* SOURCE         - JCSBP
*
* PROGRAM        - SF.PROMPT.MAINT
*
* BY
*
* DATE           - 04/15/94
*
* DESCRIPTION    -
* This program is used to maintain the SF.PROMPT file.
*
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>SF.PROMPT
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'JCS.SCREENS FILE MISSING';GOTO 93000
  OPEN '','SF.PROMPT' TO SF.PROMPT ELSE ERRMSG = 'SF.PROMPT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  MAT EDIT.COM = ""
  MAT EDIT.COM.DRIVER = ""
  ERRMSG = ""
*
*---- GET COMPANY NUMBER
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  MAT EDIT.COM.DRIVER = ""
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'SF.PROMPT.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  SF.PROMPT.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'E' OR ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.SF.PROMPT = "NO"
      * T26126 v
      MATREADU SFP.REC FROM SF.PROMPT,CONO:SF.PROMPT.KEY LOCKED
        ERRMSG = 'Record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
      * T26126 ^
        MAT SFP.REC=''
        NEW.SF.PROMPT = 'YES'
      END
  END CASE
  IF NEW.SF.PROMPT = 'YES' THEN 
    FOR S = 1 TO 5
      ON S GOSUB 10,20,30,40,50
      IF ECD.RET.VALUE = 'END' THEN 
        RELEASE SF.PROMPT,CONO:SF.PROMPT.KEY
        GOSUB 300;GOTO 5
      END
    NEXT S
  END ELSE
    GOSUB 100
  END
  GOSUB 200
  GOSUB 300
  GOTO 5
*
*---- ENTER PROMPT (DESCRIPTION)
*
10*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SFP.PROMPT = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER RESPONSE DATA TYPE
*
20*
  ECD.NUM = 3
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SFP.RESP.TYPE = ECD.RET.VALUE
    IF SFP.RESP.TYPE = 6 THEN
      SFP.RESP.LEN = 8
      SCV.REC(4)<ECD.SCRN.NO> = SFP.RESP.LEN
      ECD.NUM = 4
      ECD.ACTION = 5; CALL SCRN.EDIT
    END
    IF SFP.RESP.TYPE = 8 THEN
      SFP.RESP.LEN = 1
      SCV.REC(4)<ECD.SCRN.NO> = SFP.RESP.LEN
      ECD.NUM = 4
      ECD.ACTION = 5; CALL SCRN.EDIT
    END
  END
  RETURN
*
*---- ENTER MAX LENGTH
*
30*
  IF SFP.RESP.TYPE = 6 OR SFP.RESP.TYPE = 8 THEN RETURN
  ECD.NUM = 4
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SFP.RESP.LEN = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER SCALER POSITIONS(IF RESP TYPE IS 4)
*
40*
  ECD.NUM = 5
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SFP.RESP.SCALER = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER VALID DATA RESPONSES
*
50*
  ECD.NUM = 6
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SFP.VALDAT = ECD.RET.VALUE
  END 
  RETURN
*
*---- GET VALUES FROM FILE
*
100*
  SCV.REC(2)<ECD.SCRN.NO> = SFP.PROMPT
  SCV.REC(3)<ECD.SCRN.NO> = SFP.RESP.TYPE
  SCV.REC(4)<ECD.SCRN.NO> = SFP.RESP.LEN
  SCV.REC(5)<ECD.SCRN.NO> = SFP.RESP.SCALER
  SCV.REC(6)<ECD.SCRN.NO> = SFP.VALDAT
  ECD.ACTION = 3;CALL SCRN.EDIT
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 0
  LOOP
    ECD.NUM = 20
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE SF.PROMPT,CONO:SF.PROMPT.KEY   ;* T26126
        MORE = 1
      CASE NUM(OPTION)
        ON OPTION GOSUB 10,20,30,40,50
*        CASE OPTION = "D"
*         ECD.NUM=9 ; CALL SCRN.EDIT
*         IF ECD.RET.VALUE = 'Y' THEN
*           DELETE SF.PROMPT,CONO:SF.PROMPT.KEY
*           MORE = 1
*          END
      CASE OPTION = 'F'
        IF LEN(SFP.PROMPT) + SFP.RESP.LEN > 38 THEN
          ERRMSG = "Combined length of Message and Maximum length cannot exceed 38 characters."
          GOSUB 91000
        END ELSE
          GOSUB 400
          MORE = 1
        END
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE SF.PROMPT FILE
*
400*
  MATWRITE SFP.REC ON SF.PROMPT,CONO:SF.PROMPT.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
