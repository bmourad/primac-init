*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - PNP.ACCESS.MAINT
* AUTHOR      - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 11/12/84
* DESCRIPTION
* This program will be used to update and maintain PNP.ACCESS file.
*T21177 diane 10/30/1997 * REV11 UPG Prompt for password
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>PNP.ACCESS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
  OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
  OPEN "","JCS.SCREENS" TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE MISSING";GOTO 93000
  OPEN "","PMC.SCREENS" TO SECOND.SCREENS ELSE ERRMSG = "PMC.SCREENS FILE MISSING";GOTO 93000
  OPEN "","PNP.ACCESS" TO PNP.ACCESS ELSE ERRMSG = "PNP.ACCESS FILE MISSING";GOTO 93000
  OPEN "","EMPLOYEE" TO EMPLOYEE ELSE ERRMSG = "EMPLOYEE FILE MISSING";GOTO 93000
*
*---- INITIALIZATION
*
* MAT SEC.REC = ""
  MAT SCV.REC = ""
  KW.PW = "VERCOM"
  READ CURR.PW FROM CONTROL, "PNP.PW" ELSE
    NEW.PW = "PW"
    CALL ENCODE.SUB("E",KW.PW,NEW.PW,CURR.PW)
  END
  CALL ENCODE.SUB("D",KW.PW,CURR.PW,PNP.PW)
  MAT EDIT.COM = ""
  MAT EDIT.COM.DRIVER = ""
  MAT PNPA.REC = ""
  ERRMSG = ""
*
*---- GET COMPANY NUMBER
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 2
  ECD.SCRN.NAME<1> = "PNP.ACCESS.MAINT"
  ECD.SCRN.NAME<2> = "SEC.PASSWORD"
  ECD.SCRN.FLAG<2> = 2
  ECD.ACTION = 1; CALL SCRN.EDIT
5*
  GOSUB 2100
*PRINT @(0,23):'ECD.RET.VALUE = (':ECD.RET.VALUE:')': ; INPUT ACK
  IF ECD.RET.VALUE = "END" THEN GOTO 99999
  CURR.PW = FIELD(ECD.RET.VALUE,"/",1)
  NEW.PW = FIELD(ECD.RET.VALUE,"/",2)
  BEGIN CASE
    CASE CURR.PW # PNP.PW
      ERRMSG = "** Illegal Password **"
      GOSUB 91000
      GOTO 99999
    CASE CURR.PW = "PW" AND (NEW.PW = "" OR NEW.PW = CURR.PW)
      ERRMSG = "** Please change the current password **"
      GOSUB 91000; GOTO 5
  END CASE
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*      ECD.NUM=8;ECD.ACTION=4;CALL SCRN.EDIT
*      IF ECD.RET.VALUE="END" THEN GOTO 99999
*
*---- MAIN PROCESSING
*
10*
  SCV.REC(7)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=7
  ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM=1;ECD.ACTION=4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 99999
    CASE ECD.RET.VALUE # ""
      EMP.NUM = ECD.RET.VALUE
      MATREAD EMP.REC FROM EMPLOYEE,CONO:EMP.NUM ELSE
        ERRMSG = "Invalid Employee ID. Try again! "
        GOSUB 91000;GOSUB 300;GOTO 10
      END
      EMP.NAME = EMP.FRST.NAME:" ":EMP.LAST.NAME
      SCV.REC(2)<ECD.SCRN.NO,1> = EMP.NAME
      ECD.NUM = 2;ECD.ACTION=5;CALL SCRN.EDIT
      NEW.PNP.ACCESS = "NO"
      * T26126 v
      MATREADU PNPA.REC FROM PNP.ACCESS,EMP.NUM LOCKED
        ERRMSG = 'PNP ACCESS record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE
      * T26126 ^
        MAT PNPA.REC = ""
        NEW.PNP.ACCESS = "YES"
      END
  END CASE
  IF NEW.PNP.ACCESS = "YES" THEN
    FOR S = 1 TO 3
      ON S GOSUB 20,30,40
      IF ECD.RET.VALUE = "END" THEN
        GOSUB 300;GOTO 10
      END
    NEXT S
  END ELSE
    GOSUB 100
    ECD.ACTION = 3; CALL SCRN.EDIT
  END
  GOSUB 200
  GOSUB 300
  GOTO 10
*
*---- ENTER EMPLOYEE PASSWORD
*
20*
  ECD.NUM=3;ECD.ACTION=4;CALL SCRN.EDIT
  PNPA.PWD=ECD.RET.VALUE
  RETURN
*
*---- ENTER Y / N FOR TIME ENTRY FACILITY
*
30*
  ECD.NUM=4;ECD.ACTION=4;CALL SCRN.EDIT
  PNPA.TIME=ECD.RET.VALUE
  RETURN
*
*---- ENTER Y / N FOR MATERIAL ENTRY FACILITY
*
40*
  ECD.NUM=5;ECD.ACTION=4;CALL SCRN.EDIT
  PNPA.MATL=ECD.RET.VALUE
  RETURN
*
*---- GET ALL VALUES FROM FILE
*
100*
  SCV.REC(3)<ECD.SCRN.NO,1> = PNPA.PWD
  SCV.REC(4)<ECD.SCRN.NO,1> = PNPA.TIME
  SCV.REC(5)<ECD.SCRN.NO,1> = PNPA.MATL
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 1
  LOOP
    ECD.NUM = 6
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "END" OR OPTION = "E"
        RELEASE
        MORE = 0
      CASE NUM(OPTION)
        ON OPTION GOSUB 20,30,40
        MORE = 1
      CASE OPTION = "D"
        ECD.NUM=9;ECD.ACTION=4;CALL SCRN.EDIT
        IF ECD.RET.VALUE = "Y" THEN
          DELETE PNP.ACCESS, EMP.NUM
          RELEASE
          MORE = 0
        END
      CASE OPTION = "F"
        MATWRITE PNPA.REC ON PNP.ACCESS,EMP.NUM
        MORE = 0
    END CASE
  WHILE MORE = 1 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- GET PASSWORD
*
2100*
*   ECD.NUM = 11
*   ECD.ACTION=4;CALL SCRN.EDIT
  ECD.SCRN.NO = 2
  CALL SEC.PASSWORD(ERROR)
* ECD.SCRN.NO = 1
* ESN = 1
* ECD.ACTION = 2
* CALL SCRN.EDIT
  ECD.RET.VALUE = ERROR
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*          PRINT @(-1)
END
