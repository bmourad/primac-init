*COPY>CPYLIB>COM1
**********************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - INP.COST.RPT
* DATE        - SEP, 29, 83
* DESCRIPTION - THIS PROGRAM GET JOB FILE AND PRINT
*               LAST WORK INFORMATION AND WORK INPROCESS
*
* TASK
*     18573 5/95 LLH 1-52 WEEK ACCOUNTING
*
* T22007 stefanie 08/26/1997 * Add Division Code Prompt.
*T22573 lanny 02/05/1998 * Error found on REV9 not carried to 10B.
*T22622 renee 02/18/1998 * Showing up as invoiced on the report (a "*"
*                          displays before the master job number) when
*                          the invoice had not yet been posted.
*T26493 cmykleb 05/09/2002 * Have pgm get the rpt # from the proc and
*                            use GET.PROG.HEAD for the heading.
************************************************
*ENDDOC
*--- INSERT FILE EQUATE
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>JCS.CPYLIB>JOB.OSP
*COPY>JCS.CPYLIB>JOB.SHIP
*COPY>JCS.CPYLIB>JOB.MISC
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*
   DIM LINE.REC(16)
   EQU LINE.MASTER TO LINE.REC(1)
   EQU LINE.STAR   TO LINE.REC(2)
   EQU LINE.JOB    TO LINE.REC(3)
   EQU LINE.CUST   TO LINE.REC(4)
   EQU LINE.DATE   TO LINE.REC(5)
   EQU LINE.DIV    TO LINE.REC(6)
   EQU LINE.DEPT   TO LINE.REC(7)
   EQU LINE.CCTR   TO LINE.REC(8)
   EQU LINE.OPER   TO LINE.REC(9)
   EQU LINE.LB     TO LINE.REC(10)
   EQU LINE.MT     TO LINE.REC(11)
   EQU LINE.OS     TO LINE.REC(12)
   EQU LINE.SP     TO LINE.REC(13)
   EQU LINE.MS     TO LINE.REC(14)
   EQU LINE.TOTAL  TO LINE.REC(15)
   EQU LINE.MAST.S TO LINE.REC(16)
*
*--- SET UP ERRMSG ROUTINE FOR CPYLIB SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*--- INITALIZATION
*
   MAT EDIT.COM = ""
   MAT EDIT.COM.DRIVER = ""
   MAT COMP.REC = ""
   MAT FILE.VARS = ""
   MAT SCV.REC = ""
*
   TOTALS = 0
   TOTAL.LABOR.COST = 0
   TOTAL.MATL.COST = 0
   TOTAL.OSP.COST = 0
   TOTAL.SHIP.COST = 0
   TOTAL.MISC.COST = 0
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   SP4 = SPACE(4)
   D2 = STR('-',2)
   D3 = STR('-',3)
   D4 = D2:D2
   D5 = D2:D3
*
*---  OPEN FILE
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"; GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOTO 93000
   OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = "PMC.SCREENS FILE MISSING"; GOTO 93000
   OPEN '','JOB' TO JOB ELSE ERRMSG = "JOB FILE IS MISSING";GOTO 93000
   OPEN '','JOB.TIME' TO JOB.TIME ELSE ERRMSG = "JOB.TIME FILE IS MISSING";GOTO 93000
   OPEN '','JOB.MATL' TO JOB.MATL ELSE ERRMSG = "JOB.MATL FILE IS MISSING";GOTO 93000
   OPEN '','JOB.OSP' TO JOB.OSP ELSE ERRMSG = "JOB.OSP FILE IS MISSING";GOTO 93000
   OPEN '','JOB.SHIP' TO JOB.SHIP ELSE ERRMSG = "JOB.SHIP FILE IS MISSING";GOTO 93000
   OPEN '','JOB.MISC' TO JOB.MISC ELSE ERRMSG = "JOB.MISC FILE IS MISSING";GOTO 93000
*
*--- GET COMPANY
*
   MAT COMP.REC = ""; CONO = ""
   CALL GET.CONO(CONO, MAT COMP.REC)
*
*--- GET FISCAL PERIOD TO REPORT
*
   PROCREAD BUFFER ELSE 
      BUFFER = ""
      GOTO 999999
   END
   MENU = BUFFER<3>
   DIVISION  = BUFFER<4>   ;* T22007 <
*
*
*--- SET UP HEADERS
*
*T26493 v
*  POS = 51 - INT(LEN(CO.NAME)/2)
*  POS2 = 70 - POS
*  HEAD1 = "DATE - ":OCONV(DATE(), 'D2/') "L#8"
*  HEAD1 = HEAD1 :SPACE(POS) :CO.NAME "L#30"
*  HEAD1 = HEAD1 :SPACE(POS2) : "PAGE : 'PL'"
*  HEAD2 = "DIVISION: ":DIVISION
*  HEAD2 = HEAD2:SPACE(30) :"WORK IN PROCESS FOR PRODUCTION COST FOR "
*  IF MENU = "CURRENT" THEN
*     HEAD2 = HEAD2 : MENU : " PERIOD" : "'LL'"
*  END ELSE
*     HEAD2 = HEAD2 : "PERIOD " : MENU[5,2] : SP2 : MENU[1,4] : "'LL'"
*  END
   CONO.NAME = ""
   RPT.NAME = ""
   RPT.NO = BUFFER<2>
   HEAD0 = ""
   HEAD1 = ""
   HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NO,"",HEAD0,HEAD1)
   HEAD0 = HEAD0 : "'PL'"
   HEAD2 = "DIVISION: ":DIVISION:SPACE(53)
   IF MENU = "CURRENT" THEN
      HEAD2 = HEAD2:"FOR ":MENU:" PERIOD 'LL'"
   END ELSE
      HEAD2 = HEAD2:"FOR PERIOD ":MENU[5,2]:SP2:MENU[1,4]:"'LL'"
   END
*T26493 ^
   HEAD3 = SPACE(30):"<-- LAST WORKING INFORMATION ->" :SP4
   HEAD3 = HEAD3 : "<----------------- COST INFORMATION ---------------->":"'L'"
   HEAD4 = " MAST JOB":SP3:"SUB JOB":SP3:"CUST #":SP2:"  DATE    DV  DEPT.  CCTR  OPER"
   HEAD4 = HEAD4 :SP4:"  LABOR     MATERIAL  O/S PURCH   SHIPPING    MISC       TOTAL":"'L'"
   HEAD5 = SP1: D4:D4 :SP3: D4:D4 :SP2: D3:D3 :SP2: D4:D4 :SP2:D2 :SP2: D5 :SP2: D4 :SP2: D4 :SP4: D4:D5
   HEAD5 = HEAD5 :SP2: D4:D5 :SP2: D4:D5 :SP2: D4:D5 :SP2: D4:D5 :SP2: D5:D5:"'L'"
   H.LINE = ''
   H.LINE = H.LINE :SPACE(64) : D5:D5 :SP1: D5:D5 :SP1: D5:D5 :SP1: D5:D5
   H.LINE = H.LINE :SP1: D5:D5 :SP1: D5:D3:D3
   PRINTER ON
*T26493 v
*  HEADING HEAD1:HEAD2:HEAD3:HEAD4:HEAD5
   HEADING HEAD0:HEAD1:HEAD2:HEAD3:HEAD4:HEAD5
*T26493 ^
*
*---  READING ALL FILES AND PROCESS ONE AT A TIME
*
   FIRST = 0
   PURGE.STATUS = 7
   PREV.MASTER = ""
100*
   READNEXT ID ELSE
      IF FIRST THEN GOSUB 5000
      GOTO 111
   END
   KEY = ID[4,99]
   MATREAD JOB.REC FROM JOB , ID ELSE GOTO 100
   LOCATE PURGE.STATUS IN JOB.STATUS<1>,1 SETTING DUMMY ELSE DUMMY = 0
   IF DUMMY # 0 THEN GOTO 100
   IF JOB.MASTER = "" THEN JOB.MASTER = KEY
   IF FIRST THEN
      IF PREV.MASTER # JOB.MASTER THEN GOSUB 5000
   END ELSE
      GOSUB 5500
   END
   ICNT = COUNT(JOB.INV.NO,VM) + (JOB.INV.NO # "")
   STAR = " "
   FOR QQ = 1 TO ICNT UNTIL STAR = "*"
*T22573 v
*    IF JOB.INV.NO[LEN(JOB.INV.NO)-1,2] # "CM" AND JOB.INV.NO[LEN(JOB.INV.NO)-1,2] # "DM" THEN STAR = "*"
*T22622     IF JOB.INV.NO<1,QQ>[LEN(JOB.INV.NO<1,QQ>)-1,2] # "CM" AND JOB.INV.NO<1,QQ>[LEN(JOB.INV.NO<1,QQ>)-1,2] # "DM" THEN STAR = "*"
*T22573 ^
* T22622 v Check invoice date to verify invoice has been posted
      IF JOB.INV.NO<1,QQ>[LEN(JOB.INV.NO<1,QQ>)-1,2] # "CM" AND JOB.INV.NO<1,QQ>[LEN(JOB.INV.NO<1,QQ>)-1,2] # "DM" THEN
         IF JOB.INV.DATE<1,QQ> NE "" THEN STAR = "*"
      END
* T22622 ^
   NEXT QQ
   IF KEY = JOB.MASTER THEN LINE.MAST.S = STAR
   CNT = COUNT(JOB.LB.DEPT,VM) + (JOB.LB.DEPT # "")
   LST = 1
   FOR QQ = 2 TO CNT
      IF JOB.LB.DATE<1,QQ,1> > JOB.LB.DATE<1,LST,1> THEN LST = QQ
   NEXT QQ
   IF MENU = "CURRENT" THEN
      GOSUB 3000
   END ELSE
      GOSUB 4000
   END
   GOTO 100
111*
*--- PRINTING THE TOTAL VALUES
   PRINT H.LINE
   T.LINE = ''
   T.LINE = T.LINE : ' TOTAL COST VALUES FOR ALL JOBS :' :SPACE(31)
   T.LINE = T.LINE : OCONV(TOTAL.LABOR.COST, 'MD2') "R#10" :SP1
   T.LINE = T.LINE : OCONV(TOTAL.MATL.COST, 'MD2') "R#10" :SP1
   T.LINE = T.LINE : OCONV(TOTAL.OSP.COST, 'MD2') "R#10" :SP1
   T.LINE = T.LINE : OCONV(TOTAL.SHIP.COST, 'MD2') "R#10" :SP1
   T.LINE = T.LINE : OCONV(TOTAL.MISC.COST, 'MD2') "R#10" :SP1
   T.LINE = T.LINE : OCONV(TOTALS, 'MD2') "R#11"
   PRINT T.LINE
*--- END OF JOB
   PRINTER OFF
   GOTO 999999
*--- PROCESS THE RECORD FOR CURRENT
3000*
* IF JOB.WIP<1,1> < 1 THEN GOTO 3999
   WIP = JOB.WIP<1,2,1> + JOB.WIP<1,2,2> + JOB.WIP<1,2,3> + JOB.WIP<1,2,4>
   IF WIP = 0 THEN GOTO 3999
   LB.WIP = JOB.LB.WIP<1,2,1> + JOB.LB.WIP<1,2,2> + JOB.LB.WIP<1,2,3> + JOB.LB.WIP<1,2,4>
   MT.WIP = JOB.MT.WIP<1,2,1> + JOB.MT.WIP<1,2,2> + JOB.MT.WIP<1,2,3>
   OS.WIP = JOB.OS.WIP<1,2,1> + JOB.OS.WIP<1,2,2>
   SP.WIP = JOB.SP.WIP<1,2,1> + JOB.SP.WIP<1,2,2>
   MS.WIP = JOB.MS.WIP<1,2,1> + JOB.MS.WIP<1,2,2>
*---FINDING THE TOTAL COST
   MAST.LABOR.COST = MAST.LABOR.COST + LB.WIP
   MAST.MATL.COST = MAST.MATL.COST + MT.WIP
   MAST.OSP.COST = MAST.OSP.COST + OS.WIP
   MAST.SHIP.COST = MAST.SHIP.COST + SP.WIP
   MAST.MISC.COST = MAST.MISC.COST + MS.WIP
   MAST.TOTAL = MAST.TOTAL + WIP
   LINE.MASTER<-1> = JOB.MASTER
   LINE.STAR<-1> = STAR
   LINE.JOB<-1> = KEY
   LINE.CUST<-1> = JOB.CUST
   LINE.DATE<-1> = JOB.LB.DATE<1,LST,1>
   LINE.DIV<-1> = JOB.DIV
   LINE.DEPT<-1> = JOB.LB.DEPT<1,LST>
   LINE.CCTR<-1> = JOB.LB.CCTR<1,LST>
   LINE.OPER<-1> = JOB.LB.OPER<1,LST>
   LINE.LB<-1> = LB.WIP
   LINE.MT<-1> = MT.WIP
   LINE.OS<-1> = OS.WIP
   LINE.SP<-1> = SP.WIP
   LINE.MS<-1> = MS.WIP
   LINE.TOTAL<-1> = WIP
3999*
   RETURN
*--- PROCESS THE RECORD FOR SPECIEFIC YEAR AND PERIOD
4000*
* IF JOB.WIP<1,4> > MENU THEN GOTO 4999
* WIP = JOB.WIP<1,2,1> + JOB.WIP<1,2,2> + JOB.WIP<1,2,3> + JOB.WIP<1,2,4>
* IF WIP = 0 THEN
*    IF JOB.WIP<1,4> = "" OR JOB.WIP<1,5> < MENU THEN GOTO 4999
* END
4010*
   TOT.LB.COST = 0
   IF JOB.LB.WIP<1,4> > MENU THEN GOTO 4020
   LB.WIP = JOB.LB.WIP<1,2,1> + JOB.LB.WIP<1,2,2> + JOB.LB.WIP<1,2,3> + JOB.LB.WIP<1,2,4>
   IF LB.WIP = 0 THEN
      IF JOB.LB.WIP<1,4> = "" OR JOB.LB.WIP<1,5> < MENU THEN GOTO 4020
   END
   FOR I = 1 TO CNT
      LB.ID = ID:"!":JOB.LB.DEPT<1,I>:"!":JOB.LB.CCTR<1,I>:"!"
      SEQ.CNT = JOB.LB.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JLB.REC FROM JOB.TIME, LB.ID:II ELSE GOTO 4015
         III = 1
         LOOP WHILE JLB.WIP<1,III> # "" DO
            IF JLB.MON<1,III> <= MENU THEN TOT.LB.COST = TOT.LB.COST + JLB.WIP<1,III,1> + JLB.WIP<1,III,2> + JLB.WIP<1,III,3> + JLB.WIP<1,III,4>
            III = III + 1
         REPEAT
4015  NEXT II
   NEXT I
4020*
   TOT.MT.COST = 0
   IF JOB.MT.WIP<1,4> > MENU THEN GOTO 4030
   MT.WIP = JOB.MT.WIP<1,2,1> + JOB.MT.WIP<1,2,2> + JOB.MT.WIP<1,2,3>
   IF MT.WIP = 0 THEN
      IF JOB.MT.WIP<1,4> = "" OR JOB.MT.WIP<1,5> < MENU THEN GOTO 4030
   END
   CNT = COUNT(JOB.MT.DEPT,VM) + (JOB.MT.DEPT # "")
   FOR I = 1 TO CNT
      MT.ID = ID:"!":JOB.MT.DEPT<1,I>:"!":JOB.MT.CCTR<1,I>:"!":JOB.MT.PROD<1,I>:"!":JOB.MT.WHSE<1,I>:"!"
      SEQ.CNT = JOB.MT.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JMT.REC FROM JOB.MATL, MT.ID:II ELSE GOTO 4025
         III = 1
         LOOP WHILE JMT.WIP<1,III> # "" DO
            IF JMT.MON<1,III> <= MENU THEN TOT.MT.COST = TOT.MT.COST + JMT.WIP<1,III,1> + JMT.WIP<1,III,2> + JMT.WIP<1,III,3>
            III = III + 1
         REPEAT
4025  NEXT II
   NEXT I
4030*
   TOT.OS.COST = 0
   IF JOB.OS.WIP<1,4> > MENU THEN GOTO 4040
   OS.WIP = JOB.OS.WIP<1,2,1> + JOB.OS.WIP<1,2,2>
   IF OS.WIP = 0 THEN
      IF JOB.OS.WIP<1,4> = "" OR JOB.OS.WIP<1,5> < MENU THEN GOTO 4040
   END
   CNT = COUNT(JOB.OS.DEPT,VM) + (JOB.OS.DEPT # "")
   FOR I = 1 TO CNT
      OS.ID = ID:"!":JOB.OS.DEPT<1,I>:"!":JOB.OS.CCTR<1,I>:"!":JOB.OS.PLINE<1,I>:"!"
      SEQ.CNT = JOB.OS.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JOS.REC FROM JOB.OSP, OS.ID:II ELSE GOTO 4035
         III = 1
         LOOP WHILE JOS.WIP<1,III> # "" DO
            IF JOS.MON<1,III> <= MENU THEN TOT.OS.COST = TOT.OS.COST + JOS.WIP<1,III,1> + JOS.WIP<1,III,2>
            III = III + 1
         REPEAT
4035  NEXT II
   NEXT I
4040*
   TOT.SP.COST = 0
   IF JOB.SP.WIP<1,4> > MENU THEN GOTO 4050
   SP.WIP = JOB.SP.WIP<1,2,1> + JOB.SP.WIP<1,2,2>
   IF SP.WIP = 0 THEN
      IF JOB.SP.WIP<1,4> = "" OR JOB.SP.WIP<1,5> < MENU THEN GOTO 4050
   END
   CNT = COUNT(JOB.SP.DEPT,VM) + (JOB.SP.DEPT # "")
   FOR I = 1 TO CNT
      SP.ID = ID:"!":JOB.SP.DEPT<1,I>:"!":JOB.SP.CCTR<1,I>:"!"
      SEQ.CNT = JOB.SP.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JSP.REC FROM JOB.SHIP, SP.ID:II ELSE GOTO 4045
         III = 1
         LOOP WHILE JSP.WIP<1,III> # "" DO
            IF JSP.MON<1,III> <= MENU THEN TOT.SP.COST = TOT.SP.COST + JSP.WIP<1,III,1> + JSP.WIP<1,III,2>
            III = III + 1
         REPEAT
4045  NEXT II
   NEXT I
4050*
   TOT.MS.COST = 0
   IF JOB.MS.WIP<1,4> > MENU THEN GOTO 4100
   MS.WIP = JOB.MS.WIP<1,2,1> + JOB.MS.WIP<1,2,2>
   IF MS.WIP = 0 THEN
      IF JOB.MS.WIP<1,4> = "" OR JOB.MS.WIP<1,5> < MENU THEN GOTO 4100
   END
   CNT = COUNT(JOB.MS.DEPT,VM) + (JOB.MS.DEPT # "")
   FOR I = 1 TO CNT
      MS.ID = ID:"!":JOB.MS.DEPT<1,I>:"!":JOB.MS.CCTR<1,I>:"!"
      SEQ.CNT = JOB.MS.SEQ<1,I>
      FOR II = SEQ.CNT TO 1 STEP -1
         MATREAD JMS.REC FROM JOB.MISC, MS.ID:II ELSE GOTO 4055
         III = 1
         LOOP WHILE JMS.WIP<1,III> # "" DO
            IF JMS.MON<1,III> <= MENU THEN TOT.MS.COST = TOT.MS.COST + JMS.WIP<1,III,1> + JMS.WIP<1,III,2>
            III = III + 1
         REPEAT
4055  NEXT II
   NEXT I
4100*
   TOT.COST = TOT.LB.COST + TOT.MT.COST + TOT.OS.COST + TOT.SP.COST + TOT.MS.COST
   IF TOT.COST = 0 THEN GOTO 4999
*---FINDING THE TOTAL COST
   MAST.LABOR.COST = MAST.LABOR.COST + TOT.LB.COST
   MAST.MATL.COST = MAST.MATL.COST + TOT.MT.COST
   MAST.OSP.COST = MAST.OSP.COST + TOT.OS.COST
   MAST.SHIP.COST = MAST.SHIP.COST + TOT.SP.COST
   MAST.MISC.COST = MAST.MISC.COST + TOT.MS.COST
   MAST.TOTAL = MAST.TOTAL + TOT.COST
   LINE.MASTER<-1> = JOB.MASTER
   LINE.STAR<-1> = STAR
   LINE.JOB<-1> = KEY
   LINE.CUST<-1> = JOB.CUST
   LINE.DATE<-1> = JOB.LB.DATE<1,LST,1>
   LINE.DIV<-1> = JOB.DIV
   LINE.DEPT<-1> = JOB.LB.DEPT<1,LST>
   LINE.CCTR<-1> = JOB.LB.CCTR<1,LST>
   LINE.OPER<-1> = JOB.LB.OPER<1,LST>
   LINE.LB<-1> = TOT.LB.COST
   LINE.MT<-1> = TOT.MT.COST
   LINE.OS<-1> = TOT.OS.COST
   LINE.SP<-1> = TOT.SP.COST
   LINE.MS<-1> = TOT.MS.COST
   LINE.TOTAL<-1> = TOT.COST
4999*
   RETURN
*--- PRINT TOTAL FOR MASTER JOB
5000*
   LN.CNT = COUNT(LINE.MASTER,AM) + (LINE.MASTER # "")
   BEGIN CASE
      CASE LN.CNT < 1
         GOTO 5500
      CASE LN.CNT = 1
         V.LINE = ""
         V.LINE = V.LINE : LINE.MAST.S "L#1" : LINE.MASTER<1> "L#8" :SP2
         IF LINE.MASTER<1> = LINE.JOB<1> THEN
            V.LINE = V.LINE :SP3:SP4:SP4
         END ELSE
            V.LINE = V.LINE : LINE.STAR<1> "L#1" : LINE.JOB<1> "L#8" :SP2
         END
         V.LINE = V.LINE : LINE.CUST<1> "R#6" :SP2
         V.LINE = V.LINE : OCONV(LINE.DATE<1>, 'D2-') "R#8"
         V.LINE = V.LINE :SP2: LINE.DIV<1> "R#2" :SP2
         V.LINE = V.LINE : LINE.DEPT<1> "L#5" :SP2: LINE.CCTR<1> "R#4"
         V.LINE = V.LINE :SP2: LINE.OPER<1> "L#4" :SP4
         V.LINE = V.LINE : OCONV(LINE.LB<1>, 'MD2') "R#9" :SP2
         V.LINE = V.LINE : OCONV(LINE.MT<1>, 'MD2') "R#9" :SP2
         V.LINE = V.LINE : OCONV(LINE.OS<1>, 'MD2') "R#9" :SP2
         V.LINE = V.LINE : OCONV(LINE.SP<1>, 'MD2') "R#9" :SP2
         V.LINE = V.LINE : OCONV(LINE.MS<1>, 'MD2') "R#9" :SP2
         V.LINE = V.LINE : OCONV(LINE.TOTAL<1>, 'MD2') "R#10" 
         PRINT V.LINE
         IF LINE.MASTER<1> = LINE.JOB<1> THEN GOTO 5100
         LST = 1
      CASE 1
         LOCATE PREV.MASTER IN LINE.JOB,1 SETTING FND ELSE FND = 0
         IF FND AND FND < LN.CNT THEN
            LINE.MASTER<-1> = LINE.MASTER<FND>
            LINE.STAR<-1> = LINE.STAR<FND>
            LINE.JOB<-1> = LINE.JOB<FND>
            LINE.CUST<-1> = LINE.CUST<FND>
            LINE.DATE<-1> = LINE.DATE<FND>
            LINE.DIV<-1> = LINE.DIV<FND>
            LINE.DEPT<-1> = LINE.DEPT<FND>
            LINE.CCTR<-1> = LINE.CCTR<FND>
            LINE.OPER<-1> = LINE.OPER<FND>
            LINE.LB<-1> = LINE.LB<FND>
            LINE.MT<-1> = LINE.MT<FND>
            LINE.OS<-1> = LINE.OS<FND>
            LINE.SP<-1> = LINE.SP<FND>
            LINE.MS<-1> = LINE.MS<FND>
            LINE.TOTAL<-1> = LINE.TOTAL<FND>
            LINE.MASTER = DELETE(LINE.MASTER,FND,0,0)
            LINE.STAR = DELETE(LINE.STAR,FND,0,0)
            LINE.JOB = DELETE(LINE.JOB,FND,0,0)
            LINE.CUST = DELETE(LINE.CUST,FND,0,0)
            LINE.DATE = DELETE(LINE.DATE,FND,0,0)
            LINE.DIV = DELETE(LINE.DIV,FND,0,0)
            LINE.DEPT = DELETE(LINE.DEPT,FND,0,0)
            LINE.CCTR = DELETE(LINE.CCTR,FND,0,0)
            LINE.OPER = DELETE(LINE.OPER,FND,0,0)
            LINE.LB = DELETE(LINE.LB,FND,0,0)
            LINE.MT = DELETE(LINE.MT,FND,0,0)
            LINE.OS = DELETE(LINE.OS,FND,0,0)
            LINE.SP = DELETE(LINE.SP,FND,0,0)
            LINE.MS = DELETE(LINE.MS,FND,0,0)
            LINE.TOTAL = DELETE(LINE.TOTAL,FND,0,0)
         END
         LST = 1
         FOR LN = 1 TO LN.CNT
            IF LINE.DATE<LN> > LINE.DATE<LST> THEN LST = LN
            V.LINE = ""
            V.LINE = V.LINE : LINE.MAST.S "L#1" : LINE.MASTER<LN> "L#8" :SP2
            V.LINE = V.LINE : LINE.STAR<LN> "L#1" : LINE.JOB<LN> "L#8" :SP2
            V.LINE = V.LINE : LINE.CUST<LN> "R#6" :SP2
            V.LINE = V.LINE : OCONV(LINE.DATE<LN>, 'D2-') "R#8"
            V.LINE = V.LINE :SP2: LINE.DIV<LN> "R#2" :SP2
            V.LINE = V.LINE : LINE.DEPT<LN> "L#5" :SP2: LINE.CCTR<LN> "R#4"
            V.LINE = V.LINE :SP2: LINE.OPER<LN> "L#4" :SP4
            V.LINE = V.LINE : OCONV(LINE.LB<LN>, 'MD2') "R#9" :SP2
            V.LINE = V.LINE : OCONV(LINE.MT<LN>, 'MD2') "R#9" :SP2
            V.LINE = V.LINE : OCONV(LINE.OS<LN>, 'MD2') "R#9" :SP2
            V.LINE = V.LINE : OCONV(LINE.SP<LN>, 'MD2') "R#9" :SP2
            V.LINE = V.LINE : OCONV(LINE.MS<LN>, 'MD2') "R#9" :SP2
            V.LINE = V.LINE : OCONV(LINE.TOTAL<LN>, 'MD2') "R#10" 
            PRINT V.LINE
         NEXT LN
   END CASE
   T.LINE = ""
   T.LINE = T.LINE : LINE.MAST.S "L#1" : PREV.MASTER "L#8" :SP4:SP4:SP4:SP1
   T.LINE = T.LINE : LINE.CUST<LN.CNT> "R#6" :SP2
   T.LINE = T.LINE : OCONV(LINE.DATE<LST>, 'D2-') "R#8"
   T.LINE = T.LINE :SP2: LINE.DIV<LST> "R#2" :SP2
   T.LINE = T.LINE : LINE.DEPT<LST> "L#5" :SP2: LINE.CCTR<LST> "R#4"
   T.LINE = T.LINE :SP2: LINE.OPER<LST> "L#4" :SP4
   T.LINE = T.LINE : OCONV(MAST.LABOR.COST, 'MD2') "R#9" :SP2
   T.LINE = T.LINE : OCONV(MAST.MATL.COST, 'MD2') "R#9" :SP2
   T.LINE = T.LINE : OCONV(MAST.OSP.COST, 'MD2') "R#9" :SP2
   T.LINE = T.LINE : OCONV(MAST.SHIP.COST, 'MD2') "R#9" :SP2
   T.LINE = T.LINE : OCONV(MAST.MISC.COST, 'MD2') "R#9" :SP2
   T.LINE = T.LINE : OCONV(MAST.TOTAL, 'MD2') "R#10"
   PRINT T.LINE
5100*
   TOTAL.LABOR.COST = TOTAL.LABOR.COST + MAST.LABOR.COST
   TOTAL.MATL.COST = TOTAL.MATL.COST + MAST.MATL.COST
   TOTAL.OSP.COST = TOTAL.OSP.COST + MAST.OSP.COST
   TOTAL.SHIP.COST = TOTAL.SHIP.COST + MAST.SHIP.COST
   TOTAL.MISC.COST = TOTAL.MISC.COST + MAST.MISC.COST
   TOTALS = TOTALS + MAST.TOTAL
*--- CLEAR MASTER TOTALS
5500*
   MAT LINE.REC = ""
   MAST.TOTAL = 0
   MAST.LABOR.COST = 0
   MAST.MATL.COST = 0
   MAST.OSP.COST = 0
   MAST.SHIP.COST = 0
   MAST.MISC.COST = 0
   PREV.MASTER = JOB.MASTER
   FIRST = 1
   RETURN
*--- CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999*
END
