*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SYSTEM      - JCSBP
* PROGRAM     - JOB.DETAIL.HRS.COST
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 02/28/86
* DESCRIPTION -
* This program produces a Job Detail Report by hours/costing for
* MURRAY.
*********************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>JOB.COST.REPORT
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM A MENU";GOSUB 91000;GOTO 99999
     CS.FLG = ID<5>
*
*---- DIMENSION RECORDS
*
     DIM REG.TOTAL.REC(10)
     DIM R.REC(10)
     DIM CO.TOTAL.REC(10)
     DIM C.REC(10)
     DIM SUB.TOTAL.REC(10)
     DIM S.REC(10)
     DIM DEPT.TOTAL.REC(10)
     DIM D.REC(10)
     DIM GRAND.TOTAL.REC(10)
     DIM G.REC(10)
*
*---- OPEN FILES
*
     OPEN '','JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG='M.SCREENS FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG='EMPLOYEE FILE MISSING';GOTO 93000
     OPEN '','OPERATION' TO OPERATION ELSE ERRMSG='OPERATION FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
     OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
     OPEN '','JOB.COST.REPORT' TO JOB.COST.REPORT ELSE ERRMSG='JOB.COST.REPORT FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=''
     RPT.NO="XXXX"
     UNKNOWN=STR("?",30)
     MAT REG.TOTAL.REC=''
     MAT CO.TOTAL.REC=''
     MAT SUB.TOTAL.REC=''
     MAT DEPT.TOTAL.REC=''
     MAT GRAND.TOTAL.REC=''
     MAT R.REC=''
     MAT C.REC=''
     MAT S.REC=''
     MAT D.REC=''
     MAT G.REC=''
     PREVIOUS.DEPT.NO=''
     PREVIOUS.DEPT=''
     PREVIOUS.CCTR=''
     PREVIOUS.OPER=''
     FIRST.FLAG=1
     NODATA=1
     MAT JCR.REC=''
     MAT JOB.REC=''
     EMP.NAME=''
     DATE.LAST.WORKED=''
     DATE.ITEMS.THROUGH=''
     PRINTER ON
*
*---- GET COMPANY NUMBER
*
     CONO=''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO='END' THEN GOTO 99999
     MAT EDIT.COM.DRIVER = ""
*
*---- MAIN PROCESSING
*
10*
     READNEXT JCR.KEY ELSE
        IF NODATA THEN GOTO 99999
        GOTO 12
     END
     TYPE=FIELD(JCR.KEY,"*",4)
     IF TYPE="EST" THEN GOTO 10
     MATREAD JCR.REC FROM JOB.COST.REPORT,JCR.KEY ELSE GOTO 10
     IF JCR.CAT # 'LAB' THEN GOTO 10
     IF JCR.LINE.TYPE # "D" THEN GOTO 10
     NODATA=0
     DEPT.NO=JCR.DEPT
     JOB.NO=JCR.JOB
     IF FIRST.FLAG=1 THEN GOSUB 15
     CCTR.CNT=COUNT(JCR.CCTR,VM) + (JCR.CCTR # '')
     I=0
     FOR I=1 TO CCTR.CNT
        CCTR.NO=JCR.CCTR<1,I>
        OPER.NO=JCR.OPER<1,I>
        EMP.NUM=JCR.EMP<1,I>
        TRANS.DATE=JCR.DATE<1,I>
        TRANS.TYPE=JCR.TYPE<1,I>
        IF CS.FLG = "S" THEN
           REG.COST=JCR.SALE<1,I,1>
           OT.COST=JCR.SALE<1,I,2>
           DT.COST=JCR.SALE<1,I,3>
        END ELSE
           REG.COST=JCR.COST<1,I,1>
           OT.COST=JCR.COST<1,I,2>
           DT.COST=JCR.COST<1,I,3>
        END
        TTL.COST=REG.COST + OT.COST + DT.COST
        QTY=JCR.QTY<1,I>
        SPOIL.COST=JCR.SPOIL.COST<1,I>
        REASON=JCR.SPOIL.CODE<1,I>
        TRAN.JOB=JCR.RC.JOB<1,I>
        IF FIRST.FLAG=1 THEN GOTO 11
        IF PREVIOUS.DEPT # DEPT.NO THEN
           GOSUB 50
           GOSUB 60
           IF LINE.CNT >= 50 THEN GOSUB 20
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.CCTR # CCTR.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
           GOTO 11
        END
        IF PREVIOUS.OPER # OPER.NO THEN
           GOSUB 50
           IF LINE.CNT >= 50 THEN
              GOSUB 20
              H.LINE=DEPT.DESC:'  ':DEPT.NO
              PRINT H.LINE
              PRINT STR('-',LEN(H.LINE))
              LINE.CNT=LINE.CNT + 2
           END
           PREVIOUS.OPER=OPER.NO
        END
11      EMP.NAME=""
        GOSUB 30
        IF FIRST.FLAG=1 THEN
           FIRST.FLAG=0
           PREVIOUS.DEPT=DEPT.NO
           PREVIOUS.CCTR=CCTR.NO
           PREVIOUS.OPER=OPER.NO
        END
     NEXT I
     GOTO 10
12*
     GOSUB 50
     GOSUB 60
     GOSUB 70
     PRINTER OFF
     GOTO 99999
*
*---- GET JOB INFORMATION FOR HEADING
*
15*
     MATREAD JOB.REC FROM JOB,CONO:JOB.NO ELSE
        ERRMSG='JOB RECORD ':CONO:JOB.NO:' IS MISSING'
        GOTO 93000
     END
     IF JCR.CONSOL.FLAG = "Y" THEN
        CONSOL = "CONSOLIDATED"
     END ELSE
        CONSOL = "            "
     END
     T=0;DATE.CNT=0
     DATE.CNT=COUNT(JOB.LB.DATE,VM) + (JOB.LB.DATE # '')
     FOR T=1 TO DATE.CNT
        IF JOB.LB.DATE<1,T,1> > DATE.LAST.WORKED THEN DATE.LAST.WORKED=JOB.LB.DATE<1,T,1>
     NEXT T
     DATE.TBL=JOB.LB.DATE:"ý":JOB.MT.DATE:"ý":JOB.OS.DATE:"ý":JOB.MS.DATE:"ý":JOB.SP.DATE
     DT.CNT=COUNT(DATE.TBL,VM) + (DATE.TBL # '')
     FOR U=1 TO DT.CNT
        IF DATE.TBL<1,U,1> > DATE.ITEMS.THROUGH THEN DATE.ITEMS.THROUGH=DATE.TBL<1,U,1>
     NEXT U
     MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE CUST.NAME=UNKNOWN
     MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE SALS.NAME=UNKNOWN
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     PG.NO=PG.NO + 1
     H.LINE='';HLINE1='';HLINE2='';HLINE3=''
     HLINE1="#":JOB.NO "L#8":SPACE(1):CONSOL:"  REPORT NO: ":RPT.NO "L#4":SPACE(3)
     K=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=K:CO.NAME:K
     HLINE3=SPACE(8):OCONV(DATE(),"D2/")"R#8":SPACE(12):'PAGE: ':PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     PRINT JOB.CUST"L#8":SPACE(33):'J O B   D E T A I L   R E P O R T (LABOR DOLLARS)':SPACE(20):'#':JOB.NO "L#8":" ":CONSOL
     PRINT CUST.NAME"L#30":SPACE(72):CUST.NAME
     PRINT JOB.QTY<1,1>"L#7":SPACE(45):'FOR ITEMS THROUGH: ':OCONV(DATE.ITEMS.THROUGH,"D2-")"R#8":SPACE(23):JOB.QTY<1,1>
     PRINT JOB.DESC<1,1>"L#30":SPACE(22):'DATE LAST ENTRY  : ':OCONV(DATE.LAST.WORKED,"D2-")"R#8":SPACE(23):JOB.DESC<1,1> "L#30"
     PRINT 'SR: ':SALS.NAME"L#27":'SALES CODE: ':JOB.SALES.CODE
     PRINT
     PRINT SPACE(60):'--------- ACTUAL COSTS ----------'
     PRINT 'DEPT  CCTR  OPER      EMPLOYEE NAME/NUMBER        DATE         REG     O/T     D/T     TOTAL   QUANTITY   SPOIL   REASON  XFER JOB'
     PRINT '----  ----  ---- ------------------------------ --------    -------- ------- ------- -------- ---------- ------- -------- --------'
     LINE.CNT=10
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF LINE.CNT >= 50 THEN GOSUB 20
     IF DEPT.NO # PREVIOUS.DEPT.NO THEN
        MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
        P.LINE=DEPT.DESC:'  ':DEPT.NO
        PRINT P.LINE
        PRINT STR('-',LEN(P.LINE))
        P.LINE=SPACE(17):EMP.NAME.NUM"L#30":' ':DATE"R#8":' ':TRANS.TYPE:' ':REG.COST"R#8":' ':OT.COST"R#7":' ':DT.COST"R#7":' ':TTL.COST"R#8":' ':IQTY"R#10":' ':SPOIL.COST"R#7":' ':REASON"L#8":' ':TRAN.JOB"L#8"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 3
     END ELSE
        P.LINE=SPACE(17):EMP.NAME.NUM"L#30":' ':DATE"R#8":' ':TRANS.TYPE:' ':REG.COST"R#8":' ':OT.COST"R#7":' ':DT.COST"R#7":' ':TTL.COST"R#8":' ':IQTY"R#10":' ':SPOIL.COST"R#7":' ':REASON"L#8":' ':TRAN.JOB"L#8"
        PRINT P.LINE
        LINE.CNT=LINE.CNT + 1
     END
     PREVIOUS.DEPT.NO=DEPT.NO
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     MATREAD EMP.REC FROM EMPLOYEE,CONO:EMP.NUM ELSE EMP.NAME=STR("?",20)
     IF EMP.NAME='' THEN EMP.NAME=EMP.FRST.NAME:' ':EMP.LAST.NAME
     EMP.NAME.NUM=EMP.NAME:'  ':EMP.NUM
     DATE=OCONV(TRANS.DATE,"D2-")
     IF TRANS.TYPE='R' THEN
        TRANS.TYPE='  '
     END ELSE
        TRANS.TYPE='CO'
     END
     IF TRANS.TYPE='CO' THEN
        CO.TOTAL.REC(1)=CO.TOTAL.REC(1) + REG.COST
        CO.TOTAL.REC(2)=CO.TOTAL.REC(2) + OT.COST
        CO.TOTAL.REC(3)=CO.TOTAL.REC(3) + DT.COST
        CO.TOTAL.REC(4)=CO.TOTAL.REC(4) + TTL.COST
        CO.TOTAL.REC(5)=CO.TOTAL.REC(5) + QTY
        CO.TOTAL.REC(6)=CO.TOTAL.REC(6) + SPOIL.COST
     END ELSE
        REG.TOTAL.REC(1)=REG.TOTAL.REC(1) + REG.COST
        REG.TOTAL.REC(2)=REG.TOTAL.REC(2) + OT.COST
        REG.TOTAL.REC(3)=REG.TOTAL.REC(3) + DT.COST
        REG.TOTAL.REC(4)=REG.TOTAL.REC(4) + TTL.COST
        REG.TOTAL.REC(5)=REG.TOTAL.REC(5) + QTY
        REG.TOTAL.REC(6)=REG.TOTAL.REC(6) + SPOIL.COST
     END
     SUB.TOTAL.REC(1)=SUB.TOTAL.REC(1) + REG.COST
     SUB.TOTAL.REC(2)=SUB.TOTAL.REC(2) + OT.COST
     SUB.TOTAL.REC(3)=SUB.TOTAL.REC(3) + DT.COST
     SUB.TOTAL.REC(4)=SUB.TOTAL.REC(4) + TTL.COST
     SUB.TOTAL.REC(5)=SUB.TOTAL.REC(5) + QTY
     SUB.TOTAL.REC(6)=SUB.TOTAL.REC(6) + SPOIL.COST
     DEPT.TOTAL.REC(1)=DEPT.TOTAL.REC(1) + REG.COST
     DEPT.TOTAL.REC(2)=DEPT.TOTAL.REC(2) + OT.COST
     DEPT.TOTAL.REC(3)=DEPT.TOTAL.REC(3) + DT.COST
     DEPT.TOTAL.REC(4)=DEPT.TOTAL.REC(4) + TTL.COST
     DEPT.TOTAL.REC(5)=DEPT.TOTAL.REC(5) + QTY
     DEPT.TOTAL.REC(6)=DEPT.TOTAL.REC(6) + SPOIL.COST
     GRAND.TOTAL.REC(1)=GRAND.TOTAL.REC(1) + REG.COST
     GRAND.TOTAL.REC(2)=GRAND.TOTAL.REC(2) + OT.COST
     GRAND.TOTAL.REC(3)=GRAND.TOTAL.REC(3) + DT.COST
     GRAND.TOTAL.REC(4)=GRAND.TOTAL.REC(4) + TTL.COST
     GRAND.TOTAL.REC(5)=GRAND.TOTAL.REC(5) + QTY
     GRAND.TOTAL.REC(6)=GRAND.TOTAL.REC(6) + SPOIL.COST
     IF REG.COST # '' THEN REG.COST=OCONV(REG.COST,"MD2")
     IF OT.COST # '' THEN OT.COST=OCONV(OT.COST,"MD2")
     IF DT.COST # '' THEN DT.COST=OCONV(DT.COST,"MD2")
     IF TTL.COST # '' THEN TTL.COST=OCONV(TTL.COST,"MD2")
     IF SPOIL.COST # '' THEN SPOIL.COST=OCONV(SPOIL.COST,"MD2")
     IQTY = OCONV(QTY,"MD2")
     RETURN
*
*---- PRINT SUB TOTALS
*
50*
     MATREAD OPER.REC FROM OPERATION,CONO:PREVIOUS.OPER ELSE OPER.DESC=UNKNOWN
     FOR G=1 TO 6
        IF REG.TOTAL.REC(G) # '' THEN R.REC(G)=OCONV(REG.TOTAL.REC(G),"MD2")
        IF CO.TOTAL.REC(G) # '' THEN C.REC(G)=OCONV(CO.TOTAL.REC(G),"MD2")
        IF SUB.TOTAL.REC(G) # '' THEN S.REC(G)=OCONV(SUB.TOTAL.REC(G),"MD2")
     NEXT G
     S.LINE=SPACE(60):'-------- ------- ------- -------- ---------- -------'
     PRINT S.LINE
     S.LINE=SPACE(39):'*   REG COST TOTAL   ':R.REC(1)"R#8":' ':R.REC(2)"R#7":' ':R.REC(3)"R#7":' ':R.REC(4)"R#8":' ':R.REC(5)"R#10":' ':R.REC(6)"R#7"
     PRINT S.LINE
     LINE.CNT=LINE.CNT + 2
     IF C.REC(1)+0 # 0 OR C.REC(2)+0 # 0 OR C.REC(3)+0 # 0 OR C.REC(4)+0 # 0 OR C.REC(5)+0 # 0 OR C.REC(6)+0 # 0 THEN
        S.LINE=SPACE(39):'* CO/AA COST TOTAL   ':C.REC(1)"R#8":' ':C.REC(2)"R#7":' ':C.REC(3)"R#7":' ':C.REC(4)"R#8":' ':C.REC(5)"R#10":' ':C.REC(6)"R#7"
        PRINT S.LINE
        LINE.CNT=LINE.CNT + 1
     END
     S.LINE=SPACE(60):'-------- ------- ------- -------- ---------- -------'
     PRINT S.LINE
     S.LINE=SPACE(4):'* ':PREVIOUS.CCTR"L#4":'  ':PREVIOUS.OPER"L#4":'  ':OPER.DESC"L#30":SPACE(12):S.REC(1)"R#8":' ':S.REC(2)"R#7":' ':S.REC(3)"R#7":' ':S.REC(4)"R#8":' ':S.REC(5)"R#10":' ':S.REC(6)"R#7"
     PRINT S.LINE
     PRINT;PRINT
     LINE.CNT=LINE.CNT + 4
     MAT REG.TOTAL.REC=''
     MAT CO.TOTAL.REC=''
     MAT SUB.TOTAL.REC=''
     MAT R.REC=''
     MAT C.REC=''
     MAT S.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT DEPT TOTALS
*
60*
     FOR B=1 TO 6
        IF DEPT.TOTAL.REC(B) # '' THEN D.REC(B)=OCONV(DEPT.TOTAL.REC(B),"MD2")
     NEXT B
     D.LINE=SPACE(60):'-------- ------- ------- -------- ---------- -------'
     PRINT D.LINE
     D.LINE='** ':DEPT.DESC:' DEPARTMENT TOTAL';D.LINE=D.LINE "L#40"
     D.LINE=D.LINE:SPACE(20):D.REC(1)"R#8":' ':D.REC(2)"R#7":' ':D.REC(3)"R#7":' ':D.REC(4)"R#8":' ':D.REC(5)"R#10":' ':D.REC(6)"R#7"
     PRINT D.LINE
     PRINT
     PRINT
     LINE.CNT=LINE.CNT + 4
     MAT DEPT.TOTAL.REC=''
     MAT D.REC=''
     IF LINE.CNT >= 50 THEN GOSUB 20
     RETURN
*
*---- PRINT GRAND TOTAL
*
70*
     FOR J=1 TO 6
        IF GRAND.TOTAL.REC(J) # '' THEN G.REC(J)=OCONV(GRAND.TOTAL.REC(J),"MD2")
     NEXT J
     G.LINE=SPACE(60):'-------- ------- ------- -------- ---------- -------'
     PRINT G.LINE
     G.LINE="*** GRAND TOTALS ***":SPACE(40):G.REC(1)"R#8":' ':G.REC(2)"R#7":' ':G.REC(3)"R#7":' ':G.REC(4)"R#8":' ':G.REC(5)"R#10":' ':G.REC(6)"R#7"
     PRINT G.LINE
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
