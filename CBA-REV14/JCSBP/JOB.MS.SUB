   SUBROUTINE JOB.MS.SUB(CONO,JOB.NUM,SUB.CNT)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>JCS.CPYLIB>COM.JOB.INQ
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - JOB.MS.SUB
* DATE        - 03/05/84
* DESCRIPTION -
*T26334 epitka 12/19/2001 * REV12
*T25978 adelgado 02/14/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.TOT.REC
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
   DIM CCTR(10)
   EQU CCTR.NO    TO CCTR(1)
   EQU OPER.NO    TO CCTR(2)
   EQU OPER.NAME  TO CCTR(3)
   EQU OPER.DATE  TO CCTR(4)
   EQU OPER.COST  TO CCTR(5)
   EQU OPER.SALE  TO CCTR(6)
   EQU CCTR.NAME  TO CCTR(7)
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
   MAT CCTR = ''
   A.PAGE.SIZE = 3
   PAGE.SIZE = 2
   A.BEGIN.PAGE = 13
   BEGIN.PAGE = 4
   A.LINE.SPACE = 2
   LINE.SPACE = 2
   A.START.LINE = 0
   OLD.START.LINE = 0
   LINES = 0; LN = 1
   A.LINES = 0; A.LN = 1
*
   UNKNOWN = '???????????????'
   DASHES = STR('-',80)
*
   LINES = COUNT(MS.DEPT.NO,VM) + (MS.DEPT.NO # '')
   FOR I = 1 TO LINES
      CNT = COUNT(MS.DEPT.PTR<1,I>,SVM) + (MS.DEPT.PTR<1,I> # '')
      FOR J = 1 TO CNT
         PTR = MS.DEPT.PTR<1,I,J>
         CCTR.NO<1,I,J> = JOB.MS.CCTR<1,PTR>
         OPER.NO<1,I,J> = JOB.MS.OPER<1,PTR>
         MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR.NO<1,I,J> ELSE CCTR.DESC = UNKNOWN
         MATREAD OPER.REC FROM OPERATION, CONO : OPER.NO<1,I,J> ELSE 
            MATREAD INV.REC FROM INVENTORY, CONO : OPER.NO<1,I,J> ELSE INV.FULL.DESC = UNKNOWN
            OPER.DESC = INV.FULL.DESC
         END
         CCTR.NAME<1,I,J> = CCTR.DESC
         OPER.NAME<1,I,J> = OPER.DESC
         OPER.DATE<1,I,J> = JOB.MS.DATE<1,PTR,1>
         OPER.COST<1,I,J> = JOB.MS.COST<1,PTR>
         OPER.SALE<1,I,J> = JOB.MS.SALE<1,PTR>
      NEXT J
   NEXT I
   GOSUB 11000
   MORE = 1
   LOOP
      ECD.NUM = 50
      ECD.MAXL = 3
      ECD.O.R = 'O'
*T25978 v
*     ECD.VALDAT = 'S'
      ECD.VALDAT = 'SR,SR,SB,ST'
*T25978 ^
      FOR I = OLD.START.LINE TO LAST.LINE
         ECD.VALDAT = ECD.VALDAT : "," : I
      NEXT I
      ECD.ACTION = 4; CALL SCRN.EDIT
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE ACTION = '' OR ACTION = 'END'
            MORE = 0
         CASE NUM(ACTION)
            LN = ACTION
            A.LINES = COUNT(CCTR.NO<1,LN>,SVM) + (CCTR.NO<1,LN> # '')
            A.START.LINE = 0
            A.LN = 1
            GOSUB 12000
            A.MORE = 1
            LOOP
               ECD.MAXL = 3
               ECD.O.R = 'O'
*T25978 v
*              ECD.VALDAT = 'S'
               ECD.VALDAT = 'S,SR,SB,ST'
*T25978 ^
               IF JOB.STATUS<1,1> # '7' AND JOB.STATUS<1,1> # '8' THEN
                  ECD.NUM = 51
                  FOR I = A.START.LINE TO A.LAST.LINE
                     ECD.VALDAT = ECD.VALDAT : "," : I
                  NEXT I
               END ELSE
                  ECD.NUM = 52
               END
               ECD.ACTION = 4; CALL SCRN.EDIT
               A.ACTION = ECD.RET.VALUE
               BEGIN CASE
                  CASE A.ACTION = '' OR A.ACTION = 'END'
                     A.MORE = 0
                  CASE NUM(A.ACTION)
                     A.LN = A.ACTION
                     PTR = MS.DEPT.PTR<1,LN,A.LN>
                     SAVE.ECD.SCRN.NO = ECD.SCRN.NO
                     ECD.SCRN.NO = 13; ECD.ACTION=2;CALL SCRN.EDIT
                     SCV.REC(6)<ECD.SCRN.NO> = MS.DEPT.NO<1,LN>
                     SCV.REC(7)<ECD.SCRN.NO> = MS.DEPT.DESC<1,LN>
                     SCV.REC(9)<ECD.SCRN.NO> = CCTR.NO<1,LN,A.LN>
                     SCV.REC(10)<ECD.SCRN.NO> = CCTR.NAME<1,LN,A.LN>
                     ECD.ACTION=3;CALL SCRN.EDIT
                     CALL JOB.MS.DET.SUB(CONO,JOB.NUM,SUB.CNT,PTR)
                     ECD.SCRN.NO = SAVE.ECD.SCRN.NO
                     ECD.ACTION=2;CALL SCRN.EDIT; ECD.ACTION=3;CALL SCRN.EDIT
                     OLD.START.LINE = 0
                     GOSUB 11000
                     A.START.LINE = 0
                     GOSUB 12000
                  CASE A.ACTION = 'S'
                     A.LN = 1+INT((A.LN-1)/A.PAGE.SIZE)*A.PAGE.SIZE+A.PAGE.SIZE
                     IF A.LN > A.LINES THEN A.LN = 1
                     GOSUB 12000
            * T25978 v
                  CASE A.ACTION = 'SR'
                     A.LN = 1+INT((A.LN-1)/A.PAGE.SIZE)*A.PAGE.SIZE-A.PAGE.SIZE
                     IF A.LN < 1 THEN A.LN = 1
                     GOSUB 12000
                  CASE A.ACTION = 'ST'
                     A.LN = 1
                     GOSUB 12000
                  CASE A.ACTION = 'SB'
                     A.LN = A.LINES
                     GOSUB 12000
            * T25978 ^
               END CASE
            WHILE A.MORE DO REPEAT
         CASE ACTION = 'S'
            LN = 1+INT((LN-1)/PAGE.SIZE)*PAGE.SIZE+PAGE.SIZE
            IF LN > LINES THEN LN = 1
            GOSUB 11000
      * T25978 v
         CASE ACTION = 'SR'
            LN = 1+INT((LN-1)/PAGE.SIZE)*PAGE.SIZE-PAGE.SIZE
            IF LN < 1 THEN LN = 1
            GOSUB 11000
         CASE ACTION = 'ST'
            LN = 1
            GOSUB 11000
         CASE ACTION = 'SB'
            LN = LINES
            GOSUB 11000
      * T25978 ^
      END CASE
   WHILE MORE DO REPEAT
   GOTO 99999
11000 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   IF LAST.LINE > LINES THEN LAST.LINE = LINES
   IF START.LINE = OLD.START.LINE THEN GOTO 11999
   OLD.START.LINE = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
      P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:MS.DEPT.NO<1,N> "L#5"
      P_X  := AM:10 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MS.DEPT.CHG<1,N>,"MD2") "R#9"
      P_X  := AM:20 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MS.DEPT.NCHG<1,N>,"MD2") "R#9"
      P_X  := AM:30 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MS.DEPT.COST<1,N>,"MD2") "R#9"
      P_X  := AM:40 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MS.DEPT.SALE<1,N>,"MD2") "R#9"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      P_X  = 4 ; P_Y = SLN+1 ; P_VALUE = MS.DEPT.DESC<1,N> "L#30" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
11999 RETURN
12000 START.LINE = 1 + INT((A.LN-1)/A.PAGE.SIZE)*A.PAGE.SIZE
   A.LAST.LINE = START.LINE + A.PAGE.SIZE - 1
   IF A.LAST.LINE > A.LINES THEN A.LAST.LINE = A.LINES
   IF START.LINE = A.START.LINE THEN GOTO 12999
   A.START.LINE = START.LINE
   CNT = 1
   P_X  = 0 ; P_Y = A.BEGIN.PAGE ; P_VALUE = MS.DEPT.NO<1,LN> "L#5" ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   FOR N = START.LINE TO A.LAST.LINE
      A.SLN = A.BEGIN.PAGE + A.LINE.SPACE * MOD(N-1,A.PAGE.SIZE)
      P_X  = 6 ; P_Y = A.SLN ; P_VALUE = N "R#3" ; P_OPT = "CL"
      P_X  := AM:10 ; P_Y := AM:A.SLN ; P_VALUE := AM:CCTR.NO<1,LN,N> "L#4"
      P_X  := AM:15 ; P_Y := AM:A.SLN ; P_VALUE := AM:OPER.NO<1,LN,N> "L#15"
      P_X  := AM:31 ; P_Y := AM:A.SLN ; P_VALUE := AM:OCONV(OPER.DATE<1,LN,N>,"D2/") "L#8"
      P_X  := AM:40 ; P_Y := AM:A.SLN ; P_VALUE := AM:OCONV(OPER.COST<1,LN,N>,"MD2") "R#9"
      P_X  := AM:50 ; P_Y := AM:A.SLN ; P_VALUE := AM:OCONV(OPER.SALE<1,LN,N>,"MD2") "R#9"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      P_X  = 10 ; P_Y = A.SLN+1 ; P_VALUE = CCTR.NAME<1,LN,N> "L#30" ; P_OPT = "CL"
      P_X  := AM:42 ; P_Y := AM:A.SLN+1 ; P_VALUE := AM:' / ':OPER.NAME<1,LN,N> "L#30"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO A.PAGE.SIZE
      A.SLN = A.BEGIN.PAGE + A.LINE.SPACE * MOD(M-1,A.PAGE.SIZE)
      P_X  = 0 ; P_Y = A.SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      P_X  = 0 ; P_Y = A.SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
12999 RETURN
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
