   SUBROUTINE OPER.MAINT.SUB(CONO,OPERATION.ID,MAT OPER.REC)
*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - OPER.MAINT.SUB
* BY
* DATE        - 04/15/94
* DESCRIPTION -
* MAINTAINS ADDITIONAL PROMPT SELECTIONS FOR OPERATION CODES
* END DOCUMENTATION
*T26920 cmykleb 10/17/2002 * Insert function does not work correctly.
*********************************************************************
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>JCS.CPYLIB>SF.PROMPT
*
*--- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN THE FILES
*
   OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG="COMMUN.SCREENS FILE IS MISSING"; GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING"; GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING"; GOTO 93000
   OPEN '','OPERATION' TO OPERATION ELSE ERRMSG="OPERATION FILE IS MISSING"; GOTO 93000
   OPEN '','SF.PROMPT' TO SF.PROMPT ELSE ERRMSG="SF.PROMPT FILE IS MISSING"; GOTO 93000
*
*--- INITIALIZATION
*
   FILL = "#"
   LINE.SPACE = 1
   PAGE.SIZE = 10
   BEGIN.PAGE = 8
   LINES = 0
   LN = 1
   OLD.START.LINE = 0
   START.LINE = 0
   OLD.LINES = 0
   OPTION = ""
100*
   ECD.SCRN.NO = 2
   ECD.ACTION=3;CALL SCRN.EDIT
   ECD.NUM = 12
   SCV.REC(ECD.NUM)<ECD.SCRN.NO> = OPERATION.ID
   ECD.ACTION=5;CALL SCRN.EDIT
   ECD.NUM = 13
   SCV.REC(ECD.NUM)<ECD.SCRN.NO> = OPER.DESC
   ECD.ACTION=5;CALL SCRN.EDIT
   IF OPER.SF.PROMPT<1> = "" THEN
      LOOP
         LN = LINES + 1
         OLD.LINES = LINES
         GOSUB 2000
      WHILE LINES > OLD.LINES DO REPEAT
      LN = LINES
   END ELSE
      LINES = DCOUNT(OPER.SF.PROMPT<1>,VM)
   END
   GOSUB 10000
*
*--- ENTER OPTION
*
   MORE = 1
   LOOP
      ECD.NUM = 10
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
      ECD.ACTION=4;CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "E" OR OPTION = "END"
            MORE = 0
         CASE OPTION = "A" AND LINES = 10
        * CAN ONLY HAVE 10 LINES *
         CASE OPTION = "A"
            LOOP
               LN = LINES + 1
               OLD.LINES = LINES
               GOSUB 2000
            WHILE LINES > OLD.LINES DO REPEAT
            LN = LINES
            GOSUB 10000
         CASE OPTION = "I" AND LINES = 10
        * CAN ONLY HAVE 10 LINES *
         CASE OPTION = "I"
            ECD.NUM = 11; ECD.PMSG = "Before Line Number :"
            ECD.MINV = 1
            IF LINES < 9 THEN
               ECD.MAXV = LINES
            END ELSE
               ECD.MAXV = 9
            END
            CALL SCRN.EDIT
            LNO = ECD.RET.VALUE
            IF LNO = "" OR LNO = "END" THEN LNO = 0
            IF LNO THEN
               PROMPT.CNT = DCOUNT(OPER.SF.PROMPT<1>,VM)
               IF PROMPT.CNT = 10 THEN PROMPT.CNT = 9
*T26920 v
*              FOR X = LINES TO LNO STEP -1
*                 OPER.SF.PROMPT<1,X+1>=OPER.SF.PROMPT<1,X>
*              NEXT X
*              OPER.SF.PROMPT<1,LNO>=''
               OPER.SF.PROMPT = INSERT(OPER.SF.PROMPT,1,LNO,0,"")
               LINES = DCOUNT(OPER.SF.PROMPT<1>,VM)
               GOSUB 10000
*T26920 ^
               LN = LNO
               SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
               GOSUB 2001
               IF VALUE = "END" THEN
*T26920 v
*                 FOR X = LNO TO PROMPT.CNT
*                    OPER.SF.PROMPT<1,X>=OPER.SF.PROMPT<1,X+1>
*                 NEXT X
                  DEL OPER.SF.PROMPT<1,LNO>
*T26920 ^
               END
            END
            LINES = DCOUNT(OPER.SF.PROMPT<1>,VM); * 26920
            LN = 1
            GOSUB 10000
         CASE OPTION = "C"
            GOSUB 70000
            IF LNO THEN
               LN = LNO
               SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
               GOSUB 2001
            END
         CASE OPTION = "D"
            GOSUB 70000
            IF LNO THEN
               LN = LNO
               DEL OPER.SF.PROMPT<1,LN>
               LINES = DCOUNT(OPER.SF.PROMPT<1>,VM)
               IF LN > 1 AND LN > LINES THEN LN = LN - 1
               IF LN < 1 THEN LN = 1
               OLD.START.LINE = 0
            END
            GOSUB 10000
      END CASE
   WHILE MORE DO REPEAT
   GOTO 999999
*
*--- GET ALL PREFIX AND NUMBERS
*
2000*
   IF LN > 10 THEN OLD.LINES = "END"; GOTO 2999
   SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
   P_X  = 10 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
   P_X  := AM:10 ; P_Y := AM:SLN ; P_VALUE := AM:LN "L#2"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
2001*
   FILL = "#"
   X = 30; Y = SLN; TYP = 1; MAXL = 2; O.R = "O"; MAXV = ""; MINV = ""
   DEFAULT = OPER.SF.PROMPT<1,LN>
   CALL EDIT.SUB
   BEGIN CASE
      CASE VALUE = ""
         IF OPTION = "C" THEN GOTO 2001 
         P_X  = 10 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 2999
      CASE VALUE = "END"
         P_X  = 30 ; P_Y = SLN ; P_VALUE = OPER.SF.PROMPT<1,LN> "L#2" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         MATREAD SFP.REC FROM SF.PROMPT,CONO:OPER.SF.PROMPT<1,LN> ELSE
            MAT SFP.REC = ''
         END
         P_X  = 40 ; P_Y = SLN ; P_VALUE = SFP.PROMPT "L#20" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 2999
      CASE VALUE # ""
         IF VALUE # OPER.SF.PROMPT<1,LN> THEN
            LOCATE VALUE IN OPER.SF.PROMPT<1>,1 SETTING LL ELSE LL = 0
            IF LL THEN
               ERRMSG = VALUE : " PREFIX IS ALREADY ON LINE " : LL
               GOSUB 91000
               GOTO 2001
            END
            MATREAD SFP.REC FROM SF.PROMPT, CONO:VALUE ELSE
               ERRMSG = "INVALID PROMPT CODE"
               GOSUB 91000
               GOTO 2001
            END
            OPER.SF.PROMPT<1,LN> = VALUE
            P_X  = 40 ; P_Y = SLN ; P_VALUE = SFP.PROMPT "L#20" ; P_OPT = ""
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         END
   END CASE
   LINES = DCOUNT(OPER.SF.PROMPT<1>,VM)
2999*
   RETURN
*
*--- SCROLL ROUTINE
*
10000*
   START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
   OLD.START.LINE = START.LINE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   CNT = 1
   FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 10 ; P_Y = SLN ; P_VALUE = N "L#2" ; P_OPT = ""
      P_X  := AM:30 ; P_Y := AM:SLN ; P_VALUE := AM:OPER.SF.PROMPT<1,N> "L#2"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      MATREAD SFP.REC FROM SF.PROMPT,CONO:OPER.SF.PROMPT<1,N> ELSE
         MAT SFP.REC = ''
      END
      P_X  = 40 ; P_Y = SLN ; P_VALUE = SFP.PROMPT "L#20" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 10 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
10001*
   RETURN
*
*--- GET LINE NUMBER
*
70000*
   ECD.NUM = 11
   ECD.MINV = OLD.START.LINE
   IF LAST.LINE < LINES THEN
      ECD.MAXV = LAST.LINE
   END ELSE
      ECD.MAXV = LINES
   END
   CALL SCRN.EDIT
   LNO = ECD.RET.VALUE
   IF LNO = "" OR LNO = "END" THEN LNO = 0
   RETURN
*
*--- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
999999*
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
