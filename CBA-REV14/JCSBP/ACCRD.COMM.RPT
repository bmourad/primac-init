***************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - ACCRD.COMM.RPT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/06/85
* DESCRIPTION -
* This program produces an Accrued Commission report sorted by
* Salesman and by Customer.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T26556 cmykleb 06/05/2002 * Hard code rpt # JC518 into pgm because
*                            this pgm has 2 rpts in it.
***************************************************************
*
*---- FILE EQUATES
*
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JCS.CPYLIB>COMMISSION
*COPY>PMC.CPYLIB>CUSTOMER
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD XX ELSE
      ERRMSG = "MUST BE RUN FROM A PROC"
      GOSUB 91000
      GOTO 99999
   END
   CONO = XX<1>
   CO.NAME = XX<2>
   SELECT.TYPE = XX<3>
   PERIOD = XX<4>
*
*---- DIMENSION VARIABLES
*
   DIM SUBTOTAL.REC(10)
   DIM TOTAL.REC(10)
   DIM SP(132)
   DIM DSH(40)
   DIM COMM.TBL(10)
   DIM SUM.REC(11)
*
*---- OPEN FILES
*
   OPEN '','JOB' TO JOB ELSE ERRMSG = 'JOB FILE MISSING';GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
   OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG = 'SALESMAN FILE MISSING';GOTO 93000
   OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG = 'CUSTOMER FILE MISSING';GOTO 93000
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "COMPANY ":CONO:" IS MISSING"
      GOTO 93000
   END
   IF CO.COMMISSION # "Y" THEN GOTO 99999
   OPEN '','COMMISSION' TO COMMISSION ELSE ERRMSG='COMMISSION FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
*T26493 v
*  RNAME = "ACCRUED COMMISSIONS"
*  RPT.NO = "XXXX"
   CO.NAME = ""
   RNAME = ""
   RPT.NO = XX<2>
*T26493 ^
   RDATE = DATE()
   PER = PERIOD[5,2]
   ATT = PER + 1
   READV REC FROM CONTROL, CONO:"SALESDATES",ATT ELSE REC = ""
   PDATE = REC<1,1>
   CALL GET.PROG.HEAD(CONO,CO.NAME,RNAME,RPT.NO,RDATE,HLINE1,HLINE2)
   PG.NO = 0
   LINE.CNT = 0
   ERRMSG = ""
   NO.DATA = 1
   UNKNOWN = STR("?",25)
   PREV.JOB = ""
   PREV.SLSMN = ""
   FOR S = 1 TO 132
      SP(S) = SPACE(S)
   NEXT S
   FOR D = 1 TO 40
      DSH(D) = STR("-",D)
   NEXT D
   EQL11 = STR("=",11)
   EQL10 = STR("=",10)
   MAT COMM.TBL = ""
   MAT SUBTOTAL.REC = ""
   MAT TOTAL.REC = ""
   MAT SUM.REC = ""
   FIRST.FLAG = 1
   NEW.PAGE = 0
   COL.HEAD1 = SP(26):"JOB    INVOICE   INVOICE    INVOICE    NET SALE     MARKET       MARK     COMMISSION   ACTUAL     ADJUST"
   COL.HEAD2 = SP(8):"CUSTOMER         NUMBER   NUMBER    DATE      AMOUNT      AMOUNT       COST         UP        AMOUNT      COST       COST"
   COL.LINE = DSH(23):SP(1):DSH(8):SP(1):DSH(8):SP(1):DSH(8):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(10):SP(1):DSH(10)
   SLINE = SP(51):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(10):SP(1):DSH(10)
   GLINE = SP(51):EQL11:SP(1):EQL11:SP(1):EQL11:SP(1):EQL11:SP(1):EQL11:SP(1):EQL10:SP(1):EQL10
   PRINTER ON
*
*---- MAIN PROCESSING
*
10*
   READNEXT COMM.KEY ELSE
      IF NO.DATA THEN GOTO 99999
      GOTO 15
   END
   MATREAD COMM.REC FROM COMMISSION,COMM.KEY ELSE GOTO 10
   MAT COMM.TBL = ""
   I.CNT = COUNT(COMM.INV.NO,VM) + (COMM.INV.NO # "")
   K = 0
   FOR I = 1 TO I.CNT
      IF SELECT.TYPE = "R" THEN
         IF COMM.INV.MON<1,I> > PERIOD THEN GOTO 11
      END ELSE
         IF COMM.INV.MON<1,I> # PERIOD THEN
            IF COMM.INV.NO<1,I>[7,2] = 'PB' THEN
               IF (COMM.INV.MON<1,I>+1) # PERIOD THEN GOTO 11
            END ELSE
               GOTO 11
            END
         END
      END
*        IF COMM.INV.AMT<1,I>+0 = 0 THEN GOTO 11
*        IF COMM.INV.BAL<1,I>+0 = 0 THEN GOTO 11
      NET.SALES = COMM.INV.AMT<1,I> - COMM.INV.TAX<1,I> - COMM.INV.SHP<1,I>
      K = K + 1
      COMM.TBL(1)<1,K> = COMM.INV.NO<1,I>
      COMM.TBL(2)<1,K> = COMM.INV.DATE<1,I>
      COMM.TBL(3)<1,K> = COMM.INV.AMT<1,I>
      COMM.TBL(4)<1,K> = NET.SALES
      COMM.TBL(5) = COMM.TBL(5) + NET.SALES
      NET.SALES = ""
11*
   NEXT I
   C.CNT = COUNT(COMM.TBL(1),VM) + (COMM.TBL(1) # "")
   IF C.CNT = 0 THEN GOTO 10
   ACT.COST = COMM.ACT.COST
   MKT.COST = COMM.MKT.COST
   ADJ.COST = COMM.ADJ.COST
   OS.COMM = COMM.OS.COMM
   MKUP = COMM.TBL(5) - MKT.COST
   COMM.AMT = COMM.CALC + COMM.ADJ
   IF COMM.AMT = COMM.PAID THEN GOTO 10
   NO.DATA = 0
   A.COMM.AMT = COMM.AMT - COMM.PAID
   IF A.COMM.AMT < 0 THEN A.COMM.AMT = 0
   JOB.NO = COMM.KEY[4,99]
   CUST.NO = COMM.CUST
   SLSMN = COMM.SLSM
   IF PREV.SLSMN # SLSMN THEN
      IF FIRST.FLAG = 0 THEN GOSUB 50
      MATREAD SALESMAN.REC FROM SALESMAN,CONO:SLSMN ELSE SALS.NAME = UNKNOWN
      GOSUB 20
   END
   FIRST.LINE = 1
   GOSUB 30
   PREV.JOB = JOB.NO
   IF C.CNT = 1 THEN GOTO 14
   FOR CC = 2 TO C.CNT
      FIRST.LINE = 0
      GOSUB 30
   NEXT CC
14*
   PREV.SLSMN = SLSMN
   GOTO 10
15*
   GOSUB 50
   GOSUB 100
   GOSUB 8000
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
   IF FIRST.FLAG = 1 THEN FIRST.FLAG = 0
   PRINT CHAR(12)
   PG.NO = PG.NO + 1
   PRINT HLINE1:PG.NO
   PRINT HLINE2
   IF SELECT.TYPE = "R" THEN
      PRINT SP(54):"JOB FINAL INVOICE THROUGH ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
   END ELSE
      PRINT SP(56):"JOB FINAL INVOICE FOR ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
   END
   PRINT "SALESREP - ":SLSMN "L#3":" ":SALS.NAME "L#25"
   PRINT
   PRINT COL.HEAD1
   PRINT COL.HEAD2
   PRINT COL.LINE
   LINE.CNT = 8
   NEW.PAGE = 1
   RETURN
*
*---- PRINT THE VALUES
*
30*
   GOSUB 40
   IF LINE.CNT >= 55 THEN GOSUB 20
   P.LINE = ""
   IF JOB.NO # PREV.JOB OR NEW.PAGE = 1 THEN
      MATREAD CUST.REC FROM CUSTOMER,CONO:CUST.NO ELSE CUST.NAME = UNKNOWN
      IF NEW.PAGE = 1 THEN NEW.PAGE = 0
      PRINT
      LINE.CNT = LINE.CNT + 1
      P.LINE = CUST.NAME "L#23":SP(1):JOB.NO "L#8":SP(1)
   END ELSE
      P.LINE = SP(33)
   END
   P.LINE = P.LINE:INV.NUM "L#8":SP(1):INV.DATE "L#8":SP(1):INV.AMT "R#11":SP(1):NET.SAMT "R#11":SP(1)
   IF FIRST.LINE THEN
      P.LINE = P.LINE:MKT.CST "R#11":SP(1):MKUP.CST "R#11":SP(1):A.COMM.AMT "R#11":SP(1):ACT.CST "R#10":SP(1):ADJ.CST "R#10"
      FIRST.LINE = 0
   END
   PRINT P.LINE
   LINE.CNT = LINE.CNT + 1
   RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
   FOR SS = 1 TO 10
      FOR II = 1 TO C.CNT
         IF COMM.TBL(SS)<1,II> = 0 THEN COMM.TBL(SS)<1,II> = ""
      NEXT II
   NEXT SS
   IF FIRST.LINE THEN
      INV.NUM = COMM.TBL(1)<1,1>
      INV.DATE = OCONV(COMM.TBL(2)<1,1>,"D2-")
      INV.AMT = COMM.TBL(3)<1,1>
      NET.SAMT = COMM.TBL(4)<1,1>
      SUBTOTAL.REC(3) = SUBTOTAL.REC(3) + MKT.COST
      SUBTOTAL.REC(4) = SUBTOTAL.REC(4) + MKUP
      SUBTOTAL.REC(5) = SUBTOTAL.REC(5) + A.COMM.AMT
      SUBTOTAL.REC(6) = SUBTOTAL.REC(6) + ACT.COST
      SUBTOTAL.REC(7) = SUBTOTAL.REC(7) + ADJ.COST
      TOTAL.REC(3) = TOTAL.REC(3) + MKT.COST
      TOTAL.REC(4) = TOTAL.REC(4) + MKUP
      TOTAL.REC(5) = TOTAL.REC(5) + A.COMM.AMT
      TOTAL.REC(6) = TOTAL.REC(6) + ACT.COST
      TOTAL.REC(7) = TOTAL.REC(7) + ADJ.COST
      IF MKT.COST = 0 OR MKT.COST = "" THEN
         MKT.CST = ""
      END ELSE
         MKT.CST = OCONV(MKT.COST,"MD2-")
      END
      IF MKUP = 0 OR MKUP = "" THEN
         MKUP.CST = ""
      END ELSE
         MKUP.CST = OCONV(MKUP,"MD2-")
      END
      IF A.COMM.AMT = 0 OR A.COMM.AMT = "" THEN
         A.COMM.AMT = ""
      END ELSE
         A.COMM.AMT = OCONV(A.COMM.AMT,"MD2-")
      END
      IF ACT.COST = 0 OR ACT.COST = "" THEN
         ACT.CST = ""
      END ELSE
         ACT.CST = OCONV(ACT.COST,"MD2-")
      END
      IF ADJ.COST = 0 OR ADJ.COST = "" THEN
         ADJ.CST = ""
      END ELSE
         ADJ.CST = OCONV(ADJ.COST,"MD2-")
      END
   END ELSE
      INV.NUM = COMM.TBL(1)<1,CC>
      INV.DATE = OCONV(COMM.TBL(2)<1,CC>,"D2-")
      INV.AMT = COMM.TBL(3)<1,CC>
      NET.SAMT = COMM.TBL(4)<1,CC>
   END
   SUBTOTAL.REC(1) = SUBTOTAL.REC(1) + INV.AMT
   SUBTOTAL.REC(2) = SUBTOTAL.REC(2) + NET.SAMT
   TOTAL.REC(1) = TOTAL.REC(1) + INV.AMT
   TOTAL.REC(2) = TOTAL.REC(2) + NET.SAMT
   IF INV.AMT = 0 OR INV.AMT = "" THEN
      INV.AMT = ""
   END ELSE
      INV.AMT = OCONV(INV.AMT,"MD2-")
   END
   IF NET.SAMT = 0 OR NET.SAMT = "" THEN
      NET.SAMT = ""
   END ELSE
      NET.SAMT = OCONV(NET.SAMT,"MD2-")
   END
   RETURN
*
*---- PRINT SUB TOTALS
*
50*
   GOSUB 60
   PRINT SLINE
   PRINT "** SALESREP TOTALS":SP(33):S.INV.AMT "R#11":SP(1):S.NET.SAMT "R#11":SP(1):S.MKT "R#11":SP(1):S.MKUP "R#11":SP(1):S.COMM "R#11":SP(1):S.ACT "R#10":SP(1):S.ADJ "R#10"
   PRINT
   LINE.CNT = LINE.CNT + 3
*     IF LINE.CNT >= 55 THEN GOSUB 20
   MAT SUBTOTAL.REC = ""
   RETURN
*
*---- VALUES FOR SUB TOTALS
*
60*
   SCNT = COUNT(SUM.REC(1),VM) + (SUM.REC(1) # "") + 1
   FOR SPP = 1 TO 10
      SUM.REC(SPP)<1,SCNT> = SUBTOTAL.REC(SPP)
   NEXT SPP
   SUM.REC(11)<1,SCNT> = SALS.NAME
*
   S.INV.AMT = OCONV(SUBTOTAL.REC(1),"MD2-")
   S.NET.SAMT = OCONV(SUBTOTAL.REC(2),"MD2-")
   S.MKT = OCONV(SUBTOTAL.REC(3),"MD2-")
   S.MKUP = OCONV(SUBTOTAL.REC(4),"MD2-")
   S.COMM = OCONV(SUBTOTAL.REC(5),"MD2-")
   S.ACT = OCONV(SUBTOTAL.REC(6),"MD2-")
   S.ADJ = OCONV(SUBTOTAL.REC(7),"MD2-")
   RETURN
*
*---- PRINT GRAND TOTALS
*
100*
   GOSUB 110
   PRINT GLINE
   PRINT "*** GRAND TOTALS ***":SP(31):T.INV.AMT "R#11":SP(1):T.NET.SAMT "R#11":SP(1):T.MKT "R#11":SP(1):T.MKUP "R#11":SP(1):T.COMM "R#11":SP(1):T.ACT "R#10":SP(1):T.ADJ "R#10"
   RETURN
*
*---- VALUES FOR GRAND TOTALS
*
110*
   T.INV.AMT = OCONV(TOTAL.REC(1),"MD2-")
   T.NET.SAMT = OCONV(TOTAL.REC(2),"MD2-")
   T.MKT = OCONV(TOTAL.REC(3),"MD2-")
   T.MKUP = OCONV(TOTAL.REC(4),"MD2-")
   T.COMM = OCONV(TOTAL.REC(5),"MD2-")
   T.ACT = OCONV(TOTAL.REC(6),"MD2-")
   T.ADJ = OCONV(TOTAL.REC(7),"MD2-")
   RETURN
*
*---- PRINT SUMMARY REPORT
*
8000*
*T26556 v
*  RNAME = "SUMMARY ACCRUED COMMISSIONS"
*  RPT.NO = "XXXX"
   CO.NAME = ""
   RPT.NO = "JC518"
   HLINE1 = ""
   HLINE2 = ""
*T26556 ^
   RDATE = DATE()
   CALL GET.PROG.HEAD(CONO,CO.NAME,RNAME,RPT.NO,RDATE,HLINE1,HLINE2)
   PG.NO = 0
   COL.HEAD1 = "                                  INVOICE    NET SALE     MARKET       MARK     COMMISSION   ACTUAL     ADJUST"
   COL.HEAD2 = "           SALESREP               AMOUNT      AMOUNT       COST         UP        AMOUNT      COST       COST"
   COL.LINE = DSH(30):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(11):SP(1):DSH(10):SP(1):DSH(10)
*
   PRINT CHAR(12)
   PG.NO = PG.NO + 1
   PRINT HLINE1:PG.NO
   PRINT HLINE2
   IF SELECT.TYPE = "R" THEN
*T26556 v
*     PRINT SP(44):"SUMMARY ACCRUED COMMISSIONS THROUGH ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
      PRINT SPACE(63):"THROUGH ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
*T26556 ^
   END ELSE
*T26556 v
*     PRINT SP(46):"SUMMARY ACCRUED COMMISSIONS FOR ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
      PRINT SP(65):"FOR ":PERIOD:" (":OCONV(PDATE,"D2/"):")"
*T26556 ^
   END
   PRINT
   PRINT COL.HEAD1
   PRINT COL.HEAD2
   PRINT COL.LINE
   LINE.CNT = 7
*
   SCNT = COUNT(SUM.REC(1),VM) + (SUM.REC(1) # "")
   FOR SPP = 1 TO SCNT
      P.LINE = SUM.REC(11)<1,SPP>"L#30"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(1)<1,SPP>,"MD2-")"R#11"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(2)<1,SPP>,"MD2-")"R#11"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(3)<1,SPP>,"MD2-")"R#11"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(4)<1,SPP>,"MD2-")"R#11"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(5)<1,SPP>,"MD2-")"R#11"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(6)<1,SPP>,"MD2-")"R#10"
      P.LINE = P.LINE:SP(1):OCONV(SUM.REC(7)<1,SPP>,"MD2-")"R#10"
      PRINT P.LINE
      PRINT
      LINE.CNT = LINE.CNT + 1
   NEXT SPP
   PRINT
   PRINT "*** GRAND TOTALS ***""L#30":SP(1):T.INV.AMT "R#11":SP(1):T.NET.SAMT "R#11":SP(1):T.MKT "R#11":SP(1):T.MKUP "R#11":SP(1):T.COMM "R#11":SP(1):T.ACT "R#10":SP(1):T.ADJ "R#10"
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   PRINTER OFF
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
