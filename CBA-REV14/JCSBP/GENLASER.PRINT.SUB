      SUBROUTINE GENLASER.PRINT.SUB (CONO, TARG.KEY, MAT TARG.REC, INTERPRET)
*********************************************************************
*
* PROGRAM  - GENLASER.PRINT.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/25/86
*
* DESCRIPTION
*
* This subroutine is used to print the shop-floor bar-code templates
* on a GENICOM bar code printer with cartridge 20.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JCS.CPYLIB>TARGET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      EQU HT TO CHAR(9)
      EQU CR TO CHAR(13)
      EQU EM TO CHAR(25)
      EQU ESC TO CHAR(27)
      SET.TAB = ESC:"1"
      RTABS = ESC:"2"
      COLW = 28
      CFMT = "L#":COLW
      SPL = 5
      COMPRESS.ON = ESC:" ":CHAR(16)
      COMPRESS.OFF = ESC:" ":CHAR(0)
      BOLD.ON = ESC:"O"
      BOLD.OFF = ESC:"X"
      BARCODE.ON = ESC:" ":CHAR(164)         ;* FONT = LAST DIGIT = 4
      BARCODE.OFF = ESC:" ":CHAR(0)
      PRINTER ON
*
*---- MAIN PROCESSING
*
100*
      PRINT
      PRINT BOLD.ON:TARG.NAME"L#35":BOLD.OFF:SPACE(30):OCONV(DATE(),"D2-")
      FOR RP = 1 TO 10
         PL0="";PL1=""; PL2=""; PL3=""
         FOR CP = 1 TO 3
            PL0C="";PL1C=""; PL2C=""; PL3C=""
            DEFS = TARG.DEF<1,CP,RP>
            DSCS = TARG.DSC<1,CP,RP>
            CNT = COUNT(DEFS,"\")+1
            CSZ = INT(COLW/CNT)
            FMT = "L#":CSZ
            FOR PTR = 1 TO CNT
               DEF = TRIM(FIELD(DEFS,"\",PTR))
               PFX = DEF[1,1]
               DSC = SPACE(1):TRIM(FIELD(DSCS,"\",PTR))
               BEGIN CASE
               CASE PFX = "F"
                  BC = DEF[2,99]
               CASE PFX = "C"
                  BC = ""
               CASE PFX = "1" OR PFX = "4" OR PFX = "7"
                  BC = PFX
               CASE PFX = "2" OR PFX = "8"
                  GOSUB 60000
                  OPER = DEF[2,99]
                  BC = PFX:CCTR:OPER
               CASE PFX = "3" OR PFX = "6"
                  GOSUB 60000
                  BC = PFX:CCTR
               CASE 1
                  BC = ""
               END CASE
               IF BC = "" THEN BCX = BC ELSE BCX = "*":BC:"*"
               INDNT = INT((LEN(BC)+4)/2)
               IF CP = 1 AND PTR = 1 THEN
                  PL0C = SPACE(CSZ)
                  PL1C = COMPRESS.ON:DSC:COMPRESS.OFF
                  PL2C = BARCODE.ON:BCX:BARCODE.OFF
                  PL3C = COMPRESS.ON:SPACE(INDNT):BC:COMPRESS.OFF
               END ELSE
                  IF CP = 3 AND PTR = CNT THEN
                     PL0C = PL0C:SET.TAB
                  END ELSE
                     PL0C = PL0C:SET.TAB:SPACE(CSZ)
                  END
                  PL1C = PL1C:HT:COMPRESS.ON:DSC:COMPRESS.OFF
                  PL2C = PL2C:HT:BARCODE.ON:BCX:BARCODE.OFF
                  PL3C = PL3C:HT:COMPRESS.ON:SPACE(INDNT):BC:COMPRESS.OFF
               END
            NEXT PTR
            PL0 = PL0:PL0C
            PL1 = PL1:PL1C
            PL2 = PL2:PL2C
            PL3 = PL3:PL3C
         NEXT CP
         PRINT RTABS:
         PRINT PL0
         PRINT PL1
         PRINT
         PRINT PL2
         PRINT PL2
         IF INTERPRET = "Y" THEN
            PRINT PL3
         END ELSE
            PRINT
         END
      NEXT RP
      PRINT ESC:EM:"R"
      GOTO 99999
*
*---- GET PREVIOUS COST CENTER
*
60000*
      CCTR = ""
      SDONE = 0
      FOR CP1 = CP TO 1 STEP -1 UNTIL SDONE
         FOR RP1 = 10 TO 1 STEP -1 UNTIL SDONE
            IF CP1 < CP OR RP1 < RP THEN
               SDEF = TARG.DEF<1,CP1,RP1>
               SPFX = SDEF[1,1]
               IF SPFX = "C" THEN
                  CCTR = SDEF[2,3]
                  SDONE = 1
               END
            END
         NEXT RP1
      NEXT CP1
      IF CCTR = "" THEN CCTR = FIELD(TARG.KEY,"-",1)
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER OFF
      RETURN
   END
