      SUBROUTINE POST.MS.REVERSE.SUB(CONO)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>APS.CPYLIB>COM.APS.FILE.VARS
*********************************************************************
* REVISION     - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - JCSBP
* PROGRAM      - POST.MS.REVERSE.SUB
* BY           - WALID A. YAMOUT , C.B.A
* DESCRIPTION  -
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUETE
*
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>DAILY.REVERSE
*COPY>JCS.CPYLIB>JOB.MISC
*COPY>JCS.CPYLIB>JOB.REVERSE
*COPY>JCS.CPYLIB>COST.CNTR.WIP
*COPY>JCS.CPYLIB>SPOIL.STATS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
***** MAIN PROCESSING
*
      SEQ.CNT = COUNT(DRV.SEQ,VM) + (DRV.SEQ # "")
1000  FOR MLP = SEQ.CNT TO 1 STEP -1
         DEPT = DRV.DEPT<1,MLP>; CCTR = DRV.CCTR<1,MLP>
         SEQ = DRV.SEQ<1,MLP>
         MS.ID = CONO:DRV.JOB:"!":DEPT:"!":CCTR:"!"
         MATREAD JMS.REC FROM JOB.MISC, MS.ID : SEQ ELSE
            DRV.STATUS<1,MLP> = "CANNOT LOCATE JOB.MISC - ":MS.ID:SEQ
            GOTO 1999
         END
         BEGIN CASE
         CASE JMS.OPER # DRV.OPER<1,MLP>
            DRV.STATUS<1,MLP> = "OPERATION MISMATCH"
            GOTO 1999
         CASE (JMS.COST-JMS.RC.COST) = DRV.COST<1,MLP>
            REV.ALL = 1
         CASE (JMS.COST-JMS.RC.COST) > DRV.COST<1,MLP>
            REV.ALL = 0
         CASE 1
            DRV.STATUS<1,MLP> = "COST MISMATCH"
            GOTO 1999
         END CASE
         PTR = 1
         LOOP
            LOCATE DEPT IN JOB.MS.DEPT<1>,PTR SETTING DFND ELSE
               DFND = 0; PTR = 0
            END
            IF DFND THEN
               IF CCTR = JOB.MS.CCTR<1,DFND> THEN
                  PTR = 0
               END ELSE
                  PTR = DFND + 1
               END
            END
         WHILE PTR DO REPEAT
         IF DFND = 0 THEN
            DRV.STATUS<1,MLP> = "CANNOT LOCATE MAIN DEPT/CCTR/SEQ"
            GOTO 1999
         END
         WCNT = COUNT(JMS.WIP,VM) + (JMS.WIP # "")
         WPTR = 0; TOT.WIP = 0; INV.WIP = ""
         FOR I = 1 TO 2
            IF JMS.WIP<1,1,I> + 0 <> 0 THEN
               INV.WIP<1,1,I> = 0 - JMS.WIP<1,1,I>
               TOT.WIP = TOT.WIP + JMS.WIP<1,1,I>
               WPTR = I
            END
         NEXT I
         FOR W = 2 TO WCNT
            FOR I = 1 TO WPTR
               INV.WIP<1,1,I> = INV.WIP<1,1,I> - JMS.WIP<1,W,I>
            NEXT I
         NEXT W
         TOT.RC.DCOST = 0; TOT.RC.COST = 0; TOT.RC.SALE = 0
         TOT.RC.WIP = 0; REV.WIP = ""
         IF JMS.RC.COST + 0 <> 0 THEN
            FOR S = SEQ + 1 TO JOB.MS.SEQ<1,DFND>
               MATREAD JMS.REC FROM JOB.MISC, MS.ID : S ELSE
                  DRV.STATUS<1,MLP> = "CANNOT LOCATE JOB.MISC - ":MS.ID:S
                  GOTO 1999
               END
               IF JMS.RC.TRAN = DRV.TRAN<1,MLP> THEN
                  FOR I = 1 TO WPTR
                     REV.WIP<1,1,I> = REV.WIP<1,1,I> - JMS.WIP<1,1,I>
                     TOT.RC.WIP = TOT.RC.WIP - JMS.WIP<1,1,I>
                  NEXT I
                  TOT.RC.DCOST = TOT.RC.DCOST - JMS.DCOST
                  TOT.RC.COST = TOT.RC.COST - JMS.COST
                  TOT.RC.SALE = TOT.RC.SALE - JMS.SALE
               END
            NEXT S
            MATREAD JMS.REC FROM JOB.MISC, MS.ID : SEQ ELSE
               DRV.STATUS<1,MLP> = "CANNOT LOCATE JOB.MISC - ":MS.ID:SEQ
               GOTO 1999
            END
            IF TOT.RC.COST <> JMS.RC.COST THEN
               DRV.STATUS<1,MLP> = "REVERSE COST MISMATCH ":OCONV(TOT.RC.COST,"MD2")
               GOTO 1999
            END
         END
         NEW.WIP = ""
         IF REV.ALL THEN
            NEW.DCOST = TOT.RC.DCOST - JMS.DCOST
            NEW.SALE = TOT.RC.SALE - JMS.SALE
            FOR I = 1 TO WPTR
               NEW.WIP<1,1,I> = REV.WIP<1,1,I> - JMS.WIP<1,1,I>
            NEXT I
         END ELSE
            PCT = DRV.COST<1,MLP> / (JMS.COST/1000)
            IF JMS.COST = JMS.DCOST THEN
               NEW.DCOST = 0 - DRV.COST<1,MLP>
            END ELSE
               NEW.DCOST = 0 - INT((JMS.DCOST/1000) * PCT + .5)
               IF TOT.RC.DCOST - NEW.DCOST > JMS.DCOST THEN
                  NEW.DCOST = TOT.RC.DCOST - JMS.DCOST
               END
            END
            IF JMS.SALE = JMS.COST THEN
               NEW.SALE = 0 - DRV.COST<1,MLP>
            END ELSE
               NEW.SALE = 0 - INT(((JMS.SALE-JMS.COST)/(JMS.COST/1000)) * (DRV.COST<1,MLP>/1000) + DRV.COST<1,MLP> + .5)
               IF TOT.RC.SALE - NEW.SALE > JMS.SALE THEN
                  NEW.SALE = TOT.RC.SALE - JMS.SALE
               END
            END
            BEGIN CASE
            CASE TOT.WIP = JMS.COST
               CHK.WIP = DRV.COST<1,MLP>
            CASE TOT.WIP = JMS.DCOST
               CHK.WIP = 0 - NEW.DCOST
            CASE 1
               CHK.WIP = INT((TOT.WIP/1000) * PCT + .5)
               IF TOT.RC.WIP + CHK.WIP > TOT.WIP THEN
                  CHK.WIP = TOT.WIP - TOT.RC.WIP
               END
            END CASE
            TOT.WIP = 0
            FOR I = 1 TO WPTR
               NEW.WIP<1,1,I> = 0 - INT((JMS.WIP<1,1,I>/1000) * PCT + .5)
               IF REV.WIP<1,1,I> - NEW.WIP<1,1,I> > JMS.WIP<1,1,I> THEN
                  NEW.WIP<1,1,I> = REV.WIP<1,1,I> - JMS.WIP<1,1,I>
               END
               TOT.WIP = TOT.WIP - NEW.WIP<1,1,I>
            NEXT I
            DIFF = CHK.WIP - TOT.WIP
            BEGIN CASE
            CASE DIFF = 0
            CASE DIFF > 0
               FOR I = WPTR TO 1 STEP -1 WHILE DIFF > 0
                  IF NEW.WIP<1,1,I> < 0 - DIFF THEN
                     IF REV.WIP<1,1,I>-NEW.WIP<1,1,I>+DIFF <= JMS.WIP<1,1,I> THEN
                        NEW.WIP<1,1,I> = NEW.WIP<1,1,I> - DIFF
                        TOT.WIP = TOT.WIP + DIFF
                        DIFF = 0
                     END
                  END
               NEXT I
            CASE 1
               FOR I = WPTR TO 1 STEP -1 WHILE DIFF < 0
                  IF NEW.WIP<1,1,I> < DIFF THEN
                     NEW.WIP<1,1,I> = NEW.WIP<1,1,I> - DIFF
                     TOT.WIP = TOT.WIP + DIFF
                     DIFF = 0
                  END
               NEXT I
            END CASE
            IF TOT.WIP <> CHK.WIP THEN
               DRV.STATUS<1,MLP> = "WIP MISMATCH ":OCONV(TOT.WIP,"MD2")
               GOTO 1999
            END
         END
         ADD.MON = 0; WIP.CNTR = 0
         FOR I = 1 TO WPTR
            INV.WIP<1,1,I> = INV.WIP<1,1,I> + 0
            BEGIN CASE
            CASE INV.WIP<1,1,I> < NEW.WIP<1,1,I>
               INV.WIP<1,1,I> = NEW.WIP<1,1,I>
               ADD.MON = 1; WIP.CNTR = 3
            CASE INV.WIP<1,1,I> <> 0
               ADD.MON = 1; WIP.CNTR = 2
            CASE NEW.WIP<1,1,I> <> 0
               WIP.CNTR = 2
            END CASE
         NEXT I
         IF ADD.MON THEN
            IF JMS.MON<1,WCNT> > JRV.MON THEN
               NEW.MON = JMS.MON<1,WCNT>
            END ELSE
               NEW.MON = JRV.MON
            END
            WCNT = WCNT + 1
            JMS.MON<1,WCNT> = NEW.MON
            JMS.WIP<1,WCNT> = INV.WIP
            CCW.ID = CONO : JMS.DIV : DEPT : "!" : CCTR : NEW.MON
            MATREADU CCW.REC FROM COST.CNTR.WIP, CCW.ID ELSE MAT CCW.REC = ""
            FOR I = 1 TO WPTR
               CCW.MS.O<1,I> = CCW.MS.O<1,I> - INV.WIP<1,1,I>
            NEXT I
            MATWRITE CCW.REC ON COST.CNTR.WIP, CCW.ID
         END ELSE
            IF JMS.MON<1,1> > JRV.MON THEN
               NEW.MON = JMS.MON<1,1>
            END ELSE
               NEW.MON = JRV.MON
            END
         END
         IF WIP.CNTR THEN
            FOR I = 1 TO WPTR
               NEW.WIP<1,2,I> = 0 - NEW.WIP<1,1,I>
            NEXT I
            NEW.MON<1,2> = NEW.MON<1,1>
         END
         JMS.RC.COST = JMS.RC.COST + DRV.COST<1,MLP>
         MATWRITE JMS.REC ON JOB.MISC, MS.ID : SEQ
         JMS.RC.COST = 0
         JMS.COST = 0 - DRV.COST<1,MLP> 
         JMS.DCOST = NEW.DCOST 
         JMS.SALE = NEW.SALE 
         JMS.DATE = DRV.DATE
         JMS.MON = NEW.MON
         JMS.WIP = NEW.WIP
         JMS.RC.TRAN = DRV.TRAN<1,MLP>
         JMS.RC.JOB = DRV.JOB
         JMS.RC.DESC = DRV.REASON<1,MLP>
         IF WIP.CNTR THEN
            JOB.MS.WIP<1,1> = JOB.MS.WIP<1,1> + WIP.CNTR
            JOB.WIP<1,1> = JOB.WIP<1,1> + WIP.CNTR
            FOR I = 1 TO WPTR
               JOB.MS.WIP<1,2,I> = JOB.MS.WIP<1,2,I> + INV.WIP<1,1,I>
               JOB.MS.WIP<1,3,I> = JOB.MS.WIP<1,3,I> - INV.WIP<1,1,I> + JMS.WIP<1,1,I>
               JOB.WIP<1,2,I> = JOB.WIP<1,2,I> + INV.WIP<1,1,I>
               JOB.WIP<1,3,I> = JOB.WIP<1,3,I> - INV.WIP<1,1,I> + JMS.WIP<1,1,I>
            NEXT I
         END
         IF JMS.MON<1,1> < JOB.MS.WIP<1,4> OR JOB.MS.WIP<1,4> = "" THEN
            JOB.MS.WIP<1,4> = JMS.MON<1,1>
            IF JMS.MON<1,1> < JOB.WIP<1,4> OR JOB.WIP<1,4> = "" THEN
               JOB.WIP<1,4> = JMS.MON<1,1>
            END
         END
         IF JMS.MON<1,1> > JOB.MS.WIP<1,5> THEN
            JOB.MS.WIP<1,5> = JMS.MON<1,1>
            IF JMS.MON<1,1> > JOB.WIP<1,5> THEN
               JOB.WIP<1,5> = JMS.MON<1,1>
            END
         END
         JOB.MS.DCOST<1,DFND> = JOB.MS.DCOST<1,DFND> + JMS.DCOST
         JOB.MS.COST<1,DFND> = JOB.MS.COST<1,DFND> + JMS.COST
         JOB.MS.SALE<1,DFND> = JOB.MS.SALE<1,DFND> + JMS.SALE
         BEGIN CASE
         CASE JMS.TYPE = "N"
            PTR = 2
         CASE JMS.TYPE = "C"
            PTR = 3
         CASE JMS.TYPE = "S"
            PTR = 4
            IF JMS.SPOIL.CODE # "" THEN
               SSR.ID=CONO:JMS.DIV:DEPT:"!":CCTR:NEW.MON<1,1>:JMS.SPOIL.CODE
               MATREADU SSR.REC FROM SPOIL.STATS,SSR.ID ELSE MAT SSR.REC = ""
               SSR.MS<1,1> = SSR.MS<1,1> + JMS.DCOST
               IF JMS.DCOST <> JMS.COST THEN
                  SSR.MS<1,2> = SSR.MS<1,2> + JMS.COST - JMS.DCOST
               END
               MATWRITE SSR.REC ON SPOIL.STATS,SSR.ID
            END
         CASE 1
            PTR = 1
         END CASE
         JOB.MS.TCOST<1,DFND,PTR> = JOB.MS.TCOST<1,DFND,PTR> + JMS.COST
         JOB.TOT.DCOST = JOB.TOT.DCOST + JMS.DCOST
         JOB.TOT.COST = JOB.TOT.COST + JMS.COST
         JOB.TOT.SALE = JOB.TOT.SALE + JMS.SALE
         READU JMS.SEQ FROM CONTROL, CONO:"DAILY.MISC" ELSE JMS.SEQ = 10001
         NEXT.SEQ = JMS.SEQ + 1
         WRITE NEXT.SEQ ON CONTROL,CONO:"DAILY.MISC"
         JOB.MS.SEQ<1,DFND> = JOB.MS.SEQ<1,DFND> + 1
         JMS.GLA.DATE = ""
         MATWRITE JMS.REC ON JOB.MISC, MS.ID:JOB.MS.SEQ<1,DFND>
         PPTR = COUNT(JRV.TRAN,VM) + (JRV.TRAN # "") + 1
         JRV.TRAN<1,PPTR> = DRV.TRAN<1,MLP>
         JRV.TTRAN<1,PPTR> = JMS.SEQ
         JRV.REASON<1,PPTR> = DRV.REASON<1,MLP>
         JRV.COST<1,PPTR> = DRV.COST<1,MLP>
         JRV.SEQ<1,PPTR> = SEQ
         JRV.TSEQ<1,PPTR> = JOB.MS.SEQ<1,DFND>
         DRV.DEPT = DELETE(DRV.DEPT,1,MLP,0)
         DRV.CCTR = DELETE(DRV.CCTR,1,MLP,0)
         DRV.OPER = DELETE(DRV.OPER,1,MLP,0)
         DRV.EMP = DELETE(DRV.EMP,1,MLP,0)
         DRV.TRAN = DELETE(DRV.TRAN,1,MLP,0)
         DRV.REASON = DELETE(DRV.REASON,1,MLP,0)
         DRV.COST = DELETE(DRV.COST,1,MLP,0)
         DRV.STATUS = DELETE(DRV.STATUS,1,MLP,0)
         DRV.CONV = DELETE(DRV.CONV,1,MLP,0)
         DRV.QTY = DELETE(DRV.QTY,1,MLP,0)
         DRV.SEQ = DELETE(DRV.SEQ,1,MLP,0)
1999  NEXT MLP
99999 RETURN
   END
