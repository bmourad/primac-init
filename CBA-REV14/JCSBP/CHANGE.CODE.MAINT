*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - CHANGE.CODE.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 01/21/85
* DESCRIPTION -
* This program will update and maintain CHANGE.ORD.CODE file.
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'M.SCREENS FILE MISSING';GOTO 93000
  OPEN '','CHANGE.ORD.CODE' TO CHANGE.ORD.CODE ELSE ERRMSG = 'CHANGE.ORD.CODE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  MAT EDIT.COM = ''
  MAT EDIT.COM.DRIVER = ''
  ERRMSG = ''
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'CHANGE.CODE.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
  SCV.REC(5)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=5
  ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  CHANGE.ORD.CODE.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.CHANGE.ORD.CODE = 'NO'
      * T26126 v
      READU COC.DESC FROM CHANGE.ORD.CODE,CONO:CHANGE.ORD.CODE.KEY LOCKED
        ERRMSG = 'CHANGE ORDER record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE
      * T26126 ^
        COC.DESC=''
        NEW.CHANGE.ORD.CODE = 'YES'
      END
  END CASE
  IF NEW.CHANGE.ORD.CODE = 'YES' THEN 
    GOSUB 20
    IF ECD.RET.VALUE = 'END' THEN GOTO 10
  END ELSE
    SCV.REC(2)<ECD.SCRN.NO,1>=COC.DESC
    ECD.NUM=2;ECD.ACTION = 5; CALL SCRN.EDIT
  END
  GOSUB 100
  GOSUB 200
  GOTO 10
*
*---- ENTER CONTACT CODE DESCRIPTION
*
20*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    COC.DESC = ECD.RET.VALUE
  END ELSE
    RELEASE CHANGE.ORD.CODE,CONO:CHANGE.ORD.CODE.KEY
    GOSUB 200
    GOTO 25
  END
25*
  RETURN
*
*---- ENTER OPTIONS
*
100*
  MORE = 0
  LOOP
    ECD.NUM = 4
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE CHANGE.ORD.CODE,CONO:CHANGE.ORD.CODE.KEY       ;* T26126
        MORE = 1
      CASE OPTION = '1'
        GOSUB 20
      CASE OPTION = 'F'
        GOSUB 300
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
200*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE CHANGE.ORD.CODE FILE
*
300*
  WRITE COC.DESC ON CHANGE.ORD.CODE,CONO:CHANGE.ORD.CODE.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
END
