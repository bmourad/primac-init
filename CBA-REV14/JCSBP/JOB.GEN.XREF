      SUBROUTINE JOB.GEN.XREF(MAT GEN.XREF.REC,PREFIX)
*COPY>CPYLIB>COM3
*******************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC-JCS
* SOURCE      - JCSBP
* PROGRAM     - JOB.GEN.XREF
* BY          - WALID YAMOUT, C.B.A
* DATE        - 08/06/86
* DESCRIPTION -
* COMMENT     - REVISED JOB.STATUS TO ACCEPT "5" - "REOPENED"
* TASK #12907 (RRG)
*ENDDOC
*******************************************************
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>TCC
*COPY>CPYLIB>GEN.XREF
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
***** SETUP ERRMSG ROUTINE FOR SYSCOM
*
      IF PC.PORT.TYPE = "termulator" THEN
         CALL JOB_GNXRF_TU(MAT GEN.XREF.REC,PREFIX)
         GOTO 99999
      END
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*     MAT EDIT.COM = ''
*     TYP = 0
*     CALL EDIT.SUB
*     FILL = '#'
      GXR.ACTION = ""
      DESC.LEN = 50
      IF GXR.SRCH.ID = '' THEN
         GXR.ACTION = 'X'
         GOTO 99999
      END ELSE
         DISP.CNT = COUNT(GXR.HEADING,VM) + 1
      END
      TOP.CNT = COUNT(GXR.TOP.LINE, VM) + (GXR.TOP.LINE # "")
      SCRN.SIZE = 17 - TOP.CNT
      BEG.SCRN = 3 + TOP.CNT
      END.SCRN = 20
      SCRN.LINE = ''
*
      MATREAD CUST.REC FROM GXR.XREF, GXR.CO : GXR.SRCH.ID ELSE MAT CUST.REC = ""
      IF CUST.JOB = "" THEN
         GXR.ACTION = 'X'
         GOTO 99999
      END
      REC.CNT = COUNT(CUST.JOB,VM) + 1
      F.M = "L#":DESC.LEN
      HDR.LINE = " SEL JOB NUM  " : "DESCRIPTION" F.M : " " : "STATUS"
      DSH.LINE = "---- -------- " : STR("-",DESC.LEN) : " --------------"
      P_X  = 0 ; P_Y = 0 ; P_VALUE = CS ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      FOR PP = 1 TO TOP.CNT
         SP = 68 - LEN(GXR.TOP.LINE<1,PP>)
         SP = INT(SP/2)
         IF PP = 1 THEN
            HD.LINE ='PRIMAC':STR(' ',SP):GXR.TOP.LINE<1,PP>:STR(' ',SP):'[08.1]'
         END ELSE
            HD.LINE = SPACE(5):STR(' ',SP):GXR.TOP.LINE<1,PP>
         END
         P_X  = 0 ; P_Y = PP-1 ; P_VALUE = HD.LINE "L#80" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT PP
      P_X  = 0 ; P_Y = TOP.CNT ; P_VALUE = STR('-',80) ; P_OPT = ""
      P_X  := AM:0 ; P_Y := AM:TOP.CNT+1 ; P_VALUE := AM:HDR.LINE "L#80"
      P_X  := AM:0 ; P_Y := AM:TOP.CNT+2 ; P_VALUE := AM:DSH.LINE "L#80"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      SCRN = 1
      TOTAL.SCRNS = INT((REC.CNT / SCRN.SIZE) + .99)
1000*
      START.LINE = ((SCRN - 1) * SCRN.SIZE) + 1
      END.LINE = START.LINE + SCRN.SIZE - 1
      IF END.LINE > REC.CNT THEN END.LINE = REC.CNT
      FOR PRT.LINE = BEG.SCRN TO END.SCRN
         P_X  = 0 ; P_Y = PRT.LINE ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT PRT.LINE
      PRT.LINE = BEG.SCRN
      FOR PLP = START.LINE TO END.LINE
         JOBNO = CUST.JOB<1,REC.CNT-PLP+1>
         MATREAD JOB.REC FROM GXR.FILE, GXR.CO : JOBNO ELSE
            GXR.ITEM = ''
            MAT JOB.REC = ""
            JOB.DESC = STR("?",39)
            JOB.STATUS = STR("?",13)
         END
         SCRN.LINE = PLP "R#4" : ' ' : JOBNO "L#8"
         SCRN.LINE = SCRN.LINE : ' ' : JOB.DESC<1,1> F.M
         STATUS = ""
         BEGIN CASE
         CASE JOB.STATUS<1,1> = ""
            STATUS = "BOOKED"
         CASE JOB.STATUS<1,1> = "7"
            STATUS = "READY TO PURGE"
         CASE JOB.STATUS<1,1> = "8"
            STATUS = "WAS PURGED"
         CASE JOB.STATUS<1,1> = "9"
            STATUS = "CANCELLED"
         CASE JOB.TRACK.DATE<1,10> # ""
            BEGIN CASE
            CASE JOB.STATUS<1,1> = "1"
               STATUS = "IN PROCESS"
            CASE JOB.STATUS<1,1> = "5"
               STATUS="REOPENED"
            CASE 1
               STATUS = "COMPLETED"
            END CASE
         CASE JOB.TRACK.DATE<1,9> # ""
            BEGIN CASE
            CASE JOB.STATUS<1,1> = "1"
               STATUS = "IN PROCESS"
            CASE JOB.STATUS<1,1> = "5"
               STATUS="REOPENED"
            CASE 1
               STATUS = "INVOICED"
            END CASE
         CASE JOB.TRACK.DATE<1,8> # ""
            STATUS = "COSTED"
         CASE JOB.TRACK.DATE<1,7> # ""
            STATUS = "RDY-TO-BILL"
         CASE JOB.TRACK.DATE<1,6> # ""
            STATUS = "DELIVERED"
         CASE JOB.TRACK.DATE<1,5> # ""
            STATUS = "IN PROCESS"
         CASE JOB.TRACK.DATE<1,3> # ""
            STATUS = "IN PROCESS"
         CASE JOB.STATUS<1,1> = "1"
            STATUS = "IN PROCESS"
         CASE 1
            STATUS = JOB.STATUS<1,1>
         END CASE
         SCRN.LINE = SCRN.LINE : ' ' : STATUS "L#14"
         PRT.LINE = PRT.LINE + 1
         P_X  = 0 ; P_Y = PRT.LINE ; P_VALUE = SCRN.LINE "L#80" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT PLP
2000*
      IF SCRN = TOTAL.SCRNS THEN
         P_X  = 0 ; P_Y = 23 ; P_VALUE = '* * LAST SCREEN * *' ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END ELSE
         P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      X = 0
      Y = 22
      TYP = 1
      MAXL = 4
      PMSG = 'Please enter selection #, (N)one, (F)orward, (B)ack :'
      CALL EDIT.SUB
      BEGIN CASE
      CASE VALUE = 'END' OR VALUE = 'N'
         GXR.ACTION = ''
         GOTO 99999
      CASE VALUE = 'F'
         IF SCRN = TOTAL.SCRNS THEN
            ERRMSG = '* * THIS IS THE LAST SCREEN * *'
            GOSUB 91000
         END ELSE
            SCRN = SCRN + 1
            GOTO 1000
         END
      CASE VALUE = 'B'
         IF SCRN = 1 THEN
            ERRMSG = '* * THIS IS THE FIRST SCREEN * *'
            GOSUB 91000
         END ELSE
            SCRN = SCRN - 1
            GOTO 1000
         END
      CASE NUM(VALUE)
         IF VALUE >= 1 AND VALUE <= REC.CNT THEN
            GXR.ID = CUST.JOB<1,REC.CNT-VALUE+1>
            GOTO 99999
         END ELSE
            ERRMSG = '* * INVALID SELECTION * *'
            GOSUB 91000
         END
      CASE 1
         ERRMSG = '* * INVALID SELECTION * *'
         GOSUB 91000
      END CASE
      GOTO 2000
91000 ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999*
      RETURN
   END
