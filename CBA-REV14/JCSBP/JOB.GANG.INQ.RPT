   SUBROUTINE JOB.GANG.INQ.RPT(CONO,CO.NAME,JOB.NUM,HD1,HD2)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JOB.INQ
*********************************************************************
* PROGRAM     - JOB.GANG.INQ.RPT
* AUTHOR      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE        - 11/23/92
* DESCRIPTION
* This program produces the gang job allocation report for gang jobs.
*T26493 cmykleb 04/03/2002 * Change pgm to use headings from calling pgm.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>GANG.JOB
*COPY>JCS.CPYLIB>GANG.ALLOC
*COPY>ICS.CPYLIB>INVENTORY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   PAGE.NO = 0
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   DASHES = STR("-",80)
   LM1A = SPACE(3)
   LM1B = SPACE(10)
   LM2A = SPACE(3)
   LM2B = SPACE(10)
   LINE.COUNT = 99
*T26493 v
   HD3 = "JOB # : ":JOB.NUM"L#8":SPACE(47)
   HD3 = HD3 : "GANG JOB ALLOCATION REPORT"
*T26493 ^
*
*---- OPEN ALL FILES
*
   OPEN "","GANG.JOB" TO GANG.JOB ELSE GOTO 99999
   OPEN "","GANG.ALLOC" TO GANG.ALLOC ELSE GOTO 99999
*
*---- MAIN PROCESSING
*
100 *
   MATREAD GJOB.REC FROM GANG.JOB, CONO:JOB.NUM ELSE GOTO 99999
   GOSUB 10000
   GOSUB 10100
   GOSUB 10200
   LINE.CNT = DCOUNT(GJOB.JOB,VM)
   FOR GJPTR = 1 TO LINE.CNT
      TEMP.JOB = GJOB.JOB<1,GJPTR>
      TEMP.PROD = GJOB.PROD<1,GJPTR>
      IF TEMP.PROD = "" THEN
         TEMP.DESC = GJOB.PDESC<1,GJPTR>
      END ELSE
         MATREAD INV.REC FROM INVENTORY, CONO:TEMP.PROD ELSE
            MAT INV.REC = ""
         END
         TEMP.DESC = INV.DESC
      END
      TEMP.WIDTH = GJOB.WIDTH<1,GJPTR>
      TEMP.LENGTH = GJOB.LENGTH<1,GJPTR>
      TEMP.SPOTS = GJOB.SPOTS<1,GJPTR>
      TEMP.PCT = GJOB.PCT<1,GJPTR>
      GOSUB 1000
   NEXT GJPTR
*
   IF GJOB.ALLOC = "" THEN
      MAT GAT.REC = ""
      GAT.DESC = GJOB.DESC
      GAT.DEPT = GJOB.DEPT
      GAT.CCTR = GJOB.CCTR
      GAT.TYPE = GJOB.TYPE
   END ELSE
      MATREAD GAT.REC FROM GANG.ALLOC, CONO:GJOB.ALLOC ELSE
         MAT GAT.REC = ""
         GAT.DESC = "???????????????"
      END
   END
   GOSUB 10300
   GOSUB 10400
   DCNT = DCOUNT(GAT.DEPT,VM)
   FOR DPTR = 1 TO DCNT
      TEMP.DEPT = GAT.DEPT<1,DPTR>
      TEMP.CCTR = GAT.CCTR<1,DPTR>
      TEMP.TYPE = GAT.TYPE<1,DPTR>
      BEGIN CASE
         CASE TEMP.TYPE = "A"
            TEMP.TYPE = "AREA"
         CASE TEMP.TYPE = "S"
            TEMP.TYPE = "SPOTS"
         CASE TEMP.TYPE = "B"
            TEMP.TYPE = "AREA & SPOTS"
         CASE TEMP.TYPE = "P"
            TEMP.TYPE = "PERCENT"
      END CASE
      IF TEMP.CCTR = "" THEN
         MAT CCTR.REC = ""
      END ELSE
         MATREAD CCTR.REC FROM COST.CNTR, CONO:TEMP.CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "???????????????"
         END
         TEMP.DEPT = CCTR.DEPT
      END
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:TEMP.DEPT ELSE
         MAT DEPT.REC = ""
         DEPT.DESC = "???????????????"
      END
      GOSUB 1200
   NEXT DPTR
   GOTO 99999
*
*---- PRINT JOB ALLOCATION DETAIL LINE
*
1000 *
   IF (LINE.COUNT+1) > 55 THEN
      GOSUB 10000
      GOSUB 10200
   END
   PLINE = TEMP.JOB"L#8"
   PLINE = PLINE:SP2:TEMP.PROD"L#15"
   PLINE = PLINE:SP2:TEMP.DESC"L#30"
   PLINE = PLINE:SP2:OCONV(TEMP.WIDTH,"MD4")"R#8"
   PLINE = PLINE:SP2:OCONV(TEMP.LENGTH,"MD4")"R#8"
   PLINE = PLINE:SP2:TEMP.SPOTS"R#5"
   PLINE = PLINE:SP2:TEMP.PCT"R#3"
   PRINT LM1B:PLINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
*
*---- PRINT ALLOCATION TABLE DETAIL LINE
*
1200 *
   IF (LINE.COUNT+1) > 55 THEN
      GOSUB 10000
      GOSUB 10400
   END
   PLINE = TEMP.DEPT"L#5"
   PLINE = PLINE:SP2:DEPT.DESC"L#20"
   PLINE = PLINE:SP2:TEMP.CCTR"L#3"
   PLINE = PLINE:SP2:CCTR.DESC"L#30"
   PLINE = PLINE:SP2:TEMP.TYPE"L#12"
   PRINT LM2B:PLINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
*
*---- PRINT HEADING
*
10000 *
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
*T26493 v
*  HLINE = SPACE(2):"DATE - ":OCONV(DATE(),"D2/")"L#8"
*  HLINE = HLINE:SPACE(5):"TIME - ":OCONV(TIME(),"MTS")"L#8"
*  HLINE = HLINE:SPACE(22):CO.NAME"L#30"
*  HLINE = HLINE:SPACE(15):"PAGE - ":PAGE.NO"R#3"
*  PRINT HLINE
*  PRINT SPACE(48):"G A N G   J O B   A L L O C A T I O N"
*  PRINT SPACE(48):"           FOR JOB : ":JOB.NUM"L#8"
*  PRINT SPACE(48):DASHES[1,37]
   PRINT HD1:PAGE.NO"R#3"
   PRINT HD2
   PRINT HD3
   PRINT
*T26493 ^
   LINE.COUNT = 4
   RETURN
*
*---- PRINT HEADER INFORMATION
*
10100 *
   IF (LINE.COUNT+9) > 55 THEN GOSUB 10000
   TOT.AREA = INT(GJOB.IW / 10 * GJOB.IL / 10 + 0.5)
   CALC.AREA = 0
   LINE.CNT = DCOUNT(GJOB.JOB,VM)
   FOR CREF = 1 TO LINE.CNT
      CALC.AREA = CALC.AREA + INT(GJOB.WIDTH<1,CREF> / 1000 * GJOB.LENGTH<1,CREF> / 1000 * GJOB.SPOTS<1,CREF> + 0.5)
   NEXT CREF
   IF TOT.AREA = 0 THEN
      CALC.WASTE = ""
   END ELSE
      CALC.WASTE = INT((1-(CALC.AREA/TOT.AREA))*10000+0.5)
   END
   PRINT
   PRINT
   PLINE = "IMPRESSION SIZE : "
   PLINE = PLINE:OCONV(GJOB.IW,"MD2")
   PLINE = PLINE:" x "
   PLINE = PLINE:OCONV(GJOB.IL,"MD2")
   PLINE = PLINE"L#60"
   PLINE = PLINE:"CALCULATED SIZE  : "
   PLINE = PLINE:OCONV(TOT.AREA,"MD2,")"R#8":" sq in"
   PRINT LM1A:PLINE
   PLINE = """L#60"
   PLINE = PLINE:"CALCULATED USAGE : "
   PLINE = PLINE:OCONV(CALC.AREA,"MD2,")"R#8":" sq in"
   PRINT LM1A:PLINE
   LINE.COUNT = LINE.COUNT + 4
   IF CALC.WASTE # "" THEN
      PLINE = """L#60"
      PLINE = PLINE:"CALCULATED WASTE : "
      PLINE = PLINE:OCONV(CALC.WASTE,"MD2")"R#8":" %"
      PRINT LM1A:PLINE
      LINE.COUNT = LINE.COUNT + 1
   END
   RETURN
*
*---- PRINT JOB ALLOCATION SUB-HEADINGS
*
10200 *
   PRINT
   HLINE = " JOB NO     PRODUCT ID              DESCRIPTION              WIDTH    LENGTH   SPOTS   % "
   PRINT LM1B:HLINE
   HLINE = "--------  ---------------  ------------------------------  --------  --------  -----  ---"
   PRINT LM1B:HLINE
   LINE.COUNT = LINE.COUNT + 3
   RETURN
*
*---- PRINT HEADER INFORMATION
*
10300 *
   IF (LINE.COUNT+7) > 55 THEN GOSUB 10000
   PRINT
   PRINT
   IF GJOB.ALLOC = "" THEN
      PLINE = "ALLOCATION DESC : "
      PLINE = PLINE:GAT.DESC
      PRINT LM2A:PLINE
   END ELSE
      PLINE = "ALLOCATION TABLE : "
      PLINE = PLINE:GJOB.ALLOC:" - "
      PLINE = PLINE:GAT.DESC
      PRINT LM2A:PLINE
   END
   LINE.COUNT = LINE.COUNT + 3
   RETURN
*
*---- PRINT ALLOCATION TABLE SUB-HEADINGS
*
10400 *
   PRINT
   HLINE = "DEPT       DESCRIPTION       CTR           DESCRIPTION             ALLOC TYPE "
   PRINT LM2B:HLINE
   HLINE = "-----  --------------------  ---  ------------------------------  ------------"
   PRINT LM2B:HLINE
   LINE.COUNT = LINE.COUNT + 3
   RETURN
*
*---- END OF PROGRAM
*
99999 *
   RETURN
END
