*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - FCTR.MAINT
* BY          - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/15/83
* DESCRIPTION -
*T26126 adelgado 03/07/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>FACTOR
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
**************
* OPEN FILES *
**************
  MAT FILE.VARS = ''
  OPEN '', 'JCS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'JCS.SCREENS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'FACTOR' TO FACTOR ELSE
    ERRMSG = 'FACTOR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  MAT COMP.REC = ''
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
******************
* INITIALIZATION *
******************
  MAT EDIT.COM.DRIVER = ''
  DASHES = STR('-',80)
  ERRMSG = ''
  OLD.NAME = ''
*******************
* MAIN PROCESSING *
*******************
100*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'FCTR.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
200*
  MAT FCTR.REC = ''
  MAT SCV.REC = ''
  ECD.ACTION=6;CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.VALDAT.CODE = 4
  ECD.VALDAT.FILE = FACTOR
  ECD.PREFIX.ID = CONO
  ECD.ACTION=4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # '' AND  ECD.VALDAT.ITEM # ''
      * T26126 v
      READU DUMMY FROM FACTOR, CONO:ECD.RET.VALUE LOCKED
        ERRMSG = 'FACTOR record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000 ; GOTO 200
      END ELSE NULL
      * T26126 ^
      FOR I = 1 TO FCTR.REC.SIZE
        FCTR.REC(I) = ECD.VALDAT.ITEM<I>
      NEXT I
      FCTR.ID = ECD.RET.VALUE
      GOSUB 20000
      ECD.ACTION=3;CALL SCRN.EDIT
      OLD.NAME = FCTR.DESC
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
      IF LEN(ECD.RET.VALUE) > 2 THEN
        ERRMSG = "* * Max length is two digits * *"
        GOSUB 91000
        GOTO 200
      END
      FCTR.ID = ECD.RET.VALUE
      FOR MLP = 1 TO 3
        ON MLP GOSUB 1000,2000,3000
        IF ECD.RET.VALUE = 'END' OR ECD.RET.VALUE = '' THEN GOTO 200
      NEXT MLP
      GOSUB 20000
      ECD.ACTION=3;CALL SCRN.EDIT
    CASE 1
      GOTO 200
  END CASE
  MORE = 1
  LOOP
    ECD.NUM = 5
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
    ECD.ACTION=4;CALL SCRN.EDIT
    REQUEST = ECD.RET.VALUE
    BEGIN CASE
      CASE REQUEST = 'END' OR REQUEST = 'E'
        RELEASE         ;* T26126
        MORE = 0
      CASE NUM(REQUEST)
        IF REQUEST > 0 THEN
          ON REQUEST GOSUB 1000,2000,3000
        END
      CASE REQUEST = 'F'
        MATWRITE FCTR.REC ON FACTOR, CONO : FCTR.ID
        MORE = 0
    END CASE
  WHILE MORE DO REPEAT
  GOTO 200
1000*
  ECD.NUM = 2
  ECD.ACTION=4;CALL SCRN.EDIT
  FCTR.DESC = ECD.RET.VALUE
  RETURN
2000*
  ECD.NUM = 3
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN FCTR.PRCNT = ECD.RET.VALUE
  RETURN
3000*
  ECD.NUM = 4
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN FCTR.EMP.PRCNT = ECD.RET.VALUE
  RETURN
20000*
  SCV.REC(1)<ECD.SCRN.NO> = FCTR.ID
  SCV.REC(2)<ECD.SCRN.NO> = FCTR.DESC
  SCV.REC(3)<ECD.SCRN.NO> = FCTR.PRCNT
  SCV.REC(4)<ECD.SCRN.NO> = FCTR.EMP.PRCNT
  RETURN
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
