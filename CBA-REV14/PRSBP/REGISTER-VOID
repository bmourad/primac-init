*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - REGISTER-VOID
* BY          - WALID YAMOUT , C.B.A
* DATE        - 06/11/85
* DESCRIPTION -
* MOD TASK    - 13400]14345
* MOD BY      - MT]RRG
* MOD DATE    - 2/13/89]8/9/89
* MOD DESC    - Process multiple payrolls simultaneously]Remove
*             - multi-batch logic
*ENDDOC
*********************************************************************
*
*--- INSERT CPYLIB
*
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PRS.CPYLIB>PAYREG
*COPY>PRS.CPYLIB>DEDREG
*COPY>PRS.CPYLIB>PAYROLL.HOURS
*COPY>PRS.CPYLIB>HEADER
*COPY>CPYLIB>CHAR
      TOT.CCTR = ""
      TOT.HRS.CCTR = ""
      TOT.ERN.CCTR = ""
      TOT.INC.CCTR = ""
      TOT.DED.CCTR = ""
      TOT.GRS.CCTR = ""
      TOT.DEPT = ""
      TOT.HRS.DEPT = ""
      TOT.ERN.DEPT = ""
      TOT.INC.DEPT = ""
      TOT.DED.DEPT = ""
      TOT.GRS.DEPT = ""
      TOT.DIV = ""
      TOT.HRS.DIV = ""
      TOT.ERN.DIV = ""
      TOT.INC.DIV = ""
      TOT.DED.DIV = ""
      TOT.GRS.DIV = ""
      PROCREAD IBUF ELSE PRINT "CAN'T READ INPUT BUFFER":;INPUT X;STOP
      CONO=IBUF<1>
      BATCH.NO = EXTRACT(IBUF,3,0,0)
      OPEN "","PAYREG" TO PAYREG ELSE PRINT "CAN'T OPEN PAYREG";STOP
      OPEN "","DEDREG" TO DEDREG ELSE PRINT "CAN'T OPEN DEDREG";STOP
      OPEN "","PAYTYPE" TO PAYTYPE ELSE PRINT "CAN'T OPEN PAYTYPE";STOP
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE";STOP
      OPEN "","COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY";STOP
      OPEN "","DEPT" TO DEPARTMENT ELSE PRINT "CANT OPEN DEPT";STOP
      OPEN "","DIVISION" TO DIVISION ELSE PRINT "CAN'T OPEN DIVISION";STOP
      OPEN "","COST.CNTR" TO COST.CNTR ELSE PRINT "CAN'T OPEN COST.CNTR";STOP
      OPEN "","CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL";STOP
      MATREAD HRS.REC FROM CONTROL, CONO:"PAYROLL.HOURS" ELSE PRINT "PAYROLL HOURS NOT SET UP ON CONTROL";STOP
      MATREAD PTHR.REC FROM PAYTYPE, CONO:"HEADER" ELSE
         PRINT 'PAYTYPE HEADER record not set-up'
         INPUT X;STOP
      END
      PDEND = OCONV(PTHR.END.DATE,"D2/")
      PAYDATE = OCONV(PTHR.CHK.DATE,"D2/")
      HRS.CNT = COUNT(HRS.DESC,VM) + (HRS.DESC # "")
*****   
** HEADING ROUTINE   
      IF BATCH.NO = "ALL" THEN BATNO = SPACE(20) ELSE BATNO = "BATCH #: ":BATCH.NO:SPACE(8)
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         MAT COMP.REC = ""
         CO.NAME = "NOT ON FILE"
      END
      SP2 = SPACE(2)
      SP1 = SPACE(1)
      SP3 = SPACE(3)
      SP5 = SPACE(5)
      SP8 = SPACE(8)
      SP23 = SPACE(23)
      SP40 = SPACE(40)
      D2 = STR("-",2)
      D3 = STR("-",3)
      POS1 = 51 - INT(LEN(CO.NAME)/2)
      POS2 = 26 + INT(LEN(CO.NAME)/2)
      HEAD1 = "DATE - " : OCONV(DATE(), "D2/") "L#8"
      HEAD1 = HEAD1 : SPACE(POS1) : CO.NAME "L#30" : SPACE(POS2) : "PAGE - "
      HEAD2=BATNO:"H A N D   A N D   V O I D   R E G I S T E R   F O R"
      HEAD2 = HEAD2 : "   P E R I O D   E N D I N G   " : PDEND : "'LL'"
      HEAD2 = HEAD2 : SP40:SP8:SP8 : "PAY DATE : " : PAYDATE : "'LL'"
      HEAD3 = "EMPLOYEE NUMBER AND NAME" : SP5:SP5:SP5:SP3 : "REC" :SP2: "DV" :SP2
      HEAD3 = HEAD3 : "DEPT" :SP3: "CTR" :SP2: "SHIFT" :SP2: "SALARY/RATE" :SP3: "TYPE"
      HEAD3 = HEAD3 :SP2:SP2: "HOURS" :SP3: " EARNINGS" :SP3: "OTH-INCOME"
      HEAD3 = HEAD3 :SP2: "CODE"
      HEAD4 = STR("-",40) :SP2: D3 :SP2: D2 :SP2: D3:D2 :SP2: D3 :SP2: D2:D3 :SP2
      HEAD4 = HEAD4 : STR("-",11) :SP2: D2:D2:D3 :SP2: D3:D3 :SP2: STR("-",10)
      HEAD4 = HEAD4 :SP2: STR("-",10) :SP2: D2:D2 
      PRINTER ON
      LINE.COUNT = 99
      PAGE.NO = 0
      PREV.HOME = ""
*
*--- REGISTER ROUTINE
*
1000 *
      READNEXT ID ELSE GOTO 2000
      TOT.HOURS = 0; TOT.DED = 0
      MATREAD PAY.REC FROM PAYREG, ID ELSE MAT PAY.REC = ""; GOTO 1000
      MATREAD EMP.REC FROM EMPLOYEE, ID[1,7] ELSE
         MAT EMP.REC = ""; EMP.FRST.NAME = "EMPLOYEE NOT ON FILE"
      END
      IF PAY.STATUS = "DELETE" THEN
         PAY.CHECK.NO = PAY.STATUS
         PAY.OTH.INC = ""
         PAY.OTH.CODE = ""
         PAY.GROSS = ""
         PAY.TOT.HRS = ""
         PAY.HOURS = ""
         PAY.EARNINGS = ""
         PAY.NON.TAXED = ""
         PAY.TAXED = ""
         PAY.NET = ""
         MAT DED.REC = ""
         DED.DIV = EMP.DIV
         DED.DEPT = EMP.DEPT
         DED.CCTR = EMP.COST.CNTR
      END ELSE
         IF PAY.CHECK.NO = "" THEN GOTO 1000
         MATREAD DED.REC FROM DEDREG, ID ELSE MAT DED.REC = ""; GOTO 1000
      END
      IF PREV.HOME # EMP.DEPT THEN
         PREV.HOME = EMP.DEPT
         MATREAD DEPT.REC FROM DEPARTMENT, CONO : PREV.HOME ELSE MAT DEPT.REC = ""; DEPT.DESC = "NOT ON FILE"
         GOSUB 70000
      END
      IF EMP.STATUS = "S" THEN DC = 2 ELSE DC = 4
      LOCATE DED.DIV IN TOT.DIV,1 BY 'AR' SETTING DFND ELSE 
         INS DED.DIV BEFORE TOT.DIV<DFND>
         INS ""      BEFORE TOT.HRS.DIV<DFND>
         INS ""      BEFORE TOT.ERN.DIV<DFND>
         INS ""      BEFORE TOT.INC.DIV<DFND>
         INS ""      BEFORE TOT.DED.DIV<DFND>
         INS ""      BEFORE TOT.GRS.DIV<DFND>
      END
      LOCATE DED.DEPT IN TOT.DEPT,1 BY 'AR' SETTING FND ELSE 
         INS DED.DEPT BEFORE TOT.DEPT<FND>
         INS ""      BEFORE TOT.HRS.DEPT<FND>
         INS ""      BEFORE TOT.ERN.DEPT<FND>
         INS ""      BEFORE TOT.INC.DEPT<FND>
         INS ""      BEFORE TOT.DED.DEPT<FND>
         INS ""      BEFORE TOT.GRS.DEPT<FND>
      END
      LOCATE DED.CCTR IN TOT.CCTR,1 BY 'AR' SETTING CFND ELSE 
         INS DED.CCTR BEFORE TOT.CCTR<CFND>
         INS ""      BEFORE TOT.HRS.CCTR<CFND>
         INS ""      BEFORE TOT.ERN.CCTR<CFND>
         INS ""      BEFORE TOT.INC.CCTR<CFND>
         INS ""      BEFORE TOT.DED.CCTR<CFND>
         INS ""      BEFORE TOT.GRS.CCTR<CFND>
      END
      FOR RECORD = 1 TO PAY.RECORDS
         IF RECORD = 1 THEN
            NAME = PAY.EMP : " " : EMP.FRST.NAME : " " : EMP.LAST.NAME
            V.LINE = NAME "L#40" :SP2
         END ELSE
            V.LINE = SP40 :SP2
         END
         V.LINE = V.LINE : RECORD "R#3" :SP2: PAY.DIV<1,RECORD> "R#2" :SP2
         V.LINE = V.LINE : PAY.DEPT<1,RECORD> "L#5" :SP2
         V.LINE = V.LINE : PAY.COST.CNTR<1,RECORD> "L#3" :SP2
         V.LINE = V.LINE : PAY.SHIFT<1,RECORD> "R#5" :SP2
         V.LINE = V.LINE : OCONV(PAY.RATE<1,RECORD>, "MD":DC) "R#11" :SP2
         STRT = 1; GOSUB 10000
         IF BLK THEN
            V.LINE = V.LINE : H.LINE
         END ELSE
            V.LINE = V.LINE : SP23:SP2
         END
         V.LINE = V.LINE :SP2: OCONV(PAY.OTH.INC<1,RECORD>, "MD2") "R#10"
         V.LINE = V.LINE :SP2: PAY.OTH.CODE<1,RECORD> "R#4"
         PRINT V.LINE
         LINE.COUNT = LINE.COUNT + 1
         IF LINE.COUNT > 55 THEN GOSUB 70000
         LOOP
         WHILE BLK AND BLK < HRS.CNT DO
            STRT = BLK + 1; GOSUB 10000
            IF LINE.COUNT > 55 THEN GOSUB 70000
            IF BLK THEN PRINT SP40:SP40:SP3: H.LINE; LINE.COUNT = LINE.COUNT + 1
         REPEAT
         TOT.HOURS = TOT.HOURS + PAY.TOT.HRS<1,RECORD>
         FOR H = 1 TO HRS.CNT
            TOT.HRS.DIV<DFND,H> = TOT.HRS.DIV<DFND,H> + PAY.HOURS<1,RECORD,HRS.SMV<1,H>>
            TOT.HRS.DEPT<FND,H> = TOT.HRS.DEPT<FND,H> + PAY.HOURS<1,RECORD,HRS.SMV<1,H>>
            TOT.HRS.CCTR<CFND,H> = TOT.HRS.CCTR<CFND,H> + PAY.HOURS<1,RECORD,HRS.SMV<1,H>>
            TOT.ERN.DIV<DFND,H> = TOT.ERN.DIV<DFND,H> + PAY.EARNINGS<1,RECORD,HRS.SMV<1,H>>
            TOT.ERN.DEPT<FND,H> = TOT.ERN.DEPT<FND,H> + PAY.EARNINGS<1,RECORD,HRS.SMV<1,H>>
            TOT.ERN.CCTR<CFND,H> = TOT.ERN.CCTR<CFND,H> + PAY.EARNINGS<1,RECORD,HRS.SMV<1,H>>
         NEXT H
      NEXT RECORD
      PRINT
      LINE.COUNT = LINE.COUNT + 1
      IF LINE.COUNT > 55 THEN GOSUB 70000
      V.LINE = "DEDUCTIONS -"
      P.CNT = 0
      TOT.DED.DIV<DFND,3> = TOT.DED.DIV<DFND,3> + DED.BONDS
      TOT.DED.DEPT<FND,3> = TOT.DED.DEPT<FND,3> + DED.BONDS
      TOT.DED.CCTR<CFND,3> = TOT.DED.CCTR<CFND,3> + DED.BONDS
      TOT.DED = TOT.DED + DED.BONDS
      IF DED.BONDS+0 # 0 THEN
         V.LINE = V.LINE :SP2: OCONV(DED.BONDS, "MD2") "R#9" :SP1: " B" "L#2"
         P.CNT = P.CNT + 1
      END
      MISC.CNT = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
      FOR D = 1 TO MISC.CNT
         IF DED.MISC.CODE<1,D> = "L" THEN
            TOT.DED.DIV<DFND,1> = TOT.DED.DIV<DFND,1> + DED.MISC.AMT<1,D>
            TOT.DED.DEPT<FND,1> = TOT.DED.DEPT<FND,1> + DED.MISC.AMT<1,D>
            TOT.DED.CCTR<CFND,1> = TOT.DED.CCTR<CFND,1> + DED.MISC.AMT<1,D>
         END ELSE
            IF DED.MISC.CODE<1,D>[1,1] = "U" THEN
               TOT.DED.DIV<DFND,2> = TOT.DED.DIV<DFND,2> + DED.MISC.AMT<1,D>
               TOT.DED.DEPT<FND,2> = TOT.DED.DEPT<FND,2> + DED.MISC.AMT<1,D>
               TOT.DED.CCTR<CFND,2> = TOT.DED.CCTR<CFND,2> + DED.MISC.AMT<1,D>
            END ELSE
               TOT.DED.DIV<DFND,3> = TOT.DED.DIV<DFND,3> + DED.MISC.AMT<1,D>
               TOT.DED.DEPT<FND,3> = TOT.DED.DEPT<FND,3> + DED.MISC.AMT<1,D>
               TOT.DED.CCTR<CFND,3> = TOT.DED.CCTR<CFND,3> + DED.MISC.AMT<1,D>
            END
         END
         IF P.CNT = 8 THEN
            PRINT V.LINE
            LINE.COUNT = LINE.COUNT + 1
            IF LINE.COUNT > 55 THEN GOSUB 70000
            V.LINE = SP5:SP5:SP2
            P.CNT = 0
         END
         V.LINE = V.LINE :SP2: OCONV(DED.MISC.AMT<1,D>, "MD2") "R#9" :SP1: DED.MISC.CODE<1,D> "L#2"
         P.CNT = P.CNT + 1
         TOT.DED = TOT.DED + DED.MISC.AMT<1,D>
      NEXT D
      PRINT V.LINE
      PRINT
      LINE.COUNT = LINE.COUNT + 2
      IF LINE.COUNT > 55 THEN GOSUB 70000
      TOT.DED.DIV<DFND,4> = TOT.DED.DIV<DFND,4> + DED.FWT
      TOT.DED.DEPT<FND,4> = TOT.DED.DEPT<FND,4> + DED.FWT
      TOT.DED.CCTR<CFND,4> = TOT.DED.CCTR<CFND,4> + DED.FWT
      TOT.DED.DIV<DFND,5> = TOT.DED.DIV<DFND,5> + DED.FICA
      TOT.DED.DEPT<FND,5> = TOT.DED.DEPT<FND,5> + DED.FICA
      TOT.DED.CCTR<CFND,5> = TOT.DED.CCTR<CFND,5> + DED.FICA
      TOT.DED.DIV<DFND,6> = TOT.DED.DIV<DFND,6> + DED.STATE
      TOT.DED.DEPT<FND,6> = TOT.DED.DEPT<FND,6> + DED.STATE
      TOT.DED.CCTR<CFND,6> = TOT.DED.CCTR<CFND,6> + DED.STATE
      TOT.DED.DIV<DFND,7> = TOT.DED.DIV<DFND,7> + DED.CITY
      TOT.DED.DEPT<FND,7> = TOT.DED.DEPT<FND,7> + DED.CITY
      TOT.DED.CCTR<CFND,7> = TOT.DED.CCTR<CFND,7> + DED.CITY
      TOT.DED.DIV<DFND,8> = TOT.DED.DIV<DFND,8> + DED.SUI
      TOT.DED.DEPT<FND,8> = TOT.DED.DEPT<FND,8> + DED.SUI
      TOT.DED.CCTR<CFND,8> = TOT.DED.CCTR<CFND,8> + DED.SUI
      TOT.DED.DIV<DFND,9> = TOT.DED.DIV<DFND,9> + DED.EIC
      TOT.DED.DEPT<FND,9> = TOT.DED.DEPT<FND,9> + DED.EIC
      TOT.DED.CCTR<CFND,9> = TOT.DED.CCTR<CFND,9> + DED.EIC
      TOT.DED = TOT.DED + DED.FWT + DED.FICA + DED.STATE + DED.CITY + DED.SUI + DED.EIC
      TOT.GRS.DIV<DFND,2> = TOT.GRS.DIV<DFND,2> + TOT.DED
      TOT.GRS.DEPT<FND,2> = TOT.GRS.DEPT<FND,2> + TOT.DED
      TOT.GRS.CCTR<CFND,2> = TOT.GRS.CCTR<CFND,2> + TOT.DED
      TOT.INC.DIV<DFND,1> = TOT.INC.DIV<DFND,1> + PAY.NON.TAXED
      TOT.INC.DEPT<FND,1> = TOT.INC.DEPT<FND,1> + PAY.NON.TAXED
      TOT.INC.CCTR<CFND,1> = TOT.INC.CCTR<CFND,1> + PAY.NON.TAXED
      TOT.INC.DIV<DFND,2> = TOT.INC.DIV<DFND,2> + PAY.TAXED
      TOT.INC.DEPT<FND,2> = TOT.INC.DEPT<FND,2> + PAY.TAXED
      TOT.INC.CCTR<CFND,2> = TOT.INC.CCTR<CFND,2> + PAY.TAXED
      TOT.GRS.DIV<DFND,1> = TOT.GRS.DIV<DFND,1> + PAY.GROSS
      TOT.GRS.DEPT<FND,1> = TOT.GRS.DEPT<FND,1> + PAY.GROSS
      TOT.GRS.CCTR<CFND,1> = TOT.GRS.CCTR<CFND,1> + PAY.GROSS
      TOT.GRS.DIV<DFND,3> = TOT.GRS.DIV<DFND,3> + PAY.NET
      TOT.GRS.DEPT<FND,3> = TOT.GRS.DEPT<FND,3> + PAY.NET
      TOT.GRS.CCTR<CFND,3> = TOT.GRS.CCTR<CFND,3> + PAY.NET
      IF LINE.COUNT > 50 THEN GOSUB 70000
      V.LINE = "TOT HRS : " "L#10" : OCONV(TOT.HOURS, "MD2") "R#7" :SP5
      V.LINE = V.LINE : "GROSS : " "L#8" : OCONV(PAY.GROSS, "MD2") "R#10" :SP5
      V.LINE = V.LINE : "FWHT  : " "L#8" : OCONV(DED.FWT, "MD2") "R#8" :SP5
      V.LINE = V.LINE : "STATE : " "L#8" : OCONV(DED.STATE, "MD2") "R#8" :SP5
      V.LINE = V.LINE : "SUTA : " "L#7" : OCONV(DED.SUI, "MD2") "R#8" :SP5
      V.LINE = V.LINE : "TOT DED : " "L#10" : OCONV(TOT.DED, "MD2") "R#10"
      PRINT V.LINE
      V.LINE = "DAYS    : " "L#10" : PAY.DAYS "R#1" :SP3:SP8
      V.LINE = V.LINE : "ISSUE : " "L#8" : PAY.ISSUE "R#2" :SP5:SP8
      V.LINE = V.LINE : "FICA  : " "L#8" : OCONV(DED.FICA, "MD2") "R#8" :SP5
      V.LINE = V.LINE : "CITY  : " "L#8" : OCONV(DED.CITY, "MD2") "R#8" :SP5
      V.LINE = V.LINE : "EIC : " "L#6" : OCONV(DED.EIC, "MD2") "R#8" :SP5
      V.LINE = V.LINE : "NET PAY : " "L#10" : OCONV(PAY.NET, "MD2") "R#10"
      PRINT V.LINE
      V.LINE = "WEEKS   : " "L#10" : OCONV(PAY.WEEKS, "MD3") "R#6" :SP1:SP5
      IF BATCH.NO = "ALL" THEN V.LINE = V.LINE : "BATCH : " "L#8" : PAY.BATCH.NO "R#3" :SP8:SP2:SP2
      V.LINE = V.LINE : "CHECK : " "L#8" : PAY.CHECK.NO "R#8"
      PRINT V.LINE
      PRINT
      PRINT STR("-",131)
      LINE.COUNT = LINE.COUNT + 5
      GOTO 1000
*
*--- TOTAL PAGES
*
2000 *
*
*--- COST CENTERS
*
      CCTR.CNT = COUNT(TOT.CCTR,AM) + 1
      FOR D = 1 TO CCTR.CNT
         GOSUB 80000
         PRINT; PRINT
         IF TOT.CCTR<D> = "000" THEN
            MAT CCTR.REC = ""
            CCTR.DESC = "GENERAL COST CENTER"
         END ELSE
            MATREAD CCTR.REC FROM COST.CNTR, CONO:TOT.CCTR<D> ELSE
               MAT CCTR.REC = ""; CCTR.DESC = "NOT ON FILE"
            END
         END
         PRINT "COST CENTER : " "R#35" :TOT.CCTR<D> "R#3" :SP2:CCTR.DESC "L#40"
         PRINT
         PRINT SP40: "  HOURS   " :SP8: " EARNINGS"
         PRINT SP40: D3:D2:D3:D2 :SP8: D3:D2:D3:D2
         TOT.HOURS = 0
         FOR H = 1 TO HRS.CNT
            V.LINE = HRS.DESC<1,H> "R#35" :SP5: OCONV(TOT.HRS.CCTR<D,H>, "MD2") "R#10"
            V.LINE = V.LINE :SP8: OCONV(TOT.ERN.CCTR<D,H>, "MD2") "R#10"
            PRINT V.LINE
            TOT.HOURS = TOT.HOURS + TOT.HRS.CCTR<D,H>
         NEXT H
         PRINT "TOTAL HOURS" "R#35" :SP5: OCONV(TOT.HOURS, "MD2") "R#10"
         PRINT
         PRINT "MISC INCOME   -   NON-TAX" "R#35" :SP23: OCONV(TOT.INC.CCTR<D,1>, "MD2") "R#10"
         PRINT "TAXABLE" "R#35" :SP23: OCONV(TOT.INC.CCTR<D,2>, "MD2") "R#10"
         PRINT
         PRINT "GROSS PAYROLL" "R#35" :SP23: OCONV(TOT.GRS.CCTR<D,1>, "MD2") "R#10"
         PRINT
         PRINT "DEDUCTIONS   -           " "R#35"
         PRINT "LOANS" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,1>, "MD2") "R#10"
         PRINT "UNION DUES" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,2>, "MD2") "R#10"
         PRINT "OTHER" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,3>, "MD2") "R#10"
         PRINT
         PRINT "FWHT" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,4>, "MD2") "R#10"
         PRINT "FICA" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,5>, "MD2") "R#10"
         PRINT "STATE" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,6>, "MD2") "R#10"
         PRINT "CITY" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,7>, "MD2") "R#10"
         PRINT "SUTA" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,8>, "MD2") "R#10"
         PRINT "EIC" "R#35" :SP23: OCONV(TOT.DED.CCTR<D,9>, "MD2") "R#10"
         PRINT "TOTAL DEDUCTIONS" "R#35" :SP23: OCONV(TOT.GRS.CCTR<D,2>, "MD2") "R#10"
         PRINT
         PRINT "NET (GROSS - DEDUCTIONS)" "R#35" :SP23: OCONV(TOT.GRS.CCTR<D,3>, "MD2") "R#10"
      NEXT D
*
*--- DEPARTMENTS
*
      DEPT.CNT = COUNT(TOT.DEPT,AM) + 1
      FOR D = 1 TO DEPT.CNT
         GOSUB 80000
         PRINT; PRINT
         MATREAD DEPT.REC FROM DEPARTMENT, CONO:TOT.DEPT<D> ELSE
            MAT DEPT.REC = ""; DEPT.DESC = "NOT ON FILE"
         END
         PRINT "DEPARTMENT : " "R#35" :TOT.DEPT<D> "L#5" :SP2:DEPT.DESC "L#40"
         GOSUB 20000
      NEXT D
*
*--- DIVISION
*
      TOT.DEPT = ""
      TOT.HRS.DEPT = ""
      TOT.ERN.DEPT = ""
      TOT.INC.DEPT = ""
      TOT.DED.DEPT = ""
      TOT.GRS.DEPT = ""
      DIV.CNT = COUNT(TOT.DIV,AM) + 1
      FOR D = 1 TO DIV.CNT
         GOSUB 80000
         PRINT; PRINT
         MATREAD DIV.REC FROM DIVISION, CONO:TOT.DIV<D> ELSE
            MAT DIV.REC = ""; DIV.DESC = "NOT ON FILE"
         END
         PRINT "DIVISION : " "R#35" :TOT.DIV<D> "R#2" :SP2:DIV.DESC "L#40"
         PRINT
         PRINT SP40: "  HOURS   " :SP8: " EARNINGS"
         PRINT SP40: D3:D2:D3:D2 :SP8: D3:D2:D3:D2
         TOT.HOURS = 0
         FOR H = 1 TO HRS.CNT
            V.LINE = HRS.DESC<1,H> "R#35" :SP5: OCONV(TOT.HRS.DIV<D,H>, "MD2") "R#10"
            V.LINE = V.LINE :SP8: OCONV(TOT.ERN.DIV<D,H>, "MD2") "R#10"
            PRINT V.LINE
            TOT.HOURS = TOT.HOURS + TOT.HRS.DIV<D,H>
            TOT.HRS.DEPT<1,H> = TOT.HRS.DEPT<1,H> + TOT.HRS.DIV<D,H>
            TOT.ERN.DEPT<1,H> = TOT.ERN.DEPT<1,H> + TOT.ERN.DIV<D,H>
         NEXT H
         PRINT "TOTAL HOURS" "R#35" :SP5: OCONV(TOT.HOURS, "MD2") "R#10"
         PRINT
         PRINT "MISC INCOME   -   NON-TAX" "R#35" :SP23: OCONV(TOT.INC.DIV<D,1>, "MD2") "R#10"
         PRINT "TAXABLE" "R#35" :SP23: OCONV(TOT.INC.DIV<D,2>, "MD2") "R#10"
         TOT.INC.DEPT<1,1> = TOT.INC.DEPT<1,1> + TOT.INC.DIV<D,1>
         TOT.INC.DEPT<1,2> = TOT.INC.DEPT<1,2> + TOT.INC.DIV<D,2>
         PRINT
         PRINT "GROSS PAYROLL" "R#35" :SP23: OCONV(TOT.GRS.DIV<D,1>, "MD2") "R#10"
         TOT.GRS.DEPT<1,1> = TOT.GRS.DEPT<1,1> + TOT.GRS.DIV<D,1>
         PRINT
         PRINT "DEDUCTIONS   -           " "R#35"
         PRINT "LOANS" "R#35" :SP23: OCONV(TOT.DED.DIV<D,1>, "MD2") "R#10"
         PRINT "UNION DUES" "R#35" :SP23: OCONV(TOT.DED.DIV<D,2>, "MD2") "R#10"
         PRINT "OTHER" "R#35" :SP23: OCONV(TOT.DED.DIV<D,3>, "MD2") "R#10"
         PRINT
         PRINT "FWHT" "R#35" :SP23: OCONV(TOT.DED.DIV<D,4>, "MD2") "R#10"
         PRINT "FICA" "R#35" :SP23: OCONV(TOT.DED.DIV<D,5>, "MD2") "R#10"
         PRINT "STATE" "R#35" :SP23: OCONV(TOT.DED.DIV<D,6>, "MD2") "R#10"
         PRINT "CITY" "R#35" :SP23: OCONV(TOT.DED.DIV<D,7>, "MD2") "R#10"
         PRINT "SUTA" "R#35" :SP23: OCONV(TOT.DED.DIV<D,8>, "MD2") "R#10"
         PRINT "EIC" "R#35" :SP23: OCONV(TOT.DED.DIV<D,9>, "MD2") "R#10"
         PRINT "TOTAL DEDUCTIONS" "R#35" :SP23: OCONV(TOT.GRS.DIV<D,2>, "MD2") "R#10"
         TOT.GRS.DEPT<1,2> = TOT.GRS.DEPT<1,2> + TOT.GRS.DIV<D,2>
         PRINT
         PRINT "NET (GROSS - DEDUCTIONS)" "R#35" :SP23: OCONV(TOT.GRS.DIV<D,3>, "MD2") "R#10"
         TOT.GRS.DEPT<1,3> = TOT.GRS.DEPT<1,3> + TOT.GRS.DIV<D,3>
         FOR I = 1 TO 9
            TOT.DED.DEPT<1,I> = TOT.DED.DEPT<1,I> + TOT.DED.DIV<D,I>
         NEXT I
      NEXT D
*
*--- COMPANY
*
      D = 1
      GOSUB 80000
      PRINT; PRINT
      PRINT "COMPANY : " "R#35" :CONO "R#3" :SP2: CO.NAME "L#40"
      GOSUB 20000
      PRINTER OFF
      GOTO 999999
*******************
*   SUBROUTINES   *
*******************
*
*--- GET HOURS LINE (H.LINE)
*
10000 *
      BLK = 0
      FOR H = STRT TO HRS.CNT UNTIL BLK
         IF PAY.HOURS<1,RECORD,HRS.SMV<1,H>>+0 # 0 THEN
            BLK = H
            H.LINE = HRS.SHORT<1,H> "L#7" :SP2
            H.LINE = H.LINE : OCONV(PAY.HOURS<1,RECORD,HRS.SMV<1,H>>, "MD2") "R#6" :SP2
            H.LINE = H.LINE : OCONV(PAY.EARNINGS<1,RECORD,HRS.SMV<1,H>>, "MD2") "R#10"
         END
      NEXT H
      RETURN
*
*--- DEPARTMENT & COMPANY
*
20000 *
      PRINT
      PRINT SP40: "  HOURS   " :SP8: " EARNINGS"
      PRINT SP40: D3:D2:D3:D2 :SP8: D3:D2:D3:D2
      TOT.HOURS = 0
      FOR H = 1 TO HRS.CNT
         V.LINE = HRS.DESC<1,H> "R#35" :SP5: OCONV(TOT.HRS.DEPT<D,H>, "MD2") "R#10"
         V.LINE = V.LINE :SP8: OCONV(TOT.ERN.DEPT<D,H>, "MD2") "R#10"
         PRINT V.LINE
         TOT.HOURS = TOT.HOURS + TOT.HRS.DEPT<D,H>
      NEXT H
      PRINT "TOTAL HOURS" "R#35" :SP5: OCONV(TOT.HOURS, "MD2") "R#10"
      PRINT
      PRINT "MISC INCOME   -   NON-TAX" "R#35" :SP23: OCONV(TOT.INC.DEPT<D,1>, "MD2") "R#10"
      PRINT "TAXABLE" "R#35" :SP23: OCONV(TOT.INC.DEPT<D,2>, "MD2") "R#10"
      PRINT
      PRINT "GROSS PAYROLL" "R#35" :SP23: OCONV(TOT.GRS.DEPT<D,1>, "MD2") "R#10"
      PRINT
      PRINT "DEDUCTIONS   -           " "R#35"
      PRINT "LOANS" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,1>, "MD2") "R#10"
      PRINT "UNION DUES" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,2>, "MD2") "R#10"
      PRINT "OTHER" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,3>, "MD2") "R#10"
      PRINT
      PRINT "FWHT" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,4>, "MD2") "R#10"
      PRINT "FICA" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,5>, "MD2") "R#10"
      PRINT "STATE" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,6>, "MD2") "R#10"
      PRINT "CITY" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,7>, "MD2") "R#10"
      PRINT "SUTA" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,8>, "MD2") "R#10"
      PRINT "EIC" "R#35" :SP23: OCONV(TOT.DED.DEPT<D,9>, "MD2") "R#10"
      PRINT "TOTAL DEDUCTIONS" "R#35" :SP23: OCONV(TOT.GRS.DEPT<D,2>, "MD2") "R#10"
      PRINT
      PRINT "NET (GROSS - DEDUCTIONS)" "R#35" :SP23: OCONV(TOT.GRS.DEPT<D,3>, "MD2") "R#10"
      RETURN
*
*--- DETAIL HEADING
*
70000 *
      GOSUB 80000
      PRINT "DEPARTMENT - " :PREV.HOME "L#5":SP2: DEPT.DESC "L#30"
      PRINT
      PRINT HEAD3
      PRINT HEAD4
      LINE.COUNT = LINE.COUNT + 4
      RETURN
*
*--- HEADING
*
80000 *
      PAGE.NO = PAGE.NO + 1
      HEADING HEAD1 : PAGE.NO "R#3" : "'L'" : HEAD2
      LINE.COUNT = 5
      RETURN
999999 *
      END
