*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - PRS.EOM.LIST
*
* SYSTEM        - PRSBP
*
* BY            - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 02/28/87
*
* DESCRIPTION   -
* This program produces an PRS EOM LIST by Detail,Customer or
* Summary.
*
* TASK 19409 - BOB SHERER, AUG. 16 1995.  DIVISIONALIZE PAYROLL
*
*T22547 stefanie 03/17/1998 * Change text from Monthly to Period.
*T26486 cmykleb 04/11/2002 * Do not update period control record either
*                            if the debits and credits are out of balance
*                            or the report was only (v)iewed.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>EMPLOYEE
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST RUN FROM A PROC"; GOTO 93000
   END
   CONO = BUFFER<1>
   PRINT.FLAG = BUFFER<10> ; * T26486
   OB.FLAG = "" ; * T26486
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '',BUFFER<3> TO EOM.TRANS ELSE ERRMSG='MUST RUN EOM POSTING FOR COMPANY - ':CONO;GOSUB 91000;GOTO 99999
   OPEN '','COA' TO COA ELSE ERRMSG='COA FILE MISSING';GOTO 93000
   OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG = 'EMPLOYEE FILE MISSING';GOTO 93000
*
*---- READ COMPANY RECORD
*
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "CANNOT LOCATE COMPANY - " : CONO; GOTO 93000
   END
   MATREAD FISCAL.REC FROM CONTROL, CONO : "PRFISCAL" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL - PRFISCAL"; GOTO 93000
   END
   IF FR.PRINT = "X" THEN
      ERRMSG = "EOM FLAG IS SET TO ABORT EOM PROCESSING"; GOTO 93000
   END
   DIV = BUFFER<4>
   OPTION = BUFFER<5>
   SEL = BUFFER<6>
   LEVEL = BUFFER<7> + 1
*
*---- DIMENSION RECORDS
*
   DIM NM(4)
   DIM PV(4)
   DIM BI(4)
   BI(1) = 11
   BI(2) = 4
   BI(3) = 6
   BI(4) = 8
   DIM EI(4)
   EI(1) = LEN(CO.ACCT.PIC)
   EI(2) = 2
   EI(3) = 2
   EI(4) = 3
   DIM BB(4)
   BB(1) = ""
   BB(2) = ""
   BB(3) = SPACE(3)
   BB(4) = SPACE(6)
   DIM AB(4,4)
   AB(1,1) = 0
   AB(1,2) = 0
   AB(2,1) = 3
   AB(2,2) = 8
   AB(3,1) = 6
   AB(3,2) = 5
   AB(4,1) = 10
   AB(4,2) = 1
   DIM DB(4)
   MAT DB = 0
   DIM CR(4)
   MAT CR = 0
   DIM BRK(4)
   BRK(1) = "ACCT TOT "
   BRK(2) = "DIV  TOT "
   BRK(3) = "DEPT TOT "
   BRK(4) = "CCTR TOT "
   DB.TOT = 0
   CR.TOT = 0
   SP = SPACE(1)
   SP30 = SPACE(30)
   SP31 = SPACE(31)
*
*---- SETUP HEADINGS
*
   N = (50 - LEN(CO.NAME)) / 2
   HD1 = "  REPORT NO : XXXX" : SPACE(23 + N) : CO.NAME : SPACE(10 + N) : " 'TL'"
   HD2 = SPACE(14) : "DIV EMPLOYEE LVL"
   HD4 = "ACCOUNT NUMBER    ACCOUNT DESCRIPTION    DV DP CTR "
   HD5 = "-------------- ------------------------- -- -- --- "
   IF OPTION = "M" THEN
      TSP = 91
   END ELSE
      TSP = 60
   END
   DSH = SPACE(TSP) : STR("-",13) : SP : STR("-",13)
   BEGIN CASE
      CASE OPTION="S"
*T22547 >         HD2=HD2:SPACE(18):"PAYROLL SUMMARY END OF MONTH LISTING" : SPACE(34)
         HD2=HD2:SPACE(18):"PAYROLL SUMMARY END OF PERIOD LISTING" : SPACE(33)
         HD4 = HD4 : "         DEBIT AMOUNT  CREDIT AMOUNT"
         HD5 = HD5 : "         ------------- -------------"
      CASE OPTION="D"
*T22547 >         HD2 = HD2:SPACE(18) : "PAYROLL DETAIL END OF MONTH LISTING" : SPACE(35)
         HD2 = HD2:SPACE(18) : "PAYROLL DETAIL END OF PERIOD LISTING" : SPACE(34)
         HD4 = HD4 : "EMPLOYEE DEBIT AMOUNT  CREDIT AMOUNT TRANS ID  TRN DATE TRANS AMOUNT"
         HD5 = HD5 : "-------- ------------- ------------- -------- -------- --------------"
         DSH = DSH : SPACE(19) : STR("-",14)
      CASE 1
*T22547 >         HD2=HD2:SPACE(17):"PAYROLL EMPLOYEE END OF MONTH LISTING" : SPACE(34)
         HD2=HD2:SPACE(17):"PAYROLL EMPLOYEE END OF PERIOD LISTING" : SPACE(33)
         HD4 = HD4 : "EMPLOYEE          EMPLOYEE NAME         DEBIT AMOUNT  CREDIT AMOUNT"
         HD5 = HD5 : "-------- ------------------------------ ------------- -------------"
   END CASE
   SPN = SPACE(10)
   HD2 = HD2 : "PAGE : 'PL'"
   HD3 = "  SELECTION : " : DIV "L#4" : SP : SEL "L#8" : SP : (LEVEL-1) "L#1" : SPACE(21)
   HD3 = HD3 : "FOR PERIOD " : FR.CURR.PER : " ENDING " : OCONV(FR.CURR.DATE,"D2/")
   HD3 = HD3 : " 'LL'"
   HD4 = HD4 : " 'L'"
*
*---- MAIN PROCESSING
*
   READNEXT ETR.ID ELSE
*T26486 v
*     IF OPTION = "S" AND SEL = "ALL" AND DIV = "ALL" THEN
*        FR.PRINT = "P"
      IF OPTION = "S" AND SEL = "ALL" AND DIV = "ALL" AND PRINT.FLAG # "V" THEN
         FR.PRINT = "P"
         IF OB.FLAG # "" THEN FR.PRINT = OB.FLAG
*T26486 ^
         MATWRITE FISCAL.REC ON CONTROL, CONO : "PRFISCAL"
      END
      GOTO 99999
   END
   DATA=1     
   PRINTER ON
   HEADING HD1 : HD2 : HD3 : HD4 : HD5
   FOR I = 1 TO 4
      NM(I) = ETR.ID[BI(I),EI(I)]
      PV(I) = NM(I)
   NEXT I
   MATREAD COA.REC FROM COA, CONO : NM(1) ELSE COA.DESC = "UNKNOWN"
   OCOA.DESC = COA.DESC
   LOOP
      NAME = SPN
      NO.BREAK = 1
      FOR I = 1 TO LEVEL WHILE NO.BREAK
         IF PV(I) <> NM(I) THEN
            NO.BREAK = 0
            FOR J = LEVEL TO I STEP -1
               DB.CHK = DB(J); CR.CHK = CR(J)
               BEGIN CASE
                  CASE J = 1
                     DB.CHK = DB.CHK + 1
                     BLN = PV(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25" : SP
                     MATREAD COA.REC FROM COA, CONO : NM(1) ELSE COA.DESC = "UNKNOWN"
                  CASE J = I
                     DB.CHK = DB.CHK + 1
                     BLN = SPACE(41)
                     NAME = BB(J) : PV(J) : NAME[AB(J,1),AB(J,2)]
                  CASE 1
                     DB.CHK = DB(J-1); CR.CHK = CR(J-1)
                     BLN = SPACE(41)
                     NAME = BB(J) : PV(J) : NAME[AB(J,1),AB(J,2)]
               END CASE
               BEGIN CASE
                  CASE DB.CHK = DB(J) AND CR.CHK = CR(J)
                  CASE OPTION = "S"
                     PRINT PV(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25" : SP :
                     PRINT NAME:BRK(J):OCONV(DB(J),"MD2Z,") "R#13":SP:OCONV((0 - CR(J)),"MD2Z,") "R#13"
                     PRINT
                     NAME = SPN
                  CASE OPTION = "D"
                     PRINT DSH
                     PRINT BLN:NAME:BRK(J):OCONV(DB(J),"MD2Z,") "R#13":SP:OCONV((0 - CR(J)),"MD2Z,") "R#13" :
                     PRINT SPACE(19) : OCONV(DB(J)+CR(J),"MD2,C") "R#14"
                     PRINT
                     NAME = SPN
                  CASE 1
                     PRINT DSH
                     PRINT BLN:NAME:SP31:BRK(J):OCONV(DB(J),"MD2Z,") "R#13":SP:OCONV((0 - CR(J)),"MD2Z,") "R#13"
                     PRINT
                     NAME = SPN
               END CASE
            NEXT J
            FOR J = I TO LEVEL
               PV(J) = NM(J)
               DB(J) = 0
               CR(J) = 0
            NEXT J
            OCOA.DESC = COA.DESC
         END
      NEXT I
      BEGIN CASE
         CASE NO.BREAK AND OPTION = "D"
            PRINT
         CASE DATA = 0
            PRINT SPACE(TSP) : STR("=",13) : SP : STR("=",13)
            PRINT SPACE(TSP) : OCONV(DB.TOT,"MD2,") "R#13" : SP : OCONV(CR.TOT,"MD2,") "R#13"
*T26486 v
            IF (DB.TOT + 0) # (CR.TOT + 0) THEN
               PRINT
               PRINT SPACE(TSP) : "* REPORT IS OUT OF BALANCE *"
               OB.FLAG = "O"
            END
*T26486 ^
      END CASE
   WHILE DATA DO
      MATREAD ETR.REC FROM EOM.TRANS,ETR.ID ELSE MAT ETR.REC=''
      TYP=FIELD(ETR.ID,"!",1)[BI(1) + EI(1),99] "L#8"
      EMP.TYP = TRIM(TYP)
      MATREAD EMP.REC FROM EMPLOYEE,CONO:EMP.TYP ELSE
         MAT EMP.REC = ''
         EMP.LAST.NAME = "UNKNOWN"
      END
      TYP.DESC = EMP.LAST.NAME:",":EMP.FRST.NAME
      IF FIELD(FIELD(ETR.ID,"!",2),"*",1) = "D" THEN
         DEBIT = OCONV(ETR.AMT,"MD2Z,") "R#13"
         CREDIT = SPACE(13)
         FOR I = 1 TO LEVEL
            DB(I) = DB(I) + ETR.AMT
         NEXT I
         DB.TOT = DB.TOT + ETR.AMT
      END ELSE
         DEBIT = SPACE(13)
         CREDIT = OCONV((0 - ETR.AMT),"MD2Z,") "R#13"
         FOR I = 1 TO LEVEL
            CR(I) = CR(I) + ETR.AMT
         NEXT I
         CR.TOT = CR.TOT - ETR.AMT
      END
      IF OPTION # "S" THEN
         IF OPTION = "M" THEN
            PRINT NM(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25" : SP :
            PRINT NM(2) "L#2" : SP : NM(3) "L#2" : SP : NM(4) "L#3" : SP :
            PRINT TYP : SP : TYP.DESC "L#30" : SP : DEBIT : SP : CREDIT
         END ELSE
            IF OPTION = "D" THEN
               PRINT NM(1) CO.ACCT.MASK : SP : OCOA.DESC "L#25": SP :
               PRINT NM(2) "L#2" : SP : NM(3) "L#2" : SP : NM(4) "L#3" : SP :
               PRINT TYP : SP : DEBIT : SP : CREDIT :
               PRINT SP : ETR.TRAN<1,1> "L#8" : SP : OCONV(ETR.DATE<1,1>,"D2/") "L#8" :
               PRINT SP : OCONV(ETR.TAMT<1,1>,"MD2,C") "R#14"
               CNT = COUNT(ETR.TAMT,VM) + 1
               FOR T = 2 TO CNT
                  PRINT SPACE(88) : ETR.TRAN<1,T> "L#8" : SP : OCONV(ETR.DATE<1,T>,"D2/") "L#8" :
                  PRINT SP : OCONV(ETR.TAMT<1,T>,"MD2,C") "R#14"
               NEXT T
            END ELSE
               PRINT
            END
         END
      END
      READNEXT ETR.ID ELSE
         ETR.ID=STR('X',25)
         DATA=0
      END
      FOR I = 1 TO 4
         NM(I) = ETR.ID[BI(I),EI(I)]
      NEXT I
   REPEAT
   PRINTER OFF
*T26486 v
*  IF OPTION = "S" AND SEL = "ALL" AND DIV = "ALL" THEN
*     FR.PRINT = "P"
   IF OPTION = "S" AND SEL = "ALL" AND DIV = "ALL" AND PRINT.FLAG # "V" THEN
      FR.PRINT = "P"
      IF OB.FLAG # "" THEN FR.PRINT = OB.FLAG
*T26486 ^
      MATWRITE FISCAL.REC ON CONTROL, CONO : "PRFISCAL"
   END
   GOTO 99999
*   
*---- CALLS FOR SYSCOM   
*   
91000*   
   PRINTER OFF
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   PRINTER ON
   RETURN
93000*   
   PRINTER OFF
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*   
END
