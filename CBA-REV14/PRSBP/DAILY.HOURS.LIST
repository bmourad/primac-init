*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - DAILY.HOURS.LIST
* BY          - WALID YAMOUT, CBA;MODIFIED BY CAROL SAGE
* DATE        - 06/11/85;MODIFIED 06/29/87
* DESCRIPTION - This program prints the Daily Hours Summary which
*               gives a daily total of each employee's Job Costing
*               hours, shows any existing PAYREG totals, and 
*               shows what the new PAYREG record will look like once
*               the Job Costing hours are posted.
*********************************************************************
*
*---- INSERT FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PRS.CPYLIB>DAILY.HOURS
*COPY>PRS.CPYLIB>PAYREG
*COPY>PRS.CPYLIB>PAYROLL.HOURS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SETUP ERRMSG ROUTINE
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- READ ALL FILES
*
   OPEN "CONTROL" TO CONTROL ELSE ERRMSG="CONTROL FILE MISSING";GO 93000
   OPEN "COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GO 93000
   OPEN "EMPLOYEE" TO EMPLOYEE ELSE ERRMSG="EMPLOYEE FILE MISSING";GO 93000
   OPEN "DAILY.HOURS" TO DAILY.HOURS ELSE ERRMSG="DAILY.HOURS FILE MISSING";GO 93000
   OPEN "PAYREG" TO PAYREG ELSE ERRMSG="PAYREG FILE MISSING";GO 93000
   OPEN "DEPARTMENT" TO DEPARTMENT ELSE ERRMSG="DEPARTMENT FILE MISSING";GO 93000
*
*---- READ PROC VALUES
*
   PROCREAD X ELSE
      ERRMSG = 'CANNOT PERFORM PROCREAD';GO 93000
   END
   CONO=X<1>
   CO.NAME=X<2>
   END.DATE=X<3>
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
      ERRMSG='CANNOT READ COMPANY':CONO;GO 93000
   END
   MATREAD HRS.REC FROM CONTROL,CONO:'PAYROLL.HOURS' ELSE MAT HRS.REC=''
   HRS.CNT=COUNT(HRS.DESC,VM)+(HRS.DESC#'')
   IF HRS.CNT < 1 OR HRS.CNT > 11 THEN
      ERRMSG='NEED TO SET UP PAYROLL HOURS FOR COMPANY ':CONO
      GOSUB 91000;GO 99999
   END
*
*---- HEADING ROUTINE
*
   REPORT.NAME="DAILY HOURS SUMMARY"
   REPORT.NUMBER="XXXX"
   REPORT.DATE=END.DATE
   HD1='';HD2=''
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
*---- INITIALIZATION
*
   LINE.COUNT=99
   PAGE.NO=0
   PTR=0;*---COUNTER FOR DEPARTMENT TOTAL PAGE
   U.LINE1='------- ------- -------   ------- ------- -------   ------- ------- -------'
   U.LINE2='======= ======= =======   ======= ======= =======   ======= ======= ======='
*
*---- TOTAL TABLE FOR COMPANY
*
   DIM TOT.COM(10);MAT TOT.COM=''
   EQU COM.CREG TO TOT.COM(1)
   EQU COM.CO.T TO TOT.COM(2)
   EQU COM.CD.T TO TOT.COM(3)
   EQU COM.NREG TO TOT.COM(4)
   EQU COM.NO.T TO TOT.COM(5)
   EQU COM.ND.T TO TOT.COM(6)
   EQU COM.TREG TO TOT.COM(7)
   EQU COM.TO.T TO TOT.COM(8)
   EQU COM.TD.T TO TOT.COM(9)
*
*---- TOTAL TABLE FOR DEPARTMENT
*
   DIM TOT.DPT(12);MAT TOT.DPT=''
   EQU DPT.CREG TO TOT.DPT(1)
   EQU DPT.CO.T TO TOT.DPT(2)
   EQU DPT.CD.T TO TOT.DPT(3)
   EQU DPT.NREG TO TOT.DPT(4)
   EQU DPT.NO.T TO TOT.DPT(5)
   EQU DPT.ND.T TO TOT.DPT(6)
   EQU DPT.TREG TO TOT.DPT(7)
   EQU DPT.TO.T TO TOT.DPT(8)
   EQU DPT.TD.T TO TOT.DPT(9)
   EQU DPT.NUMB TO TOT.DPT(10)
   EQU DPT.DESC TO TOT.DPT(11)
*
*---- TOTAL TABLE FOR EMPLOYEE
*
   DIM TOT.EMP(10);MAT TOT.EMP=''
   EQU EMP.CREG TO TOT.EMP(1)
   EQU EMP.CO.T TO TOT.EMP(2)
   EQU EMP.CD.T TO TOT.EMP(3)
   EQU EMP.NREG TO TOT.EMP(4)
   EQU EMP.NO.T TO TOT.EMP(5)
   EQU EMP.ND.T TO TOT.EMP(6)
   EQU EMP.TREG TO TOT.EMP(7)
   EQU EMP.TO.T TO TOT.EMP(8)
   EQU EMP.TD.T TO TOT.EMP(9)
*
*---- TOTAL TABLE FOR DAY
*
   DIM TOT.DAY(10);MAT TOT.DAY=''
   EQU DAY.CREG TO TOT.DAY(1)
   EQU DAY.CO.T TO TOT.DAY(2)
   EQU DAY.CD.T TO TOT.DAY(3)
   EQU DAY.NREG TO TOT.DAY(4)
   EQU DAY.NO.T TO TOT.DAY(5)
   EQU DAY.ND.T TO TOT.DAY(6)
   EQU DAY.TREG TO TOT.DAY(7)
   EQU DAY.TO.T TO TOT.DAY(8)
   EQU DAY.TD.T TO TOT.DAY(9)
*
*---- TOTAL TABLE 
*
   DIM TOT.SAV(10);MAT TOT.SAV=''
   EQU SAV.CREG TO TOT.SAV(1)
   EQU SAV.CO.T TO TOT.SAV(2)
   EQU SAV.CD.T TO TOT.SAV(3)
   EQU SAV.NREG TO TOT.SAV(4)
   EQU SAV.NO.T TO TOT.SAV(5)
   EQU SAV.ND.T TO TOT.SAV(6)
   EQU SAV.TREG TO TOT.SAV(7)
   EQU SAV.TO.T TO TOT.SAV(8)
   EQU SAV.TD.T TO TOT.SAV(9)
*
*---- READ AND PROCESS FIRST RECORD
*
   PRINTER ON
100*
   READNEXT ID ELSE GO 99999
   IF ID[1,3]#CONO THEN GO 100
   MATREADU DHR.REC FROM DAILY.HOURS,ID ELSE
      MAT DHR.REC='';RELEASE DAILY.HOURS,ID;GO 100
   END
   DHR.PRINTED = "Y"
   EMP.KEY=FIELD(ID,'!',1)
   EMP.NO=EMP.KEY[4,4]
   DAT.WK=OCONV(FIELD(ID,'!',5),'D2/')
   MATREAD EMP.REC FROM EMPLOYEE,EMP.KEY ELSE
      MAT EMP.REC=''
      DHR.STATUS='EMPLOYEE NOT ON FILE'
      MATWRITE DHR.REC ON DAILY.HOURS,ID
      GO 100
   END
   NAME=EMP.FRST.NAME:' ':EMP.LAST.NAME
   DPT.NO=EMP.DEPT
   PTR=PTR+1
   MATREAD DEPT.REC FROM DEPARTMENT,CONO:DPT.NO ELSE
      MAT DEPT.REC="";DEPT.DESC="NOT ON FILE"
   END
110*
   MATWRITE DHR.REC ON DAILY.HOURS,ID
   PREV.DPT=DPT.NO
   PREV.EMP=EMP.NO
   PREV.DAY=DAT.WK
   PREV.NAM=NAME
   GOSUB 500
   GOSUB 1000
*
*---- READ AND PROCESS ALL RECORDS
*
   DATA=0
   LOOP
      READNEXT ID ELSE DATA=1
   WHILE DATA=0 DO
      IF ID[1,3]#CONO THEN GO 111
      MATREADU DHR.REC FROM DAILY.HOURS,ID ELSE
         MAT DHR.REC='';RELEASE DAILY.HOURS,ID;GO 111
      END
      DHR.PRINTED = "Y"
      EMP.KEY=FIELD(ID,'!',1)
      EMP.NO=EMP.KEY[4,4]
      MATREAD EMP.REC FROM EMPLOYEE,EMP.KEY ELSE
         MAT EMP.REC=''
         DHR.STATUS='EMPLOYEE NOT ON FILE'
         MATWRITE DHR.REC ON DAILY.HOURS,ID
         GO 111
      END
      NAME=EMP.FRST.NAME:' ':EMP.LAST.NAME
      DPT.NO=EMP.DEPT
      DAT.WK=OCONV(FIELD(ID,'!',5),'D2/')
      MATWRITE DHR.REC ON DAILY.HOURS,ID
      GOSUB 300
      GOSUB 1000
111*
   REPEAT
   GOSUB 301
   GOSUB 302
   GOSUB 303
   GO 400
*
*---- FIND BREAK AND PRINT TOTALS
300*
   IF PREV.DPT#DPT.NO THEN
      GOSUB 301
      GOSUB 302
      GOSUB 303
   END
   IF PREV.EMP#EMP.NO THEN
      GOSUB 301
      GOSUB 302
      PRINT
      LINE.COUNT=LINE.COUNT+1
   END
   IF PREV.DAY#DAT.WK THEN
      GOSUB 301
   END
   RETURN
*
*---- PRINT TOTAL FOR DAY
301*
   IF LINE.COUNT>52 THEN LINE.COUNT=0;GOSUB 9000
   DAY.LINE=PREV.EMP'R#4 ':PREV.NAM'L#30 ':PREV.DAY'L#8   '
   DAY.LINE=DAY.LINE:OCONV(DAY.CREG,'MD2Z')'R#7 '
   DAY.LINE=DAY.LINE:OCONV(DAY.CO.T,'MD2Z')'R#7 '
   DAY.LINE=DAY.LINE:OCONV(DAY.CD.T,'MD2Z')'R#7   '
   DAY.LINE=DAY.LINE:OCONV(DAY.NREG,'MD2Z')'R#7 '
   DAY.LINE=DAY.LINE:OCONV(DAY.NO.T,'MD2Z')'R#7 '
   DAY.LINE=DAY.LINE:OCONV(DAY.ND.T,'MD2Z')'R#7   '
   DAY.LINE=DAY.LINE:OCONV(DAY.TREG,'MD2Z')'R#7 '
   DAY.LINE=DAY.LINE:OCONV(DAY.TO.T,'MD2Z')'R#7 '
   DAY.LINE=DAY.LINE:OCONV(DAY.TD.T,'MD2Z')'R#7   '
   PRINT DAY.LINE
   LINE.COUNT=LINE.COUNT+1
   PREV.DAY=DAT.WK
   GOSUB 2000
   MAT TOT.DAY=''
   RETURN
*
*---- PRINT TOTAL FOR EMPLOYEE
302*
   U.LINE=SPACE(47):U.LINE1
   PRINT U.LINE
   GOSUB 3000
   FOR X=1 TO 3
      BEGIN CASE
         CASE X=1
            EMP.LINE=SPACE(31):'JOB COSTING -   '
         CASE X=2
            EMP.LINE=SPACE(35):'PAYROLL -   '
         CASE X=3
            EMP.LINE=SPACE(32):'CALCULATED -   '
      END CASE
      EMP.LINE=EMP.LINE:OCONV(EMP.CREG<1,X>,'MD2Z')'R#7 '
      EMP.LINE=EMP.LINE:OCONV(EMP.CO.T<1,X>,'MD2Z')'R#7 '
      EMP.LINE=EMP.LINE:OCONV(EMP.CD.T<1,X>,'MD2Z')'R#7   '
      EMP.LINE=EMP.LINE:OCONV(EMP.NREG<1,X>,'MD2Z')'R#7 '
      EMP.LINE=EMP.LINE:OCONV(EMP.NO.T<1,X>,'MD2Z')'R#7 '
      EMP.LINE=EMP.LINE:OCONV(EMP.ND.T<1,X>,'MD2Z')'R#7   '
      EMP.LINE=EMP.LINE:OCONV(EMP.TREG<1,X>,'MD2Z')'R#7 '
      EMP.LINE=EMP.LINE:OCONV(EMP.TO.T<1,X>,'MD2Z')'R#7 '
      EMP.LINE=EMP.LINE:OCONV(EMP.TD.T<1,X>,'MD2Z')'R#7   '
      PRINT EMP.LINE
   NEXT X
   LINE.COUNT=LINE.COUNT+4
   PREV.EMP=EMP.NO
   PREV.NAM=NAME
   GOSUB 4000
   MAT TOT.EMP=''
   GOSUB 500
   RETURN
*
*---- PRINT TOTAL FOR DEPARTMENT
303*
   U.LINE=SPACE(47):U.LINE2
   PRINT U.LINE
   FOR X=1 TO 3
      BEGIN CASE
         CASE X=1
            DPT.LINE=SPACE(25):'TOTAL JOB COSTING -   '
         CASE X=2
            DPT.LINE=SPACE(29):'TOTAL PAYROLL -   '
         CASE X=3
            DPT.LINE=SPACE(26):'TOTAL CALCULATED -   '
      END CASE
      DPT.LINE=DPT.LINE:OCONV(DPT.CREG<PTR,X>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.CO.T<PTR,X>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.CD.T<PTR,X>,'MD2Z')'R#7   '
      DPT.LINE=DPT.LINE:OCONV(DPT.NREG<PTR,X>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.NO.T<PTR,X>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.ND.T<PTR,X>,'MD2Z')'R#7   '
      DPT.LINE=DPT.LINE:OCONV(DPT.TREG<PTR,X>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.TO.T<PTR,X>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.TD.T<PTR,X>,'MD2Z')'R#7   '
      PRINT DPT.LINE
   NEXT X
   LINE.COUNT=99
   PREV.DPT=DPT.NO
   PTR=PTR+1
   MATREAD DEPT.REC FROM DEPARTMENT,CONO:DPT.NO ELSE
      MAT DEPT.REC="";DEPT.DESC="NOT ON FILE"
   END
   RETURN
*
*---- PRINT DEPARTMENT SYNOPSIS AND COMPANY TOTAL
400*
   GOSUB 9010
   FOR X=1 TO PTR
      COM.CREG = COM.CREG + DPT.CREG<X,1>
      COM.CO.T = COM.CO.T + DPT.CO.T<X,1>
      COM.CD.T = COM.CD.T + DPT.CD.T<X,1>
      COM.NREG = COM.NREG + DPT.NREG<X,1>
      COM.NO.T = COM.NO.T + DPT.NO.T<X,1>
      COM.ND.T = COM.ND.T + DPT.ND.T<X,1>
      COM.TREG = COM.TREG + DPT.TREG<X,1>
      COM.TO.T = COM.TO.T + DPT.TO.T<X,1>
      COM.TD.T = COM.TD.T + DPT.TD.T<X,1>
*
      DPT.LINE=DPT.NUMB<X,1>'L#5 ':DPT.DESC<X,1>'L#39  '
      DPT.LINE=DPT.LINE:OCONV(DPT.CREG<X,1>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.CO.T<X,1>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.CD.T<X,1>,'MD2Z')'R#7   '
      DPT.LINE=DPT.LINE:OCONV(DPT.NREG<X,1>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.NO.T<X,1>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.ND.T<X,1>,'MD2Z')'R#7   '
      DPT.LINE=DPT.LINE:OCONV(DPT.TREG<X,1>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.TO.T<X,1>,'MD2Z')'R#7 '
      DPT.LINE=DPT.LINE:OCONV(DPT.TD.T<X,1>,'MD2Z')'R#7   '
      PRINT DPT.LINE
      LINE.COUNT=LINE.COUNT+1
      IF LINE.COUNT>58 THEN LINE.COUNT=0;GOSUB 9010
   NEXT X
   H.LINE=SPACE(47):U.LINE2
   PRINT H.LINE
   COM.LINE=SPACE(29):'COMPANY TOTALS -  '
   COM.LINE=COM.LINE:OCONV(COM.CREG,'MD2Z')'R#7 '
   COM.LINE=COM.LINE:OCONV(COM.CO.T,'MD2Z')'R#7 '
   COM.LINE=COM.LINE:OCONV(COM.CD.T,'MD2Z')'R#7   '
   COM.LINE=COM.LINE:OCONV(COM.NREG,'MD2Z')'R#7 '
   COM.LINE=COM.LINE:OCONV(COM.NO.T,'MD2Z')'R#7 '
   COM.LINE=COM.LINE:OCONV(COM.ND.T,'MD2Z')'R#7   '
   COM.LINE=COM.LINE:OCONV(COM.TREG,'MD2Z')'R#7 '
   COM.LINE=COM.LINE:OCONV(COM.TO.T,'MD2Z')'R#7 '
   COM.LINE=COM.LINE:OCONV(COM.TD.T,'MD2Z')'R#7   '
   PRINT COM.LINE
   GO 99999
*
*---- TOTAL EMPLOYEE'S PAYREG
500*
   MAT TOT.SAV=''
   FOR ISSUE = 1 TO 20
      MATREAD PAY.REC FROM PAYREG,CONO:PREV.EMP:ISSUE ELSE
         MAT PAY.REC="";GO 501
      END
      IF PAY.STATUS="" THEN
         FOR R = 1 TO PAY.RECORDS
            EMP.CREG<1,2>=EMP.CREG<1,2>+PAY.HOURS<1,R,1>
            EMP.CO.T<1,2>=EMP.CO.T<1,2>+PAY.HOURS<1,R,2>
            EMP.CD.T<1,2>=EMP.CD.T<1,2>+PAY.HOURS<1,R,3>
            EMP.NREG<1,2>=EMP.NREG<1,2>+PAY.HOURS<1,R,4>+PAY.HOURS<1,R,5>+PAY.HOURS<1,R,6>+PAY.HOURS<1,R,7>+PAY.HOURS<1,R,8>+PAY.HOURS<1,R,10>
            EMP.NO.T<1,2>=EMP.NO.T<1,2>+PAY.HOURS<1,R,9>+PAY.HOURS<1,R,11>
         NEXT R
      END
501*
   NEXT ISSUE
   EMP.TREG<1,2> = EMP.TREG<1,2>+EMP.CREG<1,2>+EMP.NREG<1,2>
   EMP.TO.T<1,2> = EMP.TO.T<1,2>+EMP.CO.T<1,2>+EMP.NO.T<1,2>
   EMP.TD.T<1,2> = EMP.TD.T<1,2>+EMP.CD.T<1,2>+EMP.ND.T<1,2>
   TOT.HRS=EMP.TREG<1,2>+EMP.TO.T<1,2>+EMP.TD.T<1,2>
   RETURN
*
*---- TOTAL DAY
1000*
   DAY.CREG = DAY.CREG + DHR.REG
   DAY.CO.T = DAY.CO.T + DHR.OT
   DAY.CD.T = DAY.CD.T + DHR.DT
   DAY.NREG = DAY.NREG + DHR.REG.NON
   DAY.NO.T = DAY.NO.T + DHR.OT.NON
   DAY.ND.T = DAY.ND.T + DHR.DT.NON
   DAY.TREG = DAY.TREG + DHR.REG+DHR.REG.NON
   DAY.TO.T = DAY.TO.T + DHR.OT+DHR.OT.NON
   DAY.TD.T = DAY.TD.T + DHR.DT+DHR.DT.NON
*
*---- CALCULATE OVERTIME IF REQUIRED
1200*
   IF CO.CALC.JCS.OT='Y' THEN GO 1250
   SAV.CREG = SAV.CREG + DHR.REG
   SAV.CO.T = SAV.CO.T + DHR.OT
   SAV.CD.T = SAV.CD.T + DHR.DT
   SAV.NREG = SAV.NREG + DHR.REG.NON
   SAV.NO.T = SAV.NO.T + DHR.OT.NON
   SAV.ND.T = SAV.ND.T + DHR.DT.NON
   RETURN
1250*
   HRS=DHR.REG    ;GOSUB 1260
   HRS=DHR.OT     ;GOSUB 1260
   HRS=DHR.DT     ;GOSUB 1260
   HRS=DHR.REG.NON;GOSUB 1270
   HRS=DHR.OT.NON ;GOSUB 1270
   HRS=DHR.DT.NON ;GOSUB 1270
   RETURN
*
*---- OVERTIME FOR CHARGABLE HOURS
1260*
   BEGIN CASE
      CASE TOT.HRS >= 4000
         SAV.CO.T =SAV.CO.T +HRS
      CASE TOT.HRS + HRS > 4000
         SAV.CREG=SAV.CREG+4000-TOT.HRS
         SAV.CO.T=SAV.CO.T+TOT.HRS+HRS-4000
      CASE 1
         SAV.CREG=SAV.CREG+HRS
   END CASE
   TOT.HRS=TOT.HRS+HRS
   RETURN
*
*---- OVERTIME FOR NONCHARGABLE HOURS
1270*
   BEGIN CASE
      CASE TOT.HRS >= 4000
         SAV.NO.T =SAV.NO.T +HRS
      CASE TOT.HRS + HRS > 4000
         SAV.NREG=SAV.NREG+4000-TOT.HRS
         SAV.NO.T=SAV.NO.T+TOT.HRS+HRS-4000
      CASE 1
         SAV.NREG=SAV.NREG+HRS
   END CASE
   TOT.HRS=TOT.HRS+HRS
   RETURN
*
*---- TOTAL EMPLOYEE'S JOB COSTING
2000*
   EMP.CREG<1,1> = EMP.CREG<1,1> + DAY.CREG
   EMP.CO.T<1,1> = EMP.CO.T<1,1> + DAY.CO.T
   EMP.CD.T<1,1> = EMP.CD.T<1,1> + DAY.CD.T
   EMP.NREG<1,1> = EMP.NREG<1,1> + DAY.NREG
   EMP.NO.T<1,1> = EMP.NO.T<1,1> + DAY.NO.T
   EMP.ND.T<1,1> = EMP.ND.T<1,1> + DAY.ND.T
   EMP.TREG<1,1> = EMP.TREG<1,1> + DAY.TREG
   EMP.TO.T<1,1> = EMP.TO.T<1,1> + DAY.TO.T
   EMP.TD.T<1,1> = EMP.TD.T<1,1> + DAY.TD.T
   RETURN
*
*---- TOTAL EMPLOYEE'S CALCULATED
3000*
   EMP.CREG<1,3> = EMP.CREG<1,3> + EMP.CREG<1,2>
   EMP.CO.T<1,3> = EMP.CO.T<1,3> + EMP.CO.T<1,2>
   EMP.CD.T<1,3> = EMP.CD.T<1,3> + EMP.CD.T<1,2>
   EMP.NREG<1,3> = EMP.NREG<1,3> + EMP.NREG<1,2>
   EMP.NO.T<1,3> = EMP.NO.T<1,3> + EMP.NO.T<1,2>
   EMP.ND.T<1,3> = EMP.ND.T<1,3> + EMP.ND.T<1,2>
*
   EMP.CREG<1,3> = EMP.CREG<1,3> + SAV.CREG
   LOCATE '2' IN HRS.SMV<1>,1 SETTING FNDHRS ELSE
      FND.OT=0
      EMP.CREG<1,3>=EMP.CREG<1,3>+SAV.CO.T
      GO 3010
   END
   FND.OT=1
   EMP.CO.T<1,3>=EMP.CO.T<1,3>+SAV.CO.T
3010*
   LOCATE '3' IN HRS.SMV<1>,1 SETTING FNDHRS ELSE
      FND.DT=0
      BEGIN CASE
         CASE FND.OT
            EMP.CO.T<1,3>=EMP.CO.T<1,3>+SAV.CD.T+SAV.ND.T
         CASE 1
            EMP.CREG<1,3>=EMP.CREG<1,3>+SAV.CD.T+SAV.ND.T
      END CASE
      GO 3050
   END
   FND.DT=1
   EMP.CD.T<1,3>=EMP.CD.T<1,3>+SAV.CD.T+SAV.ND.T
3050*
   LOCATE '8' IN HRS.SMV<1>,1 SETTING FNDHRS ELSE
      FND.REGNON=0
      EMP.CREG<1,3>=EMP.CREG<1,3>+SAV.NREG
      GO 3051
   END
   FND.REGNON=1
   EMP.NREG<1,3>=EMP.NREG<1,3>+SAV.NREG
3051*
   LOCATE '9' IN HRS.SMV<1>,1 SETTING FNDHRS ELSE
      BEGIN CASE
         CASE FND.OT
            EMP.CO.T<1,3>=EMP.CO.T<1,3>+SAV.NO.T
         CASE FND.REGNON
            EMP.NREG<1,3>=EMP.NREG<1,3>+SAV.NO.T
         CASE 1
            EMP.CREG<1,3>=EMP.CREG<1,3>+SAV.NO.T
      END CASE
      GO 3052
   END
   EMP.NO.T<1,3>=EMP.NO.T<1,3>+SAV.NO.T
3052*
   EMP.TREG<1,3> = EMP.TREG<1,3> + EMP.CREG<1,3> + EMP.NREG<1,3>
   EMP.TO.T<1,3> = EMP.TO.T<1,3> + EMP.CO.T<1,3> + EMP.NO.T<1,3>
   EMP.TD.T<1,3> = EMP.TD.T<1,3> + EMP.CD.T<1,3> + EMP.ND.T<1,3>
   RETURN
*
*---- TOTAL DEPARTMENT
4000*
   FOR X=1 TO 3
      DPT.CREG<PTR,X> = DPT.CREG<PTR,X> + EMP.CREG<1,X>
      DPT.CO.T<PTR,X> = DPT.CO.T<PTR,X> + EMP.CO.T<1,X>
      DPT.CD.T<PTR,X> = DPT.CD.T<PTR,X> + EMP.CD.T<1,X>
      DPT.NREG<PTR,X> = DPT.NREG<PTR,X> + EMP.NREG<1,X>
      DPT.NO.T<PTR,X> = DPT.NO.T<PTR,X> + EMP.NO.T<1,X>
      DPT.ND.T<PTR,X> = DPT.ND.T<PTR,X> + EMP.ND.T<1,X>
      DPT.TREG<PTR,X> = DPT.TREG<PTR,X> + EMP.TREG<1,X>
      DPT.TO.T<PTR,X> = DPT.TO.T<PTR,X> + EMP.TO.T<1,X>
      DPT.TD.T<PTR,X> = DPT.TD.T<PTR,X> + EMP.TD.T<1,X>
   NEXT X
   DPT.NUMB<PTR,1>=PREV.DPT
   DPT.DESC<PTR,1>=DEPT.DESC
   RETURN
*
*---- PRINT HEADINGS
9000*
   PRINT CHAR(12):
   PAGE.NO=PAGE.NO+1
   PRINT HD1:PAGE.NO
   PRINT HD2
   H.LINE=''
   H.LINE="DEPARTMENT - ":PREV.DPT"L#5   ":DEPT.DESC"L#30   ":SPACE(9):"PAY PERIOD ENDING ":END.DATE
   PRINT H.LINE
   PRINT
   H.LINE=''
   H.LINE=SPACE(38):"DATE     CHARGED CHARGED CHARGED   NON-CHG NON-CHG NON-CHG    TOTAL   TOTAL   TOTAL"
   PRINT H.LINE
   H.LINE=''
   H.LINE="EMP# EMPLOYEE NAME                   WORKED      REG     O/T     D/T       REG     O/T     D/T       REG     O/T     D/T     "
   PRINT H.LINE
   H.LINE="---- ":STR('-',30):" --------   ":U.LINE1
   PRINT H.LINE
   LINE.COUNT=LINE.COUNT+7
   RETURN
*
*---- PRINT DEPARTMENT HEADING
9010*
   PRINT CHAR(12):
   PAGE.NO=PAGE.NO+1
   PRINT HD1:PAGE.NO
   PRINT HD2
   H.LINE=SPACE(63):"PAY PERIOD ENDING ":END.DATE
   PRINT H.LINE
   PRINT
   H.LINE=SPACE(47):"CHARGED CHARGED CHARGED   NON-CHG NON-CHG NON-CHG    TOTAL   TOTAL   TOTAL"
   PRINT H.LINE
   H.LINE=''
   H.LINE="DEPT# DEPARTMENT DESCRIPTION                     REG     O/T     D/T       REG     O/T     D/T       REG     O/T     D/T"
   PRINT H.LINE
   H.LINE="----- ":STR('-',39):"  ":U.LINE1
   PRINT H.LINE
   LINE.COUNT=7
   RETURN
*
*---- CALL SYSCOM(MAT SYSCOM.REC)
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- END OF JOB
99999*
   PRINTER OFF
END
