************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRSBP
* PROGRAM     - WAGES.LIST
* BY          - WALID YAMOUT
* DATE        - 05/25/84
* DESCRIPTION -
* LAST COMPILE- 312
*ENDDOC
***********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>CPYLIB>CHAR
     OPEN "","EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE":;INPUT GO$;STOP
     OPEN "","QTD-YTD" TO QTD.YTD ELSE PRINT "CAN'T OPEN QTD-YTD":;INPUT GO$;STOP
     OPEN "","COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY":;INPUT GO$;STOP
     PROCREAD INBUFF ELSE PRINT "CAN'T READ INPUT BUFFER":;INPUT GO$;STOP
     CONO = INBUFF<1>
     MATREAD COMP.REC FROM COMPANY, CONO ELSE PRINT "CAN'T READ COMPANY":;INPUT GO$;STOP
     QTR = INBUFF<3>
     YEAR = INBUFF<4>
     END.DATE = INBUFF<5>
     RPT.QTR = ""
     DUE.DATE = ""
     POST.DATE = ""
     YR = YEAR
     IF QTR = 1 THEN
        RPT.QTR = "1ST QTR "
        DUE.DATE = "APR 1,"
        POST.DATE = "APR 30,"
     END
     IF QTR = 2 THEN
        RPT.QTR = "2ND QTR "
        DUE.DATE = "JUL 1,"
        POST.DATE = "JUL 31,"
     END
     IF QTR = 3 THEN
        RPT.QTR = "3RD QTR "
        DUE.DATE = "OCT 1,"
        POST.DATE = "OCT 31,"
     END
     IF QTR = 4 THEN
        RPT.QTR = "4TH QTR "
        DUE.DATE = "JAN 1,"
        POST.DATE = "JAN 31,"
        YR = YR + 1
     END
     RPT.QTR = RPT.QTR : YEAR
     DUE.DATE = DUE.DATE : YR
     POST.DATE = POST.DATE : YR
     SP14 = SPACE(14)
     SP10 = SPACE(10)
     SP2 = SPACE(2)
     SP3 = SPACE(3)
     SP62 = SPACE(62)
     SP6 = SPACE(6)
     SP15 = SPACE(15)
     SP5 = SPACE(5)
     EMP = ""
     S.S = ""
     FICA = ""
     LINES = 0
     PAID = ""
     NET = 0
     PAID.MAX = ""
100*
   READNEXT ID ELSE GOTO 200
   MAT EMP.REC = ""
   MAT QTD.REC = ""
   MATREAD EMP.REC FROM EMPLOYEE,ID ELSE GOTO 100
   MATREAD QTD.REC FROM QTD.YTD,ID ELSE GOTO 100
   IF QTD.GROSS<1,QTR> + 0 = 0 THEN GOTO 100
   LINES = LINES + 1
   EMP<LINES> = EMP.LAST.NAME : " " : EMP.FRST.NAME
   S.S<LINES> = EMP.SOC.SEC
   FICA<LINES> = QTD.GROSS<1,QTR>
   BLK = 0
   FOR I = 1 TO QTR UNTIL BLK
    PAID<LINES> = PAID<LINES> + QTD.GROSS<1,I>
    IF PAID<LINES> > CO.MAX.FICA.TAX THEN
       BLK = 1
       IF I = QTR THEN NET = NET + FICA<LINES> - (PAID<LINES> - CO.MAX.FICA.TAX)
    END
   NEXT I
   IF BLK = 0 THEN NET = NET + FICA<LINES>
   BLK = 0
   FOR I = 1 TO QTR UNTIL BLK
    PAID.MAX<LINES> = PAID.MAX<LINES> + QTD.GROSS<1,I>
    IF PAID.MAX<LINES> > CO.FICA.MAX THEN
       BLK = 1
       IF I < QTR THEN
          FICA<LINES> = 0
       END ELSE
          FICA<LINES> = FICA<LINES> - (PAID.MAX<LINES> - CO.FICA.MAX)
       END
    END
   NEXT I
   GOTO 100
200*
   CALL WAGES.LIST.ALIGN
   PRINTER ON
   GOSUB 2000
   CNT = 0
   TOTAL = 0
   FOR I = 1 TO LINES
       CNT = CNT + 1
       IF CNT = 45 THEN GOSUB 1000
       PRINT SP5:S.S<I> "L#11":SP10:EMP<I> "L#25":SP5:OCONV(FICA<I>, 'MD2') "R#9"
       TOT.PAGE = TOT.PAGE + FICA<I>
       TOTAL = TOTAL + FICA<I>
   NEXT I
   FOR I = CNT TO 44
     PRINT
   NEXT I
   PRINT SP62:OCONV(TOT.PAGE, 'MD2') "R#10"
   IF LINES > 44 THEN
      PRINT
      PRINT SP62:OCONV(TOTAL, 'MD2') "R#10"
   END
   FOR I = 1 TO 25
     PRINT
   NEXT I
   PRINT SP10:SP15:"NET TAXABLE WAGES TO ":OCONV(CO.FICA.MAX, 'MD2,')"R#8":" ":OCONV(NET, 'MD2') "R#11"
   PRINTER OFF
   GOTO 999999
1000*
    PRINT
    PRINT SP62:OCONV(TOT.PAGE, 'MD2') "R#10"
    PRINT
    PRINT
    PRINT
    PRINT
2000*
    CNT = 1
    TOT.PAGE = 0
    PRINT
    PRINT
    PRINT
    PRINT SP10:CO.FED.TAX.ID "L#11":SP3:CO.COUNTY.CODE "L#3":SP3:CO.AREA.CODE "L#2":SP3:
    PRINT OCONV(CO.FICA.PCNT, 'MD2') "R#3":"%":SP2:YEAR "R#4":
    PRINT SP14:END.DATE "L#10"
    PRINT SP14:CO.NAME "L#30"
    PRINT SP14:CO.ADDRESS "L#30"
    PRINT SP14:CO.CITY "L#13":SP3:CO.STATE "L#2":SP2:CO.ZIP[1,5]:'-':CO.ZIP[6,4]
    PRINT
    PRINT
    PRINT
    PRINT SP15:RPT.QTR "L#12":SP6:DUE.DATE "L#10"
    PRINT SP15:POST.DATE "L#11"
    PRINT
    PRINT
    PRINT
   RETURN
999999*
   END
