*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - PRSBP
* PROGRAM     - MATCH.401K.PCT.MAINT
* BY          - MARK TRAYWICK, CBA
* DATE        - 6/17/88
* DESCRIPTION - Maintains the 401(k) matching percentages
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*COPY>PRS.CPYLIB>401K.MATCH
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN FILES
*
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING'; GOSUB 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING'; GOSUB 93000
  OPEN '','PRS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PRS.SCREENS FILE IS MISSING'; GOSUB 93000
*
*--- GET COMPANY
*
  CONO = ""
  CALL GET.CONO(CONO, MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
*
*--- INITIALIZATION
*
  OLD.START.LINE =''; PAGE.SIZE = 15
  LN = 1
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME = 'MATCH.401K.PCT.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ''; MAT MTCH.REC = ''
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*--- MAIN PROCESSING
*
  MATREADU MTCH.REC FROM CONTROL, CONO:"401K.MATCH" THEN
    GOSUB 11000
    GOSUB 10000
  END ELSE
    LAST.LINE = 0
    OPTION = 'A'
    GOTO 10
  END
5 *
  ECD.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
10 *
  BEGIN CASE
    CASE OPTION = 'END' OR OPTION = 'E'
      GOTO 99999
    CASE OPTION = 'A'
      LOOP
        IF LAST.LINE AND MTCH.EMP.PCT<1,LAST.LINE> >= "9999" THEN
          OLD.LINES = LAST.LINE
        END ELSE
          LN = LAST.LINE + 1
          OLD.LINES = LAST.LINE
          GOSUB 10000
          SCV.REC(3)<ECD.SCRN.NO,LN> = LN
          ECD.NUM = 3; ECD.SUB.NUM = LN
          ECD.ACTION = 5; CALL SCRN.EDIT
          GOSUB 500
        END
      WHILE LAST.LINE > OLD.LINES DO
      REPEAT
      LN = LAST.LINE
      GOSUB 11000
    CASE OPTION = 'C' AND LAST.LINE
      GOSUB 20000
      IF ECD.RET.VALUE # 'END' THEN
        IF ECD.RET.VALUE > 0 AND ECD.RET.VALUE <= LAST.LINE THEN
          LN = ECD.RET.VALUE
          GOSUB 500
          OLD.START.LINE = 0
          GOSUB 11000
        END
      END
    CASE OPTION = 'D' AND LAST.LINE
      GOSUB 20000
      IF ECD.RET.VALUE # 'END' THEN
        LN = ECD.RET.VALUE
        MTCH.EMP.PCT = DELETE(MTCH.EMP.PCT,1,LN,0)
        MTCH.CO.PCT = DELETE(MTCH.CO.PCT,1,LN,0)
        OLD.START.LINE = 0
        GOSUB 11000
        IF LN > LAST.LINE THEN LN = LAST.LINE
        IF LN < 1 THEN LN = 1
      END
    CASE OPTION = 'I' AND LAST.LINE
      GOSUB 20000
      IF ECD.RET.VALUE # 'END' THEN
        LN = ECD.RET.VALUE
        MTCH.EMP.PCT = INSERT(MTCH.EMP.PCT,1,LN,0,'')
        MTCH.CO.PCT = INSERT(MTCH.CO.PCT,1,LN,0,'')
        OLD.START.LINE = 0
        GOSUB 11000
        GOSUB 10000
        GOSUB 500
        IF ECD.RET.VALUE = "END" THEN
          MTCH.EMP.PCT = DELETE(MTCH.EMP.PCT,1,LN,0)
          MTCH.CO.PCT = DELETE(MTCH.CO.PCT,1,LN,0)
        END
        OLD.START.LINE = 0
        GOSUB 11000
      END
    CASE OPTION = 'S'
      LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
      IF LN > LAST.LINE THEN LN = 1
    * T25978 v
    CASE OPTION = 'SR'
      LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
      IF LN < 1 THEN LN = 1
    CASE OPTION = 'ST'
      LN = 1
    CASE OPTION = 'SB'
      LN = LAST.LINE
    * T25978 ^
    CASE OPTION = 'F' AND LAST.LINE
      MATWRITE MTCH.REC ON CONTROL, CONO:"401K.MATCH"
      GOTO 99999
    CASE OPTION = 'F'
      DELETE CONTROL, CONO:"401K.MATCH"
      GOTO 99999
  END CASE
  GOSUB 10000
  GOTO 5
*
*--- GET EMPLOYEE PERCENT
500 *
  TEMP.EMP = MTCH.EMP.PCT<1,LN>
  IF LN > 1 THEN
    SCV.REC(4)<ECD.SCRN.NO,LN> = MTCH.EMP.PCT<1,LN-1>+1
  END ELSE
    SCV.REC(4)<ECD.SCRN.NO,LN> = 0
  END
  ECD.NUM = 4; ECD.SUB.NUM = LN
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.NUM = 5; ECD.SUB.NUM = LN
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      OLD.START.LINE = 0
      GOTO 699
    CASE LN < LAST.LINE AND ECD.RET.VALUE >= MTCH.EMP.PCT<1,LN+1>
      ERRMSG = 'MUST BE LESS THAN NEXT PERCENTAGE RANGE'
      GOSUB 91000
      GOTO 500
    CASE LN > 1 AND ECD.RET.VALUE <= MTCH.EMP.PCT<1,LN-1>
      ERRMSG = 'MUST BE GREATER THAN LAST PERCENTAGE RANGE'
      GOSUB 91000
      GOTO 500
    CASE 1
      TEMP.EMP = ECD.RET.VALUE
  END CASE
*
*--- GET COMPANY PCT
600 *
  ECD.NUM = 6; ECD.SUB.NUM = LN
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 500
    CASE LN < LAST.LINE AND ECD.RET.VALUE >= MTCH.CO.PCT<1,LN+1>
      ERRMSG = 'MUST BE LESS THAN NEXT PERCENTAGE RANGE'
      GOSUB 91000
      GOTO 600
    CASE LN > 1 AND ECD.RET.VALUE <= MTCH.CO.PCT<1,LN-1>
      ERRMSG = 'MUST BE GREATER THAN LAST PERCENTAGE RANGE'
      GOSUB 91000
      GOTO 600
    CASE 1
      MTCH.CO.PCT<1,LN> = ECD.RET.VALUE
  END CASE
  MTCH.EMP.PCT<1,LN> = TEMP.EMP
699*
  LAST.LINE = DCOUNT(MTCH.EMP.PCT<1>,VM)
  RETURN
*
*--- SCROLLING ROUTINE
10000 *
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE)* PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN RETURN
  OLD.START.LINE = START.LINE
  ECD.NUM = 3
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  ECD.NUM = 4
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  ECD.NUM = 5
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  ECD.NUM = 6
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  RETURN
*
*--- LOAD SCROLLING AREA
11000 *
  MAT SCV.REC = ''
  LAST.LINE = DCOUNT(MTCH.EMP.PCT<1>,VM)
  FOR I = 1 TO LAST.LINE
    SCV.REC(3)<ECD.SCRN.NO,I> = I
    BEGIN CASE
      CASE I = 1
        SCV.REC(4)<ECD.SCRN.NO,I> = 0
      CASE MTCH.EMP.PCT<1,I> > MTCH.EMP.PCT<1,I-1> AND MTCH.EMP.PCT<1,I-1> > 0
        SCV.REC(4)<ECD.SCRN.NO,I> = MTCH.EMP.PCT<1,I-1>+1
      CASE 1
        SCV.REC(4)<ECD.SCRN.NO,I> = ""
    END CASE
    SCV.REC(5)<ECD.SCRN.NO,I> = MTCH.EMP.PCT<1,I>
    SCV.REC(6)<ECD.SCRN.NO,I> = MTCH.CO.PCT<1,I>
  NEXT I
11099 *
  RETURN
*
*--- GET LINE NUMBER
*
20000 *
  GOSUB 10000
  ECD.NUM = 2
  ECD.MINV = START.LINE
  ECD.MAXV = START.LINE + PAGE.SIZE - 1
  IF ECD.MAXV > LAST.LINE THEN ECD.MAXV = LAST.LINE
  ECD.ACTION = 4
  CALL SCRN.EDIT
  RETURN
*
*--- SYSCOM CALLS
91000 * 
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 * 
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * 
END
