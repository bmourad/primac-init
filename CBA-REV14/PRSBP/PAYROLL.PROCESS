*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE         - PRSBP
* PROGRAM        - PAYROLL.PROCESS
* BY             - REBECCA GARCIA
* DATE           - 07/31/89
* DESCRIPTION    - This program will update and maintain payroll
*                  batch for payroll processing.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PRS.CPYLIB>HEADER
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
      OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
      OPEN '','PRS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
      OPEN '', 'PAYTYPE' TO PAYTYPE ELSE ERRMSG = 'PAYTYPE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
      MAT EDIT.COM = ""
      MAT EDIT.COM.DRIVER = ""
      ERRMSG = ""
      UNKNOWN = STR("?",30)
*
*---- GET COMPANY NUMBER
*
      CONO = ""
      CALL GET.CONO(CONO,MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
      MAT EDIT.COM.DRIVER = ""
*
*---- PRINT SCREEN
*
      ECD.SCRN.CNT = 1
      ECD.SCRN.NAME<1> = 'PAYROLL.PROCESS'
      ECD.ACTION = 1; CALL SCRN.EDIT
      ECD.SCRN.NO = 1
      MAT SCV.REC = ""
      ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- DISPLAY COMPANY
*
      ECD.NUM = 1
      SCV.REC(1) = CONO
      ECD.ACTION = 5; CALL SCRN.EDIT
*
*---- MAIN PROCESSING ----------
*
*---- ENTER BATCH NUMBER
*
100 *
      ECD.NUM = 2
      ECD.ACTION = 4; CALL SCRN.EDIT
      BATCH.NO = ECD.RET.VALUE
      IF BATCH.NO = 'END' THEN GOTO 99999
      MATREAD PTHR.REC FROM PAYTYPE, CONO:"HEADER":BATCH.NO THEN
         SCV.REC(3) = PTHR.END.DATE
         SCV.REC(4) = PTHR.CHK.DATE
         SCV.REC(5) = PTHR.PAY.PER
         SCV.REC(6) = PTHR.DED.WEEK
         ECD.ACTION = 3; CALL SCRN.EDIT
      END ELSE
         MAT PTHR.REC = ""
         FOR OPTION = 1 TO 4 WHILE ECD.RET.VALUE # "END"
            ON OPTION GOSUB 300,400,500,600
         NEXT OPTION
         IF ECD.RET.VALUE = "END" THEN GOTO 100
      END
*---- ENTER OPTIONS
*
      LOOP
         ECD.NUM = 7
         SCV.REC(ECD.NUM) = ""
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
         CASE OPTION = 'END' OR OPTION = 'E'
            OPTION = "END"
         CASE NUM(OPTION)
            IF OPTION > 0 AND OPTION < 5 THEN
               ON OPTION GOSUB 300,400,500,600
            END
         CASE OPTION = 'F'
            MATWRITE PTHR.REC ON PAYTYPE,CONO:"HEADER":BATCH.NO
            OPTION = "END"
PROC.VALUES = ""
            PROC.VALUES<1> = CONO
            PROC.VALUES<2> = BATCH.NO
            PROCWRITE PROC.VALUES
         END CASE
      WHILE OPTION # "END" DO REPEAT
      GOTO 99999
******************************************************************
*
*---- ENTER PERIOD ENDING DATE
*
300 *
      ECD.NUM = 3
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         PTHR.END.DATE = ECD.RET.VALUE
      END 
      RETURN
*
*---- ENTER PAYCHECK DATE
*
400 *
      ECD.NUM = 4
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         IF ECD.RET.VALUE < (PTHR.END.DATE - 10) THEN
            ERRMSG = "Invalid Check Date"
            GOSUB 91000
            GOTO 400
         END
         PTHR.CHK.DATE = ECD.RET.VALUE
      END 
      RETURN
*
*---- ENTER PAY PERIOD NUMBER
*
500 *
      ECD.NUM = 5
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         PTHR.PAY.PER = ECD.RET.VALUE
      END 
      RETURN
*
*--- ENTER DEDUCTION WEEK
*
600 *
      ECD.NUM = 6
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         PTHR.DED.WEEK = ECD.RET.VALUE
      END 
      RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999 *
PRINT @(0,21):CL
      END
