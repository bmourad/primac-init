*COPY>CPYLIB>COM1
**********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - PRSBP
* PROGRAM       - PETTY.CASH.MAINT
* BY            - WALID YAMOUT, CBA
* DATE          - 08/06/84
* DESCRIPTION   -
* LAST COMPILE - 605
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST).
**********************************************************************
*ENDDOC
*COPY>PRS.CPYLIB>PETTY.CASH
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN THE FILES
*
  OPEN '','PRS.SCREENS' TO M.SCREENS ELSE ERRMSG="M.SCREENS FILE IS MISSING"; GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING"; GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING"; GOTO 93000
  OPEN '','PETTY.CASH' TO PETTY.CASH ELSE ERRMSG="PETTY.CASH FILE IS MISSING"; GOTO 93000
  OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG="EMPLOYEE FILE IS MISSING"; GOTO 93000
*
*--- GET COMPANY NUMBER
*
  MAT COMP.REC = ''
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 999999
*
  READU PTY.CODE FROM CONTROL, CONO:"PETTY.CASH.CODE" ELSE ERRMSG = "PETTY CASH CODE IS NOT ON CONTROL FILE";GOSUB 91000; GOTO 999999
*
*--- INITIALIZATION
*
  MAT EDIT.COM = ""
  TYP = 0
  CALL EDIT.SUB
  FILL = "#"
  MAT EDIT.COM.DRIVER = ""
  LINE.SPACE = 1
  PAGE.SIZE = 14
  BEGIN.PAGE = 4
  LINES = 0
  LN = 1
  OLD.START.LINE = 0
  START.LINE = 0
  OLD.LINES = 0
  OPTION = ""
  NAMES = ""
  WITHDRAW = ""
  DEDUCT = ""
  EMPLOY = ""
  BALANCE = ""
  UNKNOWN = "??????????"
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "PETTY.CASH.MAINT"
  ECD.ACTION=1;CALL SCRN.EDIT
100*
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
  I = 0
  SELECT PETTY.CASH
125*
  READNEXT KEY ELSE GOTO 150
  MAT PTY.REC = ""
  MATREAD PTY.REC FROM PETTY.CASH, KEY ELSE GOTO 125
  I = I + 1
  EMPLOY<1,I> = PTY.EMP.NO
  BALANCE<1,I> = PTY.BALANCE
  DEDUCT<1,I> = PTY.DEDUCT
  GOTO 125
150*
  IF EMPLOY = "" THEN
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 2000
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END ELSE
    LINES = COUNT(EMPLOY,VM) + (EMPLOY # "")
    TOTAL = 0
    FOR I = 1 TO LINES
      MAT EMP.REC = ""
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPLOY<1,I> ELSE
        EMP.LAST.NAME = UNKNOWN
        EMP.FRST.NAME = UNKNOWN
      END
      NAMES<1,I> = EMP.LAST.NAME : " " : EMP.FRST.NAME
      TOTAL = TOTAL + BALANCE<1,I>
    NEXT I
  END
  GOSUB 10000
  SCV.REC(9)<1> = TOTAL ; ECD.NUM = 9 ; ECD.ACTION = 5 ; CALL SCRN.EDIT
*
*--- ENTER OPTION
*
  MORE = 1
  LOOP
    ECD.NUM = 7
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
    ECD.ACTION=4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "E" OR OPTION = "END"
        MORE = 0
      CASE OPTION = "A"
        LOOP
          LN = LINES + 1
          OLD.LINES = LINES
          GOSUB 2000
        WHILE LINES > OLD.LINES DO REPEAT
        LN = LINES
        GOSUB 10000
      CASE OPTION = "S"
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 10000
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 10000
      CASE OPTION = 'ST'
        LN = 1
        GOSUB 10000
      CASE OPTION = 'SB'
        LN = LINES
        GOSUB 10000
      * T25978 ^
      CASE OPTION = "C"
        GOSUB 70000
        IF LNO THEN
          LN = LNO
          SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          GOSUB 2010
        END
        GOSUB 10000
*     CASE OPTION = "D"
*        GOSUB 70000
*        IF LNO THEN
*           LN = LNO
*           EMPLOY = DELETE(EMPLOY,1,LN,0)
*           NAMES = DELETE(NAMES,1,LN,0)
*           WITHDRAW = DELETE(WITHDRAW,1,LN,0)
*           DEDUCT = DELETE(DEDUCT,1,LN,0)
*           BALANCE = DELETE(BALANCE,1,LN,0)
*           LINES = COUNT(EMPLOY,VM) + (EMPLOY # "")
*           IF LN > 1 AND LN > LINES THEN LN = LN - 1
*           IF LN < 1 THEN LN = 1
*           OLD.START.LINE = 0
*        END
*        GOSUB 10000
      CASE OPTION = "F"
        LINES = COUNT(EMPLOY,VM) + (EMPLOY # "")
        FOR I = 1 TO LINES
          IF BALANCE<1,I> <> 0 THEN
            MAT PTY.REC = ""
            PTY.EMP.NO = EMPLOY<1,I>
            PTY.BALANCE = BALANCE<1,I>
            PTY.DEDUCT = DEDUCT<1,I>
            MATWRITE PTY.REC ON PETTY.CASH, CONO:PTY.EMP.NO
          END ELSE
            DELETE PETTY.CASH, CONO:EMPLOY<1,I>
          END
        NEXT I
        MORE = 0
    END CASE
  WHILE MORE DO REPEAT
  GOTO 999999
*
*--- GET ALL EMPLOYEE NUMBERS
*
2000*
  GOSUB 10000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  PRINT @(0,SLN) : CL :
  PRINT @(0,SLN) : LN "R#3" :
  EMPLOY = INSERT(EMPLOY,1,LN,0,"")
  NAMES = INSERT(NAMES,1,LN,0,"")
  WITHDRAW = INSERT(WITHDRAW,1,LN,0,"")
  DEDUCT = INSERT(DEDUCT,1,LN,0,"")
  BALANCE = INSERT(BALANCE,1,LN,0,"")
2010*
  FILL = "#"
  X = 4; Y = SLN; TYP = 3; MAXL = 4; MAXV = ""; MINV = ""
  HMSG = "ENTR EMPLOYEE NUMBER"
  IF EMPLOY<1,LN> = "" THEN O.R = "R" ELSE DEFAULT = EMPLOY<1,LN>; O.R = "O"
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END"
      IF OPTION = "C" THEN
        PRINT @(4,SLN) : EMPLOY<1,LN> "L#4" :
        PRINT @(9,SLN) : NAMES<1,LN> "L#30" :
        PRINT @(40,SLN) : OCONV(WITHDRAW<1,LN>, 'MD2') "R#9" :
        PRINT @(50,SLN) : OCONV(DEDUCT<1,LN>, 'MD2') "R#9" :
        PRINT @(60,SLN) : OCONV(BALANCE<1,LN>, 'MD2') "R#9" :
        GOTO 2999
      END ELSE
        EMPLOY = DELETE(EMPLOY,1,LN,0)
        NAMES = DELETE(NAMES,1,LN,0)
        WITHDRAW = DELETE(WITHDRAW,1,LN,0)
        DEDUCT = DELETE(DEDUCT,1,LN,0)
        BALANCE = DELETE(BALANCE,1,LN,0)
        PRINT @(0,SLN):CL:
        GOTO 2888
      END
    CASE VALUE # ""
      IF LEN(VALUE) < 4 THEN
        VALUE = STR("0",4-LEN(VALUE)):VALUE
        PRINT @(4,SLN):VALUE "L#4":
      END
      IF VALUE # EMPLOY<1,LN> THEN
        LOCATE VALUE IN EMPLOY<1>,1 SETTING LL ELSE LL = 0
        IF LL THEN
          ERRMSG = VALUE : " EMPLOYEE IS ALREADY INPUTED LINE " : LL
          GOSUB 91000
          GOTO 2010
        END
        MAT EMP.REC = ""
        MATREAD EMP.REC FROM EMPLOYEE, CONO:VALUE ELSE
          ERRMSG = "EMPLOYEE ":VALUE:" IS NOT ON FILE"; GOSUB 91000 ; GOTO 2010
        END
        LOCATE PTY.CODE IN EMP.MSC.CODE<1>,1 SETTING FND ELSE
          ERRMSG = "PETTY CASH IS NOT SETUP AS A DEDUCTION FOR THIS EMPLOYEE ":EMPLOY<1,LN>
          GOSUB 91000; GOTO 2010
        END
        EMPLOY<1,LN> = VALUE
        NAMES<1,LN> = EMP.LAST.NAME: " " : EMP.FRST.NAME
        PRINT @(9,SLN):NAMES<1,LN> "L#30":
      END
  END CASE
2020*
  X = 40; Y = SLN; TYP = 4; MAXL = 9; MAXV = ""; MINV = ""
  DEFAULT = OCONV(WITHDRAW<1,LN>, 'MD2') ; SCALER = 2
  IF WITHDRAW<1,LN> = "" THEN O.R ="R" ELSE O.R = "O"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 2010
  IF VALUE = "" THEN GOTO 2020
  BALANCE<1,LN> = BALANCE<1,LN> - WITHDRAW<1,LN> + VALUE
  WITHDRAW<1,LN> = VALUE
  PRINT @(60,SLN) : OCONV(BALANCE<1,LN>, 'MD2') "R#9" :
2888*
  LINES = COUNT(EMPLOY,VM) + (EMPLOY # "")
  TOTAL = 0
  FOR I = 1 TO LINES ; TOTAL = TOTAL + BALANCE<1,I> ; NEXT I
  ECD.NUM = 9 ; SCV.REC(9)<1> = TOTAL ; ECD.ACTION = 5 ; CALL SCRN.EDIT
2999*
  RETURN
*
*--- SCROLL ROUTINE
*
10000*
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN GOTO 10001
  OLD.START.LINE = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(0,SLN) : N "R#3" :
    PRINT @(4,SLN) : EMPLOY<1,N> "R#4" :
    PRINT @(9,SLN) : NAMES<1,N> "L#30" :
    PRINT @(40,SLN) : OCONV(WITHDRAW<1,N>, 'MD2') "R#9" :
    PRINT @(50,SLN) : OCONV(DEDUCT<1,N>, 'MD2') "R#9":
    PRINT @(60,SLN) : OCONV(BALANCE<1,N>, 'MD2') "R#9" :
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    PRINT @(0,SLN) : CL :
  NEXT M
10001*
  RETURN
*
*--- GET LINE NUMBER
*
70000*
  GOSUB 10000
  X = 0; Y = 23; TYP = 3; HMSG = ""; PMSG = "ENTER LINE NUMBER : "
  MINV = OLD.START.LINE; O.R = "O"; MAXL = 3
  IF LAST.LINE < LINES THEN
    MAXV = LAST.LINE
  END ELSE
    MAXV = LINES
  END
  CALL EDIT.SUB
  PRINT @(0,23) : CL :
  LNO = VALUE
  IF LNO = "" OR LNO = "END" THEN LNO = 0
  RETURN
*
*--- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
999999*
*   PRINT @(-1):
  ECD.ACTION = 99; CALL SCRN.EDIT
  RELEASE
END
