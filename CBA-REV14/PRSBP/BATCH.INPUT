**************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SOURCE     - PRSBP
* PROGRAM    - BATCH.INPUT
* BY         - WALID YAMOUT , C.B.A
* DATE       - 06/11/85
* DESCRIPTION-
* LAST COMPILE - 171
*ENDDOC
**************************************************************************
*
*--- INSERT FILES EQUETES
*
*COPY>PRS.CPYLIB>PAYROLL.HOURS
*COPY>PRS.CPYLIB>BATCH
*COPY>CPYLIB>CHAR
ERRMSG = ""
PROMPT""
********
     OPEN "","BATCH" TO BATCH ELSE PRINT "BATCH FILE ZAP";STOP
     OPEN "","COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY";STOP
     OPEN "","CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL";STOP
     LBMASK="R############"
     PROCREAD X ELSE PRINT "MUST COME FROM PROC  -  HIT RETURN":;INPUT X;STOP
    CONO=X<1>
    CO.NAME = X<2>
    MATREAD HRS.REC FROM CONTROL, CONO:"PAYROLL.HOURS" ELSE PRINT "PAYROLL.HOURS NOT ON CONTROL"; STOP
    HRS.CNT = COUNT(HRS.DESC,VM) + (HRS.DESC # "")
    IF HRS.CNT < 1 OR HRS.CNT > 11 THEN
      PRINT "ERROR IN PAYROLL.HOURS ON CONTROL"
      STOP
    END
***************************************************************************
5  *** INITIAL SCREEN DISPLAY
***************************************************************************
*     PRINT CHAR(12):
PRINT CS:
     PRINT@(19,1):"* * *  WEEKLY BATCH INPUT * * *":
     PRINT@(19,2):"       ------------------":
     PRINT @(5,4):"BATCH NUMBER:":
     PRINT @(5,6):"HOURS---":
     FOR H = 1 TO HRS.CNT
       PRINT @(5,H+7):H "R#2":" - ":HRS.SHORT<1,H> "L#7":" :":
     NEXT H
***************************************************************************
200 * CALL UP AND MAINTAIN OR ENTER NEW
***************************************************************************
     MAT BAT.REC=""
     PRINT@(10,22):"ENTER BATCH NUMBER OR END: ###":STR(CHAR(8),3):;INPUT BATNO
     PRINT@(10,22):SPACE(65):
     IF BATNO = "END" THEN STOP
     IF BATNO="" THEN GOTO 200
     IF BATNO<1 OR BATNO>999 THEN GOTO 200
     BATNO = STR("0",3-LEN(BATNO)):BATNO
     PRINT@(23,4):BATNO"R#3":
     MATREADU BAT.REC FROM BATCH,CONO:BATNO ELSE GOTO 800
***************************************************************************
     FOR Y = 1 TO HRS.CNT
       PRINT @(23,Y+7):OCONV(BAT.REC(HRS.SMV<1,Y>+2), "MD2") "R#8":
     NEXT Y
***************************************************************************
300 * TEST FOR DELETE OPTION
***************************************************************************
     PRINT@(10,22):"DO YOU WANT TO DELETE THIS RECORD ? (Y/RTN): #":CHAR(8):;INPUT AN
     PRINT@(10,22):SPACE(55)
     IF AN='Y***' THEN GOTO 700
     IF AN#"" THEN GOTO 300
****************************************************************************
400 * TEST FOR CHANGE OPTION
***************************************************************************
     PRINT@(10,22):"DO YOU WANT TO MAKE ANY CHANGES ? (RTN/N): #":CHAR(8):;INPUT AN
     PRINT@(10,22):SPACE(65):
     IF AN="N" THEN RELEASE BATCH,CONO:BATNO; GOTO 5
     IF AN#"" THEN GOTO 400
***************************************************************************
500 * GET LINE NUMBER OF FIELD TO BE CHANGED
***************************************************************************
     PRINT@(10,22):"ENTER NUMBER OF LINE TO BE CHANGED OR (RTN): ##":STR(CHAR(8),2):;INPUT LN
***************************************************************************
600 * TEST FOR END OF CHANGES
***************************************************************************
     IF LN="" THEN
        PRINT@(10,22):SPACE(65):
***************************************************************************
650 * RECHECK BEFORE WRITING RECORD
***************************************************************************
        PRINT@(10,22):"IS THIS WHAT YOU WANT ? (RTN/N): #":CHAR(8):;INPUT AN
        PRINT@(10,22):SPACE(50):
        IF AN="" THEN
         MATWRITE BAT.REC ON BATCH,CONO:BATNO
         GOTO 5
         END
        IF AN="N" THEN GOTO 500
        GOTO 650
     END
     IF LN<1 OR LN>HRS.CNT THEN GOTO 500
     GOSUB 1001
     GOTO 500
***************************************************************************
700 * DELETE RECORD
***************************************************************************
     DELETE BATCH, CONO:BATNO
     FOR B1=1 TO 10
     PRINT@(10,22):"RECORD DELETED FROM FILE ! ! !":CHAR(7)
     NEXT B1
     PRINT@(10,22):SPACE(50)
     GOTO 5
***************************************************************************
800 * CHECK FOR NEW BATCH NUMBER
***************************************************************************
     PRINT@(10,22):"IS THIS A NEW RECORD ? (RTN/N): #":CHAR(8):;INPUT AN
     PRINT@(10,22):SPACE(55):
     IF AN="N" THEN GOTO 5
     IF AN#"" THEN GOTO 800
     BAT.CONO = CONO; BAT.NO = BATNO
     FOR LN = 1 TO HRS.CNT
       GOSUB 1001
     NEXT LN
     GOTO 650
***************************************************************************
1001 * INITIALIZE CHANGE ROUTINE
***************************************************************************
   NBLOC="#####.##  ";CLM=23;MAX=8;NMAX=999999;NMIN=(-999999);DC=2;VAL=0;SVAL=0
   LIN = LN + 7; ATTR = HRS.SMV<1,LN> + 2
   GOSUB 8600
RETURN
***************************************************************************
8600 * NUMERIC INPUT SUB-ROUTINE *
***************************************************************************
8601 PRINT@(CLM,LIN):NBLOC:@(CLM,LIN):;INPUT ENTRY
     IF ENTRY # "" THEN
       IF ENTRY=' ' THEN ENTRY=0
        IF NUM(ENTRY) THEN NULL ELSE GOTO 8601
        IF DC THEN ENTRY=ICONV(ENTRY,"MD":DC)
        IF ENTRY >= NMIN AND ENTRY <= NMAX THEN NULL ELSE GOTO 8601
***************************************************************************
     * STORE ENTRY IN RECORD IF NEW OR CHANGED
***************************************************************************
       IF ENTRY=0 THEN ENTRY=''
        BAT.REC(ATTR)=REPLACE(BAT.REC(ATTR),1,VAL,SVAL,ENTRY)
     END
     PRINT@(CLM,LIN):SPACE(MAX+1)
     PRINT@(CLM,LIN):OCONV(EXTRACT(BAT.REC(ATTR),1,VAL,SVAL),"MD":DC) LBMASK[1,MAX+1]
     VAL=0;SVAL=0;DC=0
     RETURN
END
