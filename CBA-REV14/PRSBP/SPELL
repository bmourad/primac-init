      SUBROUTINE SPELL
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* ALL RIGHTS RESERVED
*TITLE: SPELL CHECK AMOUNT
*AUTHOR: MICRODATA / DCT
      COM NET,WORD
      DIM DOL(19),TEN(9)
      DOL(1)="ONE"
      DOL(2)="TWO"
      DOL(3)="THREE"
      DOL(4)="FOUR"
      DOL(5)="FIVE"
      DOL(6)="SIX"
      DOL(7)="SEVEN"
      DOL(8)="EIGHT"
      DOL(9)="NINE"
      DOL(10)="TEN"
      DOL(11)="ELEVEN"
      DOL(12)="TWELVE"
      DOL(13)="THIRTEEN"
      DOL(14)="FOURTEEN"
      DOL(15)="FIFTEEN"
      DOL(16)="SIXTEEN"
      DOL(17)="SEVENTEEN"
      DOL(18)="EIGHTEEN"
      DOL(19)="NINETEEN"
      TEN(2)="TWENTY"
      TEN(3)="THIRTY"
      TEN(4)="FORTY"
      TEN(5)="FIFTY"
      TEN(6)="SIXTY"
      TEN(7)="SEVENTY"
      TEN(8)="EIGHTY"
      TEN(9)="NINETY"
      IF NET<=0 THEN WORD=""; FOR X=1 TO 10;WORD=WORD:"*VOID"; NEXT X; WORD=WORD:"*"; RETURN
500   L=LEN(NET)
      CENTS=NET[L-1,2]
      IF CENTS=0 THEN CENTS="NO"
      IF L>2 THEN UNITS=NET[L-2,1] ELSE UNITS=0
      IF L>3 THEN TENS=NET[L-3,1] ELSE TENS=0
      IF L>4 THEN HUND=NET[L-4,1] ELSE HUND=0
      IF L>5 THEN THOU=NET[L-5,1] ELSE THOU=0
      IF L>6 THEN TENTHOU=NET[L-6,1] ELSE TENTHOU=0
      IF L>7 THEN HUNDTHOU=NET[L-7,1] ELSE HUNDTHOU=0
      WORD=CENTS:"/100"
      YY=UNITS
      IF TENS<2 THEN YY=TENS:YY
      IF YY#0 THEN WORD=DOL(YY):" AND ":WORD
      IF TENS>1 THEN
         IF UNITS=0 THEN WORD=" AND ":WORD ELSE WORD="-":WORD
      END
      IF TENS>1 THEN WORD=TEN(TENS):WORD
      IF HUND#0 THEN WORD=DOL(HUND):" HUNDRED ":WORD
      YY=THOU
      IF TENTHOU<2 THEN YY=TENTHOU:YY
      IF YY#0 OR TENTHOU#0 THEN
         IF HUND=0 THEN WORD=" THOUSAND AND ":WORD ELSE WORD=" THOUSAND ":WORD
      END
      IF YY#0 THEN WORD=DOL(YY):WORD
      IF TENTHOU>1 THEN
         IF THOU#0 THEN WORD="-":WORD
         WORD=TEN(TENTHOU):WORD
      END
      IF HUNDTHOU=0 THEN GOTO 510
      YY=HUNDTHOU
      HWORD=DOL(YY):" HUNDRED"
      IF TENTHOU=0 AND THOU=0 THEN WORD=HWORD:"  THOUSAND, ":WORD ELSE WORD=HWORD:" ":WORD
510   WORD="***":WORD
      RETURN
   END
