*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - EMP.CONTR.RPT
* BY          - KERRY WILSON
* DATE        - 05/24/88
* DESCRIPTION - THIS PROGRAM CALCULATES MATCHING PERCENTAGES FOR
*             - CALCULATION OF YTD MATCHING 401K CONTRIBUTIONS.
*T21313 lanny 12/10/1996 * BAD EMPLOYER CALC ON MATCH.
*T26275 cm 11/09/2001 * Heading correction problems.
*T26493 cmykleb 04/30/2002 * Get the rpt # from the proc.
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>DIVISION
*COPY>PRS.CPYLIB>EMPLOYEE.INVEST.CONTR
*COPY>PRS.CPYLIB>401K.MATCH
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD INBUF ELSE
      ERRMSG = 'MUST BE RUN FROM A PROC'
      GOTO 93000
   END
   CONO = INBUF<1>
   CONO.NAME = INBUF<2>
   START.MO = INBUF<3>
   END.MO = INBUF<4>
   BACKUP = INBUF<5>
*
*---- OPEN FILES
*
   IF BACKUP = 'Y' THEN
      OPEN 'S.COMPANY' TO COMPANY ELSE
         ERRMSG = 'S.COMPANY FILE MISSING'
         GOTO 93000
      END
      OPEN 'S.EMPLOYEE.INVEST.CONTR' TO EMPLOYEE.INVEST.CONTR ELSE
         ERRMSG = 'S.EMPLOYEE.INVEST.CONTR FILE IS MISSING'
         GOTO 93000
      END
      OPEN 'S.EMPLOYEE' TO EMPLOYEE ELSE
         ERRMSG = 'S.EMPLOYEE FILE IS MISSING'
         GOTO 93000
      END
   END ELSE
      OPEN 'COMPANY' TO COMPANY ELSE
         ERRMSG = 'COMPANY FILE MISSING'
         GOTO 93000
      END
      OPEN 'EMPLOYEE.INVEST.CONTR' TO EMPLOYEE.INVEST.CONTR ELSE
         ERRMSG = 'EMPLOYEE.INVEST.CONTR FILE IS MISSING'
         GOTO 93000
      END
      OPEN 'EMPLOYEE' TO EMPLOYEE ELSE
         ERRMSG = 'EMPLOYEE FILE IS MISSING'
         GOTO 93000
      END
   END
   OPEN 'CONTROL' TO CONTROL ELSE
      ERRMSG = 'CONTROL FILE IS MISSING'
      GOTO 93000
   END
   OPEN 'DEPARTMENT' TO DEPARTMENT ELSE
      ERRMSG = 'DEPARTMENT FILE IS MISSING'
      GOTO 93000
   END
   OPEN 'DIVISION' TO DIVISION ELSE
      ERRMSG = 'DIVISION FILE IS MISSING'
      GOTO 93000
   END
*
*---- INITIALIZATION
*
   COMP.DEPT.MATCH = 0
   COMP.DEPT.TOTAL = 0
   COMP.DIV.MATCH = 0
   COMP.DIV.TOTAL = 0
   GRAND.MATCH = 0
   GRAND.TOTAL = 0
   GRAND.BTM = 0
   GRAND.BTU = 0
   GRAND.ATM = 0
   GRAND.ATU = 0
   DIM CODE.TOT(4)
   EQU CODE.BTM TO CODE.TOT(1)
   EQU CODE.BTU TO CODE.TOT(2)
   EQU CODE.ATM TO CODE.TOT(3)
   EQU CODE.ATU TO CODE.TOT(4)
   MAT CODE.TOT = 0
   CODE.MATCH = 0
   CODE.TOTAL = 0
   DIM DIV.TOT(4)
   EQU DIV.BTM TO DIV.TOT(1)
   EQU DIV.BTU TO DIV.TOT(2)
   EQU DIV.ATM TO DIV.TOT(3)
   EQU DIV.ATU TO DIV.TOT(4)
   MAT DIV.TOT = 0
   DIM DEPT.TOT(4)
   EQU DEPT.BTM TO DEPT.TOT(1)
   EQU DEPT.BTU TO DEPT.TOT(2)
   EQU DEPT.ATM TO DEPT.TOT(3)
   EQU DEPT.ATU TO DEPT.TOT(4)
   MAT DEPT.TOT = 0
   TOT.MATCH = ''; TOT.PCT.MATCH = ''
   YTD.TOTAL = ''; YTD.MATCH = ''
   BTM.TOT = 0; BTU.TOT = 0; ATM.TOT = 0; ATU.TOT = 0
   UNDER.LINE = SPACE(53)
   FOR I = 1 TO 6
      UNDER.LINE = UNDER.LINE:'---------- '
   NEXT I
*
*---- INITIALIZE HEADINGS
*
   HD1 = ''
   HD2 = ''
*T26493 v
*  REPORT.NAME = 'TOTAL 401K CONTRIBUTIONS BY TYPE'
*  REPORT.NUMBER = 'XXXX'
   REPORT.NAME = ""
   CONO.NAME = ""
   REPORT.NUMBER = INBUF<2>
*T26493 ^
   REPORT.DATE = DATE()
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*T26275  v
*     HD3 =SPACE(50):"FOR PERIOD ":START.MO:" THROUGH PERIOD ":END.MO
*     HD4 = 'DIV  DEPT EMP#       EMPLOYEE NAME        SOC SEC #    PTD-BTM    PTD-BTU    '
*     HD4 = HD4:'PTD-ATU    PTD-ATM   PTD MATCH  PTD TOTAL'
   HD3 =SPACE(50):"FOR MONTH ":START.MO:" THROUGH MONTH ":END.MO
   HD4 = 'DIV  DEPT EMP#       EMPLOYEE NAME        SOC SEC #    MTD-BTM    MTD-BTU    '
   HD4 = HD4:'MTD-ATU    MTD-ATM   MTD MATCH  MTD TOTAL'
*T26275 ^
   HD5 = '--- ----- ---- ------------------------- ----------- ---------- ---------- '
   HD5 = HD5:'---------- ---------- ---------- ----------'
*
*---- MAIN PROCESSING
*
   MATREAD MTCH.REC FROM CONTROL, CONO:"401K.MATCH" ELSE 
      ERRMSG = '401K.MATCH RECORD NOT FOUND'; GOTO 93000
   END
   MCNT = DCOUNT(MTCH.EMP.PCT,VM)
   IF MCNT < 1 THEN
      ERRMSG = '401K.MATCH RECORD NOT FOUND'; GOTO 93000
   END
   PRINTER ON
   HEADING HD1:"'PL'":HD2:"'L'":HD3:"'LL'":HD4:"'L'":HD5:"'L'"
   READNEXT ID ELSE 
      GOTO 99999
   END
   EMP.ID = FIELD(ID,'!',1)
   DED.ID = FIELD(ID,'!',2)
   EMP.NUMBER = EMP.ID[4,7]
   CODE = FIELD(ID,'!',3)
   GOSUB 200
*T21313 GOSUB 300
   MATREAD EICR.REC FROM EMPLOYEE.INVEST.CONTR,ID ELSE MAT EICR.REC = ''
   PREV.EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
   PREV.EMP.NUMBER = EMP.NUMBER
   PREV.SOC.SEC = EMP.SOC.SEC
   PREV.CODE = CODE
   PREV.DIV = EMP.DIV
   PREV.DEPT = EMP.DEPT
   GOSUB 500
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      EMP.ID = FIELD(ID,'!',1)
      DED.ID = FIELD(ID,'!',2)
      EMP.NUMBER = EMP.ID[4,7]
      CODE = FIELD(ID,'!',3)
      BEGIN CASE
         CASE EMP.NUMBER # PREV.EMP.NUMBER
            GOSUB 200
            GOSUB 600
            BEGIN CASE
               CASE EMP.DIV # PREV.DIV
                  GOSUB 1000
                  GOSUB 2000
                  GOSUB 3000
               CASE EMP.DEPT # PREV.DEPT
                  GOSUB 1000
                  GOSUB 2000
               CASE 1
                  GOSUB 1000
            END CASE
*T21313     GOSUB 300
         CASE CODE # PREV.CODE
            GOSUB 600
      END CASE
      MATREAD EICR.REC FROM EMPLOYEE.INVEST.CONTR,ID ELSE MAT EICR.REC = ''
      GOSUB 500
   REPEAT
   GOSUB 600
   GOSUB 1000
   GOSUB 2000
   GOSUB 3000
   PRINT UNDER.LINE
   V.LINE = SPACE(43)
   V.LINE = V.LINE: 'COMPANY   '
   V.LINE = V.LINE: GRAND.BTM "MD2," "R#10":' '
   V.LINE = V.LINE: GRAND.BTU "MD2," "R#10":' '
   V.LINE = V.LINE: GRAND.ATU "MD2," "R#10":' '
   V.LINE = V.LINE: GRAND.ATM "MD2," "R#10":' '
   V.LINE = V.LINE: GRAND.MATCH "MD2," "R#10":' '
   V.LINE = V.LINE: GRAND.TOTAL "MD2," "R#10"
   PRINT V.LINE
   GOTO 99999
*
*---- PERCENTAGES
200 *
   MATREAD EMP.REC FROM EMPLOYEE,EMP.ID ELSE MAT EMP.REC = ''
   MATREAD EICR.REC FROM EMPLOYEE.INVEST.CONTR,EMP.ID ELSE MAT EICR.REC = ''
   SAVE.PCT.CNT = EICR.PCT.CNT
   XXX.ATM = EICR.ATM.PCT
   XXX.BTM = EICR.BTM.PCT
   RETURN
*
*---- PERCENTAGES
300 *
   FOR I = START.MO TO END.MO
      FOR J = 1 TO SAVE.PCT.CNT<1,I>
         TOT.PCT.MATCH<1,I,J> = XXX.ATM<1,I,J> + XXX.BTM<1,I,J>
      NEXT J
   NEXT I
   RETURN
*
*---- TOTAL TYPES
500 *
   FOR I = START.MO TO END.MO
      FOR J = 1 TO SAVE.PCT.CNT<1,I>
         CODE.BTM = CODE.BTM + EICR.BTM<1,I,J>
         CODE.BTU = CODE.BTU + EICR.BTU<1,I,J>
         CODE.ATM = CODE.ATM + EICR.ATM<1,I,J>
         CODE.ATU = CODE.ATU + EICR.ATU<1,I,J>
*T21313 v
         IF EICR.ATM<1,I,J>+0 # 0 THEN
            TOT.MATCH<1,I,J> = TOT.MATCH<1,I,J> + EICR.BTM<1,I,J> + EICR.ATM<1,I,J>
            TOT.PCT.MATCH<1,I,J> = XXX.ATM<1,I,J> + XXX.BTM<1,I,J>
         END ELSE
            TOT.MATCH<1,I,J> = TOT.MATCH<1,I,J> + EICR.BTM<1,I,J>
            TOT.PCT.MATCH<1,I,J> = XXX.BTM<1,I,J>
         END
*T21313 ^
      NEXT J
   NEXT I
   RETURN
*
*---- EMPLOYEE TOTAL
600 *
   FOR I = START.MO TO END.MO
      PCT.CNT = DCOUNT(TOT.PCT.MATCH<1,I>,SVM)
      FOR J = 1 TO PCT.CNT
         LOCATE TOT.PCT.MATCH<1,I,J> IN MTCH.EMP.PCT<1> BY "AR" SETTING FND ELSE NULL
*T21313 v
*           IF FND > MCNT THEN FND = MCNT
         IF FND > MCNT THEN
            EMPR.PCT = MTCH.EMP.PCT<1,MCNT> / TOT.PCT.MATCH<1,I,J>
            TOT.MATCH<1,I,J> = INT((TOT.MATCH<1,I,J> * EMPR.PCT) + .5)
            FND = MCNT
         END
*T21313 ^
         CODE.MATCH = CODE.MATCH + INT((TOT.MATCH<1,I,J>/100)*(MTCH.CO.PCT<1,FND>/100) + .5)
      NEXT J
   NEXT I
   CODE.TOTAL = CODE.MATCH + CODE.BTM + CODE.BTU + CODE.ATM + CODE.ATU
   YTD.TOTAL = YTD.TOTAL + CODE.TOTAL
   YTD.MATCH = YTD.MATCH + CODE.MATCH
   BTM.TOT = BTM.TOT + CODE.BTM
   BTU.TOT = BTU.TOT + CODE.BTU
   ATM.TOT = ATM.TOT + CODE.ATM
   ATU.TOT = ATU.TOT + CODE.ATU
   MAT CODE.TOT = 0
   CODE.TOTAL = 0
   CODE.MATCH = 0
   TOT.MATCH = ""
   PREV.CODE = CODE
   RETURN
*
*---- PRINT DETAIL LINE
1000 *
   V.LINE = ' ':PREV.DIV "R#2":' '
   V.LINE = V.LINE: PREV.DEPT "R#5":' '
   V.LINE = V.LINE: PREV.EMP.NUMBER:' '
   V.LINE = V.LINE: PREV.EMP.NAME "L#25":' '
   V.LINE = V.LINE: PREV.SOC.SEC "L#11":' '
   V.LINE = V.LINE: BTM.TOT "MD2," "R#10":' '
   V.LINE = V.LINE: BTU.TOT "MD2," "R#10":' '
   V.LINE = V.LINE: ATU.TOT "MD2," "R#10":' '
   V.LINE = V.LINE: ATM.TOT "MD2," "R#10":' '
   V.LINE = V.LINE: YTD.MATCH "MD2," "R#10":' '
   V.LINE = V.LINE: YTD.TOTAL "MD2," "R#10":' '
   PRINT V.LINE
   COMP.DEPT.MATCH = COMP.DEPT.MATCH + YTD.MATCH
   COMP.DEPT.TOTAL = COMP.DEPT.TOTAL + YTD.TOTAL
   DEPT.BTM = DEPT.BTM + BTM.TOT
   DEPT.BTU = DEPT.BTU + BTU.TOT
   DEPT.ATM = DEPT.ATM + ATM.TOT
   DEPT.ATU = DEPT.ATU + ATU.TOT
   BTM.TOT = 0; BTU.TOT = 0; ATM.TOT = 0; ATU.TOT = 0
   YTD.MATCH = 0; YTD.TOTAL = 0
   TOT.PCT.MATCH = ""
   PREV.EMP.NAME = EMP.LAST.NAME:', ':EMP.FRST.NAME
   PREV.EMP.NUMBER = EMP.NUMBER
   PREV.SOC.SEC = EMP.SOC.SEC
   RETURN
*
*---- PRINT DEPT TOTALS
2000 *
   MATREAD DEPT.REC FROM DEPARTMENT, CONO:PREV.DEPT ELSE MAT DEPT.REC = ''
   PRINT UNDER.LINE
   V.LINE =  SPACE(21)
   V.LINE = V.LINE: '*DEPT '
   V.LINE = V.LINE: DEPT.DESC "L#25":' '
   V.LINE = V.LINE: DEPT.BTM "MD2," "R#10":' '
   V.LINE = V.LINE: DEPT.BTU "MD2," "R#10":' '
   V.LINE = V.LINE: DEPT.ATU "MD2," "R#10":' '
   V.LINE = V.LINE: DEPT.ATM "MD2," "R#10":' '
   V.LINE = V.LINE: COMP.DEPT.MATCH "MD2," "R#10":' '
   V.LINE = V.LINE: COMP.DEPT.TOTAL "MD2," "R#10":' '
   PRINT V.LINE
   COMP.DIV.MATCH = COMP.DIV.MATCH + COMP.DEPT.MATCH
   COMP.DIV.TOTAL = COMP.DIV.TOTAL + COMP.DEPT.TOTAL
   COMP.DEPT.MATCH = 0
   COMP.DEPT.TOTAL = 0
   PREV.DEPT = EMP.DEPT
   DIV.BTM = DIV.BTM + DEPT.BTM
   DIV.BTU = DIV.BTU + DEPT.BTU
   DIV.ATM = DIV.ATM + DEPT.ATM
   DIV.ATU = DIV.ATU + DEPT.ATU
   MAT DEPT.TOT = 0
   PRINT
   RETURN
*
*---- PRINT DIV TOTALS
3000 *
   MATREAD DIV.REC FROM DIVISION, CONO:PREV.DIV ELSE MAT DIV.REC = ''
   PRINT UNDER.LINE
   V.LINE = SPACE(21)
   V.LINE = V.LINE: '**DIV '
   V.LINE = V.LINE: DIV.DESC "L#25":' '
   V.LINE = V.LINE: DIV.BTM "MD2," "R#10":' '
   V.LINE = V.LINE: DIV.BTU "MD2," "R#10":' '
   V.LINE = V.LINE: DIV.ATU "MD2," "R#10":' '
   V.LINE = V.LINE: DIV.ATM "MD2," "R#10":' '
   V.LINE = V.LINE: COMP.DIV.MATCH "MD2," "R#10":' '
   V.LINE = V.LINE: COMP.DIV.TOTAL "MD2," "R#10":' '
   PRINT V.LINE
   GRAND.MATCH = GRAND.MATCH + COMP.DIV.MATCH
   GRAND.TOTAL = GRAND.TOTAL + COMP.DIV.TOTAL
   COMP.DIV.MATCH = 0
   COMP.DIV.TOTAL = 0
   GRAND.BTM = GRAND.BTM + DIV.BTM
   GRAND.BTU = GRAND.BTU + DIV.BTU
   GRAND.ATM = GRAND.ATM + DIV.ATM
   GRAND.ATU = GRAND.ATU + DIV.ATU
   MAT DIV.TOT = 0
   PREV.DIV = EMP.DIV
   PRINT
   RETURN
*
*---- SYSCOM CALLS
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   PRINTER OFF
END
