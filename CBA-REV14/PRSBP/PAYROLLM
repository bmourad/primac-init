*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SOURCE     - PRSBP
* PROGRAM    - PAYROLLM
* BY         - WALID YAMOUT , C.B.A
* DATE       - 06/11/85
* DESCRIPTION-
* MOD        - RWW 7.31.90, CSF 13520 TASK 15424
*ENDDOC
*********************************************************************
*
*--- INSERT CPYLIB
*
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>FILE.VARS
*COPY>PRS.CPYLIB>PAYREG
*COPY>PRS.CPYLIB>DEDREG
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
      PROMPT""
LOCID=""
SVAL = 0
VAL = 0
      OPEN "","PAYREG" TO PAYREG ELSE PRINT "PAYREG FILE ZAP";STOP
      OPEN "","DEDREG" TO DEDREG ELSE PRINT "CAN'T OPEN DEDREG";STOP
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE PRINT "EMPLOYEE FILE ZAP";STOP
      OPEN "","MISCDED" TO MISCDED ELSE PRINT "CAN'T OPEN MISCDED";STOP
      OPEN "","COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY";STOP
      OPEN "","CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL":;INPUT ANS;STOP
      LBMASK="R############"
      DES = ""
      LINE.SPACE = 1
      BEGIN.PAGE = 10
      PAGE.SIZE = 9
*********************************************************************
1* COMPANY CODE
*********************************************************************
MAT COMP.REC = ""
CONO = ""
CALL GET.CONO(CONO,MAT COMP.REC)
IF CONO = "END" THEN STOP
    IF CO.EMP.XREF = "Y" THEN OPEN "","EMPLOYEE.XREF" TO XREF ELSE PRINT "NO XREF FILE":;INPUT X;STOP
*********************************************************************
5*** INITIAL SCREEN DISPLAY
*********************************************************************
      PRINT @(-1):
      PRINT@(15,0):"* * *  WEEKLY PAYROLL MAINTENANCE  * * *":
      PRINT@(5,2):"COMPANY NUMBER: ":CONO:"    ":CO.NAME:
      PRINT@(5,3):"EMPLOYEE NUMBER:":@(30,3):"NAME:":@(5,4):"CHECK ISSUE NO:":
      PRINT@(3,6):"  DAYS WORKED:":@(31,6):"8.FWHT:":@(50,6):"16.CHECK NO:":
      PRINT@(3,7):"  WEEKS WORKED:":@(30,7):"9.FICA1:":@(50,7):"* * * DEDUCTION * * *":
     PRINT @(30,8):"10.MEDI:"
      PRINT@(30,9):"11.SUTA:":@(50,8):"LN DESCRIPTION AMOUNT":
      PRINT@(5,9):"CALC GROSS:":@(30,10):"12.STATE:":
      PRINT@(50,9):"-- ----------- ------":
      PRINT@(3,10):"  NON TAXED:":@(30,11):"13.CITY:":@(30,12):"14.EIC:":@(3,11):"  TAXED:":
      PRINT@(5,12):"TOT GROSS:":@(30,13):"15.BONDS:":
      PRINT @(5,13):"NET:":
*********************************************************************
200* CALL UP AND MAINTAIN OR ENTER NEW
*********************************************************************
      MAT PAY.REC="";MAT DED.REC="";MAT EMP.REC="";DES=""
      LINES = 0
      OLD.LINES = 0
      D.LN = 1
      TOTDED=0
      PRINT@(10,20):"ENTER EMPLOYEE NUMBER OR END: ####":; IF LOCID # "" THEN PRINT @(40,20):LOCID:
     PRINT @(40,20):;INPUT EMPNO
      PRINT@(5,20):SPACE(71):
       IF LOCID = "" AND EMPNO = "" THEN GOTO 200
       IF LOCID # "" AND EMPNO = "" THEN EMPNO=LOCID;LOCID=""
      IF EMPNO = "END" THEN STOP
      IF LEN(EMPNO)>4 THEN GOTO 200
     IF EMPNO = STR("?",LEN(EMPNO)) THEN
       IF CO.EMP.XREF = "Y" THEN CALL LOCATOR (CONO,XREF,EMPLOYEE,4,5,4,7,LOCID)
       GOTO 5
     END
      ID=CONO:STR("0",4-LEN(EMPNO)):EMPNO
      MATREAD EMP.REC FROM EMPLOYEE,ID ELSE
210      PRINT@(5,20):"EMPLOYEE NOT ON FILE - ENTER 'RTN' TO CONTINUE: ":;INPUT AN
         PRINT@(5,20):SPACE(70):
         IF AN#"" THEN GOTO 210
         GOTO 200
      END
      NAME=EMP.FRST.NAME:" ":EMP.LAST.NAME;PRINT@(22,3):EMPNO"R#4":@(36,3):NAME:
*********************************************************************
240* CHECK ISSUE NO
*********************************************************************
      PRINT@(22,4):"##  ":@(22,4):;INPUT CKISS
      IF NOT(NUM(CKISS)) THEN GOTO 240
      IF CKISS>20 OR CKISS<0 THEN GOTO 240
      PAYID=ID:CKISS
      MATREAD PAY.REC FROM PAYREG,PAYID ELSE
250      PRINT@(10,20):"THIS ENTRY NOT ON FILE ! ! - ENTER 'RTN': ":;INPUT AN
         PRINT@(10,20):SPACE(65):
         IF AN#"" THEN GOTO 250
         GOTO 5
      END
      IF PAY.STATUS # "" THEN GOTO 250
      MATREAD DED.REC FROM DEDREG,PAYID ELSE GOTO 250
      LINES = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
      TEMP = ""
      FOR Y=1TO LINES
         TOTDED=TOTDED+DED.MISC.AMT<1,Y>
         READV TEMP FROM MISCDED,CONO:DED.MISC.CODE<1,Y>,1 ELSE TEMP = "NOT ON FILE"
         DES<Y> = TEMP
      NEXT Y
      NET=PAY.GROSS-TOTDED-DED.FWT-DED.FICA-DED.SUI-DED.STATE-DED.CITY-DED.BONDS-DED.EIC
*********************************************************************
      PRINT@(19,6):PAY.DAYS"R#3":@(38,6):OCONV(DED.FWT,"MD2")"R#8":@(63,6):PAY.CHECK.NO"R#6":
      PRINT@(20,7):OCONV(PAY.WEEKS,"MD3")"R#6":@(38,7):OCONV(DED.LVL1.FICA,"MD2")"R#8":
      PRINT @(38,8):OCONV(DED.LVL2.FICA,"MD2")"R#8":
      PRINT@(40,9):OCONV(DED.SUI,"MD2")"R#6":
      PRINT@(17,9):OCONV(PAY.GROSS-(PAY.TAXED+PAY.NON.TAXED),"MD2")"R#9":@(39,10):OCONV(DED.STATE,"MD2")"R#7":
      PRINT@(17,10):OCONV(PAY.NON.TAXED,"MD2")"R#9":@(39,11):OCONV(DED.CITY,"MD2")"R#7":
      PRINT@(17,11):OCONV(PAY.TAXED,"MD2")"R#9":@(39,12):OCONV(DED.EIC,"MD2")"R#7":
      PRINT@(17,12):OCONV(PAY.GROSS,"MD2")"R#9":@(40,13):OCONV(DED.BONDS,"MD2")"R#6":
      PRINT@(10,13):OCONV(NET,"MD2")"R#8":
      GOSUB 6000
*********************************************************************
280* TEST FOR CORRECT ENTRY
*********************************************************************
      PRINT@(10,20):"IS THIS THE PAYROLL ENTRY YOU WANT ? (Y/N): ":;INPUT AN
      PRINT@(10,20):SPACE(55)
      IF AN="Y" THEN GOTO 300
      IF AN#"N" THEN GOTO 280
      GOTO 5
*********************************************************************
300* TEST FOR DELETE OPTION
*********************************************************************
      PRINT@(10,20):"DO YOU WANT TO DELETE THIS ENTRY ? (Y/RTN): #":CHAR(8):;INPUT AN
      PRINT@(10,20):SPACE(55)
      IF AN='Y***' THEN GOTO 700
      IF AN#"" THEN GOTO 300
*********************************************************************
400* TEST FOR CHANGE OPTION
*********************************************************************
      PRINT@(10,20):"DO YOU WANT TO MAKE ANY CHANGES ? (RTN/N): #":CHAR(8):;INPUT AN
      PRINT@(10,20):SPACE(65):
      IF AN="N" THEN RELEASE PAYREG,PAYID; GOTO 5
      IF AN#"" THEN GOTO 400
*********************************************************************
500* GET LINE NUMBER OF FIELD TO BE CHANGED
*********************************************************************
      PRINT@(10,20):"ENTER LINE NUMBER TO CHANGE ,(D)EDUCTION OR (RTN): ##":STR(CHAR(8),2):;INPUT LN
      CH=""
*********************************************************************
600* TEST FOR END OF CHANGES
*********************************************************************
      IF LN="" THEN
         PRINT@(10,20):SPACE(65):
*********************************************************************
650* RECHECK BEFORE WRITING RECORD
*********************************************************************
         PRINT@(10,20):"IS THIS WHAT YOU WANT ? (RTN/N): #":CHAR(8):;INPUT AN
         PRINT@(10,20):SPACE(50):
         IF AN="" THEN
*CSF13520
    NET = 0
    NET=PAY.GROSS-DED.FWT-DED.FICA-DED.SUI-DED.STATE-DED.CITY-DED.BONDS-DED.EIC
    NET = NET - SUM(DED.MISC.AMT)
    PAY.NET = NET
*
            MATWRITE PAY.REC ON PAYREG,PAYID
            MATWRITE DED.REC ON DEDREG,PAYID
            GOTO 5
         END
         IF AN="N" THEN GOTO 500
         GOTO 650
      END
      IF LN = "D" THEN
        GOSUB 4000
        GOTO 500
      END
      IF NOT(NUM(LN)) THEN GOTO 500
      IF LN < 1 OR LN > 15 THEN GOTO 500
      IF LN >= 1 AND LN <= 7 THEN GOTO 500;*1
      GOTO 1001
*********************************************************************
700* DELETE EMPLOYEE RECORD
*********************************************************************
      PAY.STATUS = "DELETE"
      MATWRITE PAY.REC ON PAYREG,PAYID
      DELETE DEDREG,PAYID
      FOR B1=1 TO 10
         PRINT@(10,20):"ENTRY DELETED FROM FILE ! ! !":CHAR(7)
      NEXT B1
      PRINT@(10,20):SPACE(50)
      GOTO 5
*********************************************************************
1001* INITIALIZE CHANGE ROUTINE
*********************************************************************
* DAYS WORKED - 9
      IF LN=1 THEN
         FILE="PAY"
         NBLOC="###  ";CLM=19;LIN=6;MAX=3;NMAX=999;NMIN=(-999);DC=0;ATTR=9;GOSUB 8600
      END
* WEEKS WORKED - 10
      IF LN=2 THEN
         FILE="PAY"
         NBLOC="##  ";CLM=20;LIN=7;MAX=2;NMAX=99;NMIN=(-99);DC=0;ATTR=10;GOSUB 8600
      END
* FWHT - 4
      IF LN=8 THEN
         PAY.NET=PAY.NET+DED.FWT
         FILE="DED";NBLOC="#####.##  ";MAX=8;NMAX=9999999;NMIN=(-9999999);DC=2;CLM=38
         LIN=6;ATTR=4;GOSUB 8600
         PAY.NET=PAY.NET-DED.FWT
      END
* FICA - 5
      IF LN=9 THEN
         PAY.NET=PAY.NET+DED.LVL1.FICA
         FILE="DED";NBLOC="#####.##  ";MAX=8;NMAX=9999999;NMIN=(-9999999);DC=2;CLM=39
         LIN=7;ATTR=18
         GOSUB 8600
         PAY.NET=PAY.NET-DED.LVL1.FICA
*        DED.FICA = DED.LVL1.FICA + DED.LVL2.FICA
*        GOSUB 8600
      END
      IF LN=10 THEN
         PAY.NET=PAY.NET+DED.LVL2.FICA
         FILE="DED";NBLOC="#####.##  ";MAX=8;NMAX=9999999;NMIN=(-9999999);DC=2;CLM=39
         LIN=8;ATTR=19
         GOSUB 8600
         PAY.NET=PAY.NET-DED.LVL2.FICA
*        DED.FICA = DED.LVL1.FICA + DED.LVL2.FICA
*        GOSUB 8600
      END
* SUID - 6
      IF LN=11 THEN
         PAY.NET=PAY.NET+DED.SUI
         FILE="DED";NBLOC="###.##  ";MAX=6;NMAX=99999;NMIN=(-99999);DC=2;CLM=40
         LIN=9;ATTR=6;GOSUB 8600
         PAY.NET=PAY.NET-DED.SUI
      END
* STATE - 7
      IF LN=12 THEN
         PAY.NET=PAY.NET+DED.STATE
         FILE="DED";NBLOC="####.##  ";MAX=7;NMAX=999999;NMIN=(-999999);DC=2;CLM=39
         LIN=10;ATTR=7;GOSUB 8600
         PAY.NET=PAY.NET-DED.STATE
      END
* CITY - 8
      IF LN=13 THEN
         PAY.NET=PAY.NET+DED.CITY
         FILE="DED";NBLOC="####.##  ";MAX=7;NMAX=999999;NMIN=(-999999);DC=2;CLM=39
         LIN=11;ATTR=8;GOSUB 8600
         PAY.NET=PAY.NET-DED.CITY
      END
* EARNED INCOME CREDIT = 10
      IF LN=14 THEN
         PAY.NET=PAY.NET+DED.EIC
         FILE="DED";NBLOC="####.##  ";MAX=7;NMAX=999999;NMIN=(-999999);DC=2;CLM=39
         LIN=12;ATTR=(10);GOSUB 8600
         PAY.NET=PAY.NET-DED.EIC
      END
* BONDS - 9
      IF LN=15 THEN
         PAY.NET=PAY.NET+DED.BONDS
         FILE="DED";NBLOC="###.##  ";MAX=6;NMAX=99999;NMIN=(-99999);DC=2;CLM=40
         LIN=13;ATTR=9;GOSUB 8600
         PAY.NET=PAY.NET-DED.BONDS
      END
* CHECK NO
      IF LN=16 THEN
         FILE="PAY";NBLOC="######  ";MAX=6;NMAX=999999;NMIN=1;DC=0;CLM=63
         LIN=6;ATTR=11;GOSUB 8600
      END
      IF CH THEN GOTO 650 ELSE GOTO 500
*
*--- DEDUCTION REQUEST
**
4000*
  PRINT @(10,20):CL:"ENTER (C)HANGE , (S)CROLL , (RTN) : ":;INPUT VALUE
  IF VALUE = "" THEN GOTO 4999
  BEGIN CASE
  CASE VALUE = "S"
    D.LN = D.LN + PAGE.SIZE
    IF D.LN > LINES THEN D.LN = 1
    GOSUB 6000
  CASE VALUE = "C"
    GOSUB 6000
4500*
    PRINT @(10,20):CL:"ENTER LINE NUMBER : ":;INPUT DVALUE
    IF DVALUE = "END" THEN GOTO 4000
    IF NOT(NUM(DVALUE)) THEN GOTO 4500
    IF DVALUE < START.LINE OR DVALUE > LAST.LINE THEN GOTO 4500
    IF DVALUE THEN
       D.LN = DVALUE
       PAY.NET = PAY.NET + DED.MISC.AMT<1,D.LN>
       FILE="DED";NBLOC="###.##  ";MAX=6;NMAX=99999;NMIN=(-99999);DC=2
       LIN = BEGIN.PAGE + LINE.SPACE * MOD(D.LN-1,PAGE.SIZE)
       CLM=65;ATTR=14;VAL=D.LN;GOSUB 8600
       PAY.NET = PAY.NET - DED.MISC.AMT<1,D.LN>
    END
  END CASE
  GOTO 4000
4999*
RETURN
*
*--- SCROLL
*
6000*
  START.LINE = 1 + INT((D.LN-1)/PAGE.SIZE)*PAGE.SIZE
  IF START.LINE = OLD.LINES THEN GOTO 6999
  OLD.LINES = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  CNT = 1
  FOR N = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(50,SLN) : CL : N "R#2":
    PRINT @(53,SLN) : DES<N> "L#11":
    PRINT @(65,SLN) : OCONV(DED.MISC.AMT<1,N>, "MD2") "R#6":
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(50,SLN) : CL :
  NEXT N
6999*
RETURN
*********************************************************************
8600* NUMERIC INPUT SUB-ROUTINE *
*********************************************************************
8601  PRINT@(CLM,LIN):NBLOC:@(CLM,LIN):;INPUT ENTRY
      IF ENTRY # "" THEN
         IF ENTRY=' ' THEN ENTRY=0
         IF NUM(ENTRY) THEN NULL ELSE GOTO 8601
         IF DC THEN ENTRY=ICONV(ENTRY,"MD":DC)
         IF ENTRY >= NMIN AND ENTRY <= NMAX THEN NULL ELSE GOTO 8601
*********************************************************************
* STORE ENTRY IN RECORD IF NEW OR CHANGED
*********************************************************************
         IF ENTRY=0 THEN ENTRY=''
         IF FILE="DED" THEN DED.REC(ATTR)=REPLACE(DED.REC(ATTR),1,VAL,SVAL,ENTRY)
         IF FILE="PAY" THEN PAY.REC(ATTR)=REPLACE(PAY.REC(ATTR),1,VAL,SVAL,ENTRY)
      END
      PRINT@(CLM,LIN):SPACE(MAX+1)
      IF FILE="DED" THEN ENTRY=EXTRACT(DED.REC(ATTR),1,VAL,SVAL)
      IF FILE="PAY" THEN ENTRY=EXTRACT(PAY.REC(ATTR),1,VAL,SVAL)
      PRINT@(CLM,LIN):OCONV(ENTRY,"MD":DC) LBMASK[1,MAX+1]
      TOTDED=0
      FOR Y=1 TO LINES
         TOTDED=TOTDED+EXTRACT(DED.MISC.AMT,1,Y,0)
      NEXT Y
      DED.FICA = DED.LVL1.FICA + DED.LVL2.FICA
      NET=PAY.GROSS-TOTDED-DED.FWT-DED.FICA-DED.SUI-DED.STATE-DED.CITY-DED.BONDS-DED.EIC
      PRINT @(10,13):OCONV(NET,"MD2")"R#8"
      VAL=0;SVAL=0;DC=0
      RETURN
   END
