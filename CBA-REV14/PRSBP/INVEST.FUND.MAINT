*COPY>CPYLIB>COM1
********************************************************************
* REVISION       - [08.1]
* Copyright 1988 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE         - PRSBP
* PROGRAM        - INVEST.FUND.MAINT
* BY             - MARK TRAYWICK
* DATE           - 5/16/88
* DESCRIPTION    _ This program maintains the INVEST.FUND.CODE file.
*********************************************************************
*
*---- FILE EQU
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PRS.CPYLIB>INVEST.FUND.CODE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SYSCOM CALL
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN 'PRS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'SCREENS FILE MISSING'; GOTO 93000
      OPEN 'COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE IS MISSING';GOTO 93000
      OPEN 'CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE IS MISSING';GOTO 93000
      OPEN 'INVEST.FUND.CODE' TO INVEST.FUND.CODE ELSE ERRMSG = 'INVEST.FUND.CODE FILE MISSING'; GOTO 93000
*
*---- GET COMPANY NUMBER
*
      CONO='';CALL GET.CONO(CONO,MAT COMP.REC)
      IF CONO='END' THEN GOTO 99999
*
*---- INITIALIZATION
*
      MAT EDIT.COM.DRIVER = ''
      ECD.SCRN.NAME = 'INVEST.FUND.MAINT'; ECD.SCRN.NO = 1
      ECD.ACTION = 1; CALL SCRN.EDIT
      ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
10 *
      MAT SCV.REC = ''
      MAT IFCR.REC = ''
      ECD.NUM = 1
      ECD.ACTION = 4; CALL SCRN.EDIT
      KEY = ECD.RET.VALUE
      BEGIN CASE
      CASE ECD.RET.VALUE = 'END' OR ECD.RET.VALUE = 'E'
         GOTO 99999
      CASE ECD.RET.VALUE # ''
         NEW.CODE = 0
         READU IFCR.DESC FROM INVEST.FUND.CODE, CONO:KEY ELSE
            IFCR.DESC=''
            NEW.CODE = 1
         END
      END CASE
*
      IF NEW.CODE THEN 
         ECD.NUM = 2
         ECD.ACTION = 4; CALL SCRN.EDIT
         IF ECD.RET.VALUE # 'END' THEN
            IFCR.DESC = ECD.RET.VALUE
         END ELSE
            RELEASE INVEST.FUND.CODE,CONO:KEY
            ECD.ACTION = 6; CALL SCRN.EDIT
            GOTO 10
         END
      END ELSE
         SCV.REC(2) = IFCR.DESC
         ECD.NUM=2; ECD.ACTION = 5; CALL SCRN.EDIT
      END
*
      MORE = 1
      LOOP
         ECD.NUM = 21
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
         CASE OPTION = 'F'
            WRITE IFCR.DESC ON INVEST.FUND.CODE,CONO:KEY
               MORE = 0
         CASE OPTION = 'C'
            ECD.NUM = 2
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN
               MORE = 0
            END ELSE
               IFCR.DESC = ECD.RET.VALUE
            END
         CASE OPTION = 'D'
            DELETE INVEST.FUND.CODE, CONO:KEY
            MORE = 0
         CASE OPTION = 'E' OR OPTION = 'END'
            MORE = 0
         END CASE
      WHILE MORE DO REPEAT
      ECD.ACTION = 6; CALL SCRN.EDIT
      GOTO 10
*
*---- ERROR MESSAGES
91000 *
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999 *
      PRINT @(-1)
      END
