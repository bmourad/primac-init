*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - EMP.INVEST.LIST
* BY          - WALID YAMOUT , C.B.A
* DATE        - 07/18/88
* DESCRIPTION -
*T20021 RICKB 01/16/96 * ADD SELECTED DIVISION TO REPORT
*T26493 cmykleb 04/30/2002 Change rpt to get the rpt # from the proc.
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>INVEST.FUND.CODE
*COPY>CPYLIB>CHAR
   PROCREAD BUFF ELSE
      PRINT "CANNOT READ INPUT BUFFER"
      INPUT BUFF
      STOP
   END
   CONO = BUFF<1>
   CO.NAME = BUFF<2>
   SELECTED.DIV = BUFF<5> ;* T20021
   DSH = STR("-",45)
   SP = SPACE(132)
   LINE.COUNT = 99
   PAGE.NO = 0
   OPEN "","EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE";STOP
   OPEN "","MISCDED" TO MISCDED ELSE PRINT "CAN'T OPEN MISCDED";STOP
   OPEN "","INVEST.FUND.CODE" TO INVEST.FUND.CODE ELSE PRINT "CAN'T OPEN INVEST.FUND.CODE";STOP
*
*---- INITIALIZE HEADINGS
*
   HD1 = ''
   HD2 = ''
   HD3 = ""
   HD4 = ""
*T26493 v
*  REPORT.NAME = 'EMPLOYEE INVESTMENTS LISTING'
*  REPORT.NUMBER = 'XXXX'
   REPORT.NAME = ""
   CO.NAME = ""
   REPORT.NUMBER = BUFF<2>
*T26493 ^
   REPORT.DATE = DATE()
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   HD3 = "      EMPLOYEE NUMBER & NAME      SOC-SEC-NUM  TERM'D  REHIRED  "
   HD3 = HD3 : "   MISC DEDUCTION   T AMOUNT W  "
   HD3 = HD3 : SP[1,7] : "INVESTMENT FUND" : SP[1,9] : "PCT"
   HD3.1="DIVISION : ":SELECTED.DIV ;*T20021
   HD4 = DSH[1,33]:" ":DSH[1,11]:" ":DSH[1,8]:" ":DSH[1,8]:"  "
   HD4 = HD4 : DSH[1,18] :" - ------ -  "
   HD4 = HD4 : DSH[1,30]:" ": DSH[1,3]
   PRINTER ON
*****
300*
   READNEXT ID ELSE GOTO 999999
   IF ID[1,3] # CONO THEN GOTO 300
   MAT EMP.REC = ""
   MATREAD EMP.REC FROM EMPLOYEE,ID ELSE GOTO 300
   EMP.NO = ID[4,4]
   MCNT = DCOUNT(EMP.MSC.CODE,VM)
   BLK = 0
   FOR Y = 1 TO MCNT UNTIL BLK
      ICNT = DCOUNT(EMP.INVEST<1,Y>,SVM)
      FOR YY = 1 TO ICNT UNTIL BLK
         IF EMP.INVEST<1,Y,YY> # "" THEN
            BLK = 1
         END
      NEXT YY
   NEXT Y
   IF BLK = 0 THEN GOTO 300
   IF LINE.COUNT > 55 THEN GOSUB 8000
   NAME=EMP.FRST.NAME:" ":EMP.LAST.NAME
   VLINE = EMP.NO"R#4 ":NAME"L#28 "
   VLINE = VLINE : EMP.SOC.SEC "L#11 "
   VLINE = VLINE : OCONV(EMP.TERM.DATE, "D2/") "R#8 "
   VLINE = VLINE : OCONV(EMP.REHR.DATE, "D2/") "R#8  "
   FOR Y = 1 TO MCNT
      IF EMP.INVEST<1,Y> = "" THEN GOTO 359
      MATREAD MDED.REC FROM MISCDED, CONO : EMP.MSC.CODE<1,Y> ELSE
         MAT MDED.REC = ""
         MDED.SCRN.DESC = "??????"
      END
      ICNT = DCOUNT(EMP.INVEST<1,Y>,SVM)
      FOR YY = 1 TO ICNT
         IF YY = 1 THEN
            VLINE = VLINE : EMP.MSC.CODE<1,Y> "L#2 "
            VLINE = VLINE : MDED.SCRN.DESC "L#15 "
            VLINE = VLINE : MDED.TYPE "L#1 "
            VLINE = VLINE : OCONV(EMP.MSC.AMT<1,Y>, "MD2") "R#6 "
            VLINE = VLINE : EMP.MSC.WK<1,Y> "R#1  "
         END ELSE
            VLINE = VLINE : SP[1,31]
         END
         MATREAD IFCR.REC FROM INVEST.FUND.CODE, CONO : EMP.INVEST<1,Y,YY> ELSE
            MAT IFCR.REC = ""
            IFCR.DESC = "??????"
         END
         VLINE = VLINE : EMP.INVEST<1,Y,YY> "L#4 "
         VLINE = VLINE : IFCR.DESC "L#25 "
         VLINE = VLINE : EMP.INVEST.PCT<1,Y,YY> "R#3"
         IF LINE.COUNT > 55 THEN GOSUB 8000
         PRINT VLINE
         LINE.COUNT = LINE.COUNT + 1
         VLINE = SP[1,65]
      NEXT YY
359*
   NEXT Y
   IF VLINE # "" AND VLINE # SP[1,65] THEN PRINT
   PRINT
   LINE.COUNT = LINE.COUNT + 2
   GOTO 300
*-------------------*
*--- SUBROUTINES ---*
*-------------------*
*
*--- HEADING ROUTINE
*
8000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1 : PAGE.NO "R#3"
   PRINT HD2
   PRINT
   PRINT HD3
   PRINT HD3.1 ;*T20021
   PRINT HD4
   LINE.COUNT = 6 ;*T20021 change from 5 to 6
   RETURN
*
*--- END OF REPORT
*
999999*
   PRINTER OFF
END
