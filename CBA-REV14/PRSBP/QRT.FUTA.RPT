*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRSBP
* PROGRAM     - FUTA.RPT
* BY          - WALID YAMOUT
* DATE        - 05/25/84
* MODIFIED BY - Paul Koldjeski
* DATE        - 03/04/88
* DESCRIPTION - Prints the Federal Unemployment Insurance Tax report
* ENDDOC
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>CPYLIB>CHAR
*COPY>PRS.CPYLIB>MISCDED
*
****** READ PROC
*
       PROCREAD INBUFF ELSE PRINT "CANT READ INPUT BUFFER":;INPUT GO$;STOP
       CONO=INBUFF<1>
       QTR=INBUFF<4>
       BACKUP=INBUFF<5>
*
***** OPEN FILES
*
 IF BACKUP="Y" THEN
*
     OPEN "S.COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN S.COMPANY":;INPUT GO$;STOP
     OPEN "S.EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN S.EMPLOYEE":;INPUT GO$;STOP
     OPEN "S.QTD-YTD" TO QTD.YTD ELSE PRINT "CAN'T OPEN S.QTD-YTD":;INPUT GO$;STOP
*
  END ELSE
*
     OPEN "COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY":;INPUT  GO$;STOP
     OPEN "EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE":;INPUT GO$;STOP
     OPEN "QTD-YTD" TO QTD.YTD ELSE PRINT "CAN'T OPEN QTD-YTD":;INPUT  GO$;STOP
*
 END
*
     OPEN "MISCDED" TO MISCDED ELSE PRINT "CAN'T OPEN MISCDED":;INPUT GO$;STOP
*
***** DETERMINE QUARTER
*
       RPT.QTR=''
       ANNUAL.FLAG="N"
       IF QTR=1 THEN RPT.QTR="1ST QUARTER "
       IF QTR=2 THEN RPT.QTR="2ND QUARTER "
       IF QTR=3 THEN RPT.QTR="3RD QUARTER "
       IF QTR=4 THEN RPT.QTR="4TH QUARTER "
       IF QTR="Y" THEN RPT.QTR="ANNUALLY    "; QTR=4; ANNUAL.FLAG="Y"
*
     MATREAD COMP.REC FROM COMPANY, CONO ELSE PRINT "CAN'T READ COMPANY":;INPUT GO$;STOP
     SP2 = SPACE(2)
     SP20 = SPACE(20)
     SP3 = SPACE(3)
     SP5 = SPACE(5)
*
*--- HEADING
*
     POS1 = 51 - INT(LEN(CO.NAME)/2)
     POS2 = 26 + INT(LEN(CO.NAME)/2)
     HEAD1 = "DATE - " : OCONV(DATE(), "D2/") "L#8"
     HEAD1 = HEAD1 : SPACE(POS1) : CO.NAME "L#30"
     HEAD1 = HEAD1 : SPACE(POS2) : "PAGE : 'P'":"'L'"
     HEAD2 = SPACE(42): "FEDERAL UNEMPLOYMENT TAX (FUTA) REPORT ":RPT.QTR:"'LL'"
     HEAD3 = SPACE(47): "EXEMPTION LEVEL = " : OCONV(CO.MAX.FUI.TAX, "MD2,$"):"'L'"
     HEAD4 = SPACE(47): "FUTA PERCENT = " : OCONV(CO.FUI.PCNT, "MD3") :"'LL'"
     HEAD5 = "SOCIAL SECURITY" :SP3: "EMPLOYEE NAME" :SP20: "GROSS WAGES " :SP3: "EXEMPT WAGES'L'"
     HEAD6 = "---------------" :SP3: "------------------------------" :SP3: "------------" :SP3: "------------'L'"
     PRINTER ON
     HEADING HEAD1 : HEAD2 : HEAD3 : HEAD4 : HEAD5 : HEAD6
*
*---- INITIALIZE
*
   TOT.GROSS = 0
   TOT.EXEMPT = 0
   PAG.GROSS = 0
   PAG.EXEMPT = 0
   PAGE.NO = 0
   LINE.COUNT = 0
*
100** READ EMPLOYEE RECORD
*
   READNEXT ID ELSE GOTO 200
   MAT EMP.REC = ""
   MAT QTD.REC = ""
   MATREAD EMP.REC FROM EMPLOYEE,ID ELSE GOTO 100
   ALT.GROSS=0;ALT.NTSICK=0
*
***** CHECK FOR ALTERNATIVE EMPLOYEE NO.
*
   IF EMP.ALT.NO # "" THEN
      MATREAD QTD.REC FROM QTD.YTD,ID[1,3]:EMP.ALT.NO ELSE MAT QTD.REC=""
*
      FOR I = 1 TO QTR
         ALT.GROSS=ALT.GROSS + QTD.GROSS<1,I>
         ALT.NTSICK=ALT.NTSICK+QTD.SK.NO.FI<1,I>
      NEXT I
      GOSUB 5000;ALT.GROSS=ALT.GROSS-NON.TAX.DED
   END
*
***** READ EMPLOYEE NO.
*
   MATREAD QTD.REC FROM QTD.YTD,ID ELSE GOTO 100
   IF ANNUAL.FLAG="Y" THEN GOTO 50
   IF QTD.GROSS<1,QTR> + 0 = 0 THEN GOTO 100
*
50*
*
   GROSS.YTD=0;NTSICK.YTD=0
*
   FOR Y = 1 TO QTR
      GROSS.YTD=GROSS.YTD+QTD.GROSS<1,Y>
      NTSICK.YTD=NTSICK.YTD+QTD.SK.NO.FI<1,Y>
   NEXT Y
*
   IF ANNUAL.FLAG="Y" AND GROSS.YTD=0 THEN GOTO 100
*
   GOSUB 5000;GROSS.YTD=GROSS.YTD-NON.TAX.DED
*
   PAID=QTD.GROSS<1,QTR>-QTD.SK.NO.FI<1,QTR>
   THIS.FUI=GROSS.YTD-NTSICK.YTD;THAT.FUI=ALT.GROSS-ALT.NTSICK
   PRE.FUI=THIS.FUI-PAID
*
   BEGIN CASE
     CASE (THIS.FUI+THAT.FUI) > CO.MAX.FUI.TAX
        THIS.FUI=(CO.MAX.FUI.TAX-THAT.FUI)-PRE.FUI
     CASE THIS.FUI > CO.MAX.FUI.TAX
        THIS.FUI=CO.MAX.FUI.TAX-PRE.FUI
     CASE THAT.FUI > CO.MAX.FUI.TAX
        THIS.FUI=0
     CASE 1
        THIS.FUI=PAID
   END CASE
*
   IF THIS.FUI<0 THEN THIS.FUI=0
   FUI=THIS.FUI
   EXEMPT=0
*
   IF ANNUAL.FLAG="Y" THEN
      EXEMPT=GROSS.YTD-CO.MAX.FUI.TAX
      IF EXEMPT<0 THEN EXEMPT=0
   END ELSE
      EXEMPT=0
      IF PAID> FUI THEN EXEMPT=PAID-FUI
   END
*
***** PRINT DETAIL LINE
*
   NAME = EMP.LAST.NAME : " " : EMP.FRST.NAME
   V.LINE = SP2 : EMP.SOC.SEC "L#11" :SP5: NAME "L#30" :SP3
*
   IF ANNUAL.FLAG="Y" THEN QTD.GROSS<1,QTR>=GROSS.YTD
*
   V.LINE = V.LINE : OCONV(QTD.GROSS<1,QTR>, "MD2Z,") "R#12" :SP3
   V.LINE = V.LINE : OCONV(EXEMPT, "MD2Z,") "R#12"
   PRINT V.LINE
   TOT.GROSS = TOT.GROSS + (QTD.GROSS<1,QTR>-QTD.SK.NO.FI<1,QTR>)
   TOT.EXEMPT = TOT.EXEMPT + EXEMPT
   PAG.GROSS = PAG.GROSS + (QTD.GROSS<1,QTR>-QTD.SK.NO.FI<1,QTR>)
   PAG.EXEMPT = PAG.EXEMPT + EXEMPT
   GOTO 100
*
200*** PRINT PAGE TOTALS
*
   PRINT SPACE(51):"------------":SP3:"------------"
   V.LINE = SPACE(37):"PAGE TOTALS - "
   V.LINE = V.LINE : OCONV(PAG.GROSS, "MD2,") "R#12":SP3
   V.LINE = V.LINE : OCONV(PAG.EXEMPT, "MD2,") "R#12"
   PRINT V.LINE
   PRINT
   PRINT SPACE(51) : "------------" :SP3: "------------"
   V.LINE = SPACE(51) : OCONV(TOT.GROSS, "MD2,") "R#12" :SP3
   V.LINE = V.LINE : OCONV(TOT.EXEMPT, "MD2,") "R#12"
   PRINT V.LINE
   PRINT
   GROSS = TOT.GROSS - TOT.EXEMPT
   PRINT SPACE(29): "TOTAL TAXABLE WAGES = " : OCONV(GROSS, "MD2,")
   EXEMPT = INT((GROSS / 100) * (CO.FUI.PCNT / 1000) + .5)
   PRINT SP20: "TAX DUE BEFORE STATE CREDITS = " : OCONV(EXEMPT, "MD2,")
   PRINTER OFF
   GOTO 999999
*
*---- NON TAXABLE DEDUCTIONS
*
5000*
   NON.TAX.DED=0
   NTD.CNT=DCOUNT(QTD.DED,VM)
   FOR REF=1 TO NTD.CNT
      MATREAD MDED.REC FROM MISCDED,CONO:QTD.DED<1,REF> ELSE MAT MDED.REC=""
      IF MDED.TAXABLE="B" THEN
         FOR X =1 TO QTR
            NON.TAX.DED=NON.TAX.DED+QTD.AMNT<1,REF,X>
         NEXT X
      END
   NEXT REF
   RETURN
*
999999*
   END
