**************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SOURCE     - PRSBP
* PROGRAM    - BATCH.PRINT
* BY         - WALID YAMOUT , C.B.A
* DATE       - 06/11/85
* DESCRIPTION-
* LAST COMPILE - 163
*ENDDOC
**************************************************************************
*
*--- INSERT FILES EQUETES
*
*COPY>PRS.CPYLIB>PAYROLL.HOURS
*COPY>PRS.CPYLIB>BATCH
*COPY>PRS.CPYLIB>PAYREG
*COPY>CPYLIB>CHAR
********
     OPEN "","BATCH" TO BATCH ELSE PRINT "BATCH FILE ZAP";STOP
     OPEN "","PAYREG" TO PAYREG ELSE PRINT "CAN'T OPEN PAYREG";STOP
     OPEN "","CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL";STOP
     PROCREAD X ELSE PRINT "MUST COME FROM PROC  -  HIT RETURN":;INPUT X;STOP
    CONO=X<1>
    CO.NAME = X<2>
    MATREAD HRS.REC FROM CONTROL, CONO:"PAYROLL.HOURS" ELSE PRINT "PAYROLL.HOURS NOT ON CONTROL"; STOP
    HRS.CNT = COUNT(HRS.DESC,VM) + (HRS.DESC # "")
    IF HRS.CNT < 1 OR HRS.CNT > 11 THEN
      PRINT "ERROR IN PAYROLL.HOURS ON CONTROL"
      STOP
    END
     HOLD = ""
     DIM T(15)
*****
** HEADING ROUTINE
     SP2 = SPACE(2)
     SP1 = SPACE(1)
     D5 = STR("-",5)
     D8 = STR("-",8)
     POS1 = 51 - INT(LEN(CO.NAME)/2)
     POS2 = 26 + INT(LEN(CO.NAME)/2)
     HEAD1 = "DATE - " : OCONV(DATE(), "D2/") "L#8"
     HEAD1 = HEAD1 : SPACE(POS1) : CO.NAME "L#30"
     HEAD1 = HEAD1 : SPACE(POS2) : "PAGE : 'P'":"'L'"
     HEAD2 = SPACE(45):"W E E K L Y   B A T C H   A N A L Y S I S":"'LL'"
     HEAD3 = "BATCH":SPACE(13)
     HEAD4 = D5:SPACE(13)
     FOR H = 1 TO HRS.CNT
       NAME = STR(" ",INT((8-LEN(HRS.SHORT<1,H>))/2)):HRS.SHORT<1,H>
       HEAD3 = HEAD3 :SP2: NAME "L#8"
       HEAD4 = HEAD4 :SP2: D8
     NEXT H
     HEAD3 = HEAD3 :"'L'"
     HEAD4 = HEAD4 :"'L'"
   PRINTER ON
   HEADING HEAD1:HEAD2:HEAD3:HEAD4
*****
    EOJ = ""
300  READNEXT ID ELSE
       EOJ = "YES"
       IF HOLD = "" THEN GOTO 2020 ELSE GOTO 2000
     END
     MAT PAY.REC =""
     MATREAD PAY.REC FROM PAYREG,ID ELSE GOTO 300
     IF CONO#ID[1,3] THEN GOTO 300
     IF HOLD = "" THEN GOTO 2020
     IF HOLD#PAY.BATCH.NO THEN GOTO 2000
500*
    FOR Y = 1 TO PAY.RECORDS
     FOR H = 1 TO HRS.CNT
       T(HRS.SMV<1,H>) = T(HRS.SMV<1,H>) + PAY.HOURS<1,Y,HRS.SMV<1,H>>
     NEXT H
    NEXT Y
     GOTO 300
*****
** BATCH NUMBER BREAK
2000*
     MATREAD BAT.REC FROM BATCH,CONO:STR("0",3-LEN(HOLD)):HOLD ELSE
      PRINT "   BATCH NUMBER ":HOLD:" NOT ENTERED *******";PRINT
      MAT BAT.REC = ""
      GOTO 2020
     END
     BLK = 0
     FOR H = 1 TO HRS.CNT UNTIL BLK
       IF T(HRS.SMV<1,H>) # BAT.REC(HRS.SMV<1,H>+2) THEN BLK = 1
     NEXT H
     IF BLK = 0 THEN GOTO 2020
     V1.LINE = HOLD "R#3" :SP2:SP1: "ENTERED" :SP2:SP2:"-"
     V2.LINE = SP2:SP2:SP2: "CALCULATED" :SP1:"-"
     V3.LINE = SP2:SP2:SP2: "DIFFERENCE" :SP1:"-"
     FOR H = 1 TO HRS.CNT
       BATCH.HRS = BAT.REC(HRS.SMV<1,H>+2) + 0
       CAL.HRS = T(HRS.SMV<1,H>)
       V1.LINE = V1.LINE :SP2: OCONV(BATCH.HRS, "MD2") "R#8"
       V2.LINE = V2.LINE :SP2: OCONV(CAL.HRS, "MD2") "R#8"
       V3.LINE = V3.LINE :SP2: OCONV((BATCH.HRS - CAL.HRS), "MD2") "R#8"
     NEXT H
     PRINT V1.LINE
     PRINT V2.LINE
     PRINT V3.LINE
     PRINT
2020 IF EOJ = "YES" THEN
       PRINTER OFF
       STOP
     END ELSE
       MAT T = 0
       HOLD = PAY.BATCH.NO
       GOTO 500
     END
END
