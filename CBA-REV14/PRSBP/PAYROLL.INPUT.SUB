  SUBROUTINE PAYROLL.INPUT.SUB
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.EMPLOYEE
*COPY>CPYLIB>COM.GEN.XREF
*COPY>PRS.CPYLIB>COM.PAYROLL.INPUT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - PAYROLL.INPUT.SUB
* BY          - WALID YAMOUT, CBA
* DATE        - 06/14/85
* DESCRIPTION -
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>CHAR
  ERRMSG = ""
  LINE.DESC = ""
  TYP = 0; CALL EDIT.SUB; FILL = "#"
*
*--- INITIALIZATION
*
  ECD.SCRN.NO = 2
  FOR SCV = 1 TO SCV.REC.SIZE
    SCV.REC(SCV)<ECD.SCRN.NO> = ""
  NEXT SCV
  ECD.ACTION = 2; CALL SCRN.EDIT
  LINE.SPACE = 1
  PAGE.SIZE = 14
  BEGIN.PAGE = 5
  OLD.START = 0
  LN = 1
  LINES = 0
  IF VOID.EMP = "" THEN
    OPTION = "A"
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 5000
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END ELSE
    LINES = COUNT(VOID.EMP,AM) + (VOID.EMP # "")
  END
  GOSUB 10000
*
*--- ENTER REQUEST
*
500*
  ECD.NUM = 1; SCV.REC(1)<ECD.SCRN.NO> = ""
  ECD.ACTION = 4; CALL SCRN.EDIT
  REQUEST = ECD.RET.VALUE
  BEGIN CASE
    CASE REQUEST = "E" OR REQUEST = "END"
      GOTO 999999
    CASE REQUEST = "A"
      LOOP
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 5000
      WHILE LINES > OLD.LINES DO REPEAT
      LN = LINES
    CASE REQUEST = "C"
      GOSUB 30000
      IF LNO THEN
        LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
        GOSUB 5010
      END
    CASE REQUEST = "D"
      GOSUB 30000
      IF LNO THEN
        LN = LNO
        VOID.EMP = DELETE(VOID.EMP,LN,0,0)
        LINE.DESC = DELETE(LINE.DESC,LN,0,0)
        LINES = COUNT(VOID.EMP,AM) + (VOID.EMP # "")
        IF LN > LINES THEN LN = LINES
        IF LN < 1 THEN LN = 1
        OLD.START = 0
      END
    CASE REQUEST = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
    * T25978 v
    CASE REQUEST = 'SR'
      LN -= PAGE.SIZE
      IF LN < 1 THEN LN = 1
    CASE REQUEST = 'ST'
      LN = 1
    CASE REQUEST = 'SB'
      LN = LINES
    * T25978 ^
  END CASE
  GOSUB 10000
  GOTO 500
*-----------------*
*   SUBROUTINES   *
*-----------------*
*
*--- VOIDED EMPLOYEES
*
5000*
  GOSUB 10000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  PRINT @(0,SLN) : CL :
  PRINT @(0,SLN) : LN "R#2" :
  LINES = LINES + 1
5010*
  X = 4; Y = SLN; TYP = 3; MAXL = 4; O.R = "O"
  IF CO.EMP.XREF = "Y" THEN
    HMSG = "Enter Employee Number or <RETURN> for XREF"
  END ELSE
    HMSG = "Enter Employee Number"
  END
  DEFAULT = VOID.EMP<LN>
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END" AND OPTION = "A"
      VOID.EMP = DELETE(VOID.EMP,LN,0,0)
      LINE.DESC = DELETE(LINE.DESC,LN,0,0)
      GOTO 5999
    CASE VALUE = "END"
      OLD.START = 0
      GOTO 5999
    CASE VALUE = ""
      IF CO.EMP.XREF # "Y" THEN GOTO 5010
      X = 10; Y = SLN; TYP = 1; MAXL = 41; O.R = "O"
      CALL EDIT.SUB
      IF VALUE = "" OR VALUE = "END" THEN GOTO 5010
      MAT GEN.XREF.REC = ""
      GXR.CO = CONO
      GXR.LOC = 1
      GXR.ID = ""
      GXR.SRCH.ID = VALUE
      GXR.XREF = EMPLOYEE.XREF
      GXR.FILE = EMPLOYEE
      GXR.TOP.LINE<1,1> = "EMPLOYEE XREF SEARCH"
      GXR.TOP.LINE<1,2> = "FOR STRING : ":VALUE
      GXR.HEADING<1,1> = "EMP#"
      GXR.ATT<1,1> = 0
      GXR.LEN<1,1> = 4
      GXR.HEADING<1,2> = "FIRST NAME"
      GXR.ATT<1,2> = 5
      GXR.LEN<1,2> = 20
      GXR.HEADING<1,3> = "LAST NAME"
      GXR.ATT<1,3> = 4
      GXR.LEN<1,3> = 20
      GXR.HEADING<1,4> = "STATUS"
      GXR.ATT<1,4> = 30
      GXR.LEN<1,4> = 6
      GXR.HEADING<1,5> = "CYCLE"
      GXR.ATT<1,5> = 31
      GXR.LEN<1,5> = 5
      CALL GEN.XREF(MAT GEN.XREF.REC,PREFIX)
      ECD.ACTION = 2; CALL SCRN.EDIT
      LINE.DESC<LN> = ""; OLD.START = 0; GOSUB 10000
      IF GXR.ID = "" THEN
        GOTO 5010
      END ELSE
        LOCATE GXR.ID IN VOID.EMP,1 SETTING FND ELSE FND = 0
        IF FND AND FND # LN THEN
          ERRMSG = "EMPLOYEE EXISTS ON LINE ":FND
          GOSUB 91000; GOTO 5010
        END
        MATREAD EMP.REC FROM EMPLOYEE, CONO:GXR.ID ELSE
          MAT EMP.REC = ""
          ERRMSG = "EMPLOYEE ":GXR.ID:" IS NOT ON FILE"
          GOSUB 91000; GOTO 5010
        END
        IF EMP.STATUS # "S" THEN
          ERRMSG = "This is not a SALARY Employee"; GOSUB 91000; GOTO 5010
        END
        IF INPUT.CYCLE # "ALL" AND EMP.PAY.CYCLE # INPUT.CYCLE THEN
          ERRMSG = "Missmatch between the process and employee pay cycle"
          GOSUB 91000; GOTO 5010
        END
        VOID.EMP<LN> = GXR.ID
        IF EMP.REHR.DATE - EMP.TERM.DATE < 0 THEN
          ERRMSG = "WARNING : Employee ":VOID.EMP<LN>:" was terminated on ":OCONV(EMP.TERM.DATE,"D2/"):"."
          GOSUB 91000
        END
      END
    CASE VALUE # ""
      VALUE = STR("0",4-LEN(VALUE)):VALUE
      LOCATE VALUE IN VOID.EMP,1 SETTING FND ELSE FND = 0
      IF FND AND FND # LN THEN
        ERRMSG = "EMPLOYEE EXISTS ON LINE ":FND
        GOSUB 91000; GOTO 5010
      END
      MATREAD EMP.REC FROM EMPLOYEE, CONO:VALUE ELSE
        MAT EMP.REC = ""
        ERRMSG = "EMPLOYEE ":VALUE:" IS NOT ON FILE"
        GOSUB 91000; GOTO 5010
      END
      IF EMP.STATUS # "S" THEN
        ERRMSG = "This is not a SALARY Employee"; GOSUB 91000; GOTO 5010
      END
      IF INPUT.CYCLE # "ALL" AND EMP.PAY.CYCLE # INPUT.CYCLE THEN
        ERRMSG = "Missmatch between the process and employee pay cycle"
        GOSUB 91000; GOTO 5010
      END
      VOID.EMP<LN> = VALUE
      IF EMP.REHR.DATE - EMP.TERM.DATE < 0 THEN
        ERRMSG = "WARNING : Employee ":VOID.EMP<LN>:" was terminated on ":OCONV(EMP.TERM.DATE,"D2/"):"."
        GOSUB 91000
      END
  END CASE
  LINE.DESC<LN> = EMP.FRST.NAME :" ":EMP.LAST.NAME
  PRINT @(4,SLN) : VOID.EMP<LN> "R#4" :
  PRINT @(10,SLN) : LINE.DESC<LN> "L#41" :
5999*
  LINES = COUNT(VOID.EMP,AM) + (VOID.EMP # "")
  RETURN
*
*--- SCROLL
*
10000*
  ST.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF ST.LINE = OLD.START THEN RETURN
  OLD.START = ST.LINE
  LST.LINE = ST.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = ST.LINE TO LST.LINE UNTIL N > LINES
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(0,SSLN) : N "R#2" :
    PRINT @(4,SSLN) : VOID.EMP<N> "L#4" :
    IF LINE.DESC<N> = "" AND VOID.EMP<N> # "" THEN
      MATREAD EMP.REC FROM EMPLOYEE, CONO:VOID.EMP<N> ELSE MAT EMP.REC = ""
      LINE.DESC<N> = EMP.FRST.NAME :" ": EMP.LAST.NAME
    END
    PRINT @(10,SSLN) : LINE.DESC<N> "L#41" :
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(0,SSLN) : CL :
  NEXT N
  RETURN
*
*--- GET LINE
*
30000*
  GOSUB 10000
  IF LST.LINE > LINES THEN ECD.MAXV = LINES ELSE ECD.MAXV = LST.LINE
  ECD.MINV = ST.LINE
  ECD.NUM = 2; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*--- ERROR ROUTINE
*
91000*
  PRINT @(0,23):ERRMSG:CL:
  INPUT REPLY,1 :
  PRINT @(0,23):CL:
  RETURN
999999*
  RETURN
END
