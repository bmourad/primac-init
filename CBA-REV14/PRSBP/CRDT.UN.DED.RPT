**********************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRS
* PROGRAM  - CRDT.UN.DED.RPT
* DATE     - 06/29/84
* DESCRIPTION - THIS REPORT MUST BE RUN BEFORE THE DISTRIBUTION SUMMARY
*               OF THE PAYROLL MONTHLY REPORT MENU.
* LAST COMPILE - 154
************************************************
*ENDDOC
*--- INSERT FILE EQUATE
*COPY>PRS.CPYLIB>MDS
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>EMPLOYEE
*--- GET COMPANY NAME
   PROCREAD ID ELSE PRINT "MUST RUN FROM PROC":;INPUT REPLY;STOP
   CONO = ID<1>
   CO.NAME = ID<2>
*---  OPEN FILE 
   OPEN '','MDS' TO MDS ELSE PRINT "MDS FILE IS MISSING":;INPUT REPLY;STOP
   OPEN '','EMPLOYEE' TO EMPLOYEE ELSE PRINT "EMPLOYEE FILE IS MISSING":;INPUT REPLY;STOP
   OPEN '','CONTROL' TO CONTROL ELSE PRINT "CONTROL FILE IS MISSING":;INPUT REPLY;STOP
   READ CODE FROM CONTROL , CONO:"CREDITUNION" ELSE
      PRINT "CREDIT UNION DEDUCTION CODE IS NOT SET ON CONTROL FILE":
      INPUT REPLY;STOP
   END
*--- INITALIZATION 
   POS1 = 51 - INT(LEN(CO.NAME)/2)
   POS2 = 26 + INT(LEN(CO.NAME)/2)
   UNKNOWN = "??????"
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   D11 = STR('-',11)
   D40 = STR('-',40)
   D10 = STR('-',10)
*--- HEADING 
   HEAD1 = "DATE - ":OCONV(DATE(), 'D2/') "L#8"
   HEAD1 = HEAD1 :SPACE(POS1) :CO.NAME "L#30"
   HEAD1 = HEAD1 :SPACE(POS2): "PAGE - 'PL'"
   HEAD2 = SPACE(51) : "CREDIT UNION DEDUCTIONS REPORT'LL'"
   HEAD3 = "SOC. SEC. #" :SP2: "             EMPLOYEE NAME              "
   HEAD3 = HEAD3 :SP2: "DED AMOUNT'L'"
   HEAD4 = D11:SP2:D40:SP2:D10:"'LL'"
   PRINTER ON
   HEADING HEAD1 : HEAD2 : HEAD3 : HEAD4
*---  READING ALL FILES AND PROCESS ONE AT A TIME 
100* 
   READNEXT ID ELSE GOTO 999999
   IF ID[1,3] # CONO THEN GOTO 100
   EMP.NO = ID[4,4]
*--- PROCESS THE RECORD 
   MAT MDS.REC = ''
   MAT EMP.REC = ""
   EMP.NAME = ""
   MATREAD MDS.REC FROM MDS , ID ELSE GOTO 100
   LOCATE CODE IN MDS.DED.CODE<1>,1 SETTING FND ELSE FND = 0
   IF FND THEN
      MATREAD EMP.REC FROM EMPLOYEE , CONO:EMP.NO ELSE
         EMP.SOC.SEC = MDS.EMP
         EMP.LAST.NAME = UNKNOWN
         EMP.FRST.NAME = UNKNOWN
      END
      EMP.NAME = EMP.LAST.NAME:SP1:EMP.FRST.NAME
*--- PRINT THE VALUES 
      V.LINE = ''
      V.LINE = V.LINE : EMP.SOC.SEC "L#11" :SP2
      V.LINE = V.LINE : EMP.NAME "L#40" :SP2
      V.LINE = V.LINE : OCONV(MDS.DED.AMT<1,FND>, 'MD2') "R#10"
      PRINT V.LINE
   END
   GOTO 100
999999* 
   PRINTER CLOSE
   PRINTER OFF
   END 
