*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRSBP
* PROGRAM     - YTDQTD.PRT
* DATE        - 12/26/84
* BY          - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* MODIFIED    - 3/15/88 BY KERRY WILSON
* DESCRIPTION - This program produces a listing of an employee's
*               hours, earnings, taxes, and deductions by quarter
*               with year-to-date totals. One page per employee is
*               provided for filing purposes.
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PRS.CPYLIB>MISCDED
*COPY>CPYLIB>CHAR
DIM COT(50)
*
*---- INITIALIZATION
*
      FIRST.TIME = 1
      LINE.COUNT = 99
      PAGE.NO = 0
      CONO = ""
      MAT COMP.REC = ""
      MAT COT = ""
      SP2 = SPACE(2)
      D13 = STR('-',13)
      D30 = STR('-',30)
      D15 = STR('-',15)
      D12 = STR('-',12)
      D2 = STR('-',2)
      D4 = STR('-',4)
*
*---- PROCREAD
*
     PROCREAD ID ELSE
        ERRMSG = "MUST BE RUN FROM A PROC"
        STOP
     END
     BACKUP = ID<5>
*
*---- OPEN ALL FILES
*
      IF BACKUP='Y' THEN
         OPEN 'S.COMPANY' TO COMPANY ELSE PRINT 'S.COMPANY FILE IS MISSING';STOP
         OPEN 'S.EMPLOYEE' TO EMPLOYEE ELSE PRINT 'S.EMPLOYEE FILE IS MISSING';STOP
         OPEN 'S.QTD-YTD' TO QTDYTD ELSE PRINT 'S.QTD-YTD FILE IS MISSING';STOP
      END ELSE
         OPEN 'COMPANY' TO COMPANY ELSE PRINT 'COMPANY FILE IS MISSING';STOP
         OPEN 'EMPLOYEE' TO EMPLOYEE ELSE PRINT 'EMPLOYEE FILE IS MISSING';STOP
         OPEN 'QTD-YTD' TO QTDYTD ELSE PRINT 'QTD-YTD FILE IS MISSING';STOP
      END
      OPEN 'MISCDED' TO MISCDED ELSE PRINT 'MISCDED FILE IS MISSING';STOP
      HH.LINE = "EMP#":SP2:"EMPLOYEE NAME" "L#30":SP2:"CD":SP2
      HH.LINE = HH.LINE:"DESCRIPTION" "L#15":SP2:"    QTR-1   ":SP2:"    QTR-2   "
      HH.LINE = HH.LINE:SP2:"    QTR-3   ":SP2:"    QTR-4   ":SP2:"    Y-T-D"
      DH.LINE = D4:SP2:D30:SP2:D2:SP2:D15:SP2:D12:SP2:D12:SP2:D12:SP2:D12:SP2:D13
      PRINTER ON
*
*---- MAIN PROCESSING
*
100*
      FLAG = "Y"
      READNEXT ID ELSE
         IF FIRST.TIME THEN GOTO 999999
         MAT QTD.REC = MAT COT
         EMP.NO = ""
         EMP.NAME = "COMPANY TOTALS"
         GOSUB 200
         GOTO 999999
      END
      FIRST.TIME = 0
      IF CONO # ID[1,3] THEN
         CONO = ID[1,3]
         MATREAD COMP.REC FROM COMPANY,CONO ELSE GOTO 100
         LINE.COUNT = 99
         POS1 = 51 - INT(LEN(CO.NAME)/2)
         POS2 = 26 + INT(LEN(CO.NAME)/2)
      END
      MATREAD QTD.REC FROM QTDYTD,ID ELSE GOTO 100
      MATREAD EMP.REC FROM EMPLOYEE, ID ELSE GOTO 100
      EMP.NO = ID[4,4]
      EMP.NAME = EMP.FRST.NAME:" ":EMP.LAST.NAME
      GOSUB 200
      GOTO 100
*
*---- PROCESS REPORT LINES
*
200*
      DED.CD=""
*
      DESC="HOURS";GOSUB 4000
      DESC="REG";CQPTR=1;CYPTR=0;QT=QTD.REG.HRS;YT="";GOSUB 5000
      DESC="O/T";CQPTR=2;CYPTR=0;QT=QTD.OT.HRS;YT="";GOSUB 5000
      DESC="D/T";CQPTR=3;CYPTR=0;QT=QTD.DT.HRS;YT="";GOSUB 5000
      DESC="SICK";CQPTR=4;CYPTR=0;QT=QTD.SCK.HRS;YT="";GOSUB 5000
      DESC="HOL";CQPTR=5;CYPTR=0;QT=QTD.HOL.HRS;YT="";GOSUB 5000
      DESC="VAC";CQPTR=6;CYPTR=0;QT=QTD.VAC.HRS;YT="";GOSUB 5000
      DESC="MISC";CQPTR=29;CYPTR=0;QT=QTD.MSC.HRS;YT="";GOSUB 5000
      DESC="REG-N/C";CQPTR=31;CYPTR=0;QT=QTD.REGN.HRS;YT="";GOSUB 5000
      DESC="O/T-N/C";CQPTR=33;CYPTR=0;QT=QTD.OTN.HRS;YT="";GOSUB 5000
      DESC="REG-SUP";CQPTR=35;CYPTR=0;QT=QTD.10.HRS;YT="";GOSUB 5000
      DESC="O/T-SUP";CQPTR=37;CYPTR=0;QT=QTD.11.HRS;YT="";GOSUB 5000
*
      DESC="EARNINGS";GOSUB 4000
      DESC="REG";CQPTR=22;CYPTR=0;QT=QTD.REG.ERN;YT="";GOSUB 5000
      DESC="O/T";CQPTR=23;CYPTR=0;QT=QTD.OT.ERN;YT="";GOSUB 5000
      DESC="D/T";CQPTR=24;CYPTR=0;QT=QTD.DT.ERN;YT="";GOSUB 5000
      DESC="SICK (TAX)";CQPTR=11;CYPTR=0;QT=QTD.SCK.PAY;YT="";GOSUB 5000
      DESC="SICK (N/T)";CQPTR=18;CYPTR=0;QT=QTD.SK.NO.FI;YT="";GOSUB 5000
      DESC="HOL";CQPTR=26;CYPTR=0;QT=QTD.HOL.ERN;YT="";GOSUB 5000
      DESC="VAC";CQPTR=27;CYPTR=0;QT=QTD.VAC.ERN;YT="";GOSUB 5000
      DESC="MISC";CQPTR=28;CYTPR=0;QT=QTD.MSC.ERN;YT="";GOSUB 5000
      DESC="REG-N/C";CQPTR=30;CYPTR=0;QT=QTD.REGN.ERN;YT="";GOSUB 5000
      DESC="O/T-N/C";CQPTR=32;CYPTR=0;QT=QTD.OTN.ERN;YT="";GOSUB 5000
      DESC="REG-SUP";CQPTR=34;CYPTR=0;QT=QTD.10.ERN;YT="";GOSUB 5000
      DESC="O/T-SUP";CQPTR=36;CYTPR=0;QT=QTD.11.ERN;YT="";GOSUB 5000
      DESC="OTHER";CQPTR=38;CYPTR=0;QT=QTD.OTH.INC;YT="";GOSUB 5000
      DESC="NON-TAX";CQPTR=10;CYPTR=0;QT=QTD.NON.TAX;YT="";GOSUB 5000
      DESC="GROSS";CQPTR=9;CYPTR=0;QT=QTD.GROSS;YT="";GOSUB 5000
*
      DESC="TAXES";GOSUB 4000
      DESC="FWHT";CQPTR=12;CYPTR=0;QT=QTD.FWHT;YT="";GOSUB 5000
      DESC="FICA";CQPTR=13;CYPTR=0;QT=QTD.FICA;YT="";GOSUB 5000
      DESC="SUI";CQPTR=14;CYPTR=0;QT=QTD.SUI;YT="";GOSUB 5000
      DESC="STATE";CQPTR=15;CYPTR=0;QT=QTD.SWHT;YT="";GOSUB 5000
      DESC="CITY";CQPTR=16;CYPTR=0;QT=QTD.CWHT;YT="";GOSUB 5000
      DESC="EIC";CQPTR=17;CYPTR=0;QT=QTD.EIC;YT="";GOSUB 5000
*
      DESC="DEDUCTIONS";GOSUB 4000
      CQPTR=0;CYPTR=0
      DED.CNT = COUNT(QTD.DED,VM) + (QTD.DED # "")
      FOR I = 1 TO DED.CNT
         DED.CD=QTD.DED<1,I>
         MATREAD MDED.REC FROM MISCDED,CONO:QTD.DED<1,I> ELSE
            MAT MDED.REC = ""
            MDED.SCRN.DESC="??????"
         END
         DESC=MDED.SCRN.DESC
         QT="";YT=""
         FOR II = 1 TO 4
            QT<1,II>=QTD.AMNT<1,I,II>
         NEXT II
         YT=""
         GOSUB 5000
      NEXT I
*
      DED.CD="BD"
      DESC="BONDS";QT=QTD.BONDS;YT="";GOSUB 5000
      LINE.COUNT=99
      RETURN
*
*---- PRINT CATEGORY HEADING
*
4000*
      IF LINE.COUNT > 54 THEN
         GOSUB 80000
         FLAG="Y"
      END
      IF FLAG="Y" THEN
         H.LINE=EMP.NO "L#4":SP2:EMP.NAME "L#30":SP2
         FLAG="N"
      END ELSE
         H.LINE=SPACE(38)
      END
      H.LINE=H.LINE:DED.CD "L#2":SP2:DESC "L#15"
      PRINT H.LINE
      LINE.COUNT=LINE.COUNT+1
      RETURN
*
*---- PRINT DETAIL LINE
*
5000*
      IF LINE.COUNT > 55 THEN
         GOSUB 80000
         FLAG="Y"
      END
      IF FLAG="Y" THEN
         H.LINE=EMP.NO "L#4":SP2:EMP.NAME "L#30":SP2
         FLAG="N"
      END ELSE
         H.LINE=SPACE(38)
      END
      DESC="  ":DESC
      H.LINE=H.LINE:DED.CD "L#2":SP2:DESC "L#15"
      FOR Q = 1 TO 4
         H.LINE=H.LINE:SP2:OCONV(QT<1,Q>,"MD2Z,") "R#12"
         BEGIN CASE
         CASE CQPTR > 0
            COT(CQPTR)<1,Q>=COT(CQPTR)<1,Q>+QT<1,Q>
            YT=YT+QT<1,Q>
         CASE DED.CD # ""
            YT=YT+QT<1,Q>
         END CASE
      NEXT Q
      H.LINE=H.LINE:SP2:OCONV(YT,"MD2Z,") "R#13"
      IF CYPTR > 0 THEN
         COT(CYPTR)=COT(CYPTR)+YT
      END
      PRINT H.LINE
      LINE.COUNT=LINE.COUNT+1
      IF DED.CD # "" THEN
         IF DED.CD = "BD" THEN
            FOR Q = 1 TO 4
               COT(21)<1,Q>=COT(21)<1,Q>+QT<1,Q>
            NEXT Q
         END ELSE
            LOCATE DED.CD IN COT(19)<1>,1 BY "AL" SETTING P ELSE
               COT(19)=INSERT(COT(19),1,P,0,DED.CD)
               COT(20)=INSERT(COT(20),1,P,0,"")
            END
            FOR Q = 1 TO 4
               COT(20)<1,P,Q>=COT(20)<1,P,Q>+QT<1,Q>
            NEXT Q
         END
      END
      RETURN
*
*---- PRINT HEADINGS
*
80000*
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      H.LINE = 'DATE : ' : OCONV(DATE(), 'D2/') "L#8"
      H.LINE = H.LINE : SPACE(POS1) : CO.NAME "L#30"
      H.LINE = H.LINE : SPACE(POS2) : 'PAGE ' : PAGE.NO "R#3"
      PRINT H.LINE
      PRINT SPACE(50) : "EMPLOYEE YTD AND QTD INFORMATION"
      PRINT
      PRINT HH.LINE
      PRINT DH.LINE
      LINE.COUNT = 5
      RETURN
*
*---- END OF PROGRAM
*
999999*
      PRINTER OFF
   END
