  SUBROUTINE MISCDED.MAINT2(CONO,NEW,DED.NO,MAT MDED.REC)
*COPY>CPYLIB>COM1
*COPY>PRS.CPYLIB>COM.PRS.FILE.VARS
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - PRSBP
* PROGRAM     - MISCDED.MAINT2
* BY          - MARK TRAYWICK, CBA
* DATE        - 05/10/88
* DESCRIPTION - This sub defines the investment options in the
*               MISCDED file for defined contribution plans.
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PRS.CPYLIB>PRS.FILE.VARS
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>INVEST.FUND.CODE
*
*---- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
*
*--- INITIALIZATION
*
  OLD.START.LINE =''; PAGE.SIZE = 8
  LAST.LINE = 0
  MAT IFCR.REC = ''
  FOR I = 1 TO SCV.REC.SIZE
    SCV.REC(I)<ECD.SCRN.NO> = ""
  NEXT I
*
*--- MAIN PROCESSING
*
  SCV.REC(1)<ECD.SCRN.NO> = DED.NO
  SCV.REC(2)<ECD.SCRN.NO> = MDED.SCRN.DESC
  ECD.ACTION = 3; CALL SCRN.EDIT
*
  IF NEW THEN
    FOR REQUEST = 1 TO 4 UNTIL ECD.RET.VALUE = "END"
      ON REQUEST GOSUB 1000,2000,3000,4000
    NEXT REQUEST
  END ELSE
    SCV.REC(3)<ECD.SCRN.NO> = MDED.BT.MAX
    SCV.REC(4)<ECD.SCRN.NO> = MDED.VEST.MON
    SCV.REC(5)<ECD.SCRN.NO> = MDED.DED.MAX
    GOSUB 11000
    ECD.ACTION = 3; CALL SCRN.EDIT
  END
  LN = 1
  GOSUB 10000
*
  MORE = 1
  LOOP
  WHILE MORE DO
    ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
    REQUEST = ECD.RET.VALUE
    BEGIN CASE
      CASE REQUEST = 'END' OR REQUEST = 'E'
        MORE = 0
      CASE NUM(REQUEST)
        ON REQUEST GOSUB 1000,2000,3000,4000
    END CASE
  REPEAT
  GOTO 99999
*
*--- B/T LIMIT
1000 *
  ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN MDED.BT.MAX = ECD.RET.VALUE
  RETURN
*
*--- VESTING MONTHS
2000 *
  ECD.NUM = 4; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN MDED.VEST.MON = ECD.RET.VALUE
  RETURN
*
*--- MAX DEDUCTION
3000 *
  ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN MDED.DED.MAX = ECD.RET.VALUE
  RETURN
*
*--- INVESTMENT FUNDS
4000 *
  IF NOT(NEW) THEN
    ECD.NUM = 22; ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
  END ELSE
    OPTION = 'A'
  END
  BEGIN CASE
    CASE OPTION = 'END' OR OPTION = 'E'
      GOTO 4999
    CASE OPTION = 'A'
      DONE = 0
      LOOP
      UNTIL DONE DO
        LN = LAST.LINE + 1
        GOSUB 10000
        SCV.REC(6)<ECD.SCRN.NO,LN> = LN
        ECD.NUM = 6; ECD.SUB.NUM = LN
        ECD.ACTION = 5; CALL SCRN.EDIT
*
4010    ECD.NUM = 7; ECD.SUB.NUM = LN
        ECD.ACTION = 4; CALL SCRN.EDIT
        IF ECD.RET.VALUE # 'END' THEN
          LOCATE ECD.RET.VALUE IN MDED.INVEST<1>,1 SETTING FND THEN
            ERRMSG = 'DUPLICATE CODE ENTERED LINE - ':FND
            GOSUB 91000
            GOTO 4010
          END
          MATREAD IFCR.REC FROM INVEST.FUND.CODE, CONO:ECD.RET.VALUE THEN
            MDED.INVEST<1,LN> = ECD.RET.VALUE
            SCV.REC(8)<ECD.SCRN.NO,LN> = IFCR.DESC
            ECD.NUM = 8; ECD.SUB.NUM = LN
            ECD.ACTION = 5; CALL SCRN.EDIT
            LAST.LINE = LN
          END ELSE
            MAT IFCR.REC = ""
            ERRMSG = 'INVESTMENT FUND CODE ':ECD.RET.VALUE:' NOT FOUND'
            GOSUB 91000
            GOTO 4010
          END
        END ELSE
          DONE = 1
          OLD.START.LINE = 0
          GOSUB 11000
          IF LN > LAST.LINE THEN LN = LAST.LINE
          IF LN < 1 THEN LN = 1
          IF NEW THEN NEW = 0
        END
      REPEAT
    CASE OPTION = 'S'
      LN = 1 + INT((LN-1)/PAGE.SIZE)* PAGE.SIZE + PAGE.SIZE
      IF LN > LAST.LINE THEN LN = 1
    * T25978 v
    CASE OPTION = 'SR'
      LN = 1 + INT((LN-1)/PAGE.SIZE)* PAGE.SIZE - PAGE.SIZE
      IF LN < 1 THEN LN = 1
    CASE OPTION = 'ST'
      LN = 1
    CASE OPTION = 'SB'
      LN = LAST.LINE
    * T25978 ^
    CASE OPTION = 'D'
      LAST.LINE = DCOUNT(MDED.INVEST,VM)
      IF LAST.LINE THEN
        GOSUB 10000
        ECD.MINV = START.LINE
        ECD.MAXV = START.LINE + PAGE.SIZE - 1
        IF ECD.MAXV > LAST.LINE THEN ECD.MAXV = LAST.LINE
        ECD.NUM = 23; ECD.ACTION = 4; CALL SCRN.EDIT
        IF ECD.RET.VALUE # 'END' THEN
          LN = ECD.RET.VALUE
          MDED.INVEST = DELETE(MDED.INVEST,1,LN,0)
          SCV.REC(6) = DELETE(SCV.REC(6),ECD.SCRN.NO,LAST.LINE,0)
          SCV.REC(7) = DELETE(SCV.REC(7),ECD.SCRN.NO,LN,0)
          SCV.REC(8) = DELETE(SCV.REC(8),ECD.SCRN.NO,LN,0)
          LAST.LINE = DCOUNT(MDED.INVEST,VM)
          OLD.START.LINE = 0
          IF LN > LAST.LINE THEN LN = LAST.LINE
          IF LN < 1 THEN LN = 1
        END
      END
  END CASE
  GOSUB 10000
  GOTO 4000
4999 *
  RETURN
*
*--- SCROLLING ROUTINE
10000 *
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE)* PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN RETURN
  OLD.START.LINE = START.LINE
  ECD.NUM = 6
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  ECD.NUM = 7
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  ECD.NUM = 8
  ECD.SUB.NUM = START.LINE
  ECD.ACTION = 7
  CALL SCRN.EDIT
  RETURN
*
*--- LOAD SCROLLING AREA
11000 *
  SCV.REC(6)<ECD.SCRN.NO> = ''
  SCV.REC(7)<ECD.SCRN.NO> = ''
  SCV.REC(8)<ECD.SCRN.NO> = ''
  LAST.LINE = DCOUNT(MDED.INVEST,VM)
  FOR I = 1 TO LAST.LINE
    MATREAD IFCR.REC FROM INVEST.FUND.CODE,CONO:MDED.INVEST<1,I> ELSE
      MAT IFCR.REC = ""
      IFCR.DESC = "?????"
    END
    SCV.REC(6)<ECD.SCRN.NO,I> = I
    SCV.REC(7)<ECD.SCRN.NO,I> = MDED.INVEST<1,I>
    SCV.REC(8)<ECD.SCRN.NO,I> = IFCR.DESC
  NEXT I
11099 *
  RETURN
*
*--- SYSCOM CALLS
91000 * 
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 * 
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * 
  PRINT CS:
  RETURN
END
