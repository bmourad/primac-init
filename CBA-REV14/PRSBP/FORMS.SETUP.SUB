  SUBROUTINE FORMS.SETUP.SUB
*COPY>PRS.CPYLIB>COM.FORMS
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - PRSBP
* PROGRAM     - FORMS.SETUP.SUB
* AUTHOR      - ZIAD YAMOUT, CBA
* DATE        - 09/20/88
* DESCRIPTION - This program is used to maintain questions and setups
*               for various forms.
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*COPY>PRS.CPYLIB>AUTO.FORMS
*COPY>PRS.CPYLIB>AUTO.FORMS.COM
*COPY>PRS.CPYLIB>AUTO.FORMS.DATA
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*--- Display scrolling lines
  DIM P.REC(120)
  BEGIN.PAGE = 7; PAGE.SIZE = 13; LINE.SPACE = 1
  OLD.START.LINE = 0
  LINES = DCOUNT(H.REF,VM)
  LN = 1; GOSUB 2000
*--- Prompt line routine
  LOOP
    ECD.NUM = 3; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    ACTION = ECD.RET.VALUE
    BEGIN CASE
      CASE ACTION = "END" OR ACTION = "E" OR ACTION = ""
        ACTION = "END"
        ERRFLG = 0; GOSUB 1100
        IF ERRMSG # "" THEN
          ERRFLG = 1; GOSUB 91000
        END ELSE
          GOSUB 1200
          IF ERRMSG # "" THEN
            ERRFLG = 1; GOSUB 91000
          END
        END
      CASE NUM(ACTION)
        IF ACTION > 0 AND ACTION <= LINES THEN
          OLD.LINES = LINES
          ECD.SCRN.NO = ECD.SCRN.NO + 1
          ECD.ACTION = 2; CALL SCRN.EDIT
          REF.NO = H.REF<1,ACTION>
          CALL FORMS.DATA.SUB
          ECD.SCRN.NO = ECD.SCRN.NO - 1
          ECD.ACTION = 2; CALL SCRN.EDIT
          ECD.ACTION = 3; CALL SCRN.EDIT
          LINES = DCOUNT(H.REF,VM)
          IF OLD.LINES <> LINES THEN LN = LINES
          OLD.START.LINE = 0; GOSUB 2000
        END
      CASE ACTION = "D"
        OLD.LINES = LINES
        ECD.SCRN.NO = ECD.SCRN.NO + 1
        ECD.ACTION = 2; CALL SCRN.EDIT
        REF.NO = ""
        CALL FORMS.DATA.SUB
        ECD.SCRN.NO = ECD.SCRN.NO - 1
        ECD.ACTION = 2; CALL SCRN.EDIT
        ECD.ACTION = 3; CALL SCRN.EDIT
        LINES = DCOUNT(H.REF,VM)
        IF OLD.LINES <> LINES THEN LN = LINES
        OLD.START.LINE = 0; GOSUB 2000
      CASE ACTION = "S"
        LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 2000
      * T25978 v
      CASE ACTION = 'SR'
        LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 2000
      CASE ACTION = 'ST'
        LN = 1
        GOSUB 2000
      CASE ACTION = 'SB'
        LN = LINES
        GOSUB 2000
      * T25978 ^
      CASE ACTION = "H"
        YCNT = DCOUNT(H.S.Y,VM)
        PRINT CS :
        FOR I = 1 TO YCNT
          XCNT = DCOUNT(H.S.X<1,I>,SVM)
          Y = H.S.Y<1,I>
          FOR J = 1 TO XCNT
            X = H.S.X<1,I,J>
            F.ID = FORM.ID:"!":OPTION:"!":H.S.R<1,I,J>
            MATREAD AFD.REC FROM AUTO.FORMS, F.ID THEN
              BEGIN CASE
                CASE AFD.S.SEQ = "S"
                  VAR = STR(AFD.S.HEAD,AFD.MAXL)
                CASE AFD.S.SEQ = "H"
                  VAR = AFD.S.HEAD
                CASE AFD.S.SEQ # "N" AND X = AFD.S.X
                  IF AFD.S.SEQ < 1 OR AFD.S.SEQ > 98 THEN
                    VAR = AFD.S.HEAD
                  END ELSE
                    VAR = AFD.S.SEQ "R#2" : "- " : AFD.S.HEAD
                  END
                CASE AFD.TYP = 6
                  VAR = "99/99/99" AFD.FMT
                CASE AFD.CONV # ""
                  VAR = OCONV(STR(9,AFD.MAXL-1),AFD.CONV) AFD.FMT
                CASE 1
                  VAR = STR("X",AFD.MAXL) AFD.FMT
              END CASE
              PRINT @(X,Y) : VAR :
            END
          NEXT J
        NEXT I
        ERRMSG = "Hit <RETURN> to continue : "
        GOSUB 91000
        ECD.ACTION = 2; CALL SCRN.EDIT
        ECD.ACTION = 3; CALL SCRN.EDIT
        OLD.START.LINE = 0; GOSUB 2000
      CASE ACTION = "MT" AND AFH.WIDTH > 80
        ERRMSG = "Form width is greater than terminal width (80)"
        GOSUB 91000
      CASE ACTION = "MT" OR ACTION = "MP"
        GOSUB 1100
        IF ERRMSG # "" THEN
          GOSUB 91000; GOTO 799
        END
        YCNT = DCOUNT(T.F.Y,VM)
        FOR I = 1 TO AFH.LENGTH
          P.REC(I) = SPACE(AFH.WIDTH)
        NEXT I
        FOR I = 1 TO YCNT
          XCNT = DCOUNT(T.F.X<1,I>,SVM)
          Y = T.F.Y<1,I>
          FOR J = 1 TO XCNT
            X = T.F.X<1,I,J>
            F.ID = FORM.ID:"!":OPTION:"!":T.F.R<1,I,J>
            MATREAD AFD.REC FROM AUTO.FORMS, F.ID THEN
              BEGIN CASE
                CASE AFD.OPT = "P"
                  VAR = STR(9,AFD.MAXL)
                CASE AFD.TYP = 6
                  VAR = "99/99/99" AFD.FMT
                CASE AFD.CONV # ""
                  VAR = OCONV(STR(9,AFD.MAXL-1),AFD.CONV:AFD.OUTPUT) AFD.FMT
                CASE 1
                  VAR = STR("X",AFD.MAXL) AFD.FMT
              END CASE
              P.REC(Y) = P.REC(Y)[1,X-1]:VAR:P.REC(Y)[X+AFD.MAXL,AFH.WIDTH]
            END
          NEXT J
        NEXT I
        IF ACTION = "MP" THEN
          PRINTER ON
          PRINT CHAR(12)
          FOR I = 1 TO 2
            FOR J = 1 TO AFH.LENGTH
              PRINT P.REC(J)
            NEXT J
          NEXT I
          PRINTER CLOSE
          PRINTER OFF
        END ELSE
          S = 1; L = 23
          IF AFH.LENGTH < L THEN L = AFH.LENGTH
          LOOP
            PRINT CS :
            LNO = 0
            FOR I = S TO L
              PRINT @(0,LNO) : P.REC(I) : CL :
              LNO = LNO + 1
            NEXT I
            ERRMSG = "Hit <RETURN> to continue : "
            GOSUB 91000
          WHILE L < AFH.LENGTH DO
            S = L + 1; L = L + 23
            IF AFH.LENGTH < L THEN L = AFH.LENGTH
          REPEAT
          ECD.ACTION = 2; CALL SCRN.EDIT
          ECD.ACTION = 3; CALL SCRN.EDIT
          OLD.START.LINE = 0; GOSUB 2000
        END
799   CASE ACTION = "FT" AND AFH.WIDTH > 80
        ERRMSG = "Form width is greater than terminal width (80)"
        GOSUB 91000
      CASE ACTION = "FP" OR ACTION = "FT"
        GOSUB 1200
        IF ERRMSG # "" THEN
          GOSUB 91000; GOTO 899
        END
        FOR I = 1 TO AFH.LENGTH
          P.REC(I) = SPACE(AFH.WIDTH)
        NEXT I
        YCNT = DCOUNT(T.F.Y,VM)
        FOR I = 1 TO YCNT
          XCNT = DCOUNT(T.F.X<1,I>,SVM)
          Y = T.F.Y<1,I>
          FOR J = 1 TO XCNT
            X = T.F.X<1,I,J>
            F.ID = FORM.ID:"!":T.F.O<1,I,J>:"!":T.F.R<1,I,J>
            MATREAD AFD.REC FROM AUTO.FORMS, F.ID THEN
              BEGIN CASE
                CASE AFD.OPT = "P"
                  VAR = STR(9,AFD.MAXL)
                CASE AFD.TYP = 6
                  VAR = "99/99/99" AFD.FMT
                CASE AFD.CONV # ""
                  VAR = OCONV(STR(9,AFD.MAXL-1),AFD.CONV:AFD.OUTPUT) AFD.FMT
                CASE 1
                  VAR = STR("X",AFD.MAXL) AFD.FMT
              END CASE
              P.REC(Y) = P.REC(Y)[1,X-1]:VAR:P.REC(Y)[X+AFD.MAXL,AFH.WIDTH]
            END
          NEXT J
        NEXT I
        IF ACTION = "FP" THEN
          PRINTER ON
          PRINT CHAR(12)
          FOR I = 1 TO 2
            FOR J = 1 TO AFH.LENGTH
              PRINT P.REC(J)
            NEXT J
          NEXT I
          PRINTER CLOSE
          PRINTER OFF
        END ELSE
          S = 1; L = 23
          IF AFH.LENGTH < 23 THEN L = AFH.LENGTH
          LOOP
            PRINT CS :
            LNO = 0
            FOR I = S TO L
              PRINT @(0,LNO) : P.REC(I) : CL :
              LNO = LNO + 1
            NEXT I
            ERRMSG = "Hit <RETURN> to continue : "
            GOSUB 91000
          WHILE L < AFH.LENGTH DO
            S = L + 1; L = L + 23
            IF AFH.LENGTH < L THEN L = AFH.LENGTH
          REPEAT
          ECD.ACTION = 2; CALL SCRN.EDIT
          ECD.ACTION = 3; CALL SCRN.EDIT
          OLD.START.LINE = 0; GOSUB 2000
        END
899 END CASE
  WHILE ACTION # "END" DO REPEAT
  GOTO 99999
1100 T.F.Y = H.F.Y; T.F.X = H.F.X; T.F.L = H.F.L; T.F.R = H.F.R
  ERRMSG = ""
  IF OPTION # "H" THEN GOTO 1199
  YCNT = DCOUNT(AFH.HT.Y,VM)
  FOR I = 1 TO YCNT WHILE ERRMSG = ""
    Y = AFH.HT.Y<1,I>
    LOCATE Y IN T.F.Y<1> BY "AR" SETTING YNO ELSE
      INS Y BEFORE T.F.Y<1,YNO>
      INS "" BEFORE T.F.X<1,YNO>
      INS "" BEFORE T.F.L<1,YNO>
      INS "" BEFORE T.F.R<1,YNO>
    END
    XCNT = DCOUNT(AFH.HT.X<1,I>,SVM)
    FOR J = 1 TO XCNT WHILE ERRMSG = ""
      X = AFH.HT.X<1,I,J>
      LOCATE X IN T.F.X<1,YNO> BY "AR" SETTING XNO THEN
        ERRMSG = "Subtotal Ref # ":AFH.HT.REF<1,I,J>:" is overlaying Header ref # ":T.F.R<1,YNO,XNO>
      END ELSE
        IF XNO > 1 THEN
          IF T.F.X<1,YNO,XNO-1>+T.F.L<1,YNO,XNO-1> > X THEN
            ERRMSG = "Subtotal Ref # ":AFH.HT.REF<1,I,J>:" is overlaying the back end of Header ref # ":T.F.R<1,YNO,XNO-1>
          END
        END
        IF T.F.X<1,YNO,XNO> > 0 AND X + AFH.HT.LEN<1,I,J> > T.F.X<1,YNO,XNO> THEN
          ERRMSG = "Subtotal Ref # ":AFH.HT.REF<1,I,J>:" is overlaying the front end of Header ref # ":T.F.R<1,YNO,XNO>
        END
      END
      INS X BEFORE T.F.X<1,YNO,XNO>
      INS AFH.HT.LEN<1,I,J> BEFORE T.F.L<1,YNO,XNO>
      INS AFH.HT.REF<1,I,J> BEFORE T.F.R<1,YNO,XNO>
    NEXT J
  NEXT I
1199 RETURN
1200 T.F.Y = H.F.Y; T.F.X = H.F.X; T.F.L = H.F.L; T.F.R = H.F.R
  T.F.O = ""; YCNT = DCOUNT(T.F.Y,VM)
  FOR I = 1 TO YCNT
    XCNT = DCOUNT(T.F.X<1,I>,SVM)
    FOR J = 1 TO XCNT
      T.F.O<1,I,J> = OPTION
    NEXT J
  NEXT I
  IF OPTION = "H" THEN
    F.Y = AFH.DF.Y; F.X = AFH.DF.X
    F.L = AFH.DF.LEN; F.R = AFH.DF.REF
    OP = "D"; OP1 = "Detail"; OP2 = "Header"
  END ELSE
    F.Y = AFH.HF.Y; F.X = AFH.HF.X
    F.L = AFH.HF.LEN; F.R = AFH.HF.REF
    OP = "H"; OP1 = "Header"; OP2 = "Detail"
  END
  YCNT = DCOUNT(F.Y,VM); ERRMSG = ""
  FOR I = 1 TO YCNT WHILE ERRMSG = ""
    Y = F.Y<1,I>
    LOCATE Y IN T.F.Y<1> BY "AR" SETTING YNO ELSE
      INS Y BEFORE T.F.Y<1,YNO>
      INS "" BEFORE T.F.X<1,YNO>
      INS "" BEFORE T.F.L<1,YNO>
      INS "" BEFORE T.F.R<1,YNO>
      INS "" BEFORE T.F.O<1,YNO>
    END
    XCNT = DCOUNT(F.X<1,I>,SVM)
    FOR J = 1 TO XCNT WHILE ERRMSG = ""
      X = F.X<1,I,J>
      LOCATE X IN T.F.X<1,YNO> BY "AR" SETTING XNO THEN
        ERRMSG = OP1:" Ref # ":F.R<1,I,J>:" is overlaying ":OP2:" ref # ":T.F.R<1,YNO,XNO>
      END ELSE
        IF XNO > 1 THEN
          IF T.F.X<1,YNO,XNO-1>+T.F.L<1,YNO,XNO-1> > X THEN
            ERRMSG = OP1:" Ref # ":F.R<1,I,J>:" is overlaying the back end of ":OP2:" ref # ":T.F.R<1,YNO,XNO-1>
          END
        END
        IF T.F.X<1,YNO,XNO> > 0 AND X + F.L<1,I,J> > T.F.X<1,YNO,XNO> THEN
          ERRMSG = OP1:" Ref # ":F.R<1,I,J>:" is overlaying the front end of ":OP2:" ref # ":T.F.R<1,YNO,XNO>
        END
      END
      INS X BEFORE T.F.X<1,YNO,XNO>
      INS F.L<1,I,J> BEFORE T.F.L<1,YNO,XNO>
      INS F.R<1,I,J> BEFORE T.F.R<1,YNO,XNO>
      INS OP BEFORE T.F.O<1,YNO,XNO>
    NEXT J
  NEXT I
  RETURN
*---- Scrolling routine
2000 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE = OLD.START.LINE THEN GOTO 2099
  OLD.START.LINE = START.LINE
  CNT = 1
  FOR LNO = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(LNO-1,PAGE.SIZE)
    PRINT @(0,SLN) : LNO "R#3" : CL :
    PRINT @(4,SLN) : H.REF<1,LNO> "R#3" :
    PRINT @(8,SLN) : H.ATT<1,LNO> "R#3" :
    PRINT @(14,SLN): P.D<1,LNO> "L#30" :
    PRINT @(43,SLN): P.F.T<1,LNO> "L#1" :
    PRINT @(45,SLN): P.F.Y<1,LNO> "R#3" :
    PRINT @(49,SLN): P.F.X<1,LNO> "R#3" :
    PRINT @(54,SLN): H.SEQ<1,LNO> "L#3" :
    PRINT @(58,SLN): P.H.Y<1,LNO> "R#3" :
    PRINT @(62,SLN): P.H.X<1,LNO> "R#3" :
    PRINT @(67,SLN): P.D.T<1,LNO> "R#1" :
    PRINT @(69,SLN): P.D.Y<1,LNO> "R#3" :
    PRINT @(73,SLN): P.D.X<1,LNO> "R#3" :
    CNT = CNT + 1
  NEXT LNO
  FOR LNO = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(LNO-1,PAGE.SIZE)
    PRINT @(0,SLN) : CL :
  NEXT LNO
2099 RETURN
91000PRINT @(0,23) : ERRMSG : CL :
  INPUT REPLY :
  PRINT @(0,23) : CL :
  RETURN
99999 RETURN
END
