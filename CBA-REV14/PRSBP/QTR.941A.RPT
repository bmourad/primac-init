*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - QTR.941A.RPT
* BY          - WALID YAMOUT, CBA
* DATE        - 06/11/85
* DESCRIPTION - Prints the Quarterly 941A Report
* REMARKS     - Discussion as to whether report should be report
*               adjusted gross (less before tax deductions).  Jan
*               determined  that this figure should be adj. amount
*               See CSF #10323 and conflicting CSF #7799. (RRG).
*********************************************************************
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PRS.CPYLIB>MISCDED
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
      PROCREAD X ELSE PRINT "CANNOT READ INPUT BUFFER";INPUT X;STOP
      CONO=X<1>;CO.NAME=X<2>;QTR=X<3>;END.DATE=X<4>;BACKUP=X<5>
      IF BACKUP='Y' THEN
         OPEN "S.EMPLOYEE" TO EMPLOYEE ELSE STOP 201,"CAN'T OPEN S.EMPLOYEE"
         OPEN "S.QTD-YTD" TO QTD.YTD ELSE STOP 201,"CAN'T OPEN S.QTD-YTD"
         OPEN "S.COMPANY" TO COMPANY ELSE STOP 201,"CAN'T OPEN S.COMPANY"
      END ELSE
         OPEN "EMPLOYEE" TO EMPLOYEE ELSE STOP 201,"CAN'T OPEN EMPLOYEE"
         OPEN "QTD-YTD" TO QTD.YTD ELSE STOP 201,"CAN'T OPEN QTD-YTD"
         OPEN "COMPANY" TO COMPANY ELSE STOP 201,"CAN'T OPEN COMPANY"
      END
      OPEN "MISCDED" TO MISCDED ELSE STOP 201,"CAN'T OPEN MISCDED FILE"
      MATREAD COMP.REC FROM COMPANY,CONO ELSE PRINT "CAN'T READ COMPANY":;INPUT X;STOP
      SP4=SPACE(4)
      SP15=SPACE(15)
      SP30=SPACE(30)
      SP45=SPACE(45)
      EMP="";S.S=""
      LINES=0;NET=0;FCA=0;PAGE.NO=0
100*
      READNEXT ID ELSE GOTO 200
      MAT EMP.REC="";MAT QTD.REC=""
      MATREAD EMP.REC FROM EMPLOYEE,ID ELSE GOTO 100
      ALT.GROSS=0;ALT.NTSICK=0;QTD.NTD=0
      IF EMP.ALT.NO # "" THEN
         MATREAD QTD.REC FROM QTD.YTD,ID[1,3]:EMP.ALT.NO ELSE MAT QTD.REC=""
         FOR X=1 TO QTR
            ALT.GROSS=ALT.GROSS+QTD.GROSS<1,X>
            ALT.NTSICK=ALT.NTSICK+QTD.SK.NO.FI<1,X>
         NEXT X
        GOSUB 5000;ALT.GROSS=ALT.GROSS-NON.TAX.DED
      END
      MATREAD QTD.REC FROM QTD.YTD,ID ELSE GOTO 100
      IF QTD.GROSS<1,QTR>+0=0 THEN GOTO 100
      GROSS.YTD=0;NTSICK.YTD=0
      FOR Y=1 TO QTR
         GROSS.YTD=GROSS.YTD+QTD.GROSS<1,Y>
         NTSICK.YTD=NTSICK.YTD+QTD.SK.NO.FI<1,Y>
      NEXT Y
     GOSUB 5000;GROSS.YTD=GROSS.YTD-NON.TAX.DED
*
      FICA=QTD.GROSS<1,QTR>-QTD.SK.NO.FI<1,QTR>-QTD.NTD
      THIS.FICA=GROSS.YTD-NTSICK.YTD;THAT.FICA=ALT.GROSS-ALT.NTSICK
      PRE.FICA=THIS.FICA-FICA
      BEGIN CASE
      CASE (THIS.FICA+THAT.FICA) > CO.MAX.FICA.TAX
         THIS.FICA=(CO.MAX.FICA.TAX-THAT.FICA)-PRE.FICA
      CASE THIS.FICA > CO.MAX.FICA.TAX
         THIS.FICA=CO.MAX.FICA.TAX-PRE.FICA
      CASE THAT.FICA > CO.MAX.FICA.TAX
         THIS.FICA=0
      CASE 1
         THIS.FICA=FICA
      END CASE
      IF THIS.FICA<0 THEN THIS.FICA=0
      FICA=THIS.FICA
*
      LINES=LINES+1
      EMP<LINES>=EMP.LAST.NAME:", ":EMP.FRST.NAME
      S.S<LINES>=EMP.SOC.SEC
      NET<LINES>=NET<LINES>+FICA
      FCA<LINES>=FCA<LINES>+(QTD.GROSS<1,QTR>-QTD.SK.NO.FI<1,QTR>-QTD.NTD)
      GOTO 100
200*
*     CALL ALIGN.941A
      PRINTER ON
      GOSUB 2000
      CNT=0
      FOR I=1 TO LINES
         CNT=CNT + 1
         IF CNT=45 THEN GOSUB 1000
         PRINT S.S<I>"L#11    ":EMP<I>"L#28    ":OCONV(NET<I>,'MD2,')"R#10    ":OCONV(FCA<I>,'MD2,')"R#10"
         TOT.PAGE=TOT.PAGE + FCA<I>
         TOT.NET=TOT.NET + NET<I>
      NEXT I
      FOR I=CNT TO 45;PRINT;NEXT I
      PRINT SP45:OCONV(TOT.NET,'MD2,')"R#10      ":OCONV(TOT.PAGE,'MD2,')"R#10"
      PRINTER OFF
      GOTO 999999
1000*
      PRINT
      PRINT
      PRINT SP45:OCONV(TOT.NET,'MD2,')"R#10      ":OCONV(TOT.PAGE,'MD2,')"R#10"
      FOR X=1 TO 6;PRINT;NEXT X
2000*
      CNT=1
      TOT.PAGE=0
      TOT.NET=0
      PAGE.NO=PAGE.NO + 1
      PRINT
      PRINT
      PRINT
      PRINT SP4:CO.FED.TAX.ID"L#10":SP30:END.DATE"L#8":SP15:PAGE.NO"R#2"
      PRINT SP4:CO.NAME"L#30"
      PRINT SP4:CO.ADDRESS"L#30"
      PRINT SP4:CO.CITY"L#13   ":CO.STATE"L#2  ":CO.ZIP[1,5]:'-':CO.ZIP[6,4]
      FOR X=1 TO 6;PRINT;NEXT X
      RETURN
*
*---- NON TAXABLE DEDUCTIONS
5000*
      NON.TAX.DED=0
      NTD.CNT=DCOUNT(QTD.DED,VM)
      FOR REF=1 TO NTD.CNT
         MATREAD MDED.REC FROM MISCDED,CONO:QTD.DED<1,REF> ELSE MAT MDED.REC=""
         IF MDED.TAXABLE="B" THEN
            FOR X=1 TO QTR
               NON.TAX.DED=NON.TAX.DED+QTD.AMNT<1,REF,X>
               IF X=QTR THEN QTD.NTD=QTD.NTD+QTD.AMNT<1,REF,X>
            NEXT X
         END
      NEXT REF
      RETURN
999999*
   END
