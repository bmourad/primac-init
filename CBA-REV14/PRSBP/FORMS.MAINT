*COPY>CPYLIB>COM3
*********************************************************************
* REVISION    - [08.1]
* SOURCE      - PRSBP
* PROGRAM     - FORMS.MAINT
* AUTHOR      - ZIAD YAMOUT, CBA
* DATE        - 09/28/88
*********************************************************************
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*T25236 v
*COPY>CPYLIB>TCC
*T25236 ^
*COPY>PRS.CPYLIB>AUTO.FORMS
*COPY>PRS.CPYLIB>AUTO.FORMS.DATA
*
*--- Get information passed from the calling PROC
     PROCREAD BUFFER ELSE
        ERRMSG = "This function must be executed from a PROC"
        GOSUB 91000; GOTO 99999
     END
     CONO = BUFFER<1>
     CO.NAME = BUFFER<2>
     FORM.NAME = BUFFER<3>
     FORM.TYPE = BUFFER<4>
*--- Setup system error messages
     SYS.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
*--- Open files
     OPEN "AUTO.FORMS" TO AUTO.FORMS ELSE
        ERRMSG="Cannot open AUTO.FORMS file";GOTO 93000
     END
     OPEN "",FORM.NAME TO FORM.FILE ELSE
        ERRMSG = "Cannot open ":FORM.NAME:" file"; GOTO 93000
     END
*--- Initialization
*T25236 v
PC.PORT.TYPE.SAVE = PC.PORT.TYPE
PC.PORT.TSEL.SAVE = PC.PORT.TSEL
PC.PORT.TYPE = "STD"
PC.PORT.TSEL = "STD"
*T25236 ^
     DIM I.REC(100)
     MAT EDIT.COM=""
     TYP=0;CALL EDIT.SUB;FILL="#"
*--- Get header information for the FORM
     MATREAD AFH.REC FROM AUTO.FORMS, FORM.NAME ELSE
        ERRMSG = "Cannot locate AUTO.FORMS, ":FORM.NAME
        GOSUB 91000; GOTO 99999
     END
     IF FORM.TYPE =  "H" THEN
        H.REF = AFH.H.REF
        H.ATT = AFH.H.ATT
        H.SEQ = AFH.H.SEQ
        H.S.Y = AFH.HS.Y
        H.S.X = AFH.HS.X
        H.S.R = AFH.HS.REF
     END ELSE
        H.REF = AFH.D.REF
        H.ATT = AFH.D.ATT
        H.SEQ = AFH.D.SEQ
        H.S.Y = AFH.DS.Y
        H.S.X = AFH.DS.X
        H.S.R = AFH.DS.REF
        LOCATE 0 IN H.SEQ<1> SETTING LNO ELSE
           ERRMSG = "Cannot locate reference 0 for form ":FORM.NAME
           GOSUB 91000; GOTO 99999
        END
        I.REF = H.REF<1,LNO>
     END
     LOCATE 99 IN H.SEQ<1> SETTING LNO ELSE
        ERRMSG = "Cannot locate reference 99 for form ":FORM.NAME
        GOSUB 91000; GOTO 99999
     END
     P.REF = H.REF<1,LNO>
*--- Build screen
     HSCRN = CS
     YCNT = DCOUNT(H.S.Y,VM)
     P.SEQ = ""; H.PTR = ""
     H.CONV = ""; H.FMT = ""; H.Y = ""; H.X = ""
     FOR I = 1 TO YCNT
        XCNT = DCOUNT(H.S.X<1,I>,SVM)
        Y = H.S.Y<1,I>
        FOR J = 1 TO XCNT
           X = H.S.X<1,I,J>
           F.ID = FORM.NAME:"!":FORM.TYPE:"!":H.S.R<1,I,J>
           MATREAD AFD.REC FROM AUTO.FORMS, F.ID THEN
              BEGIN CASE
              CASE AFD.S.SEQ = "S"
                 HSCRN=HSCRN:@(X,Y):STR(AFD.S.HEAD,AFD.MAXL)
              CASE AFD.S.SEQ = "H"
                 HSCRN=HSCRN:@(X,Y):AFD.S.HEAD
              CASE AFD.S.SEQ # "N" AND X = AFD.S.X
                 IF AFD.S.SEQ < 1 OR AFD.S.SEQ > 98 THEN
                    HSCRN=HSCRN:@(X,Y):AFD.S.HEAD
                 END ELSE
                    HSCRN=HSCRN:@(X,Y):AFD.S.SEQ "R#2":"- ":AFD.S.HEAD
                 END
              CASE 1
                 LOCATE H.S.R<1,I,J> IN H.REF<1> SETTING LNO THEN
                    BEGIN CASE
                    CASE H.SEQ<1,LNO> < 1
                    CASE H.SEQ<1,LNO> > 98
                    CASE H.ATT<1,LNO> < 1
                    CASE H.ATT<1,LNO> > 99
                    CASE 1
                        LOCATE H.SEQ<1,LNO> IN P.SEQ BY "AR" SETTING K ELSE
                          INS H.SEQ<1,LNO> BEFORE P.SEQ<K>
                          INS LNO BEFORE H.PTR<K>
                          INS AFD.Y BEFORE H.Y<K>
                          INS AFD.X BEFORE H.X<K>
                          INS AFD.CONV BEFORE H.CONV<K>
                          INS AFD.FMT BEFORE H.FMT<K>
                       END
                    END CASE
                 END
              END CASE
           END
        NEXT J
     NEXT I
     P.SEQ = ""; H.CNT = DCOUNT(H.PTR,AM)
     FOR I = H.CNT TO 1 STEP -1
        IF H.PTR<I> = "" THEN
           H.PTR = DELETE(H.PTR,I,0,0)
           H.Y = DELETE(H.Y,I,0,0)
           H.X = DELETE(H.X,I,0,0)
           H.CONV = DELETE(H.CONV,I,0,0)
           H.FMT = DELETE(H.FMT,I,0,0)
        END
     NEXT I
     H.CNT = DCOUNT(H.PTR,AM)
*--- Prompt for item id
100  PRINT HSCRN :
110  IF FORM.TYPE = "H" THEN
        ITEM.ID = FORM.NAME
     END ELSE
        REF.NO = I.REF; GOSUB 1000
        BEGIN CASE
        CASE VALUE = "END"
           GOTO 99999
        CASE VALUE = FORM.NAME
           ERRMSG = "Cannot have an item id equivalent to form name"
           GOSUB 91000; GOTO 110
        CASE INDEX(VALUE," ",1)
           ERRMSG = "Spaces are not allowed in an item id field"
           GOSUB 91000; GOTO 110
        END CASE
        ITEM.ID = VALUE
     END
     I.ID = CONO : ITEM.ID
     MATREADU I.REC FROM FORM.FILE, I.ID THEN
        FOR I = 1 TO H.CNT
           IF H.CONV<I> = "" THEN
              PRINT @(H.X<I>,H.Y<I>):I.REC(H.ATT<1,H.PTR<I>>) H.FMT<I> :
           END ELSE
              PRINT @(H.X<I>,H.Y<I>):OCONV(I.REC(H.ATT<1,H.PTR<I>>),H.CONV<I>) H.FMT<I> :
           END
        NEXT I
     END ELSE
        IF FORM.TYPE # "H" THEN
           PMSG = "Record (":VALUE:") is not of file, do you want to add (Y/N) : "
           TYP = 8; X = 0; Y = 23
           CALL EDIT.SUB
           PRINT @(0,23) : CL :
           IF VALUE # "Y" THEN
              RELEASE FORM.FILE, I.ID
              GOTO 110
           END
        END
        MAT I.REC = ""
        FOR REQ = 1 TO H.CNT WHILE VALUE # "END"
           REF.NO = H.REF<1,H.PTR<REQ>>; GOSUB 1000
           IF AFD.ATT THEN I.REC(AFD.ATT) = VALUE
        NEXT REQ
        IF VALUE = "END" THEN
           IF FORM.TYPE = "H" THEN
              GOTO 99999
           END ELSE
              GOTO 110
           END
        END
     END
*--- Prompt line routine
     LOOP
        REF.NO = P.REF; GOSUB 1000
        REQ = VALUE
        BEGIN CASE
        CASE REQ = "END" OR REQ = "E" OR REQ = ""
           REQ = "END"
           RELEASE FORM.FILE, I.ID
        CASE NUM(REQ)
           LOCATE REQ IN H.SEQ<1> SETTING LNO THEN
              REF.NO = H.REF<1,LNO>; GOSUB 1000
              IF VALUE # "END" AND AFD.ATT THEN I.REC(AFD.ATT) = VALUE
           END ELSE
              ERRMSG = "*** OUT OF RANGE ***"; GOSUB 91000
           END
        CASE REQ = "P"
           DELETE FORM.FILE, I.ID
           REQ = "END"
        CASE REQ = "F"
           MATWRITE I.REC ON FORM.FILE, I.ID
           REQ = "END"
        END CASE
     WHILE REQ # "END" DO REPEAT
     IF FORM.TYPE = "H" THEN
        GOTO 99999
     END ELSE
        GOTO 100
     END
*--- Prompt for all values routine
1000 F.ID = FORM.NAME:"!":FORM.TYPE:"!":REF.NO
     MATREAD AFD.REC FROM AUTO.FORMS, F.ID ELSE
        ERRMSG = "Cannot locate reference # " : REF.NO
        GOSUB 91000; VALUE = "END"; GOTO 1999
     END
     TYP = AFD.TYP; SCALER = AFD.SCALER
     Y = AFD.Y; X = AFD.X
     MINL = AFD.MINL; MAXL = AFD.MAXL
     MINV = AFD.MINV; MAXV = AFD.MAXV
     JUSTIFY = AFD.JUSTIFY; FILL.CHR = AFD.FILL.CHR
     VALDAT = AFD.VALDAT; PATRN = AFD.PATRN
     HMSG = AFD.HMSG
     O.R = AFD.O.R; DEFAULT = AFD.DEFAULT
     AFD.ATT = AFD.ATT + 0
     IF AFD.ATT THEN
        IF I.REC(AFD.ATT) # "" THEN DEFAULT = I.REC(AFD.ATT)
     END
     IF DEFAULT # "" THEN
        O.R = "O"
        IF AFD.CONV # "" THEN DEFAULT = OCONV(DEFAULT,AFD.CONV)
     END
     CALL EDIT.SUB
1999 RETURN
*--- Error message routines
91000PRINT @(0,23) : CL : ERRMSG :
     INPUT REPLY,1 :
     PRINT @(0,23) : CL :
     RETURN
93000ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
*T25236 v
PC.PORT.TYPE = PC.PORT.TYPE.SAVE
PC.PORT.TSEL = PC.PORT.TSEL.SAVE
*T25236 ^
END
