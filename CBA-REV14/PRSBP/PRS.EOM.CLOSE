*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - PRS.EOM.CLOSE
* BY          - ZIAD YAMOUT, C.B.A
* DATE        - 02/28/87
* DESCRIPTION -
*T25975 cmykleb 01/31/2002 * Save EOM transaction data so history
*                            reports can be run.
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUETE
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>PMC.CPYLIB>EOM.TRANS.HIST         ; * T25975
*COPY>GLS.CPYLIB>GLA
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>POST.REJECTS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>TCC
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
***** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*--- PROCREAD
*
   PROCREAD INBUFF ELSE
     ERRMSG = "MUST RUN FROM PROC"
     GOSUB 91000; GOTO 99999
   END
   CONO = INBUFF<1>
   POST.FLG = INBUFF<6>
*
**** INTITIALIZATION
*
   MAT FILE.VARS = ""
   MAT EDIT.COM.DRIVER = ""
   TODAY = DATE()
   UNKNOWN = "NOT ON FILE"
   SYS.FISCAL = "PRFISCAL"
   PORT.NO = 'TTY'
   CALL SYSVARS.SUB(PORT.NO)
   PROG.ID = PORT.NO:"!":CONO:"!PRS.EOM"
*
***** OPEN FILES
*
   OPEN "","CONTROL" TO CONTROL ELSE
     ERRMSG="CONTROL FILE MISSING"
     GOSUB 91000; GOTO 99000
   END
   OPEN "",INBUFF<3> TO EOM.TRANS ELSE
     ERRMSG = "YOU NEED TO RUN END OF MONTH POSTING BEFORE YOU CLOSE THE MONTH"
     GOSUB 91000; GOTO 99000
   END
   OPEN "",INBUFF<4> TO POST.REJECTS ELSE
     ERRMSG = "YOU NEED TO RUN END OF MONTH POSTING BEFORE YOU CLOSE THE MONTH"
     GOSUB 91000; GOTO 99000
   END
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
   OPEN "","PMC.SCREENS" TO M.SCREENS ELSE ERRMSG="PMC.SCREENS FILE IS MISSING";GOTO 93000
   OPEN "","MDS" TO MDS ELSE ERRMSG="MDS FILE IS MISSING";GOTO 93000
   OPEN "","MPAYREG" TO MPAYREG ELSE ERRMSG="MPAYREG FILE IS MISSING";GOTO 93000
   OPEN "","EMPLOYEE" TO EMPLOYEE ELSE ERRMSG="EMPLOYEE FILE IS MISSING";GOTO 93000
   OPEN "DICT","MTH-UNION" TO DICT.MTH.UNION ELSE ERRMSG="DICT MTH-UNION FILE IS MISSING";GOTO 93000
   OPEN "","MTH-UNION" TO MTH.UNION ELSE ERRMSG="MTH-UNION FILE IS MISSING";GOTO 93000
   OPEN "","PAYTYPE" TO PAYTYPE ELSE ERRMSG="PAYTYPE FILE IS MISSING";GOTO 93000
*T25975 v
   OPEN "","PRS.EOM.TRANS.HIST" TO PRS.EOM.TRANS.HIST ELSE ERRMSG="PRS.EOM.TRANS.HIST FILE IS MISSING";GOTO 93000
*T25975 ^
   MATREAD COMP.REC FROM COMPANY,CONO ELSE
     ERRMSG = "CANNOT LOCATE COMPANY # ":CONO; GOTO 93000
   END
   IF CO.GLS # "N" THEN
     IF CO.GL.LINK<1,3> = "Y" THEN
       OPEN "","GLA" TO GLA ELSE ERRMSG="GLA FILE IS MISSING";GOTO 93000
     END
     OPEN "","COA" TO COA ELSE ERRMSG="COA FILE IS MISSING";GOTO 93000
     EQV.FLG = 1
     OPEN "","COA.EQUIV" TO COA.EQUIV ELSE EQV.FLG = 0
   END
   STMT.HEAD = "PAYROLL"
   SAVE.PC.PORT.TYPE = PC.PORT.TYPE
   IF PC.PORT.TYPE = "termulator" THEN
     PC.PORT.TYPE = "STD"
   END
*COPY>PMCBP>EOM.CLOSE
   IF ECD.RET.VALUE = 'END' THEN
     ECD.ACTION = 99 ; CALL SCRN.EDIT
     PC.PORT.TYPE = SAVE.PC.PORT.TYPE
     GOTO 99000
   END
   CLEARFILE POST.REJECTS
   PRR.SEQ = 10000
*
***** MAIN PROCESSING
*
   IF CO.GLS = "N" THEN GOTO 7000
   IF CO.GL.LINK<1,3> # "Y" THEN GOTO 7000
**** EOM.TRANS (1) ****
6000 ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING EOM.TRANS (1) FILE"
   ECD.ACTION = 5; CALL SCRN.EDIT
   ACCOUNTS = ""; AMOUNTS = ""
   SELECT EOM.TRANS
   WRITE "EOM.TRANS (1)" ON CONTROL , PROG.ID
   DATA = 1
   LOOP
     READNEXT ETR.ID ELSE DATA = 0
   WHILE DATA DO
     IF ETR.ID[1,3] # CONO THEN GOTO 6500
     MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE GOTO 6400
     ACCT = ETR.ID[1,ACCT.LEN]
     TYPE = FIELD(FIELD(ETR.ID,"!",2),"*",1)
     LOCATE ACCT IN ACCOUNTS,1 SETTING FND ELSE
       ACCOUNTS<FND> = ACCT
       AMOUNTS<FND> = ""
     END
     IF TYPE = "D" THEN
       AMOUNTS<FND,1> = AMOUNTS<FND,1> + ETR.AMT
     END ELSE
       AMOUNTS<FND,2> = AMOUNTS<FND,2> + ETR.AMT
     END
6400 RELEASE EOM.TRANS, ETR.ID
6500 REPEAT
   MAT GLA.REC = ""
   GLA.DATE = TODAY
   GLA.SRC = "PR"
   GLA.MON = FR.CURR.PER
   ODATE = " " : OCONV(FR.CURR.DATE,"D2/")
   ACNT = COUNT(ACCOUNTS,AM)+(ACCOUNTS # "")
   READU COUNTER FROM CONTROL, CONO : "GLACOUNTER" ELSE COUNTER = 0
   WRITE "GLA ":COUNTER ON CONTROL, PROG.ID
   FOR I = 1 TO ACNT
     IF EQV.FLG = 1 THEN
       READ ACCT FROM COA.EQUIV, ACCOUNTS<I> ELSE ACCT = ACCOUNTS<I>
     END ELSE
       ACCT = ACCOUNTS<I>
     END
     COA.ID = CONO : ACCT[11,IN.ACCT.LEN]
     MATREAD COA.REC FROM COA, COA.ID ELSE COA.DESC = UNKNOWN
     GLA.DESC = COA.DESC "L#25" : ODATE
     FOR J = 1 TO 2
       IF AMOUNTS<I,J> = "" THEN GOTO 6900
       LOOP
         COUNTER = COUNTER + 1
         IF COUNTER > 999998 THEN COUNTER = 1
         GLA.ID = ACCT : STR("0",6-LEN(COUNTER)) : COUNTER
         READ DUMMY FROM GLA, GLA.ID ELSE DUMMY = ""
       WHILE DUMMY # "" DO REPEAT
       GLA.AMT = AMOUNTS<I,J>
       MATWRITE GLA.REC ON GLA, GLA.ID
6900 NEXT J
   NEXT I
   WRITE COUNTER ON CONTROL, CONO : "GLACOUNTER"
**** EOM.TRANS (2) ****
7000 ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING EOM.TRANS (2) FILE"
   ECD.ACTION = 5; CALL SCRN.EDIT
   SELECT EOM.TRANS
   WRITE "EOM.TRANS (2)" ON CONTROL , PROG.ID
   DATA = 1
   LOOP
     READNEXT ETR.ID ELSE DATA = 0
   WHILE DATA DO
     IF ETR.ID[1,3] # CONO THEN GOTO 7999
*T25975 v
     MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE
       RELEASE EOM.TRANS, ETR.ID
       GOTO 7999
     END
     CURR.PER = FR.CURR.PER
     MAT ETH.REC = MAT ETR.REC
     DONE = 0
     SEQ = 0
     ETH.ID = FIELD(ETR.ID,"*",1):"*":CURR.PER
     LOOP UNTIL DONE = 1 DO
       SEQ = SEQ + 1
       ETH.KEY = ETH.ID:"*":SEQ
       READ CHECKIT FROM PRS.EOM.TRANS.HIST, ETH.KEY ELSE DONE = 1
     REPEAT
     MATWRITE ETH.REC ON PRS.EOM.TRANS.HIST, ETH.KEY
*T25975 ^
     DELETE EOM.TRANS, ETR.ID
7999 REPEAT
   ECD.NUM = 14; SCV.REC(ECD.NUM)<1> = "NOW PROCESSING EOM.TRANS (3) FILE"
   WRITE "EOM.TRANS (3)" ON CONTROL, PROG.ID
   SELECT EOM.TRANS
   READNEXT ETR.ID ELSE
**** MDS ****
8000 SELECT MDS
     WRITE "MDS" ON CONTROL, PROG.ID
     DATA = 1
     LOOP
       READNEXT MDS.ID ELSE DATA = 0
     WHILE DATA DO
       IF MDS.ID[1,3] = CONO THEN
         DELETE MDS, MDS.ID
       END
8099 REPEAT
**** MPAYREG ****
8100 SELECT MPAYREG
     WRITE "MPAYREG" ON CONTROL, PROG.ID
     DATA = 1
     LOOP
       READNEXT MPR.ID ELSE DATA = 0
     WHILE DATA DO
       IF MPR.ID[1,3] = CONO THEN
         DELETE MPAYREG, MPR.ID
       END
8199 REPEAT
**** EMPLOYEE ****
8200 SELECT EMPLOYEE
     WRITE "EMPLOYEE" ON CONTROL, PROG.ID
     DATA = 1
     LOOP
       READNEXT EMP.ID ELSE DATA = 0
     WHILE DATA DO
       IF EMP.ID[1,3] # CONO THEN GOTO 8299
       MATREADU EMP.REC FROM EMPLOYEE, EMP.ID ELSE
         RELEASE EMPLOYEE, EMP.ID
         GOTO 8299
       END
       IF EMP.BONDS<1,3> + 0 = 0 THEN
         RELEASE EMPLOYEE, EMP.ID
         GOTO 8299
       END
       BD2 = EMP.BONDS<1,2>; BD3 = EMP.BONDS<1,3>
       LOOP WHILE BD3 < BD2 DO
         BD2 = BD2 - BD3
       REPEAT
       EMP.BONDS<1,2> = BD2
       MATWRITE EMP.REC ON EMPLOYEE, EMP.ID
8299 REPEAT
**** MTH-UNION ****
8300 SELECT MTH.UNION
     WRITE "MTH-UNION" ON CONTROL, PROG.ID
     DATA = 1
     LOOP
       READNEXT MUR.ID ELSE DATA = 0
     WHILE DATA DO
       IF MUR.ID[1,3] = CONO THEN
         DELETE MTH.UNION, MUR.ID
       END
8399 REPEAT
**** PAYTYPE, HEADER ****
8400 WRITE "PAYTYPE, HEADER" ON CONTROL, PROG.ID
     DELETE PAYTYPE, CONO : "HEADER"
**** DICT MTH-UNION WEEKS ****
8500 WRITE "DICT MTH-UNION, WEEKS" ON CONTROL, PROG.ID
     READ ITEM FROM DICT.MTH.UNION, CONO : "WEEKS" ELSE GOTO 8999
     FOR I = 1 TO 10
       ITEM<I> = ""
     NEXT I
     WRITE ITEM ON DICT.MTH.UNION, CONO : "WEEKS"
8999 
     FR.CURR.DATE = FR.NEXT.DATE
     FR.CURR.PER = FR.NEXT.PER
     FR.CLOSE.DATE = TODAY
     FR.NEXT.PER = ""; FR.NEXT.DATE = ""; FR.PRINT = ""
     MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
     DELETE CONTROL, PROG.ID
     ECD.ACTION = 99 ; CALL SCRN.EDIT
     PC.PORT.TYPE = SAVE.PC.PORT.TYPE
     GOTO 99999
   END
   FR.PRINT = "X"
   MATWRITE FISCAL.REC ON CONTROL, CONO:SYS.FISCAL
   ERRMSG = "E R R O R !!! CANNOT LOCATE ALL TRANSACTIONS"
   GOTO 93000
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 WRITEV ERRMSG ON CONTROL, PROG.ID,2
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99000 INBUFF<5> = "END"
   PROCWRITE INBUFF
*** END OF JOB ***
99999 END
