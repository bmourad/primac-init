*COPY>CPYLIB>COM1
**********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - PRSBP
* PROGRAM       - EMP.DED.IN
* BY            - WALID YAMOUT, CBA
* DATE          - 06/11/84
* DESCRIPTION   -
* LAST COMPILE - 651
**********************************************************************
*ENDDOC
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
DIM MISC.REC(11)
EQU MISC.DESC  TO MISC.REC(1)
*
*--- SERUP SYSTEM ERROR MESSAGES
*
    SYS.TYPE = 1
    CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN THE FILES
*
    OPEN '','PRS.SCREENS' TO M.SCREENS ELSE ERRMSG='PRS.SCREENS FILE IS MISSING'; GOTO 93000
    OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING"; GOTO 93000
    OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG="EMPLOYEE FILE IS MISSING"; GOTO 93000
    OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING"; GOTO 93000
    OPEN '','EMPLOYEE.XREF' TO EMPLOYEE.XREF ELSE ERRMSG="EMPLOYEE.XREF FILE IS MISSING"; GOTO 93000
    OPEN '','QTD-YTD' TO QTDYTD ELSE ERRMSG="QTD-YTD FILE IS MISSING"; GOTO 93000
    OPEN '','MISCDED' TO MISCDED ELSE ERRMSG="MISCDED FILE IS MISSING"; GOTO 93000
*
*--- GET COMPANY NUMBER
*
    MAT COMP.REC = ''
    CONO = ''
    CALL GET.CONO(CONO,MAT COMP.REC)
    IF CONO = "END" THEN GOTO 999999
*
*--- INITIALIZATION
*
   MAT EDIT.COM = ""
   TYP = 0
   CALL EDIT.SUB
   FILL = "#"
   MAT EDIT.COM.DRIVER = ""
   QTR = ""
   LOCID = ""
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = "EMP.DED.IN"
   ECD.ACTION=1;CALL SCRN.EDIT
100*
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION=2;CALL SCRN.EDIT
125*
   CODE = ""
   DESC = ""
   AMNT = ""
   TOTAL = ""
   TOT.QTR = ""
   TOT.YTD = ""
   SCV.REC(1)<1> = CO.NAME
   ECD.NUM=1;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(2)<1> = CONO
   ECD.NUM=2;ECD.ACTION=5;CALL SCRN.EDIT
150*
   IF LOCID # "" THEN
      PRINT @(22,22):"ENTER EMPLOYEE NUMBER OR END : ":LOCID:
      PRINT STR(CHAR(8),4):;INPUT VALUE
   END ELSE
      X=22;Y=22;TYP=1;PMSG="ENTER EMPLOYEE NUMBER OR END :";MAXL=4;O.R="R"
      CALL EDIT.SUB
   END
   IF VALUE = "END" THEN GOTO 999999
   IF VALUE = "????" THEN CALL LOCATOR (CONO,EMPLOYEE.XREF,EMPLOYEE,4,5,4,7,LOCID);GOTO 100
   IF VALUE = "" AND LOCID # "" THEN VALUE = LOCID; LOCID = ""
   EMP.NO = LEN(VALUE)
   IF EMP.NO > 4 THEN GOTO 200
   EMP.NO = STR("0",4-EMP.NO):VALUE
   MATREAD EMP.REC FROM EMPLOYEE, CONO:EMP.NO ELSE
     FOR I = 1 TO 25 ; PRINT @(22,22):" EMPLOYEE ":EMP.NO:" IS NOT ON THE MASTER FILE" ; NEXT I ; GOTO 150
   END
   ECD.NUM = 3;SCV.REC(3)<1> = EMP.NO; ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 4;SCV.REC(4)<1> = EMP.FRST.NAME:" ":EMP.LAST.NAME
   ECD.ACTION=5;CALL SCRN.EDIT
*--- PRINT THE DATA
   MATREAD QTD.REC FROM QTDYTD,CONO:EMP.NO ELSE MAT QTD.REC = ""
   CNT = COUNT(QTD.DED,VM) + (QTD.DED # "")
   IF CNT > 13 THEN CNT = 13
   FOR I = 1 TO CNT
      CODE<1,I> = QTD.DED<1,I>
      MATREAD MISC.REC FROM MISCDED, CONO:CODE<1,I> ELSE
        MAT MISC.REC = ""
        MISC.DESC = "??????"
      END
      DESC<1,I> = MISC.DESC
      FOR II = 1 TO 4
        AMNT<1,I,II> = QTD.AMNT<1,I,II>
        TOT.YTD<1,I> = TOT.YTD<1,I> + QTD.AMNT<1,I,II>
        TOT.QTR<1,II> = TOT.QTR<1,II> + QTD.AMNT<1,I,II>
      NEXT II
        TOTAL = TOTAL + TOT.YTD<1,I>
   NEXT I
   IF CNT = 13 THEN GOTO 175
   IF QTD.BONDS # "" THEN
      CNT = CNT + 1
      CODE<1,CNT> = "BD"
      DESC<1,CNT> = "BONDS"
      FOR I = 1 TO 4
       AMNT<1,CNT,I> = QTD.BONDS<1,I>
       TOT.QTR<1,I> = TOT.QTR<1,I> + QTD.BONDS<1,I>
       TOT.YTD<1,CNT> = TOT.YTD<1,CNT> + QTD.BONDS<1,I>
      NEXT I
     TOTAL = TOTAL + TOT.YTD<1,CNT>
   END
175*
   GOSUB 10000
200*
   X=22;Y=22;TYP=8;PMSG="DO YOU WANT TO MAKE ANY CHANGES ? (RTN/N):";O.R="O"
   CALL EDIT.SUB
   IF VALUE = "N" THEN
     ECD.ACTION = 6;CALL SCRN.EDIT ; GOTO 125
   END
   IF VALUE # "" THEN GOTO 200
300*
   X=22;Y=22;TYP=1;PMSG=" ENTER NUMBER OF QTR TO BE CHANGED , (A)DD OR (RTN):";O.R="O"
   VALDAT = "A":VM:1:VM:2:VM:3:VM:4
   CALL EDIT.SUB
   IF VALUE = "" THEN GOTO 400
   IF NUM(VALUE) AND VALUE # 0 THEN
      QTR = VALUE
      GOSUB 1000
      GOTO 300
   END ELSE
      GOSUB 2000
      GOTO 300
   END
400*
   X=22;Y=22;TYP=8;PMSG="IS THIS WHAT YOU WANT ? (RTN/N):";O.R="O"
   VALDAT = "";MAXL=1
   CALL EDIT.SUB
   IF VALUE = "N" THEN GOTO 300
   IF VALUE # "" THEN GOTO 400
***--- UPDATE
   QTD.DED = ""
   QTD.AMNT = ""
   QTD.BONDS = ""
   LOCATE "BD" IN CODE<1>,1 SETTING FND ELSE FND = 0
   IF FND THEN
     IF TOT.YTD<1,FND> # 0 THEN FOR LL = 1 TO 4;QTD.BONDS<1,LL> = AMNT<1,FND,LL>;NEXT LL
     CODE = DELETE(CODE,1,FND,0)
     AMNT = DELETE(AMNT,1,FND,0)
     TOT.YTD = DELETE(TOT.YTD,1,FND,0)
     CNT = CNT - 1
   END
   FND = 0
   FOR I = 1 TO CNT
     IF TOT.YTD<1,I> # 0 THEN
       FND = FND + 1
       QTD.DED<1,FND> = CODE<1,I>
       QTD.AMNT<1,FND> = AMNT<1,I>
     END
   NEXT I
   MATWRITE QTD.REC ON QTDYTD,CONO:EMP.NO
   GOTO 100
   GOTO 999999
*--- ROUTINE TO CHANGE
1000*
  BEGIN CASE
    CASE QTR = 1
      X = 20
    CASE QTR = 2
      X = 31
    CASE QTR = 3
      X = 42
    CASE QTR = 4
      X = 53
    CASE 1
     GOTO 1999
  END CASE
  BLK = 0
  FOR I = 1 TO CNT UNTIL BLK
1001 Y=I+6;TYP=4;SCALER=2;O.R="O";MAXL = 9
   MINV="";MAXV="";DEFAULT=OCONV(AMNT<1,I,QTR>, 'MD2')
   CALL EDIT.SUB
   IF VALUE = "END" THEN
     PRINT @(X,Y):OCONV(AMNT<1,I,QTR>, 'MD2') "R#8":
     BLK = 1
   END ELSE
     IF VALUE # AMNT<1,I,QTR> THEN
        TOT.YTD<1,I> = TOT.YTD<1,I> - AMNT<1,I,QTR>
        TOT.QTR<1,QTR> = TOT.QTR<1,QTR> - AMNT<1,I,QTR>
        TOTAL = TOTAL - AMNT<1,I,QTR>
        AMNT<1,I,QTR> = VALUE
        TOT.YTD<1,I> = TOT.YTD<1,I> + AMNT<1,I,QTR>
        TOT.QTR<1,QTR> = TOT.QTR<1,QTR> + AMNT<1,I,QTR>
        TOTAL = TOTAL + AMNT<1,I,QTR>
        PRINT @(65,Y):OCONV(TOT.YTD<1,I>, 'MD2') "R#9":
        ECD.NUM = QTR + 11;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,QTR>
        ECD.ACTION = 5;CALL SCRN.EDIT
        ECD.NUM = 16;SCV.REC(ECD.NUM)<1> = TOTAL
        ECD.ACTION = 5;CALL SCRN.EDIT
     END
   END
  NEXT I
1999*
RETURN
*--- ROUTINE TO ADD CODE
2000*
    CNT = CNT + 1
    IF CNT > 13 THEN 
       CNT = 13
       GOTO 2999
    END
2001*
    X=2;Y=CNT+6;TYP=1;MAXL=2;MINV="";MAXV="";HMSG=""
    IF CODE<1,CNT> = "" THEN O.R = "R" ELSE O.R = "O";DEFAULT = CODE<1,CNT>
    CALL EDIT.SUB
    IF VALUE = "END" THEN
       CODE = DELETE(CODE,1,CNT,0)
       DESC = DELETE(DESC,1,CNT,0)
       AMNT = DELETE(AMNT,1,CNT,0)
       PRINT @(0,CNT+6):CL:
       CNT = CNT - 1
       GOTO 2999
    END ELSE
       IF VALUE # CODE<1,CNT> THEN
         LOCATE VALUE IN CODE<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN GOTO 2001
         MAT MISC.REC = ""
         IF VALUE = "BD" THEN
            MISC.DESC = "BONDS"
         END ELSE
            MATREAD MISC.REC FROM MISCDED, CONO:VALUE ELSE GOTO 2001
         END
         CODE<1,CNT> = VALUE
         DESC<1,CNT> = MISC.DESC
         PRINT @(7,CNT+6):DESC<1,CNT> "L#11":
      END
    END
    X=20;Y=CNT+6;TYP=4;SCALER=2;O.R="O";MAXL = 9;QTR=1
    MINV="";MAXV="";DEFAULT=OCONV(AMNT<1,CNT,QTR>, 'MD2')
    CALL EDIT.SUB
    IF VALUE = "END" THEN GOTO 2001
    AMNT<1,CNT,QTR> = VALUE
    X=31;Y=CNT+6;TYP=4;SCALER=2;O.R="O";MAXL = 9;QTR=2
    MINV="";MAXV="";DEFAULT=OCONV(AMNT<1,CNT,QTR>, 'MD2')
    CALL EDIT.SUB
    IF VALUE = "END" THEN GOTO 2001
    AMNT<1,CNT,QTR> = VALUE
    X=42;Y=CNT+6;TYP=4;SCALER=2;O.R="O";MAXL = 9;QTR=3
    MINV="";MAXV="";DEFAULT=OCONV(AMNT<1,CNT,QTR>, 'MD2')
    CALL EDIT.SUB
    IF VALUE = "END" THEN GOTO 2001
    AMNT<1,CNT,QTR> = VALUE
    X=53;Y=CNT+6;TYP=4;SCALER=2;O.R="O";MAXL = 9;QTR=4
    MINV="";MAXV="";DEFAULT=OCONV(AMNT<1,CNT,QTR>, 'MD2')
    CALL EDIT.SUB
    IF VALUE = "END" THEN GOTO 2001
    AMNT<1,CNT,QTR> = VALUE
    FOR QTR = 1 TO 4
        TOT.YTD<1,CNT> = TOT.YTD<1,CNT> + AMNT<1,CNT,QTR>
        TOT.QTR<1,QTR> = TOT.QTR<1,QTR> + AMNT<1,CNT,QTR>
        TOTAL = TOTAL + AMNT<1,CNT,QTR>
    NEXT QTR
    PRINT @(65,Y):OCONV(TOT.YTD<1,CNT>, 'MD2') "R#9":
    FOR QTR = 1 TO 4
        ECD.NUM = QTR + 11;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,QTR>
        ECD.ACTION = 5;CALL SCRN.EDIT
    NEXT QTR
    ECD.NUM = 16;SCV.REC(ECD.NUM)<1> = TOTAL
    ECD.ACTION = 5;CALL SCRN.EDIT
2999*
   RETURN
*--- PRINT DEDUCTION VALUES
10000*
   FOR I = 1 TO CNT
     PRINT @(2,I+6):CODE<1,I> "L#2":
     PRINT @(7,I+6):DESC<1,I> "L#11":
     PRINT @(21,I+6):OCONV(AMNT<1,I,1>, 'MD2') "R#8":
     PRINT @(32,I+6):OCONV(AMNT<1,I,2>, 'MD2') "R#8":
     PRINT @(43,I+6):OCONV(AMNT<1,I,3>, 'MD2') "R#8":
     PRINT @(54,I+6):OCONV(AMNT<1,I,4>, 'MD2') "R#8":
     PRINT @(65,I+6):OCONV(TOT.YTD<1,I>, 'MD2') "R#9":
   NEXT I
   ECD.NUM = 12;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,1>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 13;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,2>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 14;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,3>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 15;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,4>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 16;SCV.REC(ECD.NUM)<1> = TOTAL
   ECD.ACTION = 5;CALL SCRN.EDIT
RETURN
*
*--- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINT @(-1):
   END
