*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*   SYSTEM        - PRS
*   PROGRAM       - DED.YTDQTDP
*   DATE          - 05/24/84
*   BY            - WALID YAMOUT , CBA
*   DESCRIPTION   -
*********************************************************************
*ENDDOC
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PRS.CPYLIB>MISCDED
*COPY>CPYLIB>CHAR
    DIM A(35)
***   INITIALIZATION
    LINE.COUNT        = 99
    PAGE.NO           = 0
    CONO = ""
    MAT COMP.REC = ""
    SP2 = SPACE(2)
    D10 = STR('-',10)
    D30 = STR('-',30)
    D11 = STR('-',11)
    D9 = STR('-',9)
    D2 = STR('-',2)
    D4 = STR('-',4)
***   OPEN THE FILE
    OPEN '','COMPANY' TO COMPANY ELSE
       PRINT 'COMPANY FILE IS MISSING'
       STOP
    END
    OPEN '','EMPLOYEE' TO EMPLOYEE ELSE
       PRINT 'EMPLOYEE FILE IS MISSING'
       STOP
    END
    OPEN '','MISCDED' TO MISCDED ELSE
       PRINT 'MISCDED FILE IS MISSING'
       STOP
    END
    OPEN '','QTD-YTD' TO QTDYTD ELSE
       PRINT 'QTD-YTD FILE IS MISSING'
       STOP
    END
    HH.LINE = "EMP#" :SP2: "EMPLOYEE NAME                 " :SP2: "CD" :SP2
    HH.LINE = HH.LINE: "DESCRIPTION" :SP2: "  QTR-1  " :SP2: "  QTR-2  "
    HH.LINE = HH.LINE:SP2: "  QTR-3  " :SP2: "  QTR-4  " :SP2: "  Y-T-D"
    DH.LINE = D4 :SP2: D30 :SP2: D2 :SP2: D11 :SP2: D9 :SP2: D9 :SP2: D9 :SP2: D9 :SP2: D10
***   PUT PRINTER ON
    PRINTER ON
***   READ AND PROCESS ALL THE RECORDS
100*
    MAT QTD.REC = ""
    MAT EMP.REC = ""
    MAT MDED.REC = ""
    YTD = ""
    FLAG = "Y"
    READNEXT ID ELSE GOTO 999999
    IF CONO # ID[1,3] THEN
       CONO = ID[1,3]
       MATREAD COMP.REC FROM COMPANY,CONO ELSE GOTO 100
       LINE.COUNT = 99
       POS1 = 51 - INT(LEN(CO.NAME)/2)
       POS2 = 26 + INT(LEN(CO.NAME)/2)
    END
***   PROCESS THE FILE
    MATREAD QTD.REC FROM QTDYTD,ID ELSE GOTO 100
    MATREAD EMP.REC FROM EMPLOYEE, ID ELSE GOTO 100
    EMP.NAME = EMP.FRST.NAME:" ":EMP.LAST.NAME
    DED.CNT = COUNT(QTD.DED,VM) + (QTD.DED # "")
    FOR I = 1 TO DED.CNT
       MATREAD MDED.REC FROM MISCDED,CONO:QTD.DED<1,I> ELSE
          MAT MDED.REC = ""
          MDED.SCRN.DESC="??????"
       END
       FOR II = 1 TO 4
          YTD<1,I> = YTD<1,I> + QTD.AMNT<1,I,II>
       NEXT II
       GOSUB 20000
    NEXT I
    IF QTD.BONDS # "" THEN
       I = I + 1
       FOR II = 1 TO 4
          YTD<1,I> = YTD<1,I> + QTD.BONDS<1,II>
       NEXT II
       GOSUB 21000
    END ELSE
       IF LINE.COUNT + 2 > 56 THEN
          GOSUB 80000
       END ELSE
          PRINT
          PRINT
          LINE.COUNT = LINE.COUNT + 2
       END
    END
    GOTO 100
***  ROUTINE TO PRINT THE RECORD
20000*
    IF LINE.COUNT > 56 THEN GOSUB 80000
    LINE.COUNT = LINE.COUNT + 1
    H.LINE = ""
    IF FLAG = "Y" THEN
       H.LINE = ID[4,4] "L#4" :SP2: EMP.NAME "L#30" :SP2
       FLAG = "N"
    END ELSE
       H.LINE = SPACE(38)
    END
    H.LINE = H.LINE : QTD.DED<1,I> "L#2" :SP2: MDED.SCRN.DESC "L#11" :SP2
    FOR II = 1 TO 4
       H.LINE = H.LINE : OCONV(QTD.AMNT<1,I,II>, 'MD2') "R#9" :SP2
    NEXT II
    H.LINE = H.LINE : OCONV(YTD<1,I>, 'MD2') "R#10"
    PRINT H.LINE
    RETURN
21000*
    IF LINE.COUNT > 56 THEN GOSUB 80000
    LINE.COUNT = LINE.COUNT + 1
    H.LINE = ""
    IF FLAG = "Y" THEN
       H.LINE = ID[4,4] "L#4" :SP2: EMP.NAME "L#30" :SP2
       FLAG = "N"
    END ELSE
       H.LINE = SPACE(38)
    END
    H.LINE = H.LINE : "BD  BONDS      " :SP2
    FOR II = 1 TO 4
       H.LINE = H.LINE : OCONV(QTD.BONDS<1,II>, 'MD2') "R#9" :SP2
    NEXT II
    H.LINE = H.LINE : OCONV(YTD<1,I>, 'MD2') "R#10"
    PRINT H.LINE
    IF LINE.COUNT + 2 > 56 THEN
       GOSUB 80000
    END ELSE
       PRINT
       PRINT
       LINE.COUNT = LINE.COUNT + 2
    END
    RETURN
***  ROUTINE TO PRINT HEADINGS
80000*
    PRINT CHAR(12)
    PAGE.NO = PAGE.NO + 1
    H.LINE = 'DATE : ' : OCONV(DATE(), 'D2/') "L#8"
    H.LINE = H.LINE : SPACE(POS1) : CO.NAME "L#30"
    H.LINE = H.LINE : SPACE(POS2) : 'PAGE ' : PAGE.NO "R#3"
    PRINT H.LINE
    PRINT SPACE(51) : "EMPLOYEE YTD AND QTD DEDUCTIONS"
    PRINT
    PRINT HH.LINE
    PRINT DH.LINE
    LINE.COUNT = 5
    RETURN
999999*
    PRINTER OFF
 END
