   SUBROUTINE W2.COMPANY.MAINT2(MAT EFR.REC)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*********************************************************************
* REVISION    - [08.0]
* PROGRAM     - W2.COMPANY.MAINT
* DATE        - 11/13/2001
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DESCRIPTION - W2 ELECTRONIC FILING, COMPANY/SUBMITTER/EMPLOYER 
*               MAINTENANCE.  SCREEN 2 OF 3
*T26277 cm 11/13/2001 * Initial Program Creation.
*********************************************************************
*
*** INSERT EQUATE FILE FROM CPYLIB
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>COMPANY
*COPY>PRS.CPYLIB>ELECTRONIC.FILING.REPORT
*** SET UP ERRMSG ROUTINE FOR SYSCOM
   SYS.TYPE = 1
   RO.CNTR = ''
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** SET UP SCREEN
*
100 *
   ECD.SCRN.NO = 2
   ECD.ACTION = 2; CALL SCRN.EDIT
   GOSUB 62000
   IF EFR.CONTACT.NAME # '' THEN
      SCV.REC(1)<2> = EFR.CONTACT.NAME
      SCV.REC(2)<2> = EFR.CONTACT.PHONE
      SCV.REC(3)<2> = EFR.CONTACT.EXT
      SCV.REC(4)<2> = EFR.CONTACT.EMAIL
      SCV.REC(5)<2> = EFR.CONTACT.FAX
      SCV.REC(6)<2> = EFR.NOTIFY.CODE
      SCV.REC(7)<2> = EFR.PREPARER.CODE
      SCV.REC(13)<2> = EFR.PIN
      SCV.REC(10)<2> = EFR.RESUBMIT.FLAG
      SCV.REC(12)<2> = EFR.RESUBMIT.TLCN
      GOSUB 200
      GOSUB 210
      GOSUB 220
      GOSUB 63000
   END ELSE
      FOR OPTION = 1 TO 9 WHILE ECD.RET.VALUE # "END"
         ON OPTION GOSUB 101,102,103,104,105,106,107,108,109
      NEXT OPTION
   END
*** OPTION ROUTINE
   MORE = 1
   LOOP
      ECD.NUM = 20
      SCV.REC(ECD.NUM)<2> = ""
      GOSUB 64000
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "E" OR OPTION = "END"
            MORE = 0
         CASE OPTION = "N"
            GOTO 300
         CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 11
            ON OPTION GOSUB 101,102,103,104,105,106,107,108,109,110
         CASE 1
            ERRMSG = "* * INVALID SELECTION * *"; GOSUB 91000
      END CASE
   WHILE MORE DO REPEAT
   GOTO 99999
101 ECD.NUM = 1; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN EFR.CONTACT.NAME = ECD.RET.VALUE
   RETURN
102 ECD.NUM = 2; GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN EFR.CONTACT.PHONE = ECD.RET.VALUE
   RETURN
103 ECD.NUM = 3; GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN EFR.CONTACT.EXT = ECD.RET.VALUE
   RETURN
104 ECD.NUM = 4; GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN EFR.CONTACT.EMAIL = ECD.RET.VALUE
   RETURN
105 ECD.NUM = 5; GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN EFR.CONTACT.FAX = ECD.RET.VALUE
   RETURN
106 ECD.NUM = 6; GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN
      EFR.NOTIFY.CODE = ECD.RET.VALUE
      GOSUB 200
      ECD.NUM = 8
      GOSUB 65000
   END
   RETURN
107 ECD.NUM = 7; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN
      EFR.PREPARER.CODE = ECD.RET.VALUE
      GOSUB 210
      ECD.NUM = 9
      GOSUB 65000
   END
   RETURN
108 ECD.NUM = 13; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN EFR.PIN = ECD.RET.VALUE
   RETURN
109 ECD.NUM = 10; GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN
      EFR.RESUBMIT.FLAG = ECD.RET.VALUE
      IF EFR.RESUBMIT.FLAG = 0 THEN EFR.RESUBMIT.TLCN = ''
      GOSUB 220
      ECD.NUM = 11
      GOSUB 65000
      IF EFR.RESUBMIT.FLAG = 0 THEN
         EFR.RESUBMIT.TLCN = ''
         ECD.NUM = 12
         SCV.REC(ECD.NUM)<2> = EFR.RESUBMIT.TLCN
         GOSUB 65000
      END
   END
110 *
   IF EFR.RESUBMIT.FLAG # 1 THEN RETURN
   ECD.NUM = 12
   GOSUB 64000
   IF ECD.RET.VALUE # 'END' THEN EFR.RESUBMIT.TLCN = ECD.RET.VALUE
   RETURN
200 *
   *** GET TYPE OF NOTIFICATION ***
   BEGIN CASE
      CASE EFR.NOTIFY.CODE = 1
         NOTIFY.DISPLAY = 'E-Mail/Internet'
      CASE EFR.NOTIFY.CODE = 2
         NOTIFY.DISPLAY = 'FAX'
      CASE EFR.NOTIFY.CODE = 3
         NOTIFY.DISPLAY = 'Postal Service'
      CASE 1
         NOTIFY.DISPLAY = ''
   END CASE
   SCV.REC(8)<2> = NOTIFY.DISPLAY
   RETURN
210 *
   *** GET PREPARER TYPE ***
   BEGIN CASE
      CASE EFR.PREPARER.CODE = 'A'
         PREPARER.DISPLAY = 'Accounting Firm'
      CASE EFR.PREPARER.CODE = 'L'
         PREPARER.DISPLAY = 'Self-Prepared'
      CASE EFR.PREPARER.CODE = 'S'
         PREPARER.DISPLAY = 'Service Bureau'
      CASE EFR.PREPARER.CODE = 'P'
         PREPARER.DISPLAY = 'Parent Company'
      CASE EFR.PREPARER.CODE = 'O'
         PREPARER.DISPLAY = 'Other'
      CASE 1
         PREPARER.DISPLAY = ''
   END CASE
   SCV.REC(9)<2> = PREPARER.DISPLAY
   RETURN
220 *
     *** SUBMITTER FLAG TYPE ***
   BEGIN CASE
      CASE EFR.RESUBMIT.FLAG = '0'
         RESUBMIT.DISPLAY = 'New Filing'
      CASE EFR.RESUBMIT.FLAG = '1'
         RESUBMIT.DISPLAY = 'Resubmmision'
      CASE 1
         RESUBMIT.DISPLAY = ''
   END CASE
   SCV.REC(11)<2> = RESUBMIT.DISPLAY
   RETURN
300 *
   CALL W2.COMPANY.MAINT3(MAT EFR.REC)
   GOTO 100
*** CALLS FOR SCRN.EDIT
61000 ECD.ACTION = 1; CALL SCRN.EDIT
   RETURN
62000 ECD.ACTION = 2; CALL SCRN.EDIT
   RETURN
63000 ECD.ACTION = 3; CALL SCRN.EDIT
   RETURN
64000 ECD.ACTION = 4; CALL SCRN.EDIT
   RETURN
65000 ECD.ACTION = 5; CALL SCRN.EDIT
   RETURN
*
*** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
   RETURN
END
