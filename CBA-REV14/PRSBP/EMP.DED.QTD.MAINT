*COPY>CPYLIB>COM1
**********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - PRSBP
* PROGRAM       - EMP.DED.QTD.MAINT
* BY            - WALID YAMOUT, CBA
* DATE          - 05/17/84
* DESCRIPTION   -
* LAST COMPILE - 584
**********************************************************************
*ENDDOC
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>QTD-YTD
DIM MISC.REC(20)
EQU MISC.DESC  TO MISC.REC(1)
*
*--- SERUP SYSTEM ERROR MESSAGES
*
    SYS.TYPE = 1
    CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN THE FILES
*
    OPEN '','PRS.SCREENS' TO M.SCREENS ELSE ERRMSG="M.SCREENS FILE IS MISSING"; GOTO 93000
    OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING"; GOTO 93000
    OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG="EMPLOYEE FILE IS MISSING"; GOTO 93000
    OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING"; GOTO 93000
    OPEN '','EMPLOYEE.XREF' TO EMPLOYEE.XREF ELSE ERRMSG="EMPLOYEE.XREF FILE IS MISSING"; GOTO 93000
    OPEN '','QTD-YTD' TO QTDYTD ELSE ERRMSG="QTD-YTD FILE IS MISSING"; GOTO 93000
    OPEN '','MISCDED' TO MISCDED ELSE ERRMSG="MISCDED FILE IS MISSING"; GOTO 93000
*
*--- GET COMPANY NUMBER
*
    MAT COMP.REC = ''
    CONO = ''
    CALL GET.CONO(CONO,MAT COMP.REC)
    IF CONO = "END" THEN GOTO 999999
*
*--- INITIALIZATION
*
   MAT EDIT.COM = ""
   TYP = 0
   CALL EDIT.SUB
   FILL = "#"
   MAT EDIT.COM.DRIVER = ""
   QTR = ""
   LOCID = ""
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = "EMP.DED.QTD.MAINT"
   ECD.ACTION=1;CALL SCRN.EDIT
100*
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION=2;CALL SCRN.EDIT
125*
   CODE = ""
   DESC = ""
   AMNT = ""
   TOTAL = ""
   TOT.QTR = ""
   TOT.YTD = ""
   E.MSC.CODE = ""
   SCV.REC(1)<1> = CO.NAME
   ECD.NUM=1;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(2)<1> = CONO
   ECD.NUM=2;ECD.ACTION=5;CALL SCRN.EDIT
150*
   IF LOCID # "" THEN
      PRINT @(22,21):"ENTER EMPLOYEE NUMBER OR END : ":LOCID:
      PRINT STR(CHAR(8),4):;INPUT VALUE
   END ELSE
      X=22;Y=21;TYP=1;PMSG="ENTER EMPLOYEE NUMBER OR END :";MAXL=4;O.R="R"
      CALL EDIT.SUB
   END
   IF VALUE = "END" THEN GOTO 999999
   IF VALUE = "????" THEN CALL LOCATOR (CONO,EMPLOYEE.XREF,EMPLOYEE,4,5,4,7,LOCID);GOTO 100
   IF VALUE = "" AND LOCID # "" THEN VALUE = LOCID; LOCID = ""
   EMP.NO = LEN(VALUE)
   IF EMP.NO > 4 THEN GOTO 200
   EMP.NO = STR("0",4-EMP.NO):VALUE
   MATREAD EMP.REC FROM EMPLOYEE, CONO:EMP.NO ELSE
     FOR I = 1 TO 25 ; PRINT @(22,21):" EMPLOYEE ":EMP.NO:" IS NOT ON THE MASTER FILE" ; NEXT I ; GOTO 150
   END
   ECD.NUM = 3;SCV.REC(3)<1> = EMP.NO; ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 4;SCV.REC(4)<1> = EMP.FRST.NAME:" ":EMP.LAST.NAME
   ECD.ACTION=5;CALL SCRN.EDIT
   CNT = 0
   FOR I = 1 TO 9
     IF EMP.REC(I+29) # "" THEN
       CNT = CNT + 1
       E.MSC.CODE<1,CNT> = EMP.REC(I+29)<1,1>
     END
   NEXT I
*--- PRINT THE DATA
   MATREAD QTD.REC FROM QTDYTD,CONO:EMP.NO ELSE MAT QTD.REC = ""
   FOR I = 1 TO CNT 
      CODE<1,I> = E.MSC.CODE<1,I>
      MATREAD MISC.REC FROM MISCDED, CONO:CODE<1,I> ELSE
        MAT MISC.REC = ""
        MISC.DESC = "??????"
      END
      DESC<1,I> = MISC.DESC
      LOCATE CODE<1,I> IN QTD.DED<1>,1 SETTING FOND ELSE
        FOND = COUNT(QTD.DED,VM) + (QTD.DED # "") + 1
        QTD.AMNT<1,FOND> = ""
        QTD.DED<1,FOND> = CODE<1,I>
      END
      FOR II = 1 TO 4
        AMNT<1,I,II> = QTD.AMNT<1,FOND,II>
        TOT.YTD<1,I> = TOT.YTD<1,I> + QTD.AMNT<1,FOND,II>
        TOT.QTR<1,II> = TOT.QTR<1,II> + QTD.AMNT<1,FOND,II>
      NEXT II
        TOTAL = TOTAL + TOT.YTD<1,I>
   NEXT I
   IF EMP.BONDS # "" THEN
      CNT = CNT + 1
      CODE<1,CNT> = "BD"
      DESC<1,CNT> = "BONDS"
      FOR I = 1 TO 4
       AMNT<1,CNT,I> = QTD.BONDS<1,I>
       TOT.QTR<1,I> = TOT.QTR<1,I> + QTD.BONDS<1,I>
       TOT.YTD<1,CNT> = TOT.YTD<1,CNT> + QTD.BONDS<1,I>
      NEXT I
     TOTAL = TOTAL + TOT.YTD<1,CNT>
   END
   GOSUB 10000
200*
   X=22;Y=21;TYP=8;PMSG="DO YOU WANT TO MAKE ANY CHANGES ? (RTN/N):";O.R="O"
   CALL EDIT.SUB
   IF VALUE = "N" THEN
     ECD.ACTION = 6;CALL SCRN.EDIT ; GOTO 125
   END
   IF VALUE # "" THEN GOTO 200
300*
   X=22;Y=21;TYP=3;PMSG=" ENTER NUMBER OF QTR TO BE CHANGED OR (RTN):";O.R="O"
   VALDAT = 1:VM:2:VM:3:VM:4
   CALL EDIT.SUB
   IF VALUE = "" THEN GOTO 400
   IF NUM(VALUE) AND VALUE # 0 THEN
      QTR = VALUE
      GOSUB 1000
      GOTO 300
   END
400*
   X=22;Y=21;TYP=8;PMSG="IS THIS WHAT YOU WANT ? (RTN/N):";O.R="O"
   VALDAT = "";MAXL=1
   CALL EDIT.SUB
   IF VALUE = "N" THEN GOTO 300
   IF VALUE # "" THEN GOTO 400
***--- UPDATE
   IF CODE<1,CNT> = "BD" THEN
     FOR LL = 1 TO 4;QTD.BONDS<1,LL> = AMNT<1,CNT,LL>;NEXT LL
      AMNT = DELETE(AMNT,1,CNT,0)
      CODE = DELETE(CODE,1,CNT,0)
      CNT = CNT - 1
   END
   FOR I = 1 TO CNT
      LOCATE CODE<1,I> IN QTD.DED<1>,1 SETTING FOND ELSE
        FOND = COUNT(QTD.DED,VM) + (QTD.DED # "") + 1
        QTD.DED<1,FOND> = CODE<1,I>
      END
      QTD.AMNT<1,FOND> = AMNT<1,I>
   NEXT I
   MATWRITE QTD.REC ON QTDYTD,CONO:EMP.NO
   GOTO 100
   GOTO 999999
*--- ROUTINE TO CHANGE
1000*
  BEGIN CASE
    CASE QTR = 1
      X = 20
    CASE QTR = 2
      X = 31
    CASE QTR = 3
      X = 42
    CASE QTR = 4
      X = 53
    CASE 1
     GOTO 1999
  END CASE
  BLK = 0
  FOR I = 1 TO CNT UNTIL BLK
1001 Y=I+7;TYP=4;SCALER=2;O.R="O";MAXL = 9
   MINV="";MAXV="";DEFAULT=OCONV(AMNT<1,I,QTR>, 'MD2')
   CALL EDIT.SUB
   IF VALUE = "END" THEN
     PRINT @(X,Y):OCONV(AMNT<1,I,QTR>, 'MD2') "R#8":
     BLK = 1
   END ELSE
     IF VALUE # AMNT<1,I,QTR> THEN
        TOT.YTD<1,I> = TOT.YTD<1,I> - AMNT<1,I,QTR>
        TOT.QTR<1,QTR> = TOT.QTR<1,QTR> - AMNT<1,I,QTR>
        TOTAL = TOTAL - AMNT<1,I,QTR>
        AMNT<1,I,QTR> = VALUE
        TOT.YTD<1,I> = TOT.YTD<1,I> + AMNT<1,I,QTR>
        TOT.QTR<1,QTR> = TOT.QTR<1,QTR> + AMNT<1,I,QTR>
        TOTAL = TOTAL + AMNT<1,I,QTR>
        PRINT @(65,Y):OCONV(TOT.YTD<1,I>, 'MD2') "R#9":
        ECD.NUM = QTR + 11;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,QTR>
        ECD.ACTION = 5;CALL SCRN.EDIT
        ECD.NUM = 16;SCV.REC(ECD.NUM)<1> = TOTAL
        ECD.ACTION = 5;CALL SCRN.EDIT
     END
   END
  NEXT I
1999*
RETURN
*--- PRINT DEDUCTION VALUES
10000*
   FOR I = 1 TO CNT
     PRINT @(2,I+7):CODE<1,I> "L#2":
     PRINT @(7,I+7):DESC<1,I> "L#11":
     PRINT @(21,I+7):OCONV(AMNT<1,I,1>, 'MD2') "R#8":
     PRINT @(32,I+7):OCONV(AMNT<1,I,2>, 'MD2') "R#8":
     PRINT @(43,I+7):OCONV(AMNT<1,I,3>, 'MD2') "R#8":
     PRINT @(54,I+7):OCONV(AMNT<1,I,4>, 'MD2') "R#8":
     PRINT @(65,I+7):OCONV(TOT.YTD<1,I>, 'MD2') "R#9":
   NEXT I
   ECD.NUM = 12;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,1>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 13;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,2>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 14;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,3>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 15;SCV.REC(ECD.NUM)<1> = TOT.QTR<1,4>
   ECD.ACTION = 5;CALL SCRN.EDIT
   ECD.NUM = 16;SCV.REC(ECD.NUM)<1> = TOTAL
   ECD.ACTION = 5;CALL SCRN.EDIT
RETURN
*
*--- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINT @(-1):
   END
