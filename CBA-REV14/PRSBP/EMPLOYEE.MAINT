*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.EMPLOYEE
*COPY>PRS.CPYLIB>COM.QTD-YTD
*COPY>PRS.CPYLIB>COM.MISCDED
*COPY>PRS.CPYLIB>COM.PRS.FILE.VARS
*COPY>PRS.CPYLIB>COM.EMPLOYEE.MAINT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - EMPLOYEE.MAINT
* BY          - WALID YAMOUT , C.B.A
* DATE        - 06/11/85
* MODIFIED    - 03/16/88 BY KERRY WILSON
* - 07/25/89]RRG]#14032]UPDATE MISCDED WITH FLAG
* DESCRIPTION -
*ENDDOC
*********************************************************************
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PRS.CPYLIB>PRS.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
   DIM S.EMP.REC(110)
   MAT S.EMP.REC = ""
*- INITALIZE EDIT.SUB
   MAT EDIT.COM = ""
   TYP = 0; CALL EDIT.SUB; FILL = "#"
   ERRMSG = ""
*- GET COMPANY
   MAT FILE.VARS = ""
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING'; GOSUB 91000; GOTO 999999
   CONO = ""
   MAT COMP.REC = ""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = "END" THEN GOTO 999999
*- GET INITIALS
   PRINT CS:
   TRAN.INT = ""
   OLD = ""
   NEW = ""
   X = 10; Y = 15; TYP = 1; MAXL = 3; PMSG = "ENTER YOUR INITIALS OR END:"
   CALL EDIT.SUB
   IF VALUE = "END" THEN GOTO 999999 ELSE TRAN.INT = VALUE
*- OPEN FILES
   OPEN '','EMPLOYEE' TO EMPLOYEE ELSE ERRMSG = 'EMPLOYEE FILE IS MISSING'; GOSUB 91000; GOTO 999999
   S.EMP.FILE = "Y"
   OPEN '','S.EMPLOYEE' TO S.EMPLOYEE ELSE
      S.EMP.FILE = "N"
   END
   OPEN '','DIVISION' TO DIVISION ELSE ERRMSG = 'DIVISION FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG = 'DEPARTMENT FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG = 'COST.CNTR FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','EMPTRANS' TO EMPTRANS ELSE ERRMSG = 'EMPTRANS FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','UNION' TO UNION ELSE ERRMSG = 'UNION FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','TAX2' TO TAX2 ELSE ERRMSG = 'TAX2 FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','PAYTYPE' TO PAYTYPE ELSE ERRMSG = 'PAYTYPE FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','MISCDED' TO MISCDED ELSE ERRMSG = 'MISCDED FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','QTD-YTD' TO QTD.YTD ELSE ERRMSG = 'QTD-YTD FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','PRS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PRS.SCREENS FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','KEYWORDS' TO KEYWORDS ELSE ERRMSG = 'KEYWORDS FILE IS MISSING'; GOSUB 91000; GOTO 999999
   OPEN '','INVEST.FUND.CODE' TO INVEST.FUND.CODE ELSE ERRMSG = 'INVEST.FUND.CODE FILE IS MISSING'; GOSUB 91000; GOTO 999999
   IF CO.EMP.XREF = "Y" THEN
      OPEN '','EMPLOYEE.XREF' TO EMPLOYEE.XREF ELSE ERRMSG = 'EMPLOYEE.XREF FILE IS MISSING'; GOSUB 91000; GOTO 999999
   END
*- SCREEN
   MAT EDIT.COM.DRIVER = ''
   ECD.SCRN.CNT = 3
   ECD.SCRN.NAME<1> = 'EMPLOYEE.MAINT'
   ECD.SCRN.NAME<2> = 'EMPLOYEE.MAINT.SUB'
   ECD.SCRN.NAME<3> = 'EMPLOYEE.MAINT.DED.SUB'
   ECD.ACTION=1;CALL SCRN.EDIT
*- MAIN PROCESS
100 *
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION=6;CALL SCRN.EDIT
*- ENTER EMPLOYEE NUMBER
200 *
   EMP.NO = ""; OLD.NAME = ""; NEW.FLG = "N"; TRAN.TYPE = "3" ; S.NEW.FLG = "N"
   ECD.NUM = 2
   ECD.ACTION=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 999999
      CASE ECD.RET.VALUE # ""
         MATREADU EMP.REC FROM EMPLOYEE, CONO:ECD.RET.VALUE ELSE
            EMP.NO = ECD.RET.VALUE
            NEW.FLG = "Y"
            IF S.EMP.FILE = "Y" THEN
               MATREADU EMP.REC FROM S.EMPLOYEE, CONO:ECD.RET.VALUE ELSE
                  S.NEW.FLG = "Y"
               END
            END ELSE
               S.NEW.FLG = "Y"
            END
            IF S.NEW.FLG = "Y" THEN
               MAT EMP.REC = ""
               MAT QTD.REC = ""
               FOR OPTION = 1 TO 22 UNTIL ECD.RET.VALUE = "END"
                  IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
                  IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000
               NEXT OPTION
               IF ECD.RET.VALUE = "END" THEN
                  RELEASE EMPLOYEE, CONO:EMP.NO
                  GOTO 100
               END
               GOTO 250
            END
         END
         EMP.NO = ECD.RET.VALUE
         OLD.NAME = EMP.LAST.NAME
         GOSUB 80000
         IF EMP.SOC.SEC = "" THEN
            NEW.FLG = "Y"
            S.NEW.FLG = "Y"
            FOR OPTION = 1 TO 22 UNTIL ECD.RET.VALUE = "END"
               IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
               IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000
            NEXT OPTION
         END
250 *
      CASE ECD.RET.VALUE = ""
         IF CO.EMP.XREF # "Y" THEN GOTO 200
         ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
         IF ECD.RET.VALUE = '' OR ECD.RET.VALUE = 'END' THEN GOTO 200
         MAT GEN.XREF.REC = ''
         GXR.CO = CONO
         GXR.HEADING<1,2> = 'LAST NAME'
         GXR.HEADING<1,3> = 'FIRST NAME'
         GXR.ATT<1,1> = 0
         GXR.ATT<1,2> = 4
         GXR.ATT<1,3> = 5
         GXR.LEN<1,1> = 4
         GXR.LEN<1,2> = 30
         GXR.LEN<1,3> = 30
         GXR.XREF = EMPLOYEE.XREF
         GXR.FILE = EMPLOYEE
         GXR.LOC = 1
         GXR.TOP.LINE<1,1> = "EMPLOYEE XREF SEARCH"
         GXR.TOP.LINE<1,2> = "FOR STRING : ":ECD.RET.VALUE
         GXR.HEADING<1,1> = 'EMP#'
         GXR.ID = ''
         GXR.SRCH.ID = ECD.RET.VALUE
         CALL GEN.XREF(MAT GEN.XREF.REC,KEYWORDS)
         ECD.ACTION=2;CALL SCRN.EDIT
         IF GXR.ID = '' THEN
            SCV.REC(3)<1> = ''
            ECD.ACTION=3;CALL SCRN.EDIT
            GOTO 200
         END ELSE
            MATREADU EMP.REC FROM EMPLOYEE, CONO:GXR.ID ELSE
               RELEASE EMPLOYEE, CONO:GXR.ID
               ERRMSG = 'EMPLOYEE NUMBER ':GXR.ID:' IS MISSING'
               GOSUB 91000
               SCV.REC(3)<1> = ''
               GOTO 200
            END
            EMP.NO = GXR.ID
            OLD.NAME = EMP.LAST.NAME
            SCV.REC(2)<1> = EMP.NO
            GOSUB 80000
            IF EMP.SOC.SEC = "" THEN
               NEW.FLG = "Y"
               S.NEW.FLG = "Y"
               FOR OPTION = 1 TO 22 UNTIL ECD.RET.VALUE = "END"
                  IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
                  IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000
               NEXT OPTION
            END
         END
   END CASE
*- ENTER OPTIONS
500 *
   ECD.NUM = 1
   SCV.REC(ECD.NUM)<1> = ''
   ECD.ACTION=4;CALL SCRN.EDIT
   OPTION = ECD.RET.VALUE
   BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
         RELEASE EMPLOYEE, CONO:EMP.NO
         GOTO 100
      CASE NUM(OPTION) AND NEW.FLG = "Y" AND S.NEW.FLG = "N" AND OPTION > 6
         ERRMSG = "Can't access this option for terminated employee after end of year process" ; GOSUB 91000
      CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 26
         IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
         IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000,24000,25000,26000
         IF ECD.RET.VALUE # "END" AND NEW.FLG = "N" THEN GOSUB 90000
      CASE OPTION = 'D'
         ECD.NUM = 45; SCV.REC(45)<ECD.SCRN.NO> = ""
         ECD.ACTION = 4; CALL SCRN.EDIT
         IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "N" THEN GOTO 500
         IF NEW.FLG = "Y" THEN GOTO 550
         GROSS = QTD.GROSS<1,1> + QTD.GROSS<1,2> + QTD.GROSS<1,3> + QTD.GROSS<1,4>
         IF GROSS # 0 THEN
            ERRMSG = "EMPLOYEE CANNOT BE DELETED UNTIL PRINTING W2"
            GOSUB 91000; GOTO 550
         END
         DELETE EMPLOYEE, CONO:EMP.NO
         DELETE QTD.YTD, CONO:EMP.NO
         TRAN.TYPE = "2"; GOSUB 90000
         IF CO.EMP.XREF = "Y" THEN
            CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,"",EMPLOYEE.XREF,KEYWORDS)
         END
550 *
         RELEASE EMPLOYEE, CONO:EMP.NO
         GOTO 100
      CASE OPTION = 'N' AND NEW.FLG = 'Y' AND S.NEW.FLG = "N"
         ERRMSG = "Can't access this option for terminated employee after end of year process" ; GOSUB 91000
      CASE OPTION = 'N'
         CALL EMPLOYEE.MAINT.SUB
         SCV.REC(41)<1> = EMP.REV.DATE<1,1>
         ECD.SCRN.NO = 1; ECD.ACTION = 3; CALL SCRN.EDIT
      CASE OPTION = "F" AND S.EMP.FILE = "N"
         MATWRITE EMP.REC ON EMPLOYEE, CONO:EMP.NO
         IF NEW.FLG = "Y" THEN
            TRAN.TYPE = "1"
            GOSUB 90000
         END
         IF CO.EMP.XREF = "Y" THEN
            CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,KEYWORDS)
         END
         GOTO 100
      CASE OPTION = 'F'
         BEGIN CASE
            CASE NEW.FLG = "N"
               MATWRITE EMP.REC ON EMPLOYEE, CONO:EMP.NO
               IF CO.EMP.XREF = "Y" THEN
                  CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,KEYWORDS)
               END
               EMP.FND = 1
               MATREADU S.EMP.REC FROM S.EMPLOYEE, CONO:EMP.NO ELSE
                  RELEASE S.EMPLOYEE, CONO:EMP.NO
                  MAT S.EMP.REC = ""
                  EMP.FND = 0
               END
               IF EMP.FND THEN
                  S.EMP.REC(4) = EMP.LAST.NAME
                  S.EMP.REC(5) = EMP.FRST.NAME
                  S.EMP.REC(7) = EMP.ADDR1
                  S.EMP.REC(8) = EMP.ADDR2
                  S.EMP.REC(9) = EMP.CITY
                  S.EMP.REC(10) = EMP.STATE
                  S.EMP.REC(11) = EMP.ZIP
                  S.EMP.REC(12) = EMP.PHONE
                  S.EMP.REC(13) = EMP.BIRTHDAY
                  MATWRITE S.EMP.REC ON S.EMPLOYEE, CONO:EMP.NO
               END
            CASE NEW.FLG = "Y" AND S.NEW.FLG = "N"
               MATWRITE EMP.REC ON S.EMPLOYEE, CONO:EMP.NO
            CASE NEW.FLG = 'Y' AND S.NEW.FLG = 'Y'
               MATWRITE EMP.REC ON EMPLOYEE, CONO:EMP.NO
               TRAN.TYPE = "1"
               GOSUB 90000
               IF CO.EMP.XREF = "Y" THEN
                  CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,KEYWORDS)
               END
         END CASE
** Update MISCDED with "Y" if code is used in EMPLOYEE file
         MCNT = DCOUNT(EMP.MSC.CODE,VM)
         FOR I = 1 TO MCNT
            IF EMP.MSC.AMT<1,I> > "0" THEN
               MDED.USAGE.FLAG = "Y"
               WRITEV MDED.USAGE.FLAG ON MISCDED,CONO:EMP.MSC.CODE<1,I>,16
            END
         NEXT I
*
         GOTO 100
   END CASE
   GOTO 500
*-------------*
* SUBROUTINES *
*-------------*
*- LAST AND FIRST NAME
1000 *
   ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 1999
   IF ECD.RET.VALUE = "" THEN GOTO 1000
   DESC = "LAST NAME"
   OLD = EMP.LAST.NAME
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   EMP.LAST.NAME = ECD.RET.VALUE
1500 *
   ECD.NUM = 4; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 1999
   IF ECD.RET.VALUE = "" THEN GOTO 1500
   DESC = "FIRST NAME"
   OLD = EMP.FRST.NAME
   NEW = ECD.RET.VALUE
   EMP.FRST.NAME = ECD.RET.VALUE
1999 *
   RETURN
*- TITLE
2000 *
   ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 2999
   DESC = "TITLE"
   OLD = EMP.TITLE
   NEW = ECD.RET.VALUE
   EMP.TITLE = ECD.RET.VALUE
2999 *
   RETURN
*- ADDRESS
3000 *
   ECD.NUM = 6; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 3999
   IF ECD.RET.VALUE = "" THEN GOTO 3000
   DESC = "ADDRESS 1"
   OLD = EMP.ADDR1
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   EMP.ADDR1 = ECD.RET.VALUE
3100 *
   ECD.NUM = 7; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 3999
   DESC = "ADRESS 2"
   OLD = EMP.ADDR2
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   EMP.ADDR2 = ECD.RET.VALUE
3500 *
   ECD.NUM = 8; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 3999
   IF ECD.RET.VALUE = "" THEN GOTO 3500
   DESC = "CITY"
   OLD = EMP.CITY
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   EMP.CITY = ECD.RET.VALUE
3700 *
   ECD.NUM = 9; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 3999
   IF ECD.RET.VALUE = "" THEN GOTO 3700
   DESC = "STATE"
   OLD = EMP.STATE
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   EMP.STATE = ECD.RET.VALUE
3900 *
   ECD.NUM = 10; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 3999
   IF ECD.RET.VALUE = "" THEN GOTO 3900
   DESC = "ZIP"
   OLD = EMP.ZIP
   NEW = ECD.RET.VALUE
   EMP.ZIP = ECD.RET.VALUE
3999 *
   RETURN
*- PHONE
4000 *
   ECD.NUM = 11; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 4999
   DESC = "PHONE"
   OLD = EMP.PHONE
   NEW = ECD.RET.VALUE
   EMP.PHONE = ECD.RET.VALUE
4999 *
   RETURN
*- BIRTHDAY
5000 *
   ECD.NUM = 12; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 5999
   DESC = "BIRTH DATE"
   OLD = OCONV(EMP.BIRTHDAY, "D2/")
   NEW = OCONV(ECD.RET.VALUE, "D2/")
   EMP.BIRTHDAY = ECD.RET.VALUE
5999 *
   RETURN
*- SOC.SEC
6000 *
   ECD.NUM = 13; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 6999
   DESC = "SOCIAL SECURITY"
   OLD = EMP.SOC.SEC
   NEW = ECD.RET.VALUE
   EMP.SOC.SEC = ECD.RET.VALUE
6999 *
   RETURN
*- DIVISION & DEPARTMENT
7000 *
   ECD.NUM = 46; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         MATREAD DIV.REC FROM DIVISION, CONO:EMP.DIV ELSE MAT DIV.REC = ""
         MATREAD DEPT.REC FROM DEPARTMENT, CONO:EMP.DEPT ELSE MAT DEPT.REC = ""
         IF EMP.COST.CNTR = "000" THEN
            MAT CCTR.REC = ""
            CCTR.DESC = "GENERAL COST CENTER"
         END ELSE
            MATREAD CCTR.REC FROM COST.CNTR, CONO:EMP.COST.CNTR ELSE MAT CCTR.REC = ""
         END
         ECD.NUM = 46; SCV.REC(46)<1> = EMP.DIV; ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 47; SCV.REC(47)<1> = DIV.DESC; ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 14; SCV.REC(14)<1> = EMP.DEPT; ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 15; SCV.REC(15)<1> = DEPT.DESC; ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 51; SCV.REC(51)<1> = EMP.COST.CNTR; ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 52; SCV.REC(52)<1> = CCTR.DESC; ECD.ACTION = 5; CALL SCRN.EDIT
         GOTO 7999
      CASE 1
         MATREAD DIV.REC FROM DIVISION, CONO:ECD.RET.VALUE ELSE
            MAT DIV.REC = ""
            ERRMSG = "DIVISION ":ECD.RET.VALUE:" IS NOT ON FILE"; GOSUB 91000
            GOTO 7000
         END
   END CASE
   DESC = "DIVISION"
   OLD = EMP.DIV
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   EMP.DIV = ECD.RET.VALUE
   ECD.NUM = 47; SCV.REC(47)<1> = DIV.DESC; ECD.ACTION = 5; CALL SCRN.EDIT
   DESC = "DEPARTMENT"
   OLD = EMP.DEPT
7500 *
   ECD.NUM = 14; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 7000
      CASE ECD.RET.VALUE = "???"
         MAT GEN.XREF.REC = ""
         GXR.XREF = DIVISION
         GXR.FILE = DEPARTMENT
         GXR.TOP.LINE = "DEPARTMENT XREF SEARCH"
         GXR.HEADING<1,1> = "DEPT"
         GXR.HEADING<1,2> = "DESCRIPTION"
         GXR.LEN<1,1> = 5
         GXR.LEN<1,2> = 30
         GXR.LOC = 2
         GXR.ATT<1,1> = 0
         GXR.ATT<1,2> = 2
         GXR.CO = CONO
         GXR.SRCH.ID = EMP.DIV
         GXR.ID = ""
         CALL GEN.XREF(MAT GEN.XREF.REC,KEYWORDS)
         ECD.ACTION = 2; CALL SCRN.EDIT
         IF GXR.ID = "" THEN
            SCV.REC(14)<1> = ""
            SCV.REC(15)<1> = ""
            ECD.ACTION = 3; CALL SCRN.EDIT
            GOTO 7500
         END ELSE
            MATREAD DEPT.REC FROM DEPARTMENT, CONO:GXR.ID ELSE
               MAT DEPT.REC = ""
               SCV.REC(14)<1> = ""; SCV.REC(15)<1> = ""
               ECD.ACTION = 3; CALL SCRN.EDIT
               ERRMSG = "DEPARTMENT ":GXR.ID:" IS NOT ON FILE"; GOSUB 91000; GOTO 7500
            END
            EMP.DEPT = GXR.ID
            SCV.REC(14)<1> = GXR.ID
            SCV.REC(15)<1> = DEPT.DESC
            ECD.ACTION = 3; CALL SCRN.EDIT
         END
      CASE (ECD.RET.VALUE MATCHES "2X") OR (ECD.RET.VALUE MATCHES '2X"-"2X')
         LOCATE ECD.RET.VALUE IN DIV.DEPT<1>,1 SETTING DFND ELSE
            ERRMSG = "DEPARTMENT ":ECD.RET.VALUE:" DOES NOT BELONG TO DIVISION ":EMP.DIV
            GOSUB 91000; GOTO 7500
         END
         MATREAD DEPT.REC FROM DEPARTMENT, CONO:ECD.RET.VALUE ELSE
            MAT DEPT.REC = ""
            ERRMSG = "DEPARTMENT ":ECD.RET.VALUE:" IS NOT ON FILE"
            GOSUB 91000; GOTO 7000
         END
         EMP.DEPT = ECD.RET.VALUE
         ECD.NUM = 15; SCV.REC(15)<ECD.SCRN.NO> = DEPT.DESC
         ECD.ACTION = 5; CALL SCRN.EDIT
      CASE 1
         ERRMSG = "INVALID INPUT"; GOSUB 91000; GOTO 7500
   END CASE
   NEW = EMP.DEPT
   IF NEW.FLG = 'N' THEN GOSUB 90000
   DESC='COST CENTER'
   OLD =EMP.COST.CNTR
7575 *
   ECD.NUM = 51; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 7000
      CASE ECD.RET.VALUE = "???"
         MAT GEN.XREF.REC = ""
         GXR.XREF = DEPARTMENT
         GXR.FILE = COST.CNTR
         GXR.TOP.LINE = "COST CENTER XREF SEARCH"
         GXR.HEADING<1,1> = "COST CNTR"
         GXR.HEADING<1,2> = "DESCRIPTION"
         GXR.LEN<1,1> = 9
         GXR.LEN<1,2> = 30
         GXR.LOC = 5
         GXR.ATT<1,1> = 0
         GXR.ATT<1,2> = 1
         GXR.CO = CONO
         GXR.SRCH.ID = EMP.DEPT
         GXR.ID = ""
         CALL GEN.XREF(MAT GEN.XREF.REC,KEYWORDS)
         ECD.ACTION = 2; CALL SCRN.EDIT
         IF GXR.ID = "" THEN
            SCV.REC(51)<1> = ""
            SCV.REC(52)<1> = ""
            ECD.ACTION = 3; CALL SCRN.EDIT
            GOTO 7575
         END ELSE
            MATREAD CCTR.REC FROM COST.CNTR, CONO:GXR.ID ELSE
               MAT CCTR.REC = ""
               SCV.REC(51)<1> = ""; SCV.REC(52)<1> = ""
               ECD.ACTION = 3; CALL SCRN.EDIT
               ERRMSG = "COST CENTER ":GXR.ID:" IS NOT ON FILE"; GOSUB 91000; GOTO 7575
            END
            EMP.COST.CNTR = GXR.ID
            SCV.REC(51)<1> = GXR.ID
            SCV.REC(52)<1> = CCTR.DESC
            ECD.ACTION = 3; CALL SCRN.EDIT
         END
      CASE ECD.RET.VALUE = "000"
         MAT CCTR.REC = ""
         CCTR.DESC = "GENERAL COST CENTER"
         EMP.COST.CNTR = ECD.RET.VALUE
         ECD.NUM = 52; SCV.REC(52)<ECD.SCRN.NO> = CCTR.DESC
         ECD.ACTION = 5; CALL SCRN.EDIT
      CASE (ECD.RET.VALUE MATCHES "3N")
         LOCATE ECD.RET.VALUE IN DEPT.CCTRS<1>,1 SETTING DFND ELSE
            ERRMSG = "COST CENTER ":ECD.RET.VALUE:" DOES NOT BELONG TO DEPARTMENT ":EMP.DEPT
            GOSUB 91000; GOTO 7575
         END
         MATREAD CCTR.REC FROM COST.CNTR, CONO:ECD.RET.VALUE ELSE
            MAT CCTR.REC = ""
            ERRMSG = "COST CENTER ":ECD.RET.VALUE:" IS NOT ON FILE"
            GOSUB 91000; GOTO 7000
         END
         EMP.COST.CNTR = ECD.RET.VALUE
         ECD.NUM = 52; SCV.REC(52)<ECD.SCRN.NO> = CCTR.DESC
         ECD.ACTION = 5; CALL SCRN.EDIT
      CASE 1
         ERRMSG = "INVALID INPUT"; GOSUB 91000; GOTO 7575
   END CASE
   NEW = EMP.COST.CNTR
7999 *
   RETURN
*- MARITAL STATUS AND EIC
8000 *
   ECD.NUM = 16; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 8999
   DESC = "MARITAL STATUS"
   OLD = EMP.MAR.ST
   NEW = ECD.RET.VALUE
   EMP.MAR.ST = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
8500 *
   ECD.NUM = 17; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 8999
   IF ECD.RET.VALUE # "" THEN
      READ TAX2.REC FROM TAX2, CONO:ECD.RET.VALUE ELSE
         ERRMSG = "TAX2 ":ECD.RET.VALUE:" IS MISSING"
         GOSUB 91000; GOTO 8500
      END
   END
   DESC = "EIC"
   OLD = EMP.EIC
   NEW = ECD.RET.VALUE
   EMP.EIC = ECD.RET.VALUE
8999 *
   RETURN
*- # DEPENDENT FED & STATE
9000 *
   ECD.NUM = 18; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 9999
   DESC = "# DEPENDENTS FED"
   OLD = EMP.DEP.NO<1,1>
   NEW = ECD.RET.VALUE
   EMP.DEP.NO<1,1> = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   ECD.NUM = 19; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 9999
   DESC = "# DEPENDENTS ST"
   OLD = EMP.DEP.NO<1,2>
   NEW = ECD.RET.VALUE
   EMP.DEP.NO<1,2> = ECD.RET.VALUE
9999 *
   RETURN
*- ADDITIONAL FED & ST TAX
10000 *
   ECD.NUM = 20; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 10999
   DESC = "ADDITIONAL FED TAX"
   OLD = OCONV(EMP.ADD.WHT<1,1>, "MD2")
   NEW = OCONV(ECD.RET.VALUE, "MD2")
   EMP.ADD.WHT<1,1> = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 10999
   DESC = "ADDITIONAL ST TAX"
   OLD = OCONV(EMP.ADD.WHT<1,2>, "MD2")
   NEW = OCONV(ECD.RET.VALUE, "MD2")
   EMP.ADD.WHT<1,2> = ECD.RET.VALUE
10999 *
   RETURN
*- FEDERAL TAX OVER WRITE
11000 *
   TEMP = EMP.FIT.CALC
   IF EMP.FIT.CALC<1,2> # "" THEN GOTO 11500
   ECD.NUM = 22; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 11800
      CASE ECD.RET.VALUE = ""
         EMP.FIT.CALC<1,1> = ""
         GOTO 11500
      CASE ECD.RET.VALUE # ""
         EMP.FIT.CALC = ECD.RET.VALUE
   END CASE
   GOTO 11800
11500 *
   ECD.NUM = 23; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 11800
      CASE ECD.RET.VALUE = ""
         EMP.FIT.CALC = DELETE(EMP.FIT.CALC,1,2,0)
         IF TEMP<1,2> # "" THEN GOTO 11000
      CASE ECD.RET.VALUE # ""
         EMP.FIT.CALC<1,2> = ECD.RET.VALUE
         EMP.FIT.CALC<1,1> = ""
   END CASE
11800 *
   IF TEMP<1,1> # "" THEN OLD = OCONV(TEMP<1,1>, "MD2"):"$"
   IF TEMP<1,2> # "" THEN OLD = OCONV(TEMP<1,2>, "MD2"):"%"
   IF EMP.FIT.CALC<1,1> # "" THEN NEW = OCONV(EMP.FIT.CALC<1,1>, "MD2"):"$"
   IF EMP.FIT.CALC<1,2> # "" THEN NEW = OCONV(EMP.FIT.CALC<1,2>, "MD2"):"%"
   DESC = "FED TAX OVER WRITE"
   IF ECD.RET.VALUE = "END" AND NEW.FLG = "N" THEN GOSUB 90000
11999 *
   RETURN
*- STATE TAX CODE
12000 *
   ECD.NUM = 24; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 12999
   IF ECD.RET.VALUE # "" THEN
      READ TAX2.REC FROM TAX2, CONO:ECD.RET.VALUE ELSE
         ERRMSG = "TAX2 ":ECD.RET.VALUE:" IS MISSING"
         GOSUB 91000
         ECD.NUM = 24; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = EMP.ST.CODE
         ECD.ACTION = 5; CALL SCRN.EDIT
         GOTO 12000
      END
   END
   DESC = "STATE TAX CODE"
   OLD = EMP.ST.CODE
   NEW = ECD.RET.VALUE
   EMP.ST.CODE = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   OLD = ""
   NEW = ""
   IF EMP.ST.CODE = "" THEN
      IF EMP.STATE.CALC # "" THEN
         IF EMP.STATE.CALC<1,1> # "" THEN
            OLD = OCONV(EMP.STATE.CALC<1,1>, "MD2"):"$"
         END ELSE
            OLD = OCONV(EMP.STATE.CALC<1,2>, "MD2"):"%"
         END
         NEW = ""
         DESC = "STATE TAX OVER WRITE"
         ECD.NUM = 49; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
         ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 50; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
         ECD.ACTION = 5; CALL SCRN.EDIT
         EMP.STATE.CALC = ""
      END
      GOTO 12999
   END
   TEMP = EMP.STATE.CALC
   IF EMP.STATE.CALC<1,2> # "" THEN GOTO 12500
12300 *
   ECD.NUM = 49; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
      CASE ECD.RET.VALUE = ""
         IF TEMP<1,2> # "" THEN
            EMP.STATE.CALC = ""
         END ELSE
            EMP.STATE.CALC<1,1> = ""
            GOTO 12500
         END
      CASE ECD.RET.VALUE # ""
         EMP.STATE.CALC = ECD.RET.VALUE
   END CASE
   GOTO 12800
12500 *
   ECD.NUM = 50; ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
      CASE ECD.RET.VALUE = ""
         IF TEMP<1,2> # "" THEN
            EMP.STATE.CALC = DELETE(EMP.STATE.CALC,1,2,0)
            GOTO 12300
         END ELSE
            EMP.STATE.CALC = ""
         END
      CASE ECD.RET.VALUE # ""
         EMP.STATE.CALC<1,1> = ""
         EMP.STATE.CALC<1,2> = ECD.RET.VALUE
   END CASE
12800 *
   IF TEMP<1,1> # "" THEN OLD = OCONV(TEMP<1,1>, "MD2"):"$"
   IF TEMP<1,2> # "" THEN OLD = OCONV(TEMP<1,2>, "MD2"):"%"
   IF EMP.STATE.CALC<1,1> # "" THEN NEW = OCONV(EMP.STATE.CALC<1,1>, "MD2"):"$"
   IF EMP.STATE.CALC<1,2> # "" THEN NEW = OCONV(EMP.STATE.CALC<1,2>, "MD2"):"%"
   DESC = "STATE TAX OVER WRITE"
   IF ECD.RET.VALUE = "END" AND NEW.FLG = "N" THEN GOSUB 90000
12999 *
   RETURN
*- CITY TAX CODE
13000 *
   ECD.NUM = 25; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 13999
   IF ECD.RET.VALUE # "" THEN
      READ TAX2.REC FROM TAX2, CONO:ECD.RET.VALUE ELSE
         ERRMSG = "TAX2 ":ECD.RET.VALUE:" IS MISSING"
         GOSUB 91000; GOTO 13000
      END
   END
   DESC = "CITY TAX CODE"
   OLD = EMP.CITY.CODE
   NEW = ECD.RET.VALUE
   EMP.CITY.CODE = ECD.RET.VALUE
13999 *
   RETURN
*- SHIFT
14000 *
   ECD.NUM = 26; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 15999
   DESC = "SHIFT"
   OLD = EMP.SHIFT
   NEW = ECD.RET.VALUE
   EMP.SHIFT = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
*- SEX
15000 *
   ECD.NUM = 27; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 15999
   DESC = "SEX"
   OLD = EMP.SEX
   NEW = ECD.RET.VALUE
   EMP.SEX = ECD.RET.VALUE
15999 *
   RETURN
*- PENSION
16000 *
   ECD.NUM = 28; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 16999
   DESC = "PENSION"
   OLD = EMP.PENSION
   NEW = ECD.RET.VALUE
   EMP.PENSION = ECD.RET.VALUE
16999 *
   RETURN
*- SICK FICA
17000 *
   ECD.NUM = 29; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 17999
   DESC = "SICK FICA"
   OLD = EMP.SK.FICA
   NEW = ECD.RET.VALUE
   EMP.SK.FICA = ECD.RET.VALUE
17999 *
   RETURN
*- CREDIT UNION
18000 *
   ECD.NUM = 30; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 18999
   IF ECD.RET.VALUE # "" THEN
      READ UNION.REC FROM UNION, CONO:ECD.RET.VALUE ELSE
         ERRMSG = "UNION ":ECD.RET.VALUE:" IS MISSING"
         GOSUB 91000; GOTO 18000
      END
   END
   DESC = "CREDIT UNION"
   OLD = EMP.UNION
   NEW = ECD.RET.VALUE
   EMP.UNION = ECD.RET.VALUE
18999 *
   RETURN
*- STATUS , PAY CYCLE AND (SALARY AND OT RATE) OR (RATE)
19000 *
   ECD.NUM = 31; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 19000
   DESC = "PAY STATUS"
   OLD = EMP.STATUS
   NEW = ECD.RET.VALUE
   EMP.STATUS = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
19300 *
   ECD.NUM = 32; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 19300
   DESC = "PAY CYCLE"
   OLD = EMP.PAY.CYCLE
   EMP.PAY.CYCLE = ECD.RET.VALUE
   NEW = ECD.RET.VALUE
   IF NEW.FLG = "N" THEN GOSUB 90000
   IF EMP.STATUS = "H" THEN GOTO 19700
   EMP.RATE = ""
   SCV.REC(34)<ECD.SCRN.NO> = ""
   ECD.NUM = 34; ECD.ACTION = 5; CALL SCRN.EDIT
19500 *
   ECD.NUM = 33; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 19500
   DESC = "SALARY"
   OLD = OCONV(EMP.SALARY, "MD2")
   EMP.SALARY = ECD.RET.VALUE
   NEW = OCONV(ECD.RET.VALUE, "MD2")
   IF NEW.FLG = "N" THEN GOSUB 90000
19600 *
   ECD.NUM = 48; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 19600
   DESC = "OT RATE"
   OLD = OCONV(EMP.OT.RATE, "MD4")
   EMP.OT.RATE = ECD.RET.VALUE
   NEW = OCONV(EMP.OT.RATE, "MD4")
   GOTO 19999
19700 *
   EMP.SALARY = ""
   EMP.OT.RATE = ""
   SCV.REC(33)<ECD.SCRN.NO> = ""
   SCV.REC(48)<ECD.SCRN.NO> = ""
   ECD.NUM = 33; ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 48; ECD.ACTION = 5; CALL SCRN.EDIT
19800 *
   ECD.NUM = 34; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 19800
   DESC = "RATE"
   OLD = OCONV(EMP.RATE, "MD4")
   EMP.RATE = ECD.RET.VALUE
   NEW = OCONV(ECD.RET.VALUE, "MD4")
19999 *
   RETURN
*- ALT.NO
20000 *
   ECD.NUM = 35; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 20999
   DESC = "EMPLOYEE ALT NUMBER"
   OLD = EMP.ALT.NO
   NEW = ECD.RET.VALUE
   EMP.ALT.NO = ECD.RET.VALUE
20999 *
   RETURN
*- MAX.SIC
21000 *
   ECD.NUM = 36; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 21999
   IF ECD.RET.VALUE - INT(SUM(QTD.SCK.HRS)/100 + 0.5) < 0 THEN
      ERRMSG = "AVAILABLE SICK HOURS IS LESS THAN ZERO"
      GOSUB 91000; GOTO 21000
   END
   DESC = "MAX SICK HOURS"
   OLD = EMP.MAX.SIC
   NEW = ECD.RET.VALUE
   EMP.MAX.SIC = ECD.RET.VALUE
   ECD.NUM = 37; SCV.REC(37)<ECD.SCRN.NO> = EMP.MAX.SIC - INT(SUM(QTD.SCK.HRS)/100 + 0.5)
   ECD.ACTION = 5; CALL SCRN.EDIT
21999 *
   RETURN
*- MAX.VAC
22000 *
   ECD.NUM = 38; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 22999
   IF ECD.RET.VALUE - INT(SUM(QTD.VAC.HRS)/100 + 0.5) < 0 THEN
      ERRMSG = "AVAILABLE VACATION HOURS IS LESS THAN ZERO"
      GOSUB 91000; GOTO 22000
   END
   DESC = "MAX VAC HOURS"
   OLD = EMP.MAX.VAC
   NEW = ECD.RET.VALUE
   EMP.MAX.VAC = ECD.RET.VALUE
   ECD.NUM = 39; SCV.REC(39)<ECD.SCRN.NO> = EMP.MAX.VAC - INT(SUM(QTD.VAC.HRS)/100 + 0.5)
   ECD.ACTION = 5; CALL SCRN.EDIT
22999 *
   RETURN
*- STRT.DATE
23000 *
   ECD.NUM = 40; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 23999
   DESC = "START DATE"
   OLD = OCONV(EMP.STRT.DATE, "D2/")
   NEW = OCONV(ECD.RET.VALUE, "D2/")
   EMP.STRT.DATE = ECD.RET.VALUE
23999 *
   RETURN
*- TERM.DATE
24000 *
   ECD.NUM = 42; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 24999
   DESC = "TERMINATED DATE"
   OLD = OCONV(EMP.TERM.DATE, "D2/")
   NEW = OCONV(ECD.RET.VALUE, "D2/")
   EMP.TERM.DATE = ECD.RET.VALUE
24999 *
   RETURN
*- REHR.DATE
25000 *
   ECD.NUM = 43; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 25999
   DESC = "REHIRED DATE"
   OLD = OCONV(EMP.REHR.DATE, "D2/")
   NEW = OCONV(ECD.RET.VALUE, "D2/")
   EMP.REHR.DATE = ECD.RET.VALUE
25999 *
   RETURN
*- DISB.DATE
26000 *
   ECD.NUM = 44; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 26999
   DESC = "DISABLED DATE"
   OLD = OCONV(EMP.DISB.DATE, "D2/")
   NEW = OCONV(ECD.RET.VALUE, "D2/")
   EMP.DISB.DATE = ECD.RET.VALUE
26999 *
   RETURN
*- MAT SCV.REC
80000 *
   MATREAD QTD.REC FROM QTD.YTD, CONO:EMP.NO ELSE MAT QTD.REC = ""
   MATREAD DIV.REC FROM DIVISION, CONO:EMP.DIV ELSE MAT DIV.REC = ""
   MATREAD DEPT.REC FROM DEPARTMENT, CONO:EMP.DEPT ELSE MAT DEPT.REC = ""
   IF EMP.COST.CNTR = "000" THEN
      MAT CCTR.REC = ""
      CCTR.DESC = "GENERAL COST CENTER"
   END ELSE
      MATREAD CCTR.REC FROM COST.CNTR, CONO:EMP.COST.CNTR ELSE MAT CCTR.REC = ""
   END
   SCV.REC(3)<ECD.SCRN.NO> = EMP.LAST.NAME
   SCV.REC(4)<ECD.SCRN.NO> = EMP.FRST.NAME
   SCV.REC(5)<ECD.SCRN.NO> = EMP.TITLE
   SCV.REC(6)<ECD.SCRN.NO> = EMP.ADDR1
   SCV.REC(7)<ECD.SCRN.NO> = EMP.ADDR2
   SCV.REC(8)<ECD.SCRN.NO> = EMP.CITY
   SCV.REC(9)<ECD.SCRN.NO> = EMP.STATE
   SCV.REC(10)<ECD.SCRN.NO> = EMP.ZIP
   SCV.REC(11)<ECD.SCRN.NO> = EMP.PHONE
   SCV.REC(12)<ECD.SCRN.NO> = EMP.BIRTHDAY
   SCV.REC(13)<ECD.SCRN.NO> = EMP.SOC.SEC
   SCV.REC(14)<ECD.SCRN.NO> = EMP.DEPT
   SCV.REC(15)<ECD.SCRN.NO> = DEPT.DESC
   SCV.REC(16)<ECD.SCRN.NO> = EMP.MAR.ST
   SCV.REC(17)<ECD.SCRN.NO> = EMP.EIC
   SCV.REC(18)<ECD.SCRN.NO> = EMP.DEP.NO<1,1>
   SCV.REC(19)<ECD.SCRN.NO> = EMP.DEP.NO<1,2>
   SCV.REC(20)<ECD.SCRN.NO> = EMP.ADD.WHT<1,1>
   SCV.REC(21)<ECD.SCRN.NO> = EMP.ADD.WHT<1,2>
   SCV.REC(22)<ECD.SCRN.NO> = EMP.FIT.CALC<1,1>
   SCV.REC(23)<ECD.SCRN.NO> = EMP.FIT.CALC<1,2>
   SCV.REC(24)<ECD.SCRN.NO> = EMP.ST.CODE
   SCV.REC(25)<ECD.SCRN.NO> = EMP.CITY.CODE
   SCV.REC(26)<ECD.SCRN.NO> = EMP.SHIFT
   SCV.REC(27)<ECD.SCRN.NO> = EMP.SEX
   SCV.REC(28)<ECD.SCRN.NO> = EMP.PENSION
   SCV.REC(29)<ECD.SCRN.NO> = EMP.SK.FICA
   SCV.REC(30)<ECD.SCRN.NO> = EMP.UNION
   SCV.REC(31)<ECD.SCRN.NO> = EMP.STATUS
   SCV.REC(32)<ECD.SCRN.NO> = EMP.PAY.CYCLE
   SCV.REC(33)<ECD.SCRN.NO> = EMP.SALARY
   SCV.REC(34)<ECD.SCRN.NO> = EMP.RATE
   SCV.REC(35)<ECD.SCRN.NO> = EMP.ALT.NO
   SCV.REC(36)<ECD.SCRN.NO> = EMP.MAX.SIC
   SCV.REC(37)<ECD.SCRN.NO> = EMP.MAX.SIC - INT(SUM(QTD.SCK.HRS)/100 + 0.5)
   SCV.REC(38)<ECD.SCRN.NO> = EMP.MAX.VAC
   SCV.REC(39)<ECD.SCRN.NO> = EMP.MAX.VAC - INT(SUM(QTD.VAC.HRS)/100 + 0.5)
   SCV.REC(40)<ECD.SCRN.NO> = EMP.STRT.DATE
   SCV.REC(41)<ECD.SCRN.NO> = EMP.REV.DATE<1,1>
   SCV.REC(42)<ECD.SCRN.NO> = EMP.TERM.DATE
   SCV.REC(43)<ECD.SCRN.NO> = EMP.REHR.DATE
   SCV.REC(44)<ECD.SCRN.NO> = EMP.DISB.DATE
   SCV.REC(46)<ECD.SCRN.NO> = EMP.DIV
   SCV.REC(47)<ECD.SCRN.NO> = DIV.DESC
   SCV.REC(48)<ECD.SCRN.NO> = EMP.OT.RATE
   SCV.REC(49)<ECD.SCRN.NO> = EMP.STATE.CALC<1,1>
   SCV.REC(50)<ECD.SCRN.NO> = EMP.STATE.CALC<1,2>
   SCV.REC(51)<ECD.SCRN.NO> = EMP.COST.CNTR
   SCV.REC(52)<ECD.SCRN.NO> = CCTR.DESC
   ECD.ACTION = 3; CALL SCRN.EDIT
   RETURN
*- CREATE EMPTRANS RECORD
90000 *
   IF TRAN.TYPE = "3" AND OLD = NEW THEN GOTO 90999
   READU SEQ FROM CONTROL, CONO:"EMPTRANS" ELSE SEQ = 0
   SEQ = SEQ + 1
   WRITE SEQ ON CONTROL, CONO:"EMPTRANS"
   REC = CONO :AM: EMP.NO :AM: TRAN.INT :AM: TRAN.TYPE
   IF TRAN.TYPE = "3" THEN REC = REC :AM: DESC :AM: OLD :AM: NEW
   WRITE REC ON EMPTRANS, CONO:SEQ
90999 *
   RETURN
*- ERROR ROUTINE
91000 *
   PRINT @(0,23):ERRMSG:CL:
   INPUT REPLY,1 :
   PRINT @(0,23):CL:
   RETURN
999999 PRINT @(-1):
END
