  SUBROUTINE JobTicket.elogic(METHOD, ARGS)
**********************************************
* REVISION    - [12.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - eLOGICBP
* PROGRAM     - JobTicket.elogic
* BY          - ABDULLAH JIBALY; CBA
* DATE        - 09/13/2002
* DESCRIPTION - BUSINESS LOGIC FOR JOB TICKETS
*
*ENDDOC
**********************************************
  ;*
  ;* Libraries and initialization
  ;*
*COPY>JIS.CPYLIB>SYS_TKT_DEF
*COPY>CPYLIB>CHAR
  DEFFUN toXML(STR) CALLING "XML_FORMAT_CDATA"
  DEFFUN RECORD2XML(REC,ID,CPYLIB.PATH,RET.LIST,PROP.LIST)
  ;*
  ;* Method handler
  ;*
  ERROR_INVALID_METHOD = "Invalid method name."
  ERR.STRING = ''
  BEGIN CASE
    CASE METHOD = "getDocuSetList"
      GOSUB getDocuSetList
    CASE METHOD = "getTicketToPrint"
      GOSUB getTicketToPrint
    CASE 1
      ERR.STRING = ERROR_INVALID_METHOD
  END CASE
  IF ERR.STRING THEN
    GOSUB setupErrorMsg
    METHOD = ERR.STRING
  END
  SWAP @AM WITH "" IN METHOD
  METHOD = '<?xml version="1.0"?>':METHOD
  RETURN

  ;**
  ;* Returns list of docusets available
  ;*
  ;* @return METHOD XML listing of available docusets
  ;* @see    JISBP SCREEN_MAINT
  ;*;
getDocuSetList:
  ;*
  ;* Setup error strings
  ;*
  ERR.NOTEXIST.FILE = "Database error, please try again later."
  ;*
  ;* Open files
  ;*
  OPEN 'SYS_TKT_DEF'  TO SYS_TKT_DEF  ELSE ERR.STRING = ERR.NOTEXIST.FILE
  IF ERR.STRING THEN RETURN
  ;*
  ;* Select docusets from SYS_TKT_DEF file
  ;*
  SELECT.CMD = "SSELECT SYS_TKT_DEF WITH @ID UNLIKE ...*FLD"
  UDTEXECUTE SELECT.CMD CAPTURING CAP RETURNING RET
  ;*
  ;* Setup XML root
  ;*
  METHOD = '<docuset-list>'
  ;*
  ;* Select all locations for the product/whse combo
  ;*
  LOOP
    READNEXT STKD_ID ELSE EXIT
    METHOD<-1> = '<docuset id=' : QUOTE(toXML(STKD_ID)) : '/>'
  REPEAT
  METHOD<-1> = '</docuset-list>'
  SWAP @AM WITH "" IN METHOD
  RETURN

  ;**
  ;* Returns list of docusets available
  ;*
  ;* @param  ARGS<1> The DocuSet name
  ;* @param  ARGS<2> The Ticket name
  ;* @return METHOD XML-formatted output of ticket
  ;* @see    JISBP SCREEN_MAINT
  ;*;
getTicketToPrint:
  ;*
  ;* Setup error strings
  ;*
  RETURN

  ;**
  ;* Sets up an XML error message
  ;*
  ;* @param  ERR.STRING  The error message
  ;* @return ERR.STRING  The xml error message
  ;*;
setupErrorMsg: 
  ERR.STRING = '<error>':toXML(ERR.STRING):'</error>'
  RETURN
*
