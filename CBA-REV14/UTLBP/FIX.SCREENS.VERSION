CRT @(0,0):@(-1)
CRT @(2,10):'Enter Screen File Name: ':
INPUT FNAME
CRT @(2,12):"Enter version         : ":
INPUT VERSION
IF FNAME = '' OR FNAME = 'END' THEN STOP
FNAME = FNAME:'.SCREENS'
*
OPEN '',FNAME TO SCRN.FILE ELSE
  ERRMSG = FNAME:' Not Found'
  CRT ERRMSG
  STOP
END
*
CMD = 'SSELECT ':FNAME:' WITH @ID LIKE "...FRM" OR WITH @ID LIKE "...DEF"'
UDTEXECUTE CMD CAPTURING JUNK
*
DONE = 0
NEWLEN = LEN(VERSION)
STARTCOL = 79 - NEWLEN - 1
LOOP
  READNEXT ID ELSE DONE = 1
UNTIL DONE DO
  READ REC FROM SCRN.FILE,ID THEN
    TYPE = FIELD(ID,'*',2)
    NUMAM = DCOUNT(REC,@AM)
    DOCONTINUE = 1
    FOR II = 1 TO NUMAM WHILE DOCONTINUE
      VAR1 = REC<II>
      X = LEN(VAR1)
      STARTPOS = INDEX(VAR1,"[",1)
      IF STARTPOS > 0 THEN
        POS = INDEX(VAR1,"]",1)
        POS2 = INDEX(VAR1,"]",2)
        IF POS2 > 0 THEN POS = POS2
        IF POS > 0 THEN
          OLDLEN = POS - STARTPOS - 1
          IF OLDLEN # NEWLEN THEN
            PRINT "LEN MISMATCH ":ID
          END
          IF TYPE = 'DEF' THEN
            OLDVAL = VAR1<1,3>
            LENOLDVAL = LEN(OLDVAL)
            STARTPOS = INDEX(OLDVAL,"[",1)
            POS = INDEX(OLDVAL,"]",1)
            POS2 = INDEX(OLDVAL,"]",2)
            IF POS2 > 0 THEN POS = POS2
            NEWVAL = OLDVAL[1,STARTPOS-1]:"[":VERSION:"]"
            LENNEWVAL = LEN(NEWVAL)
            IF VAR1<1,2> > 80 - LENNEWVAL THEN
              VAR1<1,2> = 80 - LENNEWVAL
            END
            VAR1<1,3> = NEWVAL
            VAR2=VAR1
          END ELSE
            PART1 = VAR1[1,STARTPOS-1]
            PART2 = VAR1[POS+1,X]
            VAR2 = PART1:"[":VERSION:"]":PART2
          END
          REC<II> = VAR2
          WRITE REC ON SCRN.FILE,ID
          DOCONTINUE=0
        END
      END
    NEXT II
  END
REPEAT

