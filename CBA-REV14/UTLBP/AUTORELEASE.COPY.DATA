**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - UTLBP
* PROGRAM     - AUTORELEASE.COPY.DATA
* BY          - STEFANIE A. NEWTON ; CBA
* DATE        - 07/17/1997
* DESCRIPTION - Program to create and move data and dict 
*               files for autorelease.
*
* T22080 stefanie 07/17/1997 * Program to create and move data and dict 
*                              files for autorelease
*
**********************************************
*
**  Open files
*
OPEN 'AUTORELEASE' TO AUTORELEASE.FILE ELSE
  CRT 'Unable to open AUTORELEASE file!  Exiting!'
  GOTO  END.PROGRAM
END
*
**  Initialize variables
*
NO.OF.PATH.ITEMS = DCOUNT(@PATH,'/')
PATH.ACCT        = FIELD(@PATH,'/',NO.OF.PATH.ITEMS)
ID               = PATH.ACCT:'-COPY-DATA-LIST'
*
**  Start processing
*
READ THE.LIST FROM AUTORELEASE.FILE, ID THEN
  NUM.LINES = DCOUNT(THE.LIST,@AM)
  *
  FOR CURR.LINE = 1 TO NUM.LINES
    ERROR = ''
    *
    **  Ignore the line if it is a comment
    *
    IF THE.LIST<CURR.LINE>[1,1] NE '*' THEN
      FILE.NAME = THE.LIST<CURR.LINE,1>
      ACCT.NAME = THE.LIST<CURR.LINE,2>
      *
      **  Set pointer to the file
      *
      COMMAND = 'SETFILE ':@PATH:'-':ACCT.NAME:'/':FILE.NAME:' TEMP.PTR OVERWRITING'
      UDTEXECUTE COMMAND CAPTURING JUNK
      *
      IF JUNK[1,7] NE 'No such' THEN
        *
        **  Open the files
        *
        OPEN FILE.NAME TO DATA.FILE ELSE 
          CRT 'Unable to open local DATA file ':FILE.NAME
          ERROR = 1
        END
        *
        OPEN "DICT ":FILE.NAME TO DICT.FILE ELSE 
          CRT 'Unable to open local DICT ':FILE.NAME
          ERROR = 1
        END
        *
        OPEN "TEMP.PTR" TO PTR.DATA.FILE ELSE 
          CRT 'Unable to open remote DATA file TEMP.PTR: ':FILE.NAME
          ERROR = 1
        END
        *
        OPEN "DICT TEMP.PTR" TO PTR.DICT.FILE ELSE 
          CRT 'Unable to open remote DICT TEMP.PTR: ':FILE.NAME
          ERROR = 1
        END
        *
        IF NOT(ERROR) THEN
          *
          **  Read in Data
          *
          SELECT PTR.DATA.FILE
          *
          DONE = ''
          CRT 'Processing data for file ':FILE.NAME
          *
          LOOP
            READNEXT DATA.ID ELSE DONE = 1
          UNTIL DONE DO
            *
            **  Read from the remote account
            *
            READ PTR.DATA.REC FROM PTR.DATA.FILE, DATA.ID THEN
              NO.WRITE = ''
              *
              **  Write to the local account
              *
              WRITE PTR.DATA.REC ON DATA.FILE, DATA.ID ON ERROR NO.WRITE = 1
              * 
              IF NOT(NO.WRITE) THEN
                *
                **  Delete so that this can only be run once!
                *
                DELETE PTR.DATA.FILE, DATA.ID
              END
            END
          REPEAT
          *
          **  Read in Dict
          *
          SELECT PTR.DICT.FILE
          *
          DONE = ''
          CRT 'Processing DICTs for file ':FILE.NAME
          *
          LOOP
            READNEXT DICT.ID ELSE DONE = 1
          UNTIL DONE DO 
            *
            **  Read from the remote account
            *
            READ PTR.DICT.REC FROM PTR.DICT.FILE, DICT.ID THEN
              *
              **  Write to the local account
              *
              WRITE PTR.DICT.REC ON DICT.FILE, DICT.ID
            END
          REPEAT
          *
        END
      END ELSE
        CRT 'Unable to establish pointer to ':@PATH:'-':ACCT.NAME:'/':FILE.NAME
      END
    END
  NEXT CURR.LINE
END ELSE
  CRT 'No data in list ':ID:'!'
  ERROR = 1
  GOTO END.PROGRAM
END
*
**  Exit point of program
*
END.PROGRAM:
*
CRT
CRT
CRT 'Done processing!'
*
RETURN
* 
END
