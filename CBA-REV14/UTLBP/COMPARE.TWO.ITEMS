* REVISION    - [08.0]
*COPY>CPYLIB>COM1
* This program compare an item in two files (BASIC PROGRAMS, PROCS, SCREENS
* and CPYLIBS)
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*--- INITIALIZE
*
      ERRMSG = ""
*      PORT.ACCT=OCONV(0,"U50BB")
*     PORT.NO=FIELD(PORT.ACCT," ",1)
      PORT.NO = 'TTY'
      CALL SYSVARS.SUB(PORT.NO)
*      ACCT.RUN = FIELD(PORT.ACCT," ",2)
*       PORT.NO = SYSTEM(18)
      ACCT.RUN = SYSTEM(19)
      PORT.ACCT = PORT.NO:" ":ACCT.RUN
      PREV.FILE1 = ""
      PREV.FILE2 = ""
      REQUEST = ""
*
*--- OPEN FILES
*
      OPEN '','UTL.SCREENS' TO M.SCREENS ELSE ERRMSG = "UTL.SCREENS FILE IS MISSING";GOSUB 90000; GOTO 999999
      OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING";GOSUB 90000; GOTO 999999
      OPEN '','MD' TO MD ELSE ERRMSG = "MD FILE IS MISSING";GOSUB 90000; GOTO 999999
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.CNT = 1
      ECD.SCRN.NAME = "COMPARE.TWO.ITEMS"
      ECD.SCRN.NO = 1
      ECD.ACTION = 1; CALL SCRN.EDIT
      MAT SCV.REC = ""
      ECD.ACTION = 2
      CALL SCRN.EDIT
      GOTO 150
100*
      MAT SCV.REC = ""
      ECD.ACTION = 6
      CALL SCRN.EDIT
150*
      GOSUB 1000
      IF ECD.RET.VALUE = "END" THEN GOTO 999999
      GOSUB 2000
      IF ECD.RET.VALUE = "END" THEN GOTO 100
*
*--- ENTER OPTIONS
*
500*
      ECD.NUM = 5
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
      ECD.ACTION = 4
      CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
      CASE OPTION = "E" OR OPTION = "END"
         GOTO 100
      CASE OPTION = "F"
         GOSUB 10000
         GOTO 100
      CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 3
         ON OPTION GOSUB 1000,2000
      END CASE
      GOTO 500
*-----------------------*
*--- SUBROUTINES   ---*
*-----------------------*
*
*--- MAIN INPUT
*
1000*
      ECD.NUM = 1
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = PREV.FILE1
      ECD.ACTION = 4
      CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO 1999
      OPEN '',ECD.RET.VALUE TO FILE1 ELSE ERRMSG = ECD.RET.VALUE:" FILE IS MISSING"; GOSUB 90000; GOTO 1000
      READ MD.REC FROM MD, ECD.RET.VALUE ELSE MD.REC = ""
      IF MD.REC<1> = "Q" THEN
         SCV.REC(9)<ECD.SCRN.NO> = MD.REC<2>
         SCV.REC(10)<ECD.SCRN.NO> = MD.REC<3>
      END ELSE
         SCV.REC(9)<ECD.SCRN.NO> = ACCT.RUN
         SCV.REC(10)<ECD.SCRN.NO> = ECD.RET.VALUE
      END
      PREV.FILE1 = ECD.RET.VALUE
      ECD.NUM = 9
      ECD.ACTION = 5
      CALL SCRN.EDIT
      ECD.NUM = 10
      ECD.ACTION = 5
      CALL SCRN.EDIT
      FILE.TYPE = "D"
      F.LEN = LEN(SCV.REC(10)<ECD.SCRN.NO>)
      BEGIN CASE
      CASE F.LEN > 5 AND SCV.REC(10)<ECD.SCRN.NO>[F.LEN-5,6] = "CPYLIB"
         FILE.TYPE = "B"
      CASE F.LEN > 4 AND SCV.REC(10)<ECD.SCRN.NO>[F.LEN-4,5] = "PROCS"
         FILE.TYPE = "P"
      CASE F.LEN > 1 AND SCV.REC(10)<ECD.SCRN.NO>[F.LEN-1,2] = "BP"
         FILE.TYPE = "B"
      END CASE
1500*
      ECD.NUM = 2
      ECD.ACTION = 4
      CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO 1999
      READ REC FROM FILE1, ECD.RET.VALUE ELSE
         REC = ""
      END
      LEN1 = DCOUNT(REC,AM)
      IF LEN1 < 1 THEN
         ERRMSG = "CANNOT LOCATE ITEM - " : ECD.RET.VALUE
         GOSUB 90000
         GOTO 1500
      END
      REC = ""
      ITEM1 = ECD.RET.VALUE
1999*
      RETURN
*
*--- SECOND INPUT
*
2000*
      ECD.NUM = 3
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = PREV.FILE2
      ECD.ACTION = 4
      CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO 2999
      OPEN '',ECD.RET.VALUE TO FILE2 ELSE ERRMSG = ECD.RET.VALUE:" FILE IS MISSING"; GOSUB 90000; GOTO 2000
      READ MD.REC FROM MD, ECD.RET.VALUE ELSE MD.REC = ""
      IF MD.REC<1> = "Q" THEN
         SCV.REC(11)<ECD.SCRN.NO> = MD.REC<2>
         SCV.REC(12)<ECD.SCRN.NO> = MD.REC<3>
      END ELSE
         SCV.REC(11)<ECD.SCRN.NO> = ACCT.RUN
         SCV.REC(12)<ECD.SCRN.NO> = ECD.RET.VALUE
      END
      PREV.FILE2 = ECD.RET.VALUE
      ECD.NUM = 11
      ECD.ACTION = 5
      CALL SCRN.EDIT
      ECD.NUM = 12
      ECD.ACTION = 5
      CALL SCRN.EDIT
2500*
      ECD.NUM = 4
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ITEM1
      ECD.ACTION = 4
      CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO 2999
      READ REC FROM FILE2, ECD.RET.VALUE ELSE
         REC = ""
      END
      LEN2 = DCOUNT(REC,AM)
      IF LEN2 < 1 THEN
         ERRMSG = "CANNOT LOCATE ITEM - " : ECD.RET.VALUE
         GOSUB 90000
         GOTO 2500
      END
      ITEM2 = ECD.RET.VALUE
      REC = ""
2999*
      RETURN
*
*--- COMPARE
*
10000*
      PTR = 0
      SPTR = 0
      BLK = 1
      LOOP
         BEGIN CASE
         CASE PTR + 1 > LEN1 AND SPTR + 1 > LEN2
            ERRMSG = "BOTH ITEMS TERMINATED"
            GOSUB 90000
            BLK = 0
         CASE PTR + 1 > LEN1
            ERRMSG = "MAIN ITEM TERMINATED"
            GOSUB 90000
            BLK = 0
         CASE SPTR + 1 > LEN2
            ERRMSG = "SECOND ITEM TERMINATED"
            GOSUB 90000
            BLK = 0
         END CASE
         GOSUB 50000
         GOSUB 60000
11000*
         REC1 = TRIMF(REC1)
         REC2 = TRIMF(REC2)
         BEGIN CASE
         CASE REC1 = REC2
         CASE REC1[1,1] = "*" AND REC2[1,1] = "*" AND REC1[2,4] # "COPY" AND REC2[2,4] # "COPY" AND FILE.TYPE = "B"
         CASE REC1[1,1] = "*" AND REC1[2,4] # "COPY" AND FILE.TYPE = "B"
            GOSUB 50000
            GOTO 11000
         CASE REC2[1,1] = "*" AND REC2[2,4] # "COPY" AND FILE.TYPE = "B"
            GOSUB 60000
            GOTO 11000
         CASE REC1[1,1] = "C" AND REC2[1,1] = "C" AND FILE.TYPE = "P"
         CASE REC1[1,1] = "C" AND FILE.TYPE = "P"
            GOSUB 50000
            GOTO 11000
         CASE REC2[1,1] = "C" AND FILE.TYPE = "P"
            GOSUB 60000
            GOTO 11000
         CASE 1
            GOSUB 70000
            GOSUB 80000
15000*
            ECD.NUM = 6
            SCV.REC(ECD.NUM)<ECD.SCRN.NO> = REQUEST
            ECD.ACTION = 4
            CALL SCRN.EDIT
            REQUEST = ECD.RET.VALUE
            BEGIN CASE
            CASE REQUEST = "E" OR REQUEST = "END"
               BLK = 0
               GOTO 15999
            CASE REQUEST = "C"
               GOTO 15999
            CASE REQUEST = "BB"
               IF PTR > 1 THEN
                  PTR = PTR - 1
                  GOSUB 50500
                  GOSUB 70000
               END
               IF SPTR > 1 THEN
                  SPTR = SPTR - 1
                  GOSUB 60500
                  GOSUB 80000
               END
            CASE REQUEST = "B1"
               IF PTR > 1 THEN
                  PTR = PTR - 1
                  GOSUB 50500
                  GOSUB 70000
               END
            CASE REQUEST = "B2"
               IF SPTR > 1 THEN
                  SPTR = SPTR - 1
                  GOSUB 60500
                  GOSUB 80000
               END
            CASE REQUEST = "FB"
               IF PTR <= LEN1 THEN
                  GOSUB 50000
                  GOSUB 70000
               END
               IF SPTR <= LEN2 THEN
                  GOSUB 60000
                  GOSUB 80000
               END
            CASE REQUEST = "F1"
               IF PTR <= LEN1 THEN
                  GOSUB 50000
                  GOSUB 70000
               END
            CASE REQUEST = "F2"
               IF SPTR <= LEN2 THEN
                  GOSUB 60000
                  GOSUB 80000
               END
            END CASE
            GOTO 15000
15999*
            PRINT @(0,6) : CL :
            PRINT @(0,7) : CL :
            PRINT @(0,8) : CL :
            PRINT @(0,9) : CL :
            PRINT @(0,10) : CL :
            PRINT @(0,15) : CL :
            PRINT @(0,16) : CL :
            PRINT @(0,17) : CL :
            PRINT @(0,18) : CL :
            PRINT @(0,19) : CL :
         END CASE
      WHILE BLK DO REPEAT
      RETURN
*
*--- READ FROM FIRST ITEM
*
50000*
      PTR = PTR + 1
50500*
      READV REC1 FROM FILE1, ITEM1 , PTR ELSE REC1 = ""
      REC1 = TRIMB(REC1)
      REC1 = CHANGE(REC1,VM,"}")
      REC1 = CHANGE(REC1,SVM,"|")
      RETURN
*
*--- READ FROM SECOND ITEM
*
60000*
      SPTR = SPTR + 1
60500*
      READV REC2 FROM FILE2, ITEM2 , SPTR ELSE REC2 = ""
      REC2 = TRIMB(REC2)
      REC2 = CHANGE(REC2,VM,"}")
      REC2 = CHANGE(REC2,SVM,"|")
      RETURN
*
*--- PRINT LINE FROM FIRST ITEM
*
70000*
      PRINT @(0,6) : CL :
      PRINT @(0,7) : CL :
      PRINT @(0,8) : CL :
      PRINT @(0,9) : CL :
      PRINT @(0,10) : CL :
      TEMP = ""
      LINE.CNT = INT(LEN(REC1)/72)
      IF MOD(LEN(REC1),72) THEN LINE.CNT = LINE.CNT + 1
      IF LINE.CNT = 0 THEN LINE.CNT = 1
      IF LINE.CNT > 4 THEN LINE.CNT = 4
      IF PTR > 1 THEN
         READV TEMP FROM FILE1, ITEM1 , (PTR-1) ELSE TEMP = ""
         TEMP = TRIMB(TEMP)
         TEMP = CHANGE(TEMP,VM,"}")
         TEMP = CHANGE(TEMP,SVM,"|")
         PRINT @(0,6) : CL : "    " : ">>>" : TEMP[1,72] :
      END ELSE
         PRINT @(0,6) :CL: "       " : "****** START OF RECORD ******":
      END
      TEMP = ""
      PRINT @(0,7) : CL : PTR "R#4" : " - " : REC1[1,72] :
      FOR LN = 2 TO LINE.CNT
         PRINT @(0,LN+6) : CL : "       " : REC1[(((LN-1)*72)+1),72] :
      NEXT LN
      IF LINE.CNT < 4 THEN
         IF PTR + 1 > LEN1 THEN
            PRINT @(0,LINE.CNT+7) :CL: "       " : "****** END OF RECORD ******":
         END ELSE
            READV TEMP FROM FILE1, ITEM1 , (PTR+1) ELSE TEMP = ""
            TEMP = TRIMB(TEMP)
            TEMP = CHANGE(TEMP,VM,"}")
            TEMP = CHANGE(TEMP,SVM,"|")
            PRINT @(0,LINE.CNT+7) : CL : "    " : ">>>" : TEMP[1,72] :
            TEMP = ""
         END
      END
      RETURN
*
*--- PRINT LINE FROM SECOND ITEM
*
80000*
      PRINT @(0,15) : CL :
      PRINT @(0,16) : CL :
      PRINT @(0,17) : CL :
      PRINT @(0,18) : CL :
      PRINT @(0,19) : CL :
      TEMP = ""
      LINE.CNT = INT(LEN(REC2)/72)
      IF MOD(LEN(REC2),72) THEN LINE.CNT = LINE.CNT + 1
      IF LINE.CNT = 0 THEN LINE.CNT = 1
      IF LINE.CNT > 4 THEN LINE.CNT = 4
      IF SPTR > 1 THEN
         READV TEMP FROM FILE2, ITEM2 , (SPTR-1) ELSE TEMP = ""
         TEMP = TRIMB(TEMP)
         TEMP = CHANGE(TEMP,VM,"}")
         TEMP = CHANGE(TEMP,SVM,"|")
         PRINT @(0,15) : CL : "    " : ">>>" : TEMP[1,72] :
      END ELSE
         PRINT @(0,15) :CL: "       " : "****** START OF RECORD ******":
      END
      TEMP = ""
      PRINT @(0,16) : CL : SPTR "R#4" : " - " : REC2[1,72] :
      FOR LN = 2 TO LINE.CNT
         PRINT @(0,LN+15) : CL : "       " : REC2[(((LN-1)*72)+1),72] :
      NEXT LN
      IF LINE.CNT < 4 THEN
         IF SPTR + 1 > LEN2 THEN
            PRINT @(0,LINE.CNT+16) :CL: "       " : "****** END OF RECORD ******":
         END ELSE
            READV TEMP FROM FILE2, ITEM2 , (SPTR+1) ELSE TEMP = ""
            TEMP = TRIMB(TEMP)
            TEMP = CHANGE(TEMP,VM,"}")
            TEMP = CHANGE(TEMP,SVM,"|")
            PRINT @(0,LINE.CNT+16) : CL : "    " : ">>>" : TEMP[1,72] :
            TEMP = ""
         END
      END
      RETURN
*
*--- ERROR ROUTINE
*
90000*
      PRINT @(0,23) : CL : ERRMSG :
      INPUT REPLY,1 :
      PRINT @(0,23) : CL :
      RETURN
999999*
      PRINT @(-1) :
   END
