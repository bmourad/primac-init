***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM - MENU.SCAN
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 02/19/85
*
* DESCRIPTION
*
* This program will scan all procedures referenced on the specified menu
* and print the names of the procs and and program names referenced by
* those procedures.
*
***************************************************************************
*
**COPY>CPYLIB>CHAR
      EQU AM  TO CHAR(254)
      EQU VM  TO CHAR(253)
      EQU SM  TO CHAR(252)
      EQU SVM TO CHAR(252)
      EQU BEL TO CHAR(007)
      CS = CHAR(12)
      CL = CHAR(27):CHAR(75)
*
*---- PRE-INITIALIZATION
*
      PROMPT ""
      PRINT "Menu name - ":
      INPUT MENU.NAME,20
      IF MENU.NAME = "" OR MENU.NAME = "END" THEN STOP
      MENU.NAMES = MENU.NAME
*
*      PORT.ACCT = OCONV("","U50BB")
*      PORT = FIELD(PORT.ACCT," ",1)
*      ACCT = FIELD(PORT.ACCT," ",2)
      PORT = SYSTEM(18)
      ACCT = SYSTEM(19)
      PORT.ACCT = PORT:" ":ACCT
*
      BEGIN CASE
         CASE FIELD(ACCT,"-",1) = "CBA"
            MASTER.ACCT = ACCT
         CASE ACCT = "PRIMAC"
            MASTER.ACCT = "CBA"
         CASE ACCT = "DEV"
            MASTER.ACCT = "CBA-DEV"
         CASE ACCT = "QCA"
            MASTER.ACCT = "CBA-QCA"
         CASE FIELD(ACCT,"-",1) = "PRIMAC"
            MASTER.ACCT = "CBA"
         CASE FIELD(ACCT,"-",1) = "DEV"
            MASTER.ACCT = "CBA-DEV"
      END CASE
      ENG.VERBS = "SELECT"
      ENG.VERBS<-1> = "SSELECT"
      ENG.VERBS<-1> = "LIST"
      ENG.VERBS<-1> = "SORT"
      TCL.VERBS = "CLEAR-FILE"
      TCL.VERBS<-1> = "CREATE-FILE"
      TCL.VERBS<-1> = "DELETE-FILE"
      TCL.VERBS<-1> = "COPY"
      TCL.VERBS<-1> = "DELETE"
      TCL.VERBS<-1> = "LOGTO"
      TCL.VERBS<-1> = "SLEEP"
      TCL.VERBS<-1> = "SP-ASSIGN"
      DISCARD = "DICT"
      DISCARD<-1> = "THE"
      DISCARD<-1> = "FILE"
      DISCARD<-1> = "ONLY"
*
*---- DEFINE CONSTANTS
*
      DIM MENU(40)
      DIM QREC1(10)
      MAT QREC1 = ""
      QREC1(1) = "Q"
      QREC1(9) = "L"
      QREC1(10) = "10"
*
      DIM PREC1(10)
      MAT PREC1 = ""
      PREC1(1) = "P"
      PREC1(2) = "10B4"
*
*---- OPEN ALL FILES
*
      OPEN "","MD" TO MD ELSE
         ERRMSG = "CANNOT OPEN MASTER DICTIONARY"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","EASY.MENUS" TO MENU.FILE ELSE
         ERRMSG = "CANNOT OPEN EASY.MENUS FILE"
         GOSUB 90000
         GOTO 99999
      END
*
*---- GET MENU FILE NAME
*
      READ REC1 FROM MD, "EASY.MENUS" ELSE
         REC1 = ""
      END
      IF REC1<1> = "Q" THEN
         MENU.FILE.NAME = REC1<3>
      END ELSE
         MENU.FILE.NAME = "EASY.MENUS"
      END
      PRINTER ON
*
*---- MAIN PROCESSING
*
100*
      MENU.NAME = MENU.NAMES<1>
      IF MENU.NAME = "" THEN GOTO 99999
      MENU.NAMES = DELETE(MENU.NAMES,1,0,0)
      MATREAD MENU FROM MENU.FILE, MENU.NAME ELSE
         ERRMSG = "CANNOT LOCATE ":MENU.NAME:" ON MENU FILE"
         GOSUB 90000
         GOTO 99999
      END
      PRINT MENU.NAME
      PNAMES = ""
      FOR N = 2 TO 33
         IF MENU(N)<1,1> # "C" AND MENU(N) # "" THEN
            SEL = N
            GOSUB 200
         END
110   NEXT N
      PRINT
      GOTO 100
*
*---- PROCESS MENU ITEM
*
200*
      IF MENU(SEL)<1,1>[1,2] = "M." THEN
         MENU.NAMES<-1> = MENU(SEL)<1,1>
      END ELSE
         PROC.NAME = MENU(SEL)<1,1>
         GOSUB 300
      END
299   RETURN
*
*---- SCAN MENU PROCEDURE
*
300*
      READ PROC FROM MD, PROC.NAME ELSE
         ERRMSG = "CANNOT LOCATE MENU PROCEDURE - ":PROC.NAME
         GOSUB 90000
         GOTO 499
      END
      IF FIELD(PROC.NAME,".",2) = "LOGTO" THEN GOTO 499
      PRINT
      PRINT "      ":"PROCEDURE" "L#12":"MD" "L#12":PROC.NAME
*
*---- RETRIEVE ENTIRE PROCEDURE
*
      PCNT = COUNT(PROC,AM) + (PROC # "")
      DONE = 0
      P = 1
      LOOP
      UNTIL P > PCNT DO
      PROC.LINE = TRIM(PROC<P>)
      BEGIN CASE
         CASE PROC.LINE[1,1] = "(" AND NOT(NUM(PROC.LINE[2,1]))
            PROC.FLAG = "("
            PROC.LINE = FIELD(PROC.LINE[2,99],")",1)
         CASE PROC.LINE[1,1] = "["
            PROC.FLAG = "["
            PROC.LINE = FIELD(PROC.LINE[2,99],"]",1)
         CASE 1
            PROC.FLAG = ""
      END CASE
      IF PROC.FLAG # "" THEN
         PFILE = FIELD(PROC.LINE," ",1)
         PITEM = FIELD(PROC.LINE," ",2)
         IF PITEM = "" THEN PITEM = PROC.NAME
         OPEN "",PFILE TO PROC.FILE ELSE
            ERRMSG = "CANNOT OPEN ":PFILE:" FILE"
            GOSUB 90000
            GOTO 499
         END
         READ RPROC FROM PROC.FILE, PITEM ELSE
            ERRMSG = "CANNOT LOCATE ":PITEM:" ON ":PFILE
            GOSUB 90000
            GOTO 499
         END
         PRINT "      ":"PROCEDURE" "L#12":PFILE "L#12":PITEM
      END
      BEGIN CASE
         CASE PROC.FLAG = "("
            PROC = RPROC
            PCNT = COUNT(PROC,AM) + (PROC # "")
            P = 1
         CASE PROC.FLAG = "["
            PROC = DELETE(PROC,P,0,0)
            PROC = INSERT(PROC,P,0,0,RPROC)
            PCNT = COUNT(PROC,AM) + (PROC # "")
         CASE 1
            P = P + 1
      END CASE
      REPEAT
*
*---- LOCATE PROGRAM NAMES
*
      BYPASS = 0
      FOR P = 1 TO PCNT
         PROC.LINE = TRIM(PROC<P>)
         DONE = 0
         LOOP
         UNTIL DONE DO
         FIRST.CHAR = PROC.LINE[1,1]
         IF FIRST.CHAR = "" OR NOT(NUM(FIRST.CHAR)) THEN
            DONE = 1
         END ELSE
            PROC.LINE = TRIM(PROC.LINE[2,999])
         END
         REPEAT
         BEGIN CASE
            CASE PROC.LINE[1,2] = "H("
            CASE PROC.LINE[1,2] = "H<"
            CASE PROC.LINE[1,1] = "H" AND BYPASS = 0
               PROC.LINE = TRIM(PROC.LINE[2,999])
               PROC.VERB = FIELD(PROC.LINE," ",1)
               LOCATE PROC.VERB IN ENG.VERBS,1 SETTING ENG ELSE ENG = 0
               LOCATE PROC.VERB IN TCL.VERBS,1 SETTING VRB ELSE VRB = 0
               BEGIN CASE
                  CASE ENG > 0
                     BYPASS = 1
                  CASE VRB > 0
                  CASE PROC.VERB = "RUN"
                     PROG.NAME = FIELD(PROC.LINE," ",3)
                     IF PROG.NAME # "EASY.MENU" THEN
                        PNAME = FIELD(PROC.LINE," ",3)
                        PRINT "      ":"PROGRAM" "L#12":"" "L#12":PNAME
                     END
                  CASE 1
                     PROG.NAME = PROC.VERB
                     IF PROG.NAME # "EASY.MENU" THEN
                        PNAME = PROG.NAME
                        PRINT "      ":"PROGRAM" "L#12":"" "L#12":PNAME
                     END
               END CASE
            CASE PROC.LINE[1,1] = "P" AND BYPASS = 1
               BYPASS = 0
            CASE PROC.LINE = "STON" AND BYPASS = 1
               BYPASS = 0
         END CASE
      NEXT P
499   RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT ERRMSG
      RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER OFF
      PRINTER CLOSE
      STOP
   END
