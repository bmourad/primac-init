*********************************************************************
*REVISION    - [08.0]                                               *
*SYSTEM      - SUPPORT                                              *
*SOURCE      - UTLBP                                                *
*PROGRAM     - XFER.LIST                                            *
*BY          - W.R. MCKENZIE, C.B.A.                                *
*DATE        - 02/09/88                                             *
*DESCRIPTION - This program is a listing of transfers for customer  *
*********************************************************************
************************
*DIMENSIONS AND EQUATES*
************************
*
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>TRANSFER.KEY
*
*****************
*GET PROC VALUES*
*****************
*
      PROCREAD INBUF ELSE
         ERRMSG = 'CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      TRANSFER.FILE.NAME = INBUF<1>
*
***********
*OPEN FILE*
***********
*
      OPEN "DICT", TRANSFER.FILE.NAME TO D.TRANSFER.FILE ELSE
         ERRMSG = 'FILE NOT FOUND FOR OPEN'
         GOTO 93000
      END
**********
*HEADINGS*
**********
      PRINTER ON
*
*
      HD1 = TRANSFER.FILE.NAME:SPACE(20)
      HD1 = HD1:"SYSTEM TRANSFER LISTING":SPACE(20)
      HD1 = HD1:"'T'":SPACE(11)
      HD1 = HD1:"PAGE : 'P' "
*
      HD2 = SPACE(1):"TRANS NUM":SPACE(9):"ACCOUNT NAME":SPACE(8)
      HD2 = HD2:"DICT":SPACE(11):"FILE NAME":SPACE(28)
      HD2 = HD2:"ITEM NAME":SPACE(16):"TP FN OW"
*
      HD3 = STR("-",12):SPACE(1):STR("-",25):SPACE(1)
      HD3 =HD3:STR("-",4):SPACE(1):STR("-",30):SPACE(1)
      HD3 = HD3:STR("-",40):" -- -- --"
*
      HEADING HD1:"'LL'":HD2:"'L'":HD3
*
**************
*MAIN ROUTINE*
**************
      READNEXT ID ELSE GOTO 99999
      DESC.LEN = 40
************
*PRINT LINE*
************
      LOOP
         MATREAD TKEY.REC FROM D.TRANSFER.FILE,ID THEN
            LINE = ID "L#12":SPACE(1)
            LINE = LINE:TKEY.ACCT "L#25":SPACE(1)
            LINE = LINE:TKEY.DICT "L#4":SPACE(1)
            LINE = LINE:TKEY.FILE "L#30":SPACE(1)
            IF LEN(TKEY.ITEM) > DESC.LEN THEN
               CALL DESC.REFORMAT(TKEY.ITEM,DESC.LEN,NEW.DESC)
            END ELSE
               NEW.DESC = TKEY.ITEM
            END
            LINE = LINE:NEW.DESC<1,1> "L#40":SPACE(1)
            LINE = LINE:TKEY.TYPE "L#2":SPACE(1)
            LINE = LINE:TKEY.FUNC "L#2":SPACE(1)
            LINE = LINE:TKEY.OVER "L#2":SPACE(1)
            PRINT LINE
            I = 2
            LOOP WHILE NEW.DESC<1,I> # "" DO
               PRINT SPACE(75) : NEW.DESC<1,I>
               I = I + 1
            REPEAT
         END
      WHILE READNEXT ID DO REPEAT
      PRINTER OFF
      GOTO 99999
93000* ERROR ROUTINE
      PRINT ERRMSG
99999*END OF PROGRAM
   END
