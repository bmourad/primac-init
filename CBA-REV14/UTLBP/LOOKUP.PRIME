********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM     - PRIME.NUMBERS
* BY          - JIHAD A. YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 02/10/89
* DESCRIPTION
* This routine input a number or numbers spacified at the run time
* and calculate the lowest and the hightest prime number and
* display the results back on the screen or the printer.
*******************************************************************
*
*---- INIT
*
    EQU AM  TO CHAR(254)
    EQU BEL TO CHAR(007)
    CS = CHAR(12)
    CL = CHAR(27):CHAR(75)
    PROMPT ""
    PRINT @(-1)
*
*---- GET THE PROC INPUT
    PROCREAD BUFFER ELSE BUFFER = ""
    NUM.COUNT = DCOUNT(BUFFER,AM)
    IF BUFFER<NUM.COUNT> = "(P" OR BUFFER<NUM.COUNT> = "(P)" THEN
       PRINTER.FLAG = "Y"
       NUM.COUNT = NUM.COUNT - 1
    END ELSE
       PRINTER.FLAG = "N"
    END
*
*---- GET NUMBER
*
    IF NUM.COUNT > 1 THEN
       FOR BC = 2 TO NUM.COUNT
          NUMBER = BUFFER<BC>
          PRINT @(0,10):"Processing number : ":NUMBER:
          GOSUB 20
          IF ERRMSG # "" THEN GOTO 9
          GOSUB 200 ; GOSUB 300 ; GOSUB 30
9*
       NEXT BC
    END ELSE
10*
       PRINT @(0,10):CL:"Enter number or (END) :":
       INPUT NUMBER
       IF NUMBER # "END" AND NUMBER # "" THEN
          GOSUB 20
          IF ERRMSG # "" THEN GOTO 10
          GOSUB 200 ; GOSUB 300 ; GOSUB 30
          GOTO 10
       END
    END
    GOTO 99999
*
*---- CHECK INPUT
*
20*
    ERRMSG = ""
    BEGIN CASE
    CASE NOT(NUM(NUMBER))
       ERRMSG = "*** Please enter a number alphnumeric not allowed"
       GOSUB 91000
    CASE NUMBER < 0
       ERRMSG = "*** Please enter a number greater then zero"
       GOSUB 91000
    CASE INT(NUMBER) # NUMBER
       ERRMSG = "*** Please enter a number decimal not allowed"
       GOSUB 91000
    END CASE
    RETURN
*
*---- PRINT RESULTS
*
30*
    IF PRINTER.FLAG = "Y" THEN
       PRINTER ON
       PRINT "Number entered = ":NUMBER:"   Low prime = ":LO.PRIME:"   High prime = ":HI.PRIME
       PRINTER OFF
    END ELSE
       ERRMSG = "Number entered = ":NUMBER:"   Low prime = ":LO.PRIME:"   High prime = ":HI.PRIME
       GOSUB 91000
    END
    RETURN
*
*---- FIND PRIME NUMBER LOWER THAN ENTERED
*
200*
    TRAIL = NUMBER
    LOOP
       GOSUB 400
    UNTIL MORE DO
       TRAIL = TRAIL - 1
    REPEAT
    LO.PRIME = TRAIL
    RETURN
*
*---- FIND PRIME NUMBER HIGHER THE ENTERED
*
300*
    TRAIL = NUMBER
    LOOP
       GOSUB 400
    UNTIL MORE DO
       TRAIL = TRAIL + 1
    REPEAT
    HI.PRIME = TRAIL
    RETURN
*
*---- CHECK TRAIL IF PRIME
*
400*
    MORE = 1
    IF TRAIL < 4 THEN GOTO 499
    LIMIT = INT(SQRT(TRAIL)) + 1
    FOR I = 2 TO LIMIT UNTIL MORE = 0
       IF NOT(MOD(TRAIL,I)) THEN MORE = 0
    NEXT I
499*
    RETURN
*
*---- PRINT ERRMSG
*
91000*
    PRINT @(0,23):CL:ERRMSG: 
    INPUT REPLY:
    PRINT @(0,23):CL:
    RETURN
*
*---- END OF PROGRAM
*
99999*
    PRINT @(-1)
 END
