      SUBROUTINE SCAN.XFER.PROG (PROG, PFX.PROG.CALLED, PFX.CPYLIB, PFX.SCREEN)
***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM      - SCAN.XFER.PROG
*
* BY           - NICK AMENDOLA,  COMPUTER BUSINESS ASSOCIATES
*
* DATE         - 10/04/84
*
* DESCRIPTION
*
*  This program will scan the source program passed to it and return three
*  multi-attribute strings.
*
*      PFX.PROG.CALLED - will contain the names of all subroutines called.
*
*      PFX.CPYLIB      - will contain the names of all CPYLIB references.
*
*      PFX.SCREEN      - will contain the names of all screen references.
*
**************************************************************************
*
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      PROMPT ""
*
*---- PROCESS PROGRAM
*
1100*
      LINE.CNT = COUNT(PROG,AM) + (PROG # '')
      FOR L = 1 TO LINE.CNT
         PROG.LINE = PROG<L>
         IF PROG.LINE[1,6] = "*COPY>" THEN
            PROG.LINE = "COPY ":PROG.LINE[7,99]
         END
         IF PROG.LINE[1,7] = "**COPY>" THEN
            PROG.LINE = "COPY ":PROG.LINE[8,99]
         END
         IF PROG.LINE[1,1] = "*" THEN GOTO 1599
         STMT.CNT = COUNT(PROG.LINE,";") + (PROG.LINE # "")
         FOR S = 1 TO STMT.CNT
            PROG.STMT = TRIM(FIELD(PROG.LINE,";",S))
            IF PROG.STMT[1,1] = "*" THEN GOTO 1595
            IF PROG.STMT[1,13] = "ECD.SCRN.NAME" THEN
               PROG.STMT = "ECD.SCRN.NAME ":PROG.STMT[14,99]
            END
            PTR1 = INDEX(PROG.STMT," ",1)
            IF PTR1 = 0 THEN GOTO 1595
            PROG.VERB = PROG.STMT[1,PTR1-1]
            BEGIN CASE
               CASE PROG.VERB = "CALL"
                  GOSUB 6000
               CASE PROG.VERB = "COPY"
                  GOSUB 8000
               CASE PROG.VERB = "ECD.SCRN.NAME"
                  GOSUB 9000
            END CASE
1595*
         NEXT S
1599*
      NEXT L
      GOTO 99999
*
*---- PROCESS "CALL" VERB
*
6000*
      LINE.SEG = TRIM(PROG.STMT[PTR1+1,99])
      PNAME = TRIM(FIELD(LINE.SEG,"(",1))
      LOCATE PNAME IN PFX.PROG.CALLED,1 SETTING FND ELSE
         PFX.PROG.CALLED<-1> = PNAME
      END
      RETURN
*
*---- PROCESS "COPY" STATEMENT
*
*
*---- PROCESS "COPY" STATEMENT
*
8000*
      LINE.SEG = TRIM(FIELD(PROG.STMT," ",2))
      SEP.CNT = COUNT(LINE.SEG,">") + (LINE.SEG # "")
      IF SEP.CNT = 0 THEN GOTO 8090
      INSERT.ITEM = FIELD(LINE.SEG,">",SEP.CNT)
      PFX.CPYLIB<-1> = INSERT.ITEM
8090*
      RETURN
*
*---- LOCATE SCREEN NAME
*
9000*
      LINE.SEG = TRIM(FIELD(PROG.STMT,"=",2))
      SEP.CNT = COUNT(LINE.SEG,":") + (LINE.SEG # "")
      FOR N = 1 TO SEP.CNT
         SCREEN.NAME = TRIM(FIELD(LINE.SEG,":",N))
         IF SCREEN.NAME # "AM" THEN
            SCREEN.NAME = SCREEN.NAME[2,LEN(SCREEN.NAME)-2]
            PFX.SCREEN<-1> = SCREEN.NAME
         END
      NEXT N
      RETURN
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
