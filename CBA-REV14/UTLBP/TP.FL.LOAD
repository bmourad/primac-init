*COPY>CPYLIB>COM1
*******************************************************************
* REVISION    - [08.1]
* Copyright 1990 by Vercom Software, Inc.
* SYSTEM      - PRIMAC
* PROGRAM     - TP.FL.LOAD (TAPE.FILE.LOAD)
* AUTHOR      - DANIEL GRUENENFELDER
* DATE        - 10/24/90
* DESCRIPTION - This program loads the TAPE.FILE.DATA file, which
* uses the TAPE.FILE parameters to load data from tape.
********************************************************************
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>UTL.CPYLIB>TAPE.FILE
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*
*---- SETUP ERRMSG ROUTINE
*
SYS.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
OPEN 'CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING";GOTO 93000
OPEN 'UTL.SCREENS' TO M.SCREENS ELSE ERRMSG="UTL.SCREENS FILE IS MISSING";GOTO 93000
OPEN 'COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING";GOTO 93000
OPEN 'TAPE.FILE' TO TAPE.FILE ELSE ERRMSG="TAPE.FILE FILE IS MISSING";GOTO 93000
OPEN 'TAPE.LOAD' TO TAPE.LOAD ELSE ERRMSG="TAPE.LOAD FILE IS MISSING";GOTO 93000
* WHEN LOADING LIVE DATA FILE, OPEN TF.DEST.FILE HERE *
* OPEN TF.DEST.FILE TO TAPE.DEST.FILE ELSE
*    ERRMSG = TF.DEST.FILE:" FILE IS MISSING"
*    GOTO 93000
* END
*
*---- GET CONO
*
REM MAT COMP.REC=''
REM CONO=''
REM CALL GET.CONO(CONO,MAT COMP.REC)
REM IF CONO="END" THEN GOTO 99999
*
*---- INT
*
50 BEGIN.PAGE=16;PAGE.SIZE=4;LINE.SPACE=1;LINES=0;OLD.START.LINE=0
TYP=0;CALL EDIT.SUB;FILL='#'
SP15=SPACE(15)
GEN.DIV="00";GEN.DEPT="00";GEN.CCTR="000"
DIM PRINT.LINE(100)
MAT PRINT.LINE = ""
*
*---- MAIN PROCESSING
*
MAT EDIT.COM.DRIVER=''
ECD.SCRN.CNT=1
ECD.SCRN.NAME<1>="TAPE.LOAD"
ECD.ACTION = 1
CALL SCRN.EDIT
*
*---- PRINT SCREEN
*
ECD.SCRN.NO=1
MAT SCV.REC=""
ECD.ACTION=2;CALL SCRN.EDIT
*
*---- CONTROL SUB
80 *
INV.FLG=0
LINES=0;OLD.START.LINE=0;APPLY.AMT=0;CH.AMT=0
REM MAT SCV.REC="";ECD.ACTION=6;CALL SCRN.EDIT
GOSUB 100
GOSUB 200
IF REPORT.FLAG = "END" THEN GOTO 80
GOSUB 9000
GOTO 50
*
*---- ENTER TAPE ID
100 *
REM ECD.NUM=1;ECD.MAXL=4;ECD.VALDAT.CODE='2';ECD.VALDAT.FILE=TAPE.FILE
ECD.NUM=1;ECD.MAXL=4
ECD.ACTION=4;CALL SCRN.EDIT
IF ECD.RET.VALUE # 'END' THEN
   TAPE.ID=ECD.RET.VALUE
   END ELSE
   STOP
END
MATREAD TAPE.REC FROM TAPE.FILE, TAPE.ID ELSE
   ERRMSG = "CANNOT LOCATE THIS TAPE'S FORMAT RECORD"
   GOSUB 91000
   GOTO 100
END
ECD.NUM=4
SCV.REC(4)<1>=TF.DESC
ECD.ACTION=5;CALL SCRN.EDIT
RETURN
*
*---- ENTER AUDIT REPORT PRINT FLAG
200 *
ECD.NUM=2
ECD.ACTION=4;CALL SCRN.EDIT
REPORT.FLAG=ECD.RET.VALUE
RETURN
*
*---- PROCESS TAPE
300 *
IF REPORT.FLAG = "Y" THEN
   HEADING "TAPE.FILE.LOAD":SPACE(20):"LOAD ":TF.DESC 'L#40':"PAGE ":'PLDL'
END
*
* CHECK FOR LABELING *
IF TF.LABEL.FLAG = "L" THEN
  FOR X = 1 TO 2
     PERFORM "T-FWD"
  NEXT X
END
*
*---- READ TAPE
1000 READT TREC ELSE
  RETURN
END
IF TF.A.OR.E = "E" THEN
   TREC = ASCII(TREC)
END
* PROCESS EACH RECORD IN THE BLOCK *
FOR X = 1 TO TF.BLK.FCTR
   FOR Y = 1 TO DCOUNT(TF.BEG.POS<1>,VM)
      DATA.REC = ""
      KEY = ""
      ATB = FIELD(TF.FIELD<1,Y>,",",1)
      IF TF.BEG.POS<1,Y> = "LIT" THEN
         DATA = TF.CONV.MASK<1,Y>
         GOTO 1100
      END
      LEN = TF.END.POS<1,Y> - TF.BEG.POS<1,Y> + 1
      POS = (X - 1) * TF.REC.SZ + TF.BEG.POS<1,Y>
      DATA = TREC[POS,LEN]
      IF TF.CONV.MASK<1,Y> = "" THEN GO 1100
      MASK = TF.CONV.MASK<1,Y>
      TYPE = TF.CONV.TYPE<1,Y>
      IF MASK = "DATEMM" THEN
         DATA = DATA[1,2] : "-" : DATA[3,2] : "-" : DATA[5,2]
      END
      IF TYPE = "I" THEN
         DATA = ICONV(DATA,MASK)
      END
      IF TYPE = "O" THEN
         DATA = OCONV(DATA,MASK)
      END
1100 *
      IF ATB = 0 THEN
         KEY = KEY : TF.KEY.PREFIX : DATA : TF.KEY.SUFFIX
         END ELSE
         DATA.REC<ATB> = DATA
      END
      IF REPORT.FLAG = "Y" THEN GOSUB 10000; * BUILD REPORT LINE
   NEXT Y
   IF KEY = "" THEN KEY = KEY.COUNTER + 1; KEY.COUNTER = KEY
   WRITE DATA.REC ON TAPE.LOAD, KEY
   IF REPORT.FLAG = "Y" THEN GOSUB 11000; * PRINT DATA LINE
NEXT X
GOTO 1000
*
*
*---- ENTER OPTION
9000 *
MORE=1
LINES=DCOUNT(TF.BEG.POS<1>,VM)
LOOP
ECD.NUM=3;ECD.ACTION=4;SCV.REC(ECD.NUM)<1>='';CALL SCRN.EDIT
OPTION=ECD.RET.VALUE
BEGIN CASE
CASE OPTION='E' OR OPTION='END'
MORE=0
RELEASE
CASE OPTION = 'F'
GOSUB 300
MORE=0
CASE NUM(OPTION) AND OPTION < 3
ON OPTION GOSUB 100,200
IF REPORT.FLAG = "END" THEN GOTO 80
CASE 1
ERRMSG="INVALID OPTION";GOSUB 91000
END CASE
WHILE MORE=1 DO
REPEAT
RETURN
10000 *
* BUILD REPORT LINE
PRINT.LINE(ATB+1) = DATA : '"L#':LEN:'"'
RETURN
*
*---- PRINT REPORT LINE
11000 *
PRINT KEY "  ":
FOR L = 2 TO DCOUNT(TF.BEG.POS,VM)
  PRINT PRINT.LINE(L) :
NEXT L
PRINT
RETURN
*
*---- CALLS FOR SYSCOM
91000 *
ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000 *
ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000 *
ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999 *
PRINT CS:
END
