*COPY CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* PROGRAM  - MANUAL.CODE.AMINT
* BY       - JIHAD YAMOUT, C.B.A
* DATE     - 01/10/86
* REVISION - A.1.0
* DESCRIPTION
*  This program will add and maintain manual file entries.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY CPYLIB>CHAR
*COPY CPYLIB>SCREEN.COM
*COPY CPYLIB>MANUAL
*COPY CPYLIB>FILE.VARS
*
*---- OPEN ALL FILES
*
      OPEN "","UTL.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN UTL.SCREENS FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","MANUAL" TO MANUAL ELSE
         ERRMSG = "CANNOT OPEN MANUAL FILE"
         GOSUB 90000
         STOP
      END
*
*---- INITIALIZATION
*
      SCREEN INIT;#
      SCREEN DEFINE;1;MANUAL.CODE.MAINT
      SCREEN FORMAT;1
*
*---- MAIN PROCESSING
*
100*
      FOR N = 1 TO 60
         S$DATA(N) = ""
      NEXT N
      S$DATA(1)<1> = DATE()
      SCREEN DISPLAY;1;1
*
*---- GET CODE
*
10100*
      KEY = ""
      NEW.REC = ""
      MAT MANUAL.REC = ""
      RELEASE
      SCREEN FIELD;1;4
      SCREEN INPUT;1;4
      IF S$VALUE = "END" THEN GOTO 91000
      KEY = S$VALUE
      MATREADU MANUAL.REC FROM MANUAL, KEY ELSE
         SCREEN FIELD;1;5
         SCREEN INPUT;1;5
         IF S$VALUE = "N" THEN GOTO 10100
         NEW.REC = "Y"
      END
      IF NEW.REC = "Y" THEN
         GOSUB 10300
      END ELSE
         S$DATA(2)<1> = MA.DESC
         SCREEN DISPLAY;1;ALL
      END
10110*
      SCREEN FIELD;1;6
      SCREEN INPUT;1;6
      OPTION = S$VALUE
      BEGIN CASE
         CASE OPTION = "1"
            GOSUB 10300
         CASE OPTION = "E" OR OPTION = "END"
            SCREEN CLEAR;1
            GOTO 100
         CASE OPTION = "F"
            MATWRITE MANUAL.REC ON MANUAL, KEY
            SCREEN CLEAR;1
            GOTO 100
      END CASE
      GOTO 10110
*
*---- GET FIELD 2
*
10300*
      SCREEN FIELD;1;2
      SCREEN INPUT;1;2;RETURN
      MA.DESC = S$VALUE
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF JOB
*
91000*
      PRINT @(-1):
   END
