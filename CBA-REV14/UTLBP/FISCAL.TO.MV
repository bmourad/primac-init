***************************************************************
*
*  FISCAL.TO.MV
*
* CONVERTS CONTROL FISCAL REC ATTRIBS TO MULTIVALUE, BASED
* ON NUMBER OF DIVISIONS AS DEFINED FOR CONO IN CONTROL
* DIVISIONS REC.  ASLO CONVERTS MANUAL.DEPR AND UOP.DEPR RECS.
*
***************************************************************
*
*----- DIMENSIONS AND EQUATES
*
*COPY>PMC.CPYLIB>FISCAL
*COPY>FAS.CPYLIB>MAN.DEPR.CTL
*COPY>FAS.CPYLIB>UOP.DEPR.CTL
*
  DIM HOLD.FISCAL.REC(7)
  EQU HOLD.FR.CURR.PER   TO HOLD.FISCAL.REC(1)
  EQU HOLD.FR.CURR.DATE  TO HOLD.FISCAL.REC(2)
  EQU HOLD.FR.NEXT.PER   TO HOLD.FISCAL.REC(3)
  EQU HOLD.FR.NEXT.DATE  TO HOLD.FISCAL.REC(4)
  EQU HOLD.FR.PRINT      TO HOLD.FISCAL.REC(5)
  EQU HOLD.FR.CLOSE.DATE TO HOLD.FISCAL.REC(6)
  EQU HOLD.FR.D.M.FLG    TO HOLD.FISCAL.REC(7)
*
  DIM HOLD.DEPR.REC(3)
  EQU HOLD.CUR.PER   TO HOLD.DEPR.REC(1)
  EQU HOLD.CUR.DATE  TO HOLD.DEPR.REC(2)
  EQU HOLD.PRT.FLG   TO HOLD.DEPR.REC(3)
*
*----- OPEN FILES
*
  OPEN "","CONTROL" TO CONTROL ELSE
      PRINT "CANNOT LOCATE CONTROL FILE"
      GOTO 99999
  END
*
*  EXECUTE 'SSELECT CONTROL WITH @ID LIKE "...DIVISIONS"' RTNLIST DIVISIONS.KEY.LIST
  DONE = 0
*
*---- READ DIVISIONS.REC
*
  LOOP
100 *
*      READNEXT DIVISIONS.KEY FROM DIVISIONS.KEY.LIST ELSE DONE = 1
      READNEXT DIVISIONS.KEY ELSE DONE = 1
      IF DONE THEN PRINT "NO ACTIVE SELECT LIST OF CONTROL FILE DIVISIONS RECS.  MUST SELECT MANUALLY. PROGRAM ABORTING."
  UNTIL DONE DO
      CONO = DIVISIONS.KEY[1,3]
*
      PRINT "********** BEGIN PROCESSING CONO ":CONO:" **********"
      READ DIVISIONS.REC FROM CONTROL, DIVISIONS.KEY ELSE
          DONE = 1
          PRINT "DIVISIONS CONTROL REC FOR CONO ":CONO:" NOT FOUND"
          GOTO 1000
      END
*
      DIVISIONS.NUM = DCOUNT(DIVISIONS.REC,@VM)
*
      FISCAL.ID = "APDFISCAL"; GOSUB 2000
      FISCAL.ID = "APVFISCAL"; GOSUB 2000
      FISCAL.ID = "ARCFISCAL"; GOSUB 2000
      FISCAL.ID = "ARSFISCAL"; GOSUB 2000
      FISCAL.ID = "FAFISCAL"; GOSUB 2000
      FISCAL.ID = "ICFISCAL"; GOSUB 2000
      FISCAL.ID = "JCFISCAL"; GOSUB 2000
      FISCAL.ID = "OPFISCAL"; GOSUB 2000
      FISCAL.ID = "PRFISCAL"; GOSUB 2000
*
      MATREADU MAN.DEPR.CTL FROM CONTROL, CONO:"MANUAL.DEPR" ELSE
          PRINT "MANUAL.DEPR CONTROL REC FOR CONO ":CONO:" NOT FOUND" 
          GOTO 1000
      END
      MAT HOLD.DEPR.REC = MAT MAN.DEPR.CTL
      FOR J = 1 TO DIVISIONS.NUM
          MDC.CUR.PER<1,J>  = HOLD.CUR.PER
          MDC.CUR.DATE<1,J> = HOLD.CUR.DATE
          MDC.PRT.FLG<1,J>  = HOLD.PRT.FLG
      NEXT J
      MATWRITE MAN.DEPR.CTL TO CONTROL, CONO:"MANUAL.DEPR"
      PRINT "MANUAL.DEPR REC FOR CONO ":CONO:" UPDATED"
*
      MATREADU UOP.DEPR.CTL FROM CONTROL, CONO:"UOP.DEPR" ELSE
          PRINT "UOP.DEPR CONTROL REC FOR CONO ":CONO:" NOT FOUND"
          GOTO 1000
      END
      MAT HOLD.DEPR.REC = MAT UOP.DEPR.CTL
      FOR J = 1 TO DIVISIONS.NUM
          UDC.CUR.PER<1,J>  = HOLD.CUR.PER
          UDC.CUR.DATE<1,J> = HOLD.CUR.DATE
          UDC.PRT.FLG<1,J>  = HOLD.PRT.FLG
      NEXT J
      MATWRITE UOP.DEPR.CTL TO CONTROL, CONO:"UOP.DEPR"
      PRINT "UOP.DEPR REC FOR CONO ":CONO:" UPDATED"
*
1000*
      PRINT "**********  END PROCESSING CONO ":CONO:"  **********"
      PRINT
      PRINT
  REPEAT
  GOTO 99999
*
*----- READ AND UPDATE FISCAL RECS
*
2000 *
  MATREADU FISCAL.REC FROM CONTROL, CONO:FISCAL.ID ELSE
      PRINT FISCAL.ID:" CONTROL REC FOR CONO ":CONO:" NOT FOUND"
      GOTO 2100
  END
  MAT HOLD.FISCAL.REC = MAT FISCAL.REC
  FOR I = 1 TO DIVISIONS.NUM
      FR.CURR.PER<1,I>  = HOLD.FR.CURR.PER
      FR.CURR.DATE<1,I> = HOLD.FR.CURR.DATE
      FR.NEXT.PER<1,I>  = HOLD.FR.NEXT.PER
      FR.NEXT.DATE<1,I> = HOLD.FR.NEXT.DATE
      FR.PRINT<1,I>     = HOLD.FR.PRINT
      FR.CLOSE.DATE<1,I>= HOLD.FR.CLOSE.DATE
      FR.D.M.FLG<1,I>   = HOLD.FR.D.M.FLG
  NEXT I
  MATWRITE FISCAL.REC ON CONTROL, CONO:FISCAL.ID
  PRINT FISCAL.ID:" REC FOR CONO ":CONO:" UPDATED"
2100 *
  RETURN
*
99999*
END
