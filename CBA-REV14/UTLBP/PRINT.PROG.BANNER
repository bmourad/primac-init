      SUBROUTINE PRINT.PROG.BANNER (CONO, CNAME, RNAME, RDATE)
***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM  - PRINT.PROG.BANNER
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 10/13/85
*
* REVISION - A.1.0
*
* DESCRIPTION
*
* This subroutine prints a report banner page containing report descriptive
* data and distribution information.
*
***************************************************************************
*
*--- FILE COPY STATEMENTS
*
*COPY PMC.CPYLIB>COMPANY
*COPY CPYLIB>REPORT
*COPY CPYLIB>CHAR
*
*--- OPEN ALL FILES
*
      IF CONO = "" THEN
         OPEN "","CONTROL" TO CONTROL ELSE
            ERRMSG = "CANNOT OPEN CONTROL FILE"
            GOSUB 90000; GOTO 99999
         END
      END
      IF CNAME = "" THEN
         OPEN "","COMPANY" TO COMPANY ELSE
            ERRMSG = "CANNOT OPEN COMPANY FILE"
            GOSUB 90000; GOTO 99999
         END
      END
      OPEN "","REPORT" TO REPORT ELSE
         ERRMSG = "CANNOT OPEN REPORT FILE"
         GOSUB 90000; GOTO 99999
      END
*
*---- MAIN PROCESSING
*
      IF CONO = "" THEN
         READV CONO FROM CONTROL,"MASTER",1 ELSE CONO = "001"
      END
      IF CNAME = "" THEN
         MATREAD COMP.REC FROM COMPANY, CONO ELSE
            MAT COMP.REC = ""
         END
         CNAME = CO.NAME
      END
      MATREAD REPORT.REC FROM REPORT, RNAME ELSE
         MAT REPORT.REC = ""
         REPORT.ID = "XXXX"
         REPORT.HEAD = RNAME
      END
      PROG.HEAD = ""
      PROG.HEAD2 = ""
      INPUT.HEAD = REPORT.HEAD
      PTR = INDEX(INPUT.HEAD,"(",1)
      IF PTR = 0 THEN
         INPUT.HEAD1 = TRIM(INPUT.HEAD)
         INPUT.HEAD2 = ""
      END ELSE
         INPUT.HEAD1 = TRIM(INPUT.HEAD[1,PTR-1])
         INPUT.HEAD2 = TRIM(INPUT.HEAD[PTR,99])
      END
      IHL = LEN(INPUT.HEAD1)
      FOR P = 1 TO IHL
         PROG.HEAD = PROG.HEAD:" ":INPUT.HEAD1[P,1]
      NEXT P
      PROG.HEAD = PROG.HEAD[2,80]
      IF INPUT.HEAD2 # "" THEN
        IHL2 = LEN(INPUT.HEAD2)
        FOR P2 = 1 TO IHL2
           PROG.HEAD2 = PROG.HEAD2:" ":INPUT.HEAD2[P2,1]
        NEXT P2
      END
*
*---- BUILD REPORT HEADING LINES
*
      HLINE1 = SPACE(INT((130-LEN(CNAME))/2)):CNAME
      HLINE1 = HLINE1 "L#130"
      HLINE2 = SPACE(INT((130-LEN(PROG.HEAD))/2)):PROG.HEAD
      HLINE2 = HLINE2 "L#130"
      IF PROG.HEAD2 # "" THEN
        HLINE3 = SPACE(INT((130-LEN(PROG.HEAD2))/2)):PROG.HEAD2
        HLINE3 = HLINE3 "L#130"
      END ELSE
        HLINE3 = ""
      END
*
*---- PRINT BANNER
*
      PRINT CHAR(12)
      PRINT
      PRINT
      PRINT
      PRINT STR("*",132)
      PRINT "*":SPACE(130):"*"
      PRINT "*":SPACE(130):"*"
      PRINT "*":SPACE(130):"*"
      PRINT "*":HLINE1:"*"
      PRINT "*":SPACE(130):"*"
      PRINT "*":SPACE(61):OCONV(RDATE,"D2/"):SPACE(61):"*"
      PRINT "*":SPACE(130):"*"
      PRINT "*":SPACE(63):REPORT.ID"L#4":SPACE(63):"*"
      PRINT "*":SPACE(130):"*"
      PRINT "*":HLINE2:"*"
      PRINT "*":SPACE(130):"*"
      IF HLINE3 # "" THEN
         PRINT "*":HLINE3:"*"
      END
      PRINT "*":SPACE(130):"*"
      PRINT "*":SPACE(130):"*"
      PRINT "*":SPACE(130):"*"
      PRINT STR("*",132)
      PRINT
      DHEAD = "DISTRIBUTION -"
      DCNT = COUNT(REPORT.DIST,VM) + (REPORT.DIST # "")
      DC = INT(DCNT/2+0.99)
      FOR N = 1 TO DC
         PRINT DHEAD"L#15":REPORT.DIST<1,N>"L#35":REPORT.DIST<1,N+DC>
         DHEAD = ""
      NEXT N
      GOTO 99999
*
*--- ERROR ROUTINE
*
90000*
      PRINT ERRMSG
      RETURN
*
*--- END OF PROGRAM
*
99999*
      RETURN
   END
