     SUBROUTINE UPDATE.MD.SUB (ACTION,SPTR,MD)
* REVISION    - [08.0]
*COPY>CPYLIB>COMMON.INSTALL
*COPY>CPYLIB>CHAR
     ERRMSG = 'NOW UPDATING ' : SYS.ACCTS(2,SPTR) : ' ACCOUNT'
     GOSUB 90000
     READ QFILE FROM MD, 'T.ACCT.MD' ELSE
        QFILE = ''
        QFILE<1> = 'Q'
        QFILE<9> = 'L'
        QFILE<10> = '10'
     END
     QFILE<2> = SYS.ACCTS(2,SPTR)
     QFILE<3> = 'DL/ID'
     WRITE QFILE ON MD, 'T.ACCT.MD'
     OPEN '','T.ACCT.MD' TO T.ACCT.MD ELSE
        ERRMSG = 'CAN NOT OPEN T.ACCT.MD FOR ' : SYS.ACCTS(2,SPTR) : ' ACCOUNT'
        GOTO 93000
     END
     DATA = 1
     IF ACTION = 4 THEN
        QFILE<2> = SYS.ACCTS(1,SPTR)
        WRITE QFILE ON MD, 'F.ACCT.MD'
        OPEN '','F.ACCT.MD' TO F.ACCT.MD ELSE
           ERRMSG = 'CAN NOT OPEN F.ACCT.MD FOR ' : SYS.ACCTS(1,SPTR) : ' ACCOUNT'
           GOTO 93000
        END
        SELECT F.ACCT.MD
        LOOP
           READNEXT ID ELSE DATA = 0
        WHILE DATA DO
           READ ITEM FROM T.ACCT.MD, ID ELSE
              READ ITEM FROM F.ACCT.MD, ID ELSE ITEM = ''
              IF ITEM<1>[1,1] # 'D' THEN
                 WRITE ITEM ON T.ACCT.MD, ID
              END
           END
           GOSUB 1000
        REPEAT
     END ELSE
        SELECT T.ACCT.MD
        LOOP
           READNEXT ID ELSE DATA = 0
        WHILE DATA DO
           READ ITEM FROM T.ACCT.MD, ID ELSE ITEM = ''
           GOSUB 1000
        REPEAT
     END
     READ ITEM FROM T.ACCT.MD, SYS.ACCTS(1,SPTR) ELSE ITEM = ''
     IF ITEM # '' THEN
        WRITE ITEM ON T.ACCT.MD, SYS.ACCTS(2,SPTR)
        WRITE ITEM ON T.ACCT.MD, "M"
        WRITE ITEM ON T.ACCT.MD, "MAIN"
        DELETE T.ACCT.MD, SYS.ACCTS(1,SPTR)
     END
     GOTO 99999
1000 BEGIN CASE
     CASE ITEM<1> = 'Q'
        BEGIN CASE
        CASE ITEM<2> = SYS.ACCTS(1,1)
           ITEM<2> = SYS.ACCTS(2,1)
           WRITE ITEM ON T.ACCT.MD, ID
        CASE ITEM<2> = SYS.ACCTS(1,2)
           ITEM<2> = SYS.ACCTS(2,2)
           WRITE ITEM ON T.ACCT.MD, ID
        CASE FIELD(ITEM<2>,"-",1) = SYS.ACCTS(1,2)
           ITEM<2> = SYS.ACCTS(2,2) : "-" : FIELD(ITEM<2>,"-",2)
           WRITE ITEM ON T.ACCT.MD, ID
        END CASE
     CASE ITEM<1> = "P" AND ITEM<2> = "10B4" AND FIELD(ITEM<5>," ",1) = SYS.ACCTS(1,1)
        NAME = FIELD(ITEM<5>," ",2)
        IF NAME = "" THEN
           ITEM<5> = SYS.ACCTS(2,1)
        END ELSE
           ITEM<5> = SYS.ACCTS(2,1) : " " : NAME
        END
        WRITE ITEM ON T.ACCT.MD, ID
     END CASE
     RETURN
90000 PRINT @(0,23) : ERRMSG : CL :
      RETURN
93000 PRINT @(0,23) : ERRMSG : CL :
      INPUT X :
      ACTION = 0
99999 RETURN
      END
