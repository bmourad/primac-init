*
**********************************************
* REVISION    - [11.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - CNVBP
* PROGRAM     - FIX.VOC.PQN.REV11
* BY          - Diane Tovar CBA
* DATE        - 03/27/1997
*
* DESCRIPTION - This program will delete the VOC PQN items that are now
*             - run via the PMC_PROCESS file.
*ENDDOC
**********************************************
*
   PRINT "PROCESSING VOC PQN ITEMS IN ACCOUNT ":@PATH
*
   HOMEPATH = @PATH
   NUM.SLASH=DCOUNT(HOMEPATH,"/")
   PMCPATH=''
   HOMENAME=FIELD(HOMEPATH,"/",NUM.SLASH)
   IF DCOUNT(HOMENAME,"-") = 2 THEN
      IF FIELD(HOMENAME,"-",1) = "CBA" THEN
         PMCNAME = HOMENAME
      END ELSE
         PMCNAME = "CBA-":FIELD(HOMENAME,"-",1)
      END
   END ELSE
      PMCNAME = "CBA-":HOMENAME
   END
   IF PMCNAME # HOMENAME THEN
      PMCPATH = "../":PMCNAME
      STMT = "SETFILE ":PMCPATH:"/PMC_PROCESS_XREF PMC_PROCESS_XREF OVERWRITING"
      PRINT STMT
      UDTEXECUTE STMT CAPTURING RESPONSE
   END
*
*   FILE OPENS
*
   OPEN "VOC" TO VOC ELSE 
      PRINT "NO VOC FILE..   NO UPDATES DONE"
      STOP
   END
   OPEN "PMC_PROCESS_XREF" TO PMC_PROCESS_XREF ELSE 
      PRINT "NO PMC_PROCESS_XREF FILE..   NO UPDATES DONE"
      STOP
   END
*
   SELECT VOC
   DONE = 0
   LOOP
      READNEXT VOCID ELSE DONE = 1
   UNTIL DONE DO
      READU VOC.REC FROM VOC,VOCID THEN
         IF VOC.REC<1> = "PQN" THEN
            READ REC FROM PMC_PROCESS_XREF,VOCID THEN
               DELETE VOC, VOCID
            END ELSE
               PRINT "VOC PQN ITEM ***  ":VOCID:"  *** NOT SETUP AS A PROCESS ! "
            END
         END ELSE
            RELEASE VOC, VOCID
         END
      END ELSE
         RELEASE VOC, VOCID
         PRINT "VOC RECORD ":VOCID:" MISSING "
      END
   REPEAT
*
   STOP
END
