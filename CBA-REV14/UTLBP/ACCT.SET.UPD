*********************************************************************
*
* SYSTEM        - PRIMAC
*
* PROGRAM       - OVERNIGHT.PROCESSOR.INSTALL
*
* AUTHOR        - NICK AMENDOLA - COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 03/27/92
*
* DESCRIPTION
*
* This program is used to install the overnight report processor
* into a specified base account.
*
* The following Q-pointer are created in the relavent accounts.
*
*      ONP.SCREENS     - points to the CBA account
*      OVERNIGHT.PROCS - points to the main account
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>MENUS.CONTROL
*COPY>CPYLIB>CHAR
*
      DIM ACCT.MDICT(30)
*
*---- OPEN ALL FILE
*
      OPEN "","VOC" TO MDICT ELSE
         ERRMSG = "Cannot locate the MDICT file"
         GOSUB 90000; GOTO 99999
      END
*
*---- INITIALIZATION
*
      PROMPT ""
      PORT = @TTY
      PORT = FIELD(PORT,"/",1)[4,99]
      CUR.PATH = @PATH
*
      QF.ID="QF_":PORT
      READU QFILE FROM MDICT, QF.ID ELSE QFILE=""
      O.QFILE=QFILE
      QFILE="F"
*
      PRINT @(-1)
      SCRN = ""
      SCRN = SCRN:@(20,00):"OVERNIGHT PROCESSOR INSTALLATION"
      SCRN = SCRN:@(00,01):STR("-",80)
      SCRN = SCRN:@(00,20):STR("-",80)
      SCRN = SCRN:@(00,22):STR("-",80)
      SCRN = SCRN:@(05,05):"This program is used to install the Overnight Processor."
      SCRN = SCRN:@(05,07):"The following file should be created prior to running this process:"
      SCRN = SCRN:@(05,09):"     OVERNIGHT.PROCS - created in the base account (modulo = 3)."
      SCRN = SCRN:@(05,12):"The following Q-pointer are created in the relevant accounts:"
      SCRN = SCRN:@(05,14):"     ONP.SCREENS     - points to the CBA account"
      SCRN = SCRN:@(05,15):"     OVERNIGHT.PROCS - points to the main account"
      PRINT SCRN:
*
*---- MAIN PROCESSING
*
100 *
      PRINT @(0,21):@(-4):"Base Account (With Full Path) : ":
      INPUT ACCT,30:_
      WARNMSG = ""
      BEGIN CASE
      CASE ACCT = "END"
         GOTO 99997
      CASE ACCT = CHAR(27)
         GOTO 99997
      CASE ACCT = "^"
         GOTO 99997
      CASE ACCT = ""
         GOTO 99997
      END CASE
      POS = INDEX(ACCT,"/",COUNT(ACCT,"/"))
      ACCT.PATH = ACCT[1,POS]
      ACCOUNT.NAME = ACCT[POS+1,99]
      BEGIN CASE
      CASE ACCOUNT.NAME = "CBA" OR ACCOUNT.NAME[1,4] = "CBA-"
         ERRMSG = "Cannot utilize the CBA software account"
         GOSUB 90000; GOTO 100
      CASE INDEX(ACCOUNT.NAME,"-",1)
         ERRMSG = "Cannot utilize a dash (-) in the account name"
         GOSUB 90000; GOTO 100
      END CASE
      BASEACCT = FIELD(ACCOUNT.NAME,"-",1)
      QFILE<2> = ACCT.PATH:BASEACCT:"/VOC"
      QFILE<3> = ACCT.PATH:BASEACCT:"/D_VOC"
      WRITEU QFILE ON MDICT, QF.ID
      OPEN "",QF.ID TO ACCT.MDICT(2) ELSE
         ERRMSG = "(":ACCT.PATH:BASEACCT:") is an invalid account"
         GOSUB 90000; GOTO 100
      END
      QFILE<2> = ACCT.PATH:BASEACCT:"/CONTROL"
      QFILE<3> = ACCT.PATH:BASEACCT:"/D_CONTROL"
      WRITEU QFILE ON MDICT, QF.ID
      OPEN "",QF.ID TO CONTROL ELSE
         ERRMSG = "Cannot locate the CONTROL file for account (":ACCT.PATH:BASEACCT:")"
         GOSUB 90000; GOTO 100
      END
      MATREAD MENU.REC FROM CONTROL, "MENUS.CONTROL" ELSE
         ERRMSG = "Cannot locate CONTROL, MENUS.CONTROL"
         GOSUB 90000; GOTO 100
      END
      CBAACCT = CBA.PATH[INDEX(CBA.PATH,"/",COUNT(CBA.PATH,"/"))+1,99]
      QFILE<2> = CBA.PATH:"/VOC"
      QFILE<3> = CBA.PATH:"/D_VOC"
      WRITEU QFILE ON MDICT, QF.ID
      OPEN "",QF.ID TO ACCT.MDICT(1) ELSE
         ERRMSG = "Cannot locate the CBA account (":CBA.PATH:")"
         GOSUB 90000; GOTO 100
      END
      ACNT = 2
      ACCTNAMES = CBAACCT
      ACCTNAMES<ACNT> = BASEACCT
*
*---- Get all accounts
*
      CNT = DCOUNT(VALID.SYS,VM)
      FOR I = 1 TO CNT
         SUBACCT = BASEACCT:"-":VALID.SYS<1,I>
         QFILE<2> = ACCT.PATH:SUBACCT:"/VOC"
         QFILE<3> = ACCT.PATH:SUBACCT:"/D_VOC"
         WRITEU QFILE ON MDICT, QF.ID
         ACNT = ACNT + 1
         OPEN "",QF.ID TO ACCT.MDICT(ACNT) THEN
            ACCTNAMES<ACNT> = SUBACCT
         END ELSE
            ACNT = ACNT - 1
         END
      NEXT I
*
*---- Build IDLIST
*
      IDLIST = "ONP.SCREENS"
      IDLIST<2> = "OVERNIGHT.PROCS"
*
*---- Update the accounts list with the IDLIST
*
      IDCNT = DCOUNT(IDLIST,AM)
      FOR I = 1 TO IDCNT
         ID = IDLIST<I>
         READ MREC FROM MDICT, ID ELSE GOTO 907
         PRINT @(-1):
         PRINT @(0,1):I "L#6":" From ":CUR.PATH:" , ":ID:
         FOR R = 1 TO 5
            IF MREC<R> # "" THEN
               PRINT @(0,R+2):R "L#3":MREC<R>
            END
         NEXT R
         FOR J = 1 TO ACNT
            PRINT @(0,10):J "L#6":" To ":ACCTNAMES<J>:" , ":ID:
            READU REC FROM ACCT.MDICT(J), ID THEN
               GOTO 903
            END
            BEGIN CASE
            CASE MREC<1> = "PQN"
               WRITE MREC ON ACCT.MDICT(J), ID
               PRINT @(0,11):"SAME RECORD":
            CASE MREC<1> = "F" OR MREC<1> = "DIR"
               BEGIN CASE
               CASE REC<2>[1,3] = "../"
                  GOTO 903
               CASE REC<3>[1,3] = "../"
                  GOTO 903
               END CASE
               REC = MREC
               IF NOT(INDEX(REC<2>,"/",1)) THEN
                  REC<2> = CUR.PATH:"/":REC<2>
               END
               IF NOT(INDEX(REC<3>,"/",1)) THEN
                  REC<3> = CUR.PATH:"/":REC<3>
               END
               WRITE REC ON ACCT.MDICT(J), ID
               FOR R = 1 TO 5
                  IF REC<R> # "" THEN
                     PRINT @(0,R+11):R "L#3":REC<R>
                  END
               NEXT R
            END CASE
            GOTO 905
903*
            PRINT @(0,11):"NO UPDATE":
            RELEASE ACCT.MDICT(J), ID
905*
         NEXT J
907*
      NEXT I
      GOTO 99997
*
*---- ERROR ROUTINE
*
90000 *
      PRINT @(0,23):@(-4):CHAR(7):ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- END OF PROGRAM
*
99997*
      IF O.QFILE="" THEN
         DELETE MDICT, QF.ID
      END ELSE
         WRITE O.QFILE ON MDICT, QF.ID
      END
99999 *
      PRINT @(-1)
   END
