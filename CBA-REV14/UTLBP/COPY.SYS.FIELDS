*COPY>CPYLIB>CHAR
* PROGRAM TO COPY DATA FOR A COMPANY TO A NEW COMPANY WITHIN AN ACCOUNT.
* IF DATA ALREADY EXISTS IN FILE FOR TARGET COMPANY, NO COPY OF THAT FILE
* DATA WILL OCCUR.
* A LIST OF FILES IS SUPPLIED TO THIS PROGRAM TO COPY.
* LMR 1/26/96
PROMPT''
OPEN 'SYS_FIELDS' TO COMPANY ELSE STOP
DREC=''
CRT @(-1)
CRT "    ---  COPY DATA FROM ONE COMPANY NO. TO ANOTHER  ---"
10 CRT @(10,5):"FROM JIS TICKET - ":;INPUT FRM:
   IF FRM = "END" THEN STOP
   FRM=FRM 'R%3'
20 CRT @(10,7):"  TO JIS TICKET - ":;INPUT TOO:
   IF TOO = "END" THEN STOP
   TOO=TOO 'R%3'
30 CRT @(5,9):"OK TO CONTINUE ? Y/N ":;INPUT ANS:
   IF ANS # 'Y' AND ANS # 'N' THEN GOTO 30
   IF ANS = 'N' THEN STOP
MORE = 1
LOOP
  READNEXT FID ELSE MORE = 0
WHILE MORE DO
  DREC<-1> = FID
REPEAT
FOR I = 1 TO DCOUNT(DREC,AM)
  FID = DREC<I>
  OPEN FID TO XFILE THEN
    CRT @(0,16):CL:"Now processing ":FID:
    STMT='SELECT ':FID:' WITH CONO = "':TOO:'"'
    UDTEXECUTE STMT CAPTURING JUNK
    IF JUNK[2,7] = 'No data' THEN
       STMT='SELECT ':FID:' WITH CONO = "':FRM:'"'
       UDTEXECUTE STMT CAPTURING JUNK
       IF JUNK[2,7] # 'No data' THEN
          MORE2 = 1
          LOOP
            READNEXT OID ELSE MORE2 = 0
          WHILE MORE2 DO
            READ REC FROM XFILE,OID THEN
              NID = TOO:OID[4,99]
              WRITE REC ON XFILE,NID
            END
          REPEAT
       END
    END ELSE
       UDTEXECUTE 'CLEARSELECT'
    END
  END ELSE CRT @(0,19):CL:'CANNOT OPEN ':FID:;INPUT XX:
NEXT I
CRT @(0,22):CL:'FINISHED PROCESSING!':
