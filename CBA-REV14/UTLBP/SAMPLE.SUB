      SUBROUTINE SAMPLE.SUB(ACTION, CONO, SAMPLE.ID, SAMPLE, STATUS)
*********************************************************************
*
* Copyright 1982 by Vercom Software Inc.
*
* REVISION - [NN.N]
*
* PROGRAM  - SAMPLE.SUB
*
* AUTHOR   - YOUR NAME HERE, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - MM/DD/YY
*
* DESCRIPTION
*
* This program is used to maintain a sample file for the purpose of
* demonstrating the use of a SCREEN.INPUT subroutine.
*
* The subroutine is passed the key of the item to be maintained and
* the file variable of the file.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SAMPLE
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      SCREEN DEFINE;SAMPLE.MAINT
      SCREEN FORMAT
      SCREEN COUNT;;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      LINE.CNT = 0
      REF.NO = ""
      CURR.REF.NO = ""
      STATUS = ""
      DUP.ID = ""
*
*---- MAIN PROCESSING
*
100 *
      S$DATA(2)<S$SCR> = SAMPLE.ID
      SCREEN DISPLAY;;2
*
      NEW.REC = 0
      IF ACTION = "M" THEN
         MATREADU SAMPLE.REC FROM SAMPLE, CONO:SAMPLE.ID ELSE
            MAT SAMPLE.REC = ""
            NEW.REC = 1
         END
      END ELSE
         MATREAD SAMPLE.REC FROM SAMPLE, CONO:SAMPLE.ID ELSE
            ERRMSG = "Invalid Item ID"
            GOSUB 90000
            STATUS = ERRMSG
            GOTO 99999
         END
      END
120 *
      IF NEW.REC THEN
         S$DATA(31)<S$SCR> = ""
         SCREEN FIELD;;31
         SCREEN INPUT;;31;GOTO 98000
         IF S$VALUE = "Y" THEN
            S$DATA(32)<S$SCR> = DUP.ID
            SCREEN FIELD;;32
            SCREEN INPUT;;32;GOTO 98000
            MATREAD SAMPLE.REC FROM SAMPLE, CONO:S$VALUE ELSE
               ERRMSG = "Invalid Item ID. Try again! "
               GOSUB 90000
               GOTO 120
            END
            DUP.ID = S$VALUE
            NEW.REC = 0
            GOTO 120
         END
         FOR REF = 1 TO 8
            ON REF GOSUB 1010,1020,1030,1040,1050,1060,1070,1080
            IF S$VALUE = "END" THEN GOTO 98000
         NEXT REF
         LINE.CNT = 0
         OPT = "A"
         GOTO 510
      END ELSE
         GOSUB 80000
         GOSUB 81000
         LINE.CNT = DCOUNT(SAMPLE.MF1,VM)
         REF.NO = 1
         CURR.REF.NO = ""
         GOSUB 50000
      END
*
*---- GET OPERATOR REPLY
*
500 *
      BEGIN CASE
      CASE ACTION = "M"
         SCREEN FIELD;;21
         SCREEN INPUT;;21
      CASE 1
         SCREEN FIELD;;23
         SCREEN INPUT;;23
      END CASE
      OPT = S$VALUE
510 *
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         STATUS = "E"
         RELEASE SAMPLE, CONO:SAMPLE.ID
         GOTO 99999
      CASE NUM(OPT) AND OPT # ""
         ON OPT GOSUB 1010,1020,1030,1040,1050,1060,1070,1080
      CASE OPT = "A" AND LINE.CNT < 99
         MODE = "A"
         DONE = 0
         FOR REF.NO = LINE.CNT+1 TO 99 UNTIL DONE
            GOSUB 50000
            GOSUB 10000
            IF S$VALUE = "END" THEN
               DONE = 1
               GOSUB 700
            END ELSE
               LINE.CNT = LINE.CNT + 1
            END
         NEXT REF.NO
         REF.NO = LINE.CNT
         CURR.REF.NO = ""
         GOSUB 50000
      CASE OPT = "C" AND LINE.CNT > 0
         MODE = "C"
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 10000
            IF S$VALUE = "END" THEN
               CURR.REF.NO = ""
               GOSUB 50000
            END
         END
      CASE OPT = "D" AND LINE.CNT > 0
         MODE = "D"
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 700
            LINE.CNT = LINE.CNT - 1
            IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
            CURR.REF.NO = ""
            GOSUB 50000
         END
      CASE OPT = "I" AND LINE.CNT > 0
         MODE = "I"
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 800
            LINE.CNT = LINE.CNT + 1
            CURR.REF.NO = ""
            GOSUB 50000
            GOSUB 10000
            IF S$VALUE = "END" THEN
               GOSUB 700
               LINE.CNT = LINE.CNT - 1
               CURR.REF.NO = ""
               GOSUB 50000
            END
         END
      CASE OPT = "S" OR OPT = "SF"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > LINE.CNT THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "SR"
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "ST"
         REF.NO = 1
         GOSUB 50000
      CASE OPT = "SB"
         REF.NO = LINE.CNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "R"
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            S$SCR = S$SCR + 1
*           CALL ANOTHER.SUBROUTINE(AS1,AS2,AS3)
            PRINT @(-1)
            PRINT @(20,12):"S U B R O U T I N E   S I M U L A T I O N"
            SLEEP 3
*
            S$SCR = S$SCR - 1
            SCREEN FORMAT
            GOSUB 80050
            CURR.REF.NO = ""
            GOSUB 50000
         END
      CASE OPT = "P"
         SCREEN FIELD;;24
         SCREEN INPUT;;24
         IF S$VALUE = "Y" THEN
            DELETE SAMPLE, CONO:SAMPLE.ID
            STATUS = "P"
            GOTO 99999
         END
      CASE OPT = "F"
         MATWRITE SAMPLE.REC ON SAMPLE, CONO:SAMPLE.ID
         STATUS = "F"
         GOTO 99999
      END CASE
      GOTO 500
*
*---- GET LINE NUMBER
*
600 *
      SCREEN FIELD;;22
      S$MINV = CURR.REF.NO
      S$MAXV = S$MINV + LINE.COUNT - 1
      IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
      SCREEN INPUT;;22
      RETURN
*
*---- DELETE MULTI-LINE DATA
*
700 *
      SAMPLE.MF1 = DELETE(SAMPLE.MF1,1,REF.NO,0)
      SAMPLE.MF2 = DELETE(SAMPLE.MF2,1,REF.NO,0)
      SAMPLE.MF3 = DELETE(SAMPLE.MF3,1,REF.NO,0)
      SAMPLE.MF4 = DELETE(SAMPLE.MF4,1,REF.NO,0)
      SAMPLE.MF5 = DELETE(SAMPLE.MF5,1,REF.NO,0)
      SAMPLE.MF6 = DELETE(SAMPLE.MF6,1,REF.NO,0)
      SAMPLE.MF7 = DELETE(SAMPLE.MF7,1,REF.NO,0)
      SAMPLE.MF8 = DELETE(SAMPLE.MF8,1,REF.NO,0)
      GOSUB 81000
      RETURN
*
*---- INSERT MULTI-LINE DATA
*
800 *
      SAMPLE.MF1 = INSERT(SAMPLE.MF1,1,REF.NO,0,"")
      SAMPLE.MF2 = INSERT(SAMPLE.MF2,1,REF.NO,0,"")
      SAMPLE.MF3 = INSERT(SAMPLE.MF3,1,REF.NO,0,"")
      SAMPLE.MF4 = INSERT(SAMPLE.MF4,1,REF.NO,0,"")
      SAMPLE.MF5 = INSERT(SAMPLE.MF5,1,REF.NO,0,"")
      SAMPLE.MF6 = INSERT(SAMPLE.MF6,1,REF.NO,0,"")
      SAMPLE.MF7 = INSERT(SAMPLE.MF7,1,REF.NO,0,"")
      SAMPLE.MF8 = INSERT(SAMPLE.MF8,1,REF.NO,0,"")
      GOSUB 81000
      RETURN
*
*---- GET FIELD 1
*
1010 *
      SCREEN FIELD;;3
      SCREEN INPUT;;3;RETURN
      SAMPLE.F1 = S$VALUE
      RETURN
*
*---- GET FIELD 2
*
1020 *
      SCREEN FIELD;;4
      SCREEN INPUT;;4;RETURN
      SAMPLE.F2 = S$VALUE
      RETURN
*
*---- GET FIELD 3
*
1030 *
      SCREEN FIELD;;5
      SCREEN INPUT;;5;RETURN
      SAMPLE.F3 = S$VALUE
      RETURN
*
*---- GET FIELD 4
*
1040 *
      SCREEN FIELD;;6
      SCREEN INPUT;;6;RETURN
      SAMPLE.F4 = S$VALUE
      RETURN
*
*---- GET FIELD 5
*
1050 *
      SCREEN FIELD;;7
      SCREEN INPUT;;7;RETURN
      SAMPLE.F5 = S$VALUE
      RETURN
*
*---- GET FIELD 6
*
1060 *
      SCREEN FIELD;;8
      SCREEN INPUT;;8;RETURN
      SAMPLE.F6 = S$VALUE
      RETURN
*
*---- GET FIELD 7
*
1070 *
      SCREEN FIELD;;9
      SCREEN INPUT;;9;RETURN
      SAMPLE.F7 = S$VALUE
      RETURN
*
*---- GET FIELD 8
*
1080 *
      SCREEN FIELD;;10
      SCREEN INPUT;;10;RETURN
      SAMPLE.F8 = S$VALUE
      RETURN
*
*---- GET MULTI-LINE DATA
*
10000 *
      S$VAL = REF.NO
      SCREEN DISPLAY;;11
10100 *
      S$VAL = REF.NO
      SCREEN FIELD;;12
      SCREEN INPUT;;12;GOTO 19950
      TEMP.MF1 = S$VALUE
10200 *
      S$VAL = REF.NO
      SCREEN FIELD;;13
      SCREEN INPUT;;13;GOTO 19950
      TEMP.MF2 = S$VALUE
10300 *
      S$VAL = REF.NO
      SCREEN FIELD;;14
      SCREEN INPUT;;14;GOTO 19950
      TEMP.MF3 = S$VALUE
10400 *
      S$VAL = REF.NO
      SCREEN FIELD;;15
      SCREEN INPUT;;15;GOTO 19950
      TEMP.MF4 = S$VALUE
10500 *
      S$VAL = REF.NO
      SCREEN FIELD;;16
      SCREEN INPUT;;16;GOTO 19950
      TEMP.MF5 = S$VALUE
10600 *
      S$VAL = REF.NO
      SCREEN FIELD;;17
      SCREEN INPUT;;17;GOTO 19950
      TEMP.MF6 = S$VALUE
10700 *
      S$VAL = REF.NO
      SCREEN FIELD;;18
      SCREEN INPUT;;18;GOTO 19950
      TEMP.MF7 = S$VALUE
10800 *
      S$VAL = REF.NO
      SCREEN FIELD;;19
      SCREEN INPUT;;19;GOTO 19950
      TEMP.MF8 = S$VALUE
19900 *
      SAMPLE.MF1<1,REF.NO> = TEMP.MF1
      SAMPLE.MF2<1,REF.NO> = TEMP.MF2
      SAMPLE.MF3<1,REF.NO> = TEMP.MF3
      SAMPLE.MF4<1,REF.NO> = TEMP.MF4
      SAMPLE.MF5<1,REF.NO> = TEMP.MF5
      SAMPLE.MF6<1,REF.NO> = TEMP.MF6
      SAMPLE.MF7<1,REF.NO> = TEMP.MF7
      SAMPLE.MF8<1,REF.NO> = TEMP.MF8
      RETURN
19950 *
      GOSUB 81000
      RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
      START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
      SCREEN MULTI;;C;11;12;13;14;15;16;17;18;19
      RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
      S$DATA(1)<S$SCR> = DATE()
      S$DATA(2)<S$SCR> = SAMPLE.ID
      S$DATA(3)<S$SCR> = SAMPLE.F1
      S$DATA(4)<S$SCR> = SAMPLE.F2
      S$DATA(5)<S$SCR> = SAMPLE.F3
      S$DATA(6)<S$SCR> = SAMPLE.F4
      S$DATA(7)<S$SCR> = SAMPLE.F5
      S$DATA(8)<S$SCR> = SAMPLE.F6
      S$DATA(9)<S$SCR> = SAMPLE.F7
      S$DATA(10)<S$SCR> = SAMPLE.F8
80050 *
      SCREEN DISPLAY;;ALL
      RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
      S$DATA(12)<S$SCR> = SAMPLE.MF1
      S$DATA(13)<S$SCR> = SAMPLE.MF2
      S$DATA(14)<S$SCR> = SAMPLE.MF3
      S$DATA(15)<S$SCR> = SAMPLE.MF4
      S$DATA(16)<S$SCR> = SAMPLE.MF5
      S$DATA(17)<S$SCR> = SAMPLE.MF6
      S$DATA(18)<S$SCR> = SAMPLE.MF7
      S$DATA(19)<S$SCR> = SAMPLE.MF8
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      PRINT @(0,23):@(-4):ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- EXIT PROGRAM
*
98000 *
      STATUS = "E"
      RELEASE SAMPLE, CONO:SAMPLE.ID
*
*---- END OF PROGRAM
*
99999 *
      SCREEN CLEAR;;D
      RETURN
   END
