      SUBROUTINE GET.PRIME(NUMBER,LO.PRIME,HI.PRIME,STATUS)
********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM     - GET.PRIME
* BY          - JIHAD A. YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 02/10/89
* DESCRIPTION
* This routine calculate the lowest and the hightest prime number
* to the number passed to this routine and pass the prime numbers
* back to the calling program pluse a status if any problem.
*
*---- GET NUMBER
*
100*
    STATUS = ""
    BEGIN CASE
    CASE NOT(NUM(NUMBER))
       STATUS = "*** Please enter a number alphnumeric not allowed"
    CASE NUMBER < 0
       STATUS = "*** Please enter a number greater than zero"
    CASE INT(NUMBER) # NUMBER
       STATUS = "*** Please enter a number decimal not allowed"
    CASE 1
       GOSUB 200 ; GOSUB 300
    END CASE
    GOTO 99999
*
*---- FIND PRIME NUMBER LOWER THAN ENTERED
*
200*
    TRAIL = NUMBER
    LOOP
       GOSUB 400
    UNTIL MORE DO
       TRAIL = TRAIL - 1
    REPEAT
    LO.PRIME = TRAIL
    RETURN
*
*---- FIND PRIME NUMBER HIGHER THE ENTERED
*
300*
    TRAIL = NUMBER
    LOOP
       GOSUB 400
    UNTIL MORE DO
       TRAIL = TRAIL + 1
    REPEAT
    HI.PRIME = TRAIL
    RETURN
*
*---- CHECK TRAIL IF PRIME
*
400*
    MORE = 1
    IF TRAIL < 4 THEN GOTO 499
    LIMIT = INT(SQRT(TRAIL)) + 1
    FOR I = 2 TO LIMIT UNTIL MORE = 0
       IF NOT(MOD(TRAIL,I)) THEN MORE = 0
    NEXT I
499*
    RETURN
*
*---- END OF PROGRAM
*
99999*
    RETURN
 END
