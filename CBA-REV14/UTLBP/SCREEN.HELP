      SUBROUTINE SCREEN.HELP(ID, SHEAD)
*********************************************************************
*
* PROGRAM  - SCREEN.HELP
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/06/91
*
* DESCRIPTION
*
* This program is used to display help messages.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
      OPEN "","SCREEN.HELP.FILE" TO HFILE ELSE
         ERRMSG = "CANNOT LOCATE SCREEN.HELP.FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","UTL.SCREENS" TO UTL.SCREENS ELSE
         ERRMSG = "CANNOT LOCATE UTL.SCREENS FILE"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      SCREEN DEFINE;SCREEN.HELP;UTL.SCREENS
      SCREEN FORMAT
      SCREEN COUNT;;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      LINE.CNT = 0
      REF.NO = ""
      CURR.REF.NO = ""
      START.REF.NO = ""
*
*---- MAIN PROCESSING
*
100*
      READ HREC FROM HFILE, ID ELSE HREC = ""
      GOSUB 80000
      LINE.CNT = DCOUNT(HREC,AM)
      GOSUB 81000
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
500*
      SCREEN FIELD;;21
      SCREEN INPUT;;21
      OPT = S$VALUE
510*
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 99999
      CASE OPT[1,1] = "S" AND LINE.CNT > 0
         SSFX = OPT[2,99]
         ERRMSG = ""
         BEGIN CASE
         CASE SSFX = ""
            START.REF.NO = CURR.REF.NO + LINE.COUNT
            IF START.REF.NO > LINE.CNT THEN START.REF.NO = 1
         CASE SSFX = "F"
            START.REF.NO = CURR.REF.NO + LINE.COUNT
            IF START.REF.NO > LINE.CNT THEN START.REF.NO = CURR.REF.NO
         CASE SSFX = "R"
            START.REF.NO = CURR.REF.NO - LINE.COUNT
            IF START.REF.NO < 1 THEN START.REF.NO = 1
         CASE SSFX = "T"
            START.REF.NO = 1
         CASE SSFX = "B"
            START.REF.NO = LINE.CNT - LINE.COUNT + 1
            IF START.REF.NO < 1 THEN START.REF.NO = 1
         CASE NUM(SSFX)
            IF SSFX > 0 AND SSFX <= LINE.CNT THEN
               START.REF.NO = SSFX
            END ELSE
               ERRMSG = "** OUT OF RANGE **"
            END
         CASE 1
            ERRMSG = "** INVALID REQUEST **"
         END CASE
         IF ERRMSG = "" THEN
            GOSUB 50000
         END ELSE
            GOSUB 90000
         END
      END CASE
      GOTO 500
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000*
      IF START.REF.NO = "" THEN
         START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
      END
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
      SCREEN MULTI;;C;11;12
      START.REF.NO = ""
      RETURN
*
*---- LOAD SCREEN DATA
*
80000*
      S$DATA(1)<S$SCR> = DATE()
      IF SHEAD = "" THEN DHEAD = "HELP SCREEN" ELSE DHEAD = SHEAD
      LHEAD = LEN(DHEAD)
      IF LHEAD <= (50/2) THEN
         EHEAD = DHEAD[1,1]
         FOR N = 2 TO LHEAD
            EHEAD = EHEAD:" ":DHEAD[N,1]
         NEXT N
         LHEAD = LEN(EHEAD)
         S$DATA(2)<S$SCR> = SPACE((50-LHEAD)/2):EHEAD
      END ELSE
         S$DATA(2)<S$SCR> = SPACE((50-LHEAD)/2):DHEAD
      END
80090*
      SCREEN DISPLAY;;ALL
      RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000*
      S$DATA(12)<S$SCR> = ""
      FOR REF = 1 TO LINE.CNT
         S$DATA(12)<S$SCR,REF> = HREC<REF>
      NEXT REF
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):@(-4):ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- END OF PROGRAM
*
99999*
      SCREEN CLEAR;;D
      RETURN
   END
