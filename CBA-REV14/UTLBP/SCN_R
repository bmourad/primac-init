*COPY>CPYLIB>COM1
*******************************************
* REVISION      - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - DEVBP
* PROGRAM       - SCN.PRINT
* BY            - BILAL MOURAD
* DATE          - 6/9/95
* DESCRIPTION   -
*******************************************
*
*--- Data Base Libraries
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JIS.CPYLIB>SYS_SCN_DEF
*COPY>JIS.CPYLIB>SYS_FIELDS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*--- Open Files
*
OPEN "","CUSTOMER" TO CUSTOMER ELSE
  ERRMSG = "CUSTOMER FILE IS MISSING"
  GOSUB 93000
END
OPEN "","SYS_FIELDS" TO SYS_FIELDS ELSE
  ERRMSG = "SYS_FIELDS FILE IS MISSING"
  GOSUB 93000
END
OPEN "","SYS_SCN_DEF" TO SYS_SCN_DEF ELSE
  ERRMSG = "SYS_SCN_DEF FILE IS MISSING"
  GOSUB 93000
END
*
*--- Read Proc Values
*
PROCREAD ID ELSE
  ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC."
  GOSUB 93000
END
CONO = ID<1>; CONO.NAME = ID<2>
*
*--- Get The First Two Lines of Heading
*
HD1 = ""
REPORT.NAME = "S C R E E N    D E F I N I T I O N   ( ":ID<3>:" )"
REPORT.NUMBER = "REPORT # : XXXX"
LINE.FILL = STR(" ",44)
REPORT.HD = ID<2>:ID<3>:" )"
REPORT.HD2 = REPORT.NUMBER:LINE.FILL:REPORT.NAME
*
*--- INT
*
S.HD1 ="   Main File    Att             Field                     Table         Prefix    MVNO/SVNO    H T SEQ REF X/Y FMT/CNV Dist List"
S.HD2 ="--------------- --- ------------------------------ -------------------- ------ --------------- - - --- --- --- ------- ---------"
S.HD3 = STR(" ",128)
*
*--- Main Processing
*
LINES = 0
KEY = ID<3>
MATREAD SSD.REC FROM SYS_SCN_DEF,KEY ELSE
  ERRMSG = "CANNOT ACCESS RECORD ":KEY:" IN SYS_SCN_DEF FILE."
  GOSUB 93000
END
X = ""
VCNT = DCOUNT(SSD_FIELDS,@VM)
FOR I = 1 TO VCNT
  X<1,I> = SSD_FIELDS<1,I>
  LINES = LINES + 1
  MATREAD DFD.REC FROM SYS_FIELDS, X<1,I> THEN
    IF DFD_DESC # "" THEN
      X<1,I,2> = DFD_DESC 
      LINES = LINES + 1
    END
  END
  SMCNT = DCOUNT(X<1,I>,@SM)
  FOR J = 1 TO SMCNT
    IF SSD_FLD_EXPR<1,I,J> # "" THEN
      X<1,I,J+2> = SSD_FLD_EXPR<1,I,J>
      LINES = LINES + 1
    END
  NEXT J
NEXT I
*
*--- Print Report
*
CNT = 0
PAGE.CNT = 0
PAGE.SIZE = 55
P.HD = "PAGE"
PRINTER ON
GOSUB 100
FOR I = 1 TO VCNT
  LINE = DCOUNT(X<1,I>,@SM)
  IF LINE < 2 THEN LINE = 2
  FOR J = 1 TO LINE
    P.LINE = ""
    P.LINE = SSD_M_TABLE<1,I,J> "L#15":" "
    P.LINE = P.LINE:SSD_FLD_ATTNO<1,I,J> "R#3":" "
    P.LINE = P.LINE:X<1,I,J> "L#30":" "
    P.LINE = P.LINE:SSD_FLD_TABLE<1,I,J> "L#20":" "
    P.LINE = P.LINE:SSD_FLD_PFX<1,I,J> "L#6":" "
    BEGIN CASE
    CASE J = 1
      P.LINE = P.LINE:SSD_FLD_MVNO<1,I,J> "L#15":" "
      P.LINE = P.LINE:SSD_FLD_HIDE<1,I,J> "L#1":" "
      P.LINE = P.LINE:SSD_FLD_TYPE<1,I,J> "L#1":" "
      P.LINE = P.LINE:SSD_FLD_SEQ<1,I,J> "R#3":" "
      P.LINE = P.LINE:SSD_FLD_REF<1,I,J> "R#3":" "
      P.LINE = P.LINE:SSD_FLD_X<1,I,J> "R#3":" "
      P.LINE = P.LINE:SSD_FLD_FMT<1,I,J> "L#7":" "
      P.LINE = P.LINE:SSD_FLD_DSTLST<1,I,J> "L#9"
    CASE J = 2
      P.LINE = P.LINE:SSD_FLD_SMVNO<1,I,J - 1> "L#15":" "
      P.LINE = P.LINE:SSD_FLD_HIDE<1,I,J> "L#1":" "
      P.LINE = P.LINE:SSD_FLD_TYPE<1,I,J> "L#1":" "
      P.LINE = P.LINE:SSD_FLD_SEQ<1,I,J> "R#3":" "
      P.LINE = P.LINE:SSD_FLD_REF<1,I,J> "R#3":" "
      P.LINE = P.LINE:SSD_FLD_Y<1,I,J - 1> "R#3":" "
      P.LINE = P.LINE:SSD_FLD_CNV<1,I,J-1> "L#7":" "
      P.LINE = P.LINE:SSD_FLD_DSTLST<1,I,J> "L#9"
    CASE 1
      IF X<1,I,J> # "" THEN
        STR20 = STR(" ",20)
        STR48 = STR(" ",48)
        P.LINE = STR20:X<1,I,J> "L#60":STR48
      END ELSE
        STR49 = STR(" ",49)
        P.LINE = P.LINE:STR49
      END
    END CASE
    IF P.LINE # S.HD3 AND P.LINE # HD1 THEN
      CNT = CNT + 1
      IF CNT = PAGE.CNT * PAGE.SIZE + 1 THEN
        PRINT HD1
        PRINT HD1
        PRINT HD1
        GOSUB 100
      END
      PRINT P.LINE
    END
  NEXT J
NEXT I
HD = DCOUNT(SSD_M_TABLE,@SM):" record listed"
IF CNT = PAGE.CNT * PAGE.SIZE + 1 THEN
  PRINT HD1
  PRINT HD1
  PRINT HD1
  GOSUB 100
  PRINT HD
END ELSE
  PRINT HD
END
GOTO 99999
*
*--- Print Page headers
*
100*
PRINT HD1
PRINT HD1
PRINT HD1
PAGE.CNT = PAGE.CNT + 1
PAGE.NO = PAGE.CNT "R#4"
REPORT.HD = FIELD(REPORT.HD,P.HD,1,1):"PAGE":PAGE.NO
PRINT REPORT.HD
PRINT REPORT.HD2
PRINT S.HD1
PRINT S.HD2
PRINT S.HD3
RETURN
*
*--- Print Error Messages
*
93000*
CRT @(0,23):ERRMSG:CL:
INPUT REPLY,1_:
CRT @(0,23):CL:
*
*--- End of Program
*
99999*
PRINTER OFF
PRINTER CLOSE
END
