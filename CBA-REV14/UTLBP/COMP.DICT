*
********************************************************************
* PROGRAM   - COMP.DICT
* BY        - JIHAD YAMOUT 
* DATE      - 11/11/86
* DESC      - This prodram get file name enter form a proc and
*              account name to point need to check .
********************************************************************
PROCREAD X ELSE PRINT CHAR(12):"THIS PROGRAM MUST RUN FROM A PROC.";STOP
QFILE = X<1>
ACCOUNT.NAME1 = X<2>
ACCOUNT.NAME2 = X<3>
OPEN '', 'MD' TO MD ELSE PRINT CHAR(12):"MD FILE IS MISSING"; STOP
HD1 = SPACE(30):"FOR FILE : ":QFILE "L#132"
HD2 = SPACE(20):"FROM ACCOUNT : ":ACCOUNT.NAME1:" TO ACCOUNT : ":ACCOUNT.NAME2 "L#132"
*PORT.ACCT=OCONV(0,"U50BB")
*PORT.NO =FIELD(PORT.ACCT," ",1)
* PORT.NO = SYSTEM(18)
PORT.NO = 'TTY'
CALL SYSVARS.SUB(PORT.NO)
ACCT = SYSTEM(19)
PORT.ACCT = PORT.NO:" ":ACCT
MD.REC = ""
MD.REC<1> = "Q" ; MD.REC<2> = ACCOUNT.NAME1 ; MD.REC<3> = X<1>
MD.REC<9> = "L" ; MD.REC<10> = "20"
WRITE MD.REC ON MD , "Q1":"!":PORT.NO
MD.REC = ""
MD.REC<1> = "Q" ; MD.REC<2> = ACCOUNT.NAME2 ; MD.REC<3> = X<1>
MD.REC<9> = "L" ; MD.REC<10> = "20"
WRITE MD.REC ON MD , "Q2":"!":PORT.NO
OPEN 'DICT', "Q1":"!":PORT.NO TO FILE1 ELSE PRINT CHAR(12):QFILE", ":ACCOUNT.NAME1 :"FILE IS MISSING"
OPEN 'DICT', "Q2":"!":PORT.NO TO FILE2 ELSE PRINT CHAR(12):QFILE:", ":ACCOUNT.NAME2 :"FILE IS MISSING"
*
**** SET PRINTTER
*
PRINTER ON
LINE.COUNT = 99
PAGE.NO = 0
*
**** SELECT FILE1
*
SELECT FILE1
*
**** MAIN PROCESS
*
DATA = 1
LOOP
  READNEXT ID ELSE DATA = 0
WHILE DATA = 1 DO
  READ REC1 FROM FILE1 ,ID ELSE REC1 = ""
  READ REC2 FROM FILE2 ,ID ELSE REC2 = ""
  IF REC1 # "" OR REC2 # "" THEN
     IF LINE.COUNT + 14 > 55 THEN
        PAGE.NO = PAGE.NO + 1
        PRINT CHAR(12):SPACE(30):"COMPARE DICTIONARY FILE":SPACE(30):"PAGE ":PAGE.NO
        PRINT HD1
        PRINT HD2
        PRINT
        LINE.COUNT = 4
     END
     PRINT.FLG = 0
     FOR X = 1 TO 10 UNTIL PRINT.FLG = 1
        IF REC1<X> # REC2<X> THEN
           PRINT.FLG = 1
        END
     NEXT X
     IF PRINT.FLG = 0 THEN GOTO 111
     PRINT "RECORD NAME : ":ID
     PRINT STR("-",45)
     FOR X = 1 TO 10
         PRINT REC1<X> "L#66":REC2<X> "L#66"
     NEXT X
     PRINT STR("*",132)
     LINE.COUNT = LINE.COUNT + 14
  END
111 REPEAT
END
