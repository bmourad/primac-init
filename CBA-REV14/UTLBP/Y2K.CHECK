*********************** READ THIS BEFORE USING ***************************
* This program is the property of Dynamic Systems, Inc., located in      *
* Wilmington, DE. You may use this program, modify it, and distribute    *
* freely, at your own risk and with full assumption of liability for the *
* use and results of the program. In using this program, user agrees that*
* in no event shal Dynamic Systems, Inc. be liable for actual, incidental*
* or consequential damages of any kind, and user agrees to release,      *
* indemnify or otherwise hold Dynamic Systems, Inc. harmless from any and*
* all claims that may arise from use of the program, or for any errors   *
* caused by the program in its original form or with any modifications.  *
* Please send any modifications via email (lburst@dynamicsys.com) so that*
* they can be considered for incorporation into future offerings.        *
* Lee H. Burstein                                                        *
**************************************************************************
* USE OF THE PROGRAM
* After successfully compiling Y2K.CHECK simply run it.
* You will be prompted for a file to check.
* Each record in the file is checked for any occurances
* of D2.
* If any occurances of D2 are found, a record will be created
* in the file being checked, where the id will be
* Y2K.CHECK.RESULTS and each field will represent an item
* where D2 was found. Each field in DSI.Y2K.CHECK.RESULTS will be
* multivalued where the first value will be the item id
* where D2 was found and each subsequent value will be the field
* number containing D2. The first field will be the date and time checked.
* For example: If the BP file is being checked and the program
* LAST.MONTH.ACTIVITY had D2 on lines 156 and 399, and the program
* UPDATE.DATE had D2 on lines 895, 910 and 1127, then the
* following record is created in the BP file:
* id     DSI.Y2K.CHECK.RESULTS
* 001    10678]24539
* 002    LAST.MONTH.ACTIVITY]156]399
* 003    UPDATE.DATE]895]910]1127
*
* This program will not change any records on your system.
* It is designed only to identify where a potential year
* 2000 problem may exist.
******************************************************************
*
AM = CHAR(254);VM = CHAR(253);CLR = @(-1);EOL = @(-4);EOS = @(-3);PROMPT ''
LINE = 'DYNAMIC SYSTEMS, INC.'
COL = 40 - LEN(LINE)/2
TOP.LINE = CLR:@(COL,0):LINE:@(COL,1):STR('=',LEN(LINE))
SCRVAL = TOP.LINE:@(20,3):'*** PLEASE READ THIS BEFORE PROCEEDING ***'
SCRVAL = SCRVAL:@(5,5):'This program is the property of Dynamic Systems, Inc., located in'
SCRVAL = SCRVAL:@(5,6):'Wilmington, DE. You may use this program, modify it, and distribute'
SCRVAL = SCRVAL:@(5,7):'freely, at your own risk and with full assumption of liability for the'
SCRVAL = SCRVAL:@(5,8):'use and results of the program. In using this program, user agrees that'
SCRVAL = SCRVAL:@(5,9):'in no event shal Dynamic Systems, Inc. be liable for actual, incidental'
SCRVAL = SCRVAL:@(5,10):'or consequential damages of any kind, and user agrees to release,'
SCRVAL = SCRVAL:@(5,11):'indemnify or otherwise hold Dynamic Systems, Inc. harmless from any and'
SCRVAL = SCRVAL:@(5,12):'all claims that may arise from use of the program, or for any errors'
SCRVAL = SCRVAL:@(5,13):'caused by the program in its original form or with any modifications.'
SCRVAL = SCRVAL:@(10,14):'Press <return> to continue:':""
PRINT SCRVAL:
INPUT RES:
SCRVAL = TOP.LINE:@(5,3):'This program helps idenfity potential year 2000 problems'
SCRVAL = SCRVAL:@(5,4):'in your program and dictionary files.'
SCRVAL = SCRVAL:@(5,6):'No changes are made to any existing records'
SCRVAL = SCRVAL:@(5,7):'(other that updating DSI.Y2K.CHECK.RESULTS).'
SCRVAL = SCRVAL:@(5,8):'Look at the record DSI.Y2K.CHECK.RESULTS in the file being'
SCRVAL = SCRVAL:@(5,9):'checked for the results of this program.'
SCRVAL = SCRVAL:@(10,10):'Enter the file to be checked or <return> to stop: '
10 CRT SCRVAL:
INPUT FILE.NAME
BEGIN CASE
  CASE FILE.NAME = ''
    CRT CLR
    STOP
  CASE INDEX(FILE.NAME,' ',1)
  * TWO WORDS ENTERED FOR FILE.NAME. CHECK TO SEE IF FIRST IS DICT
    FIRST = FIELD(FILE.NAME,' ',1);SECOND = FIELD(FILE.NAME,' ',2)
    IF FIRST = 'DICT' THEN
      CRT @(10,12):'Opening the Dictionary of ':SECOND
      OPEN FIRST,SECOND TO CHK.FILE ELSE
        CRT @(10,13):'Cannot open the Dictionary of ':SECOND
        CRT @(10,14):'Press <return> to try again!':;INPUT RES;GOTO 10
      END
    END ELSE
      CRT @(10,12):'I thought you were trying to check a dictionary, but you were not.'
      CRT @(10,13):'Press <return> to try again!':;INPUT RES;GOTO 10
    END
  CASE 1
  * JUST A FILE NAME HAS BEEN ENTERED, ASSUME A PROGRAM FILE
    CRT @(10,12):'Opening program file ':FILE.NAME
    OPEN FILE.NAME TO CHK.FILE ELSE
      CRT @(10,13):'Cannot open the program file ':FILE.NAME
      CRT @(10,14):'Press <return> to try again!':;INPUT RES;GOTO 10
    END
END CASE
* NOW LET'S GET STARTED, BUT FIRST CHECK TO SEE IF Y2K.CHECK.RESULTS EXISTS
READ Y2K.CHK.REC FROM CHK.FILE,'DSI.Y2K.CHECK.RESULTS' THEN
  CRT @(10,13):EOL:'This file has already been checked for D2 on ':
  CRT OCONV(Y2K.CHK.REC<1,1>,'D4/'):' at ':OCONV(Y2K.CHK.REC<1,2>,'MTHS')
  CRT @(10,14):EOL:'Do you want to see the (R)eport or check (A)gain (R/A)? ':
  INPUT RES:
  BEGIN CASE
    CASE RES = 'R'
      GOSUB 100; * PRINT REPORT
      GOTO 10; * START OVER
    CASE RES = 'A'
    CASE 1
      GOTO 10
  END CASE
END
STOP.FLG = 0;CNT = 0;Y2K.CHK.REC = ''
SELECT CHK.FILE
LOOP
  READNEXT ID ELSE STOP.FLG = 1
UNTIL STOP.FLG DO
  IF ID[1,1] = '$' OR ID[1,1] = '*' OR ID[1,1] = '!' OR ID[1,1] = "_" ELSE
  * SKIP PROGRAM IS FIRST CHARACTER IS $ OR *.
    READ CHK.REC FROM CHK.FILE,ID THEN
      CRT @(12,13):EOL:'Checking record: ':ID:
      IF INDEX(CHK.REC,'D2/',1) OR INDEX(CHK.REC,"D2-",1) THEN
      * IF D2 IS FOUND ANYWHERE IN THE PROGRAM, CHECK EACH LINE
        NBR.LINES = DCOUNT(CHK.REC,AM)
        CNT = CNT + 1; *    NUMBER OF RECORDS CONTAINING D2
        Y2K.CHK.LINE = ID;LINE = 'D2 found on lines:'
        FOR I = 1 TO NBR.LINES
          IF INDEX(CHK.REC<I>,"D2/",1) OR INDEX(CHK.REC<I>,"D2-",1) THEN
            Y2K.CHK.LINE = Y2K.CHK.LINE:VM:I
            LINE = LINE:' ':Y2K.CHK.LINE<1,-1> 
            CRT @(12,14):EOL:LINE:
          END
        NEXT I
        IF Y2K.CHK.REC = '' THEN
          Y2K.CHK.REC = Y2K.CHK.LINE
        END ELSE
          Y2K.CHK.REC = Y2K.CHK.REC:AM:Y2K.CHK.LINE
        END 
      END 
    END
  END
REPEAT
CRT @(0,13):EOS:
IF Y2K.CHK.REC = '' THEN
* NO D2 FOUND IN THIS FILE
  CRT @(10,13):'D2 was not found in this file!'
  CRT @(10,14):'Press <return> to start over.':;INPUT RES;GOTO 10
END ELSE
  Y2K.CHK.REC = INSERT(Y2K.CHK.REC,1;DATE():VM:TIME())
  WRITE Y2K.CHK.REC ON CHK.FILE,'DSI.Y2K.CHECK.RESULTS'
  CRT @(10,13):CNT:' records in this file contain D2.'
  CRT @(10,14):'Please look at record Y2K.CHECK.RESULTS more information.'
  CRT @(10,15):'Do you want a report of the results (Y/N)? ':
  INPUT RES:
  IF RES = 'Y' THEN
    GOSUB 100; * PRINT REPORT
    GOTO 10; * START OVER
  END ELSE GOTO 10
END
100 * PRINT REPORT
CRT @(10,16):'Report to the (P)rinter or (T)erminal (P/T)? ':
INPUT RES:
IF RES = 'P' THEN PRINTER ON
HDR1 = "Dynamic Systems, Inc.'CL'"
HDR2 = "Y2K Results for file ":FILE.NAME:"'CL'"
HDR3 = "Checked on ":OCONV(Y2K.CHK.REC<1,1>,'D4/')
HDR3 = HDR3:" at ":OCONV(Y2K.CHK.REC<1,2>,'MTHS'):"'CL'"
HDR4 = "Printed at 'T' Page 'PCLL'"
HDR5 = "RECORD NAME" "L#31":"LINES CONTAINING D2'L'"
HEADING HDR1:HDR2:HDR3:HDR4:HDR5 
HOW.MANY = DCOUNT(Y2K.CHK.REC,AM)
FOR I = 2 TO HOW.MANY
  THIS.Y2K = Y2K.CHK.REC<I>
  NBR.D2 = DCOUNT(THIS.Y2K,VM)
  LINE = THIS.Y2K<1,1> 'L#30'
  FOR J = 2 TO NBR.D2
    LINE = LINE:' ':THIS.Y2K<1,J>
    IF LEN(LINE) GT 75 THEN
      PRINT LINE;LINE = SPACE(30)
    END
  NEXT J
  PRINT LINE
NEXT I
IF RES = 'P' THEN PRINTER OFF
CRT;CRT 'REPORT FINISHED, PRESS <RETURN> TO CONTINUE.':;INPUT RES
RETURN
END

