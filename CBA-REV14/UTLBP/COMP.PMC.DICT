********************************************************************
* REVISION    - [08.1]
* SYSTEM      - PRIMAC
* SOURCE      - UTLBP
* PROGRAM     - COMP.PMC.DICT
* BY          - ZIAD YAMOUT, Vercom Software, Inc.
* DATE        - 07/16/89
* DESCRIPTION - Comapres the file dictionaries for two accounts
********************************************************************
*
*COPY>CPYLIB>CHAR
*
      PROMPT ""
      DIM SYS.ACCTS(2,20)
      DIM SYS.MD(2,20)
*
*---- Initialization
*
      AFILE.READ = 0; AITEM.READ = 0
      AFILE.PRCS = 0; AITEM.PRCS = 0
      AFILE.MTCH = 0; AITEM.MTCH = 0
*
*---- Verify the CBA hardware
*
      HWARE = SYSTEM(22)<1>
      IF HWARE <> 31503 THEN
         ERRMSG = "Must only be executed for customer account"
         GOSUB 91000; GOTO 99999
      END
*
*---- Verify the account
*
      MAT SYS.ACCTS = ""
      SYS.ACCTS(2,1) = "CBA-8.1"
      SYS.ACCTS(2,2) = "8.1"
*      PORT.ACCT = OCONV(0,"U50BB")
*      PORT = FIELD(PORT.ACCT," ",1)
*      SYS.ACCTS(1,1) = FIELD(PORT.ACCT," ",2)
      PORT = SYSTEM(18)
      SYS.ACCTS(1,1) = SYSTEM(19)
      CBA.ACCT = FIELD(SYS.ACCTS(1,1),"-",1)
      BEGIN CASE
      CASE SYS.ACCTS(1,1) = SYS.ACCTS(2,1)
         ERRMSG = SYS.ACCTS(1,1):" is the account to be compared to"
         GOSUB 91000; GOTO 99999
      CASE CBA.ACCT # "CBA"
         ERRMSG = "Must be executed from a CBA main account"
         GOSUB 91000; GOTO 99999
      CASE SYS.ACCTS(1,1) = "CBA"
         SYS.ACCTS(1,2) = "PRIMAC"
      CASE 1
         SYS.ACCTS(1,2) = FIELD(SYS.ACCTS(1,1),"-",2)
      END CASE
*
      PMC.ACCTS = "CBA"
      PMC.ACCTS<2> = "PMC"
      PMC.ACCTS<3> = "APS"
      PMC.ACCTS<4> = "ARS"
      PMC.ACCTS<5> = "EES"
      PMC.ACCTS<6> = "FAS"
      PMC.ACCTS<7> = "GLS"
      PMC.ACCTS<8> = "ICS"
      PMC.ACCTS<9> = "JCS"
      PMC.ACCTS<10>= "JES"
      PMC.ACCTS<11>= "MLS"
      PMC.ACCTS<12>= "POS"
      PMC.ACCTS<13>= "PRS"
      PMC.ACCTS<14>= "PSS"
      PMC.ACCTS<15>= "RIS"
      PMC.ACCTS<16>= "SAS"
      LINES = 16
      LOCATE SYS.ACCTS(1,2) IN PMC.ACCTS SETTING FND THEN
         ERRMSG = "Customer logon should not be the same as the (":SYS.ACCTS(1,2):") system initials"
         GOSUB 91000; GOTO 99999
      END
*
*---- Open the files
*
      OPEN "","DL/ID" TO SYS.MD(1,1) ELSE
         ERRMSG = SYS.ACCTS(1,1):" is an invalid account"
         GOSUB 91000; GOTO 99999
      END
      QF.ID = "QF*":PORT
      DATA.ID = "DL/ID"
      READU O.QFILE FROM SYS.MD(1,1), QF.ID ELSE O.QFILE = ""
      QFILE = "Q"
      QFILE<2> = SYS.ACCTS(1,2)
      QFILE<3> = DATA.ID
      QFILE<4> = "."
      WRITEU QFILE ON SYS.MD(1,1), QF.ID
      OPEN "", QF.ID TO SYS.MD(1,2) ELSE
         ERRMSG = SYS.ACCTS(1,2):" is an invalid account"; GOTO 93000
      END
      QFILE<2> = SYS.ACCTS(2,1)
      QFILE<3> = DATA.ID
      WRITEU QFILE ON SYS.MD(1,1), QF.ID
      OPEN "",QF.ID TO SYS.MD(2,1) ELSE
         ERRMSG = SYS.ACCTS(2,1):" is an invalid account"; GOTO 93000
      END
      QFILE<2> = SYS.ACCTS(2,2)
      QFILE<3> = DATA.ID
      WRITEU QFILE ON SYS.MD(1,1), QF.ID
      OPEN "",QF.ID TO SYS.MD(2,2) ELSE
         ERRMSG = SYS.ACCTS(2,2):" is an invalid account"; GOTO 93000
      END
*
      FPTR = 3
      LOOP WHILE FPTR <= LINES DO
         SYS.ACCTS(1,FPTR) = SYS.ACCTS(1,2) : "-" : PMC.ACCTS<FPTR>
         QFILE<2> = SYS.ACCTS(1,FPTR)
         QFILE<3> = DATA.ID
         WRITEU QFILE ON SYS.MD(1,1), QF.ID
         OPEN "",QF.ID TO SYS.MD(1,FPTR) THEN
            SYS.ACCTS(2,FPTR) = SYS.ACCTS(2,2) : "-" : PMC.ACCTS<FPTR>
            QFILE<2> = SYS.ACCTS(2,FPTR)
            QFILE<3> = DATA.ID
            WRITEU QFILE ON SYS.MD(1,1), QF.ID
            OPEN "",QF.ID TO SYS.MD(2,FPTR) THEN
               FPTR = FPTR + 1
            END ELSE
               PMC.ACCTS = DELETE(PMC.ACCTS,FPTR,0,0)
               SYS.ACCTS(1,FPTR) = ""; SYS.ACCTS(2,FPTR) = ""
               LINES = LINES - 1
            END
         END ELSE
            PMC.ACCTS = DELETE(PMC.ACCTS,FPTR,0,0)
            SYS.ACCTS(1,FPTR) = ""; SYS.ACCTS(2,FPTR) = ""
            LINES = LINES - 1
         END
      REPEAT
*
*---- Start processing the account files
*
      PRINT @(-1):
      PRINT @(25,1):"M E S S A G E":
      PRINT @(12,3):"This process will compare the dictionary items of two accounts":
      PRINT @(22,5):"Enter (C)ontinue : ":
      INPUT ACTION,1 :
      IF ACTION # "C" THEN GOTO 99990
      FOR S = 1 TO LINES
         FILES.READ = 0; ITEMS.READ = 0
         FILES.PRCS = 0; ITEMS.PRCS = 0
         FILES.MTCH = 0; ITEMS.MTCH = 0
         PRINT @(15,8):"Comparing account - ":SYS.ACCTS(1,S):" to account ":SYS.ACCTS(2,S):CL:
         PRINT @(15,10):"Processing file - ":CL:
*
         SELECT SYS.MD(1,S) TO MDLIST
         DATA = 1
         LOOP
            READNEXT FILE.NAME FROM MDLIST ELSE DATA = 0
         WHILE DATA DO
            BEGIN CASE
            CASE FILE.NAME = "DL/ID"
               GOTO 1999
            CASE FILE.NAME = "M/DICT"
               GOTO 1999
            CASE FILE.NAME = "MD"
               GOTO 1999
            CASE FILE.NAME = "PROC"
               GOTO 1999
            CASE FILE.NAME = QF.ID
               GOTO 1999
            END CASE
            READ CUST.ITEM FROM SYS.MD(1,S), FILE.NAME ELSE
               ERRMSG = "Cannot open M/DICT, ": FILE.NAME
               GOSUB 91000; GOTO 1999
            END
            IF CUST.ITEM<1> # "D" THEN GOTO 1999
            FILES.READ = FILES.READ + 1
            PRINT @(38,10) : FILES.READ "L#5" : FILE.NAME : CL :
         QFILE<2> = SYS.ACCTS(1,S)
         QFILE<3> = FILE.NAME
         WRITEU QFILE ON SYS.MD(1,1), QF.ID
            OPEN "DICT", QF.ID TO D.CUST.FILE ELSE
               ERRMSG = "Cannot open DICT, ":FILE.NAME
               GOSUB 91000; GOTO 1999
            END
            READ PMC.ITEM FROM SYS.MD(2,S), FILE.NAME ELSE
               ERRMSG = "Cannot locate ":FILE.NAME:" file definition"
               GOSUB 91000; GOTO 1999
            END
            IF PMC.ITEM<1> # "D" THEN
               ERRMSG = "Invalid ":FILE.NAME:" file definition"
               GOSUB 91000; GOTO 1999
            END
            QFILE<2> = SYS.ACCTS(2,S)
            QFILE<3> = FILE.NAME
            WRITEU QFILE ON SYS.MD(1,1), QF.ID
            OPEN "DICT",QF.ID TO D.PMC.FILE ELSE
               ERRMSG = "Cannot open DICT, ":FILE.NAME
               GOSUB 91000; GOTO 1999
            END
            FILES.PRCS = FILES.PRCS + 1
            PRINT @(15,12):"Processing Item - ":
            SELECT D.CUST.FILE TO OTHLIST
            ITEM.READ = 0
            ITEM.PRCS = 0
            ITEM.MTCH = 0
            OTH.DATA = 1
            LOOP
               READNEXT ID FROM OTHLIST ELSE OTH.DATA = 0
            WHILE OTH.DATA DO
               IF ID = "DL/ID" THEN GOTO 1599
               ITEM.READ = ITEM.READ + 1
               PRINT @(38,12) : ITEM.READ "L#5" : ID : CL :
               READ CUST.ITEM FROM D.CUST.FILE, ID ELSE
                  ERRMSG = "Cannot read DICT ":FILE.NAME:", ":ID
                  GOSUB 91000; GOTO 1599
               END
               ITEM.PRCS = ITEM.PRCS + 1
               READ PMC.ITEM FROM D.PMC.FILE, ID ELSE
                  ITEM.MTCH = ITEM.MTCH + 1
                  GOTO 1599
               END
               IF CUST.ITEM # PMC.ITEM THEN
                  ITEM.MTCH = ITEM.MTCH + 1
               END
1599*
            REPEAT
*
*---- Display file action summary
*
            PRINT @(15,14) : ITEM.READ "R#5" : " dictionary items selected":CL:
            PRINT @(15,15) : ITEM.PRCS "R#5" : " dictionary items processed":CL:
            PRINT @(15,16) : ITEM.MTCH "R#5" : " dictionary items mismatched":CL:
            ITEMS.READ = ITEMS.READ + ITEM.READ
            ITEMS.PRCS = ITEMS.PRCS + ITEM.PRCS
            ITEMS.MTCH = ITEMS.MTCH + ITEM.MTCH
            IF ITEM.MTCH THEN
               FILES.MTCH = FILES.MTCH + 1
               INPUT REPLY :
            END
            FOR I = 11 TO 22
               PRINT @(0,I) : CL :
            NEXT I
1999     REPEAT
         PRINT @(15,10) : "Summary of action taken" : CL :
         FOR I = 11 TO 22
            PRINT @(0,I) : CL :
         NEXT I
         PRINT @(15,12) : FILES.READ "R#5":" Files selected"
         PRINT @(15,13) : FILES.PRCS "R#5":" Files processed"
         PRINT @(15,14) : FILES.MTCH "R#5":" Files mismatched"
         PRINT @(15,16) : ITEMS.READ "R#7":" Items selected"
         PRINT @(15,17) : ITEMS.PRCS "R#7":" Items processed"
         PRINT @(15,18) : ITEMS.MTCH "R#7":" Items mismatched"
         PRINT @(0,21) : CL :
         INPUT REPLY :
         AFILE.READ = AFILE.READ + FILES.READ
         AFILE.PRCS = AFILE.PRCS + FILES.PRCS
         AFILE.MTCH = AFILE.MTCH + FILES.MTCH
         AITEM.READ = AITEM.READ + ITEMS.READ
         AITEM.PRCS = AITEM.PRCS + ITEMS.PRCS
         AITEM.MTCH = AITEM.MTCH + ITEMS.MTCH
         FOR I = 11 TO 22
            PRINT @(0,I) : CL :
         NEXT I
      NEXT S
*
*---- Display action summary
*
      PRINT @(15,10) : "Summary of action taken" : CL :
      FOR I = 11 TO 22
         PRINT @(0,I) : CL :
      NEXT I
      PRINT @(15,12) : AFILE.READ "R#5":" Files selected"
      PRINT @(15,13) : AFILE.PRCS "R#5":" Files processed"
      PRINT @(15,14) : AFILE.MTCH "R#5":" Files mismatched"
      PRINT @(15,16) : AITEM.READ "R#7":" Items selected"
      PRINT @(15,17) : AITEM.PRCS "R#7":" Items processed"
      PRINT @(15,18) : AITEM.MTCH "R#7":" Items mismatched"
      PRINT @(0,21) : "End of process : " : CL :
      INPUT REPLY :
      GOTO 99990
90000 PRINT @(0,23) : ERRMSG : CL :
      RETURN
91000 PRINT @(0,23) : ERRMSG : CL :
      INPUT REPLY :
      RETURN
93000 PRINT @(0,23) : ERRMSG : CL :
      INPUT REPLY :
99990 IF O.QFILE = "" THEN
         RELEASE SYS.MD(1,1), QF.ID
      END ELSE
         WRITE O.QFILE ON SYS.MD(1,1), QF.ID
      END
99999END
