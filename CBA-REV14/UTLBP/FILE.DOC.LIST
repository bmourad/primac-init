***************************************************************************
*
* PROGRAM - FILE.DOC.LIST
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/20/84
*
* DESCRIPTION
*
* Print file documentation providing the following:
*
*      1.  File name
*      2.  Programs accessing the file
*      3.  File usage (open, read, lock, write and delete)
*
***************************************************************************
*
*COPY>CPYLIB>PROG.FILE.XREF
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      DIM PG(10)
      MAT PG = ""
      PG(1) = "i"
      PG(2) = "ii"
      PG(3) = "iii"
      PG(4) = "iv"
      PG(5) = "v"
      PG(6) = "vi"
      PG(7) = "vii"
      PG(8) = "viii"
      PG(9) = "ix"
      PG(10) = "x"
*
      PROMPT ""
      INDENT = SPACE(3)
      HEAD.LINES = 5
      CO.NAME = "Vercom Software, Inc."
      DESTIN = ''
*
*---- OPEN ALL FILES
*
      OPEN "","PROG.FILE.XREF" TO PROG.FILE.XREF ELSE
         STOP "PROG.FILE.XREF FILE IS MISSING"
      END
*
*---- INPUT FILE NAME
*
10*
      PRINT @(-1):
      PRINT @(0,0):"VERCOM":
      PRINT @(25,0):"PRINT FILE DOCUMENTATION":
      PRINT @(72,0):OCONV(DATE(),"D2/"):
      PRINT @(0,1):STR("-",80):
      PRINT @(0,22):STR("-",80):
      PRINT @(20,10):CL:'Enter File Name or "ALL" : ':
      INPUT SEL.FILE
      PRINT @(0,23):CL:
      IF SEL.FILE = "" THEN GOTO 9999
      IF SEL.FILE = "END" THEN GOTO 9999
      IF SEL.FILE = "^" THEN GOTO 9999
      IF SEL.FILE = CHAR(27) THEN GOTO 9999
14    PRINT @(20,12):CL:'Print on (C)rt or (P)rinter : ':
      INPUT DESTIN,1 _:
      BEGIN CASE
      CASE DESTIN = "END"
         GOTO 9999
      CASE DESTIN = "^"
         GOTO 9999
      CASE DESTIN = CHAR(27)
         GOTO 9999
      CASE DESTIN = "P"
         PRINTER ON
         PAGE.LENGTH = 55
      CASE DESTIN = "C"
         PAGE.LENGTH = 21
         PRINT @(-1):
      CASE 1
         GOTO 14
      END CASE
*
*---- MAIN PROCESSING
*
100*
      PAGE.NO = 0
      LINE.NO = 0
      PREV.FILE.NAME = ""
      FILE.NAMES = ""
      FILE.CNT = 0
      PROG.NAMES = ""
BPFILE = ""
      PROG.CNT = 0
      OPEN.FLAGS = ""
      READ.FLAGS = ""
      WRITE.FLAGS = ""
      DELETE.FLAGS = ""
      PAGE.NOS = ""
      GOSUB 10000
      MSGS = ""
      EXECUTE "GET-LIST FDOCLIST" RTNLIST FLIST CAPTURING MSGS
110*
      READNEXT PROG.NAME,VAL FROM FLIST ELSE GOTO 190
      MATREAD PFX.REC FROM PROG.FILE.XREF, PROG.NAME ELSE GOTO 110
      FILE.NAME = PFX.FILE.NAME<1,VAL>
      IF SEL.FILE # "ALL" AND FILE.NAME # SEL.FILE THEN GOTO 110
      IF FILE.NAME # PREV.FILE.NAME THEN
         GOSUB 1000
         PROG.NAMES = ""
         OPEN.FLAGS = ""
         READ.FLAGS = ""
         WRITE.FLAGS = ""
         DELETE.FLAGS = ""
         PROG.CNT = 0
         LOCATE FILE.NAME IN FILE.NAMES,1 BY "AL" SETTING P ELSE
            FILE.NAMES = INSERT(FILE.NAMES,P,0,0,FILE.NAME)
            FILE.CNT = FILE.CNT + 1
         END
      END
      PREV.FILE.NAME = FILE.NAME
      PROG.CNT = PROG.CNT + 1
      PROG.NAMES<1,PROG.CNT> = PROG.NAME
      OPEN.FLAGS<1,PROG.CNT> = PFX.OPEN.FLAG<1,VAL>
      READ.FLAGS<1,PROG.CNT> = PFX.READ.FLAG<1,VAL>
      WRITE.FLAGS<1,PROG.CNT> = PFX.WRITE.FLAG<1,VAL>
      DELETE.FLAGS<1,PROG.CNT> = PFX.DELETE.FLAG<1,VAL>
      BPFILE<1,PROG.CNT> = PFX.SOURCE.FILE<1,VAL>
      GOTO 110
190*
      GOSUB 1000
      GOSUB 11000
      IF SEL.FILE = "ALL" THEN
         GOSUB 4000                 ;* PRINT TABLE OF CONTENTS
         GOTO 9999
      END
PRINTER OFF
PRINTER CLOSE
      GOTO 10
*
*---- PRINT FILE CROSS-REFERENCE DATA
*
1000*
      IF PREV.FILE.NAME = "" THEN RETURN
      IF (LINE.NO + PROG.CNT) > PAGE.LENGTH THEN
         GOSUB 11000
         GOSUB 10000
      END
      PAGE.NOS<FILE.CNT> = PAGE.NO
      PLINE = PREV.FILE.NAME "L#30"
      FOR P = 1 TO PROG.CNT
         IF OPEN.FLAGS<1,P> = "" THEN
            P.OPEN = " "
         END ELSE
            P.OPEN = "O"
         END
         IF READ.FLAGS<1,P> = "" THEN
            P.READ = "  "            
         END ELSE
            IF READ.FLAGS<1,P> = "X" THEN
               P.READ = "R "
            END ELSE
               P.READ = "RU"
            END
         END
         IF WRITE.FLAGS<1,P> = "" THEN
            P.WRITE = "  "
         END ELSE
            IF WRITE.FLAGS<1,P> = "X" THEN
               P.WRITE = "W "
            END ELSE
               P.WRITE = "WU"
            END
         END
         IF DELETE.FLAGS<1,P> = "" THEN
            P.DELETE = " "
         END ELSE
            P.DELETE = "D"
         END
         PLINE = PLINE:SPACE(2):P.OPEN:SPACE(2):P.READ:SPACE(2):P.WRITE:SPACE(2):P.DELETE
         PLINE = PLINE:SPACE(2):PROG.NAMES<1,P> "L#30"
PLINE = PLINE:SPACE(5):BPFILE<1,1>
         IF LINE.NO > PAGE.LENGTH THEN
            GOSUB 11000
            GOSUB 10000
         END
         GOSUB 9000
         PLINE = "" "L#30"
      NEXT P
      PRINT
      LINE.NO = LINE.NO + 1
      RETURN
*
*---- PRINT TABLE OF CONTENTS
*
4000*
      PAGE.NO = 0
      GOSUB 5000
      FOR P = 1 TO FILE.CNT
         FILE.NAME = FILE.NAMES<P>
         IF LINE.NO > PAGE.LENGTH THEN
            GOSUB 6000
            GOSUB 5000
         END
         PLINE = SPACE(20):FILE.NAME "L#30":SPACE(5):PAGE.NOS<P> "R#3"
         PRINT PLINE
         LINE.NO = LINE.NO + 1
      NEXT P
      GOSUB 6000
      RETURN
*
*---- PRINT PAGE HEADINGS
*
5000*
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT
      PRINT
      PRINT
      IF PAGE.NO = 1 THEN
         PRINT SPACE(25):"Table of Contents"
      END ELSE
         PRINT SPACE(25):"Table of Contents (cont'd)"
      END
      PRINT
      PRINT
      LINE.NO = 6
      RETURN
*
*---- PRINT PAGE NUMBER
*
6000*
      BEGIN CASE
      CASE DESTIN = "P"
         FOR L = LINE.NO TO (PAGE.LENGTH+3)
            PRINT
         NEXT L
         PLINE = PG(PAGE.NO)
         PRINT SPACE(32):PG(PAGE.NO)
      CASE DESTIN = "C"
         PRINT @(0,23):CL:"Press <RETURN> to continue ":
         INPUT REPLY
         IF REPLY = "END" THEN GOTO 9999
         IF REPLY = "^" THEN GOTO 9999
         IF REPLY = CHAR(27) THEN GOTO 9999
         PRINT @(-1):
      END CASE
      RETURN
*
*---- PRINT DETAIL LINE
*
9000*
      PRINT PLINE
      LINE.NO = LINE.NO + 1
      RETURN
*
*---- PRINT HEADING
*
10000*
      IF LINE.NO = HEAD.LINES THEN GOTO 10090
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      HD1 = SPACE((70-LEN(CO.NAME))/2):CO.NAME
      HLINE = OCONV(DATE(),"D2/"):HD1[9,70]
      PRINT HLINE
      HD2 = "FILE DOCUMENTATION"
      HLINE = SPACE((70-LEN(HD2))/2):HD2
      PRINT HLINE
      PRINT
     SLINE = "          FILE NAME             O  RU  WU  D           PROGRAM NAME             SOURCE"
      PRINT SLINE
      SLINE = "------------------------------  -  --  --  -  ------------------------------    [C-------"
      PRINT SLINE
      LINE.NO = HEAD.LINES
10090*
      RETURN
*
*---- PRINT PAGE NUMBER
*
11000*
      IF LINE.NO = HEAD.LINES THEN GOTO 11090
      BEGIN CASE
      CASE DESTIN = "P"
         FOR L = LINE.NO TO (PAGE.LENGTH+3)
            PRINT
         NEXT P
         PRINT SPACE(30):"- ":PAGE.NO:" -"
      CASE DESTIN = "C"
         PRINT @(0,23):CL:"Press <RETURN> to continue ":
         INPUT REPLY
         IF REPLY = "END" THEN GOTO 9999
         IF REPLY = "^" THEN GOTO 9999
         IF REPLY = CHAR(27) THEN GOTO 9999
         PRINT @(-1):
      END CASE
11090*
      RETURN
*
*---- END PROGRAM
*
9999*
      IF DESTIN = "P" THEN
         PRINTER OFF
      END
   END
