** PGM TO CHECK TAX RATES FROM A WEB SERVICE
      OTHERWISE = @TRUE
      PROMPT ""
      RESP.HEADERS = ""
      RESP.DATA = ""
      WEB.HANDLE = ""
*
      PRINT "ENTER YOUR ZIP CODE: ":
      INPUT OLD.ZIP
*
      URL = "http://trial.serviceobjects.com/ft/FastTax.asmx/GetTaxInfo?"
      REQUEST.STATUS = createRequest(URL,"GET",WEB.HANDLE)
      IF REQUEST.STATUS THEN
         PRINT "createRequest FAILED, STATUS CODE = ":REQUEST.STATUS
         STOP
      END
      VARS = "PostalCode"
      VALS = OLD.ZIP
      VARS<1,2> = "TaxType"
      VARS<1,3> = "LicenseKey"
      VALS<1,2> = "SALES"                ; ** SET SALES OR USE TAX
      VALS<1,3> = "WS19"
      FOR I = 1 TO 3
         PARAM.STATUS = addRequestParameter(WEB.HANDLE,VARS<1,I>,VALS<1,I>,"")
         IF PARAM.STATUS THEN
            PRINT "addRequestParameter FAILED, STATUS CODE = ":PARAM.STATUS
            STOP
         END
      NEXT I
      SUBMIT.STATUS = submitRequest(WEB.HANDLE,"","",RESP.HEADERS,RESP.DATA,HTTP.STATUS)
      IF SUBMIT.STATUS THEN
         PRINT "submitRequest FAILED, STATUS CODE = ":SUBMIT.STATUS
         STOP
      END
      VAR.LIST = 'Zip':@AM:'City':@AM:'County':@AM:'CountyFIPS':@AM:'StateName':@AM:'StateAbbreviation':@AM:'TotalTaxRate':@AM:'TotalTaxExempt':@AM:'StateRate':@AM
      VAR.LIST :='CityRate':@AM:'CountyRate':@AM:'CityDistrictRate':@AM:'CountyDistrictRate'
      FOR XX = 1 TO DCOUNT(VAR.LIST,@AM)
         VAR.NAME = VAR.LIST<XX>
         GOSUB PARSEXML
         PRINT VAR.NAME:" = ":PARSE.STRNG
      NEXT XX
      PRINT RESP.HEADERS
      STOP
      *--> LOCATE XML VALUES
PARSEXML:
      XML.STRNG = RESP.DATA
      ST.VAR.NAME = "<":VAR.NAME
      END.VAR.NAME = "</":VAR.NAME:">"
      TAG.POS = INDEX(XML.STRNG,ST.VAR.NAME,1)
      NEW.STR = XML.STRNG[TAG.POS,LEN(XML.STRNG)]
      ST.POS = INDEX(NEW.STR,'>',1)+ TAG.POS
** THIS IS WILL GET THE LENGTH OF THE TAG
      END.POS = INDEX(XML.STRNG,END.VAR.NAME,1)
      IF ST.POS > 0 AND END.POS > ST.POS THEN
         STRT = ST.POS
         LNG = END.POS - STRT            ; * TOTAL LENGTH OF STRING
         PARSE.STRNG = XML.STRNG[STRT,LNG]
         PARSE.STRNG = TRIM(PARSE.STRNG)
      END ELSE
         ERR.MSG = 'XML document is missing ':VAR.NAME:' Data'
         PARSE.STRNG = ''
         PRINT ERR.MSG
      END
      RETURN
