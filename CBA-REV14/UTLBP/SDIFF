*
   DEFFUN PMC_SEND_MAIL(ADDRESSES,SUBJECT,TEXT,ATTACH)
   OPEN "","_HOLD_" TO HOLDFILE ELSE
      PRINT "_HOLD_ FILE IS MISSING"
      STOP
   END
10 *
   PRINT "Enter Left File or END ":
   INPUT LFILE
   IF LFILE = "" OR LFILE = "END" THEN STOP
20 *
   PRINT "Enter Left Item ":
   INPUT LITEM
   IF LITEM = "^" THEN GOTO 10
30 *
   PRINT "Enter Right File (":LFILE:")":
   INPUT RFILE
   IF RFILE = "" THEN RFILE = LFILE
   IF RFILE = "^" THEN GOTO 20
   PRINT "Enter Item Name (":LITEM:")":
   INPUT RITEM
   IF RITEM = '' THEN RITEM = LITEM
   IF RITEM = "^" THEN GOTO 30
   PRINT "Do you want the S-Short or L-Long Listing <L/CR> :":
   INPUT LISTTYPE
   IF LISTTYPE # 'S' THEN
      LISTTYPE = 'L'
   END
40 *
   PRINT "Enter a valid email address ":
   INPUT EMAILTO
   IF EMAILTO = "^" THEN GOTO 30
   IF EMAILTO # "" THEN
      EMAILNAME = FIELD(EMAILTO,"@",1)
      IF EMAILNAME = "" THEN GOTO 40
      EMAILSERVER = FIELD(EMAILTO,"@",2)
      IF EMAILSERVER = "" THEN GOTO 40
      IF FIELD(EMAILSERVER,".",1) = "" THEN GOTO 40
      IF UPCASE(FIELD(EMAILSERVER,".",2)) # "COM" THEN GOTO 40
   END
   OPEN "VOC" TO VOC ELSE PRINT "NO VOC" ; STOP
   IF LFILE[1,5]="DICT " THEN
      OPEN "DICT",LFILE[6,99] TO LEFTFILE ELSE PRINT "NO LEFT FILE ":LFILE ; STOP
   END ELSE
      OPEN "",LFILE TO LEFTFILE ELSE PRINT "NO LEFT FILE ":LFILE ; STOP
   END
   IF RFILE[1,5]="DICT " THEN
      OPEN "DICT",RFILE[6,99] TO RIGHTFILE ELSE PRINT "NO RIGHT. FILE ":RFILE ; STOP
   END ELSE
      OPEN "",RFILE TO RIGHTFILE ELSE PRINT "NO RIGHT FILE ":RFILE ; STOP
   END
   OPEN "TEMPCOMPFILE" TO TEMPCOMPFILE ELSE
      VERB = "CREATE-FILE DIR TEMPCOMPFILE "
      EXECUTE VERB
      VERB = "!chmod 777 TEMPCOMPFILE"
      UDTEXECUTE VERB
      OPEN "TEMPCOMPFILE" TO TEMPCOMPFILE ELSE
         PRINT "UNABLE TO CREATE TEMPCOMPFILE" ; STOP
      END
   END
   CURRPATH = @PATH
*
   LOGNAME = @LOGNAME
   CDATE = DATE()
   CTIME = TIME()
   OTIME = OCONV(CTIME,"MTHS")
   ODATE = OCONV(CDATE,"D4/")
   HF_NAME = LOGNAME : CDATE : CTIME
   RECS.MATCH = 0
   READ LREC FROM LEFTFILE,LITEM THEN
      READ RREC FROM RIGHTFILE,RITEM THEN
         PORTNUM=@UDTNO
         TREC = LREC
         NUM.LINES = DCOUNT(TREC,CHAR(254))
         FOR II = 1 TO NUM.LINES
            LREC<II> = TRIMF(TREC<II>)
         NEXT II
         TREC = RREC
         NUM.LINES = DCOUNT(TREC,CHAR(254))
         FOR II = 1 TO NUM.LINES
            RREC<II> = TRIMF(TREC<II>)
         NEXT II
         IF RREC = LREC THEN RECS.MATCH = 1
         WRITE LREC ON TEMPCOMPFILE,LITEM:PORTNUM:"L"
         WRITE RREC ON TEMPCOMPFILE,RITEM:PORTNUM:"R"
      END ELSE
         PRINT "RIGHT ITEM NOT ON FILE" ; STOP
      END
   END ELSE
      PRINT "LEFT ITEM NOT ON FILE" ; STOP
   END
   IF RECS.MATCH THEN
      PRINT 
      PRINT " The records match.   No print out will be created"
      DELETE TEMPCOMPFILE,LITEM:PORTNUM:"L"
      DELETE TEMPCOMPFILE,RITEM:PORTNUM:"R"
      STOP
   END
   READV LFPTR FROM VOC,LFILE,2 ELSE LFPTR = ''
   READV RPTR FROM VOC,RFILE,2 ELSE RPTR = ''
   RITEMNAME = RPTR:"/":RITEM
   LITEMNAME = LFPTR:"/":LITEM
   NRITEM=CURRPATH:"/TEMPCOMPFILE/":RITEM:PORTNUM:"R"
   NLITEM=CURRPATH:"/TEMPCOMPFILE/":LITEM:PORTNUM:"L"
   CMD = "!sdiff ":NLITEM:" ":NRITEM
   UDTEXECUTE CMD CAPTURING XXTMP
   DELETE TEMPCOMPFILE,LITEM:PORTNUM:"L"
   DELETE TEMPCOMPFILE,RITEM:PORTNUM:"R"
*
   IF EMAILTO # "" THEN
      CMD = 'SETPTR ,,,,,3,BRIEF,BANNER ':HF_NAME:',NOMESSAGE,NHEAD'
      UDTEXECUTE CMD CAPTURING JUNK
   END
*
   NUM.LINES = DCOUNT(XXTMP,CHAR(254))
   IF NUM.LINES < 4 THEN
      FOR I = 1 TO 4
         PRINT XXTMP<I>
      NEXT I
   END
   IF LISTTYPE = 'L' THEN
      PRINTER ON
      CTR =0
      PRINT LITEMNAME:SPACE(67-LEN(LITEMNAME)):RITEMNAME
      PRINT "Printed for ":LOGNAME:" at ":ODATE:" ":OTIME
      PRINT
      CTR = 4
      FOR I = 1 TO NUM.LINES
         CTR = CTR + 1
         IF REM(CTR,60) = 0 THEN
            PRINT CHAR(12)
         END
         LNUM = I"R%4"
         PRINT LNUM"L#5":XXTMP<I>[1,127]
      NEXT I
      PRINTER OFF
      PRINTER CLOSE
   END ELSE
      CURR.LINE = 1
      LAST.LINE.PRINTED=0
      NUM.PRINTED=4
      PRINTER ON
      PRINT LITEMNAME:SPACE(67-LEN(LITEMNAME)):RITEMNAME
      PRINT "Printed for ":LOGNAME:" at ":ODATE:" ":OTIME
      PRINT
      PRINTER OFF
      TIME1=1
      LOOP
      WHILE CURR.LINE <= NUM.LINES DO
         XCR = XXTMP<CURR.LINE>[65,1]
         IF XCR = ">" OR XCR = "<" OR XCR = "|" THEN
      * this line has a difference
            GOSUB 1000 ;* PRINT THIS LINE AND UP TO 5 LINES ABOVE
         END
         CURR.LINE = CURR.LINE + 1
      REPEAT
   END
   IF EMAILTO # "" THEN GOSUB EMAIL_SUB
   STOP 
*
1000 *
*
   IF LAST.LINE.PRINTED + 4 > CURR.LINE THEN
      TPTR = LAST.LINE.PRINTED + 1
   END ELSE
      TPTR = CURR.LINE - 5
      IF TPTR < 1 THEN TPTR = 1
   END
   PRINTER ON
   CURR.PLUS5 = CURR.LINE + 5
   FOR II = 1 TO 10 WHILE CURR.LINE <= CURR.PLUS5
      IF TPTR <= NUM.LINES THEN
         IF NUM.PRINTED >= 60 THEN
            PRINT CHAR(12):
            NUM.PRINTED = 0
         END
         LNUM = TPTR"R%4"
         PRINT LNUM"L#5":XXTMP<TPTR>[1,127]
         LAST.LINE.PRINTED = TPTR
         CURR.LINE = TPTR
         TPTR = TPTR+1
         NUM.PRINTED=NUM.PRINTED + 1
      END
   NEXT II
   IF NUM.PRINTED >= 60 THEN
      PRINT CHAR(12):
      NUM.PRINTED = 0
   END
   PRINT
   PRINTER OFF
   NUM.PRINTED=NUM.PRINTED+1
   RETURN
*
EMAIL_SUB: 
   CMD = 'SETPTR ,,,,,1,BRIEF'
   UDTEXECUTE CMD CAPTURING JUNK
   READ DUMMY FROM HOLDFILE, HF_NAME ELSE
      PRINT "NO DATA TO EMAIL"
      RETURN
   END
   CALL CONVERT.TO.PDF("_HOLD_",HF_NAME,'')
   EMAILSUB = "SDIFF"
   EMAILDESC = "This email from VERCOM Software, Inc. contains the SDIFF of "
   EMAILDESC := LFILE : " " : LITEM
   EMAILDESC := " to "
   EMAILDESC := RFILE : " " : RITEM
   EMAILDESC := CHAR(10)
   EMAILDESC := "The attached file is in "
   EMAILDESC := "Adobe PDF format. To open it, you must have "
   EMAILDESC := "Adobe Acrobat Reader, which can be downloaded "
   EMAILDESC := "at http://www.acrobat.com."
   EMAILDESC := CHAR(10) : CHAR(10)
   EMAILTXT = "./_HOLD_/":HF_NAME:".dsc"
   OSWRITE EMAILDESC ON EMAILTXT
   EMAILATT = "BIN":@AM:"_HOLD_/":HF_NAME:".pdf"
   EMAILERR = PMC_SEND_MAIL(EMAILTO,EMAILSUB,EMAILTXT,EMAILATT)
   DELETE HOLDFILE, HF_NAME
   DELETE HOLDFILE, HF_NAME : ".dsc"
   DELETE HOLDFILE, HF_NAME : ".pdf"
   IF EMAILERR # 0 THEN
      PRINT EMAILERR
   END
   RETURN
END
