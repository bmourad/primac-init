*********************************************************************
*
*Copyright 1982 by Computer Business Associates (Vercom Software Inc)
*
* REVISION - [NN.N]
*
* PROGRAM  - SAMPLE.LIST
*
* AUTHOR   - YOUR NAME HERE, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - MM/DD/YY
*
* DESCRIPTION
*
* This program produces the SAMPLE FILE listing.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SAMPLE
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- DEFINE ARRAY STORAGE
*
      DIM STOT(10)               ;* SUB   TOTALS
      DIM GTOT(10)               ;* GRAND TOTALS
*
*---- READ PARAMETERS
*
      PROCREAD PARAM ELSE
         ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
         GOSUB 90000
         GOTO 99999
      END
      CONO  = PARAM<1>
      CNAME = PARAM<2>
      SDATE = PARAM<3>
      EDATE = PARAM<4>
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         GOTO 95000
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         GOTO 95000
      END
      OPEN "","SAMPLE.FILE" TO SAMPLE ELSE
         ERRMSG = "CANNOT OPEN SAMPLE FILE"
         GOSUB 90000
         GOTO 95000
      END
*
*---- INITIALIZATION
*
      TODAY = DATE()
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      LINE.COUNT = 99
      PAGE.NO = 0
      MAT STOT = 0
      MAT GTOT = 0
      PREV.BRK1 = "!@#$%"
      PRINT.FLAG = 0
*
*---- DEFINE HEADINGS
*
      REPORT.NAME = "SAMPLE FILE LISTING"
      RNO = ""
      HD1 = ""
      HD2 = ""
      CALL GET.PROG.HEAD(CONO,CNAME,REPORT.NAME,RNO,TODAY,HD1,HD2)
      HD3 = SPACE(58):"FOR PERIOD ":SDATE:" THROUGH ":EDATE
      BH1 = "BREAK HEADING: "
      SH1 = "   ID        F1        F2        MF1        MF2   "
      SH2 = "--------  --------  --------  ---------  ---------"
      ST1 = "          --------  --------  ---------  ---------"
      GT1 = "          ========  ========  =========  ========="
*
*---- MAIN PROCESSING
*
100 *
      PRINTER ON
      DATA = 1
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         IF ID[1,3] = CONO THEN
            SID = ID[4,99]
            MATREAD SAMPLE.REC FROM SAMPLE, CONO:SID THEN
               IF SID[1,1] # PREV.BRK1 THEN
                  GOSUB 2000
                  LINE.COUNT = 99
                  PREV.BRK1 = SID[1,1]
               END
               GOSUB 1000
            END
         END
      REPEAT
      IF PRINT.FLAG THEN
         GOSUB 2000
         GOSUB 3000
      END ELSE
         MAT SAMPLE.REC = ""
         GOSUB 10000
         PRINT
         PRINT "NO ITEMS SELECTED"
      END
      PRINTER OFF
      PRINTER CLOSE
      GOTO 99999
*
*---- PRINT DETAIL LINE(S)
*
1000 *
      LCNT = COUNT(SAMPLE.MF1,VM)+1
      IF (LINE.COUNT + 1 + LCNT) > 55 THEN GOSUB 10000
      PRINT
      PLINE = SID"L#8"
      PLINE = PLINE:SP2:OCONV(SAMPLE.F1,"MD0,")"R#8"
      PLINE = PLINE:SP2:OCONV(SAMPLE.F2,"MD0,")"R#8"
      FOR LPTR = 1 TO LCNT
         PLINE = PLINE:SP2:OCONV(SAMPLE.MF1<1,LPTR>,"MD0,")"R#9"
         PLINE = PLINE:SP2:OCONV(SAMPLE.MF2<1,LPTR>,"MD0,")"R#9"
         PRINT PLINE
         PLINE = SPACE(28)
      NEXT LPTR
      LINE.COUNT = LINE.COUNT + 1 + LCNT
*
      IF NUM(SAMPLE.F1) THEN STOT(1) = STOT(1) + SAMPLE.F1
      IF NUM(SAMPLE.F2) THEN STOT(2) = STOT(2) + SAMPLE.F2
      FOR LPTR = 1 TO LCNT
         IF NUM(SAMPLE.MF1<1,LPTR>) THEN STOT(3) = STOT(3) + SAMPLE.MF1<1,LPTR>
         IF NUM(SAMPLE.MF2<1,LPTR>) THEN STOT(4) = STOT(4) + SAMPLE.MF2<1,LPTR>
      NEXT LPTR
      PRINT.FLAG = 1
      RETURN
*
*---- PRINT SUB-TOTALS
*
2000 *
      IF PRINT.FLAG THEN
         PRINT ST1
         TLINE = " SUB-TOT""L#8"
         TLINE = TLINE:SP2:OCONV(STOT(1),"MD0,")"R#8"
         TLINE = TLINE:SP2:OCONV(STOT(2),"MD0,")"R#8"
         TLINE = TLINE:SP2:OCONV(STOT(3),"MD0,")"R#8"
         TLINE = TLINE:SP2:OCONV(STOT(4),"MD0,")"R#8"
         PRINT TLINE
         LINE.COUNT = LINE.COUNT + 2
         FOR N = 1 TO 4
            GTOT(N) = GTOT(N) + STOT(N)
         NEXT N
         MAT STOT = 0
      END
      RETURN
*
*---- PRINT GRAND TOTALS
*
3000 *
      IF PRINT.FLAG THEN
         PRINT
         TLINE = " TOTAL""L#8"
         TLINE = TLINE:SP2:OCONV(GTOT(1),"MD0,")"R#8"
         TLINE = TLINE:SP2:OCONV(GTOT(2),"MD0,")"R#8"
         TLINE = TLINE:SP2:OCONV(GTOT(3),"MD0,")"R#8"
         TLINE = TLINE:SP2:OCONV(GTOT(4),"MD0,")"R#8"
         PRINT TLINE
         PRINT GT1
         LINE.COUNT = LINE.COUNT + 3
         MAT GTOT = 0
      END
      RETURN
*
*---- PRINT HEADINGS
*
10000 *
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1: PAGE.NO
      PRINT HD2
      PRINT HD3
      PRINT
      PRINT BH1:PREV.BRK1
      PRINT
      PRINT SH1
      PRINT SH2
      LINE.COUNT = 8
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      PRINT @(0,23):@(-4):BEL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- ABNORMAL END OF JOB
*
95000 *
      PROCWRITE "END"
      STOP
*
*---- END OF JOB
*
99999 *
      STOP
   END
