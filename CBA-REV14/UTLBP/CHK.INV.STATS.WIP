* REVISION    - [08.0]
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>CPYLIB>CHAR
      OPEN '','INV.STATS' TO INV.STATS ELSE STOP
      OPEN '','INV.JOB.STATS' TO INV.JOB.STATS ELSE STOP
      OPEN '','JOB.MATL' TO JOB.MATL ELSE STOP
      DATA = 1
      SELECT INV.JOB.STATS
      LOOP
         READNEXT IJS.ID ELSE DATA = 0
      WHILE DATA DO
         MATREADU INV.JS.REC FROM INV.JOB.STATS, IJS.ID ELSE
            RELEASE INV.JOB.STATS, IJS.ID
            GOTO 999
         END
         PRINT DATA "L#5" : " " : IJS.ID
         DATA = DATA + 1
         CONO = IJS.ID[1,3]
         INV.ID = FIELD(IJS.ID,"!",1)[4,99]
         WHS.ID = FIELD(IJS.ID,"!",2)
         JOB.ID = FIELD(IJS.ID,"!",3)
         J.DEL = 1; WIP = 0; RSV = 0
         FCNT = COUNT(IJS.FI.NO,VM) + (IJS.FI.NO # "")
         FOR F = FCNT TO 1 STEP -1
            F.DEL = 1; F.WIP = 0
            TCNT = COUNT(IJS.JMT.SEQ<1,F>,SVM) + (IJS.JMT.SEQ<1,F> # "")
            FOR T = TCNT TO 1 STEP -1
               SEQ.NO = IJS.JMT.SEQ<1,F,T>
               MT.ID = CONO:JOB.ID:"!":FIELD(SEQ.NO,"!",1):"!":FIELD(SEQ.NO,"!",2)
               MT.ID = MT.ID:"!":INV.ID:"!":WHS.ID:"!":FIELD(SEQ.NO,"!",3)
               MATREAD JMT.REC FROM JOB.MATL, MT.ID ELSE JMT.WIP = ""
               BEGIN CASE
               CASE JMT.WIP = ""
                  T.WIP = 0
               CASE JMT.LOC = ""
                  T.WIP = 0
               CASE JMT.WIP<1,1,1>+JMT.WIP<1,1,2>+JMT.WIP<1,1,3> = 0
                  T.WIP = 0
               CASE JMT.MON<1,2> = ""
                  T.WIP = 1
               CASE 1
                  W = 1; T.WIP = 0
                  LOOP WHILE JMT.MON<1,W> # "" DO
                     T.WIP = T.WIP + JMT.WIP<1,W,1> + JMT.WIP<1,W,2> + JMT.WIP<1,W,3>
                     W = W + 1
                  REPEAT
               END CASE
               IF T.WIP = 0 THEN
                  IJS.JMT.SEQ = DELETE(IJS.JMT.SEQ,1,F,T)
                  IJS.JMT.QTY = DELETE(IJS.JMT.QTY,1,F,T)
               END ELSE
                  F.DEL = 0
                  F.WIP = F.WIP + IJS.JMT.QTY<1,F,T>
               END
            NEXT T
            RSV = RSV + IJS.FI.QTY<1,F>
            WIP = WIP + F.WIP
            IF F.DEL AND IJS.FI.QTY<1,F> + 0 = 0 THEN
               IJS.FI.NO = DELETE(IJS.FI.NO,1,F,0)
               IJS.FI.ORG = DELETE(IJS.FI.ORG,1,F,0)
               IJS.FI.QTY = DELETE(IJS.FI.QTY,1,F,0)
               IJS.FI.AMT = DELETE(IJS.FI.AMT,1,F,0)
               IJS.JMT.SEQ = DELETE(IJS.JMT.SEQ,1,F,0)
               IJS.JMT.QTY = DELETE(IJS.JMT.QTY,1,F,0)
            END ELSE
               J.DEL = 0
               IJS.FI.ORG<1,F> = IJS.FI.QTY<1,F> + F.WIP
            END
         NEXT F
         IJS.JOB.QTY = RSV
         IJS.JOB.USED = WIP
         IF J.DEL AND IJS.JOB.QTY + 0 = 0 AND IJS.JOB.ALOC + 0 = 0 AND IJS.REQ.QTY + 0 = 0 THEN
            IF IJS.PO = "" THEN
               DELETE INV.JOB.STATS, IJS.ID
               JOBNO = FIELD(IJS.ID,"!",3)
               ISTAT.ID = CONO:INV.ID:"!":WHS.ID
               MATREADU INV.STAT.REC FROM INV.STATS, ISTAT.ID ELSE
                  MAT INV.STAT.REC = ""
               END
               LOCATE JOBNO IN ISTAT.JOB<1>,1 SETTING JLOC ELSE JLOC = 0
               IF JLOC THEN ISTAT.JOB = DELETE(ISTAT.JOB,1,JLOC,0)
               IF ISTAT.JOB = "" AND ISTAT.PO = "" THEN
                  DELETE INV.STATS, ISTAT.ID
               END ELSE
                  MATWRITE INV.STAT.REC ON INV.STATS, ISTAT.ID
               END
            END ELSE
               IJS.JOB.QTY = 0
               IJS.JOB.USED = 0
               IJS.FI.NO = ""
               IJS.FI.ORG = ""
               IJS.FI.QTY = ""
               IJS.FI.AMT = ""
               IJS.JMT.SEQ = ""
               IJS.JMT.QTY = ""
               MATWRITE INV.JS.REC ON INV.JOB.STATS, IJS.ID
            END
         END ELSE
            MATWRITE INV.JS.REC ON INV.JOB.STATS, IJS.ID
         END
999   REPEAT
   END
