      SUBROUTINE BUILD.SYS.SCREENS
********************************************************************
* REVISION    - [08.1]
********************************************************************
*COPY>CPYLIB>COM1
*COPY>UTL.CPYLIB>COM.BUILD.PMC.SYS
*COPY>UTL.CPYLIB>BUILD.PMC.SYS
*COPY>UTL.CPYLIB>SYS.SCREENS
*COPY>CPYLIB>CHAR
*
      CLEARFILE SYS.SCREENS
*
      FUNCS = "FLD"
      FUNCS<2> = "DIM"
      FUNCS<3> = "FRM"
      FUNCS<4> = "CLR"
*     DIM.ITEM = 0
*     DIM.ITEM<2> = 22
*     DIM.ITEM<3> = 0
*     DIM.ITEM<4> = 79
*     DIM.ITEM<5> = 0
*     DIM.ITEM<6> = 23
*     DIM.ITEM<7> = 0
*     DIM.ITEM<8> = 79
*
      SCRN.CNT = 0
      IF CUST.S.OK THEN
         PRINT @(0,23) : "Processing customer screens" : CL :
         SCRN.FILE = CUST.SCREENS
         SF.NAME = SYS.ACCTS(2)
         GOSUB 1000
      END
      PRINT @(0,23) : "Processing screens" : CL :
      LN = 1; D.PRD = 1
      CALL SCL.PMC.SYS
      MAX = LINES + 1
      FOR S = MAX TO CBA.LINES
         SCRN.FILE = CBA.FILES(3,S)
         SF.NAME = CBA.FNAME(3,S)
         GOSUB 1000
      NEXT S
      FOR S = 1 TO LINES
         LN = S; D.PRD = S
         CALL SCL.PMC.SYS
         SCRN.FILE = CBA.FILES(3,S)
         SF.NAME = CBA.FNAME(3,S)
         GOSUB 1000
      NEXT S
      LN = 1
      CALL SCL.PMC.SYS
      PRINT @(0,23) : "Verifying all screens" : CL :
      PRINT @(36,23) : "of " : SCRN.CNT "L#6" :
      GOSUB 2000
      GOTO 99999
*
*---- Build the screens file name reference
1000  SELECT SCRN.FILE
      DATA = 1
      LOOP
         READNEXT SCRN.ID ELSE DATA = 0
      WHILE DATA DO
         SCRN.NAME = FIELD(SCRN.ID,"*",1)
         PRINT @(30,23) : DATA "L#6" : SCRN.ID : CL :
         DATA = DATA + 1
         READ SCRN.ITEM FROM SCRN.FILE, SCRN.ID ELSE
            ERRMSG = "Cannot locate ":SF.NAME:", ":SCRN.ID
            GOSUB 91000; GOTO 1090
         END
         IF SCRN.ITEM = "" THEN
            ERRMSG = "NULL screen item ":SF.NAME:", ":SCRN.ID
            GOSUB 91000; GOTO 1090
         END
         MATREAD SSC.REC FROM SYS.SCREENS, SCRN.NAME THEN
            BEGIN CASE
            CASE SSC.SOURCE = SF.NAME
            CASE SSC.SOURCE = SYS.ACCTS(2) AND SSC.ORG.SRC = SF.NAME
               GOTO 1090
            CASE SSC.SOURCE = SYS.ACCTS(2) AND SSC.ORG.SRC = ""
               SSC.ORG.SRC = SF.NAME
               GOTO 1080
            CASE SSC.ORG.SRC # ""
               ERRMSG = "Screen (":SCRN.NAME:") resides in - ":SSC.ORG.SRC:" & ":SF.NAME
               GOSUB 91000; GOTO 1090
            CASE 1
               ERRMSG = "Screen (":SCRN.NAME:") resides in - ":SSC.SOURCE:" & ":SF.NAME
               GOSUB 91000; GOTO 1090
            END CASE
         END ELSE
            MAT SSC.REC = ""
            SSC.SOURCE = SF.NAME
         END
         FUNC = FIELD(SCRN.ID,"*",2)
         LOCATE FUNC IN FUNCS SETTING FND THEN
            SSC.FUNC<1,FND> = FUNCS<FND>
            IF FND = 1 THEN SCRN.CNT = SCRN.CNT + 1
         END ELSE
            ECD.NUM = FUNC[5,999]
            FUNC = FUNC[1,4]
            BEGIN CASE
            CASE FUNC = "HMSG"
               LOCATE ECD.NUM IN SSC.HMSG<1> BY "AR" SETTING FND ELSE
                  INS ECD.NUM BEFORE SSC.HMSG<1,FND>
               END
            CASE FUNC = "NAME"
               LOCATE ECD.NUM IN SSC.NAME<1> BY "AR" SETTING FND ELSE
                  INS ECD.NUM BEFORE SSC.NAME<1,FND>
               END
            CASE 1
               ERRMSG = "Invalid screen fuction ":SF.NAME:", ":SCRN.ID
               GOSUB 91000; GOTO 1090
            END CASE
         END
1080     MATWRITE SSC.REC ON SYS.SCREENS, SCRN.NAME
1090  REPEAT
      RETURN
2000  SELECT SYS.SCREENS
      DATA = 1
      LOOP
         READNEXT SCRN.ID ELSE DATA = 0
      WHILE DATA DO
         MATREAD SSC.REC FROM SYS.SCREENS, SCRN.ID ELSE
            ERRMSG = "Cannot locate SYS.SCREENS, ":SCRN.ID
            GOSUB 91000; GOTO 2099
         END
         IF SSC.FUNC<1> = "" THEN
            ERRMSG = "Cannot locate the FLD item for screen - ":SCRN.ID
            GOSUB 91000; GOTO 2099
         END
         IF SSC.SOURCE = SYS.ACCTS(2) THEN
            SCRN.FILE = CUST.SCREENS
            SF.NAME = SSC.SOURCE
         END ELSE
            LOCATE SSC.SOURCE[1,3] IN PMC.ACCTS SETTING SCRF THEN
               SCRN.FILE = CBA.FILES(3,SCRF)
               SF.NAME = CBA.FNAME(3,SCRF)
            END ELSE
               IF SSC.SOURCE = "M.SCREENS" THEN
                  SCRN.FILE = CBA.FILES(3,1)
                  SF.NAME = CBA.FNAME(3,1)
               END ELSE
                  ERRMSG = "Cannot locate system initials for - ":SSC.SOURCE
                  GOSUB 91000; GOTO 2099
               END
            END
         END
         READ SCRN.ITEM FROM SCRN.FILE, SCRN.ID:"*FLD" ELSE
            ERRMSG = "Cannot locate ":SF.NAME:", ":SCRN.ID:"*FLD"
            GOSUB 91000; GOTO 2099
         END
*******  IF SSC.FUNC<2> = "" THEN
*******     SSC.FUNC<2> = FUNCS<2>
*******     WRITE DIM.ITEM ON SCRN.FILE, SCRN.ID:"*DIM"
*******  END
*******  FOR S = 3 TO 4
         FOR S = 2 TO 4
            IF SSC.FUNC<1,S> = "" THEN
               ERRMSG = "Cannot locate the ":FUNCS<S>:" item for screen - ":SCRN.ID
               GOSUB 91500
            END
         NEXT S
         CNT = DCOUNT(SSC.HMSG,VM)
         FOR S = CNT TO 1 STEP -1
            LOCATE SSC.HMSG<1,S> IN SCRN.ITEM<61> SETTING FND ELSE
               ERRMSG = "Help message field (":SSC.HMSG<1,S>:") is not defined in screen - ":SCRN.ID
               GOSUB 91500
               SSC.HMSG = DELETE(SSC.HMSG,1,S,0)
            END
         NEXT S
         CNT = DCOUNT(SSC.NAME,VM)
         FOR S = CNT TO 1 STEP -1
            LOCATE SSC.NAME<1,S> IN SCRN.ITEM<61> SETTING FND ELSE
               ERRMSG = "Documentation field (":SSC.NAME<1,S>:") is not defined in screen - ":SCRN.ID
               GOSUB 91500
               SSC.NAME = DELETE(SSC.NAME,1,S,0)
            END
         NEXT S
         MATWRITE SSC.REC ON SYS.SCREENS, SCRN.ID
         PRINT @(30,23) : DATA "L#6" :
         PRINT @(45,23) : SCRN.ID : CL :
         DATA = DATA + 1
2099  REPEAT
      RETURN
91000 WRITE ERRMSG ON SYS.ERRORS, ERR.SEQ
      ERR.SEQ = ERR.SEQ + 1
      RETURN
91500 WRITE ERRMSG ON SYS.WARNINGS, WRN.SEQ
      WRN.SEQ = WRN.SEQ + 1
      RETURN
99999 RETURN
   END
