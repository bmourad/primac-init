***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM - UNDERLINE.DICT.HEAD
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 07/23/84
*
* DESCRIPTION
*
* This program will underline headings defined in dictionaries.
*
***************************************************************************
*
*---- SYSTEM EQUATES
*
      EQU VM TO CHAR(253)
      CS = CHAR(12)
      CL = CHAR(27):CHAR(75)
*
*---- INITIALIZATION
*
      PROMPT ""
      DIM DICT.REC(20)
      EQU DTYPE   TO DICT.REC(1)
      EQU HEAD    TO DICT.REC(3)
      EQU JUSTIFY TO DICT.REC(9)
      EQU WIDTH   TO DICT.REC(10)
*
*---- DISPLAY SCREEN
*
      SCRN = CS
      SCRN = SCRN : @(00,00) : "C.B.A."
      SCRN = SCRN : @(25,00) : "UNDERLINE DICTIONARY HEADINGS"
      SCRN = SCRN : @(72,00) : OCONV(DATE(),"D2/")
      SCRN = SCRN : @(00,01) : STR("-",80)
      SCRN = SCRN : @(00,22) : STR("-",80)
      PRINT SCRN :
*
*---- GET FILE NAME
*
10*
      PRINT @(20,11) : CL : "FILE NAME - ":
      INPUT FILE.NAME
      IF FILE.NAME = "" OR FILE.NAME = "END" THEN
         PRINT @(-1):
         STOP
      END
*
*---- OPEN FILE DICTIONARY
*
      OPEN "DICT", FILE.NAME TO FILE ELSE
         PRINT @(00,23) : CL : "INVALID FILE NAME"
         GOTO 10
      END
*
      SELECT FILE
      PRINT @(00,23) : CL : "** PROCESSING **":
100*
      READNEXT ID ELSE
         PRINT @(00,23) : CL : "** COMPLETED **":
         GOTO 10
      END
      IF NUM(ID[1,1]) THEN GOTO 100
      IF ID = "DL/ID" THEN GOTO 100
      MATREADU DICT.REC FROM FILE, ID ELSE
         RELEASE FILE, ID
         GOTO 100
      END
      IF DTYPE # "A" AND DTYPE # "S" THEN GOTO 190
      IF HEAD = "" THEN HEAD = ID
      IF NUM(HEAD) THEN GOTO 190
      IF INDEX(HEAD,":",1) > 0 THEN GOTO 190
      IF WIDTH = 0 THEN GOTO 190
      IF HEAD = STR(".",LEN(HEAD)) THEN GOTO 190
      IF HEAD = STR(" ",LEN(HEAD)) THEN GOTO 190
      MAXWIDTH = WIDTH
      HCNT = COUNT(HEAD,VM) + (HEAD # "")
      FOR N = 1 TO HCNT
         HEAD<1,N> = TRIM(HEAD<1,N>)
      NEXT N
      IF HEAD<1,HCNT> = STR("-",LEN(HEAD<1,HCNT>)) THEN GOTO 190
      FOR N = 1 TO HCNT
         IF LEN(HEAD<1,N>) > MAXWIDTH THEN MAXWIDTH = LEN(HEAD<1,N>)
      NEXT N
      NEW.HEAD = ""
      FMT = "L#" : MAXWIDTH
      FOR N = 1 TO HCNT
         HEAD<1,N> = TRIM(HEAD<1,N>)
         HW = LEN(HEAD<1,N>)
         IF JUSTIFY = "R" THEN
            SPC = INT((MAXWIDTH-HW+1)/2)
         END ELSE
            SPC = INT((MAXWIDTH-HW)/2)
         END
         NEW.HEAD<1,N> = SPACE(SPC):HEAD<1,N>
         NEW.HEAD<1,N> = NEW.HEAD<1,N> FMT
      NEXT N
      NEW.HEAD = NEW.HEAD : VM : STR("-",MAXWIDTH)
*     PRINT HEAD : " CHANGED TO " : NEW.HEAD
      HEAD = NEW.HEAD
      MATWRITE DICT.REC ON FILE, ID
      GOTO 100
190*
      RELEASE FILE, ID
      GOTO 100
   END
