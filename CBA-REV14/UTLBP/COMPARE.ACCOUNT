***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM - COMPARE.ACCOUNT
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/13/84
*
* DESCRIPTION
*
* This program will compare files in two different accounts as follows:
*
*    1. Compare the MD entries excluding file pointers
*    2. Compare all files for presence
*    3. Compare dictionaries of all data files
*    4. Compare programs in program file
*    5. Compare entries in library file
*    6. Compare procedures in procedure file
*    7. Compare menus in menu file
*
***************************************************************************
*
*---- SYSTEM EQUATES
*
      EQU AM TO CHAR(254)
      EQU VM TO CHAR(253)
      EQU SM TO CHAR(252)
      CS = CHAR(12)
*
*---- DEFINE VARIABLES
*
      DIM QREC1(10)
      DIM QREC2(10)
*
*---- OPEN ALL FILES
*
      OPEN "","MD" TO MD ELSE
         STOP "CANNOT OPEN MD FILE"
      END
*
*---- INITIALIZATION
*
      PROMPT ""
      MAT QREC1 = ""
      QREC1(1) = "Q"
      QREC1(9) = "L"
      QREC1(10) = "10"
      MAT QREC2 = ""
      QREC2(1) = "Q"
      QREC2(9) = "L"
      QREC2(10) = "10"
      FILES = ""
      FFLAG = ""
      PAGE.NO = 0
      LINE.NO = 99
      LM = SPACE(5)
*
*---- GET ACCOUNT NAMES
*
10*
      PRINT CS
12*
      PRINT
      PRINT "ENTER ACCOUNT 1 NAME - ":
      INPUT ACCT1
      IF ACCT1 = "" OR ACCT1 = "END" THEN
         PRINT CS
         STOP
      END
      QREC1(2) = ACCT1
      QREC1(3) = "MD"
      MATWRITE QREC1 ON MD, "QFILE1"
      OPEN "","QFILE1" TO QFILE1 ELSE
         ERRMSG = "INVALID ACCOUNT NAME"
         GOSUB 90000
         GOTO 12
      END
      READV ADDR1 FROM QFILE1, "MD",2 ELSE ADDR1 = ""
14*
      PRINT
      PRINT "ENTER ACCOUNT 2 NAME - ":
      INPUT ACCT2
      IF ACCT2 = "" OR ACCT2 = "END" THEN
         PRINT CS
         STOP
      END
      QREC2(2) = ACCT2
      QREC2(3) = "MD"
      MATWRITE QREC2 ON MD, "QFILE2"
      OPEN "","QFILE2" TO QFILE2 ELSE
         ERRMSG = "INVALID ACCOUNT NAME"
         GOSUB 90000
         GOTO 14
      END
      READV ADDR2 FROM QFILE2, "MD",2 ELSE ADDR2 = ""
16*
      PRINT
      PRINT "COMPARE MASTER DICTIONARY (Y/N) ":
      INPUT MD.FLAG
      PRINT
      PRINT "COMPARE FILE NAMES (Y/N) ":
      INPUT FILE.FLAG
      PRINT
      PRINT "COMPARE DICTIONARIES (Y/N) ":
      INPUT DICT.FLAG
      PRINT
      PRINT "COMPARE PROGRAMS (Y/N) ":
      INPUT PROG.FLAG
      IF PROG.FLAG = "Y" THEN
         PRINT
         PRINT "PROGRAM FILE NAME - ":
         INPUT PROG.FILE
      END
      PRINT
      PRINT "COMPARE CPYLIB (Y/N) ":
      INPUT CPYLIB.FLAG
      PRINT
      PRINT "COMPARE PROCS (Y/N) ":
      INPUT PROC.FLAG
      IF PROC.FLAG = "Y" THEN
         PRINT
         PRINT "PROC FILE NAME - ":
         INPUT PROC.FILE
      END
      PRINT
      PRINT "COMPARE MENUS (Y/N) ":
      INPUT MENU.FLAG
      IF MENU.FLAG = "Y" THEN
         PRINT
         PRINT "MENU FILE NAME - ":
         INPUT MENU.FILE
      END
      PRINT
      PRINT "PRINT ON LINE PRINTER (Y/N) ":
      INPUT PFLAG
      IF PFLAG = "Y" THEN
         PRINTER ON
      END
*
*---- GET FILE NAMES FOR ACCOUNT 1
*
100*
      IF FILE.FLAG # "Y" AND DICT.FLAG # "Y" THEN GOTO 300
      SELECT QFILE1
110*
      READNEXT ID1 ELSE GOTO 200
      READ REC1 FROM QFILE1, ID1 ELSE GOTO 110
      IF REC1<1> = "D" AND REC1<2> # ADDR1 THEN
         LOCATE ID1 IN FILES<1>,1 BY "AL" SETTING P ELSE NULL
         FILES = INSERT(FILES,1,P,0,ID1)
         FFLAG = INSERT(FFLAG,1,P,0,1)
      END
      GOTO 110
*
*---- GET FILE NAMES FOR ACCOUNT 2
*
200*
      SELECT QFILE2
210*
      READNEXT ID2 ELSE GOTO 250
      READ REC2 FROM QFILE2, ID2 ELSE GOTO 210
      IF REC2<1> = "D" AND REC2<2> # ADDR2 THEN
         LOCATE ID2 IN FILES<1>,1 BY "AL" SETTING P ELSE
            FILES = INSERT(FILES,1,P,0,ID2)
            FFLAG = INSERT(FFLAG,1,P,0,0)
         END
         FFLAG<1,P> = FFLAG<1,P> + 2
      END
      GOTO 210
*
*---- COMPARE FILES IF REQUESTED
*
250*
      IF FILE.FLAG = "Y" THEN
         GOSUB 1000
      END
*
*---- COMPARE MASTER DICTIONARIES
*
300*
      IF MD.FLAG = "Y" THEN
         QREC1(2) = ACCT1
         QREC1(3) = "MD"
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         QREC2(2) = ACCT2
         QREC2(3) = "MD"
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT2
         END
         CALL COMPARE.DATA (ACCT1, ACCT2, "MD", QFILE1, QFILE2, PAGE.NO, LINE.NO, "")
      END
*
*---- COMPARE FILE DICTIONARIES
*
400*
      IF DICT.FLAG = "Y" THEN
         FILE.CNT = COUNT(FILES,VM) + (FILES # "")
         FOR F = 1 TO FILE.CNT
            IF FFLAG<1,F> = 3 THEN
               QREC1(2) = ACCT1
               QREC1(3) = FILES<1,F>
               MATWRITE QREC1 ON MD, "QFILE1"
               OPEN "DICT","QFILE1" TO QFILE1 ELSE
                  PRINT "CANNOT OPEN 'DICT' ":FILES<1,F>
                  GOTO 450
               END
               QREC2(2) = ACCT2
               QREC2(3) = FILES<1,F>
               MATWRITE QREC2 ON MD, "QFILE2"
               OPEN "DICT","QFILE2" TO QFILE2 ELSE
                  PRINT "CANNOT OPEN 'DICT' ":FILES<1,F>
                  GOTO 450
               END
               FILE.NAME = "DICT*":FILES<1,F>
               CALL COMPARE.DATA (ACCT1, ACCT2, FILE.NAME, QFILE1, QFILE2, PAGE.NO, LINE.NO, "")
            END
450*
         NEXT F
      END
*
*---- COMPARE PROGRAMS
*
500*
      IF PROG.FLAG = "Y" THEN
         QREC1(2) = ACCT1
         QREC1(3) = "MD"
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         READ REC1 FROM QFILE1, PROG.FILE ELSE
            PRINT "CANNOT LOCATE PROGRAM FILE ":PROG.FILE:" FOR ":ACCT1
            GOTO 550
         END
         IF REC1<1> = "Q" THEN
            QREC1(2) = REC1<2>
            QREC1(3) = REC1<3>
         END ELSE
            QREC1(2) = ACCT1
            QREC1(3) = PROG.FILE
         END
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            PRINT "CANNOT OPEN PROGRAM FILE ":PROG.FILE:" FOR ":ACCT1
            GOTO 550
         END
         QREC2(2) = ACCT2
         QREC2(3) = "MD"
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         READ REC2 FROM QFILE2, PROG.FILE ELSE
            PRINT "CANNOT LOCATE PROGRAM FILE ":PROG.FILE:" FOR ":ACCT2
            GOTO 550
         END
         IF REC2<1> = "Q" THEN
            QREC2(2) = REC2<2>
            QREC2(3) = REC2<3>
         END ELSE
            QREC2(2) = ACCT2
            QREC2(3) = PROG.FILE
         END
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            PRINT "CANNOT OPEN PROGRAM FILE ":PROG.FILE:" FOR ACCT2"
            GOTO 550
         END
         CALL COMPARE.DATA (ACCT1, ACCT2, PROG.FILE, QFILE1, QFILE2, PAGE.NO, LINE.NO, "PROG")
550*
      END
*
*---- COMPARE PROCS
*
600*
      IF PROC.FLAG = "Y" THEN
         QREC1(2) = ACCT1
         QREC1(3) = "MD"
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         READ REC1 FROM QFILE1, PROC.FILE ELSE
            PRINT "CANNOT LOCATE PROC FILE ":PROC.FILE:" FOR ":ACCT1
            GOTO 650
         END
         IF REC1<1> = "Q" THEN
            QREC1(2) = REC1<2>
            QREC1(3) = REC1<3>
         END ELSE
            QREC1(2) = ACCT1
            QREC1(3) = PROC.FILE
         END
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            PRINT "CANNOT OPEN PROC FILE ":PROC.FILE:" FOR ":ACCT2
            GOTO 650
         END
         QREC2(2) = ACCT2
         QREC2(3) = "MD"
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         READ REC2 FROM QFILE2, PROC.FILE ELSE
            PRINT "CANNOT LOCATE PROC FILE ":PROC.FILE:" FOR ":ACCT2
            GOTO 650
         END
         IF REC2<1> = "Q" THEN
            QREC2(2) = REC2<2>
            QREC2(3) = REC2<3>
         END ELSE
            QREC2(2) = ACCT2
            QREC2(3) = PROC.FILE
         END
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            PRINT "CANNOT OPEN PROC FILE ":PROC.FILE:" FOR ":ACCT2
            GOTO 650
         END
         CALL COMPARE.DATA (ACCT1, ACCT2, PROC.FILE, QFILE1, QFILE2, PAGE.NO, LINE.NO, "")
650*
      END
*
*---- COMPARE MENUS
*
700*
      IF MENU.FLAG = "Y" THEN
         QREC1(2) = ACCT1
         QREC1(3) = "MD"
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         READ REC1 FROM QFILE1, MENU.FILE ELSE
            PRINT "CANNOT LOCATE MENU FILE ":MENU.FILE:" FOR ":ACCT1
            GOTO 750
         END
         IF REC1<1> = "Q" THEN
            QREC1(2) = REC1<2>
            QREC1(3) = REC1<3>
         END ELSE
            QREC1(2) = ACCT1
            QREC1(3) = MENU.FILE
         END
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            PRINT "CANNOT OPEN MENU FILE ":MENU.FILE:" FOR ":ACCT1
            GOTO 750
         END
         QREC2(2) = ACCT2
         QREC2(3) = "MD"
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT2
         END
         READ REC2 FROM QFILE2, MENU.FILE ELSE
            PRINT "CANNOT LOCATE MENU FILE ":MENU.FILE:" FOR ":ACCT2
            GOTO 750
         END
         IF REC2<1> = "Q" THEN
            QREC2(2) = REC2<2>
            QREC2(3) = REC2<3>
         END ELSE
            QREC2(2) = ACCT2
            QREC2(3) = MENU.FILE
         END
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            PRINT "CANNOT OPEN MENU FILE ":MENU.FILE:" FOR ":ACCT2
            GOTO 750
         END
         CALL COMPARE.DATA (ACCT1, ACCT2, MENU.FILE, QFILE1, QFILE2, PAGE.NO, LINE.NO, "")
750*
      END
*
*---- COMPARE CPYLIB
*
800*
      IF CPYLIB.FLAG = "Y" THEN
         QREC1(2) = ACCT1
         QREC1(3) = "MD"
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT1
         END
         READ REC1 FROM QFILE1, "CPYLIB" ELSE
            PRINT "CANNOT LOCATE LIBRARY FILE ":"CPYLIB":" FOR ":ACCT1
            GOTO 850
         END
         IF REC1<1> = "Q" THEN
            QREC1(2) = REC1<2>
            QREC1(3) = REC1<3>
         END ELSE
            QREC1(2) = ACCT1
            QREC1(3) = "CPYLIB"
         END
         MATWRITE QREC1 ON MD, "QFILE1"
         OPEN "","QFILE1" TO QFILE1 ELSE
            PRINT "CANNOT OPEN LIBRARY FILE ":"CPYLIB":" FOR ":ACCT1
            GOTO 850
         END
         QREC2(2) = ACCT2
         QREC2(3) = "MD"
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            STOP "CANNOT OPEN MASTER DICTIONARY FOR ":ACCT2
         END
         READ REC2 FROM QFILE2, "CPYLIB" ELSE
            PRINT "CANNOT LOCATE LIBRARY FILE ":"CPYLIB":" FOR ":ACCT2
            GOTO 850
         END
         IF REC2<1> = "Q" THEN
            QREC2(2) = REC2<2>
            QREC2(3) = REC2<3>
         END ELSE
            QREC2(2) = ACCT2
            QREC2(3) = "CPYLIB"
         END
         MATWRITE QREC2 ON MD, "QFILE2"
         OPEN "","QFILE2" TO QFILE2 ELSE
            PRINT "CANNOT OPEN LIBRARY FILE ":"CPYLIB":" FOR ":ACCT2
            GOTO 850
         END
         CALL COMPARE.DATA (ACCT1, ACCT2, "CPYLIB", QFILE1, QFILE2, PAGE.NO, LINE.NO, "")
850*
      END
      GOTO 99999
*
*---- PRINT FILE MISSING REPORT
*
1000*
      SH1 = "COMPARING ALL FILES"
      SH2 = "-------------------"
      IF LINE.NO > 50 THEN
         GOSUB 10000
      END
      PRINT
      PRINT SH1
      PRINT SH2
      LINE.NO = LINE.NO + 3
      NONE = 1
      FILE.CNT = COUNT(FILES,VM) + (FILES # "")
      FOR F = 1 TO FILE.CNT
         BEGIN CASE
            CASE FFLAG<1,F> = 2
               NONE = 0
               PLINE = "MISSING IN ACCOUNT 1 - ":FILES<1,F>
               GOSUB 900
            CASE FFLAG<1,F> = 1
               NONE = 0
               PLINE = "MISSING IN ACCOUNT 2 - ":FILES<1,F>
               GOSUB 900
         END CASE
      NEXT F
      IF NONE THEN
         PLINE = "ALL FILES MATCH"
         GOSUB 900
      END
      RETURN
*
*---- PRINT ROUTINE
*
900*
      IF LINE.NO > 55 THEN
         GOSUB 10000
         PRINT
         PRINT SH1
         PRINT SH2
         LINE.NO = LINE.NO + 3
      END
      PRINT SPACE(5):PLINE
      LINE.NO = LINE.NO + 1
      RETURN
*
*---- PRINT HEADINGS
*
10000*
      CALL COMPARE.HEADINGS (ACCT1, ACCT2, PAGE.NO, LINE.NO)
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT ERRMSG:
      INPUT REPLY
      RETURN
*
*---- END OF PROGRAM
*
99999*
      IF PFLAG = "Y" THEN
         PRINTER OFF
         PRINTER CLOSE
      END
      STOP
   END
