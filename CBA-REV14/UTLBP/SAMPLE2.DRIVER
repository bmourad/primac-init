*********************************************************************
*
* Copyright 1982 by Vercom Software Inc.
*
* REVISION - [NN.N]
*
* PROGRAM  - SAMPLE2.DRIVER
*
* AUTHOR   - YOUR NAME HERE, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - MM/DD/YY
*
* DESCRIPTION
*
* This program is used to demonstrate the code necessary to invoke
* a SCRN.EDIT subroutine.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SAMPLE
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- PRE-INITIALIZATION
*
      PROCREAD PARAM ELSE PARAM = ""
      ACTION = PARAM<1>
*
*---- INITIALIZE SYSCOM
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOTO 93000
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOTO 93000
      END
      OPEN "","SAMPLE.FILE" TO SAMPLE ELSE
         ERRMSG = "CANNOT OPEN SAMPLE FILE"
         GOTO 93000
      END
      OPEN "","UTL.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN UTL.SCREENS FILE"
         GOTO 93000
      END
*
*---- INITIALIZATION
*
      CONO = ""
      MAT COMP.REC = ""
      CALL GET.CONO (CONO, MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
*---- INITIALIZE EDIT.SUB
      MAT EDIT.COM = ""
      TYP = 0
      CALL EDIT.SUB
      FILL = "#"
*---- INITIALIZE SCRN.EDIT
      MAT SCV.REC = ""
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.CNT = 3
      ECD.SCRN.NAME<1> = "SAMPLE.DRIVER"
      ECD.SCRN.NAME<2> = "SAMPLE.MAINT"
      ECD.SCRN.NAME<3> = "SAMPLE.INQ"
      ECD.ACTION=1; CALL SCRN.EDIT
      ECD.SCRN.NO = 1
      ESN = ECD.SCRN.NO
      ECD.ACTION=2; CALL SCRN.EDIT
*---- INITIALIZE OTHER VARIABLES
      BEGIN.PAGE = 10
      PAGE.SIZE = 5
      LINE.SPACE = 1
      LINES = 0
      LN = ""
      OLD.START = ""
      SAMPLE.ID = ""
      MSG = ""
      MSG<1,1> = "This driver program demonstrates the coding"
      MSG<1,2> = "required to invoke a SCRN.EDIT subroutine"
      MSG<1,3> = "and redisplaying the data upon return from"
      MSG<1,4> = "the subroutine."
      GOTO 110
*
*---- MAIN PROCESSING
*
100 *
      ECD.ACTION=6; CALL SCRN.EDIT
110 *
      LINES = DCOUNT(MSG,VM)
      GOSUB 81000
      GOSUB 80000
      LN = 1
      GOSUB 1010
      IF ECD.RET.VALUE = "END" THEN GOTO 99999
      NEW.REC = 0
      READV TREC FROM SAMPLE, CONO:ECD.RET.VALUE,1 ELSE NEW.REC = 1
      SAMPLE.ID = ECD.RET.VALUE
*
*---- GET OPERATOR REPLY
*
500 *
      ECD.NUM = 21
      ECD.ACTION=4; CALL SCRN.EDIT
      OPT = ECD.RET.VALUE
510 *
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 100
      CASE OPT = "M"
         ECD.SCRN.NO = 2
         CALL SAMPLE2.SUB("M", CONO, SAMPLE.ID, SAMPLE, STATUS)
         ECD.SCRN.NO = 1
         ECD.ACTION=2; CALL SCRN.EDIT
         GOSUB 80050
         OLD.START = ""
         GOSUB 50000
         IF NEW.REC THEN
            NEW.REC = 0
            READV TREC FROM SAMPLE, CONO:ECD.RET.VALUE,1 ELSE NEW.REC = 1
         END
      CASE OPT = "I" AND NEW.REC
         ERRMSG = "Item not on file. Try again! "
         GOSUB 91000
      CASE OPT = "I"
         ECD.SCRN.NO = 3
         CALL SAMPLE2.INQ("I", CONO, SAMPLE.ID, SAMPLE, STATUS)
         ECD.SCRN.NO = 1
         ECD.ACTION=2; CALL SCRN.EDIT
         GOSUB 80050
         OLD.START = ""
         GOSUB 50000
      END CASE
      GOTO 500
*
*---- GET SAMPLE ITEM ID
*
1010 *
      ECD.NUM = 2
      ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      SAMPLE.ID = ECD.RET.VALUE
      RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
      START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
      LAST.LINE = START.LINE + PAGE.SIZE - 1
      IF LAST.LINE > LINES THEN LAST.LINE = LINES
      IF START.LINE = OLD.START THEN RETURN
      OLD.START = START.LINE
      CNT = 1
      FOR N = START.LINE TO LAST.LINE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
         PRINT @(15,SLN) : @(-4) : SCV.REC(12)<ESN,N> "L#50" :
         CNT = CNT + 1
      NEXT N
      FOR N = CNT TO PAGE.SIZE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
         PRINT @(15,SLN) : @(-4) :
      NEXT N
      RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
      SCV.REC(1)<ESN> = DATE()
      SCV.REC(2)<ESN> = SAMPLE.ID
80050 *
      ECD.ACTION=3; CALL SCRN.EDIT
      RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
      SCV.REC(12)<ESN> = MSG
      RETURN
*
*---- ERROR ROUTINE
*
91000 ERR.TYPE=1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 ERR.TYPE=2; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE=3; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      PRINT @(-1)
   END
