***************************************************************************
*
* PROGRAM - PROG.DOC.LIST
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/18/84
*
* DESCRIPTION
*
*  Print program technical documentation providing the following:
*
*        1.  Source file name
*        2.  Program name
*        3.  Program type (main or subroutine)
*        4.  Date written
*        5.  Program narrative
*        6.  Screen name
*        7.  Files accessed
*        8.  File usage (open, read, lock, write and delete)
*        9.  Subroutines called
*       10.  Called by program (subroutines only)
*
***************************************************************************
*
*COPY>CPYLIB>PROG.FILE.XREF
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      DIM PG(10)
      MAT PG = ""
      PG(1) = "i"
      PG(2) = "ii"
      PG(3) = "iii"
      PG(4) = "iv"
      PG(5) = "v"
      PG(6) = "vi"
      PG(7) = "vii"
      PG(8) = "viii"
      PG(9) = "ix"
      PG(10) = "x"
*
      PROMPT ""
      INDENT = SPACE(3)
      CO.NAME = "Vercom Software, Inc."
*
*---- OPEN ALL FILES
*
      OPEN "","PROG.FILE.XREF" TO PROG.FILE.XREF ELSE
         STOP "PROG.FILE.XREF FILE IS MISSING"
      END
      OPEN "","PROG.DOC" TO PROG.DOC ELSE
         STOP "CANNOT OPEN PROG.DOC FILE"
      END
*
*---- INPUT PROGRAM NAME
*
10*
      PRINT @(-1):
      PRINT @(0,0):"VERCOM":
      PRINT @(25,0):"PRINT PROGRAM DOCUMENTATION":
      PRINT @(72,0):OCONV(DATE(),"D2/"):
      PRINT @(0,1):STR("-",80):
      PRINT @(0,22):STR("-",80):
12*
      PRINT @(20,10):CL:'Enter Program Name or "ALL" : ':
      INPUT PROG.NAME
      PRINT @(0,23):CL:
      IF PROG.NAME = "" THEN GOTO 9999
      IF PROG.NAME = "^" THEN GOTO 9999
      IF PROG.NAME = "END" THEN GOTO 9999
      IF PROG.NAME = CHAR(27) THEN GOTO 9999
      IF PROG.NAME = "ALL" THEN
         MSGS = ""
         PERFORM "SSELECT PROG.FILE.XREF BY PFX.SOURCE.FILE" RTNLIST PLIST CAPTURING MSGS
      END ELSE
         MATREAD PFX.REC FROM PROG.FILE.XREF, PROG.NAME ELSE
            PRINT @(0,23):"** PROGRAM NOT PRESENT **":
            INPUT REPLY:
            GOTO 12
         END
      END
14    PRINT @(20,12):CL:'Print on (C)rt or (P)rinter : ':
      INPUT DESTIN,1 _:
      BEGIN CASE
      CASE DESTIN = "END"
         GOTO 9999
      CASE DESTIN = "^"
         GOTO 9999
      CASE DESTIN = CHAR(27)
         GOTO 9999
      CASE DESTIN = "P"
         PRINTER ON
         PAGE.LENGTH = 55
         BOT.MARGIN = 3
      CASE DESTIN = "C"
         PAGE.LENGTH = 21
         BOT.MARGIN = 3
         PRINT @(-1):
      CASE 1
         GOTO 14
      END CASE
*
*---- MAIN PROCESSING
*
      PAGE.NO = 0
      PAGE.SUB.NO = 0
      LINE.NO = 0
      BEGIN CASE
      CASE PROG.NAME = "ALL"
         SOURCE.FILES = ""
         PROG.NAMES = ""
         PREV.PROG.NAME = ""
         SORTED.BY.PROG.NAME = 1
         PROG.CNT = 0
110*
         READNEXT PROG.NAME FROM PLIST ELSE GOTO 190
         IF PROG.NAME < PREV.PROG.NAME THEN
            SORTED.BY.PROG.NAME = 0
         END
         PREV.PROG.NAME = PROG.NAME
         PROG.CNT = PROG.CNT + 1
         PROG.NAMES<PROG.CNT> = PROG.NAME
         GOTO 110
190*
         FOR P = 1 TO PROG.CNT
            PROG.NAME = PROG.NAMES<P>
            MATREAD PFX.REC FROM PROG.FILE.XREF, PROG.NAME ELSE
               MAT PFX.REC = ""
            END
            SOURCE.FILES<P> = PFX.SOURCE.FILE
            GOSUB 1000
         NEXT P
         GOSUB 4000                  ;* PRINT TABLE OF CONTENTS
      CASE 1
         PAGE.NO = 0
         GOSUB 1000
         IF DESTIN = "P" THEN
            PRINTER OFF
            PRINT @(0,23):CL:"** COMPLETED **  Press <RETURN> to continue ":
            INPUT REPLY:
         END
         GOTO 10
      END CASE
      GOTO 9999
*
*---- PRINT PROGRAM NAME OR SUBROUTINE NAME
*
1000*
      PAGE.NO = PAGE.NO + 1
      PAGE.SUB.NO = 0
      GOSUB 10000
      IF PFX.SUBR.FLAG = "" THEN
         PROG.TYPE = "MAIN"
      END ELSE
         PROG.TYPE = "SUBROUTINE"
      END
      PLINE = "SOURCE   : ":PFX.SOURCE.FILE
      GOSUB 9000
      PLINE = "PROGRAM  : ":PROG.NAME "L#30":SPACE(12):"TYPE : ":PROG.TYPE
      GOSUB 9000
      PLINE = "AUTHOR   : ":PFX.WRITTEN.BY
      GOSUB 9000
      PLINE = "DATE     : ":OCONV(PFX.DATE.WRITTEN,"D2/")
      GOSUB 9000
*
*---- PRINT NARRATIVE
*
      IF LINE.NO > (PAGE.LENGTH-BOT.MARGIN) THEN
         GOSUB 11000
         GOSUB 10000
      END
      PLINE = ""
      GOSUB 9000
      PLINE = "NARRATIVE"
      GOSUB 9000
      DESC.FLAG = 1
      READ DESC FROM PROG.DOC, PROG.NAME ELSE DESC.FLAG = 0
      IF DESC.FLAG THEN
         C3 = COUNT(DESC, AM) + (DESC # "")
         FOR C = 1 TO C3
            PLINE = INDENT:DESC<1>
            GOSUB 9000
         NEXT C
      END ELSE
         C3 = COUNT(PFX.DESC, VM) + (PFX.DESC # "")
         FOR C = 1 TO C3
            PLINE = INDENT:PFX.DESC<1,C>
            GOSUB 9000
         NEXT C
      END
*
*---- PRINT SCREEN NAMES
*
      IF LINE.NO > (PAGE.LENGTH-BOT.MARGIN) THEN
         GOSUB 11000
         GOSUB 10000
      END
      PLINE = ""
      GOSUB 9000
      PLINE = "SCREEN NAMES"
      GOSUB 9000
      C3 = COUNT(PFX.SCREEN, VM) + (PFX.SCREEN # "")
      FOR C = 1 TO C3
         PLINE = INDENT:PFX.SCREEN<1,C>
         GOSUB 9000
      NEXT C
*
*---- PRINT FILES ACCESSED
*
      IF LINE.NO > (PAGE.LENGTH-BOT.MARGIN) THEN
         GOSUB 11000
         GOSUB 10000
      END
      PLINE = ""
      GOSUB 9000
      PLINE = "FILES ACCESSED" "L#33":"    OPEN    READ   WRITE   DELETE"
      GOSUB 9000
      C3 = COUNT(PFX.FILE.NAME, VM) + (PFX.FILE.NAME # "")
      FOR C = 1 TO C3
         PLINE = INDENT:PFX.FILE.NAME<1,C> "L#30"
         IF PFX.OPEN.FLAG<1,C> = "" THEN
            P.OPEN = "    "
         END ELSE
            P.OPEN = " O  "
         END
         IF PFX.READ.FLAG<1,C> = "" THEN
            P.READ = "    "            
         END ELSE
            IF PFX.READ.FLAG<1,C> = "X" THEN
               P.READ = " R  "
            END ELSE
               P.READ = " RU "
            END
         END
         IF PFX.WRITE.FLAG<1,C> = "" THEN
            P.WRITE = "    "
         END ELSE
            IF PFX.WRITE.FLAG<1,C> = "X" THEN
               P.WRITE = " W  "
            END ELSE
               P.WRITE = " WU "
            END
         END
         IF PFX.DELETE.FLAG<1,C> = "" THEN
            P.DELETE = "    "
         END ELSE
            P.DELETE = " D  "
         END
         PLINE = PLINE:SPACE(4):P.OPEN:SPACE(4):P.READ:SPACE(4):P.WRITE:SPACE(4):P.DELETE
         GOSUB 9000
      NEXT C
*
*---- PRINT SUBROUTINES CALLED AND CALLING PROGRAMS
*
      IF LINE.NO > (PAGE.LENGTH-BOT.MARGIN) THEN
         GOSUB 11000
         GOSUB 10000
      END
      PLINE = ""
      GOSUB 9000
      PLINE = "SUBROUTINES CALLED" "L#35":"CALLED BY PROGRAMS"
      GOSUB 9000
      C1 = COUNT(PFX.PROG.CALLED, VM) + (PFX.PROG.CALLED # "")
      C2 = COUNT(PFX.CALLED.BY.PROG, VM) + (PFX.CALLED.BY.PROG # "")
      IF C1 > C2 THEN C3 = C1 ELSE C3 = C2
      FOR C = 1 TO C3
         PLINE = INDENT:PFX.PROG.CALLED<1,C> "L#30":SPACE(2)
         PLINE = PLINE:INDENT:PFX.CALLED.BY.PROG<1,C>
         GOSUB 9000
      NEXT C
      GOSUB 11000
      RETURN
*
*---- PRINT TABLE OF CONTENTS
*
4000*
      PREV.SOURCE.FILE = ""
      PAGE.NO = 0
      GOSUB 5000
      FOR P = 1 TO PROG.CNT
         SOURCE.FILE = SOURCE.FILES<P>
         PROG.NAME = PROG.NAMES<P>
         IF LINE.NO > PAGE.LENGTH THEN
            GOSUB 6000
            GOSUB 5000
         END
         IF NOT(SORTED.BY.PROG.NAME) THEN
            IF SOURCE.FILE # PREV.SOURCE.FILE THEN
               PLINE = SPACE(5):SOURCE.FILE
               PRINT PLINE
               LINE.NO = LINE.NO + 1
               PREV.SOURCE.FILE = SOURCE.FILE
            END
            PLINE = SPACE(20):PROG.NAME "L#30":SPACE(5):P "R#3"
         END ELSE
            PLINE = SPACE(5):SOURCE.FILE "L#10":SPACE(5):PROG.NAME "L#30":SPACE(5):P "R#3"
         END
         PRINT PLINE
         LINE.NO = LINE.NO + 1
      NEXT P
      GOSUB 6000
      RETURN
*
*---- PRINT PAGE HEADINGS
*
5000*
      PRINT CHAR(12):
      PRINT
      PRINT
      PRINT
      PAGE.NO = PAGE.NO + 1
      IF PAGE.NO = 1 THEN
         PRINT SPACE(25):"Table of Contents"
      END ELSE
         PRINT SPACE(25):"Table of Contents (cont'd)"
      END
      PRINT
      PRINT
      PRINT "     Source         Program Name                       Page"
      PRINT
      LINE.NO = 8
      RETURN
*
*---- PRINT PAGE NUMBER
*
6000*
      BEGIN CASE
      CASE DESTIN = "P"
         FOR L = LINE.NO TO (PAGE.LENGTH+3)
            PRINT
         NEXT L
         PLINE = PG(PAGE.NO)
         PRINT SPACE(32):PG(PAGE.NO)
      CASE DESTIN = "C"
         PRINT @(0,23):CL:"Press <RETURN> to continue ":
         INPUT REPLY
         IF REPLY = "END" THEN GOTO 9999
         IF REPLY = "^" THEN GOTO 9999
         IF REPLY = CHAR(27) THEN GOTO 9999
         PRINT @(-1):
      END CASE
      RETURN
*
*---- PRINT DETAIL LINE
*
9000*
      IF LINE.NO > PAGE.LENGTH THEN
         GOSUB 11000
         GOSUB 10000
      END
      PRINT PLINE
      LINE.NO = LINE.NO + 1
      RETURN
*
*---- PRINT HEADING
*
10000*
      PRINT CHAR(12):
      HD1 = SPACE((70-LEN(CO.NAME))/2):CO.NAME
      HLINE = OCONV(DATE(),"D2/"):HD1[9,70]
      PRINT HLINE
      HD2 = "PROGRAM DOCUMENTATION"
      HLINE = SPACE((70-LEN(HD2))/2):HD2
      PRINT HLINE
      PRINT
      PRINT
      LINE.NO = 4
      RETURN
*
*---- PRINT PAGE NUMBER
*
11000*
      BEGIN CASE
      CASE DESTIN = "P"
         FOR L = LINE.NO TO (PAGE.LENGTH+3)
            PRINT
         NEXT P
         IF PAGE.SUB.NO = 0 THEN
            PRINT SPACE(30):"- ":PAGE.NO:" -"
         END ELSE
            PRINT SPACE(29):"- ":PAGE.NO:".":PAGE.SUB.NO:" -"
         END
         PAGE.SUB.NO = PAGE.SUB.NO + 1
      CASE DESTIN = "C"
         PRINT @(0,23):CL:"Press <RETURN> to continue ":
         INPUT REPLY
         IF REPLY = "END" THEN GOTO 9999
         IF REPLY = "^" THEN GOTO 9999
         IF REPLY = CHAR(27) THEN GOTO 9999
         PRINT @(-1):
      END CASE
      RETURN
*
*---- END PROGRAM
*
9999*
      PRINTER OFF
      PRINTER CLOSE
      PRINT @(-1):
   END
