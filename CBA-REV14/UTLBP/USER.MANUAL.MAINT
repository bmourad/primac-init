*COPY CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* PROGRAM  - USER.MANUAL.MAINT
* BY       - JIHAD YAMOUT, C.B.A
* DATE     - 01/09/85
* REVISION - A.1.0
* DESCRIPTION
* This program will add and maintaim user manual file entries.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY CPYLIB>CHAR
*COPY CPYLIB>SCREEN.COM
*COPY CPYLIB>MANUAL
*COPY CPYLIB>USER.MANUAL
*COPY CPYLIB>FILE.VARS
*
*---- OPEN ALL FILES
*
      OPEN "","UTL.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN UTL.SCREENS FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","JES.SCREENS" TO JES.SCREENS ELSE
         ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","MANUAL" TO MANUAL ELSE
         ERRMSG = "CANNOT OPEN MANUAL FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","USER.MANUAL" TO USER.MANUAL ELSE
         ERRMSG = "CANNOT OPEN USER.MANUAL FILE"
         GOSUB 90000
         STOP
      END
*
*---- INITIALIZATION
*
      SCREEN INIT;#
      SCREEN DEFINE;USER.MANUAL.MAINT
      SCREEN FORMAT;1
      SCREEN FIELD;1;8
      SLCNT = S$LCNT
*
*---- MAIN PROCESSING
*
100*
     OLD.LINE = ""
     LINES = 0
     S$DATA(1)<1> = DATE()
     SCREEN DISPLAY;1;1
*
*---- GET CODE
*
10100*
      KEY = ""
      NEW.REC = ""
      MAT USER.MANUAL.REC = ""
      RELEASE
      SCREEN FIELD;1;2
      SCREEN INPUT;1;2
      IF S$VALUE = "END" THEN GOTO 91000
*     KEY = S$VALUE[1,5]
      KEY = S$VALUE
      MATREAD MANUAL.REC FROM MANUAL , KEY ELSE
        ERRMSG = "MANUAL (":KEY:") NOT ON FILE " ; GOSUB 90000 ; GOTO 10100
      END
      S$DATA(3)<1> = MA.DESC
      SCREEN DISPLAY;1;3
      SCREEN FIELD;1;4
      SCREEN INPUT;1;4
      KEY = KEY:"*":S$VALUE
      MATREADU USER.MANUAL.REC FROM USER.MANUAL, KEY ELSE
         NEW.REC = "Y"
      END
      IF NEW.REC = "Y" THEN
         FOR N = 1 TO 2
            ON N GOSUB 10300,10400
            IF S$VALUE = "END" THEN
               SCREEN CLEAR;1
               GOTO 100
            END
         NEXT N
         GOSUB 11500
      END ELSE
         S$DATA(5)<1> = UM.SECTION.DESC
         S$DATA(6)<1> = UM.SECTION.NO
         S$DATA(8)<1> = UM.SCREEN.ID
         S$DATA(9)<1> = UM.SCREEN.NAME
         S$DATA(10)<1> = UM.SCREEN.REV
         LINES = COUNT(UM.SCREEN.ID, VM) + (UM.SCREEN.ID # "")
         SCREEN DISPLAY;1;ALL
      END
      REF.NO = 1
      GOSUB 30000
10110*
      SCREEN FIELD;1;11
      SCREEN INPUT;1;11
      OPTION = S$VALUE
      BEGIN CASE
         CASE NUM(OPTION)
            ON OPTION GOSUB 10300,10400
         CASE OPTION = "A"
            LOOP
             REF.NO = LINES + 1
             OLD.REF = LINES
             GOSUB 20000
            WHILE LINES > OLD.REF DO REPEAT
            REF.NO = LINES
         CASE OPTION = "I"
            GOSUB 80000
            IF LNO THEN
               REF.NO = LNO
               GOSUB 60000
               GOSUB 20000
            END
         CASE OPTION = "C"
            GOSUB 80000
            IF LNO THEN
             REF.NO = LNO
             GOSUB 20000
            END
         CASE OPTION = "D"
            GOSUB 80000
            IF LNO THEN
             REF.NO = LNO
             GOSUB 70000
            END
         CASE OPTION = "S"
            REF.NO = REF.NO + SLCNT
            IF REF.NO > LINES THEN REF.NO = 1
            GOSUB 30000
         CASE OPTION = "E" OR OPTION = "END"
            SCREEN CLEAR;1
            GOTO 100
         CASE OPTION = "F"
            UM.SECTION.REV = ""
            C2 = COUNT(UM.SCREEN.ID,VM) + (UM.SCREEN.ID # "")
            FOR C = 1 TO C2
               IF UM.SCREEN.REV<1,C> > UM.SECTION.REV THEN
                  UM.SECTION.REV = UM.SCREEN.REV<1,C>
               END
            NEXT C
            MATWRITE USER.MANUAL.REC ON USER.MANUAL, KEY
            SCREEN CLEAR;1
            GOTO 100
         CASE 1
            PRINT @(0,23):"Selected field invalid for this record":CL:
            INPUT XX:
            PRINT @(0,23):CL:
      END CASE
      GOTO 10110
*
*--- ENTER SECTION DESC
*
10300*
      SCREEN FIELD;1;5
      SCREEN INPUT;1;5;RETURN
      UM.SECTION.DESC = S$VALUE
      RETURN
*
*--- ENTER SECTION NO
*
10400*
       SCREEN FIELD;1;6
       SCREEN INPUT;1;6;RETURN
       UM.SECTION.NO = S$VALUE
       RETURN
*
*---- ENTER MULTI LINES
*
11500*
       LOOP
        OPTION = "A"
        REF.NO = LINES + 1
        OLD.REF = LINES
        GOSUB 20000
       WHILE LINES > OLD.REF DO REPEAT
       REF.NO = LINES
       RETURN
*
*---- ENTER VALDAT
*
20000*
      GOSUB 30000
      S$VAL = REF.NO
      SCREEN DISPLAY;1;7
      S$VAL = REF.NO
      SCREEN FIELD;1;8
      S$VAL = REF.NO
      SCREEN INPUT;1;8
      IF S$VALUE = "END" AND (OPTION = "A" OR OPTION = "I") THEN
         GOSUB 70000
         GOTO 29999
      END
*     LOCATE S$VALUE IN UM.SCREEN.ID<1>,1 SETTING FNDZ ELSE FNDZ = 0
*     IF FNDZ AND FNDZ # REF.NO THEN
*        ERRMSG = "This screen already entered" ; GOSUB 90000 ; GOTO 20000
*     END
*     IF S$VALUE # "C" THEN
*        READ SS FROM JES.SCREENS, S$VALUE:"*FRM" ELSE
*           ERRMSG = "Invalid screen name" ; GOSUB 90000 ; GOTO 20000
*        END
*     END
      UM.SCREEN.ID<1,REF.NO> = S$VALUE
*
*---- ENTER DESCRIPTION
*
      SCREEN FIELD;1;9
      S$VAL = REF.NO
      SCREEN INPUT;1;9
      IF S$VALUE = "END" AND OPTION = "A" THEN GOTO 20000
      IF S$VALUE # "END" THEN
         UM.SCREEN.NAME<1,REF.NO> = S$VALUE
      END
*
*---- ENTER REVISION DATE
*
      S$VAL = REF.NO
      SCREEN FIELD;1;10
      SCREEN INPUT;1;10
      IF S$VALUE = "END" AND OPTION = "A" THEN GOTO 20000
      IF S$VALUE # "END" THEN
         UM.SCREEN.REV<1,REF.NO> = S$VALUE
      END
      LINES = COUNT(UM.SCREEN.ID, VM) + (UM.SCREEN.ID # "")
29999 RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
30000*
      SCREEN COUNT;1;8
      START.REF = 1 + INT((REF.NO-1)/SLCNT)*SLCNT
      IF START.REF = OLD.LINE THEN RETURN
      OLD.LINE = START.REF
      S$VAL = START.REF
      S$CNT = LINES
      SCREEN MULTI;1;C;7;8;9;10
      RETURN
*
*--- INSERT SUB
*
60000*
      INS "" BEFORE UM.SCREEN.ID<1,REF.NO,0>
      INS "" BEFORE UM.SCREEN.NAME<1,REF.NO,0>
      INS "" BEFORE UM.SCREEN.REV<1,REF.NO,0>
      FOR X = 8 TO 10
        INS "" BEFORE S$DATA(X)<1,REF.NO,0>
      NEXT X
      LINES = COUNT(UM.SCREEN.ID,VM) + (UM.SCREEN.ID # "")
      IF REF.NO > 1 AND REF.NO > LINES THEN REF.NO = REF.NO - 1
      IF REF.NO < 1 THEN REF.NO = 1
      OLD.LINE = 0
      GOSUB 30000
      RETURN
*
*--- DELETE SUB
*
70000*
      UM.SCREEN.ID = DELETE(UM.SCREEN.ID,1,REF.NO,0)
      UM.SCREEN.NAME = DELETE(UM.SCREEN.NAME,1,REF.NO,0)
      UM.SCREEN.REV = DELETE(UM.SCREEN.REV,1,REF.NO,0)
      FOR Y = 7 TO 10
       S$DATA(Y) = DELETE(S$DATA(Y),1,REF.NO,0)
      NEXT Y
      LINES = COUNT(UM.SCREEN.ID,VM) + (UM.SCREEN.ID # "")
      IF REF.NO > 1 AND REF.NO > LINES THEN REF.NO = REF.NO - 1
      IF REF.NO < 1 THEN REF.NO = 1
      OLD.LINE = 0
      GOSUB 30000
      RETURN
*
*---- ENTER LINE #
*
80000*
      SCREEN FIELD;1;12
      SCREEN INPUT;1;12
      LNO = S$VALUE
      MIV = OLD.LINE
      IF (OLD.LINE + SLCNT) - 1 < LINES THEN
         MAV = (OLD.LINE + SLCNT) - 1
      END ELSE
         MAV = LINES
      END
      BEGIN CASE
       CASE LNO = "END" OR LNO = ""
         LNO = 0
       CASE NOT(NUM(LNO))
         ERRMSG = "** NUMERIC INPUT REQUIRED **" ; GOSUB 90000 ; GOTO 80000
       CASE LNO < MIV
         ERRMSG = "** OUT OF RANGE **" ; GOSUB 90000
         GOTO 80000
       CASE LNO > MAV
         ERRMSG = "** OUT OF RANGE **" ; GOSUB 90000
         GOTO 80000
      END CASE
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF JOB
*
91000*
      PRINT @(-1):
   END
