***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM - SYSTEM.LOAD
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 07/12/84
*
* DESCRIPTION
*
* This program will read the specified transfer file and distribute the
* items contained to the appropriate files within the accounts specified.
*
***************************************************************************
*
CS = CHAR(12)
CL = CHAR(27):CHAR(75)
*
*---- DEFINE VARIABLES
*
      DIM QREC2(10)
*
*---- OPEN ALL FILES
*
      OPEN "","MD" TO MD ELSE
         ERRMSG = "CANNOT MASTER DICTIONARY"
         GOSUB 90000
         GOTO 99900
      END
      OPEN "","TRANSFER-BACKUP" TO TRANSFER.BACKUP ELSE
         ERRMSG = "CANNOT OPEN TRANSFER-BACKUP FILE"
         GOSUB 90000
         GOTO 99900
      END
*
*---- INITIALIZATION
*
      PROMPT ""
      MAT QREC2 = ""
      QREC2(1) = "Q"
      QREC2(9) = "L"
      QREC2(10)= 10
      PAGE.NO = 0
      LINE.NO = 99
      SEL.CNT = 0
      CREC = ""
      CREC<1> = "PQN"
      CPTR = 1
      ERR.CNT = 0
      DASHES = STR("-",130)
*
*---- DISPLAY SCREEN
*
10*
      SCRN = CS
      SCRN = SCRN:@(0,0):"C.B.A."
      SCRN = SCRN:@(30,0):"SYSTEM LOAD"
      SCRN = SCRN:@(72,0):OCONV(DATE(),"D2/")
      SCRN = SCRN:STR("-",80)
      SCRN = SCRN:@(0,22):STR("-",80)
      PRINT SCRN:
*
*---- GET TRANSFER FILE NAME
*
20*
      PRINT @(20,10):CL:"Transfer File Name : ":
      INPUT TRANSFER.FILE.NAME:
      IF TRANSFER.FILE.NAME = "" OR TRANSFER.FILE.NAME = "" THEN
         GOTO 99900
      END
      OPEN "",TRANSFER.FILE.NAME TO TRANSFER.FILE ELSE
         ERRMSG = "INVALID FILE NAME"
         GOSUB 90000
         GOTO 20
      END
30*
      PRINT @(20,12):CL:"Clear Backup File? : ":
      INPUT REPLY
      BEGIN CASE
         CASE REPLY = "Y"
            CLEARFILE TRANSFER.BACKUP
         CASE REPLY = "N"
            NULL
         CASE 1
            GOTO 30
      END CASE
*
*---- MAIN PROCESSING
*
100*
      READNEXT ID ELSE
         IF SEL.CNT = 0 THEN
            ERRMSG = "SSELECT ":TRANSFER.FILE.NAME:" prior to execution."
            GOSUB 90000
            GOTO 99900
         END
         IF ERR.CNT > 0 THEN
            PRINTER CLOSE
            ERRMSG = "PLEASE CHECK EXCEPTION REPORT"
            GOSUB 90000
         END
         GOTO 99900
      END
      SEL.CNT = SEL.CNT + 1
      TO.ACCT = FIELD(ID,">",1)
      TO.DICT = FIELD(ID,">",2)
      TO.FILE = FIELD(ID,">",3)
      TO.ITEM = FIELD(ID,">",4)
      OW.FLAG = FIELD(ID,">",5)
      IF TO.ITEM[1,1] = "$" THEN
         CREC<CPTR+1> = "H CATALOG ":TO.FILE:" ":TO.ITEM[2,99]
         CREC<CPTR+2> = "P"
         CPTR = CPTR + 2
      END
      BU.FLAG = 0
      READV REC FROM TRANSFER.BACKUP, ID,1 ELSE
         BU.FLAG = 1
      END
110*
      QREC2(2) = TO.ACCT
      QREC2(3) = "MD"
      MATWRITE QREC2 ON MD, "QFILE2"
      OPEN "","QFILE2" TO QFILE2 ELSE
         ERRMSG = "INVALID ACCOUNT NAME"
         GOSUB 5000
         GOTO 100
      END
      READ REC FROM QFILE2, TO.FILE ELSE
         ERRMSG = "INVALID FILE NAME"
         GOSUB 5000
         GOTO 100
      END
      IF REC<1> = "Q" THEN
         TO.ACCT = REC<2>
         TO.FILE = REC<3>
         GOTO 110
      END
      QREC2(3) = TO.FILE
      MATWRITE QREC2 ON MD, "QFILE2"
      OPEN TO.DICT, "QFILE2" TO QFILE2 ELSE
         ERRMSG = "INVALID FILE NAME"
         GOSUB 5000
         GOTO 100
      END
      READ REC FROM QFILE2, TO.ITEM ELSE
         IF TO.ITEM[1,1] = "$" THEN
            ERRMSG = "CHECK PROG PTR IN REQ ACCT"
            GOSUB 5000
         END
         GOTO 120
      END
      IF OW.FLAG = "N" THEN
         ERRMSG = "NOT TRANSFERRED"
         GOSUB 5000
         GOTO 100
      END
      IF BU.FLAG THEN
         WRITE REC ON TRANSFER.BACKUP, ID
      END
120*
      READ REC FROM TRANSFER.FILE, ID ELSE GOTO 130
      WRITE REC ON QFILE2, TO.ITEM
130*
      ERRMSG = ""
      GOSUB 5000
      GOTO 100
*
*---- EXCEPTION CONDITION
*
5000*
      PRINTER ON
      IF LINE.NO > 55 THEN GOSUB 10000
      PLINE = TO.ACCT "L#30"
      PLINE = PLINE:"  ":TO.DICT "L#4"
      PLINE = PLINE:"  ":TO.FILE "L#30"
      PLINE = PLINE:"  ":TO.ITEM "L#30"
      PLINE = PLINE:"  ":ERRMSG "L#30"
      PRINT PLINE
      LINE.NO = LINE.NO + 1
      IF ERRMSG # "" THEN
         ERR.CNT = ERR.CNT + 1
      END
      PRINTER OFF
      RETURN
*
*---- PRINT EXCEPTION REPORT HEADINGS
*
10000*
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      HLINE1 = ""
      HLINE1 = HLINE1:OCONV(DATE(),"D2/")"L#8":SPACE(32)
      HLINE1 = HLINE1:"COMPUTER BUSINESS ASSOCIATES""L#40":SPACE(40)
      HLINE1 = HLINE1:"PAGE : ":PAGE.NO
      PRINT HLINE1
      HLINE2 = ""
      HLINE2 = HLINE2:SPACE(26)
      HLINE2 = HLINE2:"S Y S T E M   L O A D   E X C E P T I O N   R E P O R T"
      PRINT HLINE2
      PRINT
      PRINT
      SLINE1 = "ACCOUNT NAME""L#30":SPACE(2)
      SLINE1 = SLINE1:"DICT""L#4":SPACE(2)
      SLINE1 = SLINE1:"FILE NAME""L#30":SPACE(2)
      SLINE1 = SLINE1:"ITEM NAME""L#30":SPACE(2)
      SLINE1 = SLINE1:"ERROR MESSAGE""L#30"
      PRINT SLINE1
      SLINE2 = ""
      SLINE2 = SLINE2:DASHES"L#30":SPACE(2)
      SLINE2 = SLINE2:DASHES"L#4":SPACE(2)
      SLINE2 = SLINE2:DASHES"L#30":SPACE(2)
      SLINE2 = SLINE2:DASHES"L#30":SPACE(2)
      SLINE2 = SLINE2:DASHES"L#30"
      PRINT SLINE2
      LINE.NO = 6
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF PROGRAM
*
99900*
      PRINT @(-1)
      IF CPTR > 1 THEN
         WRITE CREC ON MD, "TRANSFER.CATALOG"
99950    PRINT "Catalog transferred programs (Y/N) ":
         INPUT REPLY
         IF REPLY = "Y" THEN
            CHAIN "TRANSFER.CATALOG"
         END
         IF REPLY # "N" THEN GOTO 99950
         PRINT "Execute TRANSFER.CATALOG procedure to catalog transferred programs"
      END
      STOP
   END
