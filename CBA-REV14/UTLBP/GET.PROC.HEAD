***************************************************************************
* REVISION    - [08.0]
* PROGRAM      - GET.PROC.HEAD
* AUTHOR       - JIHAD YAMOUT, C.B.A.
* DATE         - 02/21/85
* REVISION     - A.1.0
* DESCRIPTION
* This program returns the report heading and report ID from the REPORT
* definition file.
***************************************************************************
*
*--- FILE COPY STATEMENTS
*
*COPY PMC.CPYLIB>COMPANY
*COPY CPYLIB>REPORT
*COPY CPYLIB>CHAR
*
*--- OPEN FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000 ; GOTO 99999
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000 ; GOTO 99999
      END
      OPEN "","REPORT" TO REPORT ELSE
         ERRMSG = "CANNOT OPEN REPORT FILE"
         GOSUB 90000; GOTO 99999
      END
*
*--- GET ACTION
*
      HEAD = ""
      PROG.HEAD1 = ""
      PROCREAD PARAM ELSE
         ERRMSG = "This program must run from a (PROC)."
         GOSUB 90000 ; GOTO 99999
      END
      REPORT.NAME = PARAM<1>
      MATREAD REPORT.REC FROM REPORT, REPORT.NAME ELSE
         MAT REPORT.REC = ""
         REPORT.HEAD = REPORT.NAME
         REPORT.ID = "XXXX"
      END
      INPUT.HEAD = REPORT.HEAD
      PTR = INDEX(INPUT.HEAD,"(",1)
      IF PTR = 0 THEN
         INPUT.HEAD1 = TRIM(INPUT.HEAD)
         INPUT.HEAD2 = ""
      END ELSE
         INPUT.HEAD1 = TRIM(INPUT.HEAD[1,PTR-1])
         INPUT.HEAD2 = TRIM(INPUT.HEAD[PTR,99])
      END
      IHL = LEN(INPUT.HEAD1)
      FOR P = 1 TO IHL
         PROG.HEAD1 = PROG.HEAD1:" ":INPUT.HEAD1[P,1]
      NEXT P
      PROG.HEAD1 = PROG.HEAD1[2,80]
      READV CONO FROM CONTROL,"MASTER",1 ELSE CONO = "001"
      MATREAD COMP.REC FROM COMPANY ,CONO ELSE
         MAT COMP.REC = ""
      END
      PDATE = DATE()
      HLINE1A = "RUN DATE : ":OCONV(PDATE,"D2/")
      HLINE1A = HLINE1A "L#20"
      HLINE1B = SPACE(INT((92-LEN(CO.NAME))/2)):CO.NAME
      HLINE1B = HLINE1B "L#92"
      HLINE1C = SPACE(8):"PAGE "
      HLINE1 = HLINE1A:HLINE1B:HLINE1C
      HLINE2A = "REPORT # : ":REPORT.ID
      HLINE2A = HLINE2A "L#20"
      HLINE2B = SPACE(INT((92-LEN(PROG.HEAD1))/2)):PROG.HEAD1
      HLINE2B = HLINE2B:"   ":INPUT.HEAD2
      HLINE2 = HLINE2A:HLINE2B
      HLINE2 = HLINE2 "L#132"
      LINE = ""
      LINE<1> = HLINE1
      LINE<2> = HLINE2
*
*--- PRINT BANNER
*
     PRINTER ON
     CALL PRINT.PROG.BANNER(CONO,CO.NAME,REPORT.NAME,PDATE)
     PRINTER OFF
     PROCWRITE LINE
     GOTO 99999
*
*--- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY:
      PRINT @(0,23):CL:
      RETURN
*
*--- END OF PROGRAM
*
99999*
      END
