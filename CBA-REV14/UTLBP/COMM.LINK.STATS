      SUBROUTINE COMM.LINK.SEND (RACCT, RDICT, RFILE, RITEM, REC, STATUS, ERRCNT)
***************************************************************************
*
* PROGRAM  - COMM.LINK.SEND
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/15/87
*
* DESCRIPTION
*
* This subroutine is used to transmit defined data to remote computer.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      PROMPT ""
*      DUMMY = OCONV(0,"U80E0")         ;* ECHO OFF
      ECHO OFF
      BEEP = STR(BEL,8)
      ERRMSG = ""
      ERRCNT = 0
      E2CNT = 0
      STATUS = ""
      SPEC.CHR = "010":VM:"013":VM:"017":VM:"019"
      PREV.DIR = "ZXZXZX"
      LINE.ACTIVE = 0
*
*---- MAIN PROCESSING
*
100*
      DIR = "FD"
      MSG = "\":DIR:"\":RACCT:",":RDICT:",":RFILE
      GOSUB 5000
      GOSUB 6000
      IF ERRMSG # "" THEN
         STATUS = ERRMSG
         GOTO 99999
      END
      LC = COUNT(REC,AM) + (REC # "")
      IF LC < 1 THEN LC = 1
*
      FOR LN = 1 TO LC
         RLINE = REC<LN>
         GOSUB 3000
         GOSUB 4000
      NEXT LN
      GOSUB 6000
*
      DIR = "ID"
      MSG = "\":DIR:"\":RITEM
      GOSUB 5000
      GOSUB 6000
*
      GOTO 99999
*
*---- COMPRESS DATA FOR TRANSMISSION
*
3000*
      RLEN = LEN(RLINE)
      TEXT = ""
      MODE = ""
      FOR CP = 1 TO RLEN
         CHR = RLINE[CP,1]
         CHD = SEQ(CHR)
         BEGIN CASE
         CASE CHD < 32
            C1 = "a"
            C2 = CHAR(CHD+32)
            MODE = "C"
         CASE CHD < 96
            C1 = ""
            C2 = CHR
         CASE CHD < 128
            C1 = "b"
            C2 = CHAR(CHD-64)
            MODE = "C"
         CASE CHD < 192
            C1 = "c"
            C2 = CHAR(CHD-96)
            MODE = "C"
         CASE CHD < 256
            C1 = "d"
            C2 = CHAR(CHD-160)
            MODE = "C"
         END CASE
         IF C1 # "" AND MOD(LEN(TEXT),120) = 119 THEN
            TEXT = TEXT : "e" : C1 : C2
         END ELSE
            TEXT = TEXT : C1 : C2
         END
      NEXT CP
      RETURN
*
*---- TRANSMIT ITEM LINE
*
4000*
      SG = 0
      TLEN = LEN(TEXT)
      IF TLEN < 1 THEN TLEN = 1
      FOR TP = 1 TO TLEN STEP 120
         IF LINE.ACTIVE THEN GOSUB 6000
         SG = SG + 1
         BEGIN CASE
         CASE MODE = ""
            DIR = LN:",":SG
         CASE 1
            DIR = LN:",":SG:",":MODE
         END CASE
         MSG = "\":DIR:"\":TEXT[TP,120]
         GOSUB 5000
      NEXT TP
      RETURN
*
*---- TRANSMIT DATA SEGMENT WITH CHECKSUM
*
5000*
      CSUM = 0
      ML = LEN(MSG)
      FOR MP = 1 TO ML
         CSUM = CSUM + SEQ(MSG[MP,1])
      NEXT MP
      PRINT CSUM:MSG
      LINE.ACTIVE = 1
      RETURN
*
*---- WAIT FOR ACKNOWLEDGEMENT
*
6000*
      STIME = TIME()
      INPUT REPLY:
      ETIME = TIME()
      ELAP.TIME = ETIME - STIME
      IF ELAP.TIME < 0 THEN ELAP.TIME = ELAP.TIME + 86400
      IF ELAP.TIME > 5 THEN
*         TIMEOUT = ICONV(10,"U407A")
         RQM 10
      END ELSE
         IF REPLY = "" THEN GOTO 6000
      END
      IF REPLY = PREV.DIR THEN GOTO 6000
      IF REPLY # DIR THEN
         BEGIN CASE
         CASE REPLY = "E1"
            ERRCNT = ERRCNT + 1
         CASE REPLY = "E2"
            E2CNT = E2CNT + 1
            IF E2CNT = 3 THEN
               ERRMSG = "E2"
               GOTO 6090
            END
         END CASE
         PRINT CSUM:MSG
         GOTO 6000
      END
      PREV.DIR = DIR
6090  LINE.ACTIVE = 0
      RETURN
*
*---- END OF PROGRAM
*
99999*
*      DUMMY = OCONV(0,"U70E0")         ;* ECHO ON
      ECHO ON
      RETURN
   END
