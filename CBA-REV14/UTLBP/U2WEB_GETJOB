** PGM TO CHECK TAX RATES FROM A WEB SERVICE
      OTHERWISE = @TRUE
      PROMPT ""
      RESP.HEADERS = ""
      RESP.DATA = ""
      WEB.HANDLE = ""
*
     PRINT "ENTER YOUR CONO: ":
     INPUT cono
     PRINT "ENTER YOUR Job #: ":
     INPUT jobid
*
      URL = "http://vsibpm.vercom.com/PrimacSystems.Webservices.Rev14.Dev/JCS/JobService.asmx/GetJob?"
      REQUEST.STATUS = createRequest(URL,"GET",WEB.HANDLE)
      IF REQUEST.STATUS THEN
         PRINT "createRequest FAILED, STATUS CODE = ":REQUEST.STATUS
         STOP
      END
      VARS = "cono"
      VALS = cono
      VARS<1,2> = "jobid"
      VALS<1,2> = jobid
      FOR I = 1 TO 2
        PARAM.STATUS = addRequestParameter(WEB.HANDLE,VARS<1,I>,VALS<1,I>,"")
         IF PARAM.STATUS THEN
            PRINT "addRequestParameter FAILED, STATUS CODE = ":PARAM.STATUS
            STOP
         END
      NEXT I
      SUBMIT.STATUS = submitRequest(WEB.HANDLE,"","",RESP.HEADERS,RESP.DATA,HTTP.STATUS)
      IF SUBMIT.STATUS THEN
         PRINT "submitRequest FAILED, STATUS CODE = ":SUBMIT.STATUS
         STOP
      END
      VAR.LIST = 'BookedDate':@AM:'Bookings':@AM:'Colors':@AM:'CompanyNo':@AM:'CompletedDate':@AM:'CostedDate':@AM:'CostTotalLabor':@AM:'CostTotalMaterial':@AM:'CostTotalMisc':@AM
      VAR.LIST :='CostTotalOutside':@AM:'CreditApprovedTracking':@AM:'CsrCode':@AM:'CsrName':@AM:'CurrentStatus':@AM:'CurrentStatusDate':@AM:'CurrentStatusCode':@AM:'CustomerId':@AM
      VAR.LIST :='CustomerName':@AM:'CustomerPoNo':@AM:'Department':@AM:'Division':@AM:'DivisionDescription':@AM:'DueDate':@AM:'EnteredDate':@AM:'EstimateCost':@AM
      VAR.LIST :='EstimatedDate':@AM:'EstimatedSale':@AM:'EstimateNo':@AM:'EstimateType':@AM:'FinalDeliveryDate':@AM:'FinalShipDate':@AM:'FinishedGoodsFlag':@AM:'JobCategory':@AM
      VAR.LIST :='JobCategoryCode':@AM:'JobCommentsLine1':@AM:'JobCommentsLine2':@AM:'JobDescription':@AM:'JobNo':@AM:'JobType':@AM:'JobTypeCode':@AM:'LastInvoiceDate':@AM
      VAR.LIST :='MarkupPercent':@AM:'MasterCheck':@AM:'MasterJobNo':@AM:'OrderNo':@AM:'OrderQuantity':@AM:'PassedtoBillDate':@AM:'PricePerAddlThousand':@AM:'PriorJobNo':@AM
      VAR.LIST :='ProductionStartDate':@AM:'ProofDate':@AM:'QuotedDate':@AM:'ReservedPo':@AM:'SalesCode':@AM:'SalesRepCode':@AM:'SalesRepName':@AM:'ShipVia':@AM
      VAR.LIST :='ShipViaCode':@AM:'SisActive':@AM:'SisComments':@AM:'Spoilage':@AM:'CreditCode':@AM:'TotalBalance':@AM:'TotalAvail':@AM:'OpenReceivables':@AM
      VAR.LIST :='OpenJobs':@AM:'OpenOrders':@AM:'TotalCost':@AM:'TotalDirectCost':@AM:'TotalInvoiced':@AM:'TotalSale':@AM:'QuotedAmount2':@AM
*
      FOR XX = 1 TO DCOUNT(VAR.LIST,@AM)
         VAR.NAME = VAR.LIST<XX>
         GOSUB PARSEXML
         PRINT VAR.NAME:" = ":PARSE.STRNG
      NEXT XX
      PRINT RESP.HEADERS
      STOP
      *--> LOCATE XML VALUES
PARSEXML:
      XML.STRNG = RESP.DATA
      ST.VAR.NAME = "<":VAR.NAME
      END.VAR.NAME = "</":VAR.NAME:">"
      TAG.POS = INDEX(XML.STRNG,ST.VAR.NAME,1)
      NEW.STR = XML.STRNG[TAG.POS,LEN(XML.STRNG)]
      ST.POS = INDEX(NEW.STR,'>',1)+ TAG.POS
** THIS IS WILL GET THE LENGTH OF THE TAG
      END.POS = INDEX(XML.STRNG,END.VAR.NAME,1)
      IF ST.POS > 0 AND END.POS > ST.POS THEN
         STRT = ST.POS
         LNG = END.POS - STRT            ; * TOTAL LENGTH OF STRING
         PARSE.STRNG = XML.STRNG[STRT,LNG]
         PARSE.STRNG = TRIM(PARSE.STRNG)
      END ELSE
*         ERR.MSG = 'XML document is missing ':VAR.NAME:' Data'
         PARSE.STRNG = ''
*         PRINT ERR.MSG
      END
      RETURN
