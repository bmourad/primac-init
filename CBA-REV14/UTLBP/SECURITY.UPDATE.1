SUBROUTINE SECURITY.UPDATE.1
*********************************************************************
* REVISION    - [12.0]
* DATE        - 06/16/2006
* CONVERSION  -
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - SECURITY.UPDATE.1
* BY          - DEEPAK CHAGGAR
* DESCRIPTION - THIS PROGRAM SELECTS AND READS ALL RECORDS IN SECURITY
*               FILE AND CHECKS FOR SEC.MENU.LEVEL < 2 AND 
*               SEC.MENU.NAME # "", AND CHANGES RECORDS FOR WHICH
*               SEC.MENU.PROC ENTRY DOESN'T HAVE A CORRESPONDING VALUE IN
*               SEC.MENU.NAME OR SEC.MENU.DESC
*ENDDOC
*********************************************************************
*
***** FILE INSERTS
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>SECURITY
*
*---- SET UP SYSCOM
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
*---- Open required Files
*
OPEN "","SECURITY" TO SECURITY ELSE ERRMSG = "Cannot locate the SECURITY file"; GOTO 93000
OPEN "","PRIMAC.MENUS" TO PRIMAC.MENUS ELSE ERRMSG = "Cannot locate the PRIMAC.MENUS file"; GOTO 93000
*
*---- DEFINE VARIABLES
*
DIM MENU(33)
TEMP.MENU.NAME = ""
TEMP.MENU.DESC = ""
*
*---- Program Body
*
CMD = 'SSELECT SECURITY WITH SEC.MENU.FLAG = "A" AND SEC.MENU.LEVEL < 2 AND SEC.MENU.NAME # ""'
UDTEXECUTE CMD CAPTURING XYZ
EOF = 1
LOOP
  READNEXT ID ELSE EOF = 0
WHILE EOF DO
  CHANGE.FLAG = 1
  LOOP
    MATREAD SEC.REC FROM SECURITY, ID THEN
      NCNT = DCOUNT(SEC.MENU.PROC,@VM)
      FOR I = 1 TO NCNT
        IF SEC.MENU.NAME<1,I>[1,2] = "M." THEN
          MATREAD MENU FROM PRIMAC.MENUS, SEC.MENU.NAME<1,I> THEN
            TEMP.MENU.NAME = MENU(1)<1,1>
            TEMP.MENU.DESC = MENU(1)<1,3>
          END ELSE
            ERRMSG = SEC.MENU.NAME<1,I>:" is not a valid menu."
            GOSUB 91000
          END
        END
        IF TEMP.MENU.NAME # "" THEN
          LOCATE TEMP.MENU.NAME IN SEC.MENU.NAME<1>, 1 SETTING FND THEN
            CHANGE.FLAG = 0
          END ELSE
            SEC.MENU.NAME<1,-1> = TEMP.MENU.NAME
            SEC.MENU.PROC<1,-1> = TEMP.MENU.NAME
            SEC.MENU.DESC<1,-1> = TEMP.MENU.DESC
            CHANGE.FLAG = 1
          END
          TEMP.MENU.NAME = ""
          TEMP.MENU.DESC = ""
        END
      NEXT
      MATWRITE SEC.REC ON SECURITY, ID
    END
  WHILE CHANGE.FLAG DO
  REPEAT
REPEAT
GOTO 99999
*
*---- Error routines
*
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
*
99999*
END
