***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM - SYSTEM.XFER.LIST
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 07/13/84
*
* DESCRIPTION
*
* This program will list all items on the customer transfer file. The
* listing is sorted by account, file and item names.
*
***************************************************************************
*
EQU AM TO CHAR(254)
EQU VM TO CHAR(253)
CS = CHAR(12)
CL = CHAR(27):CHAR(75)
*
*---- DEFINE VARIABLES
*
*
*---- OPEN ALL FILES
*
      OPEN "","MD" TO MD ELSE
         ERRMSG = "CANNOT OPEN MASTER DICTIONARY"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      PROMPT ""
      DASHES = STR("-",80)
*
*---- DISPLAY SCREEN
*
10*
      SCRN = CS
      SCRN = SCRN:@(00,00):"C.B.A."
      SCRN = SCRN:@(30,00):"SYSTEM TRANSFER LISTING"
      SCRN = SCRN:@(72,00):OCONV(DATE(),"D2/")
      SCRN = SCRN:@(00,01):DASHES
      SCRN = SCRN:@(00,22):DASHES
      PRINT SCRN:
*
*---- GET FILE NAME
*
20*
      PRINT @(20,11):"Transfer File Name : ":
      INPUT TRANSFER.FILE.NAME:
      IF TRANSFER.FILE.NAME = "" OR TRANSFER.FILE.NAME = "END" THEN
         GOTO 99999
      END
      OPEN "DICT",TRANSFER.FILE.NAME TO TRANSFER.FILE ELSE
         ERRMSG = "INVALID FILE NAME"
         GOSUB 90000
         GOTO 20
      END
*
*---- CREATE DICTIONARY ITEMS
*
      HEAD = SPACE(9):"ACCOUNT NAME":SPACE(9):VM:STR("-",30)
      DREC = "S":AM:"0":AM:HEAD:AM:AM:AM:AM:AM:"G0>1":AM:"L":AM:"30"
      WRITE DREC ON TRANSFER.FILE, "ACCT.NAME"
*
      HEAD = "DICT":VM:"----"
      DREC = "S":AM:"0":AM:HEAD:AM:AM:AM:AM:AM:"G1>1":AM:"L":AM:"4"
      WRITE DREC ON TRANSFER.FILE, "DICT.FLAG"
*
      HEAD = SPACE(10):"FILE NAME":SPACE(11):VM:STR("-",30)
      DREC = "S":AM:"0":AM:HEAD:AM:AM:AM:AM:AM:"G2>1":AM:"L":AM:"30"
      WRITE DREC ON TRANSFER.FILE, "FILE.NAME"
*
      DREC = "S":AM:"0":AM:AM:AM:AM:AM:AM:"G3>1":AM:"T":AM:"30"
      WRITE DREC ON TRANSFER.FILE, "ITEM.NAME"
*
      HEAD = "OW":VM:"--"
      DREC = "S":AM:"0":AM:HEAD:AM:AM:AM:AM:AM:"G4>1":AM:"L":AM:"2"
      WRITE DREC ON TRANSFER.FILE, "OW.FLAG"
*
      DREC = "S":AM:"1":AM:AM:AM:AM:AM:AM:AM:"T":AM:"30"
      WRITE DREC ON TRANSFER.FILE, "COMMENT"
*
      HEAD = SPACE(10):"ITEM NAME":SPACE(11):VM:STR("-",30)
      CORR = 'A;IF N(ACCT.NAME)["1","1"] = "*" THEN N(COMMENT) ELSE N(ITEM.NAME)'
      DREC = "S":AM:"0":AM:HEAD:AM:AM:AM:AM:AM:CORR:AM:"T":AM:"30"
      WRITE DREC ON TRANSFER.FILE, "ITEM.COMMENT"
*
*---- CREATE PROCEDURE
*
      PREC = ""
      PREC<1> = "PQN"
      PREC<2> = "H SORT ":TRANSFER.FILE.NAME
      PREC<3> = "H BY ACCT.NAME"
      PREC<4> = "H BY DICT.FLAG"
      PREC<5> = "H BY FILE.NAME"
      PREC<6> = "H BY ITEM.NAME"
      PREC<7> = 'H WITH ITEM.NAME # "$]"'
      PREC<8> = "H ACCT.NAME"
      PREC<9> = "H DICT.FLAG"
      PREC<10> = "H FILE.NAME"
      PREC<11> = "H ITEM.COMMENT"
      PREC<12> = "H OW.FLAG"
      PREC<13> = "H HEADING ":'"':TRANSFER.FILE.NAME:SPACE(20)
      PREC<13> = PREC<13>:"SYSTEM TRANSFER LISTING":SPACE(20):"'T'"
      PREC<13> = PREC<13>:SPACE(12):"PAGE : 'PLL'":'"'
      PREC<14> = "H ID-SUPP"
      PREC<15> = "H LPTR"
      PREC<16> = "P"
      PREC<17> = 'MV %1 "RETURN"'
      PREC<18> = 'H EASY.MENU'
      PREC<19> = 'P'
      WRITE PREC ON MD, "SYSTEM.XFER.LST.PROC"
      CHAIN "SYSTEM.XFER.LST.PROC"
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINT @(-1):
      STOP
   END
