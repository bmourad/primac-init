* REVISION    - [08.0]
****************************************************************
*  THIS PROGRAM FORMATS A DATA/BASIC PROGRAM TO
*  DISPLAY BLOCK STRUCTURING BY INDENTING LINES.
****************************************************************
*---- DEFINITIONS
      PROMPT ""
10    SP = 6          ;* LEFT MARGIN COLUMN NUMBER
      ID = 3          ;* NUMBER OF SPACES TO INDENT
*---- INITIALIZATION
      SPX = SP
      LINE.NO = 0
*---- INPUT FILE NAME AND  PROGRAM NAME
      PRINT
      PRINT
      PRINT "DATA/BASIC FILE NAME    - ":; INPUT FILE
      IF FILE = "" THEN STOP
      OPEN "",FILE ELSE PRINT "CANNOT OPEN FILE - ": FILE;  GOTO 10
      PRINT "DATA/BASIC PROGRAM NAME - ":; INPUT NAME
      IF NAME = "" THEN GOTO 10
      NEWITEM = ""
      READ ITEM FROM NAME ELSE
         PRINT "CANNOT FIND THAT PROGRAM"
         GOTO 10
      END
*---- GET FORMAT CHARACTERISTICS
20    PRINT "LEFT MARGIN SPACES      - ":;INPUT SP
      IF SP = "" THEN SP = 6
      IF SP < 0 OR SP > 9 THEN GOTO 20
30    PRINT "INDENT SPACES           - ":;INPUT ID
      IF ID = "" THEN ID = 3
      IF ID < 0 OR ID > 6 THEN GOTO 30
*---- GET NEW LINE, IF NONE - THEN DONE
100   LINE.NO = LINE.NO + 1
      LINE = EXTRACT(ITEM,LINE.NO,0,0)
      IF LINE = "" THEN
         PRINT;PRINT;PRINT "UPDATE (Y/N) - ":;INPUT REPLY
         IF REPLY = "Y" THEN
            WRITE NEWITEM ON NAME
            PRINT; PRINT; PRINT "--UPDATED--"
         END ELSE
            PRINT; PRINT; PRINT "--DONE--"
         END
         GOTO 10
      END
      LABEL = ""
*---- STRIP OFF LEADING/TRAILING SPACES
200   IF LINE[1,1] = " " THEN LINE = LINE[2,32767]; GOTO 200
210   IF LINE[LEN(LINE),1] = " " THEN LINE = LINE[1,LEN(LINE)-1]; GOTO 210
*---- LOOK FOR A COMMENT ("*", "!", OR "REM")
      IF LINE[1,1] = "*" THEN GOTO 1500
      IF LINE[1,2] = "! " THEN GOTO 1500
      IF LINE[1,4] = "REM " THEN GOTO 1500
*---- LOOK FOR "FOR"
      IF LINE[1,4] = "FOR " AND INDEX(LINE,"NEXT ",1)>0 THEN GOTO 2000
      IF LINE[1,4] = "FOR " AND INDEX(LINE,"NEXT ",1)=0 THEN GOTO 1000
*---- LOOK FOR "LOOP"
      IF LINE[1,4] = "LOOP" THEN GOTO 1000
*---- LOOK FOR "UNTIL" OR "WHILE"
      IF LINE[1,6] = "UNTIL " THEN
         F = INDEX(LINE,"REPEAT",1)
         IF F = 0 THEN GOTO 1200 ELSE GOTO 1100
      END
      IF LINE[1,6] = "WHILE " THEN
         F = INDEX(LINE,"REPEAT",1)
         IF F = 0 THEN GOTO 1200 ELSE GOTO 1100
      END
*---- LOOK FOR REPEAT
      IF LINE[1,6] = "REPEAT" THEN GOTO 1100
*---- LOOK FOR "BEGIN CASE"
*     IF LINE = "BEGIN CASE" THEN GOTO 1050
      IF LINE = "BEGIN CASE" THEN GOTO 1000
*---- LOOK FOR "CASE"
      IF LINE[1,5] = "CASE " THEN GOTO 1200
*---- LOOK FOR "END"
      IF LINE = "END" THEN GOTO 1100
      IF LINE[1,4] = "END " THEN
         IF LINE[LEN(LINE)-4,5] = " ELSE" THEN GOTO 1200
      END
*---- LOOK FOR "END CASE"
*     IF LINE = "END CASE" THEN GOTO 1300
      IF LINE = "END CASE" THEN GOTO 1100
*---- LOOK FOR "NEXT"
      IF LINE[1,5] = "NEXT " THEN GOTO 1100
*---- EXTRACT LEADING NUMERIC LABEL
      IF LINE[1,1] MATCHES "1N" THEN
         L = 2
300      IF LINE[L,1] MATCHES "1N" THEN L=L+1; GOTO 300
         LABEL = LINE[1,L-1]
         LINE = LINE[L,32767]
         GOTO 200
      END
*---- LOOK FOR LINE ENDING IN " ELSE" OR " THEN"  ("IF" OR "READ")
      X = LINE[LEN(LINE)-4,5]
      IF X = " THEN" THEN GOTO 1000
      IF X = " ELSE" THEN GOTO 1000
*---- THIS IS JUST ANOTHER LINE, THEREFORE NO CHANGE
      GOTO 2000
*---- INDENT ON SUBSEQUENT LINES
1000  SP = SP + ID
      GOTO 2000
*---- DOUBLE INDENT ON SUBSEQUENT LINES
1050  SP = SP + ID + ID
      GOTO 2000
*---- OUTDENT ON THIS AND SUBSEQUENT LINES
1100  SP = SP - ID
*---- OUTDENT THIS LINE ONLY
1200  SPX = SPX - ID
      GOTO 2000
*---- DOUBLE OUTDENT ON THIS AND SUBSEQUENT LINES
1300  SP = SP - ID - ID
*---- DOUBLE OUTDENT THIS LINE ONLY
1400  SPX = SPX - ID - ID
      GOTO 2000
*---- PRINT WITH NO INDENTATION
1500  SPX = 0
*---- WRITE NEW LINE
2000  NEW.LINE = LABEL : STR(" ",SPX-LEN(LABEL)) : LINE
      PRINT NEW.LINE
      NEWITEM = REPLACE(NEWITEM,LINE.NO,0,0,NEW.LINE)
      SPX = SP
      GOTO 100
   END
