      SUBROUTINE COMPARE.DATA (ACCT1, ACCT2, FILE.NAME, FILE1, FILE2, PAGE.NO, LINE.NO, TYPE)
***************************************************************************
*
* REVISION    - [08.0]
*
* PROGRAM - COMPARE.DATA
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/13/84
*
* DESCRIPTION
*
* Compare data for given file in two different accounts for
* missing and mismatched items.
*
***************************************************************************
*
*---- SYSTEM EQUATES
*
      EQU AM TO CHAR(254)
      EQU VM TO CHAR(253)
      EQU SM TO CHAR(252)
*
*---- INITIALIZATION
*
      ITEMS = ""
      IFLAG = ""
*
*---- GET ITEM ID'S FOR FILE 1
*
100*
      SELECT FILE1
110*
      READNEXT ID1 ELSE GOTO 200
      IF ID1[1,1] = "$" THEN GOTO 110
      IF FILE.NAME = "MD" THEN
         READ REC1 FROM FILE1, ID1 ELSE GOTO 110
         IF REC1<1> = "D" THEN GOTO 110
      END
      LOCATE ID1 IN ITEMS<1>,1 BY "AL" SETTING N ELSE NULL
      ITEMS = INSERT(ITEMS,1,N,0,ID1)
      IFLAG = INSERT(IFLAG,1,N,0,1)
      GOTO 110
*
*---- GET ITEM ID'S FOR FILE 2
*
200*
      SELECT FILE2
210*
      READNEXT ID2 ELSE GOTO 300
      IF ID2[1,1] = "$" THEN GOTO 210
      IF FILE.NAME = "MD" THEN
         READ REC2 FROM FILE2, ID2 ELSE GOTO 210
         IF REC2<1> = "D" THEN GOTO 210
      END
      LOCATE ID2 IN ITEMS<1>,1 BY "AL" SETTING N ELSE
         ITEMS = INSERT(ITEMS,1,N,0,ID2)
         IFLAG = INSERT(IFLAG,1,N,0,0)
      END
      IFLAG<1,N> = IFLAG<1,N> + 2
      GOTO 210
*
*---- COMPARE ITEMS
*
300*
      SH1 = 'COMPARING ITEMS FOR "':FILE.NAME:'"'
      SH2 = STR("-",LEN(SH1))
      IF LINE.NO > 50 THEN
         GOSUB 10000
      END
      PRINT
      PRINT SH1
      PRINT SH2
      LINE.NO = LINE.NO + 3
      ITEM.CNT = COUNT(ITEMS,VM) + (ITEMS # "")
      FOR N = 1 TO ITEM.CNT
         BEGIN CASE
            CASE IFLAG<1,N> = 2
               PLINE = "MISSING IN ACCOUNT 1 - " : ITEMS<1,N>
               GOSUB 900
            CASE IFLAG<1,N> = 1
               PLINE = "MISSING IN ACCOUNT 2 - " : ITEMS<1,N>
               GOSUB 900
            CASE 1
               BEGIN CASE
                  CASE TYPE = "PROG"
                     READV OBJ1 FROM FILE1, "$":ITEMS<1,N>,1 ELSE OBJ1 = ""
                     READV OBJ2 FROM FILE2, "$":ITEMS<1,N>,1 ELSE OBJ2 = ""
                     IF OBJ1 # OBJ2 THEN
*                       OBJ1<1> = OBJ2<1>
*                       IF OBJ1 # OBJ2 THEN
*                          READ REC1 FROM FILE1, ITEMS<1,N> ELSE REC1 = ""
*                          DONE = 0
*                          LCNT = COUNT(REC1,AM) + (REC1 # "")
*                          FOR L = 1 TO LCNT UNTIL DONE
*                             READV ATT2 FROM FILE2, ITEMS<1,N>,L ELSE
*                                ATT2 = "??????"
*                             END
*                             IF TRIM(ATT2) # TRIM(REC1<L>) THEN
                                 PLINE = "MISMATCHED ITEMS     - " : ITEMS<1,N>
                                 GOSUB 900
                                 DONE = 1
*                             END
*                          NEXT L
*                       END
                     END
                  CASE 1
                     READ REC1 FROM FILE1, ITEMS<1,N> ELSE REC1 = ""
                     BEGIN CASE
                        CASE FILE.NAME = "MD" AND REC1<1> = "P" AND REC1<2> = "10B4"
                        CASE FILE.NAME = "MD" AND REC1<1> = "Q"
                        CASE FILE.NAME[1,5] = "DICT*" AND REC1<1> = "D"
                        CASE 1
                           READ REC2 FROM FILE2, ITEMS<1,N> ELSE REC2 = ""
                           IF TRIM(REC1) # TRIM(REC2) THEN
                              PLINE = "MISMATCHED ITEMS     - " : ITEMS<1,N>
                              GOSUB 900
                           END
                     END CASE
               END CASE
         END CASE
      NEXT N
      GOTO 99999
*
*---- PRINT ROUTINE
*
900*
      IF LINE.NO > 55 THEN
         GOSUB 10000
         PRINT
         PRINT SH1
         PRINT SH2
         LINE.NO = LINE.NO + 3
      END
      PRINT SPACE(5):PLINE
      LINE.NO = LINE.NO + 1
      RETURN
*
*---- PRINT HEADINGS
*
10000*
      CALL COMPARE.HEADINGS (ACCT1, ACCT2, PAGE.NO, LINE.NO)
      RETURN
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
