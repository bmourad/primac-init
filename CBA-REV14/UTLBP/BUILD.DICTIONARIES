* REVISION    - [08.0]
*  BUILD FILE DICTIONARIES FROM CPYLIB FILE DESCRIPTIONS - E. PRICE
*
     PROMPT ""
     AM = CHAR(254)
     DOTS = STR(".",25)
     PRINT CHAR(12):@(25,11):"File Name - ":
     INPUT FILE.NAME
     IF FILE.NAME = "" THEN STOP
     OPEN "",FILE.NAME TO CPYLIB ELSE PRINT "INVALID FILE NAME";STOP
     DIM FILE.LINES(1000)
     DIM ITEM(10)
     DONE = 0
     LOOP
        READNEXT ID ELSE DONE = 1
     UNTIL DONE DO
        PRINT "PROCESSING ":ID
        DICT.ID = ID
        OPEN "DICT", DICT.ID TO DICT.FILE ELSE
           PRINT "CANNOT OPEN DICTIONARY OF ":DICT.ID
           GOTO 100
        END
        MAT FILE.LINES = ""
        MATREAD FILE.LINES FROM CPYLIB, ID ELSE
           PRINT "CANNOT READ FILE.LINES ":ID:" FROM CPYLIB"
           GOTO 100
        END
        NUMBER.READ = 0
        FOR I = 1 TO 1000
           IF FILE.LINES(I) = "" THEN GOTO 10
           NUMBER.READ = NUMBER.READ + 1
        NEXT I
10*
        PREV.POSITION = 0
        FOR I = 1 TO NUMBER.READ
           LINE = TRIM(FILE.LINES(I))
           IF FIELD(LINE," ",1) = "EQU" THEN
              ITEM.NAME = FIELD(LINE," ",2)
              POSITION = FIELD(LINE," ",4)
              POSITION = FIELD(POSITION,"(",2)
              POSITION = POSITION[1,LEN(POSITION)-1]
              IF NOT(NUM(POSITION)) THEN
                 GOTO 50
              END
              MAT ITEM = ""
              ITEM(1) = "A"
              ITEM(2) = POSITION
              ITEM(3) = ITEM.NAME
              BEGIN CASE
                 CASE INDEX(ITEM.NAME,"DATE",1) # 0
                    ITEM(7) = "D2/"
                    ITEM(9) = "R"
                    ITEM(10) = "8"
                 CASE INDEX(ITEM.NAME,"AMT",1) # 0 OR INDEX(ITEM.NAME,"AMOUNT",1) # 0 OR INDEX(ITEM.NAME,"$",1) # 0
                    ITEM(7) = "MD2,"
                    ITEM(9) = "R"
                    ITEM(10) = "10"
                 CASE INDEX(ITEM.NAME,"COST",1) # 0 OR INDEX(ITEM.NAME,"SALE",1) # 0 OR INDEX(ITEM.NAME,"$",1) # 0
                    ITEM(7) = "MD2,"
                    ITEM(9) = "R"
                    ITEM(10) = "10"
                 CASE INDEX(ITEM.NAME,"QTY",1) # 0 OR INDEX(ITEM.NAME,"QUANT",1) # 0
                    ITEM(7) = "MD0,"
                    ITEM(9) = "R"
                    ITEM(10) = "7"
                 CASE 1
                    ITEM(9) = "L"
                    ITEM(10) = "20"
              END CASE
              READV CHECK FROM DICT.FILE, ITEM.NAME, 1 ELSE
                 MATWRITE ITEM ON DICT.FILE, ITEM.NAME
              END
              FOR F = PREV.POSITION + 1 TO POSITION - 1
                 READV CHECK FROM DICT.FILE, F, 1 ELSE
                    FILLER = "X":AM:F:AM:AM:AM:AM:AM:AM:AM:"L":AM:"0"
                    WRITE FILLER ON DICT.FILE, F
                 END
              NEXT F
              READV CHECK FROM DICT.FILE, POSITION, 1 ELSE
                 ITEM(3) = ITEM(3):DOTS[1,25-LEN(ITEM(3))]
                 MATWRITE ITEM ON DICT.FILE, POSITION
              END
              PREV.POSITION = POSITION
           END
50*
        NEXT I
100*
     REPEAT
     STOP
     END
