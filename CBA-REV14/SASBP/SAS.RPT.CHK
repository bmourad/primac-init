**************************************************************************
* REVISION [10A]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.) *
* DESCRIPTION -  THE FOLLOWING SALES ANALYSIS SUMMARY REPORTS ALL CREATE
*                A TEMPORARY FILE ,JOB.STATS.SUM, FOR USE IN PRINTING THE
*                REPORT :
*                         SALES BY CUSTOMER
*                         SALES BY SALESPERSON
*                         SALES BY JOB.CATEGORY
*                         SALES BY SALES.CODE
*                         SALES BY COLOR.COUNT
*                         SALES BY PRESS.ID
*                         SALES BY STATE
*                IF MORE THAN ONE USER IS PRINTING ONE OF THESE REPORTS
*                UNPREDICTABLE RESULTS WILL OCCUR.
*                THIS PROGRAM PREVENTS MORE THAN ONE USER FROM RUNNING
*                ONE OF THESE REPORTS AT A TIME BY SETTING A LOCK IN
*                THE CONTROL FILE CONO:"SAS.RPT.CHK".
*ENDOC
**************************************************************************
*COPY>CPYLIB>CHAR
*
   PROMPT ""
   PROCREAD INBUFF ELSE
      ERRMSG = "MUST RUN FROM A PROC"
      GOSUB 91000; GOTO 99999
   END
   CONO = INBUFF<1>
   TYPE = INBUFF<3>
   BEGIN CASE
      CASE TYPE = "START"
      CASE TYPE = "STOP"
      CASE 1
         ERRMSG = "INVALID PARAMETER PASSED FROM PROC"
         GOSUB 91000
         GOTO 88888
   END CASE
*
   OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'CANNOT LOCATE CONTROL FILE'
      GOSUB 91000
      GOTO 88888
   END
*
   READU ITEM FROM CONTROL, CONO:"SAS.RPT.CHK" ELSE ITEM = ""
   ERRMSG = ""
   PORT = @TTY
   ME = 0
   IF ITEM<1> # "" THEN
      ME = INDEX(ITEM<1>,PORT,1)
   END
   IF TYPE = "START" THEN
      BEGIN CASE
         CASE ITEM<1> # "" AND ME
            RELEASE CONTROL, CONO:"SAS.RPT.CHK"
            GOTO 99999
         CASE ITEM<1> # "" AND ME = 0
            ITEM<1> = TRIM(ITEM<1>)
            ERRMSG = ITEM<1>:" IS RUNNING A REPORT PLEASE WAIT"
            GOSUB 91000
            RELEASE CONTROL, CONO:"SAS.RPT.CHK"
            GOTO 88888
         CASE 1
            COMMAND = "WHO"
            UDTEXECUTE COMMAND CAPTURING WHO.LIST
            I = 0
            LOOP
               I += 1
               ME = INDEX(WHO.LIST<I>,PORT,1)
               IF WHO.LIST<I> = "" THEN ME = I
            UNTIL ME REPEAT
            IF WHO.LIST<I> # "" THEN
               ITEM<1> = WHO.LIST<I>
               WRITE ITEM ON CONTROL, CONO:"SAS.RPT.CHK"
            END
            GOTO 99999
      END CASE
   END ELSE IF TYPE = "STOP" THEN
      BEGIN CASE
         CASE ITEM<1> # "" AND ME
            DELETE CONTROL, CONO:"SAS.RPT.CHK"
            RELEASE CONTROL, CONO:"SAS.RPT.CHK"
            GOTO 99999
         CASE ITEM<1> # "" AND ME = 0
            RELEASE CONTROL, CONO:"SAS.RPT.CHK"
            GOTO 88888
         CASE 1
            RELEASE CONTROL, CONO:"SAS.RPT.CHK"
            GOTO 88888
      END CASE
   END
   GOTO 99999
91000 PRINT @(0,23) : ERRMSG : CL :
   INPUT X,1 :
   PRINT @(0,23) : CL :
   RETURN
88888 INBUFF<3> = "END"
   PROCWRITE INBUFF
99999 END
