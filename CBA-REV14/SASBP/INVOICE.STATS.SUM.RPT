***************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - JOB.STATS.SUM.RPT
*
* SYSTEM        - SASBP
*
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 11/22/85
*
* DESCRIPTION   -
* This program produces a Sales Status Summary Report by:
* Customer, Salesman, Catagory, Sales Code, Press Id or Colors.
*
* MOD           - CSF9746 RWW 7.27.89
* MOD           - CSF 20042 MWS 11/15/93 FIX DECIMAL ON MAGIN ADJ. PERCENT
*                 AND MARGIN ACT. PERCENT.
*
*ENDDOC
***************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>JOB.STATS.SUM
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD XX ELSE
        ERRMSG="MUST BE RUN FROM A PROC"
        GOSUB 91000
        GOTO 99999
     END
     CONO = XX<1>
     CO.NAME = XX<2>
     FROM.DATE = XX<3>
     TO.DATE = XX<4>
     PROG.TYPE = XX<5>
     IF ICONV(FROM.DATE,"D2/") = ICONV(TO.DATE,"D2/") THEN
        DATE.TITLE = SPACE(59):"FOR ":FROM.DATE
     END ELSE
        DATE.TITLE = SPACE(53):"FROM ":FROM.DATE:" TO ":TO.DATE
     END
     BEGIN CASE
        CASE PROG.TYPE = "CUST"
           HEAD2 = SPACE(60):"BY CUSTOMER"
           COL1 = SPACE(9):"CUSTOMER NAME":SPACE(9)
           TOT.HEAD = "CUSTOMERS:     "
        CASE PROG.TYPE = "SLSM"
           HEAD2 = SPACE(60):"BY SALESREP"
           COL1 = SPACE(9):"SALESREP NAME":SPACE(9)
           TOT.HEAD = "SALESMEN:      "
        CASE PROG.TYPE = "BSLSM"
           HEAD2 = SPACE(60):"BY SALESREP BUDGET"
           COL1 = SPACE(9):"SALESREP NAME":SPACE(9)
           TOT.HEAD = "SALESMEN:      "
        CASE PROG.TYPE = "BCUST"
           HEAD2 = SPACE(60):"BY CUSTOMER BUDGET"
           COL1 = SPACE(9):"CUSTOMER NAME":SPACE(9)
           TOT.HEAD = "CUSTOMERS:     "
        CASE PROG.TYPE = "CAT"
           HEAD2 = SPACE(58):"BY CATEGORY(JCS)"
           COL1 = SPACE(11):"CATEGORY":SPACE(12)
           TOT.HEAD = "CATEGORIES:    "
        CASE PROG.TYPE = "CATOPS"
           HEAD2 = SPACE(58):"BY CATEGORY(OPS)"
           COL1 = SPACE(11):"CATEGORY":SPACE(12)
           TOT.HEAD = "CATEGORIES:    "
        CASE PROG.TYPE = "SCD"
           HEAD2 = SPACE(59):"BY SALES CODE"
           COL1 = SPACE(10):"SALES CODE":SPACE(11)
           TOT.HEAD = "SALES CODES:   "
        CASE PROG.TYPE = "PRESS"
           HEAD2 = SPACE(60):"BY PRESS ID"
           COL1 = SPACE(11):"PRESS ID":SPACE(12)
           TOT.HEAD = "PRESS IDS:     "
        CASE PROG.TYPE = "COLOR"
           HEAD2 = SPACE(61):"BY COLOR"
           COL1 = SPACE(7):"NUMBER OF COLORS":SPACE(8)
           TOT.HEAD = "COLORS:        "
        CASE PROG.TYPE = "STATE"
           HEAD2 = SPACE(61):"BY STATE"
           COL1 = SPACE(12):"STATE":SPACE(13)
           TOT.HEAD = "STATES:        "
     END CASE
*
*---- DIMENSION VARIABLES
*
     DIM TOTAL.REC(10)
*
*---- OPEN FILES
*
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     OPEN '','SALESMAN' TO SALESMAN ELSE ERRMSG='SALESMAN FILE MISSING';GOTO 93000
     OPEN '','JOB.STATS.SUM' TO JOB.STATS.SUM ELSE ERRMSG='JOB.STATS.SUM FILE MISSING';GOTO 93000
     OPEN '','JOB.CATEGORY' TO JOB.CATEGORY ELSE ERRMSG='JOB.CATEGORY FILE MISSING';GOTO 93000
     OPEN '','CATEGORY' TO CATEGORY ELSE ERRMSG='CATEGORY FILE MISSING';GOTO 93000
     OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG='COST.CNTR FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
     PG.NO=0
     LINE.CNT=0
     ERRMSG=""
     RPT.NO="XXXX"
     UNKNOWN=STR("?",30)
     DSH=STR("-",28)
     DSH9=DSH[1,9]
     DSH10=DSH[1,10]
     DSH5=DSH[1,5]
     DSH4=DSH[1,4]
     EQL=STR("=",9)
     EQL1=STR("=",10)
     EQL5=EQL[1,5]
     SP=" "
     SP6="      "
     SP7="       "
     SP12="            "
     SP29=STR(" ",29)
     MAT TOTAL.REC=""
     NODATA=0
     FIRST.FLAG = 1
     EQL.LINE = SP29:EQL1:SP:EQL1:SP6:EQL1:SP:EQL5:SP6:EQL1:SP:EQL5:SP6:EQL1:SP:EQL5:SP:EQL:SP:EQL5
     TOT.TITLE="TOTAL FOR ALL ":TOT.HEAD
     DSH.LINE = DSH:SP:DSH10:SP:DSH10:SP6:DSH10:SP:DSH5:SP6:DSH10:SP:DSH5:SP6:DSH10:SP:DSH5:SP:DSH9:SP:DSH5
     PRINTER ON
     TTL.SALES=0
     NET.SALES=0
     NET.RANK=0
     VAL.ADDED=0
     VAL.ADDED.PCT=0
     VAL.ADDED.RANK=0
*
*---- MAIN PROCESSING
*
10*
     READNEXT KEY ELSE 
        IF NODATA=0 THEN GOTO 99999
        GOTO 15
     END
     NODATA=1
     IF FIRST.FLAG THEN
        FIRST.FLAG = 0
        GOSUB 20
     END
     MATREAD JSX.REC FROM JOB.STATS.SUM, KEY ELSE GOTO 10
     KEY.NO = FIELD(KEY,"*",3)
     GOSUB 30
     GOTO 10
15*
     GOSUB 50
     PRINTER OFF
     GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
     PRINT CHAR(12)
     LINE.CNT=0
     PG.NO=PG.NO + 1
     H.LINE="";HLINE1="";HLINE2="";HLINE3=""
     HLINE1="RUN DATE : ":OCONV(DATE(),"D2/")"R#8":SPACE(22)
     N=SPACE((50 - LEN(CO.NAME)) / 2)
     HLINE2=N:CO.NAME:N
     HLINE3=SPACE(25):"PAGE ":PG.NO
     H.LINE=HLINE1:HLINE2:HLINE3
     PRINT H.LINE
     H.LINE=SPACE(45):"S A L E S   A N A L Y S I S   S U M M A R Y"
     PRINT H.LINE
     H.LINE=HEAD2
     PRINT H.LINE
     PRINT DATE.TITLE
     PRINT
     IF PROG.TYPE = "BCUST" OR PROG.TYPE = "BSLSM" THEN
        H.LINE=SPACE(31):"BUDGET     NET             VALUE                MARGIN ON             MARGIN ON"
     END ELSE
        H.LINE=SPACE(31):"TOTAL      NET             VALUE                MARGIN ON             MARGIN ON"
     END
     PRINT H.LINE
     H.LINE=COL1:"SALES      SALES    RANK   ADDED      %   RANK  ADJ COST    %   RANK  ACT COST    %   SPOILAGE    %"
     PRINT H.LINE
     H.LINE=DSH:SP:DSH10:SP:DSH10:SP:DSH4:SP:DSH10:SP:DSH5:SP:DSH4:SP:DSH10:SP:DSH5:SP:DSH4:SP:DSH10:SP:DSH5:SP:DSH9:SP:DSH5
     PRINT H.LINE
     LINE.CNT=8
     RETURN
*
*---- PRINT THE VALUES
*
30*
     GOSUB 40
     IF LINE.CNT >= 52 THEN GOSUB 20
     PRINT
     P.LINE = COLUMN1:TTL.SALES:SP:NET.SALES:SP:NET.RANK:SP:VAL.ADDED:SP:VAL.ADDED.PCT:SP:VAL.ADDED.RANK:SP
     P.LINE = P.LINE:MAR.ADJ.COST:SP:ADJ.COST.PCT:SP:MAR.ADJ.RANK:SP:MAR.ACT.COST:SP:ACT.COST.PCT:SP:SPOILAGE:SP:SPOILAGE.PCT
     PRINT P.LINE
     LINE.CNT=LINE.CNT + 2
     RETURN
*
*---- CALCULATE FIELD VALUES
*
40*
     BEGIN CASE
        CASE PROG.TYPE = "CUST"
           MATREAD CUST.REC FROM CUSTOMER,CONO:KEY.NO ELSE CUST.NAME = UNKNOWN
           COLUMN1 = CUST.NAME "L#28":SP
        CASE PROG.TYPE = "SLSM"
           MATREAD SALESMAN.REC FROM SALESMAN,CONO:KEY.NO ELSE SALS.NAME = UNKNOWN
           COLUMN1 = SALS.NAME "L#28":SP
        CASE PROG.TYPE = "BCUST"
           MATREAD CUST.REC FROM CUSTOMER,CONO:KEY.NO ELSE CUST.NAME = UNKNOWN
           COLUMN1 = CUST.NAME "L#28":SP
        CASE PROG.TYPE = "BSLSM"
           MATREAD SALESMAN.REC FROM SALESMAN,CONO:KEY.NO ELSE SALS.NAME = UNKNOWN
           COLUMN1 = SALS.NAME "L#28":SP
        CASE PROG.TYPE = "CAT"
           READ JC.DESC FROM JOB.CATEGORY,CONO:KEY.NO ELSE JC.DESC = UNKNOWN
           COLUMN1 = JC.DESC "L#28":SP
        CASE PROG.TYPE = "CATOPS"
           MATREAD CATG.REC FROM CATEGORY,CONO:KEY.NO ELSE CATG.DESC = UNKNOWN
           COLUMN1 = CATG.DESC "L#28":SP
        CASE PROG.TYPE = "SCD"
           COLUMN1 = KEY.NO "L#28":SP
        CASE PROG.TYPE = "PRESS"
           MATREAD CCTR.REC FROM COST.CNTR,CONO:KEY.NO ELSE 
              IF KEY.NO = "MULTI" THEN
                 CCTR.DESC = "MULTI PRESS"
              END ELSE
                 CCTR.DESC = UNKNOWN
              END
           END
           COLUMN1 = CCTR.DESC "L#28":SP
        CASE PROG.TYPE = "COLOR"
           DESC = KEY.NO:" COLORS"
           COLUMN1 = DESC "L#28":SP
        CASE PROG.TYPE = "STATE"
           DESC = KEY.NO:" STATE"
           COLUMN1 = DESC "L#28":SP
     END CASE
     MARGIN.ADJ = JSX.NET.SALES - JSX.ADJ.COST
     MARGIN.ACT = JSX.NET.SALES - JSX.TOT.COST
     TOTAL.REC(1) = TOTAL.REC(1) + JSX.TOT.SALES
     TOTAL.REC(2) = TOTAL.REC(2) + JSX.NET.SALES
     TOTAL.REC(3) = TOTAL.REC(3) + JSX.VAL.ADDED
     TOTAL.REC(4) = TOTAL.REC(4) + MARGIN.ADJ
     TOTAL.REC(5) = TOTAL.REC(5) + MARGIN.ACT
     TOTAL.REC(6) = TOTAL.REC(6) + JSX.SPOILAGE
     IF JSX.TOT.SALES + 0 > 0 THEN
        TTL.SALES = OCONV(INT((JSX.TOT.SALES / 100) + .5),"MD0,") "R#10"
     END ELSE
        TTL.SALES = OCONV(INT((JSX.TOT.SALES / 100) - .5),"MD0,") "R#10"
     END
     IF JSX.NET.SALES + 0 > 0 THEN
        NET.SALES = OCONV(INT((JSX.NET.SALES / 100) + .5),"MD0,") "R#10"
     END ELSE
        NET.SALES = OCONV(INT((JSX.NET.SALES / 100) - .5),"MD0,") "R#10"
     END
     NET.RANK = JSX.NET.SALES.RANK "R#4"
     IF JSX.VAL.ADDED + 0 > 0 THEN
        VAL.ADDED = OCONV(INT((JSX.VAL.ADDED / 100) + .5),"MD0,") "R#10"
     END ELSE
        VAL.ADDED = OCONV(INT((JSX.VAL.ADDED / 100) - .5),"MD0,") "R#10"
     END
     IF JSX.VAL.ADDED.PCT + 0 > 0 THEN
        VAL.ADDED.PCT = OCONV(INT((JSX.VAL.ADDED.PCT / 10) + .5),"MD1") "R#5"
     END ELSE
        VAL.ADDED.PCT = OCONV(INT((JSX.VAL.ADDED.PCT / 10) - .5),"MD1") "R#5"
     END
     VAL.ADDED.RANK = JSX.VAL.ADDED.RANK "R#4"
     IF MARGIN.ADJ + 0 > 0 THEN
        MAR.ADJ.COST = OCONV(INT((MARGIN.ADJ / 100) + .5),"MD0,") "R#10"
     END ELSE
        MAR.ADJ.COST = OCONV(INT((MARGIN.ADJ / 100) - .5),"MD0,") "R#10"
     END
*    IF JSX.ADJ.COST.PCT + 0 > 0 THEN
*       ADJ.COST.PCT = OCONV(INT((JSX.ADJ.COST.PCT / 10) + .5),"MD1") "R#5"
*    END ELSE
*       ADJ.COST.PCT = OCONV(INT((JSX.ADJ.COST.PCT / 10) - .5),"MD1") "R#5"
*    END
     IF JSX.NET.SALES NE 0 THEN
*        ADJ.COST.PCT =OCONV((MARGIN.ADJ/JSX.NET.SALES) *100,"MD01")
*CSF 20042
        ADJ.COST.PCT = (MARGIN.ADJ/JSX.NET.SALES) * 100
*
        IF ADJ.COST.PCT GT 0 THEN RND = .5 ELSE RND = -.5
        ADJ.COST.PCT = OCONV(INT(ADJ.COST.PCT * 10 + RND),"MD1")"R#5"
     END ELSE
        ADJ.COST.PCT = "0.0" "R#5"
     END
     MAR.ADJ.RANK = JSX.ADJ.COST.RANK "R#4"
     IF MARGIN.ACT + 0 > 0 THEN
        MAR.ACT.COST = OCONV(INT((MARGIN.ACT / 100) + .5),"MD0,") "R#10"
     END ELSE
        MAR.ACT.COST = OCONV(INT((MARGIN.ACT / 100) - .5),"MD0,") "R#10"
     END
*    IF JSX.TOT.COST.PCT + 0 > 0 THEN
*       ACT.COST.PCT = OCONV(INT((JSX.TOT.COST.PCT / 10) + .5),"MD1") "R#5"
*    END ELSE
*       ACT.COST.PCT = OCONV(INT((JSX.TOT.COST.PCT / 10) - .5),"MD1") "R#5"
*    END
     IF JSX.NET.SALES NE 0 THEN
*        ACT.COST.PCT =OCONV((MARGIN.ACT/JSX.NET.SALES) *100,"MD01")
*CSF 20042
        ACT.COST.PCT = (MARGIN.ACT/JSX.NET.SALES) * 100
*
        IF ACT.COST.PCT GT 0 THEN RND = .5 ELSE RND = -.5
        ACT.COST.PCT = OCONV(INT(ACT.COST.PCT * 10 + RND),"MD1")"R#5"
     END ELSE
        ACT.COST.PCT = "0.0" "R#5"
     END
     IF JSX.SPOILAGE + 0 > 0 THEN
        SPOILAGE = OCONV(INT((JSX.SPOILAGE / 100) + .5),"MD0,") "R#9"
     END ELSE
        SPOILAGE = OCONV(INT((JSX.SPOILAGE / 100) - .5),"MD0,") "R#9"
     END
     IF JSX.SPOILAGE.PCT + 0 > 0 THEN
        SPOILAGE.PCT = OCONV(INT((JSX.SPOILAGE.PCT / 10) + .5),"MD1") "R#5"
     END ELSE
        SPOILAGE.PCT = OCONV(INT((JSX.SPOILAGE.PCT / 10) - .5),"MD1") "R#5"
     END
     RETURN
*
*---- PRINT GRAND TOTALS
*
50*
     GOSUB 60
     IF LINE.CNT + 5 >= 52 THEN GOSUB 20
     PRINT DSH.LINE
     PRINT
     G.LINE = TOT.TITLE:GTTL.SALES:SP:GNET.SALES:SP6:GVAL.ADDED:SP:GVAL.PCT:SP6
     G.LINE = G.LINE:GMAR.ADJ.COST:SP:GADJ.PCT:SP6:GMAR.ACT.COST:SP:GACT.PCT:SP:GSPOILAGE:SP:GSPL.PCT
     PRINT G.LINE
     PRINT
     PRINT EQL.LINE
     RETURN
*
*---- VALUES FOR GRAND TOTALS
*
60*
     IF TOTAL.REC(2) + 0 > 0 THEN
        GVAL.PCT = OCONV(INT(((TOTAL.REC(3) / TOTAL.REC(2)) * 1000) + .5),"MD1")"R#5"
        GADJ.PCT = OCONV(INT(((TOTAL.REC(4) / TOTAL.REC(2)) * 1000) + .5),"MD1")"R#5"
        GACT.PCT = OCONV(INT(((TOTAL.REC(5) / TOTAL.REC(2)) * 1000) + .5),"MD1")"R#5"
        GSPL.PCT = OCONV(INT(((TOTAL.REC(6) / TOTAL.REC(2)) * 1000) + .5),"MD1")"R#5"
     END ELSE
        GVAL.PCT = OCONV(0,"MD1")"R#5"
        GADJ.PCT = OCONV(0,"MD1")"R#5"
        GACT.PCT = OCONV(0,"MD1")"R#5"
        GSPL.PCT = OCONV(0,"MD1")"R#5"
     END
     IF TOTAL.REC(1) + 0 > 0 THEN
        GTTL.SALES = OCONV(INT((TOTAL.REC(1) / 100) + .5),"MD0,") "R#10"
     END ELSE
        GTTL.SALES = OCONV(INT((TOTAL.REC(1) / 100) - .5),"MD0,") "R#10"
     END
     IF TOTAL.REC(2) + 0 > 0 THEN
        GNET.SALES = OCONV(INT((TOTAL.REC(2) / 100) + .5),"MD0,") "R#10"
     END ELSE
        GNET.SALES = OCONV(INT((TOTAL.REC(2) / 100) - .5),"MD0,") "R#10"
     END
     IF TOTAL.REC(3) + 0 > 0 THEN
        GVAL.ADDED = OCONV(INT((TOTAL.REC(3) / 100) + .5),"MD0,") "R#10"
     END ELSE
        GVAL.ADDED = OCONV(INT((TOTAL.REC(3) / 100) - .5),"MD0,") "R#10"
     END
     IF TOTAL.REC(4) + 0 > 0 THEN
        GMAR.ADJ.COST = OCONV(INT((TOTAL.REC(4) / 100) + .5),"MD0,") "R#10"
     END ELSE
        GMAR.ADJ.COST = OCONV(INT((TOTAL.REC(4) / 100) - .5),"MD0,") "R#10"
     END
     IF TOTAL.REC(5) + 0 > 0 THEN
        GMAR.ACT.COST = OCONV(INT((TOTAL.REC(5) / 100) + .5),"MD0,") "R#10"
     END ELSE
        GMAR.ACT.COST = OCONV(INT((TOTAL.REC(5) / 100) - .5),"MD0,") "R#10"
     END
     IF TOTAL.REC(6) + 0 > 0 THEN
        GSPOILAGE = OCONV(INT((TOTAL.REC(6) / 100) + .5),"MD0,") "R#9"
     END ELSE
        GSPOILAGE = OCONV(INT((TOTAL.REC(6) / 100) - .5),"MD0,") "R#9"
     END
     RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END


