      SUBROUTINE EQUIP.HIST.SUB
*COPY>CPYLIB>COM1
*COPY>FAS.CPYLIB>COM.EQUIP
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - FASBP
* PROGRAM     - EQUIP.HIST.SUB
* BY          - Ziad Yamout, Vercom Software, Inc.
* DATE        - 10/20/88
* DESCRIPTION - Equipment history inquiry
*********************************************************************
*
*COPY>FAS.CPYLIB>FAS.FILE.VARS
*COPY>FAS.CPYLIB>EQUIP.FILE
*COPY>FAS.CPYLIB>EQUIP.HIST
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
      SCV.REC(3)<ECD.SCRN.NO> = SCV.REC(2)<ECD.SCRN.NO>
      AST.ID = CONO:AST.NUM
      MATREAD EQH.REC FROM EQUIP.HIST, AST.ID:SCV.REC(3)<ECD.SCRN.NO> THEN
         GOSUB 1000
      END ELSE
         ERRMSG = "There is no activity logged for this year"
         GOSUB 91000
         MAT EQH.REC = ""
      END
      ECD.ACTION = 3; CALL SCRN.EDIT
*
*--- Prompt line prompt
      LOOP
         ECD.NUM = 51; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
         CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
            OPTION = "END"
         CASE NOT(NUM(OPTION))
            ERRMSG = "*** Invalid selection ***"; GOSUB 91000
         CASE OPTION < EQP.FST.YR OR OPTION > SCV.REC(2)<ECD.SCRN.NO>
            ERRMSG = "*** Out of range ***"; GOSUB 91000
         CASE 1
            MATREAD EQH.REC FROM EQUIP.HIST, AST.ID:OPTION THEN
               SCV.REC(3)<ECD.SCRN.NO> = OPTION
               GOSUB 1000
               ECD.ACTION = 3; CALL SCRN.EDIT
            END ELSE
               ERRMSG = "There is no activity logged for year - ":OPTION
               GOSUB 91000
            END
         END CASE
      WHILE OPTION # "END" DO REPEAT
      GOTO 99999
*---- Load the YTD values
1000*
      IF EQH.U.HRS + 0 = 0 THEN
         SCV.REC(11)<ECD.SCRN.NO> = 0
      END ELSE
         SCV.REC(11)<ECD.SCRN.NO> = OCONV(INT(EQH.U.UNITS/(EQH.U.HRS/100)+.5),"MD0,")
      END
      SCV.REC(21)<ECD.SCRN.NO> = OCONV(EQH.U.HRS,"MD2,")
      SCV.REC(22)<ECD.SCRN.NO> = OCONV((EQH.U.UNITS/100),"MD0,")
      SCV.REC(31)<ECD.SCRN.NO> = EQH.M.NO + 0
      SCV.REC(32)<ECD.SCRN.NO> = OCONV(EQH.M.HRS+0,"MD2,")
      SCV.REC(33)<ECD.SCRN.NO> = OCONV(EQH.M.COST+0,"MD2,")
      SCV.REC(31)<ECD.SCRN.NO,2> = EQH.R.NO + 0
      SCV.REC(32)<ECD.SCRN.NO,2> = OCONV(EQH.R.HRS+0,"MD2,")
      SCV.REC(33)<ECD.SCRN.NO,2> = OCONV(EQH.R.COST+0,"MD2,")
      SCV.REC(31)<ECD.SCRN.NO,3> = EQH.O.NO + 0
      SCV.REC(32)<ECD.SCRN.NO,3> = OCONV(EQH.O.HRS+0,"MD2,")
      SCV.REC(33)<ECD.SCRN.NO,3> = OCONV(EQH.O.COST+0,"MD2,")
      SCV.REC(41)<ECD.SCRN.NO> = EQH.M.NO + EQH.R.NO + EQH.O.NO
      SCV.REC(42)<ECD.SCRN.NO> = EQH.M.HRS + EQH.R.HRS + EQH.O.HRS
      SCV.REC(43)<ECD.SCRN.NO> = OCONV(EQH.M.COST + EQH.R.COST + EQH.O.COST,"MD2,")
      IF SCV.REC(41)<ECD.SCRN.NO> = 0 THEN
         SCV.REC(13)<ECD.SCRN.NO> = 0
      END ELSE
         SCV.REC(13)<ECD.SCRN.NO> = OCONV(INT(SCV.REC(42)<ECD.SCRN.NO>/SCV.REC(41)<ECD.SCRN.NO>+.5),"MD2,")
      END
      IF EQH.U.HRS + 0 = 0 THEN
         SCV.REC(15)<ECD.SCRN.NO> = 0
      END ELSE
         SCV.REC(15)<ECD.SCRN.NO> = INT(SCV.REC(42)<ECD.SCRN.NO>/EQH.U.HRS*10000+.5)
      END
      SCV.REC(42)<ECD.SCRN.NO> = OCONV(SCV.REC(42)<ECD.SCRN.NO>,"MD2,")
      RETURN
*
*--- Error message routine
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT REPLY :
*       PRINT @(0,23) : CL :
*       RETURN
99999 *
ECD.ACTION = 99 ; CALL SCRN.EDIT
RETURN
   END
