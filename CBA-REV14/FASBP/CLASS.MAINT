*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - FASBP
* PROGRAM       - CLASS.MAINT
* BY            - ANN POWLEY, C.B.A.
* DATE          - 05/12/87
* DESCRIPTION   - This program creates & maintain class code
*                 information.
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*********************************************************************
*
***** FILE INSERTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>FAS.CPYLIB>CLASS
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
  OPEN "","CLASS" TO CLASS ELSE ERRMSG = "CLASS"; GOTO 93000
  OPEN "","CLASS.XREF" TO CLASS.XREF ELSE ERRMSG = "CLASS.XREF"; GOTO 93000
  OPEN "","FAS.SCREENS" TO M.SCREENS ELSE ERRMSG = "FAS.SCREENS"; GOTO 93000
  OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY"; GOTO 93000
  OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL"; GOTO 93000
  OPEN "","PREFIX" TO PREFIX ELSE ERRMSG = "PREFIX"; GOTO 93000
*
***** GET COMPANY NAME
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  IF CO.GLS = "Y" THEN
    OPEN "","COA" TO COA ELSE ERRMSG = "COA"; GOTO 93000
  END
  IN.ACCT.LEN = LEN(CO.ACCT.PIC)
  MATREAD GLTABLE.REC FROM CONTROL,CONO:"GLTABLE" ELSE MAT GLTABLE.REC = ""
*
***** MAIN PROCESSING
*
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME = "CLASS.MAINT"
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  ECD.ACTION = 2; CALL SCRN.EDIT
*
**** CLEAR SCREEN
*
100 MAT SCV.REC = ""
  ECD.ACTION = 6; CALL SCRN.EDIT
*
**** ENTER CLASS CODE
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 99999
  CLS.NO = ECD.RET.VALUE
  CLS.ID = CONO : CLS.NO
  * T26126 v
  MATREADU CLS.REC FROM CLASS, CLS.ID LOCKED
    ERRMSG = 'CLASS record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 100 
  END THEN
  * T26126 ^
    SCV.REC(2) = CLS.DESC
    SCV.REC(3) = CLS.GL.POST
    SCV.REC(4) = CLS.EQUIP
    SCV.REC(5) = CLS.ALW.ACCT CO.ACCT.MASK
    IF CLS.ALW.ACCT AND CO.GLS = "Y" THEN
      MATREAD COA.REC FROM COA, CONO:CLS.ALW.ACCT ELSE
        COA.DESC = CLS.ALW.ACCT CO.ACCT.MASK
      END
    END ELSE
      COA.DESC = CLS.ALW.ACCT CO.ACCT.MASK
    END
    SCV.REC(7) = COA.DESC
    SCV.REC(6) = CLS.EXP.ACCT CO.ACCT.MASK
    IF CLS.EXP.ACCT AND CO.GLS = "Y" THEN
      MATREAD COA.REC FROM COA, CONO:CLS.EXP.ACCT ELSE
        COA.DESC = CLS.EXP.ACCT CO.ACCT.MASK
      END
    END ELSE
      COA.DESC = CLS.EXP.ACCT CO.ACCT.MASK
    END
    SCV.REC(8) = COA.DESC
    ECD.ACTION = 3; CALL SCRN.EDIT
    OLD.NAME = CLS.DESC
  END ELSE
    MAT CLS.REC = ""; OLD.NAME = ""
    FOR OPTION = 1 TO 5 WHILE ECD.RET.VALUE # "END"
      ON OPTION GOSUB 1100,1200,1300,1400,1500
    NEXT OPTION
    IF ECD.RET.VALUE = "END" THEN
      RELEASE CLASS, CLS.ID
      GOTO 100
    END
  END
*
  MORE = 1
  LOOP
    ECD.NUM = 9; SCV.REC(ECD.NUM) = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
        RELEASE CLASS, CLS.ID
        MORE = 0
      CASE NUM(OPTION)
        IF OPTION > 0 AND OPTION < 6 THEN
          ON OPTION GOSUB 1100,1200,1300,1400,1500
        END
      CASE OPTION = "F"
        MATWRITE CLS.REC ON CLASS, CLS.ID
        CALL GEN.XREF.MAINT(CONO,CLS.NO,OLD.NAME,CLS.DESC,CLASS.XREF,PREFIX)
        MORE = 0
    END CASE
  WHILE MORE DO REPEAT
  GOTO 100
*
*** ENTER CLASS DESCRIPTION
*
1100 ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    CLS.DESC = ECD.RET.VALUE
  END
  RETURN
*
*** ENTER AUTO-POST G/L
*
1200 ECD.NUM = 3
  IF CO.GLS # "Y" THEN ECD.DEFAULT = "N"
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    CLS.GL.POST = ECD.RET.VALUE
  END
  RETURN
*
*** ENTER EQUIP RECEIPT
*
1300 ECD.NUM = 4
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    CLS.EQUIP = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER DEPRECIATION ALLOWANCE
*
1400 ECD.NUM = 5; SCV.REC(ECD.NUM) = ""
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN; ECD.PATRN = CO.ACCT.MATCH
  SCV.REC(ECD.NUM) = CLS.ALW.ACCT
  IF CLS.ALW.ACCT = "" THEN
    SCV.REC(ECD.NUM) = GLTB.DEPR.ALLOW
  END ELSE
    SCV.REC(ECD.NUM) = CLS.ALW.ACCT
  END
  IF CLS.GL.POST = "N" THEN ECD.O.R = "O"
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 1499
    CASE ECD.RET.VALUE AND CO.GLS = "Y"
      MATREAD COA.REC FROM COA, CONO : ECD.RET.VALUE ELSE
        ERRMSG = "Cannot locate account - " : ECD.RET.VALUE CO.ACCT.MASK
        GOSUB 91000; GOTO 1400
      END
    CASE 1
      COA.DESC = ECD.RET.VALUE CO.ACCT.MASK
  END CASE
  CLS.ALW.ACCT = ECD.RET.VALUE
  SCV.REC(ECD.NUM) = CLS.ALW.ACCT CO.ACCT.MASK
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.NUM = 7; SCV.REC(ECD.NUM) = COA.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
1499 RETURN
*
**** ENTER DEPRECIATION EXPENSE
*
1500 ECD.NUM = 6; SCV.REC(ECD.NUM) = ""
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN; ECD.PATRN = CO.ACCT.MATCH
  SCV.REC(ECD.NUM) = CLS.EXP.ACCT
  IF CLS.EXP.ACCT = "" THEN
    SCV.REC(ECD.NUM) = GLTB.DEPR.EXP
  END ELSE
    SCV.REC(ECD.NUM) = CLS.EXP.ACCT
  END
  IF CLS.GL.POST = "N" THEN ECD.O.R = "O"
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 1599
    CASE ECD.RET.VALUE AND CO.GLS = "Y"
      MATREAD COA.REC FROM COA, CONO : ECD.RET.VALUE ELSE
        ERRMSG = "Cannot locate account - " : ECD.RET.VALUE CO.ACCT.MASK
        GOSUB 91000; GOTO 1500
      END
    CASE 1
      COA.DESC = ECD.RET.VALUE CO.ACCT.MASK
  END CASE
  CLS.EXP.ACCT = ECD.RET.VALUE
  SCV.REC(ECD.NUM) = CLS.EXP.ACCT CO.ACCT.MASK
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.NUM = 8; SCV.REC(ECD.NUM) = COA.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
1599 RETURN
*
***** CALLS FOR SYSCOM
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT REPLY :
*       PRINT @(0,23) : CL :
*       RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000 ERRMSG = "Cannot locate ":ERRMSG:" file"
*       ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 END
