*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - FASBP
* PROGRAM       - ASSETS.LIST
* BY            - Ziad Yamout, VERCOM Software, Inc.
* DATE          - 02/01/89
* MODIFIED      - 11/06/95 TERRY NORTHCUTT TASK 19408 DIVISIONALIZATION
*
* TASK
*     18573 6/95 LLH 1-52 WEEK ACCOUNTING
*T23278 markt 12/11/1998 * Make fiscal data multi-date by division.
*
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>FISCAL
*COPY>FAS.CPYLIB>ASSETS
*COPY>FAS.CPYLIB>ASSETS.DEPR
*COPY>FAS.CPYLIB>DEP.MAST
*COPY>FAS.CPYLIB>CLASS
*COPY>FAS.CPYLIB>DISPOSAL
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
  DIM M.AST.REC(50)
  DIM YR.TOT(3,2)
  DIM G.TOT(3,2)
  DIM DSP.YRS(2)
  DIM FSC.YR(2)
  DIM FST.MN(2)
  DIM LST.MN(2)
  DIM D.REC(100,2)
  DIM P.REC(52)
*
*--- Setup SYSCOM
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- Get buffer
*
  PROCREAD BUFFER ELSE
      ERRMSG = "Must be executed from a PROC"
      GOSUB 91000; GOTO 99999
  END
  CONO = BUFFER<1>
  HD = BUFFER<2>
  DFLAG = BUFFER<5>
  DIV.OWNER = BUFFER<6> ; *---- 19408
*
*--- Open files
*
  OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "Cannot locate COMPANY file"; GOTO 93000
  END
  OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "Cannot locate CONTROL file"; GOTO 93000
  END
  OPEN "","ASSETS" TO ASSETS ELSE
      ERRMSG = "Cannot locate ASSETS file"; GOTO 93000
  END
  OPEN "ASSETS.DEPR" TO ASSETS.DEPR ELSE
      ERRMSG = "Cannot locate ASSETS.DEPR file"; GOTO 93000
  END
  OPEN "DISPOSAL" TO DISPOSAL ELSE
      ERRMSG = "Cannot locate DISPOSAL file"; GOTO 93000
  END
  OPEN "CLASS" TO CLASS ELSE
      ERRMSG = "Cannot locate CLASS file"; GOTO 93000
  END
  OPEN "DEP.MAST" TO DEP.MAST ELSE
      ERRMSG = "Cannot locate DEP.MAST file"; GOTO 93000
  END
  OPEN "DIVISION" TO DIVISION ELSE
      ERRMSG = "Cannot locate DIVISION file"; GOTO 93000
  END
  OPEN "DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "Cannot locate DEPARTMENT file"; GOTO 93000
  END
  OPEN "COST.CNTR" TO COST.CNTR ELSE
      ERRMSG = "Cannot locate COST.CNTR file"; GOTO 93000
  END
  OPEN "VEND" TO VEND ELSE
      ERRMSG = "Cannot locate VEND file"; GOTO 93000
  END
*
*--- Process all the selected records
*
  READNEXT AST.ID ELSE
      ERRMSG = "No items selected"
      GOSUB 91000; GOTO 99999
  END
*
*--- Initialization
*
  MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "Cannot locate company - " : CONO
      GOSUB 91000; GOTO 99999
  END
  MATREAD SALESDATES.REC FROM CONTROL, CONO:"SALESDATES" ELSE
      ERRMSG = "Need to setup sales dates table"
      GOSUB 91000; GOTO 99999
  END
  MATREAD FISCAL.REC FROM CONTROL, CONO:"FAFISCAL" ELSE
      MATREAD FISCAL.REC FROM CONTROL, CONO:"FAFISCALMON" ELSE
          ERRMSG = "Cannot locate Fixed Assets period"
          GOSUB 91000; GOTO 99999
      END
  END
  READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = ""; PERIOD.REC<1> = 12
  END
  NUM.PERIODS = PERIOD.REC<1>
  IN.ACCT.LEN = LEN(CO.ACCT.PIC)
  IF CO.GLS = "Y" THEN
      OPEN "COA" TO COA ELSE
          ERRMSG = "Cannot locate COA file"; GOTO 93000
      END
  END
***** T23278 v
  READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL - DIVISONS"; GOTO 93000
  END
  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL - DIV.SECURITY"; GOTO 93000
  END
  IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "D" THEN
      LOCATE DIV.OWNER IN DIVISION.REC<1>,1 SETTING POS ELSE
          ERRMSG = "Division ":DIV.OWNER:" not found on Control File DIVISIONS Record."
          GOTO 93000
      END
  END ELSE
      POS = 1
  END
***** T23278 ^
      G.DV = "00"; G.DP = "00"; G.CC = "000"
      SP = SPACE(1)
      B.T = "B"; B.T<2> = "T"
      HD0 = "DIVISION ":DIV.OWNER ; *---- TASK 19408
      HD1 = SPACE(22):"<------------- B O O K --------------> <-------------- T A X --------------->"
      HD2 = SPACE(23):"--------------":SPACE(25):"--------------"
      HD3 = "<------------------------- B O O K -------------------------> <-------------------------- T A X -------------------------->"
      IF DFLAG = "M" THEN
          HD4 = " Year Mn Period     Bonus       Depreciation  Total Depr Amt   Year Mn Period     Bonus       Depreciation  Total Depr Amt"
          HD5 = " ---- -- ------ -------------- -------------- --------------   ---- -- ------ -------------- -------------- --------------"
      END ELSE
          HD4 = " Year               Bonus       Depreciation  Total Depr Amt   Year               Bonus       Depreciation  Total Depr Amt"
          HD5 = " ----           -------------- -------------- --------------   ----           -------------- -------------- --------------"
      END
      HD6 = "                -------------- -------------- --------------                  -------------- -------------- --------------"
      PRINTER ON
      DATA=1
      LOOP
          MATREAD AST.REC FROM ASSETS, AST.ID ELSE GOTO 999
          AST.NUM = AST.ID[4,99]
          CUR.YR = ""; CUR.MN = ""
          MAT DSP.YRS = 0
          FOR BT = 1 TO 2
              BEGIN CASE
                  CASE AST.FST.PER<1,BT> < 1
                      CUR.YR<BT> = 0; CUR.MN<BT> = 0
*                  CASE FR.CURR.PER < AST.FST.PER<1,BT>;* T23278
                  CASE FR.CURR.PER<1,POS> < AST.FST.PER<1,BT>;* T23278
                      CUR.YR<BT> = 0; CUR.MN<BT> = 0
                  CASE 1
*                      CUR.YR<BT> = FR.CURR.PER[1,4] - AST.FST.PER<1,BT>[1,4];* T23278
                      CUR.YR<BT> = FR.CURR.PER<1,POS>[1,4] - AST.FST.PER<1,BT>[1,4];* T23278
*                      IF FR.CURR.PER[5,2] < AST.FST.PER<1,BT>[5,2] THEN;* T23278
                      IF FR.CURR.PER<1,POS>[5,2] < AST.FST.PER<1,BT>[5,2] THEN;* T23278
*                          CUR.MN<BT> = 53 - AST.FST.PER<1,BT>[5,2] + FR.CURR.PER[5,2];* T23278
                          CUR.MN<BT> = NUM.PERIODS+1 - AST.FST.PER<1,BT>[5,2] + FR.CURR.PER<1,POS>[5,2];* T23278
                      END ELSE
                          CUR.YR<BT> = CUR.YR<BT> + 1
*                          CUR.MN<BT> = FR.CURR.PER[5,2] - AST.FST.PER<1,BT>[5,2] + 1;* T23278
                          CUR.MN<BT> = FR.CURR.PER<1,POS>[5,2] - AST.FST.PER<1,BT>[5,2] + 1;* T23278
                      END
              END CASE
              FST.YR = AST.FST.PER<1,BT>[1,4] - 1
              BEGIN CASE
                  CASE AST.FST.PER<1,BT> < 1
                  CASE AST.FISCAL.MON > AST.LST.PER<1,BT>
                      DSP.YRS(BT) = AST.FISCAL.MON[1,4] - FST.YR
                  CASE AST.LST.PER<1,BT> > 0
                      DSP.YRS(BT) = AST.LST.PER<1,BT>[1,4] - FST.YR
              END CASE
              FOR I = 1 TO DSP.YRS(BT)
                  ADR.ID = AST.ID : B.T<BT> : FST.YR + I
                  READ D.REC(I,BT) FROM ASSETS.DEPR, ADR.ID ELSE
                      D.REC(I,BT) = ""
                  END
              NEXT I
          NEXT BT
          HEADING HD:" 'L'":HD0 ; *---- TASK 19408
          PRINT
          PRINT "Asset & Description  : " :
          PRINT AST.NUM "L#17" :"&   ": AST.DESC "L#30"
          IF AST.MASTER = AST.NUM THEN
              M.AST.DESC = AST.DESC
          END ELSE
              MAT M.AST.REC = MAT AST.REC
              MATREAD AST.REC FROM ASSETS, CONO:AST.MASTER ELSE
                  AST.DESC = "UNKNOWN"
              END
              M.AST.DESC = AST.DESC
              MAT AST.REC = MAT M.AST.REC
          END
          PRINT "Master & Description : " :
          PRINT AST.MASTER "L#17" :"&   ": M.AST.DESC "L#30"
          MATREAD CLS.REC FROM CLASS,CONO:AST.CLASS ELSE
              CLS.DESC = "UNKNOWN"
          END
          PRINT "Class & Desc         : " :
          PRINT AST.CLASS "L#17" :"&   ": CLS.DESC "L#30"
          IF AST.DIV = G.DV THEN
              DIV.DESC = "General Division"
              DEPT.DESC = "General Department"
              CCTR.DESC = "General Cost Center"
          END ELSE
              MATREAD DIV.REC FROM DIVISION,CONO:AST.DIV ELSE
                  DIV.DESC = "UNKNOWN"
              END
              IF AST.DEPT = G.DP THEN
                  DEPT.DESC = "General Department"
                  CCTR.DESC = "General Cost Center"
              END ELSE
                  MATREAD DEPT.REC FROM DEPARTMENT, CONO:AST.DEPT ELSE
                      DEPT.DESC = "General Department"
                  END
                  IF AST.CCTR = G.CC THEN
                      CCTR.DESC = "General Cost Center"
                  END ELSE
                      MATREAD CCTR.REC FROM COST.CNTR,CONO:AST.CCTR ELSE
                          CCTR.DESC = "UNKNOWN"
                      END
                  END
              END
          END
          PRINT "Division & Desc      : " :
          PRINT AST.DIV "L#17" :"&   ": DIV.DESC "L#30"
          PRINT "Department & Desc    : " :
          PRINT AST.DEPT "L#17" :"&   ": DEPT.DESC "L#30"
          PRINT "Cost Center & Desc   : " :
          PRINT AST.CCTR "L#17" :"&   ": CCTR.DESC "L#30"
          PRINT "Vendor Number & Name : " :
          PRINT AST.VEND<1,1> "L#17" :"&   ": AST.VEND<1,2> "L#30"
          PRINT "Serial Number        : " :
          PRINT AST.SERIAL
          PRINT "Location             : " :
          PRINT AST.LOC
          PRINT "New / Used           : " :
          IF AST.OLD.NEW = "N" THEN
              PRINT "NEW"
          END ELSE
              PRINT "USED"
          END
          PRINT "Acquire Date         : " :
          PRINT OCONV(AST.ACQUIRE.DATE,"D2/")
          PRINT "Asset Cost           : " :
          PRINT OCONV(AST.INSTALL.COST,"MD2,")
          PRINT "Post to GLS          : " :
          PRINT AST.AUTO.POST
          PRINT "Equipment Record     : " :
          PRINT AST.EQP.REC
          IF AST.DEPR.ALLOW AND CO.GLS = "Y" THEN
              MATREAD COA.REC FROM COA, CONO:AST.DEPR.ALLOW ELSE
                  COA.DESC = "UNKNOWN"
              END
          END ELSE
              COA.DESC = AST.DEPR.ALLOW CO.ACCT.MASK
          END
          PRINT "Allowance Acct & Desc: " :
          PRINT AST.DEPR.ALLOW CO.ACCT.MASK:"   &   ": COA.DESC "L#30"
          IF AST.DEPR.EXP AND CO.GLS = "Y" THEN
              MATREAD COA.REC FROM COA, CONO:AST.DEPR.EXP ELSE
                  COA.DESC = "UNKNOWN"
              END
          END ELSE
              COA.DESC = AST.DEPR.EXP CO.ACCT.MASK
          END
          PRINT "Expense Acct & Desc  : " :
          PRINT AST.DEPR.EXP CO.ACCT.MASK:"   &   ": COA.DESC "L#30"
          PRINT "Disposal Date        : " :
          PRINT OCONV(AST.DISP.DATE,"D2/")
          IF AST.DISPOSAL # "" THEN
              MATREAD DIS.REC FROM DISPOSAL, CONO:AST.DISPOSAL ELSE
                  DIS.DESC = "UNKNOWN"
              END
          END ELSE
              DIS.DESC = ""
          END
          PRINT "Disposal Code & Desc : " :
          PRINT AST.DISPOSAL "L#17" :"&   ": DIS.DESC "L#30"
          PRINT "Disposal Proceeds    : " :
          PRINT OCONV(AST.DISP.AMT,"MD2Z,")
          PRINT "Investment Credit    : " :
          PRINT AST.INVST.CRDT
          PRINT "Last Period & Date   : " :
          PRINT AST.FISCAL.MON "L#17" :"&   ":OCONV(AST.LAST.POST,"D2/")
          PRINT
          PRINT HD1
          PRINT
          PRINT "Depreciation Method  : " :
          PRINT AST.DEPR.METHOD<1,1> "L#6":SPACE(31):": ":
          PRINT AST.DEPR.METHOD<1,2>
          PRINT "Description          : " :
          MATREAD DEPR.REC FROM DEP.MAST,CONO:AST.DEPR.METHOD<1,1> ELSE
              DEPR.DESC = "UNKNOWN"
          END
          PRINT DEPR.DESC "L#30" : SPACE(7) : ": " :
          MATREAD DEPR.REC FROM DEP.MAST,CONO:AST.DEPR.METHOD<1,2> ELSE
              DEPR.DESC = "UNKNOWN"
          END
          PRINT DEPR.DESC "L#30"
          PRINT "Life years           : " :
          PRINT AST.YEARS<1,1>[1,2] + 0 "R#2":SPACE(9):"&   ":
          PRINT AST.YEARS<1,1>[3,2] + 0 "R#2":"   /   ":NUM.PERIODS"R#2":SPACE(11):
          PRINT ": ":AST.YEARS<1,2>[1,2] + 0 "R#2":SPACE(9):"&   ":
          PRINT AST.YEARS<1,2>[3,2] + 0 "R#2":"   /   ":NUM.PERIODS"R#2"
          PRINT "Life Units           : " :
          PRINT OCONV(AST.UNITS<1,1>,"MD0,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.UNITS<1,2>,"MD0,") "R#14"
          PRINT "Begin Depreciation   : " :
          PRINT OCONV(AST.DEPR.DATE<1,1>,"D2/") "L#8":SPACE(29):": ":
          PRINT OCONV(AST.DEPR.DATE<1,2>,"D2/") "L#8"
          PRINT "Fst & Lst Period     : " :
          PRINT AST.FST.PER<1,1> "R#6":"     &   ":AST.LST.PER<1,1> "R#6":
          PRINT SPACE(16):": ":
          PRINT AST.FST.PER<1,2> "R#6":"     &   ":AST.LST.PER<1,2> "R#6"
          PRINT "Curr Year & Month    : " :
          PRINT CUR.YR<1> "R#3":SPACE(8):"&   ":CUR.MN<1> "R#2":
          PRINT SPACE(20):": ":
          PRINT CUR.YR<2> "R#3":SPACE(8):"&   ":CUR.MN<2> "R#2"
          PRINT
          PRINT "Bonus Depreciation   : " :
          PRINT AST.BONUS.FLG<1,1> "L#1":SPACE(36):": ":
          PRINT AST.BONUS.FLG<1,2>
          PRINT "Yearly Bonus         : " :
          PRINT OCONV(AST.BONUS.AMT<1,1>,"MD2Z,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.BONUS.AMT<1,2>,"MD2Z,") "R#14"
          PRINT "Monthly / Yearly     : " :
          PRINT AST.BONUS.MON<1,1> "R#2":SPACE(35):": ":
          PRINT AST.BONUS.MON<1,2> "R#2"
          PRINT "Bonus Acquisition    : " :
          PRINT OCONV(AST.BONUS.DATE<1,1>,"D2/") "L#8":"   &   ":
          PRINT AST.BONUS.PER<1,1> "R#6":SPACE(16):": ":
          PRINT OCONV(AST.BONUS.DATE<1,2>,"D2/") "L#8":"   &   ":
          PRINT AST.BONUS.PER<1,2> "R#6"
          PRINT
          PRINT "YTD Depreciation     : " :
          PRINT OCONV(AST.YTD.DEPR<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.YTD.DEPR<1,2>,"MD2,") "R#14"
          PRINT "Opening Reserve      : " :
          PRINT OCONV(AST.CURR.DEPR<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.CURR.DEPR<1,2>,"MD2,") "R#14"
          PRINT "YTD Units            : " :
          PRINT OCONV(AST.YTD.UNITS<1,1>,"MD0,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.YTD.UNITS<1,2>,"MD0,") "R#14"
          PRINT "Cumulative Units     : " :
          PRINT OCONV(AST.CUM.UNITS<1,1>,"MD0,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.CUM.UNITS<1,2>,"MD0,") "R#14"
          PRINT "Cost per Unit        : " :
          PRINT OCONV(AST.UNIT.COST<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.UNIT.COST<1,2>,"MD2,") "R#14"
          PRINT "Salvage Value        : " :
          PRINT OCONV(AST.SALVAGE<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.SALVAGE<1,2>,"MD2,") "R#14"
          PRINT "Installation Cost    : " :
          PRINT OCONV(AST.INSTALL.AMT<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.INSTALL.AMT<1,2>,"MD2,") "R#14"
          PRINT "Cumulative Depr.     : " :
          PRINT OCONV(AST.CUM.DEPR<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.CUM.DEPR<1,2>,"MD2,") "R#14"
          PRINT "Cumulative Bonus Depr: " :
          PRINT OCONV(AST.BONUS.DEPR<1,1>,"MD2,") "R#14":SPACE(23):": ":
          PRINT OCONV(AST.BONUS.DEPR<1,2>,"MD2,") "R#14"
          PRINT HD2
          PRINT "Net Value            : " :
          NET.VALUE = AST.INSTALL.AMT<1,1>-AST.CUM.DEPR<1,1>-AST.BONUS.DEPR<1,1>
          PRINT OCONV(NET.VALUE,"MD2,") "R#14":SPACE(23):": ":
          NET.VALUE = AST.INSTALL.AMT<1,2>-AST.CUM.DEPR<1,2>-AST.BONUS.DEPR<1,2>
          PRINT OCONV(NET.VALUE,"MD2,") "R#14"
          BEGIN CASE
              CASE DSP.YRS(1) + DSP.YRS(2) < 1
                  GOTO 999
              CASE DFLAG = "Y"
              CASE DFLAG = "M"
              CASE 1
                  GOTO 999
          END CASE
          HEADING HD:" 'L'":HD0:" 'L'":HD3:" 'L'":HD4:" 'L'":HD5 ; *---- TASK 19408
          MAT G.TOT = ""; MAT YR.TOT = ""; MAT FSC.YR = ""
          BT = 1; PTR = 0; MAT P.REC = SPACE(60)
          MNO = 1
          IF DSP.YRS(BT) < MNO THEN GOTO 200
          FST.MN(BT) = AST.FST.PER<1,BT>[5,2]
          LST.MN(BT) = AST.LST.PER<1,BT>[5,2]
          FSC.YR(BT) = AST.FST.PER<1,BT>[1,4]
          FOR MN = FST.MN(BT) TO NUM.PERIODS
              GOSUB 1000
          NEXT MN
200*
          BT = 2; O.PTR = PTR; PTR = 1
          IF DSP.YRS(BT) < MNO THEN GOTO 300
          FST.MN(BT) = AST.FST.PER<1,BT>[5,2]
          LST.MN(BT) = AST.LST.PER<1,BT>[5,2]
          FSC.YR(BT) = AST.FST.PER<1,BT>[1,4]
          FOR MN = FST.MN(BT) TO NUM.PERIODS
              GOSUB 2000
          NEXT MN
300*
          FOR I = PTR TO O.PTR
              PRINT P.REC(I)
          NEXT I
          GOSUB 3000
          IF DFLAG = "M" THEN PRINT
          LIMIT = DSP.YRS(1) - 1
          FOR MNO = 2 TO LIMIT
              BT = 1; PTR = 0; MAT P.REC = SPACE(60)
              FSC.YR(BT) = FSC.YR(BT) + 1
              FOR MN = 1 TO NUM.PERIODS
                  GOSUB 1000
              NEXT MN
              BT = 2; O.PTR = PTR; PTR = 1
              BEGIN CASE
                  CASE DSP.YRS(BT) < MNO
                      GOTO 400
                  CASE DSP.YRS(BT) = MNO
                      LMT = LST.MN(BT)
                  CASE 1
                      LMT = NUM.PERIODS
              END CASE
              FSC.YR(BT) = FSC.YR(BT) + 1
              FOR MN = 1 TO LMT
                  GOSUB 2000
              NEXT MN
400*
              FOR I = PTR TO O.PTR
                  PRINT P.REC(I)
              NEXT I
              GOSUB 3000
              IF DFLAG = "M" THEN
                  PRINT
                  IF MOD(MNO,3) = 0 THEN
                      HEADING HD:" 'L'":HD0:" 'L'":HD3:" 'L'":HD4:" 'L'":HD5 ; *---- TASK 19408
                  END
              END
          NEXT MNO
          BT = 1; PTR = 0; MAT P.REC = SPACE(60)
          MNO = DSP.YRS(1)
          FSC.YR(BT) = FSC.YR(BT) + 1
          FOR MN = 1 TO LST.MN(BT)
              GOSUB 1000
          NEXT MN
          BT = 2; O.PTR = PTR; PTR = 1
          BEGIN CASE
              CASE DSP.YRS(BT) < MNO
                  FOR I = PTR TO O.PTR
                      PRINT P.REC(I)
                  NEXT I
                  GOTO 700
              CASE DSP.YRS(BT) = MNO
                  FSC.YR(BT) = FSC.YR(BT) + 1
                  FOR MN = 1 TO LST.MN(BT)
                      GOSUB 2000
                  NEXT MN
                  FOR I = PTR TO O.PTR
                      PRINT P.REC(I)
                  NEXT I
                  GOTO 700
              CASE 1
                  FSC.YR(BT) = FSC.YR(BT) + 1
                  FOR MN = 1 TO NUM.PERIODS
                      GOSUB 2000
                  NEXT MN
                  GOSUB 3000
                  IF DFLAG = "M" THEN
                      PRINT
                      IF MOD(MNO,3) = 0 THEN
                          HEADING HD:" 'L'":HD0:" 'L'":HD3:" 'L'":HD4:" 'L'":HD5  ; *---- TASK 19408
                      END
                  END
          END CASE
          DSP.YRS(1) = DSP.YRS(1) + 1
          LIMIT = DSP.YRS(2) - 1
          MAT P.REC = SPACE(60)
          FOR MNO = DSP.YRS(1) TO LIMIT
              BT = 2; PTR = 1
              FSC.YR(BT) = FSC.YR(BT) + 1
              FOR MN = 1 TO NUM.PERIODS
                  GOSUB 2000
              NEXT MN
              GOSUB 3000
              IF DFLAG = "M" THEN
                  PRINT
                  IF MOD(MNO,3) = 0 THEN
                      HEADING HD:" 'L'":HD0:" 'L'":HD3:" 'L'":HD4:" 'L'":HD5 ; *---- TASK 19408
                  END
              END
          NEXT MNO
          BT = 2; PTR = 1
          MNO = DSP.YRS(2)
          FSC.YR(BT) = FSC.YR(BT) + 1
          FOR MN = 1 TO LST.MN(BT)
              GOSUB 2000
          NEXT MN
700*
          GOSUB 3000
          PRINT HD6
          PRINT " Grand Total  : ":
          PRINT OCONV(G.TOT(1,1),"MD2,") "R#14":SP:
          PRINT OCONV(G.TOT(2,1),"MD2,") "R#14":SP:
          PRINT OCONV(G.TOT(3,1),"MD2,") "R#14":SPACE(18):
          PRINT OCONV(G.TOT(1,2),"MD2,") "R#14":SP:
          PRINT OCONV(G.TOT(2,2),"MD2,") "R#14":SP:
          PRINT OCONV(G.TOT(3,2),"MD2,") "R#14"
999*
          READNEXT AST.ID ELSE DATA=0
      WHILE DATA DO REPEAT
      PRINTER CLOSE
      PRINTER OFF
      GOTO 99999
*
*--- Print book year depreciation
1000*
      YR.TOT(1,BT) = YR.TOT(1,BT) + D.REC(MNO,BT)<MN+52>
      YR.TOT(2,BT) = YR.TOT(2,BT) + D.REC(MNO,BT)<MN>
      MN.TOT = D.REC(MNO,BT)<MN+52> + D.REC(MNO,BT)<MN>
      YR.TOT(3,BT) = YR.TOT(3,BT) + MN.TOT
      IF DFLAG = "Y" THEN GOTO 1099
      IF MN < FST.MN(BT) THEN
          AGE.YR = MNO - 1
          AGE.MN = NUM.PERIODS+1 - FST.MN(BT) + MN
      END ELSE
          AGE.YR = MNO
          AGE.MN = MN - FST.MN(BT) + 1
      END
      PTR = PTR + 1
      P.REC(PTR) = SP:AGE.YR "R#4":SP: AGE.MN "R#2"
      P.REC(PTR) = P.REC(PTR):SP:(FSC.YR(BT):(MN "R%2")) "R#6":SP
      P.REC(PTR) = P.REC(PTR):OCONV(D.REC(MNO,BT)<MN+52>,"MD2Z,") "R#14":SP
      P.REC(PTR) = P.REC(PTR):OCONV(D.REC(MNO,BT)<MN>,"MD2Z,") "R#14":SP
      P.REC(PTR) = P.REC(PTR):OCONV(MN.TOT,"MD2Z,") "R#14"
1099*
      RETURN
*
*--- Print tax year depreciation
2000*
      YR.TOT(1,2) = YR.TOT(1,2) + D.REC(MNO,BT)<MN+52>
      YR.TOT(2,2) = YR.TOT(2,2) + D.REC(MNO,BT)<MN>
      MN.TOT = D.REC(MNO,BT)<MN+52> + D.REC(MNO,BT)<MN>
      YR.TOT(3,2) = YR.TOT(3,2) + MN.TOT
      IF DFLAG = "Y" THEN GOTO 2099
      IF MN < FST.MN(BT) THEN
          AGE.YR = MNO - 1
          AGE.MN = NUM.PERIODS+1 - FST.MN(BT) + MN
      END ELSE
          AGE.YR = MNO
          AGE.MN = MN - FST.MN(BT) + 1
      END
      PRINT P.REC(PTR):SPACE(3):
      PTR = PTR + 1
      PRINT AGE.YR "R#4":SP: AGE.MN "R#2":
      PRINT SP:(FSC.YR(BT):(MN "R%2")) "R#6":SP:
      PRINT OCONV(D.REC(MNO,BT)<MN+52>,"MD2Z,") "R#14":SP:
      PRINT OCONV(D.REC(MNO,BT)<MN>,"MD2Z,") "R#14":SP:
      PRINT OCONV(MN.TOT,"MD2Z,") "R#14"
2099*
      RETURN
*
*--- Print book & tax year totals
3000*
      IF DFLAG = "M" THEN PRINT HD6
      BEGIN CASE
          CASE MNO > DSP.YRS(1)
              PRINT SPACE(16) :
          CASE DFLAG = "M"
              PRINT SP:FSC.YR(1) "R#4":" Total   : ":
          CASE 1
              PRINT SP:FSC.YR(1) "R#4":SPACE(11):
      END CASE
      PRINT OCONV(YR.TOT(1,1),"MD2Z,") "R#14":SP:
      PRINT OCONV(YR.TOT(2,1),"MD2Z,") "R#14":SP:
      PRINT OCONV(YR.TOT(3,1),"MD2Z,") "R#14":SPACE(3):
      BEGIN CASE
          CASE MNO > DSP.YRS(2)
              PRINT SPACE(15) :
          CASE DFLAG = "M"
              PRINT FSC.YR(2) "R#4":" Total   : ":
          CASE 1
              PRINT FSC.YR(2) "R#4":SPACE(11):
      END CASE
      PRINT OCONV(YR.TOT(1,2),"MD2Z,") "R#14":SP:
      PRINT OCONV(YR.TOT(2,2),"MD2Z,") "R#14":SP:
      PRINT OCONV(YR.TOT(3,2),"MD2Z,") "R#14"
      FOR I = 1 TO 2
          FOR J = 1 TO 3
              G.TOT(J,I) = G.TOT(J,I) + YR.TOT(J,I)
          NEXT J
      NEXT I
      MAT YR.TOT = ""
      RETURN
*--- Error message routine
*
91000*
      PRINT @(0,23) : ERRMSG : CL :
      INPUT REPLY :
      PRINT @(0,23) : CL :
      RETURN
93000*
      ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
  END
