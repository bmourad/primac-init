*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - FASBP
* PROGRAM     - FAS.YE.CLOSE
* BY          - WALID YAMOUT , C.B.A
* DATE        - 08/13/85
* DESCRIPTION -
* MOD BY LMR 10/28/93 FOR FIXED ASSETS MODULE.
*T21177 diane 01/27/1997 * REV11 UPG ADD CLOSE
*********************************************************************
*
*** INSERT FILE EQUATES   
*COPY>PMC.CPYLIB>COMPANY   
*COPY>PMC.CPYLIB>FISCAL
*COPY>FAS.CPYLIB>ASSETS
*COPY>CPYLIB>EDIT.COM.DRIVER   
*COPY>CPYLIB>FILE.VARS   
*COPY>CPYLIB>CHAR   
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
**** OPEN FILES   
OPEN '','COMPANY' TO COMPANY ELSE
  ERRMSG = 'CANNOT LOCATE COMPANY FILE'; GOTO 93000
END
OPEN '','FAS.SCREENS' TO M.SCREENS ELSE
  ERRMSG = 'CANNOT LOCATE FAS.SCREENS FILE'; GOTO 93000
END
OPEN '','PREFIX' TO PREFIX ELSE
  ERRMSG = 'CANNOT LOCATE PREFIX FILE'; GOTO 93000
END
OPEN '','CONTROL' TO CONTROL ELSE
  ERRMSG = 'CANNOT LOCATE CONTROL FILE'; GOTO 93000
END
OPEN '','ASSETS' TO ASSETS ELSE
  ERRMSG = 'CANNOT LOCATE ASSETS FILE'; GOTO 93000
END
*** GET COMPANY NAME   
MAT COMP.REC = ''
CONO = ""
CALL GET.CONO(CONO,MAT COMP.REC)
IF CONO = 'END' THEN GOTO 99999
READ SALESDATES FROM CONTROL , CONO : "SALESDATES" ELSE
  ERRMSG = "CANNOT LOCATE CONTROL, SALESDATES"; GOTO 93000
END
READU OPENDATES FROM CONTROL , CONO : "OPENDATES" ELSE
  ERRMSG = "CANNOT LOCATE CONTROL, OPENDATES"; GOTO 93000
END
*
*--- MAIN PROCESS
*
MATREADU FISCAL.REC FROM CONTROL, CONO : "FAFISCALMON" ELSE
  ERRMSG = "YOU NEED TO CLOSE LAST FISCAL PERIOD BEFORE CLOSING THE YEAR"
  GOSUB 91000
  GOTO 99999
*         GOTO 93000
END
IF FR.CURR.PER[5,2] <> 01 THEN
  ERRMSG = 'STARTING PERIOD FOR A NEW YEAR SHOULD BE 01'
  GOSUB 91000
  GOTO 99999
*         GOTO 93000
END
*** PRINT SCREEN   
TODAY = DATE()
MAT EDIT.COM.DRIVER = ''   
ECD.SCRN.CNT = 1   
ECD.SCRN.NAME<1> = "FAS.YE.CLOSE"
ECD.ACTION=1;CALL SCRN.EDIT   
ECD.SCRN.NO = 1
MAT SCV.REC = ''
ECD.ACTION = 2; CALL SCRN.EDIT
SCV.REC(1)<1> = FR.CURR.PER[5,2]
SCV.REC(2)<1> = SALESDATES<2,2>
SCV.REC(3)<1> = OPENDATES<2> - 1
ECD.SUB.SUM = 1; ECD.ACTION = 3; CALL SCRN.EDIT
*** ENTER OPTIONS   
MORE = 1
LOOP
  ECD.NUM = 8; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
  ECD.ACTION=4;CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = 'END' OR OPTION = 'E'
      MORE = 0
    CASE OPTION = 'C'
      MORE = 0
      P_X  = 3 ; P_Y = 23 ; P_VALUE = 'NOW UPDATING ASSETS FILE' ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      SELECT ASSETS
      DATA = 1
      LOOP
        READNEXT AST.ID ELSE DATA = 0
      WHILE DATA DO
        IF AST.ID[1,3] # CONO THEN GOTO 8199
        MATREADU AST.REC FROM ASSETS, AST.ID ELSE
          RELEASE ASSETS, AST.ID
          GOTO 8199
        END
        FOR BT = 1 TO 2
          AST.CURR.DEPR<1,BT> = AST.CURR.DEPR<1,BT> + AST.YTD.DEPR<1,BT>
          AST.YTD.DEPR<1,BT> = 0
          AST.YTD.UNITS<1,BT> = 0
          IF AST.BONUS.FLG<1,BT> = "Y" THEN
            AST.BONUS.FLG<1,BT> = "D"
          END
        NEXT BT
        IF AST.INVST.CRDT = "Y" THEN
          AST.INVST.CRDT = "T"
        END
        MATWRITE AST.REC ON ASSETS, AST.ID
8199  REPEAT
      FR.CLOSE.DATE = TODAY
      MATWRITE FISCAL.REC ON CONTROL, CONO : "FAFISCAL"
      DELETE CONTROL, CONO : "FAFISCALMON"
  END CASE
WHILE MORE DO REPEAT
ECD.ACTION = 99 ; CALL SCRN.EDIT
GOTO 99999
*
*--- ERROR ROUTINE
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
*      PRINT @(0,23) : CL : ERRMSG :
*      INPUT ERRMSG,1 :
*      PRINT @(0,23) : CL :
*      RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000*
*      PRINT @(0,23) : CL : ERRMSG :
*      INPUT ERRMSG,1 :
99999*
RELEASE
END
