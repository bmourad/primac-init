*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - FASBP
* PROGRAM       - DEP.MAST.LIST
* BY            - Ziad Yamout, VERCOM Software, Inc.
* DATE          - 02/01/89
*
* T18573 7/95 LLH 1-52 ACCOUNTING
*
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SALESDATES
*COPY>FAS.CPYLIB>DEP.MAST
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
   DIM P.REC(99)
*
*--- Setup SYSCOM
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*--- Get buffer
*
   PROCREAD BUFFER ELSE
      ERRMSG = "Must be executed from a PROC"
      GOSUB 91000; GOTO 99999
   END
   CONO = BUFFER<1>
   HD = BUFFER<2>
*
*--- Open files
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "Cannot locate COMPANY file"; GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "Cannot locate CONTROL file"; GOTO 93000
   END
   OPEN "","DEP.MAST" TO DEP.MAST ELSE
      ERRMSG = "Cannot locate DEP.MAST file"; GOTO 93000
   END
*--- Initialization
*
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "Invaild Company"
      GOSUB 91000; GOTO 99999
   END
   MATREAD SALESDATES.REC FROM CONTROL, CONO:"SALESDATES" ELSE
      ERRMSG = "Need to setup sales dates table"
      GOSUB 91000; GOTO 99999
   END
   READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = ""; PERIOD.REC<1> = 12
   END
   NUM.PERIODS = PERIOD.REC<1>
   PAGE.CNT = 0
*
*--- Headings
*
   HEAD = ""
   HD1 = ""
   HD2 = ""
   REPORT.NUMBER = BUFFER<2>
   CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,REPORT.NUMBER,"",HD1,HD2)
   PRINTER ON
   DATA=1
   LOOP
   WHILE DATA = 1 DO
      READNEXT DEPR.ID ELSE DATA = 0
      MATREAD DEPR.REC FROM DEP.MAST, DEPR.ID ELSE GOTO 999
      BEGIN CASE
         CASE DEPR.MAX.YRS < 1
            MAX.YRS = 0
         CASE DEPR.YR.TYPE = "F"
            IF DEPR.ACQ.FYR = "Y" THEN
               MAX.YRS = DEPR.MAX.YRS+INT(DEPR.MAX.PER/12+.99)
            END ELSE
               MAX.YRS = DEPR.MAX.YRS+INT(DEPR.MAX.PER/12+.99)+1
            END
         CASE 1
            MAX.YRS = DEPR.MAX.YRS+INT(DEPR.MAX.PER/12+.99)
      END CASE
      IF MAX.YRS THEN
         READ YR.PCT FROM DEP.MAST, DEPR.ID:"!TAB" ELSE
            MAX.YRS = 0
         END
      END
      MAT P.REC = ""
      FOR I = 1 TO NUM.PERIODS
         P.REC(I) = I"R#6":SPACE(1):SALESDATES.REC(I+1)<1,2>"R#6":SPACE(1):DEPR.ACQ.PER<1,I>"R#7"
         P.REC(I) = P.REC(I):SPACE(12):I"R#3":SPACE(1):SALESDATES.REC(I+1)<1,2>"R#6":SPACE(1):DEPR.SVC.PER<1,I>"R#7"
         P.REC(I) = P.REC(I):SPACE(12):I"R#3":SPACE(1):SALESDATES.REC(I+1)<1,2>"R#6":SPACE(1):DEPR.DSP.PER<1,I>"R#7"
         IF I <= MAX.YRS THEN
            P.REC(I) = P.REC(I) : SPACE(9):I"R#2":SPACE(1)
            P.REC(I) = P.REC(I) : OCONV(YR.PCT<I,1>,"MD2Z") "R#7"
            P.REC(I) = P.REC(I) : OCONV(YR.PCT<I,2>,"MD2,Z") "R#13"
         END
      NEXT I
      GOSUB 1000; * print headers
      FOR I = 1 TO NUM.PERIODS
         IF LINE.CNT > 60 THEN GOSUB 1000
         PRINT P.REC(I)
         LINE.CNT = LINE.CNT + 1
      NEXT I
999*
   REPEAT
   PRINTER CLOSE
   PRINTER OFF
   GOTO 99999
*
1000 *
*
   LINE.CNT = 0
   PAGE.CNT = PAGE.CNT + 1
   HD3 = "Method/ Desc""L#30":":":DEPR.ID[4,99]"L#6":" &":DEPR.DESC"L#30"
   HD4 = "Max # Years""L#30":":":DEPR.MAX.YRS"R#2":" / ":DEPR.MAX.PER"R#2"
   HD5 = "Depr Year Type""L#30":":":DEPR.YR.TYPE"L#1"
   HD6 = "Acquisition Month""L#30":":"
   BEGIN CASE
      CASE NOT(NUM(DEPR.ACQ.MON))
         IF DEPR.ACQ.MON[2,2] > 0 THEN
            HD6 = HD6: (DEPR.ACQ.MON[2,2]+0) "R#2":" % of the month"
         END ELSE
            HD6 = HD6: DEPR.ACQ.MON "L#1":" of service days"
         END
      CASE DEPR.ACQ.MON >= 0
         HD6 = HD6: "Full month if days >= ":DEPR.ACQ.MON "R#2"
      CASE 1
         HD6 = HD6: "Full month if days < ":(0-DEPR.ACQ.MON) "R#2"
   END CASE
   HD7 = "Disposal Month""L#30":":"
   BEGIN CASE
      CASE NOT(NUM(DEPR.DSP.MON))
         IF DEPR.DSP.MON[2,2] > 0 THEN
            HD7 = HD7: (DEPR.DSP.MON[2,2]+0) "R#2":" % of the month"
         END ELSE
            HD7 = HD7: DEPR.DSP.MON "L#1":" of service days"
         END
      CASE DEPR.DSP.MON >= 0
         HD7 = HD7: "Full month if days >= ":DEPR.DSP.MON "R#2"
      CASE 1
         HD7 = HD7: "Full month if days < ":(0-DEPR.DSP.MON) "R#2"
   END CASE
   HD8 = "Main Equation ""L#30":":":DEPR.EQU.1 "L#75"
   HD9 = "Total Criteria""L#30":":":OCONV(DEPR.TOT,"MD2") "R#6"
   HD10 = "Switch Equation""L#30":":":DEPR.ALT"L#3"
*
   PRINT CHAR(12)
   PRINT HD1:PAGE.CNT"R#2"
   PRINT HD2
   PRINT
   PRINT HD3
   PRINT HD4
   PRINT HD5
   PRINT HD6
   PRINT HD7
   PRINT HD8
   IF DEPR.EQU.2 # "" THEN
      PRINT SPACE(30):":":DEPR.EQU.2 "L#75"
      LINE.CNT = LINE.CNT + 1
   END
   IF DEPR.EQU.3 # "" THEN 
      PRINT SPACE(30):":":DEPR.EQU.3 "L#75"
      LINE.CNT = LINE.CNT + 1
   END
   PRINT HD9
   IF DEPR.TOT.2 # "" THEN
      PRINT SPACE(30):":":DEPR.TOT.2 "L#75"
      LINE.CNT = LINE.CNT + 1
   END
   IF DEPR.TOT.3 # "" THEN 
      PRINT SPACE(30):":":DEPR.TOT.3 "L#75"
      LINE.CNT = LINE.CNT + 1
   END
   PRINT HD10
   IF DEPR.ALT.2 # "" THEN
      PRINT SPACE(30):":":DEPR.ALT.2 "L#75"
      LINE.CNT = LINE.CNT + 1
   END
   IF DEPR.ALT.3 # "" THEN 
      PRINT SPACE(30):":":DEPR.ALT.3 "L#75"
      LINE.CNT = LINE.CNT + 1
   END
   PRINT "Acquisition Year""C#25":SPACE(5):"Service Year""C#25":SPACE(5):"Disposal Year""C#25"
   STR1 = "# of Prd ":DEPR.ACQ.MON"R#2":" Full Year ":DEPR.ACQ.FYR
   STR2 = "# of Prd ":NUM.PERIODS"R#2":" Full Year ":"Y"
   STR3 = "# of Prd ":DEPR.DSP.MON"R#2":" Full Year ":DEPR.DSP.FYR
   PRINT STR1"C#25":SPACE(5):STR2"C#25":SPACE(5):STR3"C#25"
   PRINT "Period Mon Include""C#25":SPACE(5):"Period Mon Include""C#25":SPACE(5):"Period Mon Include""C#25":SPACE(5):"Yr Percent Not to Exceed"
   LINE.CNT = LINE.CNT + 15
   RETURN
*
*--- Error message routine
*
91000*
   PRINT @(0,23) : ERRMSG : CL :
   INPUT REPLY :
   PRINT @(0,23) : CL :
   RETURN
93000*
   ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
