  SUBROUTINE DEP.MAST.SUB
*COPY>CPYLIB>COM1
*COPY>FAS.CPYLIB>COM.DEP.MAST
********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - FASBP
* PROGRAM       - DEP.MAST.SUB
* BY            - ZIAD YAMOUT, Vercom software, Inc.
* DATE          - 11/21/88
* DESCRIPTION   - This program maintains the yearly depreciation
*                 percentage, and dollar limit.
*T25978 adelgado 02/13/2002 * Add the use of prompts (S,SR,SB,ST).
********************************************************************
*
*--- Data base libraries
*COPY>FAS.CPYLIB>DEP.MAST
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- Initialization
  PAGE.SIZE = 14
  BEGIN.PAGE = 6
  LINE.SPACE = 1
*
*--- First time prompt for percentages
  OLD.START.YR = 0
  IF YR.PCT = "" THEN
    YR = 1; YRS.CNT = 0
    OPTION = "A"
    LOOP
      YR = YRS.CNT + 1
      OLD.YRS.CNT = YRS.CNT
      GOSUB 1900
      GOSUB 1000
    WHILE YRS.CNT > OLD.YRS.CNT AND YRS.CNT < MAX.YRS DO REPEAT
    YR = YRS.CNT
  END ELSE
    YRS.CNT = DCOUNT(YR.PCT,AM)
    YR = 1
  END
  GOSUB 1900
*
*--- Prompt line routine
  MORE = 1
  LOOP
    ECD.NUM = 3; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "E" OR OPTION = "END" OR OPTION = ""
        TOT = 0
        FOR I = 1 TO YRS.CNT
          TOT = TOT + YR.PCT<I,1>
        NEXT I
        BEGIN CASE
          CASE YRS.CNT < MAX.YRS
            ERRMSG = "The last ":MAX.YRS-YRS.CNT:" years are not updated with percentages" 
            GOSUB 91000
            ECD.NUM = 4; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE # "Y" THEN
* P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
* CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            END ELSE
              MORE = 0
            END
          CASE TOT <> 10000
            ERRMSG = "NOTE:total percent does not add up to a 100" ; P_OPT = "CL"
            GOSUB 91000
            ECD.NUM = 4; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE # "Y" THEN
* P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
* CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            END ELSE
              MORE = 0
            END
          CASE 1
            MORE = 0
        END CASE
      CASE OPTION = "A" AND YRS.CNT < MAX.YRS
        LOOP
          YR = YRS.CNT + 1
          OLD.YRS.CNT = YRS.CNT
          GOSUB 1900
          GOSUB 1000
        WHILE YRS.CNT > OLD.YRS.CNT AND YRS.CNT < MAX.YRS DO REPEAT
        YR = YRS.CNT
        GOSUB 1900
      CASE OPTION = "A"
        ERRMSG = MAX.YRS : " is the maximum number of years"
        GOSUB 91000
      CASE OPTION = "S"
        YR = 1 + INT((YR-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
        IF YR > YRS.CNT THEN YR = 1
        GOSUB 1900
      * T25978 v
      CASE OPTION = 'SR'
        YR = 1 + INT((YR-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
        IF YR < 1 THEN YR = 1
        GOSUB 1900
      CASE OPTION = 'ST'
        YR = 1
        GOSUB 1900
      CASE OPTION = 'SB'
        YR = YRS.CNT
        GOSUB 1900
      * T25978 ^
      CASE OPTION = "CT"
        YR.PCT = ""
        YR = 1; YRS.CNT = 0
        OLD.START.YR = 0
        GOSUB 1900
      CASE OPTION = "CM"
        FOR I = 1 TO YRS.CNT
          YR.PCT<I> = YR.PCT<I,1>
        NEXT I
        OLD.START.YR = 0
        GOSUB 1900
      CASE NOT(NUM(OPTION))
        ERRMSG = "Invalid entry, please re-enter"
        GOSUB 91000
      CASE YRS.CNT < 1 OR OPTION < OLD.START.YR OR OPTION > LAST.YR
        ERRMSG = "*** Out of range ***"
        GOSUB 91000
      CASE 1
        YR = OPTION
        GOSUB 1000
        GOSUB 1900
    END CASE
  WHILE MORE DO REPEAT
  GOTO 99999
*
*--- Prompt for yearly percent & limits
1000 SLN = BEGIN.PAGE + LINE.SPACE * MOD(YR-1,PAGE.SIZE)
  P_X  = 1 ; P_Y = SLN ; P_VALUE = YR "R%2" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
1010 X = 6; Y = SLN; TYP = 4; SCALER = 2
  MAXL = 6; MINV = 0; MAXV = 10000
  HMSG = "Enter the depreciation percent for year # (":YR:")"
  IF YR.PCT<YR,1> # "" THEN
    O.R = "O"; DEFAULT = OCONV(YR.PCT<YR,1>,"MD2")
  END
  CALL EDIT.SUB
  BEGIN CASE
    CASE (VALUE = "" OR VALUE = "END") AND OPTION = "A"
      YR.PCT = DELETE(YR.PCT,YR,0,0)
      P_X  = 1 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 1099
    CASE VALUE = "END"
      P_X  = 1 ; P_Y = SLN ; P_VALUE = YR "R%2" ; P_OPT = "CL"
      P_X  := AM:6 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(YR.PCT<YR,1>,"MD2") "R#6"
      P_X  := AM:15 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(YR.PCT<YR,2>,"MD2") "R#11"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 1099
  END CASE
  YR.PCT<YR,1> = VALUE
1020 X = 15; Y = SLN; TYP = 4; SCALER = 2; O.R = "O"
  MAXL = 11; MINV = 0; MAXV = 9999999999
  HMSG = "Enter maximum depreciation for year # (":YR:"), or <RETURN>"
  IF YR.PCT<YR,2> # "" THEN
    DEFAULT = OCONV(YR.PCT<YR,2>,"MD2")
  END
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END"
      GOTO 1010
    CASE VALUE = ""
      YR.PCT<YR> = YR.PCT<YR,1>
    CASE 1
      YR.PCT<YR,2> = VALUE
  END CASE
  YRS.CNT = DCOUNT(YR.PCT,AM)
1099 RETURN
*
*--- Display scrolling Yearly percentages
1900 START.YR = 1 + INT((YR-1)/PAGE.SIZE)*PAGE.SIZE
  LAST.YR = START.YR + PAGE.SIZE - 1
  IF LAST.YR > YRS.CNT THEN LAST.YR = YRS.CNT
  IF START.YR = OLD.START.YR THEN GOTO 1990
  OLD.START.YR = START.YR
  CNT = 1
  FOR N = START.YR TO LAST.YR
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 1 ; P_Y = SLN ; P_VALUE = N "R%2" ; P_OPT = "CL"
    P_X  := AM:6 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(YR.PCT<N,1>,"MD2") "R#6"
    P_X  := AM:15 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(YR.PCT<N,2>,"MD2") "R#11"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
1990 RETURN
*
*--- Error message routines
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT XX :
*       PRINT @(0,23) : CL :
*       RETURN
99999 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
  RETURN
END
