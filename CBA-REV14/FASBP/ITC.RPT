*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - FASBP
* PROGRAM       - ITC.RPT
* BY            - Ziad Yamout, VERCOM Software, Inc.
* DATE          - 02/14/89
*********************************************************************
*COPY>FAS.CPYLIB>ASSETS
*COPY>FAS.CPYLIB>ITC
*COPY>FAS.CPYLIB>CLASS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- Setup SYSCOM
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*--- Get buffer
*
      PROCREAD BUFFER ELSE
         ERRMSG = "Must be executed from a PROC"
         GOSUB 91000; GOTO 99999
      END
      CONO = BUFFER<1>
      HD = BUFFER<2>
      TOT.HD = BUFFER<3>
*
*--- Open files
*
      OPEN "","ASSETS" TO ASSETS ELSE
         ERRMSG = "Cannot locate ASSETS file"; GOTO 93000
      END
      OPEN "ITC" TO ITC ELSE
         ERRMSG = "Cannot locate ITC file"; GOTO 93000
      END
      OPEN "CLASS" TO CLASS ELSE
         ERRMSG = "Cannot locate CLASS file"; GOTO 93000
      END
*
*--- Process all the selected records
*
      READNEXT AST.ID ELSE
         ERRMSG = "No items selected"
         GOSUB 91000; GOTO 99999
      END
*
*--- Initialization
*
      ITC.ID = CONO : "ITC"
      MATREAD ITC.REC FROM ITC, ITC.ID ELSE
         ERRMSG = "Investment Tax Credit table is not setup"
         GOSUB 91000; GOTO 99999
      END
      MATREAD AST.REC FROM ASSETS, AST.ID ELSE
         ERRMSG = "Cannot locate ASSET # ":AST.ID[4,99]
         GOSUB 91000; GOTO 99999
      END
      IF CONO # AST.ID[1,3] THEN
         ERRMSG = "Company selection mismatch"
         GOSUB 91000; GOTO 99999
      END
*
      OLD.YRS = AST.YEARS<1,2>[1,2] + 0
      IF OLD.YRS < ITC.REC(1) THEN
         TAX.PCT = 0
      END ELSE
         PTR = 19
         FOR I = 2 TO 19 WHILE PTR = 19
            IF OLD.YRS > ITC.REC(I) THEN PTR = I - 1
         NEXT I
         TAX.PCT = ITC.REC(PTR + 19)
      END
      YR.TOT = 0
      G.TOT = 0
*
      SP = SPACE(1)
      HD1 = "ASSET #       ASSET DESCRIPTION        DV DEPT  CTR CLASS      CLASS DESCRIPTION     INSTALL AMOUNT YRS INV TAX CREDIT"
      HD2 = "------- ------------------------------ -- ----- --- ------ ------------------------- -------------- --- --------------"
      HD3 = SPACE(104):"--------------"
      PRINTER ON
      HEADING HD:" 'LL'":HD1:" 'L'":HD2:" 'L'"
      LOOP
         MATREAD AST.REC FROM ASSETS, AST.ID ELSE GOTO 999
         YRS = AST.YEARS<1,2>[1,2] + 0
         IF YRS <> OLD.YRS THEN
            GOSUB 1000
            PRINT
         END
         PLINE = AST.ID[4,99] "L#7" : SP : AST.DESC "L#30" : SP
         PLINE = PLINE : AST.DIV "L#2" : SP : AST.DEPT "L#5" : SP
         PLINE = PLINE : AST.CCTR "L#3" : SP : AST.CLASS "L#5" : SP
         MATREAD CLS.REC FROM CLASS, CONO : AST.CLASS ELSE
            CLS.DESC = "UNKNOWN"
         END
         PLINE = PLINE : CLS.DESC "L#25" : SP
         PLINE = PLINE : OCONV(AST.INSTALL.AMT<1,2>,"MD2,") "R#14"
         PLINE = PLINE : SP : YRS "R#3" : SP
         TAX.AMT = INT(AST.INSTALL.AMT<1,2> / 100 * TAX.PCT / 100 + .5)
         PLINE = PLINE : OCONV(TAX.AMT,"MD2,") "R#14"
         PRINT PLINE
         YR.TOT = YR.TOT + TAX.AMT
999*
      WHILE READNEXT AST.ID DO REPEAT
      GOSUB 1000
      PRINT HD3
      PRINT SPACE(103-LEN(TOT.HD)):TOT.HD:SP:OCONV(G.TOT,"MD2,") "R#14"
      PRINTER CLOSE
      PRINTER OFF
      GOTO 99999
*
*--- Print tax year totals
1000*
      PRINT HD3
      PRINT SPACE(104) : OCONV(YR.TOT,"MD2,") "R#14"
      G.TOT = G.TOT + YR.TOT
      OLD.YRS = YRS
      IF YRS < ITC.REC(1) THEN
         TAX.PCT = 0
      END ELSE
         PTR = 19
         FOR I = 2 TO 19 WHILE PTR = 19
            IF YRS > ITC.REC(I) THEN PTR = I - 1
         NEXT I
         TAX.PCT = ITC.REC(PTR + 19)
      END
      YR.TOT = 0
      RETURN
*--- Error message routine
*
91000*
      PRINT @(0,23) : ERRMSG : CL :
      INPUT REPLY :
      PRINT @(0,23) : CL :
      RETURN
93000*
      ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
   END
