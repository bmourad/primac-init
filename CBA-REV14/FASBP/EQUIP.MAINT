*COPY>CPYLIB>COM1
*COPY>FAS.CPYLIB>COM.EQUIP
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - FASBP
* PROGRAM     - EQUIP.MAINT
* BY          - Ziad Yamout, Vercom Software, Inc.
* DATE        - 10/20/88
* DESCRIPTION - Equipment Maintenance Program
*T23278 markt 12/11/1998 * Add check for divisional security.
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*--- Data structure libraries
*
*COPY>FAS.CPYLIB>FAS.FILE.VARS
*COPY>FAS.CPYLIB>ASSETS
*COPY>FAS.CPYLIB>EQUIP.FILE
*COPY>FAS.CPYLIB>REPAIRS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FISCAL
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- Setup SYSCOM subroutine
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- Open files
*
  OPEN "FAS.SCREENS" TO M.SCREENS ELSE ERRMSG = "FAS.SCREENS"; GOTO 93000
  OPEN "ASSETS" TO ASSETS ELSE ERRMSG = "ASSETS"; GOTO 93000
  OPEN "ASSETS.DEPR" TO ASSETS.DEPR ELSE ERRMSG = "ASSETS.DEPR"; GOTO 93000
  OPEN "EQUIP.FILE" TO EQUIP.FILE ELSE ERRMSG = "EQUIP.FILE"; GOTO 93000
  OPEN "EQUIP.HIST" TO EQUIP.HIST ELSE ERRMSG = "EQUIP.HIST"; GOTO 93000
  OPEN "REPAIRS" TO REPAIRS ELSE ERRMSG = "REPAIRS"; GOTO 93000
  OPEN "PREFIX" TO PREFIX ELSE ERRMSG = "PREFIX"; GOTO 93000
  OPEN "CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL"; GOTO 93000
  OPEN "COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY"; GOTO 93000
  CONO = ""; MAT COMP.REC = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  MATREAD FISCAL.REC FROM CONTROL, CONO:"FAFISCAL" ELSE
    ERRMSG = "Cannot locate Fixed Assets period"
    GOSUB 91000; GOTO 99999
  END
*
*--- Initialization
*
  CUR.YR = FR.CURR.PER[1,4]
*
  REM ADDED DLG 8/14/91
  TODAY=DATE()
*
*--- Setup subroutine SCRN.EDIT
*
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 2
  ECD.SCRN.NAME<1> = "EQUIP.MAINT"
  ECD.SCRN.NAME<2> = "EQUIP.HIST.SUB"
  ECD.ACTION = 1; CALL SCRN.EDIT
*
  ECD.SCRN.NO = 1
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*--- Enter ASSET number
100*
  MAT SCV.REC = ""
  ECD.ACTION = 6; CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 99999
  AST.NUM = ECD.RET.VALUE; AST.ID = CONO : AST.NUM
  MATREAD AST.REC FROM ASSETS, AST.ID THEN
*T23278 v
    DIV.CODE = AST.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
    CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
    IF ERRMSG # '' THEN
      GOSUB 91000; GOTO 100
    END
*T23278 ^
    IF AST.EQP.REC # "Y" THEN
      ERRMSG = "Equipement records are not valid for this asset."
      GOSUB 91000; GOTO 100
    END
    * T26126 V
    MATREADU EQP.REC FROM EQUIP.FILE, AST.ID LOCKED
      ERRMSG = 'EQUIPMENT record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 100 
    END THEN
    * T26126 ^
      ONFILE = 1
      SCV.REC(1) = AST.NUM
      SCV.REC(2) = AST.DESC
      SCV.REC(3) = EQP.SCH.DATE
      SCV.REC(4) = EQP.NEXT.DATE
      SCV.REC(5) = EQP.FREQ.DAYS
      SCV.REC(6) = EQP.LST.DATE
      SCV.REC(7) = EQP.COMMENTS
      MATREAD RPR.REC FROM REPAIRS, AST.ID ELSE
        MAT RPR.REC = ""
      END
      SCV.REC(8) = RPR.COMMENTS
      SCV.REC(21) = OCONV(RPR.U.HRS,"MD2,")
      SCV.REC(22) = OCONV((RPR.U.UNITS/100),"MD0,")
      SCV.REC(31) = RPR.M.NO
      SCV.REC(32) = OCONV(RPR.M.HRS,"MD2,")
      SCV.REC(33) = OCONV(RPR.M.COST,"MD2,")
      SCV.REC(31)<1,2> = RPR.R.NO
      SCV.REC(32)<1,2> = OCONV(RPR.R.HRS,"MD2,")
      SCV.REC(33)<1,2> = OCONV(RPR.R.COST,"MD2,")
      SCV.REC(31)<1,3> = RPR.O.NO
      SCV.REC(32)<1,3> = OCONV(RPR.O.HRS,"MD2,")
      SCV.REC(33)<1,3> = OCONV(RPR.O.COST,"MD2,")
*
      IF EQP.FST.YR = "" THEN GOTO 190
      SCV.REC(1)<2> = EQP.FST.YR
      SCV.REC(2)<2> = CUR.YR
      SCV.REC(12)<2> = OCONV(INT(EQP.U.UNITS/(EQP.U.HRS/100)+.5),"MD0,")
      SCV.REC(23)<2> = OCONV(EQP.U.HRS,"MD2,")
      SCV.REC(24)<2> = OCONV((EQP.U.UNITS/100),"MD0,")
      SCV.REC(34)<2> = EQP.M.NO
      SCV.REC(35)<2> = OCONV(EQP.M.HRS,"MD2,")
      SCV.REC(36)<2> = OCONV(EQP.M.COST,"MD2,")
      SCV.REC(34)<2,2> = EQP.R.NO
      SCV.REC(35)<2,2> = OCONV(EQP.R.HRS,"MD2,")
      SCV.REC(36)<2,2> = OCONV(EQP.R.COST,"MD2,")
      SCV.REC(34)<2,3> = EQP.O.NO
      SCV.REC(35)<2,3> = OCONV(EQP.O.HRS,"MD2,")
      SCV.REC(36)<2,3> = OCONV(EQP.O.COST,"MD2,")
      SCV.REC(44)<2> = EQP.M.NO + EQP.R.NO + EQP.O.NO
      SCV.REC(45)<2> = EQP.M.HRS + EQP.R.HRS + EQP.O.HRS
      SCV.REC(46)<2> = OCONV(EQP.M.COST + EQP.R.COST + EQP.O.COST,"MD2,")
      IF SCV.REC(44)<2> = 0 THEN
        SCV.REC(14)<2> = 0
      END ELSE
        SCV.REC(14)<2> = OCONV(INT(SCV.REC(45)<2>/SCV.REC(44)<2>+.5),"MD2,")
      END
      IF EQP.U.HRS + 0 = 0 THEN
        SCV.REC(16)<2> = 0
      END ELSE
        SCV.REC(16)<2> = INT(SCV.REC(45)<2>/EQP.U.HRS*10000+.5)
      END
      SCV.REC(45)<2> = OCONV(SCV.REC(45)<2>,"MD2,")
190*
    END ELSE
      ONFILE = 0
      ECD.NUM = 2; SCV.REC(ECD.NUM) = AST.DESC
      ECD.ACTION = 5; CALL SCRN.EDIT
      MAT EQP.REC = ""; MAT RPR.REC = ""
      FOR OPTION = 1 TO 3 WHILE ECD.RET.VALUE # "END"
        ON OPTION GOSUB 1100,1200,1300
      NEXT OPTION
      IF ECD.RET.VALUE = "END" THEN GOTO 100
    END
    ECD.ACTION = 3; CALL SCRN.EDIT
  END ELSE
    ERRMSG = "Cannot locate asset # ":AST.NUM
    GOSUB 91000; GOTO 100
  END
*--- Prompt line
*
  LOOP
    ECD.NUM = 41; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
        OPTION = "END"
        RELEASE EQUIP.FILE, AST.ID
      CASE OPTION = "H" AND EQP.FST.YR = ""
        ERRMSG = "No accumulated history for this equipment"
        GOSUB 91000
      CASE OPTION = "H"
        ECD.SCRN.NO = ECD.SCRN.NO + 1
        ECD.ACTION = 2; CALL SCRN.EDIT
        CALL EQUIP.HIST.SUB
        ECD.SCRN.NO = 1
        ECD.ACTION = 2; CALL SCRN.EDIT
        ECD.ACTION = 3; CALL SCRN.EDIT
      CASE OPTION = "F"
        MATWRITE EQP.REC ON EQUIP.FILE, AST.ID
        OPTION = "END"
      CASE NOT(NUM(OPTION))
        ERRMSG = "*** Invalid selection ***"; GOSUB 91000
      CASE OPTION > 0 AND OPTION < 4
        ON OPTION GOSUB 1100,1200,1300
      CASE 1
        ERRMSG = "*** Out of range ***"; GOSUB 91000
    END CASE
  WHILE OPTION # "END" DO REPEAT
  GOTO 100
*--- Scheduled Date
1100*
  BEGIN CASE
    CASE NOT(ONFILE)
      AUTO = 1
    CASE EQP.NEXT.DATE = EQP.SCH.DATE + EQP.FREQ.DAYS
      AUTO = 1
    CASE 1
      AUTO = 0
  END CASE
  IF EQP.SCH.DATE < TODAY THEN
    ECD.MINV = EQP.SCH.DATE
  END ELSE
    ECD.MINV = TODAY
  END
  ECD.MAXV = TODAY + 1095
  ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1199
  EQP.SCH.DATE = ECD.RET.VALUE
  IF AUTO THEN
    EQP.NEXT.DATE = EQP.SCH.DATE + EQP.FREQ.DAYS
    ECD.NUM = 4; SCV.REC(ECD.NUM) = EQP.NEXT.DATE
    ECD.ACTION = 5; CALL SCRN.EDIT
  END
1199 RETURN
*--- Frequency in days
1200*
  IF EQP.NEXT.DATE = EQP.SCH.DATE + EQP.FREQ.DAYS THEN
    AUTO = 1
  END ELSE
    AUTO = 0
  END
  ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1299
  EQP.FREQ.DAYS = ECD.RET.VALUE
  IF AUTO THEN
    EQP.NEXT.DATE = EQP.SCH.DATE + EQP.FREQ.DAYS
    ECD.NUM = 4; SCV.REC(ECD.NUM) = EQP.NEXT.DATE
    ECD.ACTION = 5; CALL SCRN.EDIT
  END
1299 RETURN
*--- General comments
1300*
  ECD.NUM = 7; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    EQP.COMMENTS = ECD.RET.VALUE
  END
  RETURN
*
*--- Error message routine
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT REPLY :
*       PRINT @(0,23) : CL :
*       RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000 ERRMSG = "Cannot locate ":ERRMSG:" file"
*       ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 END
