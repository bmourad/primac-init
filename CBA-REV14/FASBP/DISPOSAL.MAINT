*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - FASBP
* PROGRAM     - DISPOSAL.MAINT
* BY          - Ziad Yamout, VERCOM software, Inc.
* DATE        - 01/27/89
* DESCRIPTION -
* This program will update and maintain DISPOSAL file.
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*--- Data structure libraries
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>FAS.CPYLIB>DISPOSAL
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- Setup SYSCOM subroutine
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- Open files
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "Cannot locate COMPANY file";GOTO 93000
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "Cannot locate CONTROL file";GOTO 93000
  END
  OPEN "","FAS.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "Cannot locate FAS.SCREENS file";GOTO 93000
  END
  OPEN "","DISPOSAL" TO DISPOSAL ELSE
    ERRMSG = "Cannot locate DISPOSAL file";GOTO 93000
  END
*
*--- Get comapny number
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
*
*--- Setup SCRN.EDIT subroutine
*
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "DISPOSAL.MAINT"
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*--- Prompt for DISPOSAL item id
100*
  MAT SCV.REC = ""
  ECD.ACTION = 6; CALL SCRN.EDIT
  ECD.NUM = 5; SCV.REC(ECD.NUM) = DATE()
  ECD.ACTION = 5;CALL SCRN.EDIT
  ECD.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 99999
  DIS.ID = CONO : ECD.RET.VALUE
  * T26126 v
  MATREADU DIS.REC FROM DISPOSAL, DIS.ID LOCKED
    ERRMSG = 'DISPOSAL record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 100 
  END THEN
  * T26126 ^
    SCV.REC(2) = DIS.DESC
    ECD.ACTION = 3; CALL SCRN.EDIT
  END ELSE
    MAT DIS.REC = ""
    GOSUB 1100
    IF ECD.RET.VALUE = "END" THEN
      RELEASE DISPOSAL, DIS.ID
      GOTO 100
    END
  END
*
*--- Prompt line
  LOOP
    ECD.NUM = 4; SCV.REC(ECD.NUM) = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "END" OR OPTION = "E"
        RELEASE DISPOSAL, DIS.ID
        OPTION = "END"
      CASE OPTION = "F"
        MATWRITE DIS.REC ON DISPOSAL, DIS.ID
        OPTION = "END"
      CASE NOT(NUM(OPTION))
        ERRMSG = "*** Invalid selection ***"; GOSUB 91000
      CASE OPTION <> 1
        ERRMSG = "*** Out of range ***"; GOSUB 91000
      CASE 1
        GOSUB 1100
    END CASE
  WHILE OPTION # "END" DO REPEAT
  GOTO 100
*
*--- Disposal description
*
1100*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    DIS.DESC = ECD.RET.VALUE
  END
  RETURN
*--- Error message routine
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
*       PRINT @(0,23) : ERRMSG : CL :
*       INPUT REPLY :
*       PRINT @(0,23) : CL :
*       RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000*
*       ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
