*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - FASBP
* PROGRAM       - EQUIP.LIST
* BY            - Ziad Yamout, VERCOM Software, Inc.
* DATE          - 10/20/89
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FISCAL
*COPY>FAS.CPYLIB>ASSETS
*COPY>FAS.CPYLIB>EQUIP.FILE
*COPY>FAS.CPYLIB>EQUIP.HIST
*COPY>FAS.CPYLIB>REPAIRS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- Setup SYSCOM
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*--- Get buffer
*
      PROCREAD BUFFER ELSE
         ERRMSG = "Must be executed from a PROC"
         GOSUB 91000; GOTO 99999
      END
      CONO = BUFFER<1>
      HD = BUFFER<2>
*
*--- Open files
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "Cannot locate COMPANY file"; GOTO 93000
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "Cannot locate CONTROL file"; GOTO 93000
      END
      OPEN "","ASSETS" TO ASSETS ELSE
         ERRMSG = "Cannot locate ASSETS file"; GOTO 93000
      END
      OPEN "EQUIP.FILE" TO EQUIP.FILE ELSE
         ERRMSG = "Cannot locate EQUIP.FILE file"; GOTO 93000
      END
      OPEN "EQUIP.HIST" TO EQUIP.HIST ELSE
         ERRMSG = "Cannot locate EQUIP.HIST file"; GOTO 93000
      END
      OPEN "REPAIRS" TO REPAIRS ELSE
         ERRMSG = "Cannot locate REPAIRS file"; GOTO 93000
      END
*
*--- Process all the selected records
*
      READNEXT AST.ID ELSE
         ERRMSG = "No items selected"
         GOSUB 91000; GOTO 99999
      END
*
*--- Initialization
*
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         ERRMSG = "Cannot locate company - " : CONO
         GOSUB 91000; GOTO 99999
      END
      MATREAD FISCAL.REC FROM CONTROL, CONO:"FAFISCAL" ELSE
         ERRMSG = "Cannot locate Fixed Assets period"
         GOSUB 91000; GOTO 99999
      END
      YEAR = FR.CURR.PER[1,4]
      SP = SPACE(2)
      ST = STR("-",15)
      HD0 = SPACE(22):"<------------ Year To Date ------------>":SPACE(3):"<------------- Cumulative ------------->"
      HD1 = SPACE(28):"Hours            Units":SPACE(21):"Hours            Units"
      DSH1 = SPACE(23):ST:SP:ST:SPACE(11):ST:SP:ST
      HD2 = SPACE(23):"Ocur     Down Time          Cost":SPACE(11):"Ocur     Down Time          Cost"
      DSH2 = SPACE(23):STR("-",4):SP:ST:SP:ST:SPACE(5):STR("-",4):SP:ST:SP:ST
      PRINTER ON
      DATA=1
      LOOP
         MATREAD AST.REC FROM ASSETS, AST.ID ELSE GOTO 999
         MATREAD EQP.REC FROM EQUIP.FILE, AST.ID ELSE GOTO 999
         MATREAD RPR.REC FROM REPAIRS, AST.ID ELSE
            MAT RPR.REC = ""
         END
         MATREAD EQH.REC FROM EQUIP.HIST, AST.ID:YEAR ELSE
            MAT EQH.REC = ""
         END
         AST.NUM = AST.ID[4,99]
         HEADING HD
         PRINT
         PRINT
         PRINT "Asset & Description  : " :
         PRINT AST.NUM "L#17" :"&   ": AST.DESC "L#30"
         PRINT "Scheduled Date       : " :
         PRINT OCONV(EQP.SCH.DATE,"D2/") "L#8"
         PRINT "Frequency (Days)     : " :
         PRINT EQP.FREQ.DAYS "R#3"
         PRINT "Next Scheduled Date  : " :
         PRINT OCONV(EQP.NEXT.DATE,"D2/") "L#8"
         PRINT "Last Maintained Date : " :
         PRINT OCONV(EQP.LST.DATE,"D2/") "L#8"
         PRINT "General Comments     : " :
         PRINT EQP.COMMENTS "L#55"
         PRINT "Current Comments     : " :
         PRINT RPR.COMMENTS "L#55"
         PRINT
         PRINT "First Year           : " :
         PRINT EQP.FST.YR "R#4"
         PRINT
         PRINT HD0
         PRINT
         YTD.NO = EQH.M.NO + EQH.R.NO + EQH.O.NO
         TOT.NO = EQP.M.NO + EQP.R.NO + EQP.O.NO
         YTD.HRS = EQH.M.HRS + EQH.R.HRS + EQH.O.HRS
         TOT.HRS = EQP.M.HRS + EQP.R.HRS + EQP.O.HRS
         YTD.COST = EQH.M.COST + EQH.R.COST + EQH.O.COST
         TOT.COST = EQP.M.COST + EQP.R.COST + EQP.O.COST
         PRINT "Units / Hour         : " :
         IF EQH.U.HRS + 0 = 0 THEN
            PRINT SPACE(40):":  ":
         END ELSE
            PRINT OCONV(INT(EQH.U.UNITS/(EQH.U.HRS/100)+.5),"MD0,") "R#15":SPACE(25):":  ":
         END
         IF EQP.U.HRS + 0 = 0 THEN
            PRINT
         END ELSE
            PRINT OCONV(INT(EQP.U.UNITS/(EQP.U.HRS/100)+.5),"MD0,") "R#15"
         END
         PRINT "Average Down Time    : " :
         IF YTD.NO = 0 THEN
            YTD.AVG.DWN = 0
         END ELSE
            YTD.AVG.DWN = OCONV(INT(YTD.HRS/YTD.NO+.5),"MD2,")
         END
         IF TOT.NO = 0 THEN
            AVG.DWN = 0
         END ELSE
            AVG.DWN = OCONV(INT(TOT.HRS/TOT.NO+.5),"MD2,")
         END
         PRINT OCONV(YTD.AVG.DWN,"MD2,") "R#15":SPACE(25):":  ":
         PRINT OCONV(AVG.DWN,"MD2,") "R#15"
         PRINT "Down Time Percentage : " :
         IF EQH.U.HRS + 0 = 0 THEN
            YTD.PCT.DWN = 0
         END ELSE
            YTD.PCT.DWN = INT(YTD.HRS/EQH.U.HRS*10000+.5)
         END
         IF EQP.U.HRS + 0 = 0 THEN
            PCT.DWN = 0
         END ELSE
            PCT.DWN = INT(TOT.HRS/EQP.U.HRS*10000+.5)
         END
         PRINT OCONV(YTD.PCT.DWN,"MD2,") "R#15":SPACE(25):":  ":
         PRINT OCONV(PCT.DWN,"MD2,") "R#15"
         PRINT
         PRINT HD1
         PRINT DSH1
         PRINT
         PRINT "Usage                : " :
         PRINT OCONV(EQH.U.HRS,"MD2,") "R#15":SP:OCONV(EQH.U.UNITS,"MD0,") "R#15":SPACE(8):":  ":
         PRINT OCONV(EQP.U.HRS,"MD2,") "R#15":SP:OCONV(EQP.U.UNITS,"MD0,") "R#15"
         PRINT
         PRINT HD2
         PRINT DSH2
         PRINT
         PRINT "Maintenance          : " :
         PRINT EQH.M.NO "R#4":SP:OCONV(EQH.M.HRS,"MD2,") "R#15":SP:OCONV(EQH.M.COST,"MD2,") "R#15":"  :  ":
         PRINT EQP.M.NO "R#4":SP:OCONV(EQP.M.HRS,"MD2,") "R#15":SP:OCONV(EQP.M.COST,"MD2,") "R#15"
         PRINT "Repairs              : " :
         PRINT EQH.R.NO "R#4":SP:OCONV(EQH.R.HRS,"MD2,") "R#15":SP:OCONV(EQH.R.COST,"MD2,") "R#15":"  :  ":
         PRINT EQP.R.NO "R#4":SP:OCONV(EQP.R.HRS,"MD2,") "R#15":SP:OCONV(EQP.R.COST,"MD2,") "R#15"
         PRINT "Overhaul             : " :
         PRINT EQH.O.NO "R#4":SP:OCONV(EQH.O.HRS,"MD2,") "R#15":SP:OCONV(EQH.O.COST,"MD2,") "R#15":"  :  ":
         PRINT EQP.O.NO "R#4":SP:OCONV(EQP.O.HRS,"MD2,") "R#15":SP:OCONV(EQP.O.COST,"MD2,") "R#15"
         PRINT DSH2
         PRINT "M/R/O Totals         : " :
         PRINT YTD.NO "R#4":SP:OCONV(YTD.HRS,"MD2,") "R#15":SP:OCONV(YTD.COST,"MD2,") "R#15":"  :  ":
         PRINT TOT.NO "R#4":SP:OCONV(TOT.HRS,"MD2,") "R#15":SP:OCONV(TOT.COST,"MD2,") "R#15"
999*
         READNEXT AST.ID ELSE DATA=0
      WHILE DATA DO REPEAT
      PRINTER CLOSE
      PRINTER OFF
      GOTO 99999
*--- Error message routine
*
91000*
      PRINT @(0,23) : ERRMSG : CL :
      INPUT REPLY :
      PRINT @(0,23) : CL :
      RETURN
93000*
      ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
   END
