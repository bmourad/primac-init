      SUBROUTINE DISPLAY.PERTS.CHART(MAT WCOM, MAT WCHT)
*********************************************************************
*
* PROGRAM  - DISPLAY.PERTS.CHART
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/11/91
*
* DESCRIPTION
*
* This subroutine is used to create a chart.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.WND          Window ID              Required
* 
*   WCOM.CAPTION      Window heading         Optional
*
*   WCOM.STYLE        Chart style            Optional
*
*   WCOM.COL          Window column          Optional
*                                            Default = 0
*                                            Range = 0 - 79
*
*   WCOM.ROW          Window row             Optional
*                                            Default = 0
*                                            Range = 0 - 79
*
*   WCOM.WIDTH        Window width           Optional
*                                            Default = 80
*                                            Range = 1 - 80
*
*   WCOM.HEIGHT       Window height          Optional
*                                            Default = 24
*                                            Range = 1 - 24
*
*   WCOM.FCOLOR       Foreground color       Optional
*                                            Default = BLACK
*
*   WCOM.BCOLOR       Background color       Optional
*                                            Default = WHITE
*
*   WCHT.TYPE         Chart type             N/A
*
*   WCHT.HEAD         Chart heading          Optional
*
*   WCHT.SHEAD        Chart sub-heading      Optional
*
*   WCHT.YHEAD        Y-Axis heading         Optional
*
*   WCHT.XHEAD        X-Axis heading         Optional
*
*   WCHT.DESC         Chart set desc         N/A
*
*   WCHT.DHEAD        X-Axis data desc       Required (MV-2)
*
*   WCHT.DATA         Chart data             Required (MV-1)
*
*   WCHT.BWIDTH       Bar width              Required (MV-1)
*
*   WCHT.BSPACE       Bar spacing            N/A
*
*   WCHT.SSPACE       Set spacing            Optional
*                                            Default = 1
*
*   WCHT.COLOR        Bar colors             Required (MV-1)
*
*   WCHT.SHADOW       Bar shadows            N/A
*
*   WCHT.SELECT       Bar selection ID       Optional
*
*********************************************************************
*
INCLUDE WINCOM FROM NDCBP
INCLUDE WINCOM2 FROM NDCBP
*
*---- INITIALIZATION
*
      EMSG = ""
*
      IF WCOM.WND     = "" THEN EMSG = EMSG : " " : "WCOM.WND"
      IF WCOM.COL     = "" THEN WCOM.COL =  0
      IF WCOM.ROW     = "" THEN WCOM.ROW =  0
      IF WCOM.WIDTH   = "" THEN WCOM.WIDTH = 80
      IF WCOM.HEIGHT  = "" THEN WCOM.HEIGHT = 22
      IF WCHT.SSPACE  = "" THEN WCHT.SSPACE = 1
*
      CHART.WIDTH  = WCOM.WIDTH
      CHART.HEIGHT = WCOM.HEIGHT
*
      XCNT = DCOUNT(WCHT.DATA,CHAR(253))      ;* DATA COUNT
      DCNT = DCOUNT(WCHT.DHEAD,CHAR(253))     ;* PERIOD COUNT
      DMIN = 0
      DMAX = 0
      FOR XPTR = 1 TO XCNT
         DVAL = WCHT.DATA<1,XPTR>
         IF DVAL > DMAX THEN
            DMAX = DVAL
         END ELSE
            IF DVAL < DMIN THEN DMIN = DVAL
         END
      NEXT XPTR
*
      BEG.COL = 0
      IF WCHT.YHEAD # "" THEN BEG.COL = BEG.COL + 2
*
      END.COL = WCOM.WIDTH - 1
*
      BEG.ROW = 0
      IF WCHT.HEAD  # "" THEN BEG.ROW = BEG.ROW + 1
      IF WCHT.SHEAD # "" THEN BEG.ROW = BEG.ROW + 1
      BEG.ROW = BEG.ROW + 1
*
      END.ROW   = WCOM.HEIGHT
      END.ROW = END.ROW - 1
      IF WCHT.DHEAD # "" THEN END.ROW = END.ROW - 1
      IF WCHT.XHEAD # "" THEN END.ROW = END.ROW - 1
      IF WCHT.DESC  # "" THEN END.ROW = END.ROW - 2
*
      SET.SPACE = WCHT.SSPACE
      MAX.WIDTH = DCNT*SET.SPACE
      MAX.BAR.HEIGHT = END.ROW - BEG.ROW
      SET.WIDTH = SET.SPACE
      BAR.REPEAT = SET.SPACE
*
      BEGIN CASE
      CASE (DMAX-DMIN) <= 1
         DMAX = INT(DMAX*10+0.999)/10      ;* RAISE TO NEXT 1
         DMIN = INT(DMIN*10-0.999)/10      ;* LOWER TO NEXT 1
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT*100+0.999)
         SCLR = 1
         CNV = "MD2"
         CHEAD = ""
      CASE (DMAX-DMIN) <= 10
         DMAX = INT(DMAX+0.999)            ;* RAISE TO NEXT 1
         DMIN = INT(DMIN-0.999)            ;* LOWER TO NEXT 1
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT*10+0.999)
         SCLR = 1
         CNV = "MD1"
         CHEAD = ""
      CASE (DMAX-DMIN) <= 100
         DMAX = INT(DMAX/10+0.999)*10      ;* RAISE TO NEXT 10
         DMIN = INT(DMIN/10-0.999)*10      ;* LOWER TO NEXT 10
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT+0.999)
         INCR = INT(INCR/(5*1)+0.999)*(5*1)
         SCLR = 1
         CNV = "MD0"
         CHEAD = ""
      CASE (DMAX-DMIN) <= 1000
         DMAX = INT(DMAX/100+0.999)*100    ;* RAISE TO NEXT 100
         DMIN = INT(DMIN/100-0.999)*100    ;* LOWER TO NEXT 100
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/10+0.999)*10
         INCR = INT(INCR/(5*10)+0.999)*(5*10)
         SCLR = 1
         CNV = "MD0"
         CHEAD = ""
      CASE (DMAX-DMIN) <= 10000
         DMAX = INT(DMAX/1000+0.999)*1000  ;* RAISE TO NEXT 1000
         DMIN = INT(DMIN/1000-0.999)*1000  ;* LOWER TO NEXT 1000
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/100+0.999)*100
         INCR = INT(INCR/(5*100)+0.999)*(5*100)
         SCLR = 1
         CNV = "MD0,"
         CHEAD = ""
      CASE (DMAX-DMIN) <= 100000
         DMAX = INT(DMAX/10000+0.999)*10000 ;* RAISE TO NEXT 10000
         DMIN = INT(DMIN/10000-0.999)*10000 ;* LOWER TO NEXT 10000
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/1000+0.999)*1000
         INCR = INT(INCR/(5*1000)+0.999)*(5*1000)
         SCLR = 1000
         CNV = "MD0,"
         CHEAD = "( Thousands )"
      CASE (DMAX-DMIN) <= 1000000
         DMAX = INT(DMAX/100000+0.999)*100000 ;* RAISE TO NEXT 100000
         DMIN = INT(DMIN/100000-0.999)*100000 ;* LOWER TO NEXT 100000
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/10000+0.999)*10000
         INCR = INT(INCR/(5*10000)+0.999)*(5*10000)
         SCLR = 1000
         CNV = "MD0,"
         CHEAD = "( Thousands )"
      CASE (DMAX-DMIN) <= 10000000
         DMAX = INT(DMAX/1000000+0.999)*1000000 ;* RAISE TO NEXT 1000000
         DMIN = INT(DMIN/1000000-0.999)*1000000 ;* LOWER TO NEXT 1000000
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/100000+0.999)*100000
         INCR = INT(INCR/(5*100000)+0.999)*(5*100000)
         SCLR = 1000
         CNV = "MD0,"
         CHEAD = "( Thousands )"
      CASE (DMAX-DMIN) <= 100000000
         DMAX = INT(DMAX/10000000+0.999)*10000000 ;* RAISE TO NEXT 10000000
         DMIN = INT(DMIN/10000000-0.999)*10000000 ;* LOWER TO NEXT 10000000
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/1000000+0.999)*1000000
         INCR = INT(INCR/(5*1000000)+0.999)*(5*1000000)
         SCLR = 1000000
         CNV = "MD0,"
         CHEAD = "( Millions )"
      CASE (DMAX-DMIN) <= 1000000000
         DMAX = INT(DMAX/100000000+0.999)*100000000 ;* RAISE TO NEXT 100000000
         DMIN = INT(DMIN/100000000-0.999)*100000000 ;* LOWER TO NEXT 100000000
         INCR = INT((DMAX-DMIN)/MAX.BAR.HEIGHT/10000000+0.999)*10000000
         INCR = INT(INCR/(5*10000000)+0.999)*(5*10000000)
         SCLR = 1000000
         CNV = "MD0,"
         CHEAD = "( Millions )"
      END CASE
      IF INCR = 0 THEN
         ZROW = END.ROW
      END ELSE
         ZROW = END.ROW + INT(DMIN / INCR)
      END
      YWMAX = LEN(OCONV((ZROW-BEG.ROW)*(INCR/SCLR),CNV))
      YWMIN = LEN(OCONV((ZROW-END.ROW)*(INCR/SCLR),CNV))
      IF YWMAX >= YWMIN THEN YWIDTH = YWMAX ELSE YWIDTH = YWMIN
      FMT = "R#":YWIDTH
*
      IF CHEAD # "" THEN BEG.COL = BEG.COL + 2
      BEG.COL = BEG.COL + 1 + YWIDTH + 1
      END.COL = END.COL - 1 - YWIDTH - 1
      SCROLL.WIDTH = INT((END.COL-BEG.COL+1)/BAR.REPEAT)*BAR.REPEAT
      END.COL = BEG.COL + SCROLL.WIDTH - 1
*
*---- OPEN WINDOW
*
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.COL    = BEG.COL
      WCOM.ROW    = BEG.ROW
      WCOM.WIDTH  = SCROLL.WIDTH
*     WCOM.HEIGHT = MAX.BAR.HEIGHT+1
      WCOM.HEIGHT = MAX.BAR.HEIGHT+1.75
      CALL SET.HSCROLL.AREA(MAT WCOM)
      WCOM.WIDTH  = WCHT.SSPACE
      CALL SET.HSCROLL.COUNT(MAT WCOM)
      WCOM.COL    = BEG.COL:"-"
      WCOM.ROW    = BEG.ROW
      WCOM.WIDTH  = SCROLL.WIDTH+0.25
      WCOM.HEIGHT = MAX.BAR.HEIGHT
      WCOM.FCOLOR = "IWHITE"
      WCOM.BCOLOR = "IWHITE"
      WCOM.RCOLOR = "IWHITE"
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY CHART HEADINGS
*
      IF WCHT.HEAD # "" THEN
         WCOM.COL  = 0
         WCOM.ROW  = 0.5
         WCOM.WIDTH = CHART.WIDTH
         WCOM.DATA = WCHT.HEAD
         WCOM.FONT = 1
         WCOM.ALIGN = "CH"
         CALL LOAD.DATA(MAT WCOM)
      END
*
      IF WCHT.SHEAD # "" THEN
         WCOM.COL  = 0
         WCOM.ROW  = 1.5
         WCOM.WIDTH = CHART.WIDTH
         WCOM.DATA = WCHT.SHEAD
         WCOM.ALIGN = "CH"
         CALL LOAD.DATA(MAT WCOM)
      END
      IF WCHT.YHEAD # "" THEN
         WCOM.COL  = 0
         WCOM.ROW  = END.ROW
         WCOM.WIDTH = 2
         WCOM.HEIGHT = 0 - (END.ROW-BEG.ROW)
         WCOM.DATA = WCHT.YHEAD
         WCOM.FONT = 1
         WCOM.ALIGN = "CV"
         CALL LOAD.DATA(MAT WCOM)
      END
      IF CHEAD # "" THEN
         WCOM.COL  = 2
         WCOM.ROW  = END.ROW
         WCOM.WIDTH = 2
         WCOM.HEIGHT = 0 - (END.ROW-BEG.ROW)
         WCOM.DATA = CHEAD
         WCOM.FONT = 1
         WCOM.ALIGN = "CV"
         CALL LOAD.DATA(MAT WCOM)
      END
      CURR.ROW = END.ROW - 1
*     CURR.ROW = CURR.ROW + 1
      IF WCHT.DHEAD # "" THEN
         CURR.ROW = CURR.ROW + 1
         FOR DPTR = 1 TO DCNT
            WCOM.TYPE = "H"
            COL = (BEG.COL+(DPTR-1)*BAR.REPEAT)+(BAR.REPEAT/2)
            WCOM.COL  = COL
*           WCOM.ROW  = CURR.ROW + 0.25
            WCOM.ROW  = CURR.ROW + 0.75
            WCOM.WIDTH = SET.WIDTH
            WCOM.DATA = WCHT.DHEAD<1,DPTR>
            WCOM.ALIGN = "CH"
            CALL LOAD.DATA(MAT WCOM)
         NEXT DPTR
      END
      IF WCHT.XHEAD # "" THEN
         CURR.ROW  = CURR.ROW + 1
         WCOM.COL  = BEG.COL
*        WCOM.ROW  = CURR.ROW
         WCOM.ROW  = CURR.ROW + 0.75
         WCOM.WIDTH = END.COL-BEG.COL+1
         WCOM.DATA = WCHT.XHEAD
         WCOM.FONT = 1
         WCOM.ALIGN = "CH"
         CALL LOAD.DATA(MAT WCOM)
      END
*
*---- DISPLAY CHART SCALE
*
      WCOM.COL    = BEG.COL:"-"
      WCOM.ROW    = BEG.ROW
      WCOM.HEIGHT = MAX.BAR.HEIGHT
      WCOM.LEVEL = 5
      CALL DRAW.LINE(MAT WCOM)            ;* DRAW LEFT VERT LINE
*
      WCOM.COL    = END.COL+1
      WCOM.ROW    = BEG.ROW
      WCOM.HEIGHT = MAX.BAR.HEIGHT
      WCOM.COL    = END.COL+1
      CALL DRAW.LINE(MAT WCOM)            ;* DRAW RIGHT VERT LINE
*
      FOR ROW = END.ROW TO BEG.ROW STEP -1
         WCOM.COL    = BEG.COL
         WCOM.ROW    = ROW
         WCOM.WIDTH  = END.COL-BEG.COL+1
         WCOM.LEVEL = 5
         CALL DRAW.LINE(MAT WCOM)         ;* DRAW HORIZ GRID LINE
*
         IF INCR > 0 THEN
            WCOM.COL    = BEG.COL-1-YWIDTH
            WCOM.ROW    = ROW-0.5
            WCOM.WIDTH  = YWIDTH
            WCOM.DATA   = OCONV((ZROW-ROW)*(INCR/SCLR),CNV) FMT
            CALL LOAD.DATA(MAT WCOM)         ;* LEFT Y-AXIS SCALE
*
            WCOM.COL    = END.COL+2
            WCOM.ROW    = ROW-0.5
            WCOM.WIDTH  = YWIDTH
            WCOM.DATA   = OCONV((ZROW-ROW)*(INCR/SCLR),CNV) FMT
            CALL LOAD.DATA(MAT WCOM)         ;* RIGHT Y-AXIS SCALE
         END
      NEXT ROW
*
*---- DISPLAY CHART DATA
*
      IF INCR > 0 THEN
         COL = BEG.COL
         FOR XPTR = 1 TO XCNT
            DVAL = WCHT.DATA<1,XPTR>+0
*           COL = BEG.COL + (XPTR-1)*BAR.REPEAT
            IF DVAL > 0 THEN
               ROW = ZROW
               HEIGHT = 0 - INT(DVAL/INCR*10000+0.5)/10000
               IF ABS(HEIGHT) < 0.25 THEN HEIGHT = -0.25
            END ELSE
               ROW = ZROW:"+"
               HEIGHT = 0.5
            END
            WCOM.TYPE   = "H"
            WCOM.COL    = COL:"-"
*           WCOM.WIDTH  = BAR.WIDTH:"+"
            BWIDTH = WCHT.BWIDTH<1,XPTR>
            IF BWIDTH = "" THEN BWIDTH = WCHT.SSPACE
            WCOM.WIDTH = BWIDTH:"+"
            COL = COL + BWIDTH
            WCOM.ROW    = ROW
            WCOM.HEIGHT = HEIGHT
            WCOM.FCOLOR = WCHT.COLOR<1,XPTR,1>
            WCOM.BCOLOR = WCHT.COLOR<1,XPTR,2>
            WCOM.RCOLOR = WCHT.COLOR<1,XPTR,3>
            WCOM.SELECT = WCHT.SELECT<1,XPTR>
            WCOM.LEVEL  = 20
            CALL LOAD.DATA(MAT WCOM)
         NEXT XPTR
      END
      CALL SET.WSCROLL(MAT WCOM)
*
*---- RESET PARAMETERS USED
*
      WCOM.CAPTION = ""
      WCOM.STYLE = ""
      WCOM.COL = ""
      WCOM.ROW = ""
      WCOM.WIDTH = ""
      WCOM.HEIGHT = ""
      WCOM.FCOLOR = ""
      WCOM.BCOLOR = ""
      MAT WCHT = ""
      RETURN
   END
