      SUBROUTINE PERTS.JOB.INQ(CONO, MAT WCOM)
*********************************************************************
*
* PROGRAM - PERTS.JOB.INQ
*
* AUTHOR  - NICK AMENDOLA, NASTech, Inc.
*
* DATE    - 12/15/94
*
* DESCRIPTION
*
* This subroutine is used to display the job specifications and status
* for the specified job.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>NDC.CPYLIB>COM.PERTS
*COPY>JCS.CPYLIB>JOB
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>NDCBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
      OPEN "","JOB.SCHED" TO JOB.SCHED ELSE
         WCOM.STATUS = "Cannot open JOB.SCHED file"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","ESTIMATE.MATL" TO ESTIMATE.MATL ELSE
         WCOM.STATUS = "Cannot open ESTIMATE.MATL file"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      JOBNO = PERTS.CURR.JOB
      MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job Schedule # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      MATREAD JOB.REC FROM JOB, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      IF JOB.EST = "" THEN
         MAT EST.REC = ""
      END ELSE
         MATREAD EST.REC FROM ESTIMATE, CONO:JOB.EST ELSE
            MAT EST.REC = ""
         END
      END
      IF JOB.SLSMN = "" THEN
         MAT SALESMAN.REC = ""
      END ELSE
         MATREAD SALESMAN.REC FROM SALESMAN, CONO:JOB.SLSMN ELSE
            MAT SALESMAN.REC = ""
         END
      END
      IF JOB.CATG = "" THEN
         MAT JCATG.REC = ""
      END ELSE
         MATREAD JCATG.REC FROM JOB.CATEGORY, CONO:JOB.CATG ELSE
            MAT JCATG.REC = ""
         END
      END
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
         MAT CUST.REC = ""
         CUST.NAME = JBS.CUST.NAME
      END
      MATREAD EMP.REC FROM EMPLOYEE, CONO:PERTS.CURR.EMP ELSE
         MAT EMP.REC = ""
         EMP.FRST.NAME = "???????????????"
      END
      MATREAD OPER.REC FROM OPERATION, CONO:PERTS.CURR.OPER ELSE
         MAT OPER.REC = ""
         OPER.DESC = "???????????????"
      END
      MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:"I" ELSE MAT ESTM.REC = ""
*
      LOCATE WCOM.WND IN PERTS.ID,1 SETTING WPTR ELSE PERTS.ID<WPTR>=WCOM.WND
      PERTS.JSEL<WPTR> = PERTS.CURR.JOB
      LOCATE WCOM.WND IN PERTS.SEL.ID,1 SETTING SPTR ELSE PERTS.SEL.ID<SPTR>=WCOM.WND
      PERTS.SEL.JOB<SPTR>  = PERTS.CURR.JOB
*
      LCNT = 0
      CCNT = DCOUNT(JBS.CCTR,VM)
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         LCNT = LCNT + 1
         SHRS = INT(JBS.SCH.HRS<1,CPTR>/10+0.5)
         AHRS = INT(JBS.ACT.HRS<1,CPTR>/10+0.5)
         SQTY = INT(JBS.SCH.IMP<1,CPTR>/100)
         AQTY = INT(JBS.ACT.IMP<1,CPTR>/100)
      NEXT CPTR
*
*---- OPEN WINDOW
*
      WWIDTH = 77
      WHEIGHT = 16
      HEAD.CNT = 6
      COL2 = 0
      ROW2 = HEAD.CNT
      WCOM.CAPTION = "JOB STATUS"
      SEQ = WCOM.WND[2,99]
*     WCOM.COL = 0
      WCOM.COL = 4 + (SEQ-1) * 3
*     WCOM.ROW = "C"
      WCOM.ROW = 5 + (SEQ-1) * 2
      WCOM.WIDTH = WWIDTH
      WCOM.HEIGHT = 16
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "IWHITE"
      CALL OPEN.WINDOW(MAT WCOM)
      WCOM.COL = COL2
      WCOM.ROW = ROW2
      CALL SET.HSCROLL.AREA(MAT WCOM)
      WCOM.ROW = ROW2+1
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY DEMOGRAPHIC DATA
*
      WCOM.COL=1; WCOM.ROW=0; WCOM.DATA = "Job #   : ":JOBNO
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=1; WCOM.DATA = "Estimate: ":JOB.EST
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=2; WCOM.DATA = "Quantity: ":OCONV(JOB.QTY<1,1>,"MD0,")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=3; WCOM.DATA = "Category: ":JCATG.DESC"L#22"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=5; WCOM.DATA = JOB.DESC<1,1>
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=0; WCOM.DATA = "Customer : ":CUST.NAME
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=1; WCOM.DATA = "Salesman : ":SALS.NAME
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=2; WCOM.DATA = "Employee : ":EMP.FRST.NAME:" ":EMP.LAST.NAME
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=3; WCOM.DATA = "Operation: ":OPER.DESC
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=4; WCOM.DATA = "Due Date : ":OCONV(JOB.TRACK.DATE<1,4>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY COST CENTER / OPERATION STATUS
*
      COL2 = 48
      MAX2 = 12
      WCOM.DATA = "Operation"
      WCOM.TYPE = "H"
      WCOM.COL = 0
      WCOM.ROW = HEAD.CNT
      WCOM.WIDTH = 31:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      WCOM.ALIGN  = "CH"
      CALL LOAD.DATA(MAT WCOM)             ;* COST CENTER HEADING
*
      WCOM.DATA = " Sch Hr"
      WCOM.TYPE = "H"
      WCOM.COL = 31
      WCOM.ROW = HEAD.CNT
      WCOM.WIDTH = 8:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* SCHEDULED HOURS HEADING
*
      WCOM.DATA = " Sch Qty"
      WCOM.TYPE = "H"
      WCOM.COL = 39
      WCOM.ROW = HEAD.CNT
      WCOM.WIDTH = 9:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* SCHEDULED QUANTITY HEADING
*
      HDR = " % COMPLETE "
      WCOM.DATA = HDR
      WCOM.TYPE = "H"
      WCOM.COL = COL2
      WCOM.ROW = HEAD.CNT
      WCOM.WIDTH = MAX2:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* CHART HEADING
*
      WCOM.DATA = " Rem Hr"
      WCOM.TYPE = "H"
      WCOM.COL = COL2 + MAX2
      WCOM.ROW = HEAD.CNT
      WCOM.WIDTH = 8:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* REMAINING HOURS HEADING
*
      WCOM.DATA = " Rem Qty"
      WCOM.TYPE = "H"
      WCOM.COL = COL2 + MAX2 + 8
      WCOM.ROW = HEAD.CNT
      WCOM.WIDTH = 9:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* REMAINING QUANTITY HEADING
*
      FOR N = 0 TO 4
         WCOM.STYLE = "S"
         WCOM.TYPE  = "H"
         WCOM.COL = COL2+N*(MAX2/4)
         WCOM.ROW = HEAD.CNT+1
         WCOM.WIDTH = 0
         WCOM.HEIGHT = WHEIGHT
         WCOM.FCOLOR = "GREY"
         CALL DRAW.LINE(MAT WCOM)
      NEXT N
*
      ROW = HEAD.CNT
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "??????????"
         END
         ROW = ROW + 1
         WCOM.DATA = " ":CCTR"L#3":" ":CCTR.DESC
         WCOM.TYPE = "HV"
         WCOM.COL = 0
         WCOM.ROW = ROW
         WCOM.WIDTH = 31:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = ""
         CALL LOAD.DATA(MAT WCOM)
         STAT = JBS.CCTR.STATUS<1,CPTR>
         SHRS = INT(JBS.SCH.HRS<1,CPTR>/10+0.5)
         AHRS = INT(JBS.ACT.HRS<1,CPTR>/10+0.5)
         TOTHRS = SHRS + AHRS
         SQTY = INT(JBS.SCH.IMP<1,CPTR>/100)
         AQTY = INT(JBS.ACT.IMP<1,CPTR>/100)
*
         BEGIN CASE
         CASE SHRS = 0 AND AHRS = 0
            SH = SHRS
            SQ = SQTY
            W1 = 0
            W2 = 0
            RH = 0
            RQ = 0
         CASE STAT = "C" AND SHRS >= AHRS
            SH = SHRS
            SQ = SQTY
            W1 = MAX2 * AHRS / SHRS;         C1 = "GREEN"
            W2 = 0
            RH = 0
            RQ = 0
         CASE STAT = "C" AND SHRS < AHRS
            SH = SHRS
            SQ = SQTY
            W1 = MAX2;                       C1 = "GREEN"
            W2 = 0
            RH = 0
            RQ = 0
         CASE SHRS >= AHRS AND SQTY = 0
            SH = SHRS
            SQ = SQTY
            W1 = MAX2 * AHRS / SHRS;         C1 = "IBLUE"
            W2 = 0
            RH = SHRS - AHRS
            RQ = 0
         CASE SHRS < AHRS AND SQTY = 0
            SH = SHRS
            SQ = SQTY
            W1 = MAX2;                       C1 = "IBLUE"
            W2 = 0
            RH = SHRS - AHRS
            RQ = 0
         CASE SQTY = 0 OR AQTY = 0
            SH = SHRS
            SQ = SQTY
            W1 = 0
            W2 = 0
            RH = SHRS - AHRS
            RQ = SQTY - AQTY
         CASE SQTY >= AQTY
            SH = SHRS
            SQ = SQTY
            W1 = MAX2 * AQTY / SQTY;         C1 = "IBLUE"
            W2 = 0
            RH = (AHRS * SQTY / AQTY) - AHRS
            RQ = SQTY - AQTY
         CASE SQTY < AQTY
            SH = SHRS
            SQ = SQTY
            IF SQTY = 0 THEN
               W1 = 0
               W2 = 0
            END ELSE
               W1 = MAX2;                    C1 = "IBLUE"
               W2 = 0
            END
            RH = (AHRS * SQTY / AQTY) - AHRS
            RQ = SQTY - AQTY
         END CASE
*
         WCOM.DATA = OCONV(SH,"MD1")"R#7"
         WCOM.TYPE = "HV"
         WCOM.COL = 31
         WCOM.ROW = ROW
         WCOM.WIDTH = 8:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = ""
         CALL LOAD.DATA(MAT WCOM)
*
         IF SQ > 0 THEN
            LSQ = LEN(SQ)
            BEGIN CASE
            CASE LSQ < 7
               WCOM.DATA = OCONV(SQ,"MD0,")"R#8"
            CASE LSQ = 7
               WCOM.DATA = SQ[1,4]:",":SQ[5,3]
            CASE LSQ = 8
               WCOM.DATA = OCONV(SQ,"MD0")"R#8"
            CASE 1
               WCOM.DATA = "########"
            END CASE
            WCOM.TYPE = "HV"
            WCOM.COL = 39
            WCOM.ROW = ROW
            WCOM.WIDTH = 9:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = "WHITE"
            WCOM.RCOLOR = "BLACK"
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
         END
*
         IF W1 > 0 THEN
            WCOM.DATA = ""
            WCOM.TYPE = "HV"
            WCOM.LEVEL = 5
            WCOM.COL = COL2+0.2
            WCOM.ROW = ROW+0.1
            WCOM.WIDTH = W1:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "WHITE"
            WCOM.BCOLOR = "GREY"
            WCOM.RCOLOR = "GREY"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 1
            WCOM.DATA = ""
            WCOM.TYPE = "HV"
            WCOM.LEVEL = 10
            WCOM.COL = COL2
            WCOM.ROW = ROW
            WCOM.WIDTH = W1:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = C1
            WCOM.RCOLOR = "BLACK"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 1
         END
         IF W2 > 0 THEN
            WCOM.DATA = ""
            WCOM.TYPE = "HV"
            WCOM.LEVEL = 5
            WCOM.COL = COL2+W1+0.2
            WCOM.ROW = ROW+0.1
            WCOM.WIDTH = W2:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "GREY"
            WCOM.BCOLOR = "GREY"
            WCOM.RCOLOR = "GREY"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 2
            WCOM.DATA = ""
            WCOM.TYPE = "HV"
            WCOM.LEVEL = 10
            WCOM.COL = COL2+W1
            WCOM.ROW = ROW
            WCOM.WIDTH = W2:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = C2
            WCOM.RCOLOR = "BLACK"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 2
         END
*
         IF RH # 0 THEN
            WCOM.TYPE = "HV"
            WCOM.COL = COL2 + MAX2
            WCOM.ROW = ROW
            WCOM.WIDTH = 8:XADJ
            IF RH > 0 THEN
               WCOM.DATA = OCONV(RH,"MD1")"R#7"
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
            END ELSE
               WCOM.DATA = OCONV((0-RH),"MD1")"R#7"
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "IRED"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
            END
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
         END
*
         IF RQ # 0 THEN
            WCOM.TYPE = "HV"
            WCOM.COL = COL2 + MAX2 + 8
            WCOM.ROW = ROW
            WCOM.WIDTH = 9:XADJ
            LRQ = LEN(ABS(RQ))
            BEGIN CASE
            CASE LRQ < 7
               WCOM.DATA = OCONV(ABS(RQ),"MD0,")"R#8"
            CASE LRQ = 7
               WCOM.DATA = ABS(RQ)[1,4]:",":ABS(RQ)[5,3]
            CASE LRQ = 8
               WCOM.DATA = OCONV(ABS(RQ),"MD0")"R#8"
            CASE 1
               WCOM.DATA = "########"
            END CASE
            WCOM.HEIGHT = 1:YADJ
            WCOM.BCOLOR = "WHITE"
            WCOM.RCOLOR = "BLACK"
            IF RQ > 0 THEN
               WCOM.FCOLOR = "BLACK"
            END ELSE
               WCOM.FCOLOR = "IRED"
            END
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
         END
      NEXT CPTR
*
*---- DISPLAY COMPONENT DETAIL
*
      CCNT = DCOUNT(EST.PROD.DESC,VM)
      FOR COMP = 1 TO CCNT
         WCOM.TYPE = "H"
         WCOM.COL = COMP*WWIDTH
         WCOM.ROW = ROW2
         WCOM.WIDTH = WWIDTH
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "GREY"
         WCOM.RCOLOR = "BLACK"
         WCOM.DATA = "Component ":COMP:" of ":CCNT
         WCOM.ALIGN = "CH"
         CALL LOAD.DATA(MAT WCOM)
         GOSUB 1000
         IF COMP = 1 THEN GOSUB 3000
         FOR PPTR = 1 TO PCNT
            IF PREC<PPTR>[1,1] = "*" THEN
               WCOM.TYPE = "HV"
               WCOM.COL = COMP*WWIDTH
               WCOM.ROW = ROW2+PPTR
               WCOM.WIDTH = WWIDTH
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
               WCOM.DATA = PREC<PPTR>[2,99]
               WCOM.ALIGN = "CH"
               CALL LOAD.DATA(MAT WCOM)
            END ELSE
               WCOM.TYPE = "HV"
               WCOM.COL = COMP*WWIDTH
               WCOM.ROW = ROW2+PPTR
               WCOM.DATA = PREC<PPTR>
               CALL LOAD.DATA(MAT WCOM)
            END
         NEXT PPTR
         IF COMP > 1 THEN
            WCOM.TYPE = "H"
            WCOM.COL = COMP*WWIDTH
            WCOM.ROW = ROW2+1
            WCOM.WIDTH = 0
            WCOM.HEIGHT = 16-HEAD.CNT-1
            CALL DRAW.LINE(MAT WCOM)
         END
      NEXT COMP
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- FORMAT DATA
*
1000*
      PREC = ""; PCNT = 0
      IF EST.PROD.OS.TYPE<1,COMP> # "" THEN
         WEB.CNT = EST.PROD.WEB.CNT<1,COMP>+0
         IF WEB.CNT = 0 THEN WEB.CNT = 1
         FOR WPTR = 1 TO WEB.CNT
            X1 = EST.PROD.US.WIDTH<1,COMP,WPTR>/10000
            X2 = "x":EST.PROD.US.LENGTH<1,COMP,WPTR>/10000
            X3 = EST.PROD.DESC<1,COMP>
            IF WPTR = 1 THEN
               PCNT = PCNT + 1
               IF X3 = COMP THEN
                  PREC<PCNT> = ""
               END ELSE
                  PREC<PCNT> = " ":X3:" - "
               END
            END ELSE
               PCNT = PCNT + 1
               PREC<PCNT> = ("Web ":WPTR:": ")"R#12"
            END
            PREC<PCNT> = PREC<PCNT>:"Flat Size: ":X1:X2
            X1 = " ":EST.PROD.TS.WIDTH<1,COMP,WPTR>/10000
            X2 = "x":EST.PROD.TS.LENGTH<1,COMP,WPTR>/10000
            PREC<PCNT> = PREC<PCNT>:"; ":"Final Size: ":X1:X2
*
            X1 = EST.PROD.OS.WIDTH<1,COMP,WPTR>/10000
            X2 = EST.PROD.OS.LENGTH<1,COMP,WPTR>/10000
            IF X2 = 0 THEN
               X1 = X1:'"'
               X2 = ""
            END ELSE
               X1 = X1
               X2 = "x":X2
            END
            X3 = " ":EST.PROD.BASIS.WT<1,COMP,WPTR>:" lb"
            X4 = " ":EST.PROD.OS.DESC<1,COMP,WPTR>
            IF EST.PROD.COLOR<1,COMP,WPTR> = "" THEN
               X5 = ""
            END ELSE
               X5 = "; ":EST.PROD.COLOR<1,COMP,WPTR>
            END
            IF EST.PROD.FINISH<1,COMP,WPTR> = "" THEN
               X6 = ""
            END ELSE
               X6 = "; ":EST.PROD.FINISH<1,COMP,WPTR>
            END
            PCNT = PCNT + 1
            PREC<PCNT> = ("Stock: ")"R#12":X1:X2:X3:X4:X5:X6
*
            IF EST.PROD.INV.ID<1,COMP,WPTR> # "" THEN
               X1 = " ":EST.PROD.INV.ID<1,COMP,WPTR>
               PCNT = PCNT + 1
               PREC<PCNT> = ("Product: ")"R#12":X1
            END
*
            IF EST.PROD.PSS.OSS<1,COMP,WPTR>+0 > 1 THEN
               X1 = EST.PROD.PSS.OSS<1,COMP,WPTR>:"-OUT"
               X2 = EST.PROD.PS.WIDTH<1,COMP,WPTR>/10000
               X3 = EST.PROD.PS.LENGTH<1,COMP,WPTR>/10000
               IF X3 = 0 THEN
                  X2 = " to ":X2:'"'
                  X3 = ""
               END ELSE
                  X2 = " to ":X2
                  X3 = "x":X3
               END
               PCNT = PCNT + 1
               PREC<PCNT> = ("Cut: ")"R#12":X1:X2:X3
            END
*
            IF EST.PROD.NO.UP<1,COMP,WPTR> = 1 THEN
               X3 = EST.PROD.FORMS<1,COMP,1>:" ":EST.PROD.PGS.PER.FORM<1,COMP,WPTR>:"pp"
            END ELSE
               X3 = EST.PROD.PG.CNT<1,COMP,WPTR>:"PP ":EST.PROD.NO.UP<1,COMP,WPTR>+0:"-UP"
            END
            XX = EST.PROD.MODE<1,COMP,WPTR>
            BEGIN CASE
            CASE XX = "SS"
               X4 = " S/S"
            CASE XX = "WT"
               X4 = " W/T"
            CASE XX = "WR"
               X4 = " W/R"
            CASE XX = "SW"
               X4 = " S/W"
            CASE XX = "P"
               X4 = " P/F"
            END CASE
            PRESS = FIELD(EST.PROD.PRESS.ID<1,COMP>,"-",1)
            MATREAD CCTR.REC FROM COST.CNTR, CONO:PRESS ELSE
               MAT CCTR.REC = ""
               CCTR.DESC = "???????????????"
            END
            X5 = " on ":TRIM(CCTR.DESC)
            X6 = "; ":EST.PROD.COLORS.1<1,COMP,WPTR>+0:"/":EST.PROD.COLORS.2<1,COMP,WPTR>+0
            X7 = "; ":EST.PROD.PLATES<1,COMP,WPTR>+0:" Plates"
            PCNT = PCNT + 1
            PREC<PCNT> = ("Run: ")"R#12":X3:X4:X5:X6:X7
*
            ICNT = DCOUNT(EST.PROD.INK.ID<1,COMP,WPTR>,"!")
            FOR IPTR = 1 TO ICNT
               INK.ID = FIELD(EST.PROD.INK.ID<1,COMP,WPTR>,"!",IPTR)
               LOCATE INK.ID IN ESTM.PROD<1>,1 SETTING IP THEN
                  IDESC = ESTM.DESC<1,IP>
               END ELSE
                  IDESC = INK.ID
               END
               SIDES = FIELD(EST.PROD.INK.SIDES<1,COMP,WPTR>,"!",IPTR)
               COVER = FIELD(EST.PROD.INK.COVER<1,COMP,WPTR>,"!",IPTR)
               IF SIDES = 1 THEN
                  SDMSG = " side  at "
               END ELSE
                  SDMSG = " sides at "
               END
               IF IPTR = 1 THEN
                  PCNT = PCNT + 1
                  PREC<PCNT> = ("Ink: ")"R#12":IDESC"L#30":"  ":SIDES:SDMSG:COVER:"% coverage"
               END ELSE
                  PCNT = PCNT + 1
                  IF SIDES = 1 THEN
                     PREC<PCNT> = """R#12":IDESC"L#30":"  ":SIDES:SDMSG:COVER:"% coverage"
                  END ELSE
                     PREC<PCNT> = """R#12":IDESC"L#30":"  ":SIDES:SDMSG:COVER:"% coverage"
                  END
               END
            NEXT IPTR
         NEXT WPTR
      END
      GOSUB 2000
      RETURN
*
*---- GET COMPONENT INSTRUCTIONS
*
2000*
      DESC = ""
      OP = 0
      CC = DCOUNT(EST.COMMENTS,VM)
      FOR LP = 1 TO CC
         IF EST.COMMENT.TYPE<1,LP> = COMP THEN
            OP = OP + 1
            DESC<1,OP> = EST.COMMENTS<1,LP>
         END
      NEXT LP
      CC = DCOUNT(DESC,VM)
      IF CC > 0 THEN
         PCNT = PCNT + 1
         PREC<PCNT> = "*Component Instructions"
         FOR LP = 1 TO CC
            PCNT = PCNT + 1
            PREC<PCNT> = """L#4":DESC<1,LP>
         NEXT LP
      END
      RETURN
*
*---- BUILD JOB INSTRUCTIONS
*
3000*
      DCNT = DCOUNT(EST.DESC,VM)
      IF DCNT > 0 THEN
         PCNT = PCNT + 1
         PREC<PCNT> = "*Job Instructions"
         FOR DPTR = 1 TO DCNT
            PCNT = PCNT + 1
            PREC<PCNT>="""L#4":EST.DESC.HEADING<1,DPTR>"L#12":SPACE(2):EST.DESC<1,DPTR>
         NEXT DPTR
      END
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA=WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- TRACE ROUTINE
*
95000*
      READU TRACE.REC FROM CONTROL,"WINCOM.TRACE" ELSE TRACE.REC=""
      TRPTR = DCOUNT(TRACE.REC,AM)+1
      TRACE.REC<TRPTR> = TREC
      FOR XPTR = 101 TO TRPTR
         TRACE.REC = DELETE(TRACE.REC,1,0,0)
      NEXT XPTR
      WRITE TRACE.REC ON CONTROL,"WINCOM.TRACE"
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
