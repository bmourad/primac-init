*********************************************************************
*
* PROGRAM  - HNP400.DRIVER
*
* AUTHOR   - Nick Amendola, NASTech, Inc.
*
* DATE     - 02/07/94
*
* DESCRIPTION
*
* This program is used to start the HNP400 phantom.
*
*********************************************************************
*
      EQU BEL TO CHAR(7)
*
*---- PRE-INITIALIZATION
*
      PROMPT ""
      PROCREAD PARAM ELSE PARAM = ""
      ACTION = PARAM<1>[1,1]
      PCMD = "END"
*
*---- OPEN ALL FILES
*
      OPEN "","CONTROL" TO CONTROL ELSE
         PRINT @(0,23):@(-4):"CANNOT OPEN CONTROL FILE":
         INPUT REPLY,1:
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      DASHES = STR("-",80)
*
      SLINE = @(-1)
      SLINE = SLINE:@(0,0):"PRIMAC"
      SLINE = SLINE:@(20,0):"T I M E / M A T E R I A L   U P D A T E"
      SLINE = SLINE:@(72,0):OCONV(DATE(),"D2/")
      SLINE = SLINE:@(0,1):DASHES
      SLINE = SLINE:@(0,20):DASHES
      SLINE = SLINE:@(0,22):DASHES
      PRINT SLINE:
*
      BEGIN CASE
      CASE ACTION = "I"
      CASE ACTION = "T"
      CASE 1
         READU TREC FROM CONTROL, "HNP400.LOCK" LOCKED
            PRINT @(20,10):"Time/Material Update Process is ACTIVE! ":
         END ELSE
            PRINT @(20,10):"Time/Material Update Process is INACTIVE! ":
            RELEASE CONTROL, "HNP400.LOCK"
         END
         LOOP
            PRINT @(0,21):@(-4):"Enter (I)nitiate, (T)erminate, (E)xit: ":
            INPUT ACTION,3:_
            BEGIN CASE
            CASE ACTION = "E" OR ACTION = "END" OR ACTION = CHAR(27) OR ACTION = "^"
               GOTO 99999
            CASE ACTION = "I"
            CASE ACTION = "T"
            CASE 1
               ACTION = ""
            END CASE
         WHILE ACTION = "" DO
         REPEAT
         PRINT @(0,21):@(-4):
      END CASE
*
*---- MAIN PROCESSING
*
      BEGIN CASE
      CASE ACTION = "I"
         READU TREC FROM CONTROL, "HNP400.LOCK" LOCKED
            PRINT @(20,10):"Time/Material Update phantom is already running! "
         END ELSE
            RELEASE CONTROL, "HNP400.LOCK"
            LOOP
               PRINT @(0,21):@(-4):"Execute as Phantom (Y/N): ":
               INPUT ACTION,3:_
               BEGIN CASE
               CASE ACTION = "END" OR ACTION = CHAR(27) OR ACTION = "^"
                  GOTO 99999
               CASE ACTION = "Y"
                  CDATE = DATE()
                  CTIME = TIME()
                  IF CTIME < 10 THEN CDATE = DATE()
                  XREC = ""
                  XREC<1> = CDATE
                  XREC<2> = CTIME
                  WRITE XREC ON CONTROL, "HNP.PHANTOM.CHECK"
                  CMD = '!udt PHANTOM HNP400 &'
                  UDTEXECUTE CMD CAPTURING SCREENDATA
                  PRINT @(0,21):@(-4):"Please Stand-by ":
                  SLEEP 5
                  DONE=0; CNT=0
                  LOOP
                     READU TREC FROM CONTROL, "HNP400.LOCK" LOCKED
                        DONE = 1
                     END ELSE
                        RELEASE CONTROL, "HNP400.LOCK"
                     END
                  UNTIL CNT = 15 OR DONE DO
                     SLEEP 1
                     CNT = CNT + 1
                     PRINT ".":
                  REPEAT
                  IF DONE THEN
                     PRINT @(20,10):"Time/Material Update phantom initiated successfully! "
                  END ELSE
                     PRINT @(20,10):"Time/Material Update phantom initiation failed! "
                  END
               CASE ACTION = "N"
                  PCMD = "B"
                  GOTO 99999
               CASE 1
                  ACTION = ""
               END CASE
            WHILE ACTION = "" DO
            REPEAT
         END
      CASE "T"
         READU TREC FROM CONTROL, "HNP400.LOCK" LOCKED
            PRINT @(0,21):@(-4):"Please Stand-by ":
            READU TREC FROM CONTROL, "HNP400.CNTL" ELSE TREC = ""
            TREC<1> = "STOP"
            WRITE TREC ON CONTROL, "HNP400.CNTL"
            SLEEP 5
            DONE=0; CNT=0
            LOOP
               READU TREC FROM CONTROL, "HNP400.LOCK" LOCKED
                  NULL
               END ELSE
                  DONE = 1
               END
            UNTIL CNT = 15 OR DONE DO
               SLEEP 1
               CNT = CNT + 1
               PRINT ".":
            REPEAT
            IF DONE THEN
               PRINT @(20,10):"Time/Material Update phantom terminated successfully! "
            END ELSE
               PRINT @(20,10):"Time/Material Update phantom termination pending! "
            END
         END ELSE
            PRINT @(20,10):"Time/Material Update phantom is not running! "
         END
      END CASE
      RELEASE
      PRINT @(0,21):@(-4):"Press <RETURN> to continue":
      INPUT REPLY,1:
*
*---- END OF PROGRAM
*
99999 *
      PROCWRITE PCMD
   END
