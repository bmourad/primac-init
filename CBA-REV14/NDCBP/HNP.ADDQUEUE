     SUBROUTINE HNP.ADDQUEUE(NETADDR, MSG, QUEUE, QPTR, STATUS)
*********************************************************************
*
* PROGRAM  - HNP.ADDQUEUE
*
* AUTHOR   - NICK AMENDOLA - NASTech
*
* DATE     - 10/29/93
*
* DESCRIPTION
*
* This program is used to append the specified message to the end of
* the specified queue.
*
* Each remote network is identified by its unique address. The
* pointer within the specified queue is returned in QPTR.
*
*********************************************************************
*
*---- DEFINE ARRAYS
*
      DIM QREC(5)
      EQU QFPTR TO QREC(1)
      EQU QBPTR TO QREC(2)
      EQU QSIZE TO QREC(3)
*
*---- INITIALIZATION
*
      STATUS = ""
*
*---- MAIN PROCESSING
*
      MATREADU QREC FROM QUEUE, NETADDR ELSE
         MAT QREC = ""
         QFPTR = 1; QBPTR = 1; QSIZE = 30000
      END
*
      BEGIN CASE
      CASE (QFPTR - QBPTR) = 1
         STATUS = "QUEUE FULL"
      CASE QFPTR = 1 AND QBPTR = QSIZE
         STATUS = "QUEUE FULL"
      CASE QBPTR < 1 OR QBPTR > QSIZE
         STATUS = "QUEUE POINTER ERROR"
      END CASE
*
      IF STATUS = "" THEN
         CDATE=DATE(); CTIME=TIME(); IF CTIME < 10 THEN CDATE=DATE()
         MREC = ""
         MREC<1> = CDATE
         MREC<2> = CTIME
         MREC<3> = MSG
         WRITE MREC ON QUEUE, NETADDR:"!":QBPTR
         QPTR = QBPTR
         IF QBPTR = QSIZE THEN QBPTR = 1 ELSE QBPTR = QBPTR + 1
         MATWRITE QREC ON QUEUE, NETADDR
      END ELSE
         RELEASE QUEUE, NETADDR
      END
      RETURN
   END
