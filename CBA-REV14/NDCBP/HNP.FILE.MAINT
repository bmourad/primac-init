*********************************************************************
*
* PROGRAM  = HNP.FILE.MAINT
*
* AUTHOR   - NICK AMENDOLA - NASTECH, INC.
*
* DATE     - 02/04/94
*
* DESCRIPTION
*
* This program is used to maintain sequential file data on a specified
* Network controller on the NASTech Data Collection Network.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>NDC.CPYLIB>DOWNLOAD
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- PRE-INITIALIZATION
*
  PROCREAD PARAM ELSE PARAM = ""
  ACTION = PARAM<1>
*
*---- OPEN ALL FILES
*
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","DOWNLOAD" TO DOWNLOAD ELSE
    ERRMSG = "CANNOT OPEN DOWNLOAD FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","CMD.QUEUE" TO CMD.QUEUE ELSE
    ERRMSG = "CANNOT OPEN CMD.QUEUE FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","NDC.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN NDC.SCREENS FILE"
    GOSUB 91000
    STOP
  END
*
*---- INITIALIZATION
*
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;HNP.FILE.MAINT
  SCREEN FORMAT
  SCREEN COUNT;;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  LINE.CNT = 0
  REF.NO = ""
  CURR.REF.NO = ""
  NETID = ""
  FNAME = ""
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  SCREEN CLEAR
  ACTION = PARAM<1>
110 *
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  S$DATA(2)<S$SCR> = NETID
  SCREEN FIELD;;2
  SCREEN INPUT;;2;GOTO 99999
  NETID = S$VALUE+0
  MATREAD DLOAD.REC FROM DOWNLOAD, NETID ELSE
    ERRMSG = "Invalid Network ID. Try again! "
    GOSUB 91000
    GOTO 100
  END
  READU TREC FROM CONTROL, "HNP.SERVER.LOCK.":DLOAD.PORT LOCKED
    NULL
  END ELSE
    RELEASE CONTROL, "HNP.SERVER.LOCK.":DLOAD.PORT
    ERRMSG = "Network Communications not active! "
    GOSUB 91000
    GOTO 100
  END
  MSG = "GETDATE"
  CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
  IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
  GOSUB 85000
  IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
  S$DATA(3)<S$SCR> = FNAME
  SCREEN FIELD;;3
  SCREEN INPUT;;3;GOTO 100
  FNAME = S$VALUE
*
  MSG = "OPEN-SEQ^72^":FNAME:"^I"
  CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
  IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
  GOSUB 85000
  IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
  IF FIELD(RESPONSE,"^",1) = "ERROR" THEN
    ERROR.CODE = FIELD(RESPONSE,"^",2)
    BEGIN CASE
      CASE ERROR.CODE = "53"
        ERRMSG = "Invalid file name. Try again! "
        GOSUB 91000
        FNAME = ""
        GOTO 100
      CASE ERROR.CODE = "55"
        MSG = "CLOSE^72"
        CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
        IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
        MSG = "OPEN-SEQ^72^":FNAME:"^I"
        CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
        IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
        GOSUB 85000
        IF ERRMSG # "" THEN GOSUB 91000; GOTO 100
      CASE 1
        ERRMSG = "ERROR - ":ERROR.CODE
        GOSUB 91000
        FNAME = ""
        GOTO 100
    END CASE
  END
  LINE.CNT = 0
  PCDATA = ""
  ABORT = 0
  PRINT @(0,21):@(-4):"Press <ESCAPE> to abort file transfer.":
  LOOP
    LOOP
      INPUT REQ,-1
    WHILE REQ = 1 DO
      INPUT REQ,1:
      IF REQ = CHAR(27) THEN ABORT = 1
    REPEAT
    IF NOT(ABORT) THEN
      MSG = "READ^72"
      CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
      IF ERRMSG = "" THEN GOSUB 85000
    END
  UNTIL ERRMSG # "" OR FIELD(RESPONSE,"^",2) = "*EOF*" OR ABORT DO
    LINE.CNT = LINE.CNT + 1
    PCDATA<1,LINE.CNT> = FIELD(RESPONSE,"^",2)
    S$DATA(12)<S$SCR> = PCDATA
    REF.NO = LINE.CNT
    START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
    IF START.REF.NO = CURR.REF.NO THEN
      S$VAL = REF.NO
      SCREEN DISPLAY;;11
      S$VAL = REF.NO
      SCREEN DISPLAY;;12
    END ELSE
      GOSUB 50000
    END
  REPEAT
  PRINT @(0,21):@(-4):
  PRINT @(0,21):@(-4):
  IF ERRMSG # "" THEN
    GOSUB 91000
*        GOTO 100
  END
  MSG = "CLOSE^72"
  CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
  GOSUB 85000
*
  SAVE.REC = PCDATA
  GOSUB 81000
  REF.NO = 1
  CURR.REF.NO = ""
  GOSUB 50000
  IF ABORT THEN
    ERRMSG = "File transfer aborted. Press <RETURN> to continue."
    GOSUB 91000
    ACTION = ""
  END
*
*---- GET OPERATOR REPLY
*
500 *
  BEGIN CASE
    CASE ACTION = "M"
      SCREEN FIELD;;21
      SCREEN INPUT;;21
    CASE 1
      SCREEN FIELD;;23
      SCREEN INPUT;;23
  END CASE
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      IF ACTION = "M" THEN
        MISMATCH = 0
        LC = DCOUNT(SAVE.REC,VM)
        IF LC < LINE.CNT THEN LC = LINE.CNT
        FOR N = 1 TO LC
          IF PCDATA<1,N> # SAVE.REC<1,N> THEN MISMATCH = 1
        NEXT N
        IF MISMATCH THEN
          SCREEN FIELD;;25
          SCREEN INPUT;;25
          IF S$VALUE # "Y" THEN GOTO 500
        END
      END
      GOTO 100
    CASE OPT = "A" AND LINE.CNT < 99
      MODE = "A"
      DONE = 0
      FOR REF.NO = LINE.CNT+1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 700
        END ELSE
          LINE.CNT = LINE.CNT + 1
        END
      NEXT REF.NO
      REF.NO = LINE.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND LINE.CNT > 0
      MODE = "C"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10000
        IF S$VALUE = "END" THEN
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "D" AND LINE.CNT > 0
      MODE = "D"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        LINE.CNT = LINE.CNT - 1
        IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "I" AND LINE.CNT > 0
      MODE = "I"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 800
        LINE.CNT = LINE.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          GOSUB 700
          LINE.CNT = LINE.CNT - 1
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "S" OR OPT = "SF"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "SR"
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "ST"
      REF.NO = 1
      GOSUB 50000
    CASE OPT = "SB"
      REF.NO = LINE.CNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "P"
      SCREEN FIELD;;24
      SCREEN INPUT;;24
      IF S$VALUE = "Y" THEN
        MSG = "DELETE^":FNAME
        CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
        GOSUB 85000
        IF ERRMSG # "" THEN
          GOSUB 91000
        END
        GOTO 100
      END
    CASE OPT = "PRI"
      PRINTER ON
      PAGE.NO = 0
      LINES = 99
      PDATE = OCONV(DATE(),"D2-")
      PTIME = OCONV(TIME(),"MTS")"L#5"
      FOR LPTR = 1 TO LINE.CNT
        IF LINES > 55 THEN GOSUB 70000
        PLINE = LPTR"R#4"
        PLINE = PLINE:"  ":S$DATA(12)<S$SCR,LPTR>
        PRINT PLINE
        LINES = LINES + 1
      NEXT N
      PRINTER OFF
      PRINTER CLOSE
      ERRMSG = "Completed. Press <RETURN> to continue."
      GOSUB 91000
    CASE OPT = "F"
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET LINE NUMBER
*
600 *
  SCREEN FIELD;;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
  SCREEN INPUT;;22
  RETURN
*
*---- DELETE MULTI-LINE DATA
*
700 *
  PCDATA = DELETE(PCDATA,1,REF.NO,0)
  GOSUB 81000
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
800 *
  PCDATA = INSERT(PCDATA,1,REF.NO,0,"")
  GOSUB 81000
  RETURN
*
*---- GET MULTI-LINE DATA
*
10000 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;11
10100 *
  S$VAL = REF.NO
  SCREEN FIELD;;12
  SCREEN INPUT;;12;GOTO 19950
  TEMP.MF1 = S$VALUE
19900 *
  PCDATA<1,REF.NO> = TEMP.MF1
  RETURN
19950 *
  GOSUB 81000
  RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
  SCREEN MULTI;;C;11;12
  RETURN
*
*---- PRINT PAGE HEADING
*
70000 *
  PAGE.NO = PAGE.NO + 1
  PRINT CHAR(12)
  HLINE = ("Network ID: ":NETID)"L#30"
  HLINE = HLINE:("File Name: ":FNAME)"L#60"
  HLINE = HLINE:(PDATE:"  ":PTIME)"L#30"
  HLINE = HLINE:("Page: ":PAGE.NO)
  PRINT HLINE
  PRINT
  PRINT
  SH1 = "Line"
  SH1 = SH1:"  ":SPACE(20):"File Contents"
  SH2 = "----"
  SH2 = SH2:"  ":STR("-",120)
  PRINT SH1
  PRINT SH2
  LINES = 6
  RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
  S$DATA(1)<S$SCR> = DATE()
80050 *
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
  S$DATA(12)<S$SCR> = PCDATA
  RETURN
*
*---- GET NETWORK RESPONSE
*
85000 *
  ERRMSG = ""
  STIME = TIME()
  DTIME = 4
  LOOP
    READ REC FROM CMD.QUEUE, NETID:"!":QPTR ELSE REC = ""
    ETIME = TIME() - STIME
    IF ETIME < 0 THEN ETIME = ETIME + 86400
  WHILE REC<4> = "" AND ETIME < DTIME DO
  REPEAT
  IF REC<4> = "" THEN
    ERRMSG = "NETWORK TIMEOUT"
  END ELSE
    IF REC<6>[1,9] = "GETSTATS" THEN
      RESP = REC<6>[10,999]
    END ELSE
      RESP = REC<6>
    END
    RESPONSE = ""
    FOR N = 1 TO LEN(RESP)
      CH = RESP[N,1]
      CV = SEQ(CH)
      IF CV >= 32 AND CV <= 127 THEN
        RESPONSE = RESPONSE : CH
      END
    NEXT N
  END
  RETURN
*
*---- ERROR ROUTINE
*
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
91000 *
  ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
  ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
  ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
99999 *
  PRINT @(-1)
END
