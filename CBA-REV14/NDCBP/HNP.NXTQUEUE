     SUBROUTINE HNP.NXTQUEUE(NETADDR, MSG, QUEUE, QPTR, STATUS)
*********************************************************************
*
* PROGRAM  - HNP.NXTQUEUE
*
* AUTHOR   - NICK AMENDOLA - NASTech
*
* DATE     - 10/29/93
*
* DESCRIPTION
*
* This program is used to retrieve the message following that which
* is referenced by QPTR from the specified queue.
*
*********************************************************************
*
*---- DEFINE ARRAYS
*
      DIM QREC(5)
      EQU QFPTR TO QREC(1)
      EQU QBPTR TO QREC(2)
      EQU QSIZE TO QREC(3)
      EQU QNEXT TO QREC(4)
*
*---- INITIALIZATION
*
      MSG = ""
      STATUS = ""
*
*---- MAIN PROCESSING
*
      MATREADU QREC FROM QUEUE, NETADDR ELSE RETURN
      DONE = 0
      LOOP
         IF QPTR > QSIZE THEN QPTR = 1
      UNTIL DONE DO
         BEGIN CASE
         CASE QFPTR = QBPTR
            DONE = 1
         CASE QFPTR < 1 OR QFPTR > QSIZE
            STATUS = "QUEUE POINTER ERROR"
            DONE = 1
         CASE QPTR = QBPTR
            DONE = 1
         CASE 1
            READV MSG FROM QUEUE, NETADDR:"!":QPTR,3 ELSE MSG = ""
            IF MSG = "" THEN
               QPTR = QPTR + 1
            END ELSE
               QNEXT = QPTR
               DONE = 1
            END
         END CASE
      REPEAT
      MATWRITE QREC ON QUEUE, NETADDR
      RETURN
   END
