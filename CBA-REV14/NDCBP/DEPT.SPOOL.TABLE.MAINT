*COPY>CPYLIB>COM1
*********************************************************************
* REVISION     - [11]
* Copyright 1993 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - NDCBP
* PROGRAM      - DEPT.SPOOL.TABLE
* BY           - BLAIR DICKERSON, COMPUTER BUSINESS ASSOCIATES
* DATE         - 01/26/93
* DESCRIPTION  - 
*                This program enables the printing of the SHIFT
*                SUMMARY REPORT to be printed on a specified 
*                printer within each department.
*
*
*T25448 gat 12/07/2000 * FIX PROBLEMS WITH FORMS SCAPE
*T25978 adelgado 02/01/2002 * Add the use of prompts (SF,SR,SB,ST).
*T29032 cmyklebu 12/28/2006 * Move pgm from rev12 to rev14.
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>JCS.CPYLIB>DEPT.SPOOL.TABLE
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>PMC_PRINTERS
*COPY>CPYLIB>CHAR
*
******************
* INITIALIZATION *
******************
  ERRMSG = ''
  DEPTNO = ''
  DDESC  = ''
  QUEUE  = ''
  RTRN = ''
  VALID  = ''
  LINE.SPACE = 1
  PAGE.SIZE  = 15
  BEGIN.PAGE = 5
  LN = 1
  CT = 1
  N = 0
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  PROCREAD X ELSE
    ERRMSG = "MUST RUN FROM A PROC"
    GOTO 93000
  END
  CONO = X<1>
**************
* OPEN FILES *
**************
  MAT FILE.VARS = ''
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','DEPARTMENT' TO DEPARTMENT ELSE
    ERRMSG = 'DEPARTMENT FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','DEPT.SPOOL.TABLE' TO DEPT.SPOOL.TABLE ELSE
    ERRMSG = 'DEPT.SPOOL.TABLE FILE IS MISSING'
    GOTO 93000
  END
* T29032 v
* OPEN '','REV12A.SCREENS' TO M.SCREENS ELSE
*   ERRMSG = 'REV12A.SCREENS FILE IS MISSING'
*   GOTO 93000
* END
  OPEN '','NDC.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'NDC.SCREENS FILE IS MISSING'
    GOTO 93000
  END
* T29032 ^
  OPEN '','PMC_PRINTERS' TO PMC_PRINTERS ELSE
    ERRMSG = 'PMC_PRINTERS FILE IS MISSING'
    GOTO 93000
  END
**************
* GET SCREEN *
**************
  MAT EDIT.COM.DRIVER = ''
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'DEPT.SPOOL.TABLE.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  ECD.ACTION=2;CALL SCRN.EDIT
  MAT SCV.REC = ""
  ECD.ACTION=6;CALL SCRN.EDIT
  LINES=0;START.LINE=0;OLD.START.LINE=0;LAST.LINE=0
*
  FND = 0
  LOOP
    READNEXT ID ELSE FND = 1
  WHILE FND = 0 DO
    IF ID[1,3] = CONO THEN
      MATREAD DST.REC FROM DEPT.SPOOL.TABLE, ID ELSE GOTO 100
      MATREAD DEPT.REC FROM DEPARTMENT, ID ELSE DEPT.DESC = "UNKNOWN"
      DEPTNO<CT> = ID[4,99]
      DDESC<CT>  = DEPT.DESC
      QUEUE<CT>  = DST.SP.QUEUE
      SCV.REC(2)<ECD.SCRN.NO,CT> = DEPTNO<CT>
      SCV.REC(3)<ECD.SCRN.NO,CT> = DDESC<CT>
      SCV.REC(4)<ECD.SCRN.NO,CT> = QUEUE<CT>
      CT = CT + 1
    END
100*
  REPEAT
*
  LINES = DCOUNT(DEPTNO,AM)
  LINES = CT - 1
  IF LINES = 0 THEN
    OPTION = "A"
    GOSUB 1500
  END ELSE
    GOSUB 8000
  END
  N = N - 1
*******************
* MAIN PROCESSING *
*******************
*
**** PROMPT LINE
*
1000 *
  ECD.NUM = 5
  ECD.ACTION=4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "E" OR OPTION = "END"
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      GOTO 99999
    CASE OPTION = "A"
      LOOP
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 1500
      WHILE LINES > OLD.LINES DO REPEAT
      LN = LINES
    CASE OPTION = "D"
      GOSUB 1600
      IF LNO THEN
        LN = LNO
        DELETE DEPT.SPOOL.TABLE,CONO:DEPTNO<ECD.RET.VALUE>
        DEPTNO = DELETE(DEPTNO,ECD.RET.VALUE,0,0)
        DDESC = DELETE(DDESC,ECD.RET.VALUE,0,0)
        QUEUE = DELETE(QUEUE,ECD.RET.VALUE,0,0)
        LINES = DCOUNT(DEPTNO,AM)
        IF LN > LINES THEN LN = LINES
        IF LN < 1 THEN LN = 1
        OLD.START.LINE = 0
        GOSUB 8000
        N = N - 1
        GOTO 1000
      END
    CASE OPTION = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
      GOSUB 8000
      N = N - 1
      GOTO 1000
    CASE OPTION = 'SR'
      LN -= PAGE.SIZE
      IF LN < 1 THEN LN = 1
      GOSUB 8000
      N -= 1
      GOTO 1000
    CASE OPTION = 'ST'
      LN = 1
      GOSUB 8000
      N -= 1
      GOTO 1000
    CASE OPTION = 'SB'
      LN = LINES
      GOSUB 8000
      N -= 1
      GOTO 1000
    CASE OPTION = "F"
      DCT = DCOUNT(DEPTNO,AM)
      FOR XX=1 TO DCT
        WRITE QUEUE<XX> ON DEPT.SPOOL.TABLE,CONO:DEPTNO<XX>
      NEXT XX
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      GOTO 99999
    CASE OPTION = "C"
      GOSUB 1600
      IF LNO THEN LN = LNO ELSE GOTO 1000
      GOSUB 1500
  END CASE
  GOTO 1000
*
*--- ENTER DATA
*
1500 *
  ECD.NUM = 1; ECD.SUB.NUM = LN
  SCV.REC(ECD.NUM)<ECD.SCRN.NO,LN> = LN "R%3"
  ECD.ACTION = 5; CALL SCRN.EDIT
*
* --- ENTER A VALID DEPT
*
  ECD.NUM = 2; ECD.SUB.NUM = LN
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "E" OR ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "^"
      IF OPTION = "A" THEN
*        SCV.REC(ECD.NUM)<ECD.SCRN.NO,LN> = ""
*        ECD.ACTION = 5; CALL SCRN.EDIT
        ECD.NUM = 1; ECD.SUB.NUM = LN
        SCV.REC(ECD.NUM)<ECD.SCRN.NO,LN> = ""
        ECD.ACTION = 5; CALL SCRN.EDIT
      END
      OLD.START.LINE = 0
      GOTO 1599
    CASE 1
      LOCATE ECD.RET.VALUE IN DEPTNO,1 SETTING POS THEN
        IF POS # LN THEN
          ERRMSG = 'DEPARTMENT HAS ALREADY BEEN ENTERED ON LINE ':POS
          GOSUB 91000
          GOTO 1500
        END
      END
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:ECD.RET.VALUE ELSE
        ERRMSG = "Not a valid Department"
        GOSUB 91000
        GOTO 1500
      END
      TEMP.DEPTNO = ECD.RET.VALUE
  END CASE
*
*--- READ DEPT DESC
*
  TEMP.DDESC = DEPT.DESC
  ECD.NUM = 3; ECD.SUB.NUM = LN
  SCV.REC(ECD.NUM)<ECD.SCRN.NO,LN> = TEMP.DDESC
  ECD.ACTION = 5; CALL SCRN.EDIT
700 *
  ECD.NUM = 4; ECD.SUB.NUM = LN
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "^" OR ECD.RET.VALUE = "E" OR ECD.RET.VALUE = "END" THEN
    IF OPTION = "A" THEN
     ECD.NUM = 2; ECD.SUB.NUM = LN
     SCV.REC(ECD.NUM)<ECD.SCRN.NO,LN> = ""
     ECD.ACTION = 5; CALL SCRN.EDIT
     ECD.NUM = 3; ECD.SUB.NUM = LN
     SCV.REC(ECD.NUM)<ECD.SCRN.NO,LN> = ""
     ECD.ACTION = 5; CALL SCRN.EDIT
    END
    GOTO 1500
  END
  MATREAD PRTRS.REC FROM PMC_PRINTERS, ECD.RET.VALUE ELSE
    ERRMSG = ECD.RET.VALUE:" is not a valid Spooler Queue"
    GOSUB 91000
    GOTO 700
  END
  QUEUE<LN> = ECD.RET.VALUE
  DEPTNO<LN> = TEMP.DEPTNO
  DDESC<LN> = TEMP.DDESC
1599*
  LINES = DCOUNT(DEPTNO,AM)
RETURN
*
*--- GET LINE NUMBER
*
1600*
GOSUB 8000
ECD.MINV = START.LINE
ECD.MAXV = LAST.LINE
ECD.NUM = 6
ECD.ACTION = 4; CALL SCRN.EDIT
IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
RETURN
*
**** SCROLL
*
8000 *
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE = OLD.START.LINE THEN GOTO 8999
  OLD.START.LINE = START.LINE
  CNT = 1
  FOR N = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    ECD.NUM = 1; ECD.SUB.NUM = N
    SCV.REC(ECD.NUM)<ECD.SCRN.NO,ECD.SUB.NUM> = N "R%3"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 2; ECD.SUB.NUM = N
    SCV.REC(ECD.NUM)<ECD.SCRN.NO,N> = DEPTNO<N>
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 3; ECD.SUB.NUM = N
    SCV.REC(ECD.NUM)<ECD.SCRN.NO,N> = DDESC<N>
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 4; ECD.SUB.NUM = N
    SCV.REC(ECD.NUM)<ECD.SCRN.NO,N> = QUEUE<N>
    ECD.ACTION = 5; CALL SCRN.EDIT
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
8999 *
  RETURN
*
**** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
*
93000 *
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
END
