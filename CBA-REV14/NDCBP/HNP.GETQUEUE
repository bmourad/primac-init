     SUBROUTINE HNP.GETQUEUE(NETADDR, MSG, QUEUE, QPTR, STATUS)
*********************************************************************
*
* PROGRAM  - HNP.GETQUEUE
*
* AUTHOR   - NICK AMENDOLA - NASTech
*
* DATE     - 10/29/93
*
* DESCRIPTION
*
* This program is used to get the next available message from the
* specified queue.
*
*********************************************************************
*
*---- DEFINE ARRAYS
*
      DIM QREC(5)
      EQU QFPTR TO QREC(1)
      EQU QBPTR TO QREC(2)
      EQU QSIZE TO QREC(3)
*
*---- INITIALIZATION
*
      MSG = ""
      STATUS = ""
*
*---- MAIN PROCESSING
*
      DONE = 0
      LOOP
         MATREAD QREC FROM QUEUE, NETADDR ELSE RETURN
      UNTIL DONE DO
         BEGIN CASE
         CASE QFPTR = QBPTR
            DONE = 1
         CASE QFPTR < 1 OR QFPTR > QSIZE
            STATUS = "QUEUE POINTER ERROR"
            DONE = 1
         CASE 1
            READV MSG FROM QUEUE, NETADDR:"!":QFPTR,3 ELSE MSG = ""
            IF MSG = "" THEN
               CALL HNP.DELQUEUE(NETADDR, QUEUE)
            END ELSE
               DONE = 1
            END
         END CASE
      REPEAT
      QPTR = QFPTR
      RETURN
   END
