*********************************************************************
*
* PROGRAM  - HNP.STATION.INQ
*
* AUTHOR   - NICK AMENDOLA, NASTech
*
* DATE     - 11/15/93
*
* DESCRIPTION
*
* This program is used to inquire against a specified STATION at a
* specified NETWORK.
*
* The Station parameters are displayed on the top of the screen.
*
* The data currently being displayed on the remote STATION is
* displayed on the bottom of the screen.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>NDC.CPYLIB>DOWNLOAD
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE
         ERRMSG = "CANNOT OPEN EMPLOYEE FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","CMD.QUEUE" TO CMD.QUEUE ELSE
         ERRMSG = "CANNOT OPEN CMD.QUEUE FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","DOWNLOAD" TO DOWNLOAD ELSE
         ERRMSG = "CANNOT OPEN DOWNLOAD FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","NDC.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN NDC.SCREENS FILE"
         GOSUB 91000
         STOP
      END
*
*---- INITIALIZATION
*
      STYPE = "UNIDATA"
*     STYPE = "MDCSC"
      CONO = ""
      CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
      SCREEN INIT;#
      S$SCR = 1
      SCREEN DEFINE;HNP.STATION.INQ
      SCREEN FORMAT
      SCREEN COUNT;;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      LINE.CNT = 0
      REF.NO = ""
      CURR.REF.NO = ""
      NETID = ""
      STAID = ""
      GOTO 110
*
*---- MAIN PROCESSING
*
100 *
      SCREEN CLEAR
110 *
      PREV.EMP = ""
      PREV.IMSG = ""
      PREV.OMSG = ""
      S$DATA(1)<S$SCR> = DATE()
      SCREEN DISPLAY;;1
      S$DATA(2)<S$SCR> = NETID
      SCREEN FIELD;;2
      SCREEN INPUT;;2;GOTO 99999
      NETID = S$VALUE+0
      MATREAD DLOAD.REC FROM DOWNLOAD, NETID ELSE
         ERRMSG = "Invalid Network ID. Try again! "
         GOSUB 91000
         GOTO 100
      END
      READU TREC FROM CONTROL, "HNP.SERVER.LOCK.":DLOAD.PORT LOCKED
         NULL
      END ELSE
         RELEASE CONTROL, "HNP.SERVER.LOCK.":DLOAD.PORT
         ERRMSG = "Network Communications not active! "
         GOSUB 91000
         GOTO 100
      END
      S$DATA(3)<S$SCR> = STAID
      SCREEN FIELD;;3
      SCREEN INPUT;;3;GOTO 100
      STAID = S$VALUE+0
      MSG = "GETDATE"
      CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
      IF ERRMSG # "" THEN
         GOSUB 91000
         GOTO 100
      END
      GOSUB 85000
      IF ERRMSG # "" THEN
         GOSUB 91000
         GOTO 100
      END
      MATREAD DLOAD.REC FROM DOWNLOAD, NETID ELSE
         MAT DLOAD.REC = ""
         DLOAD.CONO = CONO
      END
*
*---- GET STATION DATA
*
      CODES = ""          ;   SFLDS = "";       REPTS = ""
      CODES<1>  = "09.01" ;   SFLDS<1>  = 4;    REPTS<1>  = 0
      CODES<2>  = "09.02" ;   SFLDS<2>  = 41;   REPTS<2>  = 0
      CODES<3>  = "09.03" ;   SFLDS<3>  = 42;   REPTS<3>  = 0
      CODES<4>  = "09.04" ;   SFLDS<4>  = 43;   REPTS<4>  = 0
      CODES<5>  = "09.05" ;   SFLDS<5>  = 44;   REPTS<5>  = 0
      CODES<6>  = "09.06" ;   SFLDS<6>  = 7;    REPTS<6>  = 0
      CODES<7>  = "09.07" ;   SFLDS<7>  = 0;    REPTS<7>  = 0
      CODES<8>  = "09.08" ;   SFLDS<8>  = 0;    REPTS<8>  = 0
      CODES<9>  = "09.09" ;   SFLDS<9>  = 9;    REPTS<9>  = 1
      CODES<10> = "09.10" ;   SFLDS<10> = 0;    REPTS<10> = 0
      CODES<11> = "09.11" ;   SFLDS<11> = 0;    REPTS<11> = 0
      CODES<12> = "09.12" ;   SFLDS<12> = 11;   REPTS<12> = 1
      CODES<13> = "09.13" ;   SFLDS<13> = 12;   REPTS<13> = 1
      REPTFLAG = 0
*
      GOSUB 1000
      GOSUB 1500
      IF ERRMSG # "" THEN
         GOSUB 91000
         GOTO 100
      END
*
*---- GET OPERATOR REPLY
*
500 *
      SCREEN FIELD;;21
      SCREEN INPUT;;21
      OPT = S$VALUE
510 *
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 100
      CASE OPT = "R"
         PRINT @(0,21):@(-4):"Press <ESCAPE> to stop refresh."
         REPTFLAG = 1
         LOOP
            GOSUB 1000
            GOSUB 1500
            PRINT @(40,21):
            BEGIN CASE
            CASE ERRMSG # ""
               PRINT @(0,21):@(-4):
               GOSUB 91000
               REPTFLAG = 0
            CASE STYPE = "UNIDATA"
               LOOP
                  INPUT REQ,-1
               WHILE REQ = 1 DO
                  INPUT REQ,1:
                  IF REQ = CHAR(27) THEN REPTFLAG = 0
               REPEAT
            CASE STYPE = "MDCSC"
               LOOP
                  INPUT REQ,1: FOR 1 ELSE REQ = ""
               WHILE REQ # "" DO
                  IF REQ = CHAR(27) THEN REPTFLAG = 0
               REPEAT
            END CASE
         WHILE REPTFLAG DO
            SLEEP 2
         REPEAT
         PRINT @(0,21):@(-4):
         PRINT @(0,21):@(-4):           ;* EXTRA PRINT FOR PROCOMM PROBLEM
      END CASE
      GOTO 500
*
*---- SEND STATS REQUEST
*
1000 *
      QPTRS = ""
      ERRMSG = ""
      CCNT = DCOUNT(CODES,AM)
      FOR CPTR = 1 TO CCNT WHILE ERRMSG = ""
         IF SFLDS<CPTR> > 0 THEN
            IF NOT(REPTFLAG) OR REPTS<CPTR> THEN
               MSG = "GETSTATS^":CODES<CPTR>:"^":STAID
               CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
               IF ERRMSG = "" THEN QPTRS<CPTR> = QPTR
            END
         END
      NEXT CPTR
      RETURN
*
*---- DISPLAY SCREEN DATA
*
1500 *
      FOR CPTR = 1 TO CCNT WHILE ERRMSG = ""
         IF (NOT(REPTFLAG) AND SFLDS<CPTR> > 0) OR REPTS<CPTR> THEN
            QPTR = QPTRS<CPTR>
            GOSUB 85000
            IF ERRMSG = "" THEN
               FLD = SFLDS<CPTR>
               S$DATA(FLD)<S$SCR> = RESPONSE
               BEGIN CASE
               CASE FLD = 4
                  SCREEN DISPLAY;;4
               CASE FLD = 7
                  SCREEN DISPLAY;;7
               CASE FLD = 9
                  BEGIN CASE
                  CASE RESPONSE = "" AND PREV.EMP # ""
                     SCREEN DISPLAY;;9
                     S$DATA(10)<S$SCR> = ""
                     SCREEN DISPLAY;;10
                     PREV.EMP = RESPONSE
                  CASE RESPONSE # PREV.EMP
                     SCREEN DISPLAY;;9
                     MATREAD EMP.REC FROM EMPLOYEE, DLOAD.CONO:RESPONSE ELSE
                        MAT EMP.REC = ""
                        EMP.FRST.NAME = "???????????????"
                     END
                     S$DATA(10)<S$SCR> = EMP.FRST.NAME:" ":EMP.LAST.NAME
                     SCREEN DISPLAY;;10
                     PREV.EMP = RESPONSE
                  END CASE
               CASE FLD = 11
                  IF RESPONSE # PREV.OMSG THEN
                     S$DATA(11)<S$SCR> = ""
                     RCNT = DCOUNT(RESPONSE,"^")
                     FOR RPTR = 1 TO RCNT
                        S$DATA(11)<S$SCR,RPTR>=FIELD(RESPONSE,"^",RPTR)
                     NEXT RPTR
                     S$VAL = 1
                     S$CNT = LINE.COUNT
                     SCREEN MULTI;;C;11
                     PREV.OMSG = RESPONSE
                  END
               CASE FLD = 12
                  IF RESPONSE # PREV.IMSG THEN
                     SCREEN DISPLAY;;12
                     PREV.IMSG = RESPONSE
                  END
              CASE FLD = 41
                  SCREEN DISPLAY;;41
               CASE FLD = 42
                  SCREEN DISPLAY;;42
               CASE FLD = 43
                  SCREEN DISPLAY;;43
               CASE FLD = 44
                  SCREEN DISPLAY;;44
               END CASE
            END
         END
      NEXT CPTR
      RETURN
*
*---- GET NETWORK RESPONSE
*
85000 *
      ERRMSG = ""
      STIME = TIME()
      DTIME = 4
      LOOP
         READ REC FROM CMD.QUEUE, NETID:"!":QPTR ELSE REC = ""
         ETIME = TIME() - STIME
         IF ETIME < 0 THEN ETIME = ETIME + 86400
      WHILE REC<4> = "" AND ETIME < DTIME DO
      REPEAT
      IF REC<4> = "" THEN
         ERRMSG = "NETWORK TIMEOUT"
      END ELSE
         IF REC<6>[1,9] = "GETSTATS^" THEN
            RESP = REC<6>[10,999]
         END ELSE
            RESP = REC<6>
         END
         RESPONSE = ""
         FOR N = 1 TO LEN(RESP)
            CH = RESP[N,1]
            CV = SEQ(CH)
            IF CV >= 32 AND CV <= 127 THEN
               RESPONSE = RESPONSE : CH
            END
         NEXT N
      END
      RETURN
*
*---- ERROR ROUTINE
*
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
91000 *
      ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
      ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
      ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
99999 *
      PRINT @(-1)
   END
