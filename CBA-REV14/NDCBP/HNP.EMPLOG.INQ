*********************************************************************
*
* PROGRAM  - HNP.EMPLOG.INQ
*
* AUTHOR   - NICK AMENDOLA, NASTech
*
* DATE     - 01/14/94
*
* DESCRIPTION
*
* This program is used to inquire against a specified Employee.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>NDC.CPYLIB>DOWNLOAD
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE
         ERRMSG = "CANNOT OPEN EMPLOYEE FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","DOWNLOAD" TO DOWNLOAD ELSE
         ERRMSG = "CANNOT OPEN DOWNLOAD FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","CMD.QUEUE" TO CMD.QUEUE ELSE
         ERRMSG = "CANNOT OPEN CMD.QUEUE FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","NDC.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN NDC.SCREENS FILE"
         GOSUB 90000
         STOP
      END
*
*---- INITIALIZATION
*
      CONO = ""
      CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
      SCREEN INIT;#
      S$SCR = 1
      SCREEN DEFINE;HNP.EMPLOG.INQ
      SCREEN FORMAT
      NETID = ""
      EMPID = ""
      GOTO 110
*
*---- MAIN PROCESSING
*
100 *
      SCREEN CLEAR
110 *
      S$DATA(1)<S$SCR> = DATE()
      SCREEN DISPLAY;;1
      S$DATA(2)<S$SCR> = NETID
      SCREEN FIELD;;2
      SCREEN INPUT;;2;GOTO 99999
      NETID = S$VALUE+0
      MATREAD DLOAD.REC FROM DOWNLOAD, NETID ELSE
         ERRMSG = "Invalid Network ID. Try again! "
         GOSUB 90000
         GOTO 100
      END
      READU TREC FROM CONTROL, "HNP.SERVER.LOCK.":DLOAD.PORT LOCKED
         NULL
      END ELSE
         RELEASE CONTROL, "HNP.SERVER.LOCK.":DLOAD.PORT
         ERRMSG = "Network Communications not active! "
         GOSUB 90000
         GOTO 100
      END
      MSG = "GETDATE"
      CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
      IF ERRMSG # "" THEN
         GOSUB 90000
         GOTO 100
      END
      GOSUB 1000
      IF ERRMSG # "" THEN
         GOSUB 90000
         GOTO 100
      END
120 *
      S$DATA(3)<S$SCR> = EMPID
      SCREEN FIELD;;3
      SCREEN INPUT;;3;GOTO 100
      EMPID = S$VALUE
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPID ELSE
         MAT EMP.REC = ""
         EMP.FRST.NAME = "???????????????"
      END
      S$DATA(4)<S$SCR> = EMP.FRST.NAME:" ":EMP.LAST.NAME
      SCREEN DISPLAY;;4
*
      MSG = "GET-KEY^!EMPLOG^":EMPID
      CALL HNP.ADDQUEUE(NETID,MSG,CMD.QUEUE,QPTR,ERRMSG)
      IF ERRMSG # "" THEN
         GOSUB 90000
         GOTO 100
      END
      GOSUB 1000
      IF ERRMSG # "" THEN
         GOSUB 90000
         GOTO 100
      END
*
      REC = FIELD(RESPONSE,"^",2)
      S$DATA(11)<S$SCR> = OCONV(FIELD(REC,"\",4),"D2/")
      S$DATA(12)<S$SCR> = OCONV(FIELD(REC,"\",5),"MTS")[1,5]
      S$DATA(13)<S$SCR> = FIELD(REC,"\",6)
      S$DATA(14)<S$SCR> = FIELD(REC,"\",7)
      S$DATA(15)<S$SCR> = FIELD(REC,"\",8)
      S$DATA(16)<S$SCR> = OCONV(FIELD(REC,"\",9),"D2/")
      S$DATA(17)<S$SCR> = OCONV(FIELD(REC,"\",10),"MTS")[1,5]
      S$DATA(18)<S$SCR> = FIELD(REC,"\",11)
      S$DATA(19)<S$SCR> = OCONV(FIELD(REC,"\",12),"D2/")
      S$DATA(20)<S$SCR> = OCONV(FIELD(REC,"\",13),"MTS")[1,5]
      SCREEN DISPLAY;;ALL
*
*---- GET OPERATOR REPLY
*
500 *
      SCREEN FIELD;;21
      SCREEN INPUT;;21
      OPT = S$VALUE
510 *
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 100
      END CASE
      GOTO 500
*
*---- GET NETWORK RESPONSE
*
1000 *
      ERRMSG = ""
      STIME = TIME()
      DTIME = 4
      LOOP
         READ REC FROM CMD.QUEUE, NETID:"!":QPTR ELSE REC = ""
         ETIME = TIME() - STIME
         IF ETIME < 0 THEN ETIME = ETIME + 86400
      WHILE REC<4> = "" AND ETIME < DTIME DO
      REPEAT
      IF REC<4> = "" THEN
         ERRMSG = "NETWORK TIMEOUT"
      END ELSE
         IF REC<6>[1,9] = "GETSTATS" THEN
            RESP = REC<6>[10,999]
         END ELSE
            RESP = REC<6>
         END
         RESPONSE = ""
         FOR N = 1 TO LEN(RESP)
            CH = RESP[N,1]
            CV = SEQ(CH)
            IF CV >= 32 AND CV <= 127 THEN
               RESPONSE = RESPONSE : CH
            END
         NEXT N
      END
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*---- END OF PROGRAM
*
99999 *
      PRINT @(-1)
   END
