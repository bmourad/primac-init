DOMATREADU: * THIS IS THE READ
**********************************************
* REVISION    - [10.2]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - CPYLIB
* PROGRAM     - INTERNAL SUBROUTINE DOMATREADU
* BY          - rick; CBA
* DATE        - 10/1/96
* DESCRIPTION -
*  this subroutine will handle matread and locking as a generic subroutine
*  also associated with this routine is an init cpylib stored in 
*  the CPYLIB file DOMATREADU_INIT.   In preparation to call DOMATREADU
*  it expects that 6 variable will be set up prior to it being referenced,

*  MAT READ_REC - mat array of dim 500 that contains the record to be read
*  DOREAD_FILEVAR - the filevar of the file to read result of the open stmt
*  DOREAD_FILENAME - string representation of the file being read
*  DOREAD_ID - id of the record to be read
*  DOREAD_WAITSTATE - flag for the locked clause indicating what to do
*                     0-wait without option to quit, 1-wait with option to
*                     quit  default = 0
*  DOREAD_WAITTIME - number of seconds between wait cycles - default = 15
*
*ENDDOC
**********************************************
*   CPYLIBS
*
*   FILE OPENS
*
*   INITIALIZATION SECTION
*
*   BODY OF PROGRAM SECTION
*
*   STANDARD SUBROUTINES
*
91000 PRINT @(0,23):ERRMSG:CL:
      INPUT XX:
      PRINT @(0,23):CL:
      RETURN
99999 RETURN
   END




*
* this subroutine expects that 6 variables will be setup before it is
* called.   
DOREAD_PASSONE=1
DOREAD_DONE = 0
DOREAD_SUCCESSFUL = 1
DOREAD.CHKTIME = TIME() + DOREAD_WAITTIME
LOOP
UNTIL DOREAD_DONE DO
  MATREADU READ_REC FROM DOREAD_FILEVAR,DOREAD_ID LOCKED
    * logic to allow user to readlock exit
    DOREAD.CLOCK=TIME()
    IF DOREAD.CLOCK > DOREAD.CHKTIME THEN
      DOREAD.DOPROMPT=1
      DOREAD.CHKTIME = TIME() + DOREAD_WAITTIME
    END ELSE
      DOREAD.DOPROMPT=0
      IF DOREAD_PASSONE THEN
        SLEEP DOREAD_WAITTIME
        DOREAD_PASSONE=0
        DOREAD.DOPROMPT=1
      END
    END
    BEGIN CASE
      CASE DOREAD_WAITSTATE =  0 AND DOREAD.DOPROMPT
        ERRMSG="Record locked by ":GETUSERNAME(STATUS() ):" File ":DOREAD_FILENAME:" Record ":DOREAD_ID
        ERR.TYPE = 1
        CALL SYSCOM(MAT SYSCOM.REC)
        SLEEP DOREAD_WAITTIME
      CASE DOREAD_WAITSTATE = 1 AND DOREAD.DOPROMPT
        ERRMSG = "Locked by ":GETUSERNAME(STATUS() ):" ":DOREAD_FILENAME:"/":DOREAD_ID
        ERRMSG=ERRMSG:" 'RETURN' to wait or 'Q' to quit:"
        ERR.TYPE=4
        CALL SYSCOM(MAT SYSCOM.REC)
        IF ERR.RESP = 'Q' THEN
          DOREAD_DONE=1
          DOREAD_SUCCESSFUL=0
        END ELSE
          SLEEP DOREAD_WAITTIME
        END
    END CASE
  END THEN
    DOREAD_SUCCESSFUL = 1
    DOREAD_DONE=1
  END ELSE
    DOREAD_SUCCESSFUL = 0
    DOREAD_DONE=1
  END
REPEAT
RETURN  
      
