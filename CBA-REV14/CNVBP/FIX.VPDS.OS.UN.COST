* PROGRAM TO SYNCRONIZE UNIT COST IN VEND.PROD.STATS WHEN X ORD QTY WILL
* YIELD SAME AS AMOUNT ON VEND.PO.STATS.
* PROGRAM DOES SELECT LIST OF 'O' TYPE VEND.PROD.STATS TO FEED THIS PROCESS.
* LMR 11/01/95
CRT 'PROCESSING OUTSIDE PO VPDS RECORDS'
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>CPYLIB>CHAR
PROMPT''
OPEN 'VEND.PO.STATS' TO VEND.PO.STATS ELSE STOP
OPEN 'VEND.PROD.STATS' TO VEND.PROD.STATS ELSE STOP
OPEN 'OUTSIDE.PO' TO OUTSIDE.PO ELSE STOP
UDTEXECUTE 'SELECT VEND.PROD.STATS WITH TYPE = "O"' CAPTURING JUNK
MORE = 1
LOOP
  READNEXT ID ELSE MORE = 0
WHILE MORE DO
  IF FIELD(ID,"!",2) = 'O' THEN
  MATREADU VPDS.REC FROM VEND.PROD.STATS,ID THEN
    JOB = FIELD(ID,"!",4)
    CATG = FIELD(ID,"!",5)
    ORDNO = FIELD(ID,"!",3)
    CONO = ID[1,3]
    VPS.KEY = OCONV(ID,"G!3")
    IF SUM(VPDS.ORD.UN.COST) = 0 AND SUM(VPDS.REC.UN.COST) = 0 AND SUM(VPDS.UN.COST) = 0 THEN GOTO 99
    MATREADU VPS.REC FROM VEND.PO.STATS, VPS.KEY THEN
      PTR=1
      LOOP
        LOCATE JOB IN VPS.PROD<1>,PTR SETTING JFND ELSE JFND = 0
        BEGIN CASE
        CASE JFND = 0
          CRT "CANNOT LOCATE JOB IN VPS FOR ID ":ID:;INPUT XX
          PTR = 0
        CASE CATG # VPS.WHSE<1,JFND>
          PTR = JFND + 1
        CASE 1
          PTR = 0
        END CASE
      WHILE PTR DO REPEAT
      IF JFND THEN
        BEGIN CASE
        CASE VPS.U.M<1,JFND> = 'M'
        CASE 1
          OCNT = DCOUNT(VPDS.ORD.QTY,VM)
          RCNT = DCOUNT(VPDS.REC.QTY,VM)
          VCNT = DCOUNT(VPDS.QTY,VM)
          TEMP.ORD = 0
          FOR I = 1 TO OCNT
            TEMP.ORD = TEMP.ORD + ICONV(VPDS.ORD.QTY<1,I>/100 * VPDS.ORD.UN.COST<1,I>/10000,'MD2')
          NEXT I
          TEMP.REC = 0
          FOR I = 1 TO RCNT
            TEMP.REC = TEMP.REC + ICONV(VPDS.REC.QTY<1,I>/100 * VPDS.REC.UN.COST<1,I>/10000,'MD2')
          NEXT I
          TEMP.VOU = 0
          FOR I = 1 TO VCNT
            FOR II = 1 TO DCOUNT(VPDS.QTY<1,I>,SVM)
              TEMP.VOU = TEMP.VOU + ICONV(VPDS.QTY<1,I,II>/100 * VPDS.UN.COST<1,I,II>/10000,'MD2')
            NEXT II
          NEXT I
          IF TEMP.ORD # VPS.ORD.AMT<1,JFND>+0 OR TEMP.REC # VPS.REC.AMT<1,JFND>+0 OR TEMP.VOU # VPS.VOU.AMT<1,JFND>+0 THEN
            MATREADU OPO.REC FROM OUTSIDE.PO, CONO:ORDNO THEN
              OPO.FND = 1
            END ELSE
              OPO.FND = 0
              RELEASE OUTSIDE.PO, CONO:ORDNO
              MAT OPO.REC = ''
            END
            VPS.U.M<1,JFND> = 'M'
            PTR = 1
            LOOP
              LOCATE JOB IN OPO.JOB.NO<1>,PTR SETTING OJFND ELSE OJFND = 0
              BEGIN CASE
              CASE OJFND = 0
                PTR = 0
              CASE OPO.PROD.LINE<1,OJFND> # CATG
                PTR = OJFND + 1
              CASE 1
                PTR = 0
              END CASE
            WHILE PTR DO REPEAT
            IF OJFND THEN OPO.UOM<1,OJFND> = 'M'
            NONEED = 1
            FOR I = 1 TO OCNT
              IF SUM(VPDS.ORD.QTY) # 0 THEN VPDS.ORD.UN.COST<1,I> = ICONV(VPS.ORD.AMT<1,JFND>/(SUM(VPDS.ORD.QTY)/1000),'MD4')
              IF VPDS.ORD.UN.COST<1,I> + 0 = 0 THEN VPDS.ORD.UN.COST<1,I> = '000'
              IF VPDS.ORD.UN.COST<1,I>[LEN(VPDS.ORD.UN.COST<1,I>)-2,3] # STR('0',3) THEN NONEED = 0
            NEXT I
            IF NONEED THEN
               FOR I = 1 TO RCNT
                 IF SUM(VPDS.REC.QTY) # 0 THEN VPDS.REC.UN.COST<1,I> = ICONV(VPS.REC.AMT<1,JFND>/(SUM(VPDS.REC.QTY)/1000),'MD4')
                 IF VPDS.REC.UN.COST<1,I>+0 = 0 THEN VPDS.REC.UN.COST<1,I> = '000'
                 IF VPDS.REC.UN.COST<1,I>[LEN(VPDS.REC.UN.COST<1,I>)-2,3] # STR('0',3) THEN NONEED = 0
               NEXT I
               IF NONEED THEN
                  FOR I = 1 TO VCNT
                    FOR II = 1 TO DCOUNT(VPDS.QTY<1,I>,SVM)
                      IF SUM(SUM(VPDS.QTY)) # 0 THEN VPDS.UN.COST<1,I,II> = ICONV(VPS.VOU.AMT<1,JFND>/(SUM(SUM(VPDS.QTY))/1000),'MD4')
                      IF VPDS.UN.COST<1,I,II>+0 = 0 THEN VPDS.UN.COST<1,I,II> = '000'
                      IF VPDS.UN.COST<1,I,II>[LEN(VPDS.UN.COST<1,I,II>)-2,3] # STR('0',3) THEN NONEED = 0
                    NEXT II
                  NEXT I
                  IF NONEED THEN GOTO 99
               END
            END
CRT @(-1):ID
CRT 'VPS.ORD.AMT ':VPS.ORD.AMT<1,JFND>
            FOR I = 1 TO OCNT
              IF SUM(VPDS.ORD.QTY) # 0 THEN VPDS.ORD.UN.COST<1,I> = ICONV(VPS.ORD.AMT<1,JFND>/(SUM(VPDS.ORD.QTY)/1000),'MD4')
CRT 'VPDS.ORD.QTY ':I:'= ':VPDS.ORD.QTY<1,I>:' VPDS.ORD.UN.COST = ':VPDS.ORD.UN.COST<1,I>
            NEXT I
CRT 'VPS.REC.AMT ':VPS.REC.AMT<1,JFND>
            FOR I = 1 TO RCNT
              IF SUM(VPDS.REC.QTY) # 0 THEN VPDS.REC.UN.COST<1,I> = ICONV(VPS.REC.AMT<1,JFND>/(SUM(VPDS.REC.QTY)/1000),'MD4')
CRT 'VPDS.REC.QTY ':I:'= ':VPDS.REC.QTY<1,I>:' VPDS.REC.UN.COST = ':VPDS.REC.UN.COST<1,I>
            NEXT I
*           IF OJFND AND OPO.QTY.RECVD<1,OJFND>+0 # 0 THEN OPO.U.PRICE<1,OJFND> = ICONV(OPO.AMT.RECVD<1,OJFND>/(OPO.QTY.RECVD<1,OJFND>/1000),'MD4')
            IF OJFND AND OPO.QTY.RECVD<1,OJFND> # SUM(VPDS.REC.QTY) THEN OPO.QTY.RECVD<1,OJFND> = SUM(VPDS.REC.QTY)
            IF OJFND AND OPO.AMT.RECVD<1,OJFND> # VPS.REC.AMT<1,JFND> THEN OPO.AMT.RECVD<1,OJFND> = VPS.REC.AMT<1,JFND>
            IF OJFND AND OPO.EST.COST+0 # 0 AND OPO.QTY+0 # 0 THEN
               OPO.U.PRICE<1,OJFND> = ICONV(OPO.EST.COST<1,OJFND>/(OPO.QTY<1,OJFND>/1000),'MD4')
            END
            FOR I = 1 TO VCNT
              FOR II = 1 TO DCOUNT(VPDS.QTY<1,I>,SVM)
                IF SUM(SUM(VPDS.QTY)) # 0 THEN VPDS.UN.COST<1,I,II> = ICONV(VPS.VOU.AMT<1,JFND>/(SUM(SUM(VPDS.QTY))/1000),'MD4')
              NEXT II
            NEXT I
CRT 'ACCEPT ? (Y/N) ':;INPUT XX
*XX="Y"
IF XX = 'Y' THEN
  MATWRITE VPDS.REC ON VEND.PROD.STATS,ID
  MATWRITE VPS.REC ON VEND.PO.STATS,VPS.KEY
  IF OPO.FND THEN MATWRITE OPO.REC ON OUTSIDE.PO, CONO:ORDNO
END ELSE
  RELEASE
  IF XX = 'END' THEN STOP
END
          END
        END CASE
      END
    END ELSE RELEASE VEND.PO.STATS, VPS.KEY
  END ELSE RELEASE VEND.PROD.STATS, ID
  END
99 REPEAT
CRT 'DONE'
