*COPY>CPYLIB>CHAR
  PRINT CS
  PRINT 'ENTER THE SCREEN FILE NAME : ':; INPUT SCRN.FILE
  IF SCRN.FILE = '' OR SCRN.FILE = 'END' THEN STOP
  PRINT 'ENTER THE RECORD NAME      : ':; INPUT SCRN.ID
  IF SCRN.ID = '' OR SCRN.ID = 'END' THEN STOP
  *
  OPEN '',SCRN.FILE TO FILE.SCREEN ELSE
    PRINT 'UNABLE TO OPEN ':SCRN.FILE ; STOP
  END
  OPEN '','PMCBARS' TO PMCBARS ELSE
    PRINT 'UNABLE TO OPEN PMCBARS' ; STOP
  END
  OPEN '','FILE.BARS' TO FILE.BARS ELSE
    PRINT 'UNABLE TO OPEN FILE.BARS' ; STOP
  END
  OPEN '','ALEX' TO ALEX ELSE
    PRINT 'UNABLE TO OPEN ALEX' ; STOP
  END
*
* MAIN LOOP
*
  CMD = 'SSELECT ALEX'
  PERFORM CMD
  IF @LOGNAME = 'adelgado' THEN DEBUG
  LOOP.DONE = 0
  LOOP
    READNEXT SCRN.ID ELSE LOOP.DONE = 1
  UNTIL LOOP.DONE DO
    READ FLD.REC FROM FILE.SCREEN, SCRN.ID:'*FLD' THEN
      READ VAL.REC FROM FILE.SCREEN, SCRN.ID:'_VAL' THEN
        READ BAR.REC FROM FILE.SCREEN, SCRN.ID:'*BAR' THEN
          SAVE.REC = 1 ; CHANGE.MADE = 0
          XREF.REC = '' ; ORIG.BAR = BAR.REC
          FOR X = 1 TO 60 UNTIL SAVE.REC = 0
            IF TRIM(FLD.REC<X,22>) # '' THEN
              VALDAT = CHANGE(FLD.REC<X,14>,',',@VM)
              LOCATE 'S' IN VALDAT<1>,1 SETTING POS THEN
                GOSUB CHECK.PROMPT
                FLD.REC<X,14> = CHANGE(VALDAT,@VM,',')
              END
            END
          NEXT X
          IF CHANGE.MADE # 0 THEN
            XREF.REC<3> = SAVE.REC
            WRITE XREF.REC ON FILE.BARS, SCRN.FILE:'*':SCRN.ID
          END ELSE
            SAVE.REC = 0
          END
          IF SAVE.REC = 1 THEN
            WRITE FLD.REC ON FILE.SCREEN, SCRN.ID:'*FLD'
            WRITE VAL.REC ON FILE.SCREEN, SCRN.ID:'_VAL'
            WRITE BAR.REC ON FILE.SCREEN, SCRN.ID:'*BAR'
          END
        END ELSE
          PRINT 'UNABLE TO READ ':SCRN.FILE:' ':SCRN.ID:'*BAR'
        END
      END ELSE
        PRINT 'UNABLE TO READ ':SCRN.FILE:' ':SCRN.ID:'_VAL'
      END
    END ELSE
      PRINT 'UNABLE TO READ *FLD RECORD FOR ':SCRN.ID
    END
  REPEAT
*
  STOP
*
CHECK.PROMPT: 
*
  PRINT CS
  ORIG.FLD = FLD.REC<X>
  FLD.NUM = FLD.REC<61,X>
  IF FLD.REC<X,4> LE 1 THEN
    FLD.REC<X,4> += 1
    CHANGE.MADE = 1
  END
  CNT = DCOUNT(VALDAT,@VM) ; SCROLL.CNT = 0
  FOR CC = 1 TO CNT
    BEGIN CASE
      CASE VALDAT<1,CC> = 'SR'
        SCROLL.CNT += 1
      CASE VALDAT<1,CC> = 'SB'
        SCROLL.CNT += 1
      CASE VALDAT<1,CC> = 'ST'
        SCROLL.CNT += 1
    END CASE
  NEXT CC
  IF SCROLL.CNT LT 3 THEN
    LOCATE 'ST' IN VALDAT<1>,1 SETTING XPOS ELSE
      VALDAT = INSERT(VALDAT,1,POS+1,0,'ST')
    END
    LOCATE 'SB' IN VALDAT<1>,1 SETTING XPOS ELSE
      VALDAT = INSERT(VALDAT,1,POS+1,0,'SB')
    END
    LOCATE 'SR' IN VALDAT<1>,1 SETTING XPOS ELSE
      VALDAT = INSERT(VALDAT,1,POS+1,0,'SR')
    END
    CHANGE.MADE = 2
    PRINT
    PRINT 'ORIGINAL PROMPT >>>'
    PRINT FLD.REC<X,22>
    TMP.PMSG = CHANGE(FLD.REC<X,22>,'(S)croll','(S,SR,SB,ST) to Scroll')
    PRINT
    PRINT 'REVISED PROMPT >>>>'
    PRINT TMP.PMSG
    INPUT PMSG
    BEGIN CASE
      CASE PMSG = ''
        FLD.REC<X,22> = TMP.PMSG
        CHANGE.MADE = 3
      CASE PMSG = 'O'
      CASE 1
        FLD.REC<X,22> = PMSG
        CHANGE.MADE = 3
    END CASE
    *
    LOCATE FLD.NUM IN VAL.REC<1>,1 SETTING BOS THEN
      LOCATE 'SR' IN VALDAT<1>,1 SETTING COS THEN
        IF VAL.REC<2,BOS,COS> # 'SCROLL REV' THEN
          VAL.REC = INSERT(VAL.REC,2,BOS,COS,'SCROLL REV')
          CHANGE.MADE = 4
        END
      END
      LOCATE 'SB' IN VALDAT<1>,1 SETTING COS THEN
        IF VAL.REC<2,BOS,COS> # 'SCROLL BOT' THEN
          VAL.REC = INSERT(VAL.REC,2,BOS,COS,'SCROLL BOT')
          CHANGE.MADE = 4
        END
      END
      LOCATE 'ST' IN VALDAT<1>,1 SETTING COS THEN
        IF VAL.REC<2,BOS,COS> # 'SCROLL TOP' THEN
          VAL.REC = INSERT(VAL.REC,2,BOS,COS,'SCROLL TOP')
          CHANGE.MADE = 4
        END
      END
      LOCATE FLD.NUM IN BAR.REC<1>,1 SETTING SOS THEN
        BEGIN CASE
          CASE BAR.REC<3,SOS> = 'SCROLL1'
            BAR.REC<3,SOS> = 'SCROLL' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLF1'
            BAR.REC<3,SOS> = 'SCROLLF' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLF2'
            BAR.REC<3,SOS> = 'SCROLLF6' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLF4'
            BAR.REC<3,SOS> = 'SCROLLF7' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLI1'
            BAR.REC<3,SOS> = 'SCROLLI' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLM1'
            BAR.REC<3,SOS> = 'SCROLLM' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLN1'
            BAR.REC<3,SOS> = 'SCROLLN' ; CHANGE.MADE = 5
          CASE BAR.REC<3,SOS> = 'SCROLLX1'
            BAR.REC<3,SOS> = 'SCROLLX' ; CHANGE.MADE = 5
        END CASE
      END ELSE
        PRINT 'CANNOT FIND FLD NUM ':FLD.NUM:' IN ':SCRN.FILE:' ':SCRN.ID:'*BAR'
        INPUT ANS
        SAVE.REC = 0
      END
    END ELSE
      PRINT 'CANNOT FIND FLD NUM ':FLD.NUM:' IN ':SCRN.FILE:' ':SCRN.ID:'_VAL'
      INPUT ANS
      SAVE.REC = 0
    END
  END
  IF CHANGE.MADE = 0 THEN
    SAVE.REC = 0
  END ELSE
    GOSUB PRINT.RESULTS
  END
  *
  RETURN
*
PRINT.RESULTS: 
*
  IF SAVE.REC = 0 THEN RETURN
  XREF.REC<1,-1> = FLD.NUM
  XREF.REC<2,-1> = CHANGE.MADE
  PRINT CS
  PRINT 'FILE  > ':SCRN.FILE:' > ':SCRN.ID:@(55):
  PRINT 'FIELD NUMBER > ':FLD.NUM
  PRINT
  PRINT 'FILED LENGTH > ':ORIG.FLD<1,4>:' TO ':FLD.REC<X,4>
  PRINT 
  LOCATE FLD.NUM IN VAL.REC<1>,1 SETTING BOS ELSE NULL
  CNT = DCOUNT(VALDAT,VM)
  FOR CC = 1 TO CNT
    PRINT 'VALDATION    > ':VALDAT<1,CC>'L#7':' > ':VAL.REC<2,BOS,CC>
  NEXT CC
  PRINT
  PRINT 'OLD >>>>'
  PRINT ORIG.FLD<1,22>
  PRINT 'NEW >>>>'
  PRINT FLD.REC<X,22>
  PRINT
  LOCATE FLD.NUM IN BAR.REC<1>,1 SETTING SOS THEN
    PRINT 'BAR NAME CHANGED FROM > ':ORIG.BAR<3,SOS>:' TO ':BAR.REC<3,SOS>
    PRINT
  END
  PRINT 'OKAY TO SAVE : ':;INPUT ANS
  IF ANS # "" THEN SAVE.REC = 0
  *
  RETURN
*
END
