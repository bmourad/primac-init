*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - CNVBP
* PROGRAM     - FIX.AP.REV10A.VCH
* BY          - L ROSS, C.B.A
* DATE        - 03/07/95
* DESCRIPTION - This program is to create missing Voucher records for
*               the A/P account where the vouchers were PAID manually or
*               where they were reversed and the OAP record is no longer
*               available.
*               Do a SELECT ON VOUCHERS FILE WITH VCH_DESC LIKE "...REVERS..." OR "...DELETE..." OR WITH _A4 = "PAID" to feed this program a list.
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>APS.CPYLIB>OAP
*COPY>APS.CPYLIB>SQV
*COPY>APS.CPYLIB>VOUCHERS
*COPY>APS.CPYLIB>MCD
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
DIM HOLD.REC(50)
*
**** INTITIALIZATION
*
     MAT FILE.VARS=''
     MAT EDIT.COM.DRIVER=''
*
***** SETUP ERRMSG ROUTINE
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','OAP' TO OAP ELSE ERRMSG='OAP FILE IS MISSING';GOTO 93000
     OPEN '','SQV' TO SQV ELSE ERRMSG='SQV FILE IS MISSING';GOTO 93000
     OPEN '','VOUCHERS' TO VOUCHERS ELSE ERRMSG='VOUCHERS FILE IS MISSING';GOTO 93000
     OPEN '','VOUCHERS.TAG' TO VOUCHERS.TAG ELSE ERRMSG='VOUCHERS.TAG FILE IS MISSING';GOTO 93000
     OPEN '','MCD' TO MCD ELSE ERRMSG='MCD FILE IS MISSING';GOTO 93000
     OPEN '','MCD.TAG' TO MCD.TAG ELSE ERRMSG='MCD.TAG FILE IS MISSING';GOTO 93000
     OPEN '','COA' TO COA ELSE ERRMSG='COA FILE IS MISSING';GOTO 93000
*
***** GET CONO
*
     CONO = ''
     MAT COMP.REC = ''
     CALL GET.CONO(CONO,MAT COMP.REC)
     IF CONO = 'END' THEN GOTO 99999
     MATREAD GLTABLE.REC FROM CONTROL, CONO:'GLTABLE' ELSE
        ERRMSG = 'CANNOT LOCATE CONTROL, GLTABLE'
        GOTO 93000
     END
     READV CURR.PER FROM CONTROL, CONO:"APVFISCAL",1 ELSE
        ERRMSG = 'APVFISCAL CONTROL RECORD IS MISSING FOR COMPANY ':CONO
        GOTO 93000
     END
     MATREAD COA.REC FROM COA, CONO : GLTB.AP ELSE
       ERRMSG= 'COA RECORD FOR ':GLTB.AP:' IS MISSING'; GOTO 93000
     END
     DATA = 1
     LOOP
        READNEXT ID ELSE DATA = 0
     WHILE DATA DO
        IF CONO # ID[1,3] THEN GOTO 999
* NOW CHECK FOR VOUCHERS TO BE CREATED
        SEQ = 1
        CHPOS = INDEX(ID,'D',1)
        IF NOT(CHPOS) THEN CHPOS = INDEX(ID,'V',1)
        MORE = 1
        LOOP
          KEY = ID[1,9]:SEQ
          IF CHPOS THEN KEY = KEY:ID[CHPOS,2]
          MATREADU VCH.REC FROM VOUCHERS, KEY ELSE MORE = 0
        WHILE MORE DO
          MAT HOLD.REC = MAT VCH.REC
          RELEASE VOUCHERS, KEY
          SEQ = SEQ + 1
        REPEAT
        IF SEQ # 1 THEN
           FOR I = 1 TO VCH.REC.SIZE
             VCH.REC(I) = HOLD.REC(I)
           NEXT I
           VCH.ACCT = GLTB.AP
           VCH.DIST.AMT = VCH.GRS.AMT * (-1)
           VCH.GRS.AMT = VCH.GRS.AMT * (-1)
           VCH.MER.AMT = VCH.MER.AMT * (-1)
           VCH.DSC.AMT = VCH.DSC.AMT * (-1)
           MATWRITE VCH.REC ON VOUCHERS, KEY
           WRITE "" ON VOUCHERS.TAG, KEY
        END ELSE RELEASE VOUCHERS, KEY
*
999  REPEAT
     GOTO 99999
91000 ERR.TYPE = 1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99000 *
*** END OF JOB ***
99999 END
