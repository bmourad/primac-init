*T26090 wyamout 03/13/2002 * PRINT/VIEW/EMAIL
*COPY>PMC.CPYLIB>PMC_PROCESS
*COPY>PMC.CPYLIB>PMC_REPORTS
*COPY>CPYLIB>CHAR
   OPEN '','PMC_PROCESS' TO PMC_PROCESS ELSE PRINT 'UNABLE TO OPEN PMC_PROCESS' ; STOP
   OPEN '','PMC_REPORTS' TO PMC_REPORTS ELSE PRINT 'UNABLE TO OPEN PMC_REPORTS' ; STOP
   OPEN '','OLD_REV12_PROCS' TO OLD_REV12_PROCS ELSE PRINT 'UNABLE TO OPEN OLD_REV12_PROCS' ; STOP
   OPEN '','NEW_REV12_PROCS' TO NEW_REV12_PROCS ELSE PRINT 'UNABLE TO OPEN NEW_REV12_PROCS' ; STOP
   CMD = 'SSELECT PMC_PROCESS WITH PPS_TYPE = "P" BY PPS_DIR BY PPS_PROC'
   UDTEXECUTE CMD
*
   LOOP.DONE = 0
   OLD.FILE.NM = ''
   LOOP
     READNEXT ID ELSE LOOP.DONE = 1
   UNTIL (LOOP.DONE) DO
     MATREADU PPS.REC FROM PMC_PROCESS, ID THEN
       PPS.PREPROCESS = ""
       PPS.BUILDERROR = ""
       REPORT_ID = ""
       MATWRITEU PPS.REC ON PMC_PROCESS, ID
       IF OLD.FILE.NM # PPS.DIR THEN
         OPEN '', PPS.DIR TO FILE.NM ELSE
           PPS.BUILDERROR = "CANNOT OPEN FILE"
           MATWRITE PPS.REC ON PMC_PROCESS, ID
           CONTINUE
         END
         OLD.FILE.NM = PPS.DIR
       END
       TMP.REC = ''
       READU TMP.REC FROM FILE.NM, PPS.PROCESS THEN
         GOSUB VALIDATE.PROC
         IF PPS.BUILDERROR = "" AND PPS.PREPROCESS = "R" THEN
           GOSUB CONVERT.PROC
         END
       END ELSE
         PPS.BUILDERROR = "PROGRAM NOT IN FILE"
       END
       RELEASE FILE.NM, PPS.PROCESS
       MATWRITE PPS.REC ON PMC_PROCESS, ID
     END ELSE
       RELEASE PMC_PROCESS, ID
     END
   REPEAT
   STOP
*
VALIDATE.PROC: 
*
   XCNT = DCOUNT(TMP.REC, AM)
   RSCRN.FLAG = 0
   PCONO.FLAG = 0 
   IP.FLAG = 0
   UPD.FLAG = 0
   IN_BUFFER = ""
   FOR XX = 1 TO XCNT UNTIL PPS.BUILDERROR # ""
     BEGIN CASE
       CASE TMP.REC<XX>[1,5] = "MV %1" AND RSCRN.FLAG = 0
         LL_POS = INDEX(TMP.REC<XX>,'"',1)
         IF LL_POS > 0 THEN
           LL_POS += 1
           IN_BUFFER<1> = TMP.REC<XX>[LL_POS, LEN(TMP.REC<XX>)-LL_POS]
         END
       CASE TMP.REC<XX>[1,5] = "MV %2" AND IN_BUFFER<2> = "" AND RSCRN.FLAG = 0
         LL_POS = INDEX(TMP.REC<XX>,'"',1)
         IF LL_POS > 0 THEN
           LL_POS += 1
           IN_BUFFER<2> = TMP.REC<XX>[LL_POS, LEN(TMP.REC<XX>)-LL_POS]
           IF IN_BUFFER<2>[1,4] = "XXXX" THEN IN_BUFFER<2> = ""
           IF LEN(IN_BUFFER<2>) < 5 THEN IN_BUFFER<2> = ""
         END
       CASE TMP.REC<XX>[1,5] = "MV %2" AND RSCRN.FLAG = 0
         LL_POS = INDEX(TMP.REC<XX>,'"',1)
         IF LL_POS > 0 THEN
           LL_POS += 1
           IF IN_BUFFER<2> # TMP.REC<XX>[LL_POS, LEN(TMP.REC<XX>)-LL_POS] THEN
             PPS.BUILDERROR = "BUFFER 2 MISMATCH"
           END
         END
       CASE TMP.REC<XX>[1,5] = "MV %3" AND RSCRN.FLAG = 0
         LL_POS = INDEX(TMP.REC<XX>,'"',1)
         IF LL_POS > 0 THEN
           LL_POS += 1
           IN_BUFFER<3> = TMP.REC<XX>[LL_POS, LEN(TMP.REC<XX>)-LL_POS]
         END
       CASE TMP.REC<XX>[1,5] = "MV %4" AND RSCRN.FLAG = 0
         LL_POS = INDEX(TMP.REC<XX>,'"',1)
         IF LL_POS > 0 THEN
           LL_POS += 1
           IN_BUFFER<4> = TMP.REC<XX>[LL_POS, LEN(TMP.REC<XX>)-LL_POS]
         END
       CASE INDEX(TMP.REC<XX>,'REPORT.SCRN',1) AND TMP.REC<XX>[1,1] = 'H'
         IF TMP.REC<XX+1>[1,1] = 'P' THEN
           RSCRN.FLAG = 1
         END
       CASE INDEX(TMP.REC<XX>,'GET.PROC.CONO',1) AND TMP.REC<XX>[1,1] = 'H'
         PCONO.FLAG = 1
       CASE (TMP.REC<XX>[1,2] = 'IP' OR TMP.REC<XX>[1,2] = 'T ') AND IP.FLAG = 0
         IF TMP.REC<XX>[1,3] = 'T C' THEN CONTINUE
         IP.FLAG = 1
     END CASE
   NEXT XX
   IF PPS.BUILDERROR # "" THEN RETURN
   BEGIN CASE
     CASE IP.FLAG # 0
       PPS.PREPROCESS = "O"
     CASE PCONO.FLAG = 1 AND RSCRN.FLAG = 1
       PPS.PREPROCESS = "R"
     CASE PCONO.FLAG = 1
       PPS.PREPROCESS = "N"
     CASE RSCRN.FLAG = 1
       PPS.PREPROCESS = "R"
     CASE 1
       PPS.PREPROCESS = "N"
   END CASE
   IF PPS.PREPROCESS # "R" THEN RETURN
   IF IN_BUFFER<2> = "" THEN
     PPS.BUILDERROR = "INVALID REPORT ID"
     RETURN
   END
   REPORT_ID = IN_BUFFER<2>
   MATREADU PRPT.REC FROM PMC_REPORTS, REPORT_ID THEN
     RELEASE PMC_REPORTS, REPORT_ID
     PPS.BUILDERROR = "DUPLICAT REPORT ID"
     RETURN
   END
   MAT PRPT.REC = ""
   IF IN_BUFFER<1> # "" THEN
     PRPT.HEADING = IN_BUFFER<1>
     IF IN_BUFFER<1> # IN_BUFFER<3> THEN
       PRPT.SCRN.HEADING = IN_BUFFER<3>
     END
     PRPT.RPT.SCRN.FLAG = "H"
   END ELSE
     PRPT.HEADING = IN_BUFFER<3>
     PRPT.RPT.SCRN.FLAG = "C"
   END
   IF IN_BUFFER<4> # "" THEN
     PRPT.PROMPTS = CHANGE(IN_BUFFER<4>,",",VM)
   END
   IF IN_BUFFER<2>[1,2] = "UP" OR IN_BUFFER<2>[3,1] = "U" THEN
     UPD.FLAG = 1
   END
   IF PRPT.RPT.SCRN.FLAG = "H" AND UPD.FLAG = 0 THEN
     PRPT.VIEW.FLAG = "Y"
     PRPT.PDF.FLAG = "Y"
     PRPT.RTF.FLAG = "Y"
   END
   MATWRITE PRPT.REC ON PMC_REPORTS, REPORT_ID
   LOCATE REPORT_ID IN PPS.REPORTS<1>,1 SETTING FND ELSE
     PPS.REPORTS<-1> = REPORT_ID
   END
*
   RETURN
*
CONVERT.PROC: 
*
   READU DUMMY FROM OLD_REV12_PROCS, REPORT_ID THEN
     RELEASE OLD_REV12_PROCS, REPORT_ID
     PPS.BUILDERROR = "DUP REPORT_ID"
     RETURN
   END
   READU DUMMY FROM NEW_REV12_PROCS, REPORT_ID ELSE DUMMY = ""
   DUMMY = "C File - " : PPS.DIR
   TMP.REC = INSERT(TMP.REC,2,0,0,DUMMY)
   DUMMY = "C Name - " : PPS.PROCESS
   TMP.REC = INSERT(TMP.REC,3,0,0,DUMMY)
   WRITE TMP.REC ON OLD_REV12_PROCS, REPORT_ID
   XCNT = DCOUNT(TMP.REC, AM)
   RSCRN.LN = 0
   FOR XX = 1 TO XCNT UNTIL RSCRN.LN > 0
     IF INDEX(TMP.REC<XX>,'REPORT.SCRN',1) AND TMP.REC<XX>[1,1] = 'H' THEN
       RSCRN.LN = XX
     END
   NEXT XX
   IF RSCRN.LN < 3 THEN
     PPS.BUILDERROR = "R12- REPORT.SCRN LN"
     RETURN
   END
   MV1.UPD = 0
   RSCRN.LN -= 1
   FOR XX = RSCRN.LN TO 1 STEP - 1
     BEGIN CASE
       CASE TMP.REC<XX> = "PQN"
       CASE TMP.REC<XX>[1,5] = "MV %3" AND MV1.UPD = 0
         TMP.REC<XX> = 'MV %1 "' : REPORT_ID :'"'
         MV1.UPD = 1
       CASE TMP.REC<XX>[1,11] = "C Copyright"
       CASE TMP.REC<XX>[1,10] = "C REVISION"
         TMP.REC<XX> = "C REVISION - [12.0]"
       CASE TMP.REC<XX>[1,6] = "C File"
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE TMP.REC<XX>[1,6] = "C Name"
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE TMP.REC<XX>[1,4] = "C IF"
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE TMP.REC<XX> = "C" OR TMP.REC<XX> = "C "
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE TMP.REC<XX>[1,4] = "MV %"
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE TMP.REC<XX>[1,4] = "IF %"
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE TMP.REC<XX>[1,4] = "IF A"
         TMP.REC = DELETE(TMP.REC,XX,0,0)
       CASE INDEX(TMP.REC<XX>,'GET.PROC.CONO',1) AND TMP.REC<XX>[1,1] = 'H'
         IF TMP.REC<XX+1> = "P" THEN
           TMP.REC = DELETE(TMP.REC,XX+1,0,0)
           TMP.REC = DELETE(TMP.REC,XX,0,0)
         END
     END CASE
   NEXT XX
   IF MV1.UPD = 0 THEN
     PPS.BUILDERROR = "R12- MV %3"
   END ELSE
      WRITEU TMP.REC ON FILE.NM, PPS.PROCESS
      WRITE TMP.REC ON NEW_REV12_PROCS, REPORT_ID
   END
   RETURN
 END
