      SUBROUTINE RCV.PO.XREF.MAINT(CONO, PPO.NO, CPO.NO, MAN.NO, MAT RCV.FILE.VARS, ERRMSG)
*********************************************************************
*
* PROGRAM  - RCV.PO.XREF.MAINT
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/27/97
*
* DESCRIPTION
*
* This program updates the RCV.PO.XREF file.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>RCV.CPYLIB>RCV.PO.XREF
*COPY>RCV.CPYLIB>RCV.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
*
*---- UPDATE X-REF DATA
*
      IF CPO.NO # PPO.NO THEN
         IF PPO.NO # "" THEN
            MATREADU RCVPOX.REC FROM RCV.PO.XREF, CONO:PPO.NO THEN
               LOCATE MAN.NO IN RCVPOX.MAN.NO<1>,1 SETTING PP THEN
                  RCVPOX.MAN.NO = DELETE(RCVPOX.MAN.NO,1,PP,0)
               END
               IF RCVPOX.MAN.NO = "" THEN
                  DELETE RCV.PO.XREF, CONO:PPO.NO
               END ELSE
                  MATWRITE RCVPOX.REC ON RCV.PO.XREF, CONO:PPO.NO
               END
            END ELSE
               RELEASE RCV.PO.XREF, CONO:PPO.NO
            END
         END
*
         IF CPO.NO # "" THEN
            MATREADU RCVPOX.REC FROM RCV.PO.XREF, CONO:CPO.NO ELSE
               MAT RCVPOX.REC = ""
            END
            LOCATE MAN.NO IN RCVPOX.MAN.NO<1>,1 BY "AL" SETTING PP THEN
               RELEASE RCV.PO.XREF, CONO:CPO.NO
            END ELSE
               RCVPOX.MAN.NO = INSERT(RCVPOX.MAN.NO,1,PP,0,MAN.NO)
               MATWRITE RCVPOX.REC ON RCV.PO.XREF, CONO:CPO.NO
            END
         END
      END
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
