      SUBROUTINE RCV.ROLL.XREF.MAINT(CONO, PMILL.ID, CMILL.ID, ROLL.ID, MAT RCV.FILE.VARS)
*********************************************************************
*
* PROGRAM  - RCV.ROLL.XREF.MAINT
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/27/97
*
* DESCRIPTION
*
* This program updates the RCV.ROLL.XREF file.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>RCV.CPYLIB>RCV.ROLL.XREF
*COPY>RCV.CPYLIB>RCV.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
*
*---- UPDATE X-REF DATA
*
      IF CMILL.ID # PMILL.ID THEN
         IF PMILL.ID # "" THEN
            MATREADU RCVMRX.REC FROM RCV.ROLL.XREF, CONO:PMILL.ID THEN
               LOCATE ROLL.ID IN RCVMRX.ROLL.ID<1>,1 SETTING PP THEN
                  RCVMRX.ROLL.ID = DELETE(RCVMRX.ROLL.ID,1,PP,0)
               END
               IF RCVMRX.ROLL.ID = "" THEN
                  DELETE RCV.ROLL.XREF, CONO:PMILL.ID
               END ELSE
                  MATWRITE RCVMRX.REC ON RCV.ROLL.XREF, CONO:PMILL.ID
               END
            END ELSE
               RELEASE RCV.ROLL.XREF, CONO:PMILL.ID
            END
         END
*
         IF CMILL.ID # "" THEN
            MATREADU RCVMRX.REC FROM RCV.ROLL.XREF, CONO:CMILL.ID ELSE
               MAT RCVMRX.REC = ""
            END
            LOCATE ROLL.ID IN RCVMRX.ROLL.ID<1>,1 BY "AL" SETTING PP THEN
               RELEASE RCV.ROLL.XREF, CONO:CMILL.ID
            END ELSE
               RCVMRX.ROLL.ID = INSERT(RCVMRX.ROLL.ID,1,PP,0,ROLL.ID)
               MATWRITE RCVMRX.REC ON RCV.ROLL.XREF, CONO:CMILL.ID
            END
         END
      END
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
