**********************************************
* REVISION    - [12.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RCVBP
* PROGRAM     - INTR.DIV.JOURNAL
* BY          - Alejandro Delgado  
* DATE        - 04/02/2002
* DESCRIPTION -
*
*T26497 adelgado 04/02/2002 * Original Code.
*ENDDOC
**********************************************
*   CPYLIBS
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>RCV.CPYLIB>INTR_DIV_TRANSFER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>INV.CNV
*
*   FILE OPENS
  OPEN '', 'INVENTORY' TO INVENTORY ELSE ERRMSG = "UNABLE TO LOCATE INVENTORY FILE"; GOTO 93000
  OPEN '', 'CATEGORY' TO CATEGORY ELSE ERRMSG = "UNABLE TO LOCATE CATEGORY FILE"; GOTO 93000
  OPEN '', 'COMPANY' TO COMPANY ELSE ERRMSG = "UNABLE TO LOCATE COMPANY FILE"; GOTO 93000
  OPEN '', 'INTR_DIV_TRANSFER' TO INTR_DIV_TRANSFER ELSE ERRMSG = "UNABLE TO LOCATE INTR_DIV_TRANSFER FILE"; GOTO 93000
  OPEN '', 'INV_SERIAL' TO INV_SERIAL ELSE ERRMSG = "UNABLE TO LOCATE INV_SERIAL FILE"; GOTO 93000
*
  SYS.TYPE=1; CALL SYSCOM(MAT SYSCOM.REC)
*
  PROCREAD ID ELSE
    ERRMSG = "MUST BE RUN FROM A PROC"
    GOTO 93000
  END
  CONO = ID<1>
  REPORT.NUMBER = ID<2>
  SEL.TYPE = ID<4>
  SEL.DIV = ID<5>
  SEL.START.PERIOD = ID<6>
  SEL.END.PERIOD = ID<7>
  SEL.START.DATE = ID<8>
  SEL.END.DATE = ID<9>
*
*** Heading lines
*
  EQU SP1 TO SPACE(1)
  REPORT.DATE = DATE()
  REPORT.NAME = '' ; CONAME = ''
  H1 = ""; H2 = ""
  CALL GET.PROG.HEAD(CONO,CONAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,H1,H2)
  BEGIN CASE
    CASE REPORT.NUMBER = 'IC442' ; H3 = '' ; H4 = ''
    CASE REPORT.NUMBER = 'IC443' ; H3 = '' ; H4 = ''
    CASE REPORT.NUMBER = 'IC440'
      BEGIN CASE
        CASE SEL.TYPE = 'FROM'
          H3 = SPACE(65):'FROM DIVISION : ':SEL.DIV
          H4 = SPACE(61):'TRANSIT TYPE SELECTION : FROM'
        CASE SEL.TYPE = 'TO'
          H3 = SPACE(67):'TO DIVISION : ':SEL.DIV
          H4 = SPACE(62):'TRANSIT TYPE SELECTION : TO'
        CASE 1
          H3 = '' ; H4 = ''
      END CASE
    CASE SEL.TYPE = 'FROM'
      H3 = "FROM DIVISION : ":SEL.DIV "L#3":SPACE(39):"FROM PERIOD ":SEL.START.PERIOD
      H3 := " THROUGH PERIOD ":SEL.END.PERIOD
      H4 = 'TRANSIT TYPE SELECTION : FROM'
      H4 := SPACE(37):"FROM ":SEL.START.DATE:" THROUGH ":SEL.END.DATE
    CASE SEL.TYPE = 'TO'
      H3 = "TO DIVISION : ":SEL.DIV "L#3":SPACE(41):"FROM PERIOD ":SEL.START.PERIOD
      H3 := " THROUGH PERIOD ":SEL.END.PERIOD
      H4 = 'TRANSIT TYPE SELECTION : TO'
      H4 := SPACE(38):"FROM ":SEL.START.DATE:" THROUGH ":SEL.END.DATE
  END CASE
  COL1 = SPACE(20):'SERIAL # /':SPACE(2):'MILL ROLL ID /'
  COL1 := SPACE(12):'<':STR('-',10):' FROM ':STR('-',11):'>'
  COL1 := SPACE(1):'<':STR('-',11):' TO ':STR('-',12):'> '
  COL2 = 'PRODUCT':SPACE(9):'U/M':SP1
  COL2 := 'RECEIPT #':SPACE(3):'STATUS DESCRIPTION':SPACE(8)
  COL2 := 'DIV':SP1:'WHSE':SP1:'LOC':SPACE(2)
  COL2 := 'DATE':SPACE(5):'PERIOD':SP1
  COL2 := 'DIV':SP1:'WHSE':SP1
  COL2 := 'LOC':SPACE(2):'DATE':SPACE(5)
  COL2 := 'PERIOD':SPACE(4):'QUANTITY'
  COL3 = STR('-',15):SP1:STR('-',3):SP1:STR('-',11):SP1
  COL3 := STR('-',25):SP1:STR('-',3):SP1
  COL3 := STR('-',4):SP1:STR('-',4):SP1:STR('-',8):SP1
  COL3 := STR('-',6):SP1:STR('-',3):SP1:STR('-',4):SP1
  COL3 := STR('-',4):SP1:STR('-',8):SP1:STR('-',6):SP1
  COL3 := STR('-',11)
  PRINTER ON
  PAGE.NO = 0
  PAGE.SIZE = 54
*
*   BODY OF PROGRAM SECTION
  EOF = 0
  LOOP
    READNEXT KEY ELSE EOF = 1
  UNTIL EOF DO
    MATREAD IDM.REC FROM INTR_DIV_TRANSFER, KEY ELSE CONTINUE
    TRANS.ID = KEY[4,9]
    PROD.CNT = DCOUNT(IDM.PROD.NO,@VM)
    FOR PROD.PTR = 1 TO PROD.CNT
      PROD.ID = IDM.PROD.NO<1,PROD.PTR> ; PROD.QTY = 0
      MATREAD INV.REC FROM INVENTORY, CONO:PROD.ID ELSE MAT INV.REC=''
      MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE MAT CATG.REC=''
*COPY>ICSBP>INV.UM.CNV
      BEGIN CASE           
        CASE CATG.TYPE="L" 
          U.M="LBS"        
        CASE CATG.TYPE="S" 
          U.M="SHT"        
        CASE CATG.TYPE="RL"
          U.M="PC "        
        CASE CATG.TYPE="PC"
          U.M="FT "        
        CASE 1             
          U.M = ''
      END CASE
      ROLL.CNT = DCOUNT(IDM.SERIAL<1,PROD.PTR>,@SVM)
      FOR ROLL.PTR = 1 TO ROLL.CNT
        SERIAL.ID = IDM.SERIAL<1,PROD.PTR,ROLL.PTR>
        MILL.ID = IDM.SERIAL.MILL<1,PROD.PTR,ROLL.PTR>
        IF IDM.STATUS = 1 THEN
          MATREAD ISTK.REC FROM INV_SERIAL, CONO:SERIAL.ID ELSE MAT ISTK.REC = ''
          RECEIPT.ID = ISTK.RECP
          IF MILL.ID  = '' THEN MILL.ID = ISTK.MILL.ID
          QTY = INT(((ISTK.CUR.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5)
          STATUS = 'SHIPPING OPEN'
        END ELSE
          RECEIPT.ID = IDM.SERIAL.RECP<1,PROD.PTR,ROLL.PTR>
          QTY = INT(((IDM.SERIAL.QTY<1,PROD.PTR,ROLL.PTR>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5)
          IF IDM.STATUS = 4 THEN
            IF IDM.RECV.STATUS<1,PROD.PTR,ROLL.PTR> = 'R' THEN
              STATUS = 'SERIAL REVERSED'
            END ELSE
              STATUS = 'SERIAL TRANSFERRED'
            END
          END ELSE
            IF IDM.RECV.DATE<1,PROD.PTR,ROLL.PTR> = '' THEN
              STATUS = 'SHIP POSTED/RECV OPEN'
            END ELSE
              IF IDM.RECV.STATUS<1,PROD.PTR,ROLL.PTR> = 'R' THEN
                STATUS = 'SERIAL REVERSED'
              END ELSE
                STATUS = 'SERIAL TRANSFERRED'
              END
            END
          END
        END
        GOSUB PRINT.REPORT
        PROD.QTY += QTY
      NEXT ROLL.PTR
      IF LINE+2 GT PAGE.SIZE THEN GOSUB 2000
      PRINT SPACE(118):STR('-',11)
      PRINT SPACE(89):'PRODUCT TRANSFER QTY TOTAL : ':
      PRINT OCONV(PROD.QTY,ICR.CNV1)'R#11'
      LINE += 2
      IF LINE+1 GT PAGE.SIZE THEN
        GOSUB 2000
      END ELSE
        PRINT
        LINE += 1
      END
    NEXT PROD.PTR
  REPEAT
  PRINTER OFF
  PRINTER CLOSE
  GOTO 999999
*
PRINT.REPORT: 
*
  IF PROD.PTR = 1 AND ROLL.PTR = 1 THEN GOSUB 2000
  IF ROLL.PTR = 1 THEN
    PRINT PROD.ID'L#15':SP1:U.M'L#3':SP1:
  END ELSE
    PRINT SPACE(20):
  END
  PRINT SERIAL.ID'L#11':SP1:MILL.ID'L#25':SP1:
  PRINT IDM.FROM.DIV'L#3':SP1:
  PRINT IDM.FROM.WHSE'L#4':SP1:IDM.FROM.LOC<1,PROD.PTR,ROLL.PTR>'L#4':SP1:
  PRINT OCONV(IDM.SHIP.POST,'D2/')'R#8':SP1:IDM.SHIP.MON'R#6':SP1:
  PRINT IDM.TO.DIV'L#3':SP1:IDM.TO.WHSE<1,PROD.PTR,ROLL.PTR>'L#4':SP1:
  PRINT IDM.TO.LOC<1,PROD.PTR,ROLL.PTR>'L#4':SP1:
  PRINT OCONV(IDM.RECV.POST<1,PROD.PTR,ROLL.PTR>,'D2/')'R#8':SP1:
  PRINT IDM.RECV.MON<1,PROD.PTR,ROLL.PTR>'R#6':SP1:
  PRINT OCONV(QTY,ICR.CNV1)'R#11'
  LINE += 1
  IF LINE GT PAGE.SIZE THEN GOSUB 2000
  PRINT SPACE(20):RECEIPT.ID'L#11':SP1:STATUS'L#25'
  LINE += 1
  IF LINE GT PAGE.SIZE THEN GOSUB 2000
  *
  RETURN
*
*** Print headings
*
2000 *
  PRINT NEW.PAGE
  PAGE.NO += 1
  PRINT H1:PAGE.NO
  PRINT H2
  IF H3 # '' THEN PRINT H3
  IF H4 # '' THEN PRINT H4
  PRINT
  PRINT 'TRANSIT # : ':TRANS.ID'L#10':IDM.DESC'L#40'
  PRINT
  PRINT COL1
  PRINT COL2
  PRINT COL3
  LINE = 9
  IF H3 # '' THEN LINE += 1
  IF H4 # '' THEN LINE += 1
  RETURN
***   CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  PRINTER OFF
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999*
END
