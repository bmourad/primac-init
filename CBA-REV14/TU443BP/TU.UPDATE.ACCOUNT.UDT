 INCLUDE TUBP USER.INCLUDE.H
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
* MAINLINE
***************************************************************************
*     This routine makes the termulator host library available to another
* account by creating file and program pointer entries in the Master
* Dictionary (or VOC) of that account. This routine is run from the account
* where the host library was originally installed and, when invoked,
* prompts for the name of the target account (or the path to the target
* account on platforms which provide for paths instead of account names) in
* which the library is to become available.
***************************************************************************
*
 INCLUDE TUBP TU.API.H
*
 INCLUDE TUBP SPECIAL.H
*
PROMPT ''
*
IF NOT(INDEX(SYSTEM(33), "NT", 1)) THEN
CRT "This program can only be run on Unidata NT, use TU.UPDATE.ACCOUNT"
RETURN
END
VOC = 'VOC'
OPEN '','VOC' TO F.MD ELSE
OPEN '','MD' TO F.MD ELSE
CRT 'Cannot open MD or VOC'
STOP
END
VOC = 'MD'
END
*
OPEN 'DICT','TUBP' TO DICT.TUBP ELSE CRT 'Cannot open DICT TUBP'; STOP
READ SYSTYPE FROM DICT.TUBP,'@@SYSTYPE@@' ELSE SYSTYPE = 'HOST'
SYSTYPE = SYSTYPE<1>
SYSTYPE = FIELD(SYSTYPE,',',1)
*
READV VOC.TYPE FROM F.MD, 'TUBP',1 THEN
BEGIN CASE
CASE VOC.TYPE[1,3] = "DIR"
ETYPE = 2
DELIM = '\'
ACNT.PRMPT = 'Account Path'
IF SYSTYPE = "UDTVMS" THEN ACNT.PRMPT = 'Account Name, eg [TU2]'
SETF = 'SETFILE'
SEL.CRIT = ' WITH @ID NE "_]" AND WITH @ID NE "[.H"'
CASE VOC.TYPE[1,1] = "Q"
CRT 'Cannot proceed: TUBP Must be a file in this account, not a "Q" Pointer'
STOP
CASE 1
CRT '2 Cannot determine nature of environment'
STOP
END CASE
END ELSE
CRT '1 Cannot determine nature of environment'
STOP
END
*
CRT ACNT.PRMPT:' to update with SBClient: ':
INPUT ACCOUNT
IF ACCOUNT = '' THEN STOP
*
THIS.ACCOUNT = @PATH
SETF.CMD = SETF:' ':ACCOUNT:DELIM:VOC:' QFILE OVERWRITING'
EXECUTE SETF.CMD
OPEN 'QFILE' TO REMOTE.VOC THEN
GOSUB 100
END ELSE
CRT 'Cannot open pointer to ':ACCOUNT
END
STOP
*
100 *
*
PROGFILES = 'TUBP':AM:'DEMOBP'
FILELIST = PROGFILES:AM:'TUSTATE':AM:'TUFORMS':AM:'TUCONFIG':AM:'TUDEFN'
FILELIST = FILELIST:AM:'TUERRORS':AM:'TUQUERY'
*
EXECUTE 'COMO ON TU.UPDATE.ACCOUNT'
CRT
CRT 'Installing File Pointers'
AMC = COUNT(FILELIST, AM) + 1
FOR I = 1 TO AMC
FILENAME = FILELIST<I>
CRT FILENAME
WRITE 'F':AM:THIS.ACCOUNT:DELIM:FILENAME:AM:THIS.ACCOUNT:DELIM:'D_':FILENAME ON REMOTE.VOC, FILENAME
NEXT I
*
AMC = COUNT(PROGFILES, AM) + 1
FOR I = 1 TO AMC
FILENAME = PROGFILES<I>
CRT 'Installing Program Pointers - ':FILENAME
WRITEV 'DIR' ON REMOTE.VOC, FILENAME, 1
OPEN FILENAME TO FVAR THEN
EXECUTE 'SSELECT ':FILENAME:SEL.CRIT CAPTURING OUTPUT
LOOP
READNEXT PROGNAME ELSE PROGNAME = AM
UNTIL PROGNAME = AM DO
CRT '.':
READ VOC.REC FROM F.MD, PROGNAME THEN
WRITE VOC.REC ON REMOTE.VOC, PROGNAME
BEGIN CASE
CASE PROGNAME[1 ,4] = "TCL."
TCL.NAME = PROGNAME[5, 99]
GOSUB 150
END CASE
END
REPEAT
CRT
END ELSE
CRT 'Cannot open program library ':FILENAME
END
NEXT I
EXECUTE 'COMO OFF'
RETURN
*
150 *
*
IF ETYPE = 3 OR ETYPE = 4 THEN; * PIOPEN/PI+ or Pr1me INFORMATION
PQ = 'PA':AM:'RUN ':FILENAME:' ':PROGNAME
WRITE PQ ON REMOTE.VOC,TCL.NAME
END ELSE
PQ = 'PQ':AM:'H':PROGNAME
IF ETYPE = 0 THEN PQ<-1> = "A2,999"; * Pick buffer
PQ<-1> = 'P'
WRITE PQ ON REMOTE.VOC,TCL.NAME
END
RETURN
*
END
