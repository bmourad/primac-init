 INCLUDE TUBP USER.INCLUDE.H
 INCLUDE TUBP TU.API.H
 INCLUDE TUBP SPECIAL.H
ACTIVE.TEXT = "'!*', 1)"
PORT.TEXT = CHAR(33):CHAR(42)
EQU TRUE TO 1
EQU FALSE TO 0
!@! INCLUDE TUBP CASE.INVERT.OFF.H ; !*UV,UP*!
OPEN 'DICT','TUBP' TO DICT.TUBP ELSE CRT 'Cannot open DICT TUBP'; STOP
READ SYSTYPE FROM DICT.TUBP,'@@SYSTYPE@@' ELSE
READ SYSTYPE FROM DICT.TUBP,'@SYSTYPE@' ELSE SYSTYPE = 'HOST'
END
SYSTYPE = SYSTYPE<1>
SYSTYPE = FIELD(SYSTYPE,',',1)
DOLLAR.REPLACE = (SYSTYPE = 'PI/O' OR SYSTYPE = 'PR1ME' OR SYSTYPE = 'ADDS')
10 *
PROMPT ''
 INCLUDE TUBP GET.COMMAND.LINE.H
IF TCL = "" THEN
CRT 'put>':
INPUT TCL
END
FC = 1
TCL = TRIM(TCL)
PCFILE = FIELD(TCL, ' ', FC)
FC = FC + 1
HOSTFILE = FIELD(TCL, ' ', FC)
FILENAME = HOSTFILE
DCT = ''
IF HOSTFILE = 'DICT' THEN
FC = FC + 1
DCT = 'DICT'
HOSTFILE = FIELD(TCL, ' ', FC)
FILENAME = 'DICT ':HOSTFILE
END
OPEN DCT, HOSTFILE TO F.DEST ELSE
CRT 'Could Not Open ':FILENAME
STOP
END
FC = FC + 1
ID = FIELD(TCL, ' ', FC)
DESC = TCL[COL2(), LEN(TCL)]
IF PCFILE # '' AND HOSTFILE # '' THEN
IF DESC = '' THEN DESC = 'Loading ':FILENAME:', ':ID
TRF.DATA = ''
STATUS = 0
CALL TU.UPLOAD(PCFILE, F.DEST, TRF.DATA, 'HOM', DESC, STATUS)
IF STATUS THEN
CRT 'Upload of ':FILENAME:', ':ID:' failed, Status = ':STATUS
END ELSE
IF ID = "" AND TRF.DATA[1, 5] = "(HDR)" THEN
ID = TRF.DATA<1>[6, 99]
TRF.DATA = DELETE(TRF.DATA, 1)
END
IF ID = "" THEN
CRT "ID:":
INPUT ID
IF ID = "" THEN STOP
END
IF SYSTYPE = 'PI/O' OR SYSTYPE = 'PR1ME' THEN
IF INDEX(TRF.DATA<2>,'$OPTIONS SMA', 1) THEN
TEMP = TRF.DATA<1>
TRF.DATA<1> = TRF.DATA<2>
TRF.DATA<2> = TEMP
END
END
ASSERT.FLAVOUR = 0
INCLUDE.INDEX = INDEX(TRF.DATA, ' ':'INCLUDE':' ',1)
IF INDEX(TRF.DATA, PORT.TEXT, 1) THEN ASSERT.FLAVOUR = 1
IF INCLUDE.INDEX AND DOLLAR.REPLACE THEN ASSERT.FLAVOUR = 1
IF SYSTYPE = 'MRX' THEN
IF INCLUDE.INDEX OR INDEX(TRF.DATA, 'EXECUTE':' ', 1) THEN
ASSERT.FLAVOUR = 1
END
END
IF HOSTFILE[LEN(HOSTFILE)-1, 2] = "BP" AND INDEX("PICK,AP,ADDS", SYSTYPE, 1) THEN
ASSERT.FLAVOUR = 1
END
IF ASSERT.FLAVOUR THEN
CRT 'Asserting ':SYSTYPE:' flavour...'
AMC = COUNT(TRF.DATA, AM) + (TRF.DATA # '')
FOR J = 1 TO AMC
LINE = TRF.DATA<J>
TLINE = TRIM(LINE)
IF TLINE = '' THEN
LINE = '*'
TRF.DATA<J> = LINE
END
IF DOLLAR.REPLACE THEN
IF FIELD(TLINE, " ", 1) = "INCLUDE" THEN
LINE = '$':TLINE
TRF.DATA<J> = LINE
END
END ELSE
IF TLINE[1, 8] = "$INCLUDE" THEN
LINE = TLINE[2, 999]
TRF.DATA<J> = LINE
END
END
IF SYSTYPE = "MRX" THEN
IF FIELD(TLINE, " ", 1) = "INCLUDE" THEN
IF FIELD(TLINE, " ", 3) NE "FROM" THEN
P = INDEX(TLINE, PORT.TEXT, 1)
LINE = "INCLUDE ":FIELD(TLINE, " ", 3)
LINE = LINE:" FROM "
LINE = LINE:FIELD(TLINE, " " , 2)
IF P THEN
LINE = LINE:" ; ":TLINE[P, 999]
LINE = TRIM(LINE)
END
TRF.DATA<J> = LINE
END
END
LOOP
EXECUTE.INDEX = INDEX(LINE, "EXECUTE":" ", 1)
WHILE EXECUTE.INDEX DO
NEWLINE = LINE[1, EXECUTE.INDEX-1]
NEWLINE = NEWLINE:"PERFORM":" "
NEWLINE = NEWLINE:LINE[EXECUTE.INDEX+8, 32767]
LINE = NEWLINE
TRF.DATA<J> = LINE
REPEAT
END
P = INDEX(LINE, PORT.TEXT, 1)
IF P THEN
CODETYPE = FIELD(LINE[P + 2, 999], '*', 1)
COMMENT = FALSE
BEGIN CASE
CASE (CODETYPE = ''); COMMENT = TRUE
CASE (INDEX(CODETYPE, "-":SYSTYPE, 1)); COMMENT = TRUE
CASE (INDEX(CODETYPE, "-", 1)); COMMENT = FALSE
CASE (INDEX(CODETYPE, SYSTYPE, 1)); COMMENT = FALSE
CASE (ID # 'TU.INSTALL' AND ID # 'TU.CLIENT.PUT'); COMMENT = TRUE
CASE NOT(INDEX(LINE,ACTIVE.TEXT,1)); COMMENT = TRUE
CASE (TRUE); COMMENT = TRUE
END CASE
IF SYSTYPE = 'UDT' THEN
I = INDEX(CODETYPE, SYSTYPE, 1)
IF CODETYPE[I,6] = 'UDTVMS' THEN COMMENT = TRUE
END
IF COMMENT THEN
IF LINE[1,3] # '!@!' THEN LINE = '!@!':LINE
END ELSE
IF LINE[1,3] = '!@!' THEN LINE = LINE[4, LEN(LINE)]
END
IF INDEX(LINE, 'INCLUDE', 1) AND LINE[1,3] # '!@!' THEN
I = INDEX(LINE, ' ;', 1)
IF I THEN LINE = LINE[1,I - 1] ELSE
I = INDEX(LINE, ';', 1)
IF I THEN LINE = LINE[1,I - 1]
LINE = TRIM(LINE)
END
END
TRF.DATA<J> = LINE
END
NEXT J
END
WRITE TRF.DATA ON F.DEST, ID
CRT 'Upload of ':FILENAME:', ':ID:' Done.'
!@!SLEEP 1; CRT ;!*PI/O,PR1ME*!
END
END
!@!IF PTERM THEN EXECUTE "PTERM CASE INVERT" ; !*UV,UP*!
END
