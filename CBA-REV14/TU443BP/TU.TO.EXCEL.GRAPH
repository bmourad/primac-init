SUBROUTINE TU.TO.EXCEL.GRAPH(HOSTFILENAME, FIELDLIST, SELECTION, OPTIONS, SHEETNAME, GRAPHTYPE, STATUS)
 INCLUDE TUBP USER.INCLUDE.H
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
*    See TU.TO.EXCEL for parameter break-down
*
 INCLUDE TUBP SPECIAL.H
 INCLUDE TUBP TU.API.H
 INCLUDE TUBP HEADER.H
 INCLUDE TUBP TU.ERRORCODES.H
*
TIMEOUT = 150
CALL TU.TO.EXCEL(HOSTFILENAME, FIELDLIST, SELECTION, OPTIONS, SHEETNAME, STATUS)
IF STATUS THEN RETURN
*
* Get Field Information from header record
*
OPEN '','TUXFER.DATA.':PORTNO TO TUXFER.DATA ELSE STATUS = FTE.TUXFER.DATA.FILE.OPEN.ERROR; RETURN
READ HEADER FROM TUXFER.DATA,'XFER.HEADER' ELSE STATUS = FTE.ITEM.READ.ERROR; RETURN
FIELDCOUNT = DCOUNT(HEADER<HED.DICTNAME>, VM)
*
** now establish a link with the SYSTEM topic
*
CALL TU.DDE.CONNECT('EXCEL', 'System', SYSTEMHANDLE, STATUS)
IF STATUS THEN RETURN
*
** now see what sheet Excel is looking at
*
CALL TU.DDE.READ(SYSTEMHANDLE, 'Selection', TIMEOUT, DATA, STATUS)
IF STATUS THEN GOTO 999
SIMPLENAME = FIELD(DATA, "[", 2)
SIMPLENAME = FIELD(SIMPLENAME, "]", 1)
CALL TU.DDE.CONNECT('EXCEL', SIMPLENAME, HANDLE, STATUS)
IF STATUS THEN GOTO 999
*
HDR.SUPP = INDEX(FIELDLIST, "HDR-SUPP", 1) + INDEX(FIELDLIST, "HDR.SUP", 1)
IF HDR.SUPP THEN FIRST = 1 ELSE FIRST = 2
TOT.SUPP = INDEX(FIELDLIST, "TOTAL-SUP", 1) + INDEX(FIELDLIST, "TOTAL.SUP", 1)
IF TOT.SUPP THEN
LAST = HEADER<HED.RECORDCOUNT> + (FIRST-1)
END ELSE
LAST = HEADER<HED.RECORDCOUNT> -1 + (FIRST-1)
END
IF INDEX(OPTIONS, "2", 1) THEN FIRST = FIRST + 1; LAST = LAST + 1
CALL TU.DDE.EXEC.MACRO(HANDLE, TIMEOUT, '[Select("R':FIRST:'C1:R':LAST:'C':FIELDCOUNT:'")]', STATUS)
IF STATUS THEN GOTO 998
CALL TU.DDE.EXEC.MACRO(HANDLE, TIMEOUT, '[New(2,2)][Format.Main(':GRAPHTYPE:',,,,TRUE)]', STATUS)
IF STATUS THEN GOTO 998
CALL TU.DDE.EXEC.MACRO(HANDLE, TIMEOUT, '[Legend(TRUE)]', STATUS)
IF STATUS THEN GOTO 998
CALL TU.DDE.EXEC.MACRO(HANDLE, TIMEOUT, '[ZOOM(75)]', STATUS)
IF STATUS THEN GOTO 998
IF INDEX(OPTIONS, 'P', 1) THEN
CALL TU.DDE.EXEC.MACRO(HANDLE, TIMEOUT, '[Print(1)]', STATUS)
IF STATUS THEN GOTO 998
END
CALL TU.DDE.DISCONNECT(HANDLE, STATUS)
CALL TU.DDE.DISCONNECT(SYSTEMHANDLE, DUMMY)
RETURN
*
** error exits
*
998 DUMMY = ''
IF HANDLE NE SYSTEMHANDLE THEN CALL TU.DDE.DISCONNECT(HANDLE, DUMMY)
999 DUMMY = ''
CALL TU.DDE.DISCONNECT(SYSTEMHANDLE, DUMMY)
RETURN
END
