 INCLUDE TUBP USER.INCLUDE.H
* MAINLINE
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
***************************************************************************
*     This routine makes the termulator host library available to another
* account by creating file and program pointer entries in the Master
* Dictionary (or VOC) of that account. This routine is run from the account
* where the host library was originally installed and, when invoked,
* prompts for the name of the target account (or the path to the target
* account on platforms which provide for paths instead of account names) in
* which the library is to become available.
***************************************************************************
*
 INCLUDE TUBP TU.API.H
*
 INCLUDE TUBP SPECIAL.H
*
PROMPT ''
*
IF NOT(SYSTEM(91)) THEN
CRT "This program will only run on uniVerse NT, use TU.UPDATE.ACCOUNT"
RETURN
END
*
VOC = 'VOC'
OPEN '','VOC' TO F.MD ELSE
OPEN '','MD' TO F.MD ELSE
CRT 'Cannot open MD or VOC'
STOP
END
VOC = 'MD'
END
*
OPEN 'DICT','TUBP' TO DICT.TUBP ELSE CRT 'Cannot open DICT TUBP'; STOP
READ SYSTYPE FROM DICT.TUBP,'@@SYSTYPE@@' ELSE SYSTYPE = 'HOST'
SYSTYPE = SYSTYPE<1>
SYSTYPE = FIELD(SYSTYPE,',',1)
*
READV VOC.TYPE FROM F.MD, 'TUBP',1 THEN
BEGIN CASE
CASE VOC.TYPE[1,1] = 'F'; * Pr1me INFORMATION/PI+/PIOPEN or universe
OPEN 'TUBP.O' TO TEST THEN
ETYPE = 1; * universe
DELIM = '\'
ACNT.PRMPT = 'Account or Path'
SETF = 'SET.FILE'
READ TEST FROM F.MD, "RELLEVEL" THEN
BEGIN CASE
CASE INDEX(TEST,'PICK',1)
SEL.CRIT = ' WITH @ID UNLIKE "[.H"'
CASE INDEX(TEST,'INFORMATION',1)
SEL.CRIT = ' WITH @ID UNLIKE "....H"'
CASE 1
CRT 'Cannot Proceed: unsupported UniVerse flavour'
STOP
END CASE
END ELSE
CRT 'Cannot Proceed: unsupported UniVerse flavour'
STOP
END
END
CASE 1
CRT '2 Cannot determine nature of environment'
STOP
END CASE
END ELSE
CRT '1 Cannot determine nature of environment'
STOP
END
*
CRT ACNT.PRMPT:' to update with SBClient: ':
INPUT ACCOUNT
IF ACCOUNT = '' THEN STOP
*
!@!THIS.ACCOUNT = @PATH ;!*UV,UP*!
SETF.CMD = SETF:' ':ACCOUNT:' ':VOC:' QFILE'
EXECUTE SETF.CMD CAPTURING RUBBISH
OPEN 'QFILE' TO REMOTE.VOC THEN
GOSUB 100
END ELSE
CRT 'Cannot open pointer to ':ACCOUNT
END
STOP
*
100 *
*
PROGFILES = 'TUBP':AM:'DEMOBP'
FILELIST = PROGFILES:AM:'TUSTATE':AM:'TUFORMS':AM:'TUCONFIG':AM:'TUDEFN'
FILELIST = FILELIST:AM:'TUERRORS':AM:'TUQUERY'
*
EXECUTE 'COMO ON TU.UPDATE.ACCOUNT'
CRT
CRT 'Installing File Pointers'
AMC = COUNT(FILELIST, AM) + 1
FOR I = 1 TO AMC
FILENAME = FILELIST<I>
CRT FILENAME
WRITE 'Q':AM:THIS.ACCOUNT:AM:FILENAME ON REMOTE.VOC, FILENAME
NEXT I
*
AMC = COUNT(PROGFILES, AM) + 1
FOR I = 1 TO AMC
FILENAME = PROGFILES<I>
CRT 'Installing Program Pointers - ':FILENAME
WRITE 'Q':AM:THIS.ACCOUNT:AM:FILENAME:'.O' ON REMOTE.VOC, FILENAME:'.O'
OPEN FILENAME TO FVAR THEN
EXECUTE 'SSELECT ':FILENAME:SEL.CRIT CAPTURING OUTPUT
LOOP
READNEXT PROGNAME ELSE PROGNAME = AM
UNTIL PROGNAME = AM DO
CRT '.':
READ VOC.REC FROM F.MD, PROGNAME THEN
IF ETYPE = 1 AND VOC.REC[1,1] = 'V' THEN; * universe requires full path
IF VOC.REC<2>[1,1] NE DELIM THEN
VOC.REC<2> = THIS.ACCOUNT:DELIM:VOC.REC<2>
VOC.REC<9> = THIS.ACCOUNT:DELIM:VOC.REC<9>
END
END
WRITE VOC.REC ON REMOTE.VOC, PROGNAME
BEGIN CASE
CASE PROGNAME[1 ,4] = "TCL."
TCL.NAME = PROGNAME[5, 99]
GOSUB 150
END CASE
END
REPEAT
CRT
END ELSE
CRT 'Cannot open program library ':FILENAME
END
NEXT I
EXECUTE 'COMO OFF' CAPTURING RUBBISH
RETURN
*
150 *
*
PQ = 'PQ':AM:'H':PROGNAME
PQ<-1> = 'P'
WRITE PQ ON REMOTE.VOC,TCL.NAME
RETURN
*
END
