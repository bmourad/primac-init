      SUBROUTINE WIPT.HIST.UPD(CONO,PLOC,PQTY,MAT WIPTP.REC,WIP.TRACK.HIST,ERRMSG)
*********************************************************************
*
* PROGRAM  - WIPT.HIST.UPD
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 10/06/95
*
* DESCRIPTION
*
* This program is used to update the WIP Tracking History. It
* must be called when either the pallet location or the quantity
* changes. The call should be made just before writing the
* WIP.TRACK.PALLET file.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.HIST
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
      SECT = PLOC[1,2]
      BEGIN CASE
      CASE SECT = ""
         PSECT = ""
         PWHSE = ""
      CASE SECT MATCHES "1A1N"
         PSECT = SECT[1,1]
         PWHSE = SECT[2,1]
      CASE 1
         PSECT = SECT
         PWHSE = "0"
      END CASE
      CLOC = WIPTP.MLOC<1,1>
      SECT = CLOC[1,2]
      BEGIN CASE
      CASE SECT = ""
         CSECT = ""
         CWHSE = ""
      CASE SECT MATCHES "1A1N"
         CSECT = SECT[1,1]
         CWHSE = SECT[2,1]
      CASE 1
         CSECT = SECT
         CWHSE = "0"
      END CASE
      CQTY = SUM(WIPTP.CQTY)
*
      BEGIN CASE
      CASE PLOC # WIPTP.MLOC<1,1>
         HDATE = WIPTP.MDATE<1,1>
         HTIME = WIPTP.MTIME<1,1>
      CASE (PQTY+0) # SUM(WIPTP.CQTY)
         HDATE = WIPTP.ADATE<1,1>
         HTIME = WIPTP.ATIME<1,1>
      CASE 1
         RETURN
      END CASE
      IF HTIME > ICONV("07:00:00","MTS") THEN HDATE = HDATE + 1
*
*---- MAIN PROCESSING
*
      IF PLOC # "" THEN
         MATREADU WIPTH.REC FROM WIP.TRACK.HIST, CONO:PWHSE:"!":HDATE ELSE
            WHSE = PWHSE
            GOSUB 1000
         END
         GOSUB 2000
      END
      IF CWHSE # PWHSE THEN
         IF PLOC # "" THEN
            MATWRITE WIPTH.REC ON WIP.TRACK.HIST, CONO:PWHSE:"!":HDATE
         END
         MATREADU WIPTH.REC FROM WIP.TRACK.HIST, CONO:CWHSE:"!":HDATE ELSE
            WHSE = CWHSE
            GOSUB 1000
         END
      END
      GOSUB 3000
      MATWRITE WIPTH.REC ON WIP.TRACK.HIST, CONO:CWHSE:"!":HDATE
      GOTO 99999
*
*---- GENERATE INTERVENING HISTORIC DATA
*
1000 *
      MAT WIPTH.REC = ""
      BDATE = HDATE
      FOR CDATE = (HDATE-1) TO (HDATE-100) STEP -1 WHILE BDATE = HDATE
         MATREAD WIPTH.REC FROM WIP.TRACK.HIST, CONO:WHSE:"!":CDATE THEN
            BDATE = CDATE
         END
      NEXT CDATE
      FOR CDATE = (BDATE+1) TO HDATE
         READU HREC FROM WIP.TRACK.HIST, CONO:WHSE:"!":CDATE ELSE
            IF CDATE < HDATE THEN
               MATWRITE WIPTH.REC ON WIP.TRACK.HIST, CONO:WHSE:"!":CDATE
            END ELSE
               MATWRITEU WIPTH.REC ON WIP.TRACK.HIST, CONO:WHSE:"!":CDATE
            END
         END
      NEXT CDATE
      RETURN
*
*---- UPDATE HISTORIC DATA FOR PREVIOUS PALLET STATUS
*
2000 *
      IF (PQTY+0) > 0 AND CQTY = 0 THEN
         WIPTH.PAL.CNT -= 1
         IF WIPTP.PTYPE = "I" THEN WIPTH.INS.CNT -= 1
      END
      IF (PQTY+0) > 0 THEN
         LOCATE PSECT IN WIPTH.SECTION<1>,1 BY "AL" SETTING P ELSE
            WIPTH.SECTION = INSERT(WIPTH.SECTION,1,P,0,PSECT)
            WIPTH.SEC.CNT = INSERT(WIPTH.SEC.CNT,1,P,0,"")
         END
         WIPTH.SEC.CNT<1,P> -= 1
      END
      RETURN
*
*---- UPDATE HISTORIC DATA FOR CURRECT PALLET STATUS
*
3000 *
      IF (PQTY+0) = 0 AND CQTY > 0 THEN
         WIPTH.PAL.CNT += 1
         IF WIPTP.PTYPE = "I" THEN WIPTH.INS.CNT += 1
      END
      IF CQTY > 0 THEN
         LOCATE CSECT IN WIPTH.SECTION<1>,1 BY "AL" SETTING P ELSE
            WIPTH.SECTION = INSERT(WIPTH.SECTION,1,P,0,CSECT)
            WIPTH.SEC.CNT = INSERT(WIPTH.SEC.CNT,1,P,0,"")
         END
         WIPTH.SEC.CNT<1,P> += 1
      END
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
