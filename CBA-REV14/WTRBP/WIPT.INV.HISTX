*********************************************************************
*
* PROGRAM  - WIPT.INV.HIST
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/24/95
*
* DESCRIPTION
*
* This program produces the WIP Tracking Inventory History report.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>WTR.CPYLIB>WIP.TRACK.HIST
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","JOB" TO JOB ELSE
         ERRMSG = "CANNOT OPEN JOB FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CUSTOMER" TO CUSTOMER ELSE
         ERRMSG = "CANNOT OPEN CUSTOMER FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WTR.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN WTR.SCREENS FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WIP.TRACK.TEMP" TO WIP.TRACK.TEMP ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.TEMP FILE"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      CONO = ""
      MAT COMP.REC = ""
      CALL GET.CONO (CONO, MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
*
*---- INITIALIZE SCRN.EDIT
*
      MAT SCV.REC = ""
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.CNT = 1
      ECD.SCRN.NAME<1> = "WIPT.INV.HIST"
      ECD.ACTION=1; CALL SCRN.EDIT
      ECD.SCRN.NO = 1
      ESN = ECD.SCRN.NO
      ECD.ACTION=2; CALL SCRN.EDIT
*
*---- DEFINE HEADINGS
*
      REPORT.NAME = "W I P   I N V E N T O R Y   H I S T O R Y"
      TODAY = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN TODAY = DATE()
      PTIME = OCONV(CTIME,"MTH")
      PTIME = PTIME[1,5]:" ":PTIME[6,2]
      HD1A = "RUN: ":OCONV(TODAY,"D2/"):" @ ":PTIME
      HD1B = SPACE((60-LEN(CO.NAME))/2):CO.NAME
      HD1C = "PAGE "
      HD1 = HD1A"L#30":HD1B"L#60":HD1C"R#30"
      HD2 = SPACE((120-LEN(REPORT.NAME))/2):REPORT.NAME
      SH1 = " Period   <----------- Type ------------>      "
      SH2 = " Ending    Regular    Inserts     Total        "   
      SH3 = "--------  ---------  ---------  ---------      "
      SH1 = SH1:"<------------------------- Location -----------------"
      SH2 = SH2:"Pressroom  Receiving  Warehouse   Bindery    Shipping"
      SH3 = SH3:"---------  ---------  ---------  ---------  ---------"
      SH1 = SH1:"---------->"
      SH2 = SH2:"    Other  "
      SH3 = SH3:"  ---------"
*
*---- INITIALIZE VARIABLES
*
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      SP4 = SPACE(4)
      SP7 = SPACE(7)
      LM = SPACE(10)
      BEG.DATE = ""
      END.DATE = ""
      DIM WE.DATE(1000)
      DIM DAY.CNT(1000)
      DIM REG.CNT(1000)
      DIM INS.CNT(1000)
      DIM INP.CNT(1000)
      MAT WE.DATE = ""
      MAT DAY.CNT = ""
      MAT REG.CNT = ""
      MAT INS.CNT = ""
      MAT INP.CNT = ""
      GREG.CNT = 0
      GINS.CNT = 0
      GINP.CNT = 0
      GTOT.CNT = 0
      XTIME = ICONV("07:00:00","MTS")
      RTYPE = "W"
      XX = 7
      TESTFLAG = 1                               ;*** T E S T ***
      GOTO 110
*
*---- MAIN PROCESSING
*
100 *
      ECD.ACTION=6; CALL SCRN.EDIT
110 *
      SCV.REC(1)<ESN> = DATE()
      ECD.NUM=1; ECD.ACTION=5; CALL SCRN.EDIT
      FOR REF = 1 TO 3
         ON REF GOSUB 1010,1020,1030
         IF ECD.RET.VALUE = "END" THEN GOTO 99999
      NEXT REF
*
*---- GET OPERATOR REQUEST
*
500 *
      ECD.NUM=21; ECD.ACTION=4; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "E"
            GOTO 99999
         CASE NUM(OPTION)
            ON OPTION GOSUB 1010,1020,1030
         CASE OPTION = "F"
            WCNT = 0
*        FOR WDATE = BEG.DATE TO (END.DATE+XX-1) STEP XX
            FOR WDATE = BEG.DATE TO END.DATE STEP XX
               WCNT += 1
               IF WCNT > 1000 THEN
                  ERRMSG = "INVALID DATE RANGE"
                  GOSUB 90000
                  GOTO 500
               END
               WE.DATE(WCNT) = INT(WDATE/XX)*XX+XX-1
            NEXT WDATE
            CMD = "SSELECT WIP.TRACK.TEMP" 
            CMD = CMD:" BY HDATE"
            CMD = CMD:' WITH CONO = "':CONO:'"'
            CMD = CMD:' AND WITH WHSE = "1"'
            CMD = CMD:' AND WITH HDATE >= "':OCONV(BEG.DATE,"D2/"):'"'
            CMD = CMD:' AND WITH HDATE <= "':OCONV(END.DATE,"D2/"):'"'
            PERFORM CMD CAPTURING SELMSG
            GOSUB 70000
            ERRMSG = "** COMPLETED **"
            GOSUB 90000
            GOTO 99999
      END CASE
      GOTO 500
*
*---- GET REPORT TYPE
*
1010 *
      SCV.REC(2)<ESN> = RTYPE
      ECD.NUM=2; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      RTYPE = ECD.RET.VALUE
      IF RTYPE = "D" THEN XX = 1 ELSE XX = 7
      RETURN
*
*---- GET BEGINNING DATE
*
1020 *
      SCV.REC(3)<ESN> = BEG.DATE
      ECD.NUM=3; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
*     BEG.DATE = ECD.RET.VALUE
      BEG.DATE = INT(ECD.RET.VALUE/XX)*XX
      SCV.REC(3)<ESN> = BEG.DATE
      ECD.NUM=3; ECD.ACTION=5; CALL SCRN.EDIT
      RETURN
*
*---- GET ENDING DATE
*
1030 *
      SCV.REC(4)<ESN> = END.DATE
      ECD.NUM=4; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
*     END.DATE = ECD.RET.VALUE
      END.DATE = INT((ECD.RET.VALUE+XX)/XX)*XX-1
      SCV.REC(4)<ESN> = END.DATE
      ECD.NUM=4; ECD.ACTION=5; CALL SCRN.EDIT
      RETURN
*
*---- PRINT REPORT
*
70000 *
      LINE.COUNT = 99
      PAGE.NO = 0
      PRINT.CNT = 0
      IF TESTFLAG THEN
         PRINT @(-1):
      END ELSE
         PRINTER ON
      END
      DONE = 0
      LOOP
         READNEXT ID ELSE DONE = 1
      UNTIL DONE DO
         WHSE = FIELD(ID,"!",1)[4,1]
         HDATE = FIELD(ID,"!",2)
         MATREAD WIPTH.REC FROM WIP.TRACK.TEMP, CONO:WHSE:"!":HDATE THEN
            GOSUB 74000
         END
      REPEAT
      GOSUB 76000
      GOSUB 78000
      IF TESTFLAG THEN
         NULL
      END ELSE
         PRINT CHAR(12):
         PRINTER OFF
         PRINTER CLOSE
      END
      RETURN
*
*---- PROCESS PALLET
*
74000 *
      IF HDATE >= BEG.DATE THEN
         WPTR = INT(HDATE/XX) - INT(BEG.DATE/XX) + 1
         DAY.CNT(WPTR) += 1
         REG.CNT(WPTR) += (WIPTH.PAL.CNT - WIPTH.INS.CNT)
         INS.CNT(WPTR) += WIPTH.INS.CNT
         SCNT = DCOUNT(WIPTH.SECTION,VM)
         FOR SPTR = 1 TO SCNT
            QTY = WIPTH.SEC.CNT<1,SPTR>
            SECT = WIPTH.SECTION<1,SPTR>
            BEGIN CASE
               CASE SECT = "P"
                  INP.CNT(WPTR)<1> += QTY
               CASE SECT = "R"
                  INP.CNT(WPTR)<2> += QTY
               CASE SECT = "W"
                  INP.CNT(WPTR)<3> += QTY
               CASE SECT = "B"
                  INP.CNT(WPTR)<4> += QTY
               CASE SECT = "S"
                  INP.CNT(WPTR)<5> += QTY
               CASE 1
                  INP.CNT(WPTR)<6> += QTY
            END CASE
         NEXT SPTR
      END
      RETURN
*
*---- PRINT SUMMARY LINE
*
76000 *
      FOR WPTR = 1 TO WCNT
         IF (LINE.COUNT + 1) > 55 THEN GOSUB 79000
         PLINE = OCONV(WE.DATE(WPTR),"D2/")"L#8"
         IF DAY.CNT(WPTR)+0 = 0 THEN
            REGCNT = 0
            INSCNT = 0
            INPCNT = ""
            INPCNT<1> = 0
            INPCNT<2> = 0
            INPCNT<3> = 0
            INPCNT<4> = 0
            INPCNT<5> = 0
            INPCNT<6> = 0
         END ELSE
            REGCNT = INT(REG.CNT(WPTR)/DAY.CNT(WPTR)+0.5)
            INSCNT = INT(INS.CNT(WPTR)/DAY.CNT(WPTR)+0.5)
            INPCNT = ""
            INPCNT<1> = INT(INP.CNT(WPTR)<1>/DAY.CNT(WPTR)+0.5)
            INPCNT<2> = INT(INP.CNT(WPTR)<2>/DAY.CNT(WPTR)+0.5)
            INPCNT<3> = INT(INP.CNT(WPTR)<3>/DAY.CNT(WPTR)+0.5)
            INPCNT<4> = INT(INP.CNT(WPTR)<4>/DAY.CNT(WPTR)+0.5)
            INPCNT<5> = INT(INP.CNT(WPTR)<5>/DAY.CNT(WPTR)+0.5)
            INPCNT<6> = INT(INP.CNT(WPTR)<6>/DAY.CNT(WPTR)+0.5)
         END
         TOTCNT = REGCNT+INSCNT
         PLINE = PLINE:SP3:OCONV(REGCNT,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(INSCNT,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(TOTCNT,"MD0,")"R#7":SP1
         PLINE = PLINE:SP7:OCONV(INPCNT<1>,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(INPCNT<2>,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(INPCNT<3>,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(INPCNT<4>,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(INPCNT<5>,"MD0,")"R#7":SP1
         PLINE = PLINE:SP3:OCONV(INPCNT<6>,"MD0,")"R#7":SP1
*        OTHCNT = TOTCNT - SUM(INPCNT)
*        IF OTHCNT < 0 THEN OTHCNT = 0
*        PLINE = PLINE:SP3:OCONV(OTHCNT,"MD0,")"R#7":SP1
         PRINT LM:PLINE
         LINE.COUNT = LINE.COUNT + 1
         PRINT.CNT = PRINT.CNT + 1
         GREG.CNT = GREG.CNT + REGCNT
         GINS.CNT = GINS.CNT + INSCNT
         GINP.CNT<1> += INPCNT<1>
         GINP.CNT<2> += INPCNT<2>
         GINP.CNT<3> += INPCNT<3>
         GINP.CNT<4> += INPCNT<4>
         GINP.CNT<5> += INPCNT<5>
         GINP.CNT<6> += INPCNT<6>
         GTOT.CNT = GTOT.CNT + TOTCNT
      NEXT WPTR
      RETURN
*
*---- PRINT GRAND TOTAL LINE
*
78000 *
      PRINT
      PLINE = "Average""R#8"
      PAL.AVG = INT(GREG.CNT/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(PAL.AVG,"MD0,")"R#7":SP1
      INS.AVG = INT(GINS.CNT/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(INS.AVG,"MD0,")"R#7":SP1
      TOT.AVG = INT(GTOT.CNT/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(TOT.AVG,"MD0,")"R#7":SP1
      INP.AVG = INT(GINP.CNT<1>/WCNT+0.5)
      PLINE = PLINE:SP7:OCONV(INP.AVG,"MD0,")"R#7":SP1
      INP.AVG = INT(GINP.CNT<2>/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(INP.AVG,"MD0,")"R#7":SP1
      INP.AVG = INT(GINP.CNT<3>/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(INP.AVG,"MD0,")"R#7":SP1
      INP.AVG = INT(GINP.CNT<4>/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(INP.AVG,"MD0,")"R#7":SP1
      INP.AVG = INT(GINP.CNT<5>/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(INP.AVG,"MD0,")"R#7":SP1
      INP.AVG = INT(GINP.CNT<6>/WCNT+0.5)
*     INP.AVG = INT((GTOT.CNT-SUM(GINP.CNT))/WCNT+0.5)
      PLINE = PLINE:SP3:OCONV(INP.AVG,"MD0,")"R#7":SP1
      PRINT LM:PLINE
      LINE.COUNT = LINE.COUNT + 2
      RETURN
*
*---- PRINT HEADINGS
*
79000 *
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1: PAGE.NO
      PRINT HD2
      HD3 = "PERIOD ":OCONV(BEG.DATE,"D2/"):" THROUGH ":OCONV(END.DATE,"D2/")
      BEGIN CASE
         CASE RTYPE = "D"
            HD3 = SPACE((120-LEN(HD3))/2):HD3:" (DAILY)"
         CASE RTYPE = "W"
            HD3 = SPACE((120-LEN(HD3))/2):HD3:" (WEEKLY)"
         CASE 1
            HD3 = SPACE((120-LEN(HD3))/2):HD3
      END CASE
      PRINT HD3
      PRINT
      PTIME = OCONV(XTIME,"MTH")
      PTIME = PTIME[1,5]:" ":PTIME[6,2]
      IF PTIME[1,1] = "0" THEN PTIME = PTIME[2,99]
      PRINT "Averages are as of ":PTIME
      PRINT
      PRINT
      PRINT LM:SH1
      PRINT LM:SH2
      PRINT LM:SH3
      LINE.COUNT = 10
      RETURN
*
*---- DISPLAY SCREEN DATA
*
80000 *
      SCV.REC(1)<ESN> = DATE()
      SCV.REC(2)<ESN> = RTYPE
      SCV.REC(3)<ESN> = BEG.DATE
      SCV.REC(4)<ESN> = END.DATE
80050 *
      ECD.ACTION=3; CALL SCRN.EDIT
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       CRT @(0,23):@(-4):BEL:ERRMSG:
*       INPUT REPLY,1:
*       CRT @(0,23):@(-4):
*       RETURN
*
*---- END OF JOB
*
99999 *
*     PRINT @(-1):
   END
