*********************************************************************
*
* PROGRAM  - WIPT.INV.PURGE
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/23/95
*
* DESCRIPTION
*
* This program produces the WIP Tracking Inventory Purge report.
*
*T26090 ajibaly 04/04/2002 * RUN RPT FROM PROC USING REPORT.SCRN AND
*                            GET.PROG.HEAD
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","JOB" TO JOB ELSE
      ERRMSG = "CANNOT OPEN JOB FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CUSTOMER" TO CUSTOMER ELSE
      ERRMSG = "CANNOT OPEN CUSTOMER FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
      ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","WIP.TRACK.JOB" TO WIP.TRACK.JOB ELSE
      ERRMSG = "CANNOT OPEN WIP.TRACK.JOB FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","WIP.TRACK.HIST" TO WIP.TRACK.HIST ELSE
      ERRMSG = "CANNOT OPEN WIP.TRACK.HIST FILE"
      GOSUB 90000
      GOTO 99999
   END
*
*---- INITIALIZATION
*
*T26090 v
   PROCREAD BUFFER ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
      GOSUB 90000
      GOTO  99999
   END
   CONO      = BUFFER<1>
   RPT.NUM   = BUFFER<2>
   JOB.NO    = BUFFER<3>
   OPTION    = BUFFER<4>
   RPT.NAME  = ''
   CONO.NAME = ''
   HD1 = ''
   HD2 = ''
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NUM,"",HD1,HD2)
*T26090 ^
   IF CONO = "END" THEN GOTO 99999
*
*---- DEFINE HEADINGS
*
   SH1 = "                                                          To "
   SH2 = " Job #     Form          Customer        Pallet #  Skid  CCtr"
   SH3 = "--------  -------  --------------------  --------  ----  ----"
   SH1 = SH1:"  <----- Last ----->  <-- Completion -->  <---- Discard --->"    
   SH2 = SH2:"  Quantity  Location    Date      Time      Date      Time  "
   SH3 = SH3:"  --------  --------  --------  --------  --------  --------"
*
*---- INITIALIZE VARIABLES
*
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   LM = ""
*
*---- MAIN PROCESSING
*
   PURGE.FLAG = 0
   BEGIN CASE
      CASE OPTION # "PURGE"
         CMD = "SSELECT WIP.TRACK.PALLET" 
         CMD = CMD:" BY WIPTP.JOB"
         CMD = CMD:" BY WIPTP.FORM"
         CMD = CMD:" BY WIPTP.VER"
         CMD = CMD:" BY WIPTP.SIG"
         CMD = CMD:" BY WIPTP.FSFX"
         CMD = CMD:' WITH CONO = "':CONO:'"'
         CMD = CMD:' AND WITH WIP.PALLET = "Y"'
         CMD = CMD:' AND WITH WIPTP.CQTY = "0"'
         IF JOB.NO # "ALL" THEN
            CMD = CMD:' AND WITH WIPTP.JOB = "':JOB.NO:'"'
         END
         PERFORM CMD CAPTURING SELMSG
         GOSUB 70000
      CASE OPTION = "PURGE"
         CMD = "SSELECT WIP.TRACK.PALLET" 
         CMD = CMD:" BY WIPTP.JOB"
         CMD = CMD:" BY WIPTP.FORM"
         CMD = CMD:" BY WIPTP.VER"
         CMD = CMD:" BY WIPTP.SIG"
         CMD = CMD:" BY WIPTP.FSFX"
         CMD = CMD:' WITH CONO = "':CONO:'"'
         CMD = CMD:' AND WITH WIPTP.CQTY = "0"'
         IF JOB.NO # "ALL" THEN
            CMD = CMD:' AND WITH WIPTP.JOB = "':JOB.NO:'"'
         END
         PERFORM CMD CAPTURING SELMSG
         PURGE.FLAG = 1
         GOSUB 70000
   END CASE
   GOTO 99999
*
*---- PRINT REPORT
*
70000 *
   LINE.COUNT = 99
   PAGE.NO = 0
   PREV.JOB = "!@#$%"
   PRINT.CNT = 0
   BREAK.CNT = 0
   PRINTER ON
   DONE = 0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      PAL.ID = ID[4,99]
      IF PURGE.FLAG THEN
         MATREADU WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
            IF WIPTP.CQTY > 0 THEN
               CDATE=DATE()
               CTIME=TIME()
               IF CTIME < 10 THEN CDATE=DATE()
               PREV.LOC = WIPTP.MLOC<1,1>
               PREV.QTY = WIPTP.CQTY
               WIPTP.ACODE = INSERT(WIPTP.ACODE,1,1,0,"A")
               WIPTP.AQTY = INSERT(WIPTP.AQTY,1,1,0,-WIPTP.CQTY)
               WIPTP.ADATE = INSERT(WIPTP.ADATE,1,1,0,CDATE)
               WIPTP.ATIME = INSERT(WIPTP.ATIME,1,1,0,CTIME)
               WIPTP.AEMP = INSERT(WIPTP.AEMP,1,1,0,"???")
               WIPTP.CQTY = 0
            END
            PJOB = WIPTP.JOB
            BEGIN CASE
               CASE WIPTP.VER = "" AND WIPTP.SIG = ""
                  PFORM = WIPTP.FORM
               CASE WIPTP.SIG = ""
                  PFORM = WIPTP.FORM:".":WIPTP.VER
               CASE 1
                  PFORM = WIPTP.FORM:".":WIPTP.VER:".":WIPTP.SIG
            END CASE
            CALL WIPT.XREF.MAINT(CONO,PJOB,PFORM,"","",PAL.ID,SKID,ERRMSG)
            DELETE WIP.TRACK.PALLET, CONO:PAL.ID
         END ELSE
            RELEASE WIP.TRACK.PALLET, CONO:PAL.ID
         END
         GOSUB 74000
      END ELSE
         MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
            IF WIPTP.JOB # PREV.JOB THEN
               IF LINE.COUNT # 99 THEN
                  PRINT
                  LINE.COUNT = LINE.COUNT + 1
               END
               PREV.JOB = WIPTP.JOB
            END
            GOSUB 74000
         END
      END
   REPEAT
   IF PRINT.CNT > 0 THEN
      PRINT
      PRINT "END OF REPORT"
      PRINT CHAR(12):
   END ELSE
      GOSUB 79000
      PRINT
      PRINT "NO ITEMS SELECTED"
   END
   PRINTER CLOSE
   PRINTER OFF
   RETURN
*
*---- PRINT DETAIL LINE(S)
*
74000 *
   MATREAD JOB.REC FROM JOB, CONO:WIPTP.JOB THEN
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE MAT CUST.REC=""
   END ELSE
      MAT JOB.REC = ""
      MAT CUST.REC = ""
   END
   BEGIN CASE
      CASE WIPTP.VER = "" AND WIPTP.SIG = ""
         FORM.NO = WIPTP.FORM
      CASE WIPTP.SIG = ""
         FORM.NO = WIPTP.FORM:".":WIPTP.VER
      CASE 1
         FORM.NO = WIPTP.FORM:".":WIPTP.VER:".":WIPTP.SIG
   END CASE
   IF (LINE.COUNT + 1) > 55 THEN GOSUB 79000
   PLINE = WIPTP.JOB"L#8"
   PLINE = PLINE:SP2:FORM.NO"L#7"
   PLINE = PLINE:SP2:CUST.NAME"L#20"
   PLINE = PLINE:SP2:PAL.ID"R#8"
   PLINE = PLINE:SP2:WIPTP.SKID"R#4"
   PLINE = PLINE:SP2:WIPTP.TYPE"L#4"
   PLINE = PLINE:SP2:OCONV(WIPTP.CQTY,"MD0,")"R#8"
   IF WIPTP.MLOC<1,1> = "SHREDDER" THEN
      PLINE = PLINE:SP2:WIPTP.MLOC<1,2>"L#8"
   END ELSE
      PLINE = PLINE:SP2:WIPTP.MLOC<1,1>"L#8"
   END
   IF WIPTP.CQTY = 0 THEN
      PLINE = PLINE:SP2:OCONV(WIPTP.ADATE<1,1>,"D2/")"L#8"
      PTIME = OCONV(WIPTP.ATIME<1,1>,"MTH")
      PTIME = PTIME[1,5]:" ":PTIME[6,2]
      PLINE = PLINE:SP2:PTIME"L#8"
   END ELSE
      PLINE = PLINE:SP2:"""L#8"
      PLINE = PLINE:SP2:"""L#8"
   END
   IF WIPTP.MLOC<1,1> = "SHREDDER" THEN
      PLINE = PLINE:SP2:OCONV(WIPTP.MDATE<1,1>,"D2/")"L#8"
      PTIME = OCONV(WIPTP.MTIME<1,1>,"MTH")
      PTIME = PTIME[1,5]:" ":PTIME[6,2]
      PLINE = PLINE:SP2:PTIME"L#8"
   END ELSE
      PLINE = PLINE:SP2:"""L#8"
      PLINE = PLINE:SP2:"""L#8"
   END
   PRINT LM:PLINE
   LINE.COUNT = LINE.COUNT + 1
   PRINT.CNT = PRINT.CNT + 1
   BREAK.CNT = BREAK.CNT + 1
   RETURN
*
*---- PRINT HEADINGS
*
79000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
   PRINT HD1: PAGE.NO
   PRINT HD2
   PRINT
   PRINT
   PRINT LM:SH1
   PRINT LM:SH2
   PRINT LM:SH3
   LINE.COUNT = 7
   RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
*
99999 *
END
