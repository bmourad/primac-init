*********************************************************************
*
* PROGRAM  - WIPT.INV.PALLET
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 01/17/95
*
* DESCRIPTION
*
* This program produces the WIP Tracking Pallet History report.
*
*T26090 ajibaly 04/04/2002 * USE REPORT.SCRN AND GET.PROC.HEAD (REV12)
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
*T26090 v
   PROCREAD BUFFER ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
      GOSUB 90000
      GOTO  99999
   END
   CONO      = BUFFER<1>
   RPT.NUM   = BUFFER<2>
   PAL.ID    = BUFFER<3>
   RPT.NAME  = ''
   CONO.NAME = ''
   HD1 = ''
   HD2 = ''
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NUM,"",HD1,HD2)
*T26090 ^
      IF CONO = "END" THEN GOTO 99999
*
*---- DEFINE HEADINGS
*
      SH1 = "                          To                     "
      SH2 = " Job #     Form    Skid  CCtr    Date      Time  "
      SH3 = "--------  -------  ----  ----  --------  --------"
      SH1 = SH1:"                                                "
      SH2 = SH2:"  Quantity  Location  Emp       Description     "
      SH3 = SH3:"  --------  --------  ----  --------------------"
*
*---- INITIALIZE VARIABLES
*
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      LM = SPACE(4)
*
*---- MAIN PROCESSING
*
      MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
         GOSUB 70000
      END ELSE
         ERRMSG = "** INVALID PALLET ID **"
         GOSUB 90000
      END
      GOTO 99999
*
*---- PRINT REPORT
*
70000 *
      LINE.COUNT = 99
      PAGE.NO = 0
      PREV.PAL.ID = "!@#$%"
      PRINT.CNT = 0
      BREAK.CNT = 0
      PRINTER ON
*
      TEMP.SID = ""
      TEMP.DATE = ""
      TEMP.TIME = ""
      TEMP.MLOC = ""
      TEMP.AQTY = ""
      TEMP.XEMP = ""
      TEMP.CODE = ""
*
      MCNT = DCOUNT(WIPTP.MLOC,VM)
      FOR MPTR = MCNT TO 1 STEP -1
         MDATE = WIPTP.MDATE<1,MPTR>
         MTIME = WIPTP.MTIME<1,MPTR>
         MLOC = WIPTP.MLOC<1,MPTR>
         MEMP = WIPTP.MEMP<1,MPTR>
         SID = (MDATE*100000) + MTIME
         LOCATE (SID+1) IN TEMP.SID<1>,1 BY "AR" SETTING P ELSE NULL
         TEMP.SID = INSERT(TEMP.SID,1,P,0,SID)
         TEMP.DATE = INSERT(TEMP.DATE,1,P,0,MDATE)
         TEMP.TIME = INSERT(TEMP.TIME,1,P,0,MTIME)
         TEMP.MLOC = INSERT(TEMP.MLOC,1,P,0,MLOC)
         TEMP.AQTY = INSERT(TEMP.AQTY,1,P,0,"")
         TEMP.XEMP = INSERT(TEMP.XEMP,1,P,0,MEMP)
         TEMP.CODE = INSERT(TEMP.CODE,1,P,0,"")
      NEXT MPTR
*
      ACNT = DCOUNT(WIPTP.ACODE,VM)
      FOR APTR = ACNT TO 1 STEP -1
         ADATE = WIPTP.ADATE<1,APTR>
         ATIME = WIPTP.ATIME<1,APTR>
         AQTY = WIPTP.AQTY<1,APTR>
         AEMP = WIPTP.AEMP<1,APTR>
         CODE = WIPTP.ACODE<1,APTR>
         SID = (ADATE*100000) + ATIME
         FND = 0
         LOCATE SID IN TEMP.SID<1>,1 BY "AR" SETTING P THEN
            IF TEMP.CODE<1,P>="" AND TEMP.XEMP<1,P>=AEMP THEN FND=1 ELSE P=P+1
         END
         IF FND THEN
            TEMP.AQTY<1,P> = AQTY
            TEMP.CODE<1,P> = CODE
         END ELSE
            TEMP.SID = INSERT(TEMP.SID,1,P,0,SID)
            TEMP.DATE = INSERT(TEMP.DATE,1,P,0,ADATE)
            TEMP.TIME = INSERT(TEMP.TIME,1,P,0,ATIME)
            TEMP.MLOC = INSERT(TEMP.MLOC,1,P,0,"")
            TEMP.AQTY = INSERT(TEMP.AQTY,1,P,0,AQTY)
            TEMP.XEMP = INSERT(TEMP.XEMP,1,P,0,AEMP)
            TEMP.CODE = INSERT(TEMP.CODE,1,P,0,CODE)
         END
      NEXT APTR
*
      TCNT = DCOUNT(TEMP.DATE,VM)
      GOSUB 74000
      IF PRINT.CNT > 0 THEN
         PRINT
         PRINT "END OF REPORT"
         PRINT CHAR(12):
      END ELSE
         GOSUB 79000
         PRINT
         PRINT "NO ITEMS SELECTED"
      END
      PRINTER OFF
      PRINTER CLOSE
      RETURN
*
*---- PRINT DETAIL LINE(S)
*
74000 *
      BEGIN CASE
      CASE WIPTP.VER = "" AND WIPTP.SIG = ""
         FORM.NO = WIPTP.FORM
      CASE WIPTP.SIG = ""
         FORM.NO = WIPTP.FORM:".":WIPTP.VER
      CASE 1
         FORM.NO = WIPTP.FORM:".":WIPTP.VER:".":WIPTP.SIG
      END CASE
      IF (LINE.COUNT + TCNT) > 55 THEN GOSUB 79000
      CQTY = TEMP.AQTY<1,1>
      CODE = TEMP.CODE<1,1>; AQTY = TEMP.AQTY<1,1>; GOSUB 85000
      PLINE = WIPTP.JOB"L#8"
      PLINE = PLINE:SP2:FORM.NO"L#7"
      PLINE = PLINE:SP2:WIPTP.SKID"R#4"
*T22322v
*      PLINE = PLINE:SP2:WIPTP.TYPE"L#4"
      PLINE = PLINE:SP2:WIPTP.TO.OPER"L#4"
*T22322^
      PLINE = PLINE:SP2:OCONV(TEMP.DATE<1,1>,"D2/")"L#8"
      PTIME = OCONV(TEMP.TIME<1,1>,"MTH")
      PTIME = PTIME[1,5]:" ":PTIME[6,2]
      PLINE = PLINE:SP2:PTIME"L#8"
      PLINE = PLINE:SP2:OCONV(CQTY,"MD0,")"R#8"
      PLINE = PLINE:SP2:TEMP.MLOC<1,1>"L#8"
      PLINE = PLINE:SP2:TEMP.XEMP<1,1>"L#4"
      PLINE = PLINE:SP2:CODE"L#25"
      PRINT LM:PLINE
      LINE.COUNT = LINE.COUNT + 1
*
      PREV.PAL.ID = PAL.ID
      PREV.DATE = TEMP.DATE<1,1>
*
      FOR TPTR = 2 TO TCNT
         IF (LINE.COUNT + 1) > 55 THEN GOSUB 79000
         CQTY = CQTY + TEMP.AQTY<1,TPTR>
         CODE = TEMP.CODE<1,TPTR>; AQTY = TEMP.AQTY<1,TPTR>; GOSUB 85000
         PLINE = SPACE(29)
         IF TEMP.DATE<1,TPTR> = PREV.DATE THEN
            PLINE = PLINE:SP2:"""L#8"
         END ELSE
            PLINE = PLINE:SP2:OCONV(TEMP.DATE<1,TPTR>,"D2/")"L#8"
         END
         PTIME = OCONV(TEMP.TIME<1,TPTR>,"MTH")
         PTIME = PTIME[1,5]:" ":PTIME[6,2]
         PLINE = PLINE:SP2:PTIME"L#8"
         PLINE = PLINE:SP2:OCONV(CQTY,"MD0,")"R#8"
         IF TEMP.MLOC<1,TPTR> = "" THEN TEMP.MLOC<1,TPTR> = TEMP.MLOC<1,TPTR-1>
         PLINE = PLINE:SP2:TEMP.MLOC<1,TPTR>"L#8"
         PLINE = PLINE:SP2:TEMP.XEMP<1,TPTR>"L#4"
         PLINE = PLINE:SP2:CODE"L#25"
         PRINT LM:PLINE
         LINE.COUNT = LINE.COUNT + 1
*
         PREV.DATE = TEMP.DATE<1,TPTR>
      NEXT TPTR
      PRINT.CNT = PRINT.CNT + 1
      RETURN
*
*---- PRINT HEADINGS
*
79000 *
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1: PAGE.NO
      PRINT HD2
      LINE.COUNT = 2
79500 *
      PRINT
      IF PAL.ID # PREV.PAL.ID THEN
         SH1[1,30] = ("PALLET: ":PAL.ID)"L#30"
      END ELSE
         SH1[1,30] = ("PALLET: ":PAL.ID:" (cont'd)")"L#30"
      END
      PRINT SH1[1,30]:LM:SH1[31,999]
      PRINT
      PRINT LM:SH2
      PRINT LM:SH3
      LINE.COUNT = LINE.COUNT + 5
      RETURN
*
*---- DERIVE TRANSACTION DESCRIPTION
*
85000 *
      BEGIN CASE
      CASE CODE = "N"
         CODE = "New pallet"
      CASE CODE = "C"
         CODE = "Consumed ":OCONV((-AQTY),"MD0,")
      CASE CODE = "A"
         CODE = "Adjusted ":OCONV((-AQTY),"MD0,")
      CASE CODE = "S"
         CODE = "Shred ":OCONV((-AQTY),"MD0,")
      CASE CODE = "V"
         CODE = "Transfer ":OCONV((-AQTY),"MD0,")
      CASE CODE = ""
         CODE = "Relocated"
      END CASE
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
*
99999 *
   END
