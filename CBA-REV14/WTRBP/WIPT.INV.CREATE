*********************************************************************
*
* PROGRAM  - WIPT.INV.CREATE
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 01/24/96
*
* DESCRIPTION
*
* This program produces the WIP Tracking Inventory by Initial Location.
*
*T26090 ajibaly 04/04/2002 * USE REPORT.SCRN AND GET.PROC.HEAD (REV12)
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","JOB" TO JOB ELSE
      ERRMSG = "CANNOT OPEN JOB FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CUSTOMER" TO CUSTOMER ELSE
      ERRMSG = "CANNOT OPEN CUSTOMER FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","COST.CNTR" TO COST.CNTR ELSE
      ERRMSG = "CANNOT OPEN COST.CNTR FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
      ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
      GOSUB 90000
      GOTO 99999
   END
*
*---- INITIALIZATION
*
*T26090 v
   PROCREAD BUFFER ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
      GOSUB 90000
      GOTO  99999
   END
   CONO      = BUFFER<1>
   RPT.NUM   = BUFFER<2>
   WHSE.LOC  = BUFFER<3>
   SEL.SDATE = BUFFER<4>
   SEL.EDATE = BUFFER<5>
   RPT.NAME  = ''
   CONO.NAME = ''
   HD1 = ''
   HD2 = ''
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NUM,"",HD1,HD2)
*T26090 ^
   IF CONO = "END" THEN GOTO 99999
*
*---- DEFINE HEADINGS
*
   SH1 = "                                                           "
   SH2 = "  Date      Time     Job #     Form          Customer      "   
   SH3 = "--------  --------  --------  -------  --------------------"
   SH1 = SH1:"                   To                 "    
   SH2 = SH2:"  Pallet #  Skid  CCtr  Quantity  Emp "
   SH3 = SH3:"  --------  ----  ----  --------  ----"
*
*---- INITIALIZE VARIABLES
*
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   LM = SPACE(4)
*
*---- MAIN PROCESSING
*
   PATTERN.STR = "1A1N'-'0N"
   IF NOT(WHSE.LOC MATCHES PATTERN.STR) THEN
      ERRMSG = "Invalid location format."
      GOSUB 90000
      GOTO  99999
   END
   IF SEL.SDATE > DATE() OR SEL.SDATE > SEL.EDATE THEN
      ERRMSG = "Invalid Date Range."
      GOSUB 90000
      GOTO  99999
   END
   GOSUB 1010
   CMD = "SSELECT WIP.TRACK.PALLET" 
   CMD = CMD:" BY WIPTP.DATE"
   CMD = CMD:" BY WIPTP.TIME"
   CMD = CMD:" BY WIPTP.JOB"
   CMD = CMD:" BY WIPTP.FORM"
   CMD = CMD:" BY WIPTP.VER"
   CMD = CMD:" BY WIPTP.SIG"
   CMD = CMD:" BY WIPTP.FSFX"
   CMD = CMD:" BY WIPTP.SKID"
   CMD = CMD:' WITH CONO = "':CONO:'"'
   CMD = CMD:' AND WITH WIP.PALLET = "Y"'
   CMD = CMD:' AND WITH INIT.LOC = "':WHSE.LOC:'"'
   CMD = CMD:' AND WITH WIPTP.DATE >= "':OCONV(SEL.SDATE,"D2/"):'"'
   CMD = CMD:' AND WITH WIPTP.DATE <= "':OCONV(SEL.EDATE,"D2/"):'"'
   PERFORM CMD CAPTURING SELMSG
   GOSUB 70000
*
   GOTO 99999
*
*---- GET PRESS WAREHOUSE LOCATION
*
1010 *
   IF WHSE.LOC[1,1] = "P" THEN
      CCTR = FIELD(WHSE.LOC,"-",2)
      MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE
         MAT CCTR.REC = ""
      END
   END ELSE
      MAT CCTR.REC = ""
   END
   RETURN
*
*---- PRINT REPORT
*
70000 *
   LINE.COUNT = 99
   PAGE.NO = 0
   PREV.DATE = "!@#$%"
   PRINT.CNT = 0
   PRINTER ON
   DONE = 0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      PAL.ID = ID[4,99]
      MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
         GOSUB 74000
      END
   REPEAT
   IF PRINT.CNT > 0 THEN
      PRINT
      PRINT "END OF REPORT"
      PRINT CHAR(12):
   END ELSE
      GOSUB 79000
      PRINT
      PRINT "NO ITEMS SELECTED"
   END
   PRINTER OFF
   PRINTER CLOSE
   RETURN
*
*---- PRINT DETAIL LINE(S)
*
74000 *
   MATREAD JOB.REC FROM JOB, CONO:WIPTP.JOB THEN
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE MAT CUST.REC=""
   END ELSE
      MAT JOB.REC = ""
      MAT CUST.REC = ""
   END
   BEGIN CASE
      CASE WIPTP.VER = "" AND WIPTP.SIG = ""
         FORM.NO = WIPTP.FORM
      CASE WIPTP.SIG = ""
         FORM.NO = WIPTP.FORM:".":WIPTP.VER
      CASE 1
         FORM.NO = WIPTP.FORM:".":WIPTP.VER:".":WIPTP.SIG
   END CASE
   LCNT = DCOUNT(WIPTP.MLOC,VM)
   BEGIN CASE
      CASE WIPTP.MDATE<1,LCNT> # PREV.DATE
         GOSUB 79000
         PLINE = OCONV(WIPTP.MDATE<1,LCNT>,"D2/")"L#8"
      CASE (LINE.COUNT + 1) > 55
         GOSUB 79000
         PLINE = OCONV(WIPTP.MDATE<1,LCNT>,"D2/")"L#8"
      CASE 1
         PLINE = """L#8"
   END CASE
   PTIME = OCONV(WIPTP.MTIME<1,LCNT>,"MTH")
   PTIME = PTIME[1,5]:" ":PTIME[6,2]
   PLINE = PLINE:SP2:PTIME"L#8"
   PLINE = PLINE:SP2:WIPTP.JOB"L#8"
   PLINE = PLINE:SP2:FORM.NO"L#7"
   PLINE = PLINE:SP2:CUST.NAME"L#20"
   PLINE = PLINE:SP2:PAL.ID"R#8"
   PLINE = PLINE:SP2:WIPTP.SKID"R#4"
*T22322v
*      PLINE = PLINE:SP2:WIPTP.TYPE"L#4"
   PLINE = PLINE:SP2:WIPTP.TO.OPER"L#4"
*T22322^
   PLINE = PLINE:SP2:OCONV(WIPTP.IQTY,"MD0,")"R#8"
   PLINE = PLINE:SP2:WIPTP.MEMP<1,1>"L#4"
   PRINT LM:PLINE
   LINE.COUNT = LINE.COUNT + 1
   PRINT.CNT = PRINT.CNT + 1
   PREV.DATE = WIPTP.DATE
   RETURN
*
*---- PRINT HEADINGS
*
79000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
   PRINT HD1: PAGE.NO
   PRINT HD2
   LINE.COUNT = 2
   PRINT
   IF CCTR.DESC = "" THEN
      PRINT "LOCATION: ":WHSE.LOC
   END ELSE
      PRINT "LOCATION: ":WHSE.LOC:" - ":CCTR.DESC
   END
   PRINT LM:SH1
   PRINT LM:SH2
   PRINT LM:SH3
   LINE.COUNT = LINE.COUNT + 5
   RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
*
99999 *
END
