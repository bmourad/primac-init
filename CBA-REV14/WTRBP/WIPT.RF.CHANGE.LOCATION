      SUBROUTINE WIPT.RF.CHANGE.LOCATION (ACTION, CONO, EMPID, MAT WIP.FILE.VARS)
*********************************************************************
*
* PROGRAM  - WIPT.RF.CHANGE.LOCATION
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/01/95
*
* DESCRIPTION
*
* This program is used to process Change Location transaction from 
* the hand-held R-F units used in the warehouse.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>CPYLIB>EDIT.COM
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.LOC
*COPY>WTR.CPYLIB>WIP.TRACK.CONTROL
*COPY>WTR.CPYLIB>WIP.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEST.MODE = 0
      IF LEN(ACTION) > 1 THEN
         IF ACTION[LEN(ACTION),1] = "T" THEN
            TEST.MODE = 1
         END
      END
      SPX = ""
      EMPNAME = "???????????????"
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPID THEN
         EMPNAME = (EMP.FRST.NAME:" ":EMP.LAST.NAME)[1,20]
      END
      IF LEN(EMPNAME) < 20 THEN SPX = SPACE(INT((21-LEN(EMPNAME))/2))
      EMPNAME = SPX:EMPNAME
*
*---- INITIALIZE EDIT.SUB
*
      MAT EDIT.COM = ""
      TYP = 0
      CALL WIPT.INPUT
      FILL = "#"
      ERY = 15
*
*---- DISPLAY SCREEN
*
      DIM SCRN(16)
      MAT SCRN = ""
      XXXXXXXX = " 123456789012345678901 "
      XXXXXXXX = "+---------------------+"
      SCRN(01) = "|   Change Location   |"
      SCRN(02) = "|---------------------|"
      SCRN(03) = "|@@@@@@@@@@@@@@@@@@@@@|"
      SCRN(04) = "|---------------------|"
      SCRN(05) = "| Curr Loc: ########  |"
      SCRN(06) = "|  New Loc: @@@@@@@@  |"
      SCRN(07) = "|   Verify: ########  |"
      SCRN(08) = "|                     |"
      SCRN(09) = "|                     |"
      SCRN(10) = "|------- PRIOR -------|"
      SCRN(11) = "|                     |"
      SCRN(12) = "|   Moved: @@@        |"
      SCRN(13) = "|    From: @@@@@@@@   |"
      SCRN(14) = "|      To: @@@@@@@@   |"
      SCRN(15) = "|                     |"
      SCRN(16) = "|                     |"
      XXXXXXXX = "+---------------------+"
      XXXXXXXX = " 123456789012345678901 "
*
      SLINE = @(-1)
      FOR SL = 1 TO 16
         SDATA = SCRN(SL)[2,21]
         CONVERT "#" TO " " IN SDATA
         CONVERT "@" TO " " IN SDATA
         SDATA = TRIMB(SDATA)
         LDATA = LEN(SDATA)
         LDONE = 0
         FOR N = 1 TO LDATA UNTIL LDONE
            IF SDATA[N,1] # " " THEN
               SLINE = SLINE:@((N-1),(SL-1)):SDATA[N,99]
               LDONE = 1
            END
         NEXT N
      NEXT SL
      PRINT SLINE:
      GOSUB 89990
*
*---- MAIN PROCESSING
*
1000 *
      PRINT @(0,2):EMPNAME:
      LOOP
         RELEASE
         TYP=1; X=11; Y=4; MINL=4; MAXL=8; EX=0; EY=9; ERY=9 
         CALL WIPT.INPUT
      UNTIL VALUE = "" OR VALUE = "END" DO
         CURLOC = VALUE
         MATREAD WIPTC.REC FROM WIP.TRACK.CONTROL, CONO:"CHGLOC" ELSE MAT WIPTC.REC=""
         LOCATE CURLOC IN WIPTC.CURLOC<1>,1 SETTING LOCPTR ELSE
            ERRMSG = "LOCATION NOT DEFINED"; GOSUB 91000; GOTO 1099
         END
         NEWLOC = WIPTC.NEWLOC<1,LOCPTR>
         PRINT @(11,05):NEWLOC"L#8":
1020 *
         TYP=1; X=11; Y=6; MINL=4; MAXL=8; EX=0; EY=9; ERY=9
         CALL WIPT.INPUT
         IF VALUE = "END" THEN GOTO 1099
         IF VALUE # NEWLOC THEN
            ERRMSG = "INVALID ENTRY"; GOSUB 91000; GOTO 1020
         END
         MATREADU WIPTC.REC FROM WIP.TRACK.CONTROL, CONO:"CHGLOC" THEN
            NULL
         END LOCKED
            SLEEP 1
            MATREADU WIPTC.REC FROM WIP.TRACK.CONTROL, CONO:"CHGLOC" THEN
               NULL
            END LOCKED
               ERRMSG = "CHGLOC RECORD LOCKED"; GOSUB 91000; GOTO 1099
            END ELSE
               MAT WIPTC.REC = ""
            END
         END ELSE
            MAT WIPTC.REC = ""
         END
         LOCATE CURLOC IN WIPTC.CURLOC<1>,1 SETTING LOCPTR ELSE
            ERRMSG = "LOCATION NOT DEFINED"; GOSUB 91000; GOTO 1099
         END
         IF WIPTC.NEWLOC<1,LOCPTR> # NEWLOC THEN
            ERRMSG = "NEW LOC CHANGED"; GOSUB 91000; GOTO 1099
         END
         READU CURPAL FROM WIP.TRACK.LOC, CONO:CURLOC LOCKED
            ERRMSG = "CURRENT LOC LOCKED"; GOSUB 91000; GOTO 1099
         END ELSE
            ERRMSG = "CURRENT LOC MISSING"; GOSUB 91000; GOTO 1099
         END
         READU NEWPAL FROM WIP.TRACK.LOC, CONO:NEWLOC LOCKED
            ERRMSG = "NEW LOC LOCKED"; GOSUB 91000; GOTO 1099
         END ELSE
            NEWPAL = ""
         END
         PCNT = DCOUNT(CURPAL,VM)
         FOR PPTR = 1 TO PCNT
            PAL.ID = CURPAL<1,PPTR>
            READU DUMMY FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
               NULL
            END LOCKED
               SLEEP 1
               READU DUMMY FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
                  NULL
               END LOCKED
                  ERRMSG = "FAILED: PALLET LOCKED"; GOSUB 91000; GOTO 1099
               END
            END
         NEXT PPTR
         CDATE=DATE(); CTIME=TIME(); IF CTIME < 10 THEN CDATE=DATE()
         UPDCNT = 0
         PPTR = 1
         LOOP
         WHILE PPTR <= PCNT DO
            PAL.ID = CURPAL<1,PPTR>
            MATREADU WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
               PLOC = WIPTP.MLOC<1,1>
               PQTY = WIPTP.CQTY
               WIPTP.MLOC  = INSERT(WIPTP.MLOC,1,1,0,NEWLOC)
               WIPTP.MDATE = INSERT(WIPTP.MDATE,1,1,0,CDATE)
               WIPTP.MTIME = INSERT(WIPTP.MTIME,1,1,0,CTIME)
               WIPTP.MEMP  = INSERT(WIPTP.MEMP,1,1,0,EMPID)
*NA            CALL WIPT.HIST.UPD(CONO,PLOC,PQTY,MAT WIPTP.REC,WIP.TRACK.HIST,ERRMSG)
               MATWRITE WIPTP.REC ON WIP.TRACK.PALLET, CONO:PAL.ID
               CURPAL = DELETE(CURPAL,1,PPTR,0)
               PCNT -= 1
               LOCATE PAL.ID IN NEWPAL<1>,1 BY "AL" SETTING P ELSE
                  NEWPAL = INSERT(NEWPAL,1,P,0,PAL.ID)
               END
               WRITEU CURPAL ON WIP.TRACK.LOC, CONO:CURLOC
               WRITEU NEWPAL ON WIP.TRACK.LOC, CONO:NEWLOC
               UPDCNT += 1
            END ELSE
               PPTR += 1
            END
         REPEAT
         IF CURPAL = "" THEN
            DELETE WIP.TRACK.LOC, CONO:CURLOC
            WIPTC.STATUS<1,LOCPTR> = "C"
         END ELSE
            WIPTC.STATUS<1,LOCPTR> = "I"
         END
         IF NEWPAL = "" THEN
            DELETE WIP.TRACK.LOC, CONO:NEWLOC
         END
         MATWRITE WIPTC.REC ON WIP.TRACK.CONTROL, CONO:"CHGLOC"
         PRINT @(10,11):UPDCNT"L#3":
         PRINT @(10,12):CURLOC"L#8":
         PRINT @(10,13):NEWLOC"L#8":
1099 *
         PRINT @(11,04):@(-4):
         PRINT @(11,05):@(-4):
         PRINT @(11,06):@(-4):
      REPEAT
      GOTO 99999
*
*---- DRAW BORDER
*
89990 *
      IF TEST.MODE THEN
         FOR SL = 1 TO 16
            PRINT @(21,(SL-1)):"|":
         NEXT SL
         PRINT @(0,16):"---------------------+":
      END
      RETURN
*
*---- ERROR ROUTINE
*
91000 *
      PRINT @(0,ERY):@(-4):BEL:BEL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,ERY):@(-4):
      ERY = 15
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
