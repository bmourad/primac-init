      SUBROUTINE HP.PRINT.BARCODE.39 (HPI,VPI,BPT,BSI,BDATA,VDATA,VPT)
*********************************************************************
*
* PROGRAM  - HP.PRINT.BARCODE.39
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 07/04/96
*
* DESCRIPTION
*
* This program is used to print cODE 39 barcode on an HP printer.
*
*   Parameters
*
*      HPI    = Horizontal position on page in inches
*      VPI    = Vertical position on page in inches
*      BPT    = Barcode pitch (characters per inch)
*      BSI    = Bar-code height in inches
*      BDATA  = Data to be printed
*      VDATA  = Verification data to be printed
*      VPT    = Verification data pitch (characters per inch)
*
* Print 3 of 9 bar-codes
*
*    0 = Narrow space
*    1 = Narrow bar
*    2 = Wide space
*    3 = Wide bar
*
*********************************************************************
*
*---- CONSTANTS
*
        ESC = CHAR(27)
*
*---- BARCODE SYMBOLOGY DEFINITION
*
      DIM CODE.TABLE(127)
      MAT CODE.TABLE = ""
*
      CODE.TABLE(32) = ""                   ;*
      CODE.TABLE(33) = ""                   ;*  !
      CODE.TABLE(34) = ""                   ;*  "
      CODE.TABLE(35) = ""                   ;*  #
      CODE.TABLE(36) = "1212121010"         ;*  $
      CODE.TABLE(37) = "1012121210"         ;*  %
      CODE.TABLE(38) = ""                   ;*  &
      CODE.TABLE(39) = ""                   ;*;*
      CODE.TABLE(40) = ""                   ;*  (
      CODE.TABLE(41) = ""                   ;*  )
      CODE.TABLE(42) = "1210303010"         ;*  *
      CODE.TABLE(43) = "1210121210"         ;*  +
      CODE.TABLE(44) = "1230103010"         ;*  ,
      CODE.TABLE(45) = "1210103030"         ;*  -
      CODE.TABLE(46) = "3210103010"         ;*  .
      CODE.TABLE(47) = "1212101210"         ;*  /
      CODE.TABLE(48) = "1012303010"         ;*  0
      CODE.TABLE(49) = "3012101030"         ;*  1
      CODE.TABLE(50) = "1032101030"         ;*  2
      CODE.TABLE(51) = "3032101010"         ;*  3
      CODE.TABLE(52) = "1012301030"         ;*  4
      CODE.TABLE(53) = "3012301010"         ;*  5
      CODE.TABLE(54) = "1032301010"         ;*  6
      CODE.TABLE(55) = "1012103030"         ;*  7
      CODE.TABLE(56) = "3012103010"         ;*  8
      CODE.TABLE(57) = "1032103010"         ;*  9
      CODE.TABLE(58) = ""                   ;*  :
      CODE.TABLE(59) = ""                   ;*  ;
      CODE.TABLE(60) = ""                   ;*  <
      CODE.TABLE(61) = ""                   ;*  =
      CODE.TABLE(62) = ""                   ;*  >
      CODE.TABLE(63) = ""                   ;*  ?
      CODE.TABLE(64) = ""                   ;*  @
      CODE.TABLE(65) = "3010121030"         ;*  A
      CODE.TABLE(66) = "1030121030"         ;*  B
      CODE.TABLE(67) = "3030121010"         ;*  C
      CODE.TABLE(68) = "1010321030"         ;*  D
      CODE.TABLE(69) = "3010321010"         ;*  E
      CODE.TABLE(70) = "1030321010"         ;*  F
      CODE.TABLE(71) = "1010123030"         ;*  G
      CODE.TABLE(72) = "3010123010"         ;*  H
      CODE.TABLE(73) = "1030123010"         ;*  I
      CODE.TABLE(74) = "1010323010"         ;*  J
      CODE.TABLE(75) = "3010101230"         ;*  K
      CODE.TABLE(76) = "1030101230"         ;*  L
      CODE.TABLE(77) = "3030101210"         ;*  M
      CODE.TABLE(78) = "1010301230"         ;*  N
      CODE.TABLE(79) = "3010301210"         ;*  O
      CODE.TABLE(80) = "1030301210"         ;*  P
      CODE.TABLE(81) = "1010103230"         ;*  Q
      CODE.TABLE(82) = "3010103210"         ;*  R
      CODE.TABLE(83) = "1030103210"         ;*  S
      CODE.TABLE(84) = "1010303210"         ;*  T
      CODE.TABLE(85) = "3210101030"         ;*  U
      CODE.TABLE(86) = "1230101030"         ;*  V
      CODE.TABLE(87) = "3230101010"         ;*  W
      CODE.TABLE(88) = "1210301030"         ;*  X
      CODE.TABLE(89) = "3210301010"         ;*  Y
      CODE.TABLE(90) = "1230301010"         ;*  Z
      CODE.TABLE(91) = ""                   ;*  [
      CODE.TABLE(92) = ""                   ;*  \
      CODE.TABLE(93) = ""                   ;*  ]
      CODE.TABLE(94) = ""                   ;*  ^
      CODE.TABLE(95) = ""                   ;*  _
*
*---- INITIALIZATION
*
      BWD = 720 / BPT / 16   ;* narrow bar width in decipoints
      HPD = HPI * 720        ;* first bar horizontal position in decipoints
      VPD = VPI * 720        ;* barcode vertical position in decipoints
      BSD = BSI * 720        ;* barcode height in decipoints
*
*---- PRINT BAR-CODE GRAPHICS
*
      DLEN = LEN(BDATA)
      FOR DPTR = 1 TO DLEN
         CH = BDATA[DPTR, 1]
         BCODE = CODE.TABLE(SEQ(CH))
         IF BCODE = "" THEN BCODE = "2220000000"
         FOR CPTR = 1 TO 10
            CD = BCODE[CPTR, 1]
            BEGIN CASE
            CASE CD = "0"                   ;* narrow space
               RWD = 1 * BWD
               TP = "1"
            CASE CD = "1"                   ;* narrow bar
               RWD = 1 * BWD
               TP = "0"
            CASE CD = "2"                   ;* wide spce
               RWD = 3 * BWD
               TP = "1"
            CASE CD = "3"                   ;* wide bar
               RWD = 3 * BWD
               TP = "0"
            END CASE
*
            HPOS = INT(HPD * 100 + 0.5) / 100
            RWDT = INT(RWD * 100 + 0.5) / 100
*
            PLINE = ""
            PLINE = PLINE : ESC  : "&a"
            PLINE = PLINE : HPOS : "h"      ;* set horizontal cursor position
            PLINE = PLINE : VPD  : "V"      ;* set vertical cursor position
            PLINE = PLINE : ESC  : "*c"
            PLINE = PLINE : RWDT : "h"      ;* set rectangle width
            PLINE = PLINE : BSD  : "v"      ;* set rectangle height
            PLINE = PLINE : TP   : "P"      ;* display filled rectangle
            PRINT PLINE:""
*
            HPD = HPD + RWD                 ;* next horizontal position
         NEXT CPTR
      NEXT DPTR
*
      IF LEN(VDATA) > 0 AND VPT > 0 AND VPT >= BPT THEN
         HPI = HPI + ((LEN(BDATA) / BPT) - (LEN(VDATA) / VPT)) / 2
         VPI = VPI + BSI + 2 * (0.85 / VPT)
*
         HPD = INT(HPI * 720 + 0.5)
         VPD = INT(VPI * 720 + 0.5)
*
         PLINE = ""
         PLINE = PLINE : ESC : "(8U"   ;* set default symbol set
         PLINE = PLINE : ESC : "(s"
         PLINE = PLINE : "0p"          ;* pitch (0 = fixed, 1 = variable)
         PLINE = PLINE : VPT : "h"     ;* pitch (char / inch)
         PLINE = PLINE : "0s"          ;* italic (0 = no, 1 = yes)
         PLINE = PLINE : "3b"          ;* weight (0 = normal, 3 = bold)
         PLINE = PLINE : "4102T"       ;* font = Letter Gothic
         PLINE = PLINE : ESC : "&a"
         PLINE = PLINE : HPD : "h"     ;* set horizontal cursor pos in decipoints (720 decipoints = 1 inch)
         PLINE = PLINE : VPD : "V"     ;* set vertical cursor pos in decipoints (720 decipoints = 1 inch)
         PRINT PLINE:
         PRINT VDATA
      END
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
