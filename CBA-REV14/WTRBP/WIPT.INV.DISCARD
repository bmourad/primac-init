*********************************************************************
*
* PROGRAM  - WIPT.INV.DISCARD
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/23/95
*
* DESCRIPTION
*
* This program produces the WIP Tracking Inventory Discard report.
*
*T22322 ct6 01/20/1998 * Add WIP Tracking to Bawden with Modifications.
*T26090 ajibaly 04/03/2002 * RUN RPT FROM PROC USING REPORT.SCRN AND
*                            GET.PROG.HEAD
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*T22322v
*COPY>WTR.CPYLIB>WIPT.STORAGE.LOC
*T22322^
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","JOB" TO JOB ELSE
      ERRMSG = "CANNOT OPEN JOB FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CUSTOMER" TO CUSTOMER ELSE
      ERRMSG = "CANNOT OPEN CUSTOMER FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
      ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
      GOSUB 90000
      GOTO 99999
   END
*T22322v
   OPEN "","WIPT.STORAGE.LOC" TO WIPT.STORAGE.LOC ELSE
      ERRMSG = "CANNOT OPEN WIPT.STORAGE.LOC FILE"
      GOSUB 90000
      GOTO 99999
   END
*T22322^
*
*---- INITIALIZATION
*
*T26090 v
   PROCREAD BUFFER ELSE
      ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
      GOSUB 90000
      GOTO  99999
   END
   CONO      = BUFFER<1>
   RPT.NUM   = BUFFER<2>
   RPT.NAME  = ''
   CONO.NAME = ''
   HD1 = ''
   HD2 = ''
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RPT.NAME,RPT.NUM,"",HD1,HD2)
*T26090 ^
   IF CONO = "END" THEN GOTO 99999
*
*---- DEFINE HEADINGS
*
   SH1 = "                                                              "
   SH2 = " Job #      Form                Customer        Pallet #  Skid"
   SH3 = "--------  --------------  --------------------  --------  ----"
   SH1 = SH1:"   To   <----- Final ---->  <------- Discard ------>"    
   SH2 = SH2:"  CCtr  Quantity  Location    Date      Time    Emp "
   SH3 = SH3:"  ----  --------  --------  --------  --------  ----"
*
*---- INITIALIZE VARIABLES
*
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   LM = ""
*
*---- MAIN PROCESSING
*
500 *
   CMD = "SSELECT WIP.TRACK.PALLET" 
   CMD = CMD:" BY WIPTP.JOB"
   CMD = CMD:" BY WIPTP.FORM"
   CMD = CMD:" BY WIPTP.VER"
*T22322         CMD = CMD:" BY WIPTP.SIG"
   CMD = CMD:" BY WIPTP.FSFX"
   CMD = CMD:' WITH CONO = "':CONO:'"'
   CMD = CMD:' AND WITH WIP.PALLET = "Y"'
*T22322         CMD = CMD:' AND WITH CURR.LOC # "SHREDDER"'
   CMD = CMD:' AND WITH CURR.STATUS = "D"'
   PERFORM CMD CAPTURING SELMSG
   GOSUB 70000
   GOTO 99999
*
*---- PRINT REPORT
*
70000 *
   LINE.COUNT = 99
   PAGE.NO = 0
   PREV.JOB = "!@#$%"
   PRINT.CNT = 0
   BREAK.CNT = 0
   PRINTER ON
   DONE = 0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      PAL.ID = ID[4,99]
      MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
*T22322v
         STMT = 'SSELECT WIPT.STORAGE.LOC WITH CONO = "':CONO:'"'
         STMT = STMT:' AND WITH RANGE.START <= "':WIPTP.MLOC<1,1>:'"'
         STMT = STMT:' AND WITH RANGE.END >= "':WIPTP.MLOC<1,1>:'"'
         PERFORM STMT RTNLIST LOC.LIST CAPTURING GARBAGE
         READNEXT WIPSL.ID FROM LOC.LIST THEN
            MATREAD WIPSL.REC FROM WIPT.STORAGE.LOC, CONO:WIPSL.ID ELSE
               MAT WIPSL.REC = ""
            END
            EOF.WSL = 0
            LOOP
               READNEXT TEST.ID FROM LOC.LIST ELSE EOF = 1
            UNTIL EOF DO
            REPEAT
         END ELSE
            MAT WIPSL.REC = ""
         END
         IF WIPSL.RANGE.ZERO = "Y" THEN GOTO 70199
*T22322^
         IF WIPTP.JOB # PREV.JOB THEN
            IF LINE.COUNT # 99 THEN
               PRINT
               LINE.COUNT = LINE.COUNT + 1
            END
            PREV.JOB = WIPTP.JOB
         END
         GOSUB 74000
      END
70199 *
   REPEAT
   IF PRINT.CNT > 0 THEN
      PRINT
      PRINT "END OF REPORT"
      PRINT CHAR(12):
   END ELSE
      GOSUB 79000
      PRINT
      PRINT "NO ITEMS SELECTED"
   END
   PRINTER OFF
   PRINTER CLOSE
   RETURN
*
*---- PRINT DETAIL LINE(S)
*
74000 *
   MATREAD JOB.REC FROM JOB, CONO:WIPTP.JOB THEN
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE MAT CUST.REC=""
   END ELSE
      MAT JOB.REC = ""
      MAT CUST.REC = ""
   END
   BEGIN CASE
*T22322
*      CASE WIPTP.VER = "" AND WIPTP.SIG = ""
      CASE WIPTP.VER = ""
*T22322^
         FORM.NO = WIPTP.FORM
*T22322v
*      CASE WIPTP.SIG = ""
      CASE 1
*T22322^
         FORM.NO = WIPTP.FORM:".":WIPTP.VER
*T22322      CASE 1
*T22322         FORM.NO = WIPTP.FORM:".":WIPTP.VER:".":WIPTP.SIG
   END CASE
   IF (LINE.COUNT + 1) > 55 THEN GOSUB 79000
   PLINE = WIPTP.JOB"L#8"
   PLINE = PLINE:SP2:FORM.NO"L#14"
   PLINE = PLINE:SP2:CUST.NAME"L#20"
   PLINE = PLINE:SP2:PAL.ID"R#8"
   PLINE = PLINE:SP2:WIPTP.SKID"R#4"
*T22322v
*      PLINE = PLINE:SP2:WIPTP.TYPE"L#4"
   PLINE = PLINE:SP2:WIPTP.TO.OPER"L#4"
*T22322^
   PLINE = PLINE:SP2:OCONV(WIPTP.CQTY,"MD0,")"R#8"
   PLINE = PLINE:SP2:WIPTP.MLOC<1,1>"L#8"
   PLINE = PLINE:SP2:OCONV(WIPTP.SDATE<1,1>,"D2/")"L#8"
   PTIME = OCONV(WIPTP.STIME<1,1>,"MTH")
   PTIME = PTIME[1,5]:" ":PTIME[6,2]
   PLINE = PLINE:SP2:PTIME"L#8"
   PLINE = PLINE:SP2:WIPTP.SEMP<1,1>"L#4"
   PRINT LM:PLINE
   LINE.COUNT = LINE.COUNT + 1
   PRINT.CNT = PRINT.CNT + 1
   BREAK.CNT = BREAK.CNT + 1
   RETURN
*
*---- PRINT HEADINGS
*
79000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
   PRINT HD1: PAGE.NO
   PRINT HD2
   PRINT
   PRINT
   PRINT LM:SH1
   PRINT LM:SH2
   PRINT LM:SH3
   LINE.COUNT = 7
   RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
*
99999 *
END
