      SUBROUTINE WIPT.LOC.UPD(CONO,PALID,PLOC,MAT WIPTP.REC,WIP.TRACK.LOC,ERRMSG)
*********************************************************************
*
* PROGRAM  - WIPT.LOC.UPD
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 05/27/96
*
* DESCRIPTION
*
* This program is used to update the WIP Tracking Location file. It
* must be call when the pallet location changes. The call should be
* made just before writing the WIP.TRACK.PALLET file.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.LOC
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
      CLOC = WIPTP.MLOC<1,1>
      CQTY = WIPTP.CQTY
      IF CLOC = PLOC THEN GOTO 99999
*
*---- MAIN PROCESSING
*
      IF PLOC # "" THEN
         MATREADU WIPTL.REC FROM WIP.TRACK.LOC, CONO:PLOC THEN
            LOCATE PALID IN WIPTL.PALLET<1>,1 SETTING P THEN
               WIPTL.PALLET = DELETE(WIPTL.PALLET,1,P,0)
            END
            MATWRITE WIPTL.REC ON WIP.TRACK.LOC, CONO:PLOC
         END ELSE
            RELEASE WIP.TRACK.LOC, CONO:PLOC
         END
      END ELSE
         RELEASE WIP.TRACK.LOC, CONO:PLOC
      END
*
      IF CLOC="" OR CLOC="VENDOR" OR CLOC="SHREDDER" OR CQTY=0 THEN
         NULL
      END ELSE
         MATREADU WIPTL.REC FROM WIP.TRACK.LOC, CONO:CLOC ELSE MAT WIPTL.REC = ""
         LOCATE PALID IN WIPTL.PALLET<1>,1 BY "AL" SETTING P ELSE
            WIPTL.PALLET = INSERT(WIPTL.PALLET,1,P,0,PALID)
         END
         MATWRITE WIPTL.REC ON WIP.TRACK.LOC, CONO:CLOC
      END
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
