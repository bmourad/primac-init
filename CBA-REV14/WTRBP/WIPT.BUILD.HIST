*********************************************************************
*
* PROGRAM  - WIPT.BUILD.HIST
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/26/95
*
* DESCRIPTION
*
* This program creates the WIP Tracking Inventory History file.
*
*T22322 ct6 02/19/1998 * TEST
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.HIST
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
P_X  = 0 ; P_Y = 23 ; P_VALUE = ERRMSG ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 99999
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
P_X  = 0 ; P_Y = 23 ; P_VALUE = ERRMSG ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 99999
      END
      OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
P_X  = 0 ; P_Y = 23 ; P_VALUE = ERRMSG ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 99999
      END
      OPEN "","WIP.TRACK.HIST" TO WIP.TRACK.HIST ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.HIST FILE"
P_X  = 0 ; P_Y = 23 ; P_VALUE = ERRMSG ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      CONO = ""
      MAT COMP.REC = ""
      CALL GET.CONO (CONO, MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
*
*---- INITIALIZE VARIABLES
*
      XTIME = ICONV("07:00:00","MTS")
      BDATE = DATE()
      EDATE = BDATE
      RECCNT = 0
      WHSE = ""
*
*---- MAIN PROCESSING
*
*       CRT @(-1)
P_X  = 0 ; P_Y = 23 ; P_VALUE = "Building History File" ; P_OPT = ""
* CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CLEARFILE WIP.TRACK.HIST
      SELECT WIP.TRACK.PALLET
      DONE = 0
      LOOP
         READNEXT ID ELSE DONE = 1
      UNTIL DONE DO
         IF ID[1,3] = CONO THEN
            PAL.ID = ID[4,99]
            MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
               RECCNT += 1
*              IF MOD(RECCNT,10) = 0 THEN CRT "#":
*              IF MOD(RECCNT,500) = 0 THEN CRT OCONV(RECCNT,"MD0,")"R#12"
               TEMP.DATE = ""
               TEMP.XLOC = ""
               TEMP.XQTY = ""
*
               MCNT = DCOUNT(WIPTP.MLOC,VM)
               FOR MPTR = MCNT TO 1 STEP -1
                  MDATE = WIPTP.MDATE<1,MPTR>
                  MTIME = WIPTP.MTIME<1,MPTR>
                  IF MTIME > XTIME THEN MDATE = MDATE + 1
                  MLOC = WIPTP.MLOC<1,MPTR>
                  LOCATE MDATE IN TEMP.DATE<1>,1 BY "DR" SETTING P THEN
                     TEMP.XLOC<1,P> = MLOC
                  END ELSE
                     TEMP.DATE = INSERT(TEMP.DATE,1,P,0,MDATE)
                     TEMP.XLOC = INSERT(TEMP.XLOC,1,P,0,MLOC)
                  END
               NEXT MPTR
*
               ACNT = DCOUNT(WIPTP.ACODE,VM)
               FOR APTR = ACNT TO 1 STEP -1
                  ADATE = WIPTP.ADATE<1,APTR>
                  ATIME = WIPTP.ATIME<1,APTR>
                  IF ATIME > XTIME THEN ADATE = ADATE + 1
                  AQTY = WIPTP.AQTY<1,APTR>
                  LOCATE ADATE IN TEMP.DATE<1>,1 BY "DR" SETTING P THEN
                     TEMP.XQTY<1,P> += AQTY
                  END ELSE
                     MLOC = TEMP.XLOC<1,P>
                     TEMP.DATE = INSERT(TEMP.DATE,1,P,0,ADATE)
                     TEMP.XLOC = INSERT(TEMP.XLOC,1,P,0,MLOC)
                     TEMP.XQTY = INSERT(TEMP.XQTY,1,P,0,AQTY)
                  END
               NEXT APTR
*
               TCNT = DCOUNT(TEMP.DATE,VM)
               PLOC = ""                    ;* previous location
               PQTY = 0                     ;* previous quantity
               FOR TPTR = TCNT TO 1 STEP -1
                  TDATE = TEMP.DATE<1,TPTR>
                  IF TDATE < BDATE THEN BDATE = TDATE
                  IF TDATE > EDATE THEN EDATE = TDATE
                  SECT = PLOC[1,2]
                  BEGIN CASE
                  CASE SECT = ""
                     PSECT = ""
                     PWHSE = ""
                  CASE SECT MATCHES "1A1N"
                     PSECT = SECT[1,1]
                     PWHSE = SECT[2,1]
                  CASE 1
                     PSECT = SECT
                     PWHSE = "0"
                  END CASE
                  CLOC = TEMP.XLOC<1,TPTR>
                  SECT = CLOC[1,2]
                  BEGIN CASE
                  CASE SECT = ""
                     CSECT = ""
                     CWHSE = ""
                  CASE SECT MATCHES "1A1N"
                     CSECT = SECT[1,1]
                     CWHSE = SECT[2,1]
                  CASE 1
                     CSECT = SECT
                     CWHSE = "0"
                  END CASE
                  LOCATE CWHSE IN WHSE,1 BY "AL" SETTING W ELSE WHSE<W> = CWHSE
                  CQTY = PQTY+TEMP.XQTY<1,TPTR>
                  IF PLOC # "" THEN
                     MATREADU WIPTH.REC FROM WIP.TRACK.HIST, CONO:PWHSE:"!":TDATE ELSE MAT WIPTH.REC = ""
                     IF PQTY > 0 AND CQTY = 0 THEN
                        WIPTH.PAL.CNT -= 1
                        IF WIPTP.PTYPE = "I" THEN WIPTH.INS.CNT -= 1
                     END
*
                     IF PQTY > 0 THEN
                        LOCATE PSECT IN WIPTH.SECTION<1>,1 BY "AL" SETTING P ELSE
                           WIPTH.SECTION = INSERT(WIPTH.SECTION,1,P,0,PSECT)
                           WIPTH.SEC.CNT = INSERT(WIPTH.SEC.CNT,1,P,0,"")
                        END
                        WIPTH.SEC.CNT<1,P> -= 1
                     END
                  END
*
                  BEGIN CASE
                  CASE PLOC = ""
                     MATREADU WIPTH.REC FROM WIP.TRACK.HIST, CONO:CWHSE:"!":TDATE ELSE MAT WIPTH.REC = ""
                  CASE CWHSE # PWHSE
                     MATWRITE WIPTH.REC ON WIP.TRACK.HIST, CONO:PWHSE:"!":TDATE
                     MATREADU WIPTH.REC FROM WIP.TRACK.HIST, CONO:CWHSE:"!":TDATE ELSE MAT WIPTH.REC = ""
                  END CASE
                  IF PQTY = 0 AND CQTY > 0 THEN
                     WIPTH.PAL.CNT += 1
                     IF WIPTP.PTYPE = "I" THEN WIPTH.INS.CNT += 1
                  END
*
                  IF CQTY > 0 THEN
                     LOCATE CSECT IN WIPTH.SECTION<1>,1 BY "AL" SETTING P ELSE
                        WIPTH.SECTION = INSERT(WIPTH.SECTION,1,P,0,CSECT)
                        WIPTH.SEC.CNT = INSERT(WIPTH.SEC.CNT,1,P,0,"")
                     END
                     WIPTH.SEC.CNT<1,P> += 1
                  END
*
                  MATWRITE WIPTH.REC ON WIP.TRACK.HIST, CONO:CWHSE:"!":TDATE
                  PQTY = CQTY
                  PLOC = CLOC
               NEXT TPTR
            END
         END
      REPEAT
*P_X  = RINT ; P_Y =  ; P_VALUE = RINT ; P_OPT = ""
*CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
*---- CREATE CUMMULATIVE TOTALS BY DAY
*
      WCNT = DCOUNT(WHSE,AM)
      FOR WPTR = 1 TO WCNT
         PALCNT = 0
         INSCNT = 0
         PREV.SECT = ""
         PREV.QTY = ""
         CWHSE = WHSE<WPTR>
         FOR TDATE = BDATE TO EDATE
            MATREAD WIPTH.REC FROM WIP.TRACK.HIST, CONO:CWHSE:"!":TDATE ELSE MAT WIPTH.REC = ""
            WIPTH.PAL.CNT += PALCNT
            WIPTH.INS.CNT += INSCNT
            SCNT = DCOUNT(PREV.SECT,VM)
            FOR SPTR = 1 TO SCNT
               SECT = PREV.SECT<1,SPTR>
               LOCATE SECT IN WIPTH.SECTION<1>,1 BY "AL" SETTING P ELSE
                  WIPTH.SECTION = INSERT(WIPTH.SECTION,1,P,0,SECT)
                  WIPTH.SEC.CNT = INSERT(WIPTH.SEC.CNT,1,P,0,"")
               END
               WIPTH.SEC.CNT<1,P> += PREV.QTY<1,SPTR>
            NEXT SPTR
            PALCNT = WIPTH.PAL.CNT
            INSCNT = WIPTH.INS.CNT
            PREV.SECT = WIPTH.SECTION
            PREV.QTY = WIPTH.SEC.CNT
            MATWRITE WIPTH.REC ON WIP.TRACK.HIST, CONO:CWHSE:"!":TDATE
         NEXT TDATE
      NEXT WPTR
*
*---- END OF JOB
*
99999 *
   END
