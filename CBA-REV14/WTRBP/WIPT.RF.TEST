*********************************************************************
*
* PROGRAM  - WIPT.RF.MAIN
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/01/95
*
* DESCRIPTION
*
* This program is used to process transaction from the hand-held 
* R-F units used in the warehouse.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>WTR.CPYLIB>WIP.TRACK.CONTROL
*COPY>WTR.CPYLIB>WIP.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- PRE-INITIALIZATION
*
      PROCREAD PARAM ELSE PARAM = ""
      ACTION = PARAM<1>
      TEST.MODE = 0
      IF LEN(ACTION) > 1 THEN
         IF ACTION[LEN(ACTION),1] = "T" THEN TEST.MODE = 1
      END
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOTO 93000
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOTO 93000
      END
      OPEN "","JOB" TO JOB ELSE
         ERRMSG = "CANNOT OPEN JOB FILE"
         GOTO 93000
      END
      OPEN "","CUSTOMER" TO CUSTOMER ELSE
         ERRMSG = "CANNOT OPEN CUSTOMER FILE"
         GOTO 93000
      END
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE
         ERRMSG = "CANNOT OPEN EMPLOYEE FILE"
         GOTO 93000
      END
      OPEN "","VEND" TO VEND ELSE
         ERRMSG = "CANNOT OPEN VEND FILE"
         GOTO 93000
      END
      OPEN "","WIP.TRACK.JOB" TO WIP.TRACK.JOB ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.JOB FILE"
         GOTO 93000
      END
      OPEN "","WIP.TRACK.FORM" TO WIP.TRACK.FORM ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.FORM FILE"
         GOTO 93000
      END
      OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
         GOTO 93000
      END
      OPEN "","WIP.TRACK.CONTROL" TO WIP.TRACK.CONTROL ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.CONTROL FILE"
         GOTO 93000
      END
      OPEN "","WIP.TRACK.LOC" TO WIP.TRACK.LOC ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.LOC FILE"
         GOTO 93000
      END
      OPEN "","WIP.TRACK.HIST" TO WIP.TRACK.HIST ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.HIST FILE"
         GOTO 93000
      END
*
*---- INITIALIZATION
*
      CONO = ""
      MAT COMP.REC = ""
      CALL GET.CONO (CONO, MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
      USER.ID = @LOGNAME
      EMPID = "XXXX"
      EMPNAME = ""
      MATREAD WIPTE.REC FROM WIP.TRACK.CONTROL, CONO:"EMP" THEN
         LOCATE USER.ID IN WIPTE.USER<1>,1 SETTING EPTR THEN 
            EMPID = WIPTE.EMP<1,EPTR>
            MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPID THEN
               EMPNAME = EMP.FRST.NAME:" ":EMP.LAST.NAME
            END
         END
      END
      MATREADU WIPTR.REC FROM WIP.TRACK.CONTROL, CONO:"REQ" THEN
         POS = 1
         LOOP
            LOCATE USER.ID IN WIPTR.USER<1>,POS SETTING PTR ELSE PTR = 0
         WHILE PTR > 0 DO
            WIPTR.USER<1,PTR> = ""
            WIPTR.PALLET<1,PTR> = ""
            POS = PTR + 1
         REPEAT
         MATWRITE WIPTR.REC ON WIP.TRACK.CONTROL, CONO:"REQ"
      END ELSE
         RELEASE WIP.TRACK.CONTROL, CONO:"REQ"
      END
*
*---- INITIALIZE EDIT.SUB
*
      MAT EDIT.COM = ""
      TYP = 0
      CALL WIPT.INPUT
      GOSUB 89990
      FILL = "#"
      ERY = 15
*
*---- INITIALIZE MENUS AND SCREENS
*
      DIM SCRN(16)
      MAT SCRN = ""
      XXXXXXXX = " 123456789012345678901 "
      XXXXXXXX = "+---------------------+"
      SCRN(01) = "|      Main Menu      |"
      SCRN(02) = "|---------------------|"
      SCRN(03) = "|                     |"
      SCRN(04) = "|  1- Sign-on         |"
      SCRN(05) = "|  2- Store Pallet    |"
      SCRN(06) = "|  3- Pallet Request  |"
      SCRN(07) = "|  4- Locate Pallet   |"
      SCRN(08) = "|  5- Location Inquiry|"
      SCRN(09) = "|  6- Change Location |"
      SCRN(10) = "|  7- Update Pallet   |"
      SCRN(11) = "|                     |"
      SCRN(12) = "|  X- Exit            |"
      SCRN(13) = "|                     |"
      SCRN(14) = "|    Selection: #     |"
      SCRN(15) = "|                     |"
      SCRN(16) = "|                     |"
      XXXXXXXX = "+---------------------+"
      XXXXXXXX = " 123456789012345678901 "
*
      SLINE = @(-1)
      FOR SL = 1 TO 16
         SDATA = SCRN(SL)[2,21]
         CONVERT "#" TO " " IN SDATA
         CONVERT "@" TO " " IN SDATA
         SDATA = TRIMB(SDATA)
         LDATA = LEN(SDATA)
         LDONE = 0
         FOR N = 1 TO LDATA UNTIL LDONE
            IF SDATA[N,1] # " " THEN
               SLINE = SLINE:@((N-1),(SL-1)):SDATA[N,99]
               LDONE = 1
            END
         NEXT N
      NEXT SL
      PRINT SLINE:
      GOSUB 89990
*
*---- MAIN PROCESSING
*
100 *
      RELEASE
      TYP=1; X=15; Y=13; MAXL=1
      EX=0; EY=15; ERY=15
      VALDAT="1":VM:"2":VM:"3":VM:"4":VM:"5":VM:"6":VM:"7":VM:"X"
      CALL WIPT.INPUT
      GOSUB 89990
      OPTION = VALUE
120 *
      BEGIN CASE
      CASE OPTION = "END"
         GOTO 99999
      CASE OPTION = "1"           ;* SIGN-ON
         CALL WIPT.RF.SIGN.ON(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "2"           ;* PALLET STORAGE
         CALL WIPT.RF.STORE.PALLET.NEW(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "3"           ;* PALLET REQUEST
         CALL WIPT.RF.PALLET.REQUEST(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "4"           ;* LOCATE PALLET
         CALL WIPT.RF.LOCATE.PALLET(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "5"           ;* LOCATION INQUIRY
         CALL WIPT.RF.LOCATION.INQUIRY(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "6"           ;* CHANGE LOCATION
         CALL WIPT.RF.CHANGE.LOCATION(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "7"           ;* DEFINE PALLET
         CALL WIPT.RF.UPDATE.PALLET(ACTION,CONO,EMPID,MAT WIP.FILE.VARS)
      CASE OPTION = "X"
         GOTO 99999
      END CASE
      PRINT SLINE:
      GOSUB 89990
      GOTO 100
*
*---- DRAW BORDER
*
89990 *
      IF TEST.MODE THEN
         FOR SL = 1 TO 16
            PRINT @(21,(SL-1)):"|":
         NEXT SL
         PRINT @(0,16):"---------------------+":
      END
      RETURN
*
*---- ERROR ROUTINE
*
91000 *
      PRINT @(0,ERY):@(-4):BEL:BEL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,ERY):@(-4):
      GOSUB 89990
      ERY = 15
      RETURN
93000 *
      PRINT @(0,15):@(-4):ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,15):@(-4):
      GOTO 99999
*
*---- END OF PROGRAM
*
99999 *
      PRINT @(-1):
   END
