*********************************************************************
*
* PROGRAM  - WIPT.JOB.INQ
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 08/23/96
*
* DESCRIPTION
*
* This program is used to view WIP jobs and their associated forms.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>CPYLIB>XREF.DATA
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- DEFINE ARRAY STORAGE
*
      DIM STOT(10)               ;* SUB TOTALS
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","PREFIX" TO PREFIX ELSE
         ERRMSG = "CANNOT OPEN PREFIX FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","XREF.DATA" TO XREF.DATA ELSE
         ERRMSG = "CANNOT OPEN XREF.DATA FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","JOB" TO JOB ELSE
         ERRMSG = "CANNOT OPEN JOB FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CUSTOMER" TO CUSTOMER ELSE
         ERRMSG = "CANNOT OPEN CUSTOMER FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","CUSTOMER.XREF" TO CUSTOMER.XREF ELSE
         ERRMSG = "CANNOT OPEN CUSTOMER.XREF FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WTR.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN WTR.SCREENS FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WIP.TRACK.JOB" TO WIP.TRACK.JOB ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.JOB FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WIP.TRACK.FORM" TO WIP.TRACK.FORM ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.FORM FILE"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      CONO = ""
      MAT COMP.REC = ""
      CALL GET.CONO (CONO, MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
      MAT GEN.XREF.REC = ""
      GXR.CO = CONO
*
*---- INITIALIZE SCRN.EDIT
*
      MAT SCV.REC = ""
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.CNT = 1
      ECD.SCRN.NAME<1> = "WIPT.JOB.INQ"
      ECD.ACTION=1; CALL SCRN.EDIT
      ECD.SCRN.NO = 1
      ESN = ECD.SCRN.NO
      ECD.ACTION=2; CALL SCRN.EDIT
*
*---- DEFINE HEADINGS
*
      REPORT.NAME = "W I P   J O B   I N Q U I R Y"
      TODAY = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN TODAY = DATE()
      PTIME = OCONV(CTIME,"MTH")
      PTIME = PTIME[1,5]:" ":PTIME[6,2]
      HD1A = "RUN: ":OCONV(TODAY,"D2/"):" @ ":PTIME
      HD1B = SPACE((60-LEN(CO.NAME))/2):CO.NAME
      HD1C = "PAGE "
      HD1 = HD1A"L#30":HD1B"L#60":HD1C"R#30"
      HD2 = SPACE((120-LEN(REPORT.NAME))/2):REPORT.NAME
      SH1 = "                                        WIP               Consumed"
      SH2 = "    Form #      Pallet #  Skid  Type  Quantity  Location  Quantity"   
      SH3 = "--------------  --------  ----  ----  --------  --------  --------"
      ST1 = "                                     ---------           ---------"
*
*---- INITIALIZE VARIABLES
*
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      LM = SPACE(3)
      BEGIN.PAGE = 8
      PAGE.SIZE = 12
      LINE.SPACE = 1
      OLD.START = 0
      SEL.JOB = ""
      SEL.FORM = "ALL"
      SEL.FNUM = "ALL"
      SEL.VNUM = ""
      SEL.SNUM = ""
      GOTO 110
*
*---- MAIN PROCESSING
*
100 *
      ECD.ACTION=6; CALL SCRN.EDIT
      MAT SCV.REC = ""
110 *
      SCV.REC(1)<ESN> = DATE()
      ECD.NUM=1; ECD.ACTION=5; CALL SCRN.EDIT
      GOSUB 1010
      IF ECD.RET.VALUE = "END" THEN GOTO 99999
      IF JOB.CUST = "" THEN
         MAT CUST.REC = ""
      END ELSE
         SEL.CUST = JOB.CUST
         MATREAD CUST.REC FROM CUSTOMER, CONO:SEL.CUST ELSE
            MAT CUST.REC = ""
            CUST.NAME = "???????????????"
         END
         SCV.REC(3)<ESN> = SEL.CUST
         SCV.REC(4)<ESN> = CUST.NAME
         ECD.NUM=3; ECD.ACTION=5; CALL SCRN.EDIT
         ECD.NUM=4; ECD.ACTION=5; CALL SCRN.EDIT
      END
      GOSUB 80000
      GOSUB 1030
      IF ECD.RET.VALUE = "END" THEN GOTO 100
      GOSUB 1040
      IF ECD.RET.VALUE = "END" THEN GOTO 100
      GOSUB 81000
      LN = 1
      OLD.START = 0
      GOSUB 50000
*
*---- GET OPERATOR REQUEST
*
500 *
      SCV.REC(21)<ESN> = ""
      ECD.NUM=21; ECD.ACTION=4; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "E"
            GOTO 100
         CASE OPTION = "S" OR OPTION = "SF"
            LN = LN + PAGE.SIZE
            IF LN > LINES THEN LN = 1
            GOSUB 50000
         CASE OPTION = "SR"
            LN = LN - PAGE.SIZE
            IF LN < 1 THEN LN = 1
            GOSUB 50000
         CASE OPTION = "ST"
            LN = 1
            GOSUB 50000
         CASE OPTION = "SB"
            LN = LINES
            IF LN < 1 THEN LN = 1
            GOSUB 50000
         CASE OPTION = "F"
            GOSUB 70000
            IF PRINT.CNT > 0 THEN
               ERRMSG = "** COMPLETED **"
            END ELSE
               ERRMSG = "** NO ITEMS PRINTED **"
            END
            GOSUB 90000
            GOTO 100
      END CASE
      GOTO 500
*
*---- GET JOB NUMBER
*
1010 *
      SCV.REC(2)<ESN> = SEL.JOB
      ECD.NUM=2; ECD.ACTION=4; CALL SCRN.EDIT
      BEGIN CASE
         CASE ECD.RET.VALUE = "END"
            RETURN
         CASE ECD.RET.VALUE = ""
            GOSUB 1020
            IF ECD.RET.VALUE = "END" THEN GOTO 1010
            GXR.NAME = "JOB"
            GXR.XREF = CUSTOMER
            GXR.FILE = JOB
            GXR.SRCH.ID = SEL.CUST
            CALL JOB.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
            ECD.ACTION=2; CALL SCRN.EDIT
            GOSUB 80050
            IF GXR.ID = "" THEN GOTO 1010
            ECD.RET.VALUE = GXR.ID
      END CASE
      MATREAD WIPTJ.REC FROM WIP.TRACK.JOB, CONO:ECD.RET.VALUE ELSE
         ERRMSG = "No form(s) found for specified job. Try again! "
         GOSUB 90000
         GOTO 1010
      END
      IF ECD.RET.VALUE # SEL.JOB THEN SEL.FORM = "ALL"
      SEL.JOB = ECD.RET.VALUE
      MATREAD JOB.REC FROM JOB, CONO:SEL.JOB THEN
         IF JOB.CUST # WIPTJ.CUST THEN 
            MATREADU WIPTJ.REC FROM WIP.TRACK.JOB, CONO:SEL.JOB THEN
               WIPTJ.CUST = JOB.CUST
               MATWRITE WIPTJ.REC ON WIP.TRACK.JOB, CONO:SEL.JOB
            END ELSE
               RELEASE WIP.TRACK.JOB, CONO:SEL.JOB
            END
         END
      END ELSE 
         MAT JOB.REC = ""
      END
      RETURN
*
*---- GET CUSTOMER ID
*
1020 *
      ECD.NUM=3; ECD.ACTION=4; CALL SCRN.EDIT
      BEGIN CASE
         CASE ECD.RET.VALUE = "END"
            RETURN
         CASE ECD.RET.VALUE = ""
            ECD.NUM=4; ECD.ACTION=4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = "END" THEN RETURN
            IF ECD.RET.VALUE = "" THEN RETURN
            GXR.NAME = "CUSTOMER"
            GXR.XREF = CUSTOMER.XREF
            GXR.FILE = CUSTOMER
            GXR.SRCH.ID = ECD.RET.VALUE
            CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
            ECD.ACTION=2; CALL SCRN.EDIT
            GOSUB 80050
            IF GXR.ID = "" THEN GOTO 1010
            ECD.RET.VALUE = GXR.ID
      END CASE
      MATREAD CUST.REC FROM CUSTOMER, CONO:ECD.RET.VALUE ELSE
         ERRMSG = "Customer not on file. Try again! "
         GOSUB 90000
         GOTO 1010
      END
      SEL.CUST = ECD.RET.VALUE
      SCV.REC(3)<ESN> = SEL.CUST
      ECD.NUM=3; ECD.ACTION=5; CALL SCRN.EDIT
      SCV.REC(4)<ESN> = CUST.NAME
      ECD.NUM=4; ECD.ACTION=5; CALL SCRN.EDIT
      RETURN
*
*---- GET FORM NUMBER
*
1030 *
      SCV.REC(5)<ESN> = SEL.FORM
      ECD.NUM=5; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      IF ECD.RET.VALUE = "ALL" THEN
         SEL.FORM = ECD.RET.VALUE
         FNUM = "ALL"
         VNUM = ""
         SNUM = ""
      END ELSE
         FORMNO = ECD.RET.VALUE; GOSUB 85100
         IF ERRMSG # "" THEN
            GOSUB 90000
            GOTO 1030
         END
         SEL.FORM = FORMNO
      END
      SEL.FNUM = FNUM
      SEL.VNUM = VNUM
      SEL.SNUM = SNUM
      SCV.REC(5)<ESN> = SEL.FORM
      ECD.NUM=5; ECD.ACTION=5; CALL SCRN.EDIT
      RETURN
*
*---- SHOW EMPTY PALLETS
*
1040 *
      ECD.NUM=6; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      SHOW.EMPTY = ECD.RET.VALUE
      RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
      START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
      LAST.LINE = START.LINE + PAGE.SIZE - 1
      IF LAST.LINE > LINES THEN LAST.LINE = LINES
      IF START.LINE = OLD.START THEN RETURN
      OLD.START = START.LINE
      CNT = 1
      FOR N = START.LINE TO LAST.LINE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
         P_X = 1 ; P_Y = SLN ; P_VALUE = N ; P_OPT = 'CL'
         P_X := AM:6 ; P_Y := AM:SLN ; P_VALUE = SCV.REC(12)<1,N> "L#14"         ;*FORM
         P_X := AM:22 ; P_Y := AM:SLN ; P_VALUE = SCV.REC(13)<1,N> "L#4"         ;*SKID
         P_X := AM:28 ; P_Y := AM:SLN ; P_VALUE = SCV.REC(14)<1,N> "L#8"         ;*PALLET
         P_X := AM:38 ; P_Y := AM:SLN ; P_VALUE = SCV.REC(15)<1,N> "L#4"         ;*TYPE
         P_X := AM:44 ; P_Y := AM:SLN ; P_VALUE = OCONV(SCV.REC(16)<1,N>,"MD0,") "R#8"         ;*QUANTITY
         P_X := AM:54 ; P_Y := AM:SLN ; P_VALUE = SCV.REC(17)<1,N> "L#8"         ;*LOCATION
         P_X := AM:64 ; P_Y := AM:SLN ; P_VALUE = OCONV(SCV.REC(18)<1,N>,"MD0,") "R#8"         ;*CONSUMED
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         CNT = CNT + 1
      NEXT N
      FOR N = CNT TO PAGE.SIZE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
*        PRINT @(0,SLN) : @(-4) :
         P_X = 0 ; P_Y = SLN ; P_VALUE = '' ; P_OPT = 'CL'
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT N
      RETURN
*
*---- PRINT REPORT
*
70000 *
      LINE.COUNT = 99
      PAGE.NO = 0
      MAT STOT = 0
      PREV.FORM = "!@#$%"
      PRINT.CNT = 0
      BREAK.CNT = 0
*     PERFORM "TERM ,,160"
      PRINTER ON
*     PRINT CHAR(27):"E":CHAR(27):"&l1O":
      JOB.NO = SEL.JOB
      MATREAD WIPTJ.REC FROM WIP.TRACK.JOB, CONO:JOB.NO THEN
         SCNT = DCOUNT(SORT.ID,VM)
         FOR SPTR = 1 TO SCNT
            FORM.NO = TRIM(FIELD(SORT.ID<1,SPTR>,"!",1))
            PAL.ID = FIELD(SORT.ID<1,SPTR>,"!",3)
            GOSUB 72000
         NEXT SPTR
      END
      IF PRINT.CNT > 0 THEN
         GOSUB 76000
         PRINT
         PRINT "END OF REPORT"
         PRINT CHAR(12):
      END ELSE
         JOB.NO = SEL.JOB
         GOSUB 79000
         PRINT
         PRINT "NO ITEMS SELECTED"
      END
      PRINT CHAR(27):"E":
      PRINTER OFF
      PRINTER CLOSE
      RETURN
*
*---- PROCESS JOB FORM
*
72000 *
      MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
         BEGIN CASE
            CASE FORM.NO # PREV.FORM
               GOSUB 76000
               PREV.FORM = FORM.NO
         END CASE
         GOSUB 74000
      END
      RETURN
*
*---- PRINT DETAIL LINE
*
74000 *
      BEGIN CASE
         CASE BREAK.CNT = 0 AND (LINE.COUNT + 1) = 42
            GOSUB 79000
            PLINE = FORM.NO"L#14"
         CASE (LINE.COUNT + 1) > 42
            GOSUB 79000
            IF BREAK.CNT = 0 THEN
               PLINE = FORM.NO"L#14"
            END ELSE
               PLINE = "(cont'd)""L#14"
            END
         CASE BREAK.CNT = 0 
            PLINE = FORM.NO"L#14"
         CASE 1
            PLINE = """L#14"
      END CASE
      PLINE = PLINE:SP2:PAL.ID"R#8"
      PLINE = PLINE:SP2:WIPTP.SKID"R#4"
      PLINE = PLINE:SP2:WIPTP.TYPE"L#4"
      PLINE = PLINE:SP2:OCONV(WIPTP.CQTY,"MD0Z,")"R#8"
      PLINE = PLINE:SP2:WIPTP.MLOC<1,1>"L#8"
      UQTY = 0
      ACNT = DCOUNT(WIPTP.ACODE,VM)
      FOR APTR = 1 TO ACNT
         IF WIPTP.ACODE<1,APTR> = "C" THEN UQTY -= WIPTP.AQTY<1,APTR>
      NEXT APTR
      PLINE = PLINE:SP2:OCONV(UQTY,"MD0Z,")"R#8"
      PRINT LM:PLINE
      LINE.COUNT = LINE.COUNT + 1
*
      IF NUM(WIPTP.CQTY) THEN STOT(1) = STOT(1) + WIPTP.CQTY
      IF NUM(WIPTP.CQTY) THEN STOT(2) = STOT(2) + (WIPTP.IQTY-WIPTP.CQTY)
      PRINT.CNT = PRINT.CNT + 1
      BREAK.CNT = BREAK.CNT + 1
      RETURN
*
*---- PRINT FORM SUB-TOTALS
*
76000 *
      IF BREAK.CNT > 0 THEN
         PRINT LM:ST1
         TLINE = "                                    " 
         TLINE = TLINE:OCONV(STOT(1),"MD0,")"R#10"
         TLINE = TLINE:SPACE(10)
         TLINE = TLINE:OCONV(STOT(2),"MD0,")"R#10"
         PRINT LM:TLINE
         PRINT
         LINE.COUNT = LINE.COUNT + 3
         MAT STOT = 0
         BREAK.CNT = 0
      END
      RETURN
*
*---- PRINT HEADINGS
*
79000 *
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PRINT
      PRINT ("Job #: ":JOB.NO)"L#30":SPACE(5):"Customer: ":CUST.NAME
      PRINT
      PRINT ("Form : ":SEL.FORM)"L#30":SPACE(5):"Print empty pallets : ":SHOW.EMPTY
      PRINT
      PRINT LM:SH1
      PRINT LM:SH2
      PRINT LM:SH3
      LINE.COUNT = 11
      RETURN
*
*---- DISPLAY SCREEN DATA
*
80000 *
      SCV.REC(1)<ESN> = DATE()
      SCV.REC(2)<ESN> = SEL.JOB
      SCV.REC(3)<ESN> = SEL.CUST
      SCV.REC(4)<ESN> = CUST.NAME
      SCV.REC(5)<ESN> = SEL.FORM
80050 *
      ECD.ACTION=3; CALL SCRN.EDIT
      RETURN
*
*---- LOAD MULTI-LINE DATA
*
81000 *
      SCV.REC(12) = ""
      SCV.REC(13) = ""
      SCV.REC(14) = ""
      SCV.REC(15) = ""
      SCV.REC(16) = ""
      SCV.REC(17) = ""
      SCV.REC(18) = ""
      SCV.REC(19) = ""
      LINES = 0
      SORT.ID = ""
      JOB.NO = SEL.JOB
      MATREAD WIPTJ.REC FROM WIP.TRACK.JOB, CONO:JOB.NO THEN
         FCNT = DCOUNT(WIPTJ.FORM,VM)
         FOR FPTR = 1 TO FCNT
            FORM.NO = WIPTJ.FORM<1,FPTR>
            FNUM = FIELD(FORM.NO,".",1)
            VNUM = FIELD(FORM.NO,".",2)
            SNUM = FIELD(FORM.NO,".",3)
            IF SEL.FNUM = "ALL" OR FNUM = SEL.FNUM THEN
               IF SEL.VNUM = "" OR VNUM = SEL.VNUM THEN
                  IF SEL.SNUM = "" OR SNUM = SEL.SNUM THEN
                     MATREAD WIPTF.REC FROM WIP.TRACK.FORM, CONO:JOB.NO:"!":FORM.NO THEN
                        PCNT = DCOUNT(WIPTF.PALLET,VM)
                        FOR PPTR = 1 TO PCNT
                           PAL.ID = WIPTF.PALLET<1,PPTR>
                           MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
                              IF SHOW.EMPTY = "Y" OR WIPTP.CQTY > 0 THEN
                                 SID = FORM.NO"L#15":"!":(1000+WIPTP.SKID):"!":PAL.ID
                                 LOCATE SID IN SORT.ID<1>,1 BY "AL" SETTING PP ELSE NULL
                                 SORT.ID = INSERT(SORT.ID,1,PP,0,SID)
                              END
                           END
                        NEXT PPTR
                     END
                  END
               END
            END
         NEXT FPTR
         SCNT = DCOUNT(SORT.ID,VM)
         FOR SPTR = 1 TO SCNT
            FORM.NO = TRIM(FIELD(SORT.ID<1,SPTR>,"!",1))
            PAL.ID = FIELD(SORT.ID<1,SPTR>,"!",3)
            MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
               LINES += 1
               SCV.REC(12)<1,LINES> = FORM.NO
               SCV.REC(13)<1,LINES> = WIPTP.SKID
               SCV.REC(14)<1,LINES> = PAL.ID
               SCV.REC(15)<1,LINES> = WIPTP.TYPE
               SCV.REC(16)<1,LINES> = WIPTP.CQTY
               SCV.REC(17)<1,LINES> = WIPTP.MLOC<1,1>
               UQTY = 0
               ACNT = DCOUNT(WIPTP.ACODE,VM)
               FOR APTR = 1 TO ACNT
                  IF WIPTP.ACODE<1,APTR> = "C" THEN UQTY -= WIPTP.AQTY<1,APTR>
               NEXT APTR
               SCV.REC(18)<1,LINES> = UQTY
            END
         NEXT SPTR
      END
      RETURN
*
*---- FORMAT FORM NUMBER
*
85100 *
      ERRMSG = ""
      FNUM = FIELD(FORMNO,".",1)
      VNUM = FIELD(FORMNO,".",2)
      SNUM = FIELD(FORMNO,".",3)
      BEGIN CASE
         CASE NUM(FNUM)
            BEGIN CASE
               CASE FNUM = ""
                  ERRMSG = 1
               CASE FNUM = 0
                  ERRMSG = 1
               CASE LEN(FNUM) > 4
                  ERRMSG = 1
               CASE 1
                  FNUM = (10000+FNUM)[2,4]
                  BEGIN CASE
                     CASE VNUM = "" AND SNUM = ""
                        FORMNO = FNUM
                     CASE VNUM = ""
                        ERRMSG = 2
                     CASE VNUM = 0
                        ERRMSG = 2
                     CASE LEN(VNUM) > 3
                        ERRMSG = 2
                     CASE 1
                        VNUM = (1000+VNUM)[2,3]
                        BEGIN CASE
                           CASE SNUM = ""
                              FORMNO = FNUM:".":VNUM
                           CASE SNUM = 0
                              ERRMSG = 3
                           CASE LEN(SNUM) > 5
                              ERRMSG = 3
                           CASE 1
                              SNUM = (100000+SNUM)[2,5]
                              FORMNO = FNUM:".":VNUM:".":SNUM
                        END CASE
                  END CASE
            END CASE
         CASE NUM(FNUM[2,99])
            BEGIN CASE
               CASE LEN(FNUM) = 2
                  FNUM = FNUM[1,1]:"00":FNUM[2,99]
                  FORMNO = FNUM
               CASE LEN(FNUM) = 3
                  FNUM = FNUM[1,1]:"0":FNUM[2,99]
                  FORMNO = FNUM
               CASE LEN(FNUM) = 4
               CASE 1
                  ERRMSG = 1
            END CASE
         CASE NUM(FNUM[3,99])
            BEGIN CASE
               CASE LEN(FNUM) = 3
                  FNUM = FNUM[1,2]:"0":FNUM[3,99]
                  FORMNO = FNUM
               CASE LEN(FNUM) = 4
               CASE 1
                  ERRMSG = 1
            END CASE
      END CASE
      BEGIN CASE
         CASE ERRMSG = 1
            ERRMSG = "Invalid form #, Try again! "
         CASE ERRMSG = 2
            ERRMSG = "Invalid run #, Try again! "
         CASE ERRMSG = 3
            ERRMSG = "Invalid sig #, Try again! "
      END CASE
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       CRT @(0,23):@(-4):BEL:ERRMSG:
*       INPUT REPLY,1:
*       CRT @(0,23):@(-4):
*       RETURN
*
*---- END OF JOB
*
99999 *
      PRINT @(-1):
*  END
