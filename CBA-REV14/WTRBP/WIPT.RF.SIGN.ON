      SUBROUTINE WIPT.RF.SIGN.ON (ACTION, CONO, EMPID, MAT WIP.FILE.VARS)
*********************************************************************
*
* PROGRAM  - WIPT.RF.SIGN.ON
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/01/95
*
* DESCRIPTION
*
* This program is used to process the Sign-on transaction from 
* the hand-held R-F units used in the warehouse.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>CPYLIB>EDIT.COM
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.LOC
*COPY>WTR.CPYLIB>WIP.TRACK.CONTROL
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>WTR.CPYLIB>WIP.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEST.MODE = 0
      IF LEN(ACTION) > 1 THEN
         IF ACTION[LEN(ACTION),1] = "T" THEN
            TEST.MODE = 1
            ACTION = ACTION[1,LEN(ACTION)-1]
         END
      END
      SPX = ""
*     EMPNAME = "???????????????"
      EMPNAME = ""
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPID THEN
         EMPNAME = (EMP.FRST.NAME:" ":EMP.LAST.NAME)[1,20]
      END
*     IF LEN(EMPNAME) < 20 THEN SPX = SPACE(INT((21-LEN(EMPNAME))/2))
      EMPNAME = SPX:EMPNAME
*
*---- INITIALIZE EDIT.SUB
*
      MAT EDIT.COM = ""
      TYP = 0
      CALL WIPT.INPUT
      FILL = "#"
      ERY = 15
*
*---- DISPLAY SCREEN
*
      DIM SCRN(16)
      MAT SCRN = ""
      XXXXXXXX = " 123456789012345678901 "
      XXXXXXXX = "+---------------------+"
      SCRN(01) = "|      Sign-on        |"
      SCRN(02) = "|---------------------|"
      SCRN(03) = "|                     |"
      SCRN(04) = "|Current Employee:    |"
      SCRN(05) = "|                     |"
      SCRN(06) = "| ################### |"
      SCRN(07) = "|                     |"
      SCRN(08) = "|                     |"
      SCRN(09) = "|Employee ID: ####    |"
      SCRN(10) = "|                     |"
      SCRN(11) = "|                     |"
      SCRN(12) = "|                     |"
      SCRN(13) = "|                     |"
      SCRN(14) = "|                     |"
      SCRN(15) = "|                     |"
      SCRN(16) = "|                     |"
      XXXXXXXX = "+---------------------+"
      XXXXXXXX = " 123456789012345678901 "
*
      SLINE = @(-1)
      FOR SL = 1 TO 16
         SDATA = SCRN(SL)[2,21]
         CONVERT "#" TO " " IN SDATA
         CONVERT "@" TO " " IN SDATA
         SDATA = TRIMB(SDATA)
         LDATA = LEN(SDATA)
         LDONE = 0
         FOR N = 1 TO LDATA UNTIL LDONE
            IF SDATA[N,1] # " " THEN
               SLINE = SLINE:@((N-1),(SL-1)):SDATA[N,99]
               LDONE = 1
            END
         NEXT N
      NEXT SL
      PRINT SLINE:
      GOSUB 89990
*
*---- MAIN PROCESSING
*
1000 *
      IF EMPNAME # "" THEN PRINT @(1,5):EMPNAME"L#20":
      TYP=1; X=13; Y=8; MAXL=4; EX=0; EY=15; ERY=15
      PRINT @(X,Y):STR("#",MAXL):
      PRINT @(X,Y):
      INPUT VALUE,4:_ FOR 10 ELSE VALUE = ""
      IF VALUE = "" OR VALUE = "END" THEN RETURN
      MATREAD EMP.REC FROM EMPLOYEE, CONO:VALUE ELSE
         ERRMSG = "INVALID EMPLOYEE ID"
         GOSUB 91000
         GOTO 1000
      END
      EMPID = VALUE
      EMPNAME = EMP.FRST.NAME:" ":EMP.LAST.NAME
      PRINT @(1,11):"HELLO":
      PRINT @(1,13):EMPNAME"L#20":
      MATREADU WIPTE.REC FROM WIP.TRACK.CONTROL, CONO:"EMP" ELSE MAT WIPTE.REC = ""
      LOCATE @LOGNAME IN WIPTE.USER<1>,1 SETTING EPTR ELSE WIPTE.USER<1,EPTR> = @LOGNAME
      WIPTE.EMP<1,EPTR> = EMPID
      MATWRITE WIPTE.REC ON WIP.TRACK.CONTROL, CONO:"EMP"
      SLEEP 3
      GOTO 99999
*
*---- DRAW BORDER
*
89990 *
      IF TEST.MODE THEN
         FOR SL = 1 TO 16
            PRINT @(21,(SL-1)):"|":
         NEXT SL
         PRINT @(0,16):"---------------------+":
      END
      RETURN
*
*---- ERROR ROUTINE
*
91000 *
      PRINT @(0,ERY):@(-4):BEL:BEL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,ERY):@(-4):
      ERY = 15
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
