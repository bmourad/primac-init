*********************************************************************
*
* PROGRAM  - WIPT.INV.ISBN
*
* AUTHOR   - ct6
*
* DATE     - 01/19/98
*
* DESCRIPTION
*
* This program produces the WIP Inventory Tracking report by ISBN,
* Customer and Component.
*
*T22322 ct6 01/21/1998 * Add WIP Tracking to Bawden with modifications.
*                        Create a new report for WIP by ISBN number.
*T25978 adelgado 03/28/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.ISBN
*COPY>CPYLIB>XREF.DATA
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- DEFINE ARRAY STORAGE
*
  DIM STOT(10)               ;* SUB TOTALS
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","PREFIX" TO PREFIX ELSE
    ERRMSG = "CANNOT OPEN PREFIX FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","XREF.DATA" TO XREF.DATA ELSE
    ERRMSG = "CANNOT OPEN XREF.DATA FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","JOB" TO JOB ELSE
    ERRMSG = "CANNOT OPEN JOB FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","CUSTOMER" TO CUSTOMER ELSE
    ERRMSG = "CANNOT OPEN CUSTOMER FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","CUSTOMER.XREF" TO CUSTOMER.XREF ELSE
    ERRMSG = "CANNOT OPEN CUSTOMER.XREF FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","WTR.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN WTR.SCREENS FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","WIP.TRACK.CONTROL" TO WIP.TRACK.CONTROL ELSE
    ERRMSG = "CANNOT OPEN WIP.TRACK.CONTROL FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","WIP.TRACK.JOB" TO WIP.TRACK.JOB ELSE
    ERRMSG = "CANNOT OPEN WIP.TRACK.JOB FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","WIP.TRACK.FORM" TO WIP.TRACK.FORM ELSE
    ERRMSG = "CANNOT OPEN WIP.TRACK.FORM FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE
    ERRMSG = "CANNOT OPEN WIP.TRACK.PALLET FILE"
    GOSUB 90000
    GOTO 99999
  END
  OPEN "","WIP.TRACK.ISBN" TO WIP.TRACK.ISBN ELSE
    ERRMSG = "CANNOT OPEN WIP.TRACK.ISBN FILE"
    GOSUB 90000
    GOTO 99999
  END
*
*---- INITIALIZATION
*
  CONO = ""
  MAT COMP.REC = ""
  CALL GET.CONO (CONO, MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
*
*---- INITIALIZE SCRN.EDIT
*
  MAT SCV.REC = ""
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "WIPT.INV.ISBN"
  ECD.ACTION=1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  ESN = ECD.SCRN.NO
  ECD.ACTION=2; CALL SCRN.EDIT
*
*---- DEFINE HEADINGS
*
  REPORT.NAME = "W I P   I N V E N T O R Y   B Y   I S B N"
  TODAY = DATE()
  CTIME = TIME()
  IF CTIME < 10 THEN TODAY = DATE()
  PTIME = OCONV(CTIME,"MTH")
  PTIME = PTIME[1,5]:" ":PTIME[6,2]
  HD1A = "RUN: ":OCONV(TODAY,"D2/"):" @ ":PTIME
  HD1B = SPACE((90-LEN(CO.NAME))/2):CO.NAME
  HD1C = "PAGE "
  HD1 = HD1A"L#30":HD1B"L#90":HD1C"R#30"
  HD2 = SPACE((150-LEN(REPORT.NAME))/2):REPORT.NAME
  SH1 = "                          To     WIP               Consumed"
  SH2 = " Form    Pallet #  Skid  CCtr  Quantity  Location  Quantity"   
  SH3 = "-------  --------  ----  ----  --------  --------  --------"
  ST1 = "                              ---------           ---------"
  SH1 = SH1:"  <------ Last Move ----->                    "    
  SH2 = SH2:"    Date      Time    Emp    Loc. 2    Loc. 3 "
  SH3 = SH3:"  --------  --------  ----  --------  --------"
  SH1 = SH1:"                                          "
  SH2 = SH2:"                Description               "
  SH3 = SH3:"  ----------------------------------------"
*
*---- INITIALIZE VARIABLES
*
  SP1 = SPACE(1)
  SP2 = SPACE(2)
  SP3 = SPACE(3)
  LM = SPACE(3)
  BEGIN.PAGE = 6
  PAGE.SIZE = 14
  LINE.SPACE = 1
  OLD.START = 0
  SEL.JOB = "ALL"
  SEL.CUST = "ALL"
  SEL.FORM = "ALL"
  SEL.FNUM = "ALL"
  SEL.VNUM = ""
  SEL.EMPTY = "N"
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  ECD.ACTION=6; CALL SCRN.EDIT
  MAT SCV.REC = ""
110 *
  SCV.REC(1)<ESN> = DATE()
  ECD.NUM=1; ECD.ACTION=5; CALL SCRN.EDIT
  GOSUB 1010
  IF ECD.RET.VALUE = "END" THEN GOTO 99999
  GOSUB 80000
  LN = 1
  OLD.START = 0
  GOSUB 86000
  GOSUB 1030
  IF ECD.RET.VALUE = "END" THEN GOTO 100
*
*---- GET OPERATOR REQUEST
*
500 *
  SCV.REC(21)<ESN> = ""
  ECD.NUM=21; ECD.ACTION=4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "E"
      GOTO 100
    CASE OPTION = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
      GOSUB 50000
    * T25978 v
    CASE OPTION = "SR"
      LN = LN - PAGE.SIZE
      IF LN < 1 THEN LN = 1
      GOSUB 50000
    CASE OPTION = "ST"
      LN = 1
      GOSUB 50000
    CASE OPTION = "SB"
      LN = LINES
      GOSUB 50000
    * T25978 ^
    CASE OPTION = "F"
      GOSUB 70000
      IF PRINT.CNT > 0 THEN
        ERRMSG = "** COMPLETED **"
      END ELSE
        ERRMSG = "** NO ITEMS PRINTED **"
      END
      GOSUB 90000
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET ISBN NUMBER
*
1010 *
  SCV.REC(2)<ESN> = ""
  ECD.NUM=2; ECD.ACTION=4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN RETURN
  MATREAD WIPTISBN.REC FROM WIP.TRACK.ISBN, CONO:ECD.RET.VALUE ELSE
    ERRMSG = "No form(s) found for specified ISBN. Try again! "
    GOSUB 90000
    GOTO 1010
  END
  SEL.ISBN = ECD.RET.VALUE
  RETURN
*
*---- GET PRINT EMPTY PALLET FLAG
*
1030 *
  SCV.REC(5)<ESN> = SEL.EMPTY
  ECD.NUM=5; ECD.ACTION=4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN RETURN
  SEL.EMPTY = ECD.RET.VALUE
  FNUM="ALL"; VNUM=""
  SCV.REC(5)<ESN> = SEL.EMPTY
  ECD.NUM=5; ECD.ACTION=5; CALL SCRN.EDIT
  RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE = OLD.START THEN RETURN
  OLD.START = START.LINE
  ECD.NUM=11;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=12;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=22;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=23;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=24;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=25;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=26;ECD.SUB.NUM=START.LINE;ECD.ACTION=7;CALL SCRN.EDIT
  RETURN
*
*---- PRINT REPORT
*
70000 *
  LINE.COUNT = 99
  PAGE.NO = 0
  MAT STOT = 0
  PREV.JOB = "!@#$%"
  PREV.FORM = "!@#$%"
  PRINT.CNT = 0
  BREAK.CNT = 0
  PERFORM "TERM ,,160"
  PRINTER ON
  PRINT CHAR(27):"E":CHAR(27):"&l1O":
  DONE = 0
  LOOP
    READNEXT ID FROM FLIST ELSE DONE = 1
  UNTIL DONE DO
    IF ID[1,3] = CONO THEN
      JOB.NO = FIELD(ID,"!",1)[4,99]
      FORM.NO = FIELD(ID,"!",2)
      FNUM = FIELD(FORM.NO,".",1)
      VNUM = FIELD(FORM.NO,".",2)
      GOSUB 72000
    END
  REPEAT
  IF PRINT.CNT > 0 THEN
    GOSUB 76000
    PRINT
    PRINT "END OF REPORT"
    PRINT CHAR(12):
  END ELSE
    JOB.NO = SEL.JOB
    GOSUB 79000
    PRINT
    PRINT "NO ITEMS SELECTED"
  END
  PRINT CHAR(27):"E":
  PRINTER OFF
  PRINTER CLOSE
  RETURN
*
*---- PROCESS JOB FORM
*
72000 *
  MATREAD WIPTF.REC FROM WIP.TRACK.FORM, CONO:JOB.NO:"!":FORM.NO THEN
    PCNT = DCOUNT(WIPTF.PALLET,VM)
    SORT.PAL.ID = ""
    FOR PPTR = 1 TO PCNT
      PAL.ID = WIPTF.PALLET<1,PPTR>
      MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
        IF SEL.EMPTY = "Y" OR WIPTP.CQTY # 0 THEN
          SID = (1000+WIPTP.SKID):"!":PAL.ID
          LOCATE SID IN SORT.PAL.ID<1>,1 BY "AL" SETTING PP ELSE NULL
          SORT.PAL.ID = INSERT(SORT.PAL.ID,1,PP,0,SID)
        END
      END
    NEXT PPTR
    PCNT = DCOUNT(SORT.PAL.ID,VM)
    FOR PPTR = 1 TO PCNT
      PAL.ID = FIELD(SORT.PAL.ID<1,PPTR>,"!",2)
      MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
        BEGIN CASE
          CASE JOB.NO # PREV.JOB
            GOSUB 76000
            MATREAD JOB.REC FROM JOB, CONO:JOB.NO THEN
              MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
                MAT CUST.REC = ""
                CUST.NAME = "??????????"
              END
            END ELSE
              MAT JOB.REC = ""
              MAT CUST.REC = ""
            END
            PREV.JOB = JOB.NO
            PREV.FORM = FORM.NO
            LINE.COUNT = 99
          CASE FORM.NO # PREV.FORM
            GOSUB 76000
            PREV.FORM = FORM.NO
        END CASE
        GOSUB 74000
      END
    NEXT PPTR
  END
  RETURN
*
*---- PRINT DETAIL LINE
*
74000 *
  BEGIN CASE
    CASE BREAK.CNT = 0 AND (LINE.COUNT + 1) = 42
      GOSUB 79000
      PLINE = FORM.NO"L#7"
    CASE (LINE.COUNT + 1) > 42
      GOSUB 79000
      IF BREAK.CNT = 0 THEN
        PLINE = FORM.NO"L#7"
      END ELSE
        PLINE = "(cont'd)""L#7"
      END
    CASE BREAK.CNT = 0 
      PLINE = FORM.NO"L#7"
    CASE 1
      PLINE = """L#7"
  END CASE
  PLINE = PLINE:SP2:PAL.ID"R#8"
  PLINE = PLINE:SP2:WIPTP.SKID"R#4"
  PLINE = PLINE:SP2:WIPTP.TYPE"L#4"
  PLINE = PLINE:SP2:OCONV(WIPTP.CQTY,"MD0Z,")"R#8"
  PLINE = PLINE:SP2:WIPTP.MLOC<1,1>"L#8"
  PLINE = PLINE:SP2:OCONV((WIPTP.IQTY-WIPTP.CQTY),"MD0Z,")"R#8"
  PLINE = PLINE:SP2:OCONV(WIPTP.MDATE<1,1>,"D2/")"R#8"
  PTIME = OCONV(WIPTP.MTIME<1,1>,"MTH")
  PTIME = PTIME[1,5]:" ":PTIME[6,2]
  PLINE = PLINE:SP2:PTIME"R#8"
  PLINE = PLINE:SP2:WIPTP.MEMP<1,1>"L#4"
  PLINE = PLINE:SP2:WIPTP.MLOC<1,2>"L#8"
  PLINE = PLINE:SP2:WIPTP.MLOC<1,3>"L#8"
  PLINE = PLINE:SP2:WIPTP.DESC[1,40]
  PRINT LM:PLINE
  LINE.COUNT = LINE.COUNT + 1
*
  IF NUM(WIPTP.CQTY) THEN STOT(1) = STOT(1) + WIPTP.CQTY
  IF NUM(WIPTP.CQTY) THEN STOT(2) = STOT(2) + (WIPTP.IQTY-WIPTP.CQTY)
  PRINT.CNT = PRINT.CNT + 1
  BREAK.CNT = BREAK.CNT + 1
  RETURN
*
*---- PRINT FORM SUB-TOTALS
*
76000 *
  IF BREAK.CNT > 0 THEN
    PRINT LM:ST1
    TLINE = "                             " 
    TLINE = TLINE:OCONV(STOT(1),"MD0,")"R#10"
    TLINE = TLINE:SPACE(10)
    TLINE = TLINE:OCONV(STOT(2),"MD0,")"R#10"
    PRINT LM:TLINE
    PRINT
    LINE.COUNT = LINE.COUNT + 3
    MAT STOT = 0
    BREAK.CNT = 0
  END
  RETURN
*
*---- PRINT HEADINGS
*
79000 *
  PRINT CHAR(12):
  PAGE.NO = PAGE.NO + 1
  PRINT HD1:PAGE.NO
  PRINT HD2
  PRINT
  PRINT
  PRINT "ISBN : ":SEL.ISBN"L#20"
  PRINT "Job #: ":JOB.NO"L#8":SPACE(5):"Customer: ":CUST.NAME
  PRINT
  PRINT LM:SH1
  PRINT LM:SH2
  PRINT LM:SH3
  LINE.COUNT = 10
  RETURN
*
*---- DISPLAY SCREEN DATA
*
80000 *
  SCV.REC(1)<ESN> = DATE()
  SCV.REC(2)<ESN> = SEL.ISBN
  SCV.REC(5)<ESN> = SEL.EMPTY
80050 *
  ECD.ACTION=3; CALL SCRN.EDIT
  RETURN
*
*---- LOAD MULTI-LINE DATA INTO SCREEN VARIABLES
*
86000 *
  PREV.JOB = "!@#$%"
  PREV.QTY = 0:VM:0:VM:0
  PREV.STAT = ''
  LINES = 1
  PAL.CNT = 0
  CMD = "SSELECT WIP.TRACK.FORM" 
  CMD = CMD:" BY WIPTF.ISBN"
  CMD = CMD:' WITH CONO = "':CONO:'"'
*T22322*      CMD = CMD:' AND WITH WIP.PALLET = "Y"'
  PERFORM CMD RTNLIST FLIST CAPTURING SELMSG
  SLIST = FLIST
  EOF = 0
  SELECTED = 0
  LOOP
    READNEXT ID FROM SLIST ELSE EOF = 1
  UNTIL EOF DO
    SELECTED = 1
    JOB.NO = FIELD(ID,"!",1)[4,99]
    MATREAD WIPTF.REC FROM WIP.TRACK.FORM, ID THEN
      MATREAD JOB.REC FROM JOB, CONO:JOB.NO THEN
        BEGIN CASE
          CASE PREV.JOB = JOB.NO
            PAL.CNT = PAL.CNT + DCOUNT(WIPTF.PALLET<1>,VM)
          CASE PREV.JOB = "!@#$%"
            PREV.JOB = JOB.NO
            PREV.QTY = JOB.QTY
            PREV.STATUS = JOB.STATUS
            PREV.TRACK.DATE = JOB.TRACK.DATE
            PAL.CNT = DCOUNT(WIPTF.PALLET<1>,VM)
          CASE 1
            GOSUB 86100 ;* Actual storage of data in SCV.REC
            PREV.JOB = JOB.NO
            PREV.QTY = JOB.QTY
            PREV.STATUS = JOB.STATUS
            PREV.TRACK.DATE = JOB.TRACK.DATE
            PAL.CNT = DCOUNT(WIPTF.PALLET<1>,VM)
            LINES = LINES + 1
        END CASE
      END
    END
  REPEAT
  IF SELECTED THEN
    GOSUB 86100 ;* Load data for last JOB
    GOSUB 50000
  END
  RETURN
*
*---- Store Job Summary data in the SCV rec
*
86100 *
  SCV.REC(12)<ESN,LINES> = PREV.JOB
  ECD.NUM=22; GOSUB 86500 ;* Get status
  FOR PQI = 1 TO 3
    PREV.QTY<1,PQI> = PREV.QTY<1,PQI>+0
  NEXT PQI
  SCV.REC(23)<ESN,LINES> = PREV.QTY<1,1>
  SCV.REC(24)<ESN,LINES> = PREV.QTY<1,2>
  SCV.REC(25)<ESN,LINES> = PREV.QTY<1,2> - PREV.QTY<1,3>
  SCV.REC(26)<ESN,LINES> = PAL.CNT
  RETURN
*
*---- Determine the Job Status
*
86500 *
  BEGIN CASE
    CASE PREV.STATUS<1,1>=""
      SCV.REC(ECD.NUM)<ESN,LINES>="BOOKED"
    CASE PREV.STATUS<1,1>="9"
      SCV.REC(ECD.NUM)<ESN,LINES>="CANCELLED"
    CASE PREV.STATUS<1,1>="7"
      SCV.REC(ECD.NUM)<ESN,LINES>="READY TO PURGE"
    CASE PREV.STATUS<1,1>="8"
      SCV.REC(ECD.NUM)<ESN,LINES>="WAS PURGED"
    CASE PREV.TRACK.DATE<1,10> # ""
      BEGIN CASE
        CASE PREV.STATUS<1,1> = "1"
          SCV.REC(ECD.NUM)<ESN,LINES> = "IN PROCESS"
        CASE PREV.STATUS<1,1> = "3"
          SCV.REC(ECD.NUM)<ESN,LINES> = "INVOICED"
        CASE PREV.STATUS<1,1> = "5"
          SCV.REC(ECD.NUM)<ESN,LINES>="REOPENED"
        CASE 1
          SCV.REC(ECD.NUM)<ESN,LINES> = "COMPLETED"
      END CASE
    CASE PREV.TRACK.DATE<1,9> # ""
      BEGIN CASE
        CASE PREV.STATUS<1,1> = "1"
          SCV.REC(ECD.NUM)<ESN,LINES> = "IN PROCESS"
        CASE PREV.STATUS<1,1> = "5"
          SCV.REC(ECD.NUM)<ESN,LINES>="REOPENED"
        CASE 1
          SCV.REC(ECD.NUM)<ESN,LINES> = "INVOICED"
      END CASE
    CASE PREV.TRACK.DATE<1,8> # ""
      SCV.REC(ECD.NUM)<ESN,LINES>="COSTED"
    CASE PREV.TRACK.DATE<1,7> # ""
      SCV.REC(ECD.NUM)<ESN,LINES>="RDY-TO-BILL"
    CASE PREV.TRACK.DATE<1,6> # ""
      SCV.REC(ECD.NUM)<ESN,LINES>="DELIVERED"
    CASE PREV.TRACK.DATE<1,5> # ""
      SCV.REC(ECD.NUM)<ESN,LINES>="IN PROCESS"
    CASE PREV.TRACK.DATE<1,3> # ""
      SCV.REC(ECD.NUM)<ESN,LINES>="IN PROCESS"
    CASE PREV.STATUS<1,1>="1"
      SCV.REC(ECD.NUM)<ESN,LINES>="IN PROCESS"
    CASE PREV.STATUS<1,1>="3"
      SCV.REC(ECD.NUM)<ESN,LINES>="INVOICED"
    CASE 1
      SCV.REC(ECD.NUM)<ESN,LINES>="UNKNOWN"
  END CASE
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       CRT @(0,23):@(-4):BEL:ERRMSG:
*       INPUT REPLY,1:
*       CRT @(0,23):@(-4):
*       RETURN
*
*---- END OF JOB
*
99999 *
*     PRINT @(-1):
END
