      SUBROUTINE WIPT.XREF.MAINT(CONO,PJOB,PFORM,CJOB,CFORM,PALID,SKID,ERRMSG)
*********************************************************************
*
* PROGRAM  - WIPT.XREF.MAINT
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/20/95
*
* DESCRIPTION
*
* This program updates the WIP.TRACK.JOB AND WIP.TRACK.FORM files.
*
*T22322 ct6 01/18/1998 * Add WIP Tracking to Bawden with Modifications.
*                        Add TO-JOB Xref handling.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JCS.CPYLIB>JOB
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
      OPEN "","JOB" TO JOB ELSE
         ERRMSG = "CANNOT OPEN JOB FILE"
         GOTO 99999
      END
      OPEN "","WIP.TRACK.JOB" TO WIP.TRACK.JOB ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.JOB FILE"
         GOTO 99999
      END
      OPEN "","WIP.TRACK.FORM" TO WIP.TRACK.FORM ELSE
         ERRMSG = "CANNOT OPEN WIP.TRACK.FORM FILE"
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      SKID = 0
      ERRMSG = ""
*
*---- UPDATE X-REF DATA
*
***   IF CJOB # PJOB OR CFORM # PFORM THEN
         IF PJOB # "" THEN
            WIPTF.ID = PJOB:"!":PFORM
            MATREADU WIPTF.REC FROM WIP.TRACK.FORM, CONO:WIPTF.ID THEN
               LOCATE PALID IN WIPTF.PALLET<1>,1 SETTING PP THEN
                  WIPTF.PALLET = DELETE(WIPTF.PALLET,1,PP,0)
               END
               IF WIPTF.PALLET = "" THEN
                  DELETE WIP.TRACK.FORM, CONO:WIPTF.ID
                  MATREADU WIPTJ.REC FROM WIP.TRACK.JOB, CONO:PJOB THEN
                     LOCATE PFORM IN WIPTJ.FORM<1>,1 BY "AL" SETTING FP THEN
                        WIPTJ.FORM = DELETE(WIPTJ.FORM,1,FP,0)
                     END
                     IF WIPTJ.FORM = "" AND WIPTJ.DROP = "" AND WIPTJ.VERSION = "" THEN
                        DELETE WIP.TRACK.JOB, CONO:PJOB
                     END ELSE
                        MATWRITE WIPTJ.REC ON WIP.TRACK.JOB, CONO:PJOB
                     END
                  END ELSE
                     RELEASE WIP.TRACK.JOB, CONO:PJOB
                  END
               END ELSE
                  MATWRITE WIPTF.REC ON WIP.TRACK.FORM, CONO:WIPTF.ID
               END
            END ELSE
               RELEASE WIP.TRACK.FORM, CONO:WIPTF.ID
            END
         END
*
         IF CJOB # "" THEN
            WIPTF.ID = CJOB:"!":CFORM
            MATREADU WIPTF.REC FROM WIP.TRACK.FORM, CONO:WIPTF.ID ELSE
               MAT WIPTF.REC = ""
            END
            LOCATE PALID IN WIPTF.PALLET<1>,1 SETTING PP ELSE
               WIPTF.PALLET<1,PP> = PALID
               WIPTF.SKID.SEQ = WIPTF.SKID.SEQ + 1
               SKID = WIPTF.SKID.SEQ
            END
            MATWRITE WIPTF.REC ON WIP.TRACK.FORM, CONO:WIPTF.ID
            MATREAD JOB.REC FROM JOB, CONO:CJOB ELSE 
               MAT JOB.REC = ""
            END
            MATREADU WIPTJ.REC FROM WIP.TRACK.JOB, CONO:CJOB ELSE
               MAT WIPTJ.REC = ""
            END
            IF JOB.CUST # "" THEN WIPTJ.CUST = JOB.CUST
            LOCATE CFORM IN WIPTJ.FORM<1>,1 BY "AL" SETTING FP ELSE
               WIPTJ.FORM = INSERT(WIPTJ.FORM,1,FP,0,CFORM)
            END
            MATWRITE WIPTJ.REC ON WIP.TRACK.JOB, CONO:CJOB
         END
***   END
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
