*
* THIS PROGRAM WILL CLEAN UP X-REF FILES
*
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
      OPEN "","WIP.TRACK.PALLET" TO WIP.TRACK.PALLET ELSE STOP
      OPEN "","WIP.TRACK.FORM" TO WIP.TRACK.FORM ELSE STOP
      OPEN "","WIP.TRACK.JOB" TO WIP.TRACK.JOB ELSE STOP
      SELECT WIP.TRACK.FORM
      DONE = 0
      LOOP
         READNEXT ID ELSE DONE = 1
      UNTIL DONE DO
         MATREADU WIPTF.REC FROM WIP.TRACK.FORM, ID THEN
            CONO = ID[1,3]
            CJOB = FIELD(ID,"!",1)[4,99]
            CFORM = FIELD(ID,"!",2)
            FNUM = FIELD(CFORM,".",1)
            VNUM = FIELD(CFORM,".",2)
            SNUM = FIELD(CFORM,".",3)
            PCNT = DCOUNT(WIPTF.PALLET,CHAR(253))
            FOR PPTR = PCNT TO 1 STEP -1
               PALID = WIPTF.PALLET<1,PPTR>
               MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PALID THEN
                  BEGIN CASE
                     CASE WIPTP.JOB # CJOB
                        MISMATCH = 1
                     CASE WIPTP.FORM # FNUM
                        MISMATCH = 1
                     CASE WIPTP.VER # VNUM
                        MISMATCH = 1
                     CASE WIPTP.SIG # SNUM
                        MISMATCH = 1
                     CASE 1
                        MISMATCH = 0
                  END CASE
               END ELSE
                  MISMATCH = 1
               END
               IF MISMATCH THEN
                  WIPTF.PALLET = DELETE(WIPTF.PALLET,1,PPTR,0)
                  P_X  = 0 ; P_Y = 23 ; P_VALUE = "FIXED PALLET MISMATCH FOR ":PALID:" ID = ":ID ; P_OPT = ""
                  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               END
            NEXT PPTR
            IF WIPTF.PALLET = "" THEN
               DELETE WIP.TRACK.FORM, ID
               P_X  = 0 ; P_Y = 23 ; P_VALUE = "DELETED WIP.TRACK.FORM RECORD - ":ID ; P_OPT = ""
               CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            END ELSE
               MATWRITE WIPTF.REC ON WIP.TRACK.FORM, ID
            END
         END ELSE
            RELEASE WIP.TRACK.FORM, ID
         END
      REPEAT
      SELECT WIP.TRACK.JOB
      DONE = 0
      LOOP
         READNEXT ID ELSE DONE = 1
      UNTIL DONE DO
         MATREADU WIPTJ.REC FROM WIP.TRACK.JOB, ID THEN
            CONO = ID[1,3]
            CJOB = ID[4,99]
            FCNT = DCOUNT(WIPTJ.FORM,CHAR(253))
            FOR FPTR = FCNT TO 1 STEP -1
               CFORM = WIPTJ.FORM<1,FPTR>
               MATREAD WIPTF.REC FROM WIP.TRACK.FORM, CONO:CJOB:"!":CFORM ELSE
                  WIPTJ.FORM = DELETE(WIPTJ.FORM,1,PPTR,0)
                  P_X  = 0 ; P_Y = 23 ; P_VALUE = "FIXED FORM MISMATCH FOR ":CFORM:" ID = ":ID ; P_OPT = ""
                  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               END
            NEXT FPTR
            IF WIPTJ.FORM = "" AND WIPTJ.DROP = "" AND WIPTJ.VERSION = "" THEN
*              DELETE WIP.TRACK.JOB, ID
               RELEASE WIP.TRACK.JOB, ID
               P_X  = 0 ; P_Y = 23 ; P_VALUE = "DELETED WIP.TRACK.JOB RECORD - ":ID ; P_OPT = ""
               CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            END ELSE
               MATWRITE WIPTJ.REC ON WIP.TRACK.JOB, ID
            END
         END ELSE
            RELEASE WIP.TRACK.JOB, ID
         END
      REPEAT
   END
