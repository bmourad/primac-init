      SUBROUTINE WIPT.RF.PALLET.REQUEST (ACTION, CONO, EMPID, MAT WIP.FILE.VARS)
*********************************************************************
*
* PROGRAM  - WIPT.RF.PALLET.REQUEST
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/01/95
*
* DESCRIPTION
*
* This program is used to process the Pallet Request transaction from 
* the hand-held R-F units used in the warehouse.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>CPYLIB>EDIT.COM
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.LOC
*COPY>WTR.CPYLIB>WIP.TRACK.CONTROL
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>WTR.CPYLIB>WIP.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEST.MODE = 0
      IF LEN(ACTION) > 1 THEN
         IF ACTION[LEN(ACTION),1] = "T" THEN
            TEST.MODE = 1
            ACTION = ACTION[1,LEN(ACTION)-1]
         END
      END
      SPX = ""
      EMPNAME = "???????????????"
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPID THEN
         EMPNAME = (EMP.FRST.NAME:" ":EMP.LAST.NAME)[1,20]
      END
      IF LEN(EMPNAME) < 20 THEN SPX = SPACE(INT((21-LEN(EMPNAME))/2))
      EMPNAME = SPX:EMPNAME
*
*---- INITIALIZE EDIT.SUB
*
      MAT EDIT.COM = ""
      TYP = 0
      CALL WIPT.INPUT
      FILL = "#"
      ERY = 15
*
*---- DISPLAY SCREEN
*
      DIM SCRN(16)
      MAT SCRN = ""
      XXXXXXXX = " 123456789012345678901 "
      XXXXXXXX = "+---------------------+"
      SCRN(01) = "|   Pallet Request    |"
      SCRN(02) = "|---------------------|"
      SCRN(03) = "|     @@@    @@@      |"
      SCRN(04) = "|                     |"
      SCRN(05) = "| Date: @@@@@@@@      |"
      SCRN(06) = "| Time: @@@@@@@@      |"
      SCRN(07) = "|                     |"
      SCRN(08) = "| Job : @@@@@@@@      |"
      SCRN(09) = "| Form: @@@@@@@@@@@@@@|"
      SCRN(10) = "| Type: @@@@          |"
      SCRN(11) = "|                     |"
      SCRN(12) = "| Stitcher: @@@@@@@@  |"
      SCRN(13) = "|                     |"
      SCRN(14) = "|                     |"
      SCRN(15) = "|                     |"
      SCRN(16) = "|                     |"
      XXXXXXXX = "+---------------------+"
      XXXXXXXX = " 123456789012345678901 "
*
      SLINE = @(-1)
      FOR SL = 1 TO 16
         SDATA = SCRN(SL)[2,21]
         CONVERT "#" TO " " IN SDATA
         CONVERT "@" TO " " IN SDATA
         SDATA = TRIMB(SDATA)
         LDATA = LEN(SDATA)
         LDONE = 0
         FOR N = 1 TO LDATA UNTIL LDONE
            IF SDATA[N,1] # " " THEN
               SLINE = SLINE:@((N-1),(SL-1)):SDATA[N,99]
               LDONE = 1
            END
         NEXT N
      NEXT SL
      PRINT SLINE:
      GOSUB 89990
*
*---- MAIN PROCESSING
*
1000 *
      RTYPE = "D"
      MATREAD WIPTR.REC FROM WIP.TRACK.CONTROL, CONO:"REQ" ELSE MAT WIPTR.REC = ""
      RCNT = DCOUNT(WIPTR.DATE,VM)
      FOR RPTR = RCNT TO 1 STEP -1       ;*** ITEM MUST NOT BE RE-WRITTEN ***
         IF WIPTR.TYPE<1,RPTR> # RTYPE OR WIPTR.USER<1,RPTR> # "" THEN
            WIPTR.DATE   = DELETE(WIPTR.DATE,1,RPTR,0)
            WIPTR.TIME   = DELETE(WIPTR.TIME,1,RPTR,0)
            WIPTR.TYPE   = DELETE(WIPTR.TYPE,1,RPTR,0)
            WIPTR.CCTR   = DELETE(WIPTR.CCTR,1,RPTR,0)
            WIPTR.JOB    = DELETE(WIPTR.JOB,1,RPTR,0)
            WIPTR.FORM   = DELETE(WIPTR.FORM,1,RPTR,0)
            WIPTR.USER   = DELETE(WIPTR.USER,1,RPTR,0)
            WIPTR.PALLET = DELETE(WIPTR.PALLET,1,RPTR,0)
            WIPTR.PTYPE  = DELETE(WIPTR.PTYPE,1,RPTR,0)
         END
      NEXT RPTR
      DDATE = ""
      DTIME = ""
      DCCTR = ""
      DJOB  = ""
      DFORM = ""
      RCNT = DCOUNT(WIPTR.DATE,VM)
      IF RCNT > 0 THEN
         PRINT @(09,02):"of":@(12,02):RCNT"L#3":
         RPTR = 1
         LOOP
            PRINT @(05,02):RPTR"R#3":
            PRINT @(07,04):OCONV(WIPTR.DATE<1,RPTR>,"D2/"):
            PTIME = OCONV(WIPTR.TIME<1,RPTR>,"MTH")
            PTIME = PTIME[1,5]:" ":PTIME[6,2]
            PRINT @(07,05):PTIME:
            PRINT @(07,07):WIPTR.JOB<1,RPTR>:
            PRINT @(07,08):WIPTR.FORM<1,RPTR>:
            PRINT @(07,09):WIPTR.PTYPE<1,RPTR>:
            PRINT @(11,11):WIPTR.CCTR<1,RPTR>:
1080        TYP=1; X=1; Y=13; MAXL=3; O.R="O"; EX=0; EY=15; ERY=15
            PMSG = "(P)rior, (N)ext:"
            CALL WIPT.INPUT
            GOSUB 89990
            BEGIN CASE
            CASE VALUE = "END"
            CASE (VALUE = "P" OR VALUE = "p") AND RPTR > 1
               RPTR = RPTR - 1
            CASE (VALUE = "" OR VALUE = "N" OR VALUE = "n") AND RPTR < RCNT
               RPTR = RPTR + 1
            CASE (VALUE = "D" OR VALUE = "d")
               DPTR = DCOUNT(DDATE,VM) + 1
               DDATE<1,DPTR> = WIPTR.DATE<1,RPTR>
               DTIME<1,DPTR> = WIPTR.TIME<1,RPTR>
               DCCTR<1,DPTR> = WIPTR.CCTR<1,RPTR>
               DJOB<1,DPTR>  = WIPTR.JOB<1,RPTR>
               DFORM<1,DPTR> = WIPTR.FORM<1,RPTR>
               IF RPTR < RCNT THEN RPTR = RPTR + 1
            CASE 1
               GOTO 1080
            END CASE
         UNTIL VALUE = "END" DO
         REPEAT
         DCNT = DCOUNT(DDATE,VM)
         IF DCNT > 0 THEN
            MATREADU WIPTR.REC FROM WIP.TRACK.CONTROL, CONO:"REQ" THEN
               RCNT = DCOUNT(WIPTR.DATE,VM)
               FOR RPTR = RCNT TO 1 STEP -1
                  FOR DPTR = DCNT TO 1 STEP -1
                     BEGIN CASE
                     CASE WIPTR.DATE<1,RPTR> # DDATE<1,DPTR>
                     CASE WIPTR.TIME<1,RPTR> # DTIME<1,DPTR>
                     CASE WIPTR.CCTR<1,RPTR> # DCCTR<1,DPTR>
                     CASE WIPTR.JOB<1,RPTR>  # DJOB<1,DPTR>
                     CASE WIPTR.FORM<1,RPTR> # DFORM<1,DPTR>
                     CASE 1
                        WIPTR.DATE   = DELETE(WIPTR.DATE,1,RPTR,0)
                        WIPTR.TIME   = DELETE(WIPTR.TIME,1,RPTR,0)
                        WIPTR.TYPE   = DELETE(WIPTR.TYPE,1,RPTR,0)
                        WIPTR.CCTR   = DELETE(WIPTR.CCTR,1,RPTR,0)
                        WIPTR.JOB    = DELETE(WIPTR.JOB,1,RPTR,0)
                        WIPTR.FORM   = DELETE(WIPTR.FORM,1,RPTR,0)
                        WIPTR.USER   = DELETE(WIPTR.USER,1,RPTR,0)
                        WIPTR.PALLET = DELETE(WIPTR.PALLET,1,RPTR,0)
                        WIPTR.PTYPE  = DELETE(WIPTR.PTYPE,1,RPTR,0)
                     END CASE
                  NEXT DPTR
               NEXT RPTR
               MATWRITE WIPTR.REC ON WIP.TRACK.CONTROL, CONO:"REQ"
            END ELSE
               RELEASE WIP.TRACK.CONTROL, CONO:"REQ"
            END
         END
      END ELSE
         PRINT @(07,04):"**NONE**":
         SLEEP 3
      END
      GOTO 99999
*
*---- DRAW BORDER
*
89990 *
      IF TEST.MODE THEN
         FOR SL = 1 TO 16
            PRINT @(21,(SL-1)):"|":
         NEXT SL
         PRINT @(0,16):"---------------------+":
      END
      RETURN
*
*---- ERROR ROUTINE
*
91000 *
      PRINT @(0,ERY):@(-4):BEL:BEL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,ERY):@(-4):
      ERY = 15
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
