      SUBROUTINE WIPT.RF.LOCATE.PALLET (ACTION, CONO, EMPID, MAT WIP.FILE.VARS)
*********************************************************************
*
* PROGRAM  - WIPT.RF.LOCATE.PALLET
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 09/01/95
*
* DESCRIPTION
*
* This program is used to process the Locate Pallet transaction from 
* the hand-held R-F units used in the warehouse.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>CPYLIB>EDIT.COM
*COPY>WTR.CPYLIB>WIP.TRACK.PALLET
*COPY>WTR.CPYLIB>WIP.TRACK.LOC
*COPY>WTR.CPYLIB>WIP.TRACK.CONTROL
*COPY>WTR.CPYLIB>WIP.TRACK.FORM
*COPY>WTR.CPYLIB>WIP.TRACK.JOB
*COPY>WTR.CPYLIB>WIP.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEST.MODE = 0
      IF LEN(ACTION) > 1 THEN
         IF ACTION[LEN(ACTION),1] = "T" THEN
            TEST.MODE = 1
            ACTION = ACTION[1,LEN(ACTION)-1]
         END
      END
      SPX = ""
      EMPNAME = "???????????????"
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMPID THEN
         EMPNAME = (EMP.FRST.NAME:" ":EMP.LAST.NAME)[1,20]
      END
      IF LEN(EMPNAME) < 20 THEN SPX = SPACE(INT((21-LEN(EMPNAME))/2))
      EMPNAME = SPX:EMPNAME
*
*---- INITIALIZE EDIT.SUB
*
      MAT EDIT.COM = ""
      TYP = 0
      CALL WIPT.INPUT
      FILL = "#"
      ERY = 15
*
*---- DISPLAY SCREEN
*
      DIM SCRN(16)
      MAT SCRN = ""
      XXXXXXXX = " 123456789012345678901 "
      XXXXXXXX = "+---------------------+"
      SCRN(01) = "|    Locate Pallet    |"
      SCRN(02) = "|---------------------|"
      SCRN(03) = "|@@@@@@@@@@@@@@@@@@@@@|"
      SCRN(04) = "|---------------------|"
      SCRN(05) = "| Job : ########      |"
      SCRN(06) = "| Form: ##############|"
      SCRN(07) = "|---------------------|"
      SCRN(08) = "|     @@@    @@@      |"
      SCRN(09) = "|                     |"
      SCRN(10) = "| Pallet #: @@@@@@@@  |"
      SCRN(11) = "| Location: @@@@@@@@  |"
      SCRN(12) = "| Quantity: @@@@@@@@  |"
      SCRN(13) = "|     Type: @@@@      |"
      SCRN(14) = "| @@@@@@@@@@@@@@@@@@@@|"
      SCRN(15) = "|                     |"
      SCRN(16) = "|                     |"
      XXXXXXXX = "+---------------------+"
      XXXXXXXX = " 123456789012345678901 "
*
      SLINE = @(-1)
      FOR SL = 1 TO 16
         SDATA = SCRN(SL)[2,21]
         CONVERT "#" TO " " IN SDATA
         CONVERT "@" TO " " IN SDATA
         SDATA = TRIMB(SDATA)
         LDATA = LEN(SDATA)
         LDONE = 0
         FOR N = 1 TO LDATA UNTIL LDONE
            IF SDATA[N,1] # " " THEN
               SLINE = SLINE:@((N-1),(SL-1)):SDATA[N,99]
               LDONE = 1
            END
         NEXT N
      NEXT SL
      PRINT SLINE:
      GOSUB 89990
*
*---- MAIN PROCESSING
*
1000 *
      PRINT @(0,2):EMPNAME:
1010 *
      TYP=1; X=7; Y=4; MAXL=8; EX=0; EY=15; ERY=15
      CALL WIPT.INPUT
      GOSUB 89990
      IF VALUE = "END" THEN GOTO 1090
      MATREAD JOB.REC FROM JOB, CONO:VALUE ELSE
         ERRMSG = "INVALID JOB NUMBER"
         GOSUB 91000
         GOTO 1010
      END
      REQJOB = VALUE
1020  TYP=7; X=7; Y=5; MAXL=14; EX=0; EY=15; ERY=15
      PATRN="1A0N":VM:"2A0N":VM:"0N":VM:"0N'.'0N":VM:"0N'.'0N'.'0N"
      CALL WIPT.INPUT
      GOSUB 89990
      IF VALUE = "END" THEN GOTO 1090
      FORMNO = VALUE
      GOSUB 85100
      IF ERRMSG # "" THEN
         GOSUB 91000
         GOTO 1020
      END
      REQFORM = FORMNO
      PALIDS = ""
      WIPTF.ID = REQJOB:"!":REQFORM
      MATREAD WIPTF.REC FROM WIP.TRACK.FORM, CONO:WIPTF.ID ELSE
         MAT WIPTF.REC = ""
      END
      PCNT = DCOUNT(WIPTF.PALLET,VM)
      FOR PPTR = PCNT TO 1 STEP -1
         PAL.ID = WIPTF.PALLET<1,PPTR>
         MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID THEN
            BEGIN CASE
            CASE WIPTP.CQTY = 0
*           CASE WIPTP.MLOC<1,1> = "VENDOR"
*           CASE WIPTP.MLOC<1,1> = "SHREDDER"
*           CASE WIPTP.STATUS<1,1> = "D"
            CASE 1
               PALIDS<1,-1> = PAL.ID
            END CASE
         END
      NEXT PPTR
      IF PALIDS = "" THEN
         ERRMSG = "NO MATCHING PALLETS"
         GOSUB 91000
         PRINT @(07,04):@(-4):
         PRINT @(07,05):@(-4):
         GOTO 1010
      END ELSE
         PCNT = DCOUNT(PALIDS,VM)
         PRINT @(09,07):"of":@(12,07):PCNT"L#3":
         PPTR = 1
         LOOP
            PAL.ID = PALIDS<1,PPTR>
            MATREAD WIPTP.REC FROM WIP.TRACK.PALLET, CONO:PAL.ID ELSE
               MAT WIPTP.REC = ""
            END
            PRINT @(05,07):PPTR"R#3":
            PRINT @(11,09):PAL.ID"L#8":
            PRINT @(11,10):WIPTP.MLOC<1,1>"L#8":
            PRINT @(11,11):OCONV(WIPTP.CQTY,"MD0,")"L#8":
            PRINT @(11,12):WIPTP.TYPE"L#4":
            PRINT @(01,13):WIPTP.DESC"L#20":
1080        TYP=1; X=3; Y=14; MAXL=1; O.R="O"; EX=0; EY=15; ERY=15
            PMSG = "(P)rior, (N)ext:"
            CALL WIPT.INPUT
            GOSUB 89990
            BEGIN CASE
            CASE VALUE = "END"
            CASE (VALUE = "P" OR VALUE = "p") AND PPTR > 1
               PPTR = PPTR - 1
            CASE (VALUE = "" OR VALUE = "N" OR VALUE = "n") AND PPTR < PCNT
               PPTR = PPTR + 1
            CASE 1
               GOTO 1080
            END CASE
         UNTIL VALUE = "END" DO
         REPEAT
      END
1090  GOTO 99999
*
*---- FORMAT FORM NUMBER
*
85100 *
      ERRMSG = ""
      FNUM = FIELD(FORMNO,".",1)
      VNUM = FIELD(FORMNO,".",2)
      SNUM = FIELD(FORMNO,".",3)
      BEGIN CASE
      CASE NUM(FNUM)
         BEGIN CASE
         CASE FNUM = ""
            ERRMSG = 1
         CASE FNUM = 0
            ERRMSG = 1
         CASE LEN(FNUM) > 4
            ERRMSG = 1
         CASE 1
            FNUM = (10000+FNUM)[2,4]
            BEGIN CASE
            CASE VNUM = "" AND SNUM = ""
               FORMNO = FNUM
            CASE VNUM = ""
               ERRMSG = 2
            CASE VNUM = 0
               ERRMSG = 2
            CASE LEN(VNUM) > 3
               ERRMSG = 2
            CASE 1
               VNUM = (1000+VNUM)[2,3]
               BEGIN CASE
               CASE SNUM = ""
                  FORMNO = FNUM:".":VNUM
               CASE SNUM = 0
                  ERRMSG = 3
               CASE LEN(SNUM) > 5
                  ERRMSG = 3
               CASE 1
                  SNUM = (100000+SNUM)[2,5]
                  FORMNO = FNUM:".":VNUM:".":SNUM
               END CASE
            END CASE
         END CASE
      CASE NUM(FNUM[2,99])
         BEGIN CASE
         CASE LEN(FNUM) = 2
            FNUM = FNUM[1,1]:"00":FNUM[2,99]
            FORMNO = FNUM
         CASE LEN(FNUM) = 3
            FNUM = FNUM[1,1]:"0":FNUM[2,99]
            FORMNO = FNUM
         CASE LEN(FNUM) = 4
         CASE 1
            ERRMSG = 1
         END CASE
      CASE NUM(FNUM[3,99])
         BEGIN CASE
         CASE LEN(FNUM) = 3
            FNUM = FNUM[1,2]:"0":FNUM[3,99]
            FORMNO = FNUM
         CASE LEN(FNUM) = 4
         CASE 1
            ERRMSG = 1
         END CASE
      END CASE
      BEGIN CASE
      CASE ERRMSG = 1
         ERRMSG = "Invalid form #, Try again! "
      CASE ERRMSG = 2
         ERRMSG = "Invalid run #, Try again! "
      CASE ERRMSG = 3
         ERRMSG = "Invalid sig #, Try again! "
      END CASE
      RETURN
*
*---- DRAW BORDER
*
89990 *
      IF TEST.MODE THEN
         FOR SL = 1 TO 16
            PRINT @(21,(SL-1)):"|":
         NEXT SL
         PRINT @(0,16):"---------------------+":
      END
      RETURN
*
*---- ERROR ROUTINE
*
91000 *
      PRINT @(0,ERY):@(-4):BEL:BEL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,ERY):@(-4):
      ERY = 15
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
