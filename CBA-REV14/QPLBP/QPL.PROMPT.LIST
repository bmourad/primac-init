*********************************************************************
*
* PROGRAM  - QPL.PROMPT.LIST
*
* AUTHOR   - NICK AMENDOLA
*
* DATA     - 11/11/93
*
* DESCRIPTION
*
* This program produces the QPL.PROMPT file listing.
*
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>QPL.PROMPT
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- READ PARAMETERS
*
      PROCREAD PARAM ELSE
         ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
         GOSUB 90000
         GOTO 99999
      END
      CONO  = PARAM<1>
      CNAME = PARAM<2>
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         GOTO 95000
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         GOTO 95000
      END
      OPEN "","QPL.PROMPT" TO QPL.PROMPT ELSE
         ERRMSG = "CANNOT OPEN QPL.PROMPT FILE"
         GOSUB 90000
         GOTO 95000
      END
*
*---- INITIALIZATION
*
      TODAY = DATE()
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      SEP = STR("*",120)
      LINE.COUNT = 99
      PAGE.NO = 0
      PRINT.FLAG = 0
*
*---- DEFINE HEADINGS
*
*T26493 v
*     REPORT.NAME = "QUIK-PLAN PROMPT FILE LISTING"
*     RNO = ""
      REPORT.NAME= ""
      RNO = PARAM<2>
      CNAME = ""
*T26493 ^
      HD1 = ""
      HD2 = ""
      CALL GET.PROG.HEAD(CONO,CNAME,REPORT.NAME,RNO,TODAY,HD1,HD2)
*
*---- MAIN PROCESSING
*
100 *
      PRINTER ON
      DATA = 1
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         IF ID[1,3] = CONO THEN
            PID = ID[4,99]
            MATREAD QPPMT.REC FROM QPL.PROMPT, CONO:PID THEN
               GOSUB 1000
            END
         END
      REPEAT
      IF NOT(PRINT.FLAG) THEN
         MAT QPPMT.REC = ""
         GOSUB 10000
         PRINT
         PRINT "NO ITEMS SELECTED"
      END
      PRINTER OFF
      PRINTER CLOSE
      GOTO 99999
*
*---- PRINT DETAIL LINE(S)
*
1000 *
      IF (LINE.COUNT + 10) > 60 THEN GOSUB 10000
      PLINE="PROMPT ID : ":PID "L#20":"DESC    : ":QP$DESC
      PRINT PLINE
      PLINE="HEAD FMT  : ":(QP$PMAXL:QP$PJUSTIFY)"L#20":"HEADING : ":QP$PMSG
      PRINT PLINE
      PLINE="RESP TYPE : ":QP$TYP"L#20":"PREC    : ":QP$SCALER
      PRINT PLINE
      PLINE="RESP FMT  : ":(QP$MAXL:QP$JUSTIFY)"L#20":"FILL    : ":QP$FILL
      PRINT PLINE
      PLINE="OPT / REQ : ":QP$O.R"L#20":"VALID   : ":QP$VALDAT
      PRINT PLINE
      PLINE="DEFAULT   : ":QP$DEFAULT"L#20":"PATTERN : ":QP$PATRN
      PRINT PLINE
      PLINE="MIN LEN   : ":QP$MINL"L#20":"FILE ID : ":QP$VALDAT.FILE"L#20":"PREFIX : ":QP$PREFIX
      PRINT PLINE
      PLINE="MIN VALUE : ":QP$MINV"L#20":"HELP    : ":QP$HMSG<1,1>
      PRINT PLINE
      PLINE="MAX VALUE : ":QP$MAXV"L#20":"        : ":QP$HMSG<1,2>
      PRINT PLINE
      PRINT SEP
      LINE.COUNT = LINE.COUNT + 10
*
      PRINT.FLAG = 1
      RETURN
*
*---- PRINT HEADINGS
*
10000 *
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1: PAGE.NO
      PRINT HD2
      PRINT
      PRINT
      PRINT SEP
      LINE.COUNT = 5
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      PRINT @(0,23):@(-4):ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- ABNORMAL END OF JOB
*
95000 *
      PROCWRITE "END"
      STOP
*
*---- END OF JOB
*
99999 *
      STOP
   END
