*********************************************************************
*
* PROGRAM  - QPL.SCRIPT.MAINT
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 10/05/93
*
* DESCRIPTION
*
* This program is used to organize prompts, previously defined by the
* QPL.SCREEN.MAINT program, into sub-screens to be used by QUIK-PLAN.
*
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE.QP
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.QP
*COPY>JES.CPYLIB>QPL.SCRIPT
*COPY>JES.CPYLIB>QPL.SCREEN
*COPY>JES.CPYLIB>ESTIMATE.MGRP
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*
*---- PRE-INITIALIZATION
*
  PROCREAD PARAM ELSE PARAM = ""
  ACTION = PARAM<1>
  ACTION = "M"                         ;**** R E M O V E ****
  MAT EST.QP.REC = ""
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","QPL.SCRIPT" TO QPL.SCRIPT ELSE
    ERRMSG = "CANNOT OPEN QPL.SCRIPT FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","QPL.SCREEN" TO QPL.SCREEN ELSE
    ERRMSG = "CANNOT OPEN QPL.SCREEN FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","QPL.PROMPT" TO QPL.PROMPT ELSE
    ERRMSG = "CANNOT OPEN QPL.PROMPT FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","ESTIMATE.MGRP" TO ESTIMATE.MGRP ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.MGRP FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","QPL.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN QPL.SCREENS FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","PREFIX" TO PREFIX ELSE
    ERRMSG = "CANNOT OPEN PREFIX FILE"
    GOSUB 91000
    STOP
  END
  OPEN "","XREF.DATA" TO XREF.DATA ELSE
    ERRMSG = "CANNOT OPEN XREF.DATA FILE"
    GOSUB 91000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;QPL.SCRIPT.MAINT
  SCREEN FORMAT
  SCREEN COUNT;;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  LINE.CNT = 0
  REF.NO = ""
  CURR.REF.NO = ""
  QPSPT.ID = ""
  DUP.ID = ""
  TEMP.DESC = ""
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  SCREEN CLEAR
  MAT EST.QP.REC = ""
110 *
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  S$DATA(2)<S$SCR> = QPSPT.ID
  SCREEN FIELD;;2
  SCREEN INPUT;;2;GOTO 99999
  IF S$VALUE = "" OR S$VALUE = "???" THEN
    GXR.NAME = "QPL.SCRIPT"
    GXR.FILE = QPL.SCRIPT
    CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
    SCREEN FORMAT
    SCREEN DISPLAY;;1
    IF GXR.ID = "" THEN GOTO 110
    S$VALUE = GXR.ID
    S$DATA(2)<S$SCR> = S$VALUE
    SCREEN DISPLAY;;2
  END
  NEW.REC = 0
  IF ACTION = "M" THEN
    * T26126 v
    MATREADU QPSPT.REC FROM QPL.SCRIPT, CONO:S$VALUE LOCKED
      ERRMSG = 'Record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 100 
    END ELSE
    * T26126 ^
      MAT QPSPT.REC = ""
      NEW.REC = 1
    END
  END ELSE
    MATREAD QPSPT.REC FROM QPL.SCRIPT, CONO:S$VALUE ELSE
      ERRMSG = "Invalid Item. Try again! "
      GOSUB 91000
      GOTO 100
    END
  END
  QPSPT.ID = S$VALUE
120 *
  IF NEW.REC THEN
    S$DATA(31)<S$SCR> = ""
    SCREEN FIELD;;31
    SCREEN INPUT;;31;GOTO 100
    IF S$VALUE = "Y" THEN
      S$DATA(32)<S$SCR> = DUP.ID
      SCREEN FIELD;;32
      SCREEN INPUT;;32;GOTO 100
      MATREAD QPSPT.REC FROM QPL.SCRIPT, CONO:S$VALUE ELSE
        ERRMSG = "Invalid Item ID. Try again! "
        GOSUB 91000
        GOTO 120
      END
      DUP.ID = S$VALUE
      NEW.REC = 0
      GOTO 120
    END
    FOR REF = 1 TO 2
      ON REF GOSUB 1010, 1020
      IF S$VALUE = "END" THEN GOTO 100
    NEXT REF
    LINE.CNT = 0
    OPT = "A"
    GOTO 510
  END ELSE
    GOSUB 80000
    GOSUB 81000
    LINE.CNT = DCOUNT(QPSPT.SID, VM)
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 50000
  END
*
*---- GET OPERATOR REPLY
*
500 *
  BEGIN CASE
    CASE ACTION = "M"
      SCREEN FIELD;;21
      SCREEN INPUT;;21
    CASE 1
      SCREEN FIELD;;23
      SCREEN INPUT;;23
  END CASE
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      IF ACTION = "M" THEN
        MISMATCH = 0
        READ VREC FROM QPL.SCRIPT, CONO:QPSPT.ID ELSE VREC = ""
        FOR VPTR = 1 TO QPSPT.SIZE UNTIL MISMATCH
          IF QPSPT.REC(VPTR) # VREC<VPTR> THEN MISMATCH = 1
        NEXT VPTR
        IF MISMATCH THEN
          SCREEN FIELD;;25
          SCREEN INPUT;;25
          IF S$VALUE # "Y" THEN GOTO 500
        END
      END
      GOTO 100
    CASE NUM(OPT) AND OPT # ""
      ON OPT GOSUB 1010, 1020
    CASE OPT = "A" AND LINE.CNT < 99
      MODE = "A"
      DONE = 0
      FOR REF.NO = LINE.CNT+1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 700
        END ELSE
          LINE.CNT = LINE.CNT + 1
        END
      NEXT REF.NO
      REF.NO = LINE.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND LINE.CNT > 0
      MODE = "C"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10000
        IF S$VALUE = "END" THEN
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "D" AND LINE.CNT > 0
      MODE = "D"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        LINE.CNT = LINE.CNT - 1
        IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "I" AND LINE.CNT > 0
      MODE = "I"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 800
        LINE.CNT = LINE.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          GOSUB 700
          LINE.CNT = LINE.CNT - 1
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "S" OR OPT = "SF"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "SR"
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "ST"
      REF.NO = 1
      GOSUB 50000
    CASE OPT = "SB"
      REF.NO = LINE.CNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "PUR"
      SCREEN FIELD;;24
      SCREEN INPUT;;24
      IF S$VALUE = "Y" THEN
        DELETE QPL.SCRIPT, CONO: QPSPT.ID
        GOTO 100
      END
    CASE OPT = "R" OR OPT = "P"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        QPSCN.ID = QPSPT.SID<1,REF.NO>
        MATREAD QPSCN.REC FROM QPL.SCREEN, CONO:QPSCN.ID THEN
          IF OPT = "R" THEN
            S$SCR = S$SCR + 1
            CALL QPL.SCREEN.INPUT("T",CONO,1,QPSCN.ID,MAT QPSCN.REC,QPL.PROMPT,ESTATUS)
            S$SCR = S$SCR - 1
            SCREEN FORMAT
            GOSUB 80050
            CURR.REF.NO = ""
            GOSUB 50000
          END ELSE
            CALL QPL.SCREEN.SUB("P", CONO, QPSPT.SID<1,REF.NO>, MAT QPSCN.REC, QPL.PROMPT)
          END
        END
      END
    CASE OPT = "F"
      MATWRITE QPSPT.REC ON QPL.SCRIPT, CONO:QPSPT.ID
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET LINE NUMBER
*
600 *
  SCREEN FIELD;;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
  SCREEN INPUT;;22
  RETURN
*
*---- DELETE MULTI-LINE DATA
*
700 *
  QPSPT.SID = DELETE(QPSPT.SID, 1, REF.NO, 0)
  TEMP.DESC = DELETE(TEMP.DESC, 1, REF.NO, 0)
  GOSUB 81500
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
800 *
  QPSPT.SID = INSERT(QPSPT.SID, 1, REF.NO, 0, "")
  TEMP.DESC = INSERT(TEMP.DESC, 1, REF.NO, 0, "")
  GOSUB 81500
  RETURN
*
*---- GET SCRIPT DESCRIPTION
*
1010 *
  SCREEN FIELD;;3
  SCREEN INPUT;;3;RETURN
  QPSPT.DESC = S$VALUE
  RETURN
*
*---- GET MASTER GROUP
*
1020 *
  S$DATA(4)<S$SCR> = QPSPT.MGROUP
  SCREEN FIELD;;4
  SCREEN INPUT;;4;RETURN
  IF S$VALUE = "" THEN
    MAT MGRP.REC = ""
  END ELSE
    MATREAD MGRP.REC FROM ESTIMATE.MGRP, CONO:S$VALUE ELSE
      ERRMSG = "Invalid Master Group. Try again! "
      GOSUB 91000
      GOTO 1020
    END
  END
  QPSPT.MGROUP = S$VALUE
*
  S$DATA(5)<S$SCR> = MGRP.DESC
  SCREEN DISPLAY;;5
  RETURN
*
*---- GET MULTI-LINE DATA
*
10000 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;11
10100 *
  S$DATA(12)<S$SCR,REF.NO> = QPSPT.SID<1,REF.NO>
  S$VAL = REF.NO
  SCREEN FIELD;;12
  SCREEN INPUT;;12;GOTO 19950
  LOCATE S$VALUE IN QPSPT.SID<1>,1 SETTING P THEN
    IF P # REF.NO THEN
      ERRMSG = "Duplicate item on line ": P
      GOSUB 91000
      GOTO 10100
    END
  END
  IF S$VALUE = "" OR S$VALUE = "???" THEN
    GXR.NAME = "QPL.SCREEN"
    GXR.FILE = QPL.SCREEN
    CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
    SCREEN FORMAT
    GOSUB 80050
    CURR.REF.NO = ""
    GOSUB 50000
    IF GXR.ID = "" THEN GOTO 10100
    S$VALUE = GXR.ID
    S$DATA(12)<S$SCR,REF.NO> = S$VALUE
    S$VAL = REF.NO
    SCREEN DISPLAY;;12
  END
  MATREAD QPSCN.REC FROM QPL.SCREEN, CONO:S$VALUE ELSE
    ERRMSG = "Invalid Item. Try again! "
    GOSUB 91000
    GOTO 10100
  END
  TEMP.MF1 = S$VALUE
  S$VAL = REF.NO
  S$DATA(13)<S$SCR,REF.NO> = QPSCN.DESC
  SCREEN DISPLAY;;13
19900 *
  QPSPT.SID<1,REF.NO> = TEMP.MF1
  TEMP.DESC<1,REF.NO> = QPSCN.DESC
  RETURN
19950 *
  GOSUB 81500
  RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
  SCREEN MULTI;;C;11;12;13
  RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
  S$DATA(3)<S$SCR> = QPSPT.DESC
  S$DATA(4)<S$SCR> = QPSPT.MGROUP
  IF QPSPT.MGROUP = "" THEN
    MAT MGRP.REC = ""
  END ELSE
    MATREAD MGRP.REC FROM ESTIMATE.MGRP, CONO:QPSPT.MGROUP ELSE
      MAT MGRP.REC = ""
      MGRP.DESC = "???????????????"
    END
  END
  S$DATA(5)<S$SCR> = MGRP.DESC
80050 *
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
  LCNT = DCOUNT(QPSPT.SID, VM)
  FOR REF = 1 TO LCNT
    SID = QPSPT.SID<1,REF>
    MATREAD QPSCN.REC FROM QPL.SCREEN, CONO:SID ELSE
      MAT QPSCN.REC = ""
      QPSCN.DESC = "???????????????"
    END
    TEMP.DESC<1,REF> = QPSCN.DESC
  NEXT REF
81500 *
  S$DATA(12)<S$SCR> = QPSPT.SID
  S$DATA(13)<S$SCR> = TEMP.DESC
  RETURN
*
*---- ERROR ROUTINE
*
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
91000 *
  ERR.TYPE = 1;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
  ERR.TYPE = 2;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
  ERR.TYPE = 3;CALL SI_SYSCOM(MAT SYSCOM.REC)
*
99999 *
*       PRINT @(-1)
END
