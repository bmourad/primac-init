      SUBROUTINE QPL.SCREEN.SUB(ACTION, CONO, SNAME, MAT QPSCN.REC, QPL.PROMPT)
*********************************************************************
*
* PROGRAM  - QPL.SCREEN.SUB
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 10/07/93
*
* DESCRIPTION
*
* This subroutine is used to display or print the screen defined by
* QPL.SCREEN.MAINT.
*
* T26556 cmykleb 04/29/2002 * Get rpt #/name from the PMC_REPORTS file.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>QPL.SCREEN
*COPY>JES.CPYLIB>QPL.PROMPT
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      DIM FORM(24)
      MAT FORM = SPACE(80)
*
      SPACE.LM = SPACE(20)
      COLNOS1 = "          "
      FOR N = 1 TO 7
         COLNOS1 = COLNOS1 : (N "L#10")
      NEXT N
      COLNOS2 = "0123456789"
      FOR N = 1 TO 7
         COLNOS2 = COLNOS2 : "0123456789"
      NEXT N
*
      WCOL1 = QPSCN.WCOL1
      WROW1 = QPSCN.WROW1
      WCOL2 = QPSCN.WCOL2
      WROW2 = QPSCN.WROW2
*
      PCNT = DCOUNT(QPSCN.PID,VM)
      FOR PPTR = 1 TO PCNT
         PID = QPSCN.PID<1,PPTR>
         MATREAD QPPMT.REC FROM QPL.PROMPT, CONO:PID THEN
            REF = QPSCN.REF<1,PPTR>
            HCOL = WCOL1 + QPSCN.HCOL<1,PPTR>
            HROW = WROW1 + QPSCN.HROW<1,PPTR>
            ICOL = WCOL1 + QPSCN.ICOL<1,PPTR>
            IROW = WROW1 + QPSCN.IROW<1,PPTR>
            MAXL = QP$MAXL
            BEGIN CASE
            CASE LEN(REF) = 1 AND HCOL >= 3
               FORM(HROW+1)[HCOL+1-3,2] = REF : "-"
            CASE LEN(REF) = 2 AND HCOL >= 4
               FORM(HROW+1)[HCOL+1-4,3] = REF : "-"
            END CASE
            FORM(HROW+1)[HCOL+1,LEN(QP$PMSG)] = QP$PMSG
            IF QP$TYP # "0" THEN
               FORM(IROW+1)[ICOL+1,MAXL] = STR("#",MAXL)
            END
         END
      NEXT PPTR
*
*---- MAIN PROCESSING
*
      IF ACTION = "S" THEN
         PRINT @(-1) :
         FOR ROW = WROW1 TO WROW2
*           IF WCOL2 >= 79 THEN
*              PRINT @(WCOL1,ROW) : @(-4) :
*           END ELSE
*              PRINT @(WCOL1,ROW) : SPACE(WCOL2-WCOL1+1) :
*           END
            IF TRIM(FORM(ROW+1)) # "" THEN
               PRINT @(0,ROW) : FORM(ROW+1) :
            END
         NEXT ROW
      END ELSE
         MATREAD COMP.REC FROM COMPANY, CONO ELSE
            MAT COMP.REC = ""
            CO.NAME = "???????????????"
         END
*T26556 v
*        RNAME = "QUIK-PLAN SUB-SCREEN LAYOUT"
*        CALL GET.PROG.HEAD(CONO, CO.NAME, RNAME, "", "", HLINE1, HLINE2)
         CO.NAME = ""
         RNAME = ""
         RNO = "JE416"
         CALL GET.PROG.HEAD(CONO, CO.NAME, RNAME, RNO, "", HLINE1, HLINE2)
*T26556 ^
         PRINTER ON
         PRINT CHAR(12)
         PRINT HLINE1 : "1"
         PRINT HLINE2
         PRINT
         PRINT "Sub-screen : ":SNAME
         PRINT
         PRINT
*
         PLINE = COLNOS1
         PRINT SPACE.LM:SPACE(5):PLINE
         PLINE = COLNOS2
         PRINT SPACE.LM:SPACE(5):PLINE
         PLINE = "+":STR("-",82):"+"
         PRINT SPACE.LM:SPACE(3):PLINE
         FOR ROW = 0 TO 23
            PRINT SPACE.LM:ROW "R#2":" | ":FORM(ROW+1):" | ":ROW "R#2"
         NEXT ROW
         PLINE = "+":STR("-",82):"+"
         PRINT SPACE.LM:SPACE(3):PLINE
         PLINE = COLNOS1
         PRINT SPACE.LM:SPACE(5):PLINE
         PLINE = COLNOS2
         PRINT SPACE.LM:SPACE(5):PLINE
         PRINT
         PRINT
         PRINTER OFF
         PRINTER CLOSE
      END
      ERRMSG = "Press <RETURN> to continue"
      GOSUB 90000
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
