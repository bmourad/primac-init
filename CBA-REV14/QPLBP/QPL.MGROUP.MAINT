**************************************************************************
*
* PROGRAM  - QPL.MGROUP.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/20/97
*
* DESCRIPTION
*
* This program updates and maintaines the ESTIMATE.MGRP file.
*
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*T26595 cmykleb 05/31/2002 * Add a (P)urge function.
**************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE.QP
*COPY>JES.CPYLIB>ESTIMATE.MGRP
*COPY>JES.CPYLIB>ESTIMATE.GRP
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>CPYLIB>SCREEN.COM
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","JES.SCREENS" TO M.SCREENS ELSE
      ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","QPL.SCREENS" TO QPL.SCREENS ELSE
      ERRMSG = "CANNOT OPEN QPL.SCREENS FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE.QP.GRP" TO ESTIMATE.GRP ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.GRP FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE.MGRP" TO ESTIMATE.MGRP ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.MGRP FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE.QP.GRP.XREF" TO ESTIMATE.GRP.XREF ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.QP.GRP.XREF FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "CANNOT OPEN DEPARTMENT FILE"
      GOSUB 91000
      STOP
   END
*
*---- INITIALIZATION
*
   CONO = ""
   CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
   SCREEN INIT;#
   S$SCR = 1
   SCREEN DEFINE;QPL.MGROUP.MAINT;QPL.SCREENS
   SCREEN FORMAT
   SCREEN COUNT;;11
   LINE.COUNT = S$LCNT
   LINE.SPACE = S$LSPC
   CURR.REF.NO = ""
   GOTO 110
*
*---- MAIN PROCESSING
*
100*
   SCREEN CLEAR
   RELEASE
110*
   SCREEN FIELD;;2
   SCREEN INPUT;;2
   IF S$VALUE = "END" THEN GOTO 99999
   MGRP.ID = S$VALUE
   NEW.REC = 0
  * T26126 v
   MATREADU MGRP.REC FROM ESTIMATE.MGRP, CONO:MGRP.ID LOCKED
      ERRMSG = 'MASTER GROUP record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 110 
   END ELSE
  * T26126 6
      MAT MGRP.REC = ""
      NEW.REC = 1
   END
   IF NEW.REC THEN
      GOSUB 1010
      IF S$VALUE = "END" THEN GOTO 100
      CNT = 0
      REF.NO = 0
      OPT = "A"
      GOTO 510
   END ELSE
      GOSUB 80000
      CNT = COUNT(MGRP.DEPT,VM) + (MGRP.DEPT # "")
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 50000
   END
*
*---- GET OPERATOR REPLY
*
500*
   SCREEN FIELD;;21
   SCREEN INPUT;;21
   OPT = S$VALUE
510*
   BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 100
      CASE NUM(OPT)
         ON OPT GOSUB 1010
      CASE OPT = "A" AND CNT < 99
         DONE = 0
         FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
            GOSUB 50000
            GOSUB 10000
            IF S$VALUE = "END" THEN
               DONE = 1
               GOSUB 700
            END ELSE
               CNT = CNT + 1
            END
         NEXT REF.NO
         REF.NO = CNT
         CURR.REF.NO = ""
         GOSUB 50000
      CASE OPT = "C" AND CNT > 0
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 10000
            IF S$VALUE = "END" THEN
               CURR.REF.NO = ""
               GOSUB 50000
            END
         END
      CASE OPT = "I" AND CNT > 0
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 800
            CNT = CNT + 1
            CURR.REF.NO = ""
            GOSUB 50000
            GOSUB 10000
            IF S$VALUE = "END" THEN
               GOSUB 700
               CNT = CNT - 1
            END
            CURR.REF.NO = ""
            GOSUB 50000
         END
      CASE OPT = "D" AND CNT > 0
         GOSUB 600
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 700
            CNT = CNT - 1
            IF REF.NO > CNT THEN REF.NO = REF.NO - 1
            CURR.REF.NO = ""
            GOSUB 50000
         END
*T26595 v
      CASE OPT = "P"
         SCREEN FIELD;;24
         SCREEN INPUT;;24
         IF S$VALUE = "Y" THEN
            DELETE ESTIMATE.MGRP, CONO:MGRP.ID
            GOTO 100
         END
*T26595 ^
      CASE OPT = "S" OR OPT = "SF"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > CNT THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "SR"
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "ST"
         REF.NO = 1
         GOSUB 50000
      CASE OPT = "SB"
         REF.NO = CNT
         GOSUB 50000
      CASE OPT = "F"
         MATWRITE MGRP.REC ON ESTIMATE.MGRP,CONO:MGRP.ID
         GOTO 100
   END CASE
   GOTO 500
*
*---- GET LINE REFERENCE NUMBER
*
600*
   SCREEN FIELD;;22
   S$MINV = CURR.REF.NO
   S$MAXV = S$MINV + LINE.COUNT - 1
   IF S$MAXV > CNT THEN S$MAXV = CNT
   SCREEN INPUT;;22
   RETURN
*
*---- DELETE MULTI-LINE DATA
*
700*
   MGRP.DEPT = DELETE(MGRP.DEPT,1,REF.NO,0)
   MGRP.DEPT.DESC = DELETE(MGRP.DEPT.DESC,1,REF.NO,0)
   MGRP.GRP = DELETE(MGRP.GRP,1,REF.NO,0)
   MGRP.GRP.DESC = DELETE(MGRP.GRP.DESC,1,REF.NO,0)
   S$DATA(12)<S$SCR> = MGRP.DEPT
   S$DATA(13)<S$SCR> = MGRP.DEPT.DESC
   S$DATA(14)<S$SCR> = MGRP.GRP
   S$DATA(15)<S$SCR> = MGRP.GRP.DESC
   RETURN
*
*---- INSERT MULTI-LINE DATA
*
800*
   MGRP.DEPT = INSERT(MGRP.DEPT,1,REF.NO,0,"")
   MGRP.DEPT.DESC = INSERT(MGRP.DEPT.DESC,1,REF.NO,0,"")
   MGRP.GRP = INSERT(MGRP.GRP,1,REF.NO,0,"")
   MGRP.GRP.DESC = INSERT(MGRP.GRP.DESC,1,REF.NO,0,"")
   S$DATA(12)<S$SCR> = MGRP.DEPT
   S$DATA(13)<S$SCR> = MGRP.DEPT.DESC
   S$DATA(14)<S$SCR> = MGRP.GRP
   S$DATA(15)<S$SCR> = MGRP.GRP.DESC
   RETURN
*
*---- GET MASTER GROUP DESCRIPTION
*
1010*
   SCREEN FIELD;;3
   SCREEN INPUT;;3;RETURN
   MGRP.DESC = S$VALUE
   RETURN
*
*---- GET MULTI-LINE DATA
*
10000*
   S$VAL = REF.NO
   SCREEN DISPLAY;;11
10010*
   S$DATA(12)<S$SCR,REF.NO> = MGRP.DEPT<1,REF.NO>
   S$VAL = REF.NO
   SCREEN FIELD;;12
   SCREEN INPUT;;12;GOTO 10900
   MATREAD DEPT.REC FROM DEPARTMENT, CONO:S$VALUE ELSE
      ERRMSG = "Invalid department ID. Try again! "
      GOSUB 91000
      GOTO 10010
   END
   TEMP.DEPT = S$VALUE
   S$DATA(13)<S$SCR,REF.NO> = DEPT.DESC
   S$VAL = REF.NO
   SCREEN DISPLAY;;13
10020*
   S$DATA(14)<S$SCR,REF.NO> = MGRP.GRP<1,REF.NO>
   S$VAL = REF.NO
   SCREEN FIELD;;14
   SCREEN INPUT;;14;GOTO 10900
   GRP = S$VALUE
   IF GRP = "???" THEN
      S$SCR = S$SCR + 1
      SEL.GRP = ""
      CALL EST.GROUP.XREF (CONO, TEMP.DEPT, "S", SEL.GRP)
      S$SCR = S$SCR - 1
      IF SEL.GRP = "X" THEN
         GOTO 10020
      END
      GRP = SEL.GRP
      S$DATA(14)<S$SCR,REF.NO> = GRP
      SCREEN FORMAT
      GOSUB 80090
      CURR.REF.NO = ""
      GOSUB 50000
      IF SEL.GRP = "" THEN GOTO 10020
   END
   MATREAD ESTG.REC FROM ESTIMATE.GRP, CONO:TEMP.DEPT:"!":GRP ELSE
      ERRMSG = "Invalid group ID. Try again! "
      GOSUB 91000
      GOTO 10020
   END
   TEMP.GRP = GRP
   S$DATA(15)<S$SCR,REF.NO> = ESTG.DESC
   S$VAL = REF.NO
   SCREEN DISPLAY;;15
10800*
   MGRP.DEPT<1,REF.NO> = TEMP.DEPT
   MGRP.DEPT.DESC<1,REF.NO> = DEPT.DESC
   MGRP.GRP<1,REF.NO> = TEMP.GRP
   MGRP.GRP.DESC<1,REF.NO> = ESTG.DESC
   RETURN
10900*
   S$DATA(12)<S$SCR,REF.NO> = MGRP.DEPT<1,REF.NO>
   S$DATA(13)<S$SCR,REF.NO> = MGRP.DEPT.DESC<1,REF.NO>
   S$DATA(14)<S$SCR,REF.NO> = MGRP.GRP<1,REF.NO>
   S$DATA(15)<S$SCR,REF.NO> = MGRP.GRP.DESC<1,REF.NO>
   RETURN
*
*---- DISPLAY MULTI-LINE DATA
*
50000*
   START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
   IF START.REF.NO = CURR.REF.NO THEN RETURN
   CURR.REF.NO = START.REF.NO
   S$VAL = START.REF.NO
   S$CNT = COUNT(S$DATA(12)<S$SCR>,VM) + (S$DATA(12)<S$SCR> # "")
   SCREEN MULTI;;C;11;12;13;14;15
   RETURN
*
*---- LOAD DATA MATRIX
*
80000*
   S$DATA(1)<S$SCR> = DATE()
   S$DATA(3)<S$SCR> = MGRP.DESC
   S$DATA(12)<S$SCR> = MGRP.DEPT
   S$DATA(13)<S$SCR> = ""
   S$DATA(14)<S$SCR> = MGRP.GRP
   S$DATA(15)<S$SCR> = ""
   MGRP.DEPT.DESC = ""
   MGRP.GRP.DESC = ""
   N2 = COUNT(MGRP.DEPT,VM) + (MGRP.DEPT # "")
   FOR N = 1 TO N2
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:MGRP.DEPT<1,N> ELSE
         MAT DEPT.REC = ""
      END
      MGRP.DEPT.DESC<1,N> = DEPT.DESC
      S$DATA(13)<S$SCR,N> = DEPT.DESC
      MATREAD ESTG.REC FROM ESTIMATE.GRP, CONO:MGRP.DEPT<1,N>:"!":MGRP.GRP<1,N> ELSE
         MAT ESTG.REC = ""
      END
      MGRP.GRP.DESC<1,N> = ESTG.DESC
      S$DATA(15)<S$SCR,N> = ESTG.DESC
   NEXT N
80090*
   SCREEN DISPLAY;;ALL
   RETURN
*
*---- ERROR ROUTINE
*
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
91000 *
   ERR.TYPE = 1;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
   ERR.TYPE = 2;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
   ERR.TYPE = 3;CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
99999*
END
