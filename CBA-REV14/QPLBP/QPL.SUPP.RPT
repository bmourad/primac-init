   SUBROUTINE QPL.SUPP.RPT (CONO,CO.NAME,EST.KEY,PAGE.NO,HD1,HD2)
**************************************************************************
*
* PROGRAM  - QPL.SUPP.RPT
*
* AUTHOR   - NICK AMENDOLA, NASTech, Inc.
*
* DATE     - 11/29/93
*
* DESCRIPTION
*
* This program will print the supplemental data for QUIK-PLAN estimate.
*
* T26090 cmykleb 04/05/2002 * Have rpt get rpt # from proc and use
*                             GET.PROG.HEAD for the heading.
**************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.QP
*COPY>JES.CPYLIB>QPL.SCRIPT
*COPY>JES.CPYLIB>ESTIMATE.MGRP
*COPY>JES.CPYLIB>QPL.SCREEN
*COPY>JES.CPYLIB>QPL.PROMPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
   OPEN "","ESTIMATE.QP" TO ESTIMATE.QP ELSE RETURN
   OPEN "","ESTIMATE.MGRP" TO ESTIMATE.MGRP ELSE RETURN
   OPEN "","QPL.SCRIPT" TO QPL.SCRIPT ELSE RETURN
   OPEN "","QPL.SCREEN" TO QPL.SCREEN ELSE RETURN
   OPEN "","QPL.PROMPT" TO QPL.PROMPT ELSE RETURN
*
*---- INITIALIZATION
*
   DIM FORM(24)
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   SP8 = SPACE(8)
   LM = SPACE(20)
   DASH1 = STR("-",130)
   DASH2 = STR("=",130)
   LINE.COUNT = 99
   PAGE.NO = 0
   CDATE = DATE()
   CTIME = TIME()
   IF CTIME < 10 THEN CDATE = DATE()
   CTIME = OCONV(CTIME,"MTS")
   CHR = CTIME[1,2]+0
   CMN = CTIME[4,2]
   BEGIN CASE
      CASE CHR < 12
         PTIME = CHR:":":CMN:" AM"
      CASE CHR = 12
         PTIME = CHR:":":CMN:" PM"
      CASE 1
         PTIME = (CHR-12):":":CMN:" PM"
   END CASE
*T26090 v
*  HD1A = OCONV(CDATE,"D2/"):"   ":PTIME
*  HD1B = SPACE((40-LEN(CO.NAME))/2):CO.NAME
*  HD1 = HD1A"L#46":HD1B"L#40":SPACE(34):"PAGE: "
*  HD2 = SPACE(39):"Q U I K - P L A N   S U P P L E M E N T A L   D A T A"
   HD3 = SPACE(61):"QUICK-PLAN SUPPLEMENTAL DATA"
*T26090 ^
*
*---- MAIN PROCESSING
*
100*
   MATREAD EST.QP.REC FROM ESTIMATE.QP, CONO:EST.KEY ELSE RETURN
   PRINTER ON
   CCNT = DCOUNT(EST.PROD.DESC,VM)
   FOR COMP = 1 TO CCNT
      QPSPT.ID = EST.PROD.SCRIPT<1,COMP>
      MATREAD QPSPT.REC FROM QPL.SCRIPT, CONO:QPSPT.ID THEN
         MGRP.ID = EST.PROD.MGROUP<1,COMP>
         IF MGRP.ID = "" THEN
            MAT MGRP.REC = ""
         END ELSE
            MATREAD MGRP.REC FROM ESTIMATE.MGRP, CONO:MGRP.ID ELSE
               MAT MGRP.REC = ""
               MGRP.DESC = "???????????????"
            END
         END
         CDEF = ("*COMPONENT ":COMP)"L#25"
         CDEF = CDEF:("SCRIPT: ":QPSPT.ID:SP3:QPSPT.DESC)"L#50"
         CDEF = CDEF:("MASTER GROUP: ":MGRP.ID:SP3:MGRP.DESC)"L#55"
         SCNT = DCOUNT(QPSPT.SID,VM)
         FOR SPTR = 1 TO SCNT
            SID = QPSPT.SID<1,SPTR>
            MATREAD QPSCN.REC FROM QPL.SCREEN, CONO:SID ELSE
               MAT QPSCN.REC = ""
            END
            GOSUB 1000
         NEXT SPTR
         PRINT DASH2
      END
   NEXT COMP
   GOTO 99999
*
*---- PRINT REPORT
*
1000*
   MAT FORM = SPACE(80)
*
   WCOL1 = QPSCN.WCOL1
   WROW1 = QPSCN.WROW1
   WCOL2 = QPSCN.WCOL2
   WROW2 = QPSCN.WROW2
*
   PCNT = DCOUNT(QPSCN.PID,VM)
   FOR PPTR = 1 TO PCNT
      PID = QPSCN.PID<1,PPTR>
      MATREAD QPPMT.REC FROM QPL.PROMPT, CONO:PID THEN
         REF = QPSCN.REF<1,PPTR>
         HCOL = WCOL1 + QPSCN.HCOL<1,PPTR>
         HROW = WROW1 + QPSCN.HROW<1,PPTR>
         ICOL = WCOL1 + QPSCN.ICOL<1,PPTR>
         IROW = WROW1 + QPSCN.IROW<1,PPTR>
         MAXL = QP$MAXL
         BEGIN CASE
            CASE LEN(REF) = 1 AND HCOL >= 3
               FORM(HROW+1)[HCOL+1-3,2] = REF : "-"
            CASE LEN(REF) = 2 AND HCOL >= 4
               FORM(HROW+1)[HCOL+1-4,3] = REF : "-"
         END CASE
         FORM(HROW+1)[HCOL+1,LEN(QP$PMSG)] = QP$PMSG
         IF QP$TYP # "0" THEN
            VALUE = EST.QP.REC(PID)<1,COMP>
            BEGIN CASE
               CASE QP$TYP = "4"
                  CNV = "MD":QP$SCALER
                  DVALUE = OCONV(VALUE,CNV)
               CASE QP$TYP = "6"
                  CNV = "D2/"
                  DVALUE = OCONV(VALUE,CNV)
               CASE 1
                  DVALUE = VALUE
            END CASE
            FMT = QP$JUSTIFY:"#":QP$MAXL
            FORM(IROW+1)[ICOL+1,MAXL] = DVALUE FMT
         END
      END
   NEXT PPTR
*
   LCNT = 0
   FOR ROW = WROW1 TO WROW2
      IF TRIM(FORM(ROW+1)) # "" THEN LCNT = LCNT + 1
   NEXT ROW
   IF (LINE.COUNT + LCNT) > 55 THEN GOSUB 10000
   IF CDEF # "" THEN
      PRINT CDEF
      LINE.COUNT = LINE.COUNT + 1
      CDEF = ""
   END
   PRINT
   LINE.COUNT = LINE.COUNT + 1
   FOR ROW = WROW1 TO WROW2
      IF TRIM(FORM(ROW+1)) # "" THEN
         PRINT LM : FORM(ROW+1)
         LINE.COUNT = LINE.COUNT + 1
      END
   NEXT ROW
   RETURN
*
*---- PRINT PAGE HEADINGS
*
10000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1 : PAGE.NO
   PRINT HD2
   PRINT "ESTIMATE : ":EST.KEY
   PRINT
   IF CDEF = "" THEN
      PRINT DASH1
   END ELSE
      PRINT DASH2
   END
   LINE.COUNT = 6
   RETURN
*
*---- END OF PROGRAM
*
99999*
   PRINTER OFF
   RETURN
END
