**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - CNVBP
* PROGRAM     - ICREW.INV.AUDIT.HIST.CONV
* BY          - EDVARD PITKA ;CBA
* DATE        - 06/12/2001
* DESCRIPTION -
*
*ENDDOC
**********************************************
*COPY>ICS.CPYLIB>INV.TRAN.HIST
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>TRANSFER.REV11
*COPY>OPS.CPYLIB>BOL
*
DEFFUN GET.INAH.SEQ(CONO,CONTROL.FILE,INV_AUDIT_HIST.FILE)
DEFFUN DIVISION.POSITION(CONO,CONTROL.FILE,DIV.CODE)
DEFFUN CURRENT.PERIOD(CONO,CONTROL.FILE,DIV.POS,FISCAL.FLAG)
*
OPEN "", "INV_AUDIT_HIST" TO INV_AUDIT_HIST ELSE STOP "CANNOT OPEN INV_AUDIT_HIST FILE"
OPEN "", "INV_AUDIT_BAL" TO INV_AUDIT_BAL ELSE STOP "CANNOT OPEN INV_AUDIT_BAL FILE"
OPEN "", "INV.TRAN.HIST" TO INV.TRAN.HIST ELSE STOP "CANNOT OPEN INV.TRAN.HIST FILE"
OPEN "", "COMPANY" TO COMPANY ELSE STOP "CANNOT OPEN COMPANY FILE"
OPEN "", "WAREHOUSE" TO WAREHOUSE ELSE STOP "CANNOT OPEN WAREHOUSE FILE"
OPEN "", "INV.WHSE.OLD" TO INV.WHSE.OLD ELSE STOP "CANNOT OPEN INV.WHSE.OLD FILE"
OPEN "","INV.WHSE" TO INV.WHSE ELSE ERRMSG="CANNOT OPEN INV.WHSE FILE"
OPEN "", "INV_RECEIPTS" TO INV_RECEIPTS ELSE STOP "CANNOT OPEN INV_RECEIPTS FILE"
OPEN '','INV_SERIAL' TO INV_SERIAL ELSE STOP 'CANNOT OPEN INV_SERIAL FILES'
OPEN '','TRANSFER.OLD' TO TRANSFER.OLD ELSE STOP 'CANNOT OPEN TRANSFER.OLD FILE'
OPEN '','BOL.OLD' TO BOL.OLD ELSE STOP 'CANNOT OPEN BOL.OLD FILE'
OPEN '','BOL' TO BOL ELSE STOP 'CANNOT OPEN BOL FILE'
OPEN '','ITH.INAH.XREF' TO ITH.INAH.XREF ELSE STOP 'CANNOT OPEN ITH.INAH.XREF'
OPEN '','CONTROL' TO CONTROL ELSE STOP 'CONTROL'
*
DIM HOLD.IWH.REC(IWH.REC.SIZE)
MAT HOLD.IWH.REC =""
TARR='' ;* TARR<1>=TRANSFER# TARR<2>=LAST POSITION
BARR='' ;* BARR<1>=BOL!PROD!WHSE BARR<2>=LAST POSITION
PPOS='' ; CNT=0
*
PRINT @(-1)
PRINT @(1,5):'SELECTING INV.TRAN.HIST':@(-4)
UDTEXECUTE 'SSELECT INV.TRAN.HIST BY DATE WITH M.LINE="FNGD"' CAPTURING GARBAGE
PRINT @(1,5):'CONVERTING INV.TRAN.HIST':@(-4)
*
DONE = 0
LOOP
  READNEXT ITH.ID ELSE DONE = 1
UNTIL DONE DO
  MATREAD ITH.REC FROM INV.TRAN.HIST, ITH.ID THEN
    CNT+=1
    IF REM(CNT,100)=0 THEN   
      PRINT @(45,5):CNT:@(-4)
    END                      
    CONO = ITH.ID[1,3]
    MATREAD COMP.REC FROM COMPANY, CONO  THEN 
      READ APD.REC FROM CONTROL, CONO:"AUDIT.PERIOD.DATES" THEN
        X=DCOUNT(APD.REC<1>, @VM)   
        PYR = APD.REC<1,X>[1,4]     
        PNO = APD.REC<1,X>[5,2]     
        PNO = PNO + 1 "R%2"         
        IF PNO > CO.LAST.FISCAL THEN
          PYR = PYR + 1             
          PNO = "01"                
        END                         
        APD.REC<1,X+1> = PYR:PNO  ;*
        IWH.ID = OCONV(ITH.ID,"G!2")
        DT = OCONV(ITH.ID,"G2!1")
        MATREAD IWH.REC FROM INV.WHSE.OLD,IWH.ID THEN
          IF ITH.TYPE<1,1> = "B" THEN 
            GOSUB SET.BEG.BAL
          END ELSE
            GOSUB PROCESS.HIST
          END
        END
      END
    END
  END
REPEAT
*
GOTO 99999
*
*********************
PROCESS.HIST: 
*********************
*
INAH.XREF=''
TCNT = DCOUNT(ITH.TYPE<1>,@VM)
FOR T = 1 TO TCNT
  ;*
  ;* update with INV.TRAN.HIST data
  ;*
  MAT INAH.REC = ""
  INAH.TYPE = ITH.TYPE<1,T>
  INAH.PROD = FIELD(ITH.ID,"!",1)[4,99]
  INAH.WHSE = FIELD(ITH.ID,"!",2)
  ;*
  MATREAD WHSE.REC FROM WAREHOUSE, CONO:INAH.WHSE ELSE MAT WHSE.REC=''
  IF WHS.DIV = "" THEN WHS.DIV = "00"
  DIV.CODE=WHS.DIV
  INAH.DV.DP.CC = WHS.DIV:"00":"000"
  INAH.DATE = FIELD(ITH.ID,"!",3)
  INAH.LOC = ITH.LOC<1,T>
  INAH.JOB = ITH.JOB<1,T>
  INAH.QTY = ITH.QTY<1,T>
  INAH.UNIT.COST = ITH.UN.COST<1,T>
  INAH.EXT.COST = ITH.COST<1,T>
  INAH.TRAN = ITH.TRAN<1,T>
  INAH.OPER.ID = "CONV"
  INAH.DESC = "CONV"
  ;*
  ;* get the period
  ;*
  LOCATE INAH.DATE IN APD.REC<2> BY "AR" SETTING PLOC ELSE
    NULL                                                  
  END                                                     
  PYR = APD.REC<1,PLOC>[1,4]                              
  PNO = APD.REC<1,PLOC>[5,2]                              
  CHECK.PNO = PNO + 1 "R%2"                                     
  IF CHECK.PNO > CO.LAST.FISCAL THEN                            
    PYR = PYR + 1                                         
    PNO = "01"                                            
  END                                                     
  EPERIOD = PYR:PNO                                       
  ;* check if EPERIOD is greater then                  
  ;* current period, if so then EPERIOD=current period 
  DIV.POS=DIVISION.POSITION(CONO,CONTROL,DIV.CODE)     
  IF DIV.POS<1,1>='' THEN                              
    DIV.POS=DIV.POS<1,2>                               
  END ELSE                                             
    STOP 'PROBLEM WITH DIVISOIN POSITION'              
  END                                                  
  CURR.PERIOD=CURRENT.PERIOD(CONO,CONTROL,DIV.POS,'IC')
  IF CURR.PERIOD<1,1>='' THEN                          
    CURR.PERIOD=CURR.PERIOD<1,2>                       
  END ELSE                                             
    STOP 'PROBLEM WITH FISCAL PERIOD'                  
  END                                                  
  IF EPERIOD>CURR.PERIOD THEN EPERIOD=CURR.PERIOD      
  INAH.PERIOD=EPERIOD
  ;*
  ;* set the source of transaction
  ;*
  BEGIN CASE
    CASE INAH.TYPE = "I" 
      INAH.SRC = "IT"
      INAH.EXT.COST=ABS(INAH.EXT.COST)
      INAH.QTY=ABS(INAH.QTY)
      TRAN.ID=CONO:ITH.TRAN<1,T>
      MATREAD  R11.TRAN.REC FROM TRANSFER.OLD,TRAN.ID THEN
        TMP=ITH.TRAN<1,T>:"!":INAH.WHSE
        LOCATE TMP IN TARR<1> SETTING TARR.POS THEN
          LL=TARR<2,TARR.POS>+1
        END ELSE
          TARR<1,TARR.POS>=ITH.TRAN<1,T>:"!":INAH.WHSE
          TARR<2,TARR.POS>=1
          LL=1
        END
        TFOUND=0 ; BEG.POS=1
        LOOP
          LOCATE INAH.PROD IN R11.TRAN.PROD.NO<1>,BEG.POS SETTING PPOS THEN
            IF INAH.WHSE=R11.TRAN.TO.WHSE<1,PPOS> THEN
              TFOUND=1
            END ELSE
*lross        BEG.POS+=1
              BEG.POS=PPOS+1
            END
          END ELSE
            TFOUND=1
            PPOS=0
          END
        UNTIL TFOUND DO REPEAT
        IF PPOS THEN
          INAH.RECP.NO=R11.TRAN.PO.LN<1,PPOS,LL>
          INAH.SERIAL='R':INAH.RECP.NO:"!":R11.TRAN.TO.WHSE<1,PPOS>:"!":R11.TRAN.TO.LOC<1,PPOS,LL>
          TARR<2,LL>+=1
        END
      END
    CASE INAH.TYPE = "O"
      INAH.SRC = "IT"
      INAH.QTY=-ABS(INAH.QTY)
      INAH.EXT.COST= -ABS(INAH.EXT.COST)
      TRAN.ID=CONO:ITH.TRAN<1,T>
      MATREAD  R11.TRAN.REC FROM TRANSFER.OLD,TRAN.ID THEN
        TMP=ITH.TRAN<1,T>:"!":INAH.WHSE
        LOCATE TMP IN TARR<1> SETTING TARR.POS THEN
          LL=TARR<2,TARR.POS>+1
        END ELSE
          TARR<1,TARR.POS>=ITH.TRAN<1,T>:"!":INAH.WHSE
          TARR<2,TARR.POS>=1
          LL=1
        END
        TFOUND=0 ; BEG.POS=1
        LOOP
          LOCATE INAH.PROD IN R11.TRAN.PROD.NO<1>,BEG.POS SETTING PPOS THEN
            IF INAH.WHSE=R11.TRAN.FROM.WHSE<1,PPOS> THEN
              TFOUND=1
            END ELSE
*lross        BEG.POS+=1
              BEG.POS=PPOS+1
            END
          END ELSE
            TFOUND=1
            PPOS=0
          END
        UNTIL TFOUND DO REPEAT
        IF PPOS THEN
          INAH.RECP.NO=R11.TRAN.PO.LN<1,PPOS,LL>
          INAH.SERIAL='R':INAH.RECP.NO:"!":R11.TRAN.FROM.WHSE<1,PPOS>:"!":R11.TRAN.FROM.LOC<1,PPOS,LL>
          TARR<2,LL>+=1
        END
      END
    CASE INAH.TYPE = "S"
      INAH.SRC='U'
      BOL.ID=CONO:ITH.TRAN<1,T>
      MATREAD BOL.REC FROM BOL.OLD,BOL.ID THEN
        TMP=ITH.TRAN<1,T>:"!":INAH.PROD:"!":INAH.WHSE
        LOCATE TMP IN BARR<1> SETTING BARR.POS THEN   
          BEG.POS=BARR<2,BARR.POS>+1                       
        END ELSE                                      
          BEG.POS=0
        END                                           
        BEG.POS+=1
        BFOUND=0
        LOOP 
          LOCATE INAH.PROD IN BOL.PROD<1>,BEG.POS SETTING LPOS THEN
            IF INAH.WHSE=BOL.WHSE<1,LPOS> THEN 
              BFOUND=1 
              BARR<1,BARR.POS>=TMP 
              BARR<2,BARR.POS>=LPOS   
            END ELSE 
*lross        BEG.POS+=1 
              BEG.POS=LPOS+1 
            END
          END ELSE 
            BFOUND=1 
            LPOS=0 
          END
        UNTIL BFOUND DO REPEAT 
        IF PPOS THEN
          MATREAD BOL.REC FROM BOL,BOL.ID THEN
            INAH.RECP.NO=BOL.RECP.NO<1,LPOS>
          END
          INAH.SERIAL='R':INAH.RECP.NO:"!":INAH.WHSE:"!":INAH.LOC
        END
      END
    CASE INAH.TYPE = "R"
      INAH.SRC = "IR"
      INAH.QTY=ABS(INAH.QTY)
      INAH.EXT.COST=ABS(INAH.EXT.COST)
      INAH.RECP.NO=ITH.TRAN<1,T>
      INAH.SERIAL='R':INAH.RECP.NO:"!":INAH.WHSE:"!":INAH.LOC
    CASE INAH.TYPE = "A"
      INAH.SRC = "IA"
      INAH.RECP.NO=OCONV(ITH.TRAN<1,T>,"G_1")[4,99]
      INAH.SERIAL="R":INAH.RECP.NO:"!":INAH.WHSE:"!":INAH.LOC
      INAH.TRAN=INAH.TRAN[4,99]
  END CASE
  ;*
  ;* get the sequence number
  ;*
  INAH.SEQ=GET.INAH.SEQ(CONO,CONTROL,INV_AUDIT_HIST)
  INAH.ID=CONO:INAH.SEQ
  INAH.XREF<1,-1>=INAH.ID
  MATWRITE INAH.REC ON INV_AUDIT_HIST, INAH.ID
  GOSUB UPDATE.AUDIT.BAL
NEXT T
WRITE INAH.XREF ON ITH.INAH.XREF,ITH.ID
RETURN
*
*****************
SET.BEG.BAL: 
*****************
*
MAT ITB.REC=""
ITB.BEG = ITH.QTY
ITB.BEG.AMT = ITH.COST
LOCATE FIELD(ITH.ID,"!",3) IN APD.REC<2> BY "AR" SETTING PLOC ELSE  
  NULL                                                            
END                                                               
PYR = APD.REC<1,PLOC>[1,4]                                        
PNO = APD.REC<1,PLOC>[5,2]                                        
PNO = PNO + 1 "R%2"                                               
IF PNO > CO.LAST.FISCAL THEN                                      
  PYR = PYR + 1                                                   
  PNO = "01"                                                      
END                                                               
EPERIOD = PYR:PNO                                                 
ITB.ID=OCONV(ITH.ID,"G!2"):"!":EPERIOD
MATWRITE ITB.REC ON INV_AUDIT_BAL, ITB.ID                        
RETURN
*
*******************
UPDATE.AUDIT.BAL: 
*******************
*
ITB.ID = CONO:INAH.PROD:"!":INAH.WHSE:"!":INAH.PERIOD
MATREAD ITB.REC FROM INV_AUDIT_BAL,ITB.ID ELSE MAT ITB.REC=""
BEGIN CASE                       
  CASE INAH.TYPE = "R"           
    ITB.RECV += INAH.QTY         
    ITB.RECV.AMT += INAH.EXT.COST    
  CASE INAH.TYPE = "S"
    ITB.SALE += INAH.QTY
    ITB.SALE.AMT += INAH.EXT.COST    
  CASE INAH.TYPE = "A"           
    ITB.SHRNK += INAH.QTY        
    ITB.SHRNK.AMT += INAH.EXT.COST   
  CASE INAH.TYPE = "I"           
    ITB.TRAN.IN += INAH.QTY      
    ITB.TRAN.IN.AMT += INAH.EXT.COST 
  CASE INAH.TYPE = "O"           
    ITB.TRAN.OUT += INAH.QTY     
    ITB.TRAN.OUT.AMT += INAH.EXT.COST
END CASE                         
MATWRITE ITB.REC ON INV_AUDIT_BAL,ITB.ID
RETURN
*
*********************
*
99999 
