**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - CNVBP
* PROGRAM     - ICREW.INV.AUDIT.HIST.CONV
* BY          - ct6; CBA
* DATE        - 06/12/2001
* DESCRIPTION -
*
*T????? edvard 04/16/2001 * Create the INV_AUDIT_HIST records using data
*                        from INV.TRAN.HIST, INV.ADJ.HIST and
*                        INV.REC.ADJ AND INV.COST.ADJ
*T2 thompson 09/18/2002 * FIX BUG FOR NON PAPER RECEIPTS
*ENDDOC
**********************************************
*COPY>ICS.CPYLIB>INV.TRAN.HIST
*COPY>ICS.CPYLIB>INV.ADJ.HIST
*COPY>ICS.CPYLIB>INV.REC.ADJ
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INV.WHSE.REV11
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>INV.COST.ADJ
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
*
DEFFUN GET.INAH.SEQ(CONO,CONTROL.FILE,INV_AUDIT_HIST.FILE)
DEFFUN CALC.STK.QTY(COST.QTY,MAT INV.CNV.REC,ROND,LN)
DEFFUN DIVISION.POSITION(CONO,CONTROL.FILE,DIV.CODE)
DEFFUN CURRENT.PERIOD(CONO,CONTROL.FILE,DIV.POS,FISCAL.FLAG)
*
*   FILE OPENS
*
OPEN "", "INV_AUDIT_HIST" TO INV_AUDIT_HIST ELSE STOP "CANNOT OPEN INV_AUDIT_HIST FILE"
OPEN "", "INV_AUDIT_BAL" TO INV_AUDIT_BAL ELSE STOP "CANNOT OPEN INV_AUDIT_BAL FILE"
OPEN "", "INV.TRAN.HIST" TO INV.TRAN.HIST ELSE STOP "CANNOT OPEN INV.TRAN.HIST FILE"
OPEN "", "INV.ADJ.HIST" TO INV.ADJ.HIST ELSE STOP "CANNOT OPEN INV.ADJ.HIST FILE"
OPEN "", "INV.REC.ADJ" TO INV.REC.ADJ ELSE STOP "CANNOT OPEN INV.REC.ADJ FILE"
OPEN "", "CONTROL" TO CONTROL ELSE STOP "CANNOT OPEN CONTROL FILE"
OPEN "", "COMPANY" TO COMPANY ELSE STOP "CANNOT OPEN COMPANY FILE"
OPEN "", "WAREHOUSE" TO WAREHOUSE ELSE STOP "CANNOT OPEN WAREHOUSE FILE"
OPEN "", "INV.WHSE.OLD" TO INV.WHSE.OLD ELSE STOP "CANNOT OPEN INV.WHSE.OLD FILE"
OPEN "", "INV_RECEIPTS" TO INV_RECEIPTS ELSE STOP "CANNOT OPEN INV_RECEIPTS FILE"
OPEN "", "INV.WHSE" TO INV.WHSE ELSE STOP "CANNOT OPEN INV.WHSE FILE"
OPEN '','ITH.INAH.XREF' TO ITH.INAH.XREF ELSE STOP 'CANNOT OPEN ITH.INAH.XREF'
OPEN '','INV.COST.ADJ' TO INV.COST.ADJ ELSE STOP 'CANNOT OPEN INV.COST.ADJ'
OPEN '','INVENTORY' TO INVENTORY ELSE STOP 'CANNOT OPEN INVENTORY'
*
*   INITIALIZATION SECTION
*
*
PRINT @(-1)
PRINT @(1,5):'SELECTING INV.TRAN.HIST':@(-4)
UDTEXECUTE 'SSELECT INV.TRAN.HIST BY DATE WITH M.LINE#"FNGD"' CAPTURING GARBAGE
PRINT @(1,5):'CONVERTING INV.TRAN.HIST':@(-4)
*
DONE = 0 ; CNT=0
LOOP
  READNEXT ITH.ID ELSE DONE = 1
UNTIL DONE DO
  CNT+=1                   
  IF REM(CNT,100)=0 THEN   
    PRINT @(45,5):CNT:@(-4)
  END                      
  MATREAD ITH.REC FROM INV.TRAN.HIST, ITH.ID THEN
    CONO = ITH.ID[1,3]
    MATREAD COMP.REC FROM COMPANY, CONO  THEN 
      READ APD.REC FROM CONTROL, CONO:"AUDIT.PERIOD.DATES" THEN
        X=DCOUNT(APD.REC<1>, @VM)   
        PYR = APD.REC<1,X>[1,4]     
        PNO = APD.REC<1,X>[5,2]     
        PNO = PNO + 1 "R%2"         
        IF PNO > CO.LAST.FISCAL THEN
          PYR = PYR + 1             
          PNO = "01"                
        END                         
        APD.REC<1,X+1> = PYR:PNO  ;*
        IWH.ID = OCONV(ITH.ID,"G!2")
        DT = OCONV(ITH.ID,"G2!1")
        MATREAD R11.IWH.REC FROM INV.WHSE.OLD,IWH.ID THEN
          IF ITH.TYPE<1,1> = "B" THEN 
            GOSUB SET.BEG.BAL
          END ELSE
            GOSUB PROCESS.HIST
          END
        END
      END
    END
  END ELSE
    RELEASE
  END
REPEAT
GOTO 99999
*
*********************
PROCESS.HIST: 
*********************
*
INAH.XREF=''
TCNT = DCOUNT(ITH.TYPE<1>,@VM)
FOR T = 1 TO TCNT
  ;*
  ;* update with INV.TRAN.HIST data
  ;*
  MAT INAH.REC = ""
  INAH.TYPE = ITH.TYPE<1,T>
  INAH.PROD = FIELD(ITH.ID,"!",1)[4,99]
  INV.ID=CONO:INAH.PROD
  MATREAD INV.REC FROM INVENTORY,INV.ID ELSE MAT INV.REC=''
*COPY>ICSBP>INV.UM.CNV
  INAH.WHSE = FIELD(ITH.ID,"!",2)
  ;*
  MATREAD WHSE.REC FROM WAREHOUSE, CONO:INAH.WHSE ELSE MAT WHSE.REC=''
  IF WHS.DIV = "" THEN WHS.DIV = "00"
  DIV.CODE=WHS.DIV
  INAH.DV.DP.CC = WHS.DIV:"00":"000"
  INAH.DATE = FIELD(ITH.ID,"!",3)
  INAH.LOC = ITH.LOC<1,T>
  INAH.SERIAL = ITH.R.S<1,T>
  INAH.JOB = ITH.JOB<1,T>
  INAH.QTY = ITH.QTY<1,T>
  ROND=''; LN=''
  INAH.CUR.STK.QTY=CALC.STK.QTY(INAH.QTY,MAT INV.CNV.REC,ROND,LN)
  INAH.UNIT.COST = ITH.UN.COST<1,T>
  INAH.EXT.COST = ITH.COST<1,T>
  INAH.TRAN = ITH.TRAN<1,T>
  INAH.OPER.ID = "CONV"
  INAH.DESC = "CONV"
  ;*
  ;* get the period
  ;*
  LOCATE INAH.DATE IN APD.REC<2> BY "AR" SETTING PLOC ELSE
    NULL
  END
  PYR = APD.REC<1,PLOC>[1,4]   
  PNO = APD.REC<1,PLOC>[5,2]   
  CHECK.PNO = PNO + 1 "R%2"          
  IF CHECK.PNO > CO.LAST.FISCAL THEN 
    PYR = PYR + 1              
    PNO = "01"                 
  END                          
  EPERIOD = PYR:PNO            
  ;* check if EPERIOD is greater then                  
  ;* current period, if so then EPERIOD=current period 
  DIV.POS=DIVISION.POSITION(CONO,CONTROL,DIV.CODE)     
  IF DIV.POS<1,1>='' THEN                              
    DIV.POS=DIV.POS<1,2>                               
  END ELSE                                             
    STOP 'PROBLEM WITH DIVISOIN POSITION'              
  END                                                  
  CURR.PERIOD=CURRENT.PERIOD(CONO,CONTROL,DIV.POS,'IC')
  IF CURR.PERIOD<1,1>='' THEN                          
    CURR.PERIOD=CURR.PERIOD<1,2>                       
  END ELSE                                             
    STOP 'PROBLEM WITH FISCAL PERIOD'                  
  END                                                  
  IF EPERIOD>CURR.PERIOD THEN EPERIOD=CURR.PERIOD      
  INAH.PERIOD = EPERIOD
  ;*
  ;* set the source of transaction
  ;*
  BEGIN CASE
    CASE INAH.TYPE = "I" OR INAH.TYPE = "O"
      INAH.SRC = "IT"
    CASE INAH.TYPE = "S"
      IF INAH.QTY<0 THEN
        INAH.SRC = "U"
      END ELSE
        INAH.SRC='UR'
      END
    CASE INAH.TYPE = "R"
      INAH.SRC = "IR"
      MATREAD IWH.REC FROM INV.WHSE,IWH.ID THEN
        SQCNT = DCOUNT(IWH.RECP.ENT.DATE,VM)
        ITH.DATE = FIELD(ITH.ID,"!",3)
        EOL=0
        FOR S = 1 TO SQCNT UNTIL EOL=1
          IF IWH.RECP.ENT.DATE<1,S> = ITH.DATE THEN
            MATREAD INVR.REC  FROM INV_RECEIPTS,CONO:IWH.RECP.NO<1,S> THEN
              IF ITH.TRAN<1,T> = INVR.PO THEN
                INAH.RECP.NO = IWH.RECP.NO<1,S>
                INAH.UNIT.COST = INVR.UNIT.COST
*                INAH.QTY  = INVR.ORG.QTY
                INAH.NEW.QTY = INAH.CUR.STK.QTY
                EOL=1
              END
            END
          END
        NEXT S
      END
    CASE INAH.TYPE = "A" AND ITH.JOB<1,T>[1,1] = "S"
      INAH.SRC = "IS"
    CASE INAH.TYPE = "A"
      INAH.SRC = "IA"
  END CASE
  ;*
  ;* update with INV.ADJ.HIST data
  ;*
  MATREAD IAH.REC FROM INV.ADJ.HIST, CONO:INAH.TRAN THEN
    IF INAH.EXT.COST = IAH.COST AND INAH.QTY = IAH.ADJ.QTY THEN
      IF IAH.MON THEN INAH.PERIOD = IAH.MON
      INAH.GLA.DATE = IAH.GLA.DATE
      INAH.SRC = "IR"
      INAH.ADJ.CODE = IAH.ADJ.CODE
      INAH.CUR.QTY = IAH.CUR.QTY
      INAH.NEW.QTY = IAH.NEW.QTY
      INAH.CUR.STK.QTY = IAH.CUR.SHEET
      INAH.NEW.STK.QTY = IAH.NEW.SHEET
      INAH.CUR.DIAM = IAH.CUR.DIAM
      INAH.NEW.DIAM = IAH.NEW.DIAM
    END
  END
  ;*
  ;* update with INV.REC.ADJ data
  ;*
  MATREAD IRA.REC FROM INV.REC.ADJ, CONO:INAH.TRAN THEN
    LOCATE ITH.R.S<1,T> IN IRA.R.S<1> SETTING SPOS THEN
      BEGIN CASE
        CASE INAH.QTY#IRA.QTY<1,SPOS>
          NULL
        CASE INAH.LOC#IRA.LOC<1,SPOS>
          NULL
        CASE 1
          INAH.CUR.QTY = IRA.ORG.QTY<1,SPOS>
          INAH.NEW.QTY = IRA.QTY<1,SPOS>
          INAH.CUR.STK.QTY = IRA.ORG.SHT<1,SPOS>
          INAH.NEW.STK.QTY = IRA.SHT<1,SPOS>
          INAH.CUR.DIAM = IRA.ORG.DIAM<1,SPOS>
          INAH.NEW.DIAM = IRA.DIAM<1,SPOS>
      END CASE
    END
  END
  ;* INV.COST.ADJ data
  MATREAD ICA.REC FROM INV.COST.ADJ,CONO:ITH.TRAN<1,T> THEN
  END
  ;*
  ;* get the sequence number
  ;*
  INAH.SEQ=GET.INAH.SEQ(CONO,CONTROL,INV_AUDIT_HIST)
  INAH.ID=CONO:INAH.SEQ
  INAH.XREF<1,-1>=INAH.ID
  ;*
  MATWRITE INAH.REC ON INV_AUDIT_HIST, INAH.ID
  GOSUB UPDATE.AUDIT.BAL
1888 *
NEXT T
WRITE INAH.XREF ON ITH.INAH.XREF,ITH.ID
RETURN
*
*
*****************
SET.BEG.BAL: 
*****************
*
MAT ITB.REC=""
ITB.BEG = ITH.QTY
ITB.BEG.AMT = ITH.COST
TMP=OCONV(ITH.ID,"G2!1")
LOCATE TMP IN APD.REC<2> BY "AR" SETTING PLOC ELSE
  NULL                                                            
END                                                               
PYR = APD.REC<1,PLOC>[1,4]                                        
PNO = APD.REC<1,PLOC>[5,2]                                        
CHECK.PNO = PNO + 1 "R%2"                                               
IF CHECK.PNO > CO.LAST.FISCAL THEN                                      
  PYR = PYR + 1                                                   
  PNO = "01"                                                      
END                                                               
EPERIOD = PYR:PNO                                                 
ITB.ID=OCONV(ITH.ID,"G!2"):"!":EPERIOD
MATWRITE ITB.REC ON INV_AUDIT_BAL, ITB.ID                        
RETURN
*
*******************
UPDATE.AUDIT.BAL: 
*******************
*
ITB.ID = CONO:INAH.PROD:"!":INAH.WHSE:"!":INAH.PERIOD
MATREAD ITB.REC FROM INV_AUDIT_BAL,ITB.ID ELSE MAT ITB.REC=""
BEGIN CASE                       
  CASE INAH.TYPE = "R"           
    ITB.RECV += INAH.QTY         
    ITB.RECV.AMT += INAH.EXT.COST    
  CASE INAH.TYPE = "S"           
    ITB.SALE += INAH.QTY         
    ITB.SALE.AMT += INAH.EXT.COST    
  CASE INAH.TYPE = "A"           
    ITB.SHRNK += INAH.QTY        
    ITB.SHRNK.AMT += INAH.EXT.COST   
  CASE INAH.TYPE = "I"           
    ITB.TRAN.IN += INAH.QTY      
    ITB.TRAN.IN.AMT += INAH.EXT.COST 
  CASE INAH.TYPE = "O"           
    ITB.TRAN.OUT += INAH.QTY     
    ITB.TRAN.OUT.AMT += INAH.EXT.COST
END CASE                         
MATWRITE ITB.REC ON INV_AUDIT_BAL,ITB.ID
RETURN
*
99999 
END
