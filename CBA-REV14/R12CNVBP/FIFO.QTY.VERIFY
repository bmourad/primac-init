OPEN '','INV.WHSE' TO INV.WHSE ELSE STOP 201
OPEN '','INV.WHSE.OLD' TO INV.WHSE.OLD ELSE STOP 201
OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE STOP 201
OPEN '','INV_RECP_WHSE' TO INV_RECP_WHSE ELSE STOP 201
OPEN '','CONVERSION.ERRORS' TO CONVERSION.ERRORS ELSE STOP 201
OPEN '','INVENTORY' TO INVENTORY ELSE STOP 201
OPEN '','CATEGORY' TO CATEGORY ELSE STOP 201
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.WHSE.REV11
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>CHAR
*
ERR=""
RECP.ARR=""
*
PRINT @(-1)
PRINT @(1,5):'SELECTING INV.WHSE FILE RECORDS':@(-4)
CMD='SELECT INV.WHSE'
UDTEXECUTE CMD
*
PRINT @(1,5):'VERIFYING INV.WHSE RECORD':@(-4)
DONE=0; ERR='';ERR.CNT=0
CNT=0
LOOP
  READNEXT IWH.ID ELSE DONE=1
  CNT+=1
  IF REM(CNT,100)=0 THEN
    PRINT @(45,5):CNT:@(-4)
  END
  CONO=IWH.ID[1,3]
  WHSE=OCONV(IWH.ID,"G1!1")
  ORG.QTY=0
  CUR.QTY=0
  RSV.QTY=0
  CUR.COST=0
  ERRMSG=''
UNTIL (DONE) DO
  MATREAD IWH.REC FROM INV.WHSE,IWH.ID THEN
    MATREAD R11.IWH.REC FROM INV.WHSE.OLD,IWH.ID THEN
      RECP.CNT=DCOUNT(IWH.RECP.NO,VM)
      FOR R=1 TO RECP.CNT
        IRW.ID=CONO:IWH.RECP.NO<1,R>:"!":WHSE
        MATREAD IRW.REC FROM INV_RECP_WHSE,IRW.ID THEN
          ORG.QTY +=IRW.ORG.QTY+0
          CUR.QTY +=IRW.CUR.QTY+0
          RSV.QTY +=IRW.RSVB.QTY+0
          CUR.COST+=IRW.CUR.QTY*IRW.UNIT.COST
        END
      NEXT R
      TOT.COST=0
      RCNT=DCOUNT(R11.IWH.QTY.FI<1>,@VM)
      FOR RR=1 TO RCNT
        TOT.COST +=(R11.IWH.QTY.FI<1,RR>*R11.IWH.COST.FI<1,RR>)
      NEXT RR
      IF TOT.COST#CUR.COST THEN
        ERRMSG<-1>="Original total fifo cost=":TOT.COST:" Current total fifo cost=":CUR.COST
      END
      INV.ID = OCONV(IWH.ID,"G!1") 
      IF ORG.QTY # SUM(R11.IWH.ORG.FI<1>) THEN
        ERRMSG<-1>='Original fifo IRW.ORG.FI=':SUM(R11.IWH.ORG.FI<1>):' Current fifo IWH.ORG.FI=':ORG.QTY
      END
      IF CUR.QTY # SUM(R11.IWH.QTY.FI<1>) THEN
        ERRMSG<-1>='Original fifo IRW.QTY.FI=':SUM(R11.IWH.QTY.FI<1>):' Current fifo IWH.QTY.FI=':ORG.QTY
      END
      IF RSV.QTY # SUM(R11.IWH.RSV.FI<1>) THEN
        ERRMSG<-1>='Original fifo IRW.RSV.FI=':SUM(R11.IWH.RSV.FI<1>):' Current fifo IWH.RSV.FI=':RSV.QTY
      END
    END
  END
  IF ERRMSG THEN
    ERR.CNT+=1
    ERR<ERR.CNT>=ERRMSG
    ERR=INSERT(ERR,ERR.CNT,1,0,IWH.ID)
  END
REPEAT
WRITE ERR ON CONVERSION.ERRORS,'FIFO.QTY.VERIFY'
CALL CONV.RPT("FIFO.QTY.VERIFY")
END
