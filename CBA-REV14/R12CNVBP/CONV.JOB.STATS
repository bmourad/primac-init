*
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - R12CNVBP
* PROGRAM     - CONV.JOB.STATS
* BY          - CHRIS MYKLEBUST ; PRIMAC SYSTEMS
* DATE        - 05/06/2002
* DESCRIPTION -
*
*T25755 cmykleb 05/23/2002 * Modify job stats file to include the new seq
*                            number associated with each po detail line.
*ENDDOC
**********************************************
*   CPYLIBS
*COPY>CPYLIB>CHAR
*COPY>JCS.CPYLIB>JOB.STATS
*COPY>POS.CPYLIB>OUTSIDE.PO
*
*   FILE OPENS
   OPEN "JOB.STATS" TO JOB.STATS ELSE 
      ERRMSG="Unable to open JOB.STATS file."
      GOSUB 91000
      STOP
   END
   OPEN "OUTSIDE.PO" TO OUTSIDE.PO ELSE 
      ERRMSG="Unable to open OUTSIDE.PO file."
      GOSUB 91000
      STOP
   END
*
*   INITIALIZATION SECTION
*
   DONE=0
   CTR=0
   VERB = "COUNT JOB.STATS"
   UDTEXECUTE VERB CAPTURING JUNK
   NUMITEMS = FIELD(JUNK<3>," ",1)
   NEWMOD = NUMITEMS / 10
   VERB = "CREATE.FILE HOLD_JOB.STATS ":NEWMOD
   UDTEXECUTE VERB
   OPEN "HOLD_JOB.STATS" TO HOLD_JOB.STATS ELSE
      ERRMSG="UNABLE TO OPEN HOLD_JOB.STATS FILE"
      GOSUB 91000
      STOP
   END
*
   SELECT JOB.STATS
   DONE=0
   CTR=0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      READ REC FROM HOLD_JOB.STATS,ID ELSE
         MATREAD JSTAT.REC FROM JOB.STATS,ID THEN
            GOSUB PROCESS.JOB.STAT
         END
         CTR+=1
         IF REM(CTR,100)=0 THEN PRINT CTR
      END
   REPEAT
   STOP
*
PROCESS.JOB.STAT: 
*
   MATWRITE JSTAT.REC ON HOLD_JOB.STATS,ID
   NUM.LINES = DCOUNT(JSTAT.OPO.NO,@VM)
   FOR II = 1 TO NUM.LINES
      OPO.KEY = ID[1,3]:JSTAT.OPO.NO<1,II>
      MATREAD OPO.REC FROM OUTSIDE.PO,OPO.KEY THEN
         NUM.JOB=DCOUNT(OPO.JOB.NO,@VM)
         FOUNDIT = 0
         CURRJOB = ID[4,99]
         FOR KK = 1 TO NUM.JOB WHILE NOT(FOUNDIT)
            IF OPO.JOB.NO<1,KK> = CURRJOB AND OPO.PROD.LINE<1,KK> = JSTAT.PROD.LINE<1,II> THEN
               FOUNDIT = 1
               JSTAT.PROD.LINE<1,II> = OPO.PROD.LINE<1,KK>:"@":OPO.PROD.SEQ<1,KK>
            END 
         NEXT KK
      END
   NEXT II
   MATWRITE JSTAT.REC ON JOB.STATS,ID
   RETURN
*
*   STANDARD SUBROUTINES
*
91000 PRINT @(0,23):ERRMSG:CL:
   INPUT XX:
   PRINT @(0,23):CL:
   RETURN
99999 RETURN
END
