*
*   > COMMON STATEMENTS
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - R12CNVBP
* PROGRAM     - CONV.VEND.FILES
* BY          - CHRIS MYKLEBUST - PRIMAC SYSTEMS
* DATE        - 05/06/2002
* DESCRIPTION -
*
*T25755 cmykleb 05/06/2002 * Pgm to convert the VEND.PO.STATS and
*                            VEND.PROD.STATS to match them to the new
*                            format for the seq number associated with each
*                            line. The new field should be separated with
*                            the whse field or the PO.WHSE field by
*                            an at sign "@" and the seq number.
*T28779 lross 02/09/2006 * Retain identity of multiple same Prod/Whse
*                          lines.
*                          This pgm was copied from CONV.VEND.FILES.
*                          Logic from CONV.VEND.VOUCH added in here since
*                          pointers are readily available.
*ENDDOC
**********************************************
*   CPYLIBS
*COPY>PMC.CPYLIB>PO
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>CPYLIB>CHAR
*
*   FILE OPENS
   OPEN "VEND.PO.STATS" TO VEND.PO.STATS ELSE
      ERRMSG = "NO VEND.PO.STATS FILE"
      GOSUB 91000
      STOP
   END
   OPEN "VEND.VOUCH.STATS" TO VEND.VOUCH.STATS ELSE
      ERRMSG = "NO VEND.VOUCH.STATS FILE"
      GOSUB 91000
      STOP
   END
   OPEN "VEND.PROD.STATS" TO VEND.PROD.STATS ELSE
      ERRMSG = "NO VEND.PROD.STATS FILE"
      GOSUB 91000
      STOP
   END
   OPEN "PO" TO PO ELSE
      ERRMSG = "NO PO FILE"
      GOSUB 91000
      STOP
   END
*
*   INITIALIZATION SECTION
*
   DONE=0
   CTR=0
   VERB = "COUNT VEND.PROD.STATS"
   UDTEXECUTE VERB CAPTURING JUNK
   NUMITEMS = FIELD(JUNK<3>," ",1)
   NEWMOD = NUMITEMS / 10
   VERB = "CREATE.FILE HOLD_VEND.PROD.STATS_R ":NEWMOD
   UDTEXECUTE VERB
   OPEN "HOLD_VEND.PROD.STATS_R" TO HOLD_VEND.PROD.STATS_R ELSE
      ERRMSG="UNABLE TO OPEN HOLD_VEND.PROD.STATS_R FILE"
      GOSUB 91000
      STOP
   END
   VERB = "COUNT VEND.PO.STATS"
   UDTEXECUTE VERB CAPTURING JUNK
   NUMITEMS = FIELD(JUNK<3>," ",1)
   NEWMOD = NUMITEMS / 10
   VERB = "CREATE.FILE HOLD_VEND.PO.STATS_R ":NEWMOD
   UDTEXECUTE VERB
   OPEN "HOLD_VEND.PO.STATS_R" TO HOLD_VEND.PO.STATS_R ELSE
      ERRMSG="UNABLE TO OPEN HOLD_VEND.PO.STATS_R FILE"
      GOSUB 91000
      STOP
   END
   VERB = "COUNT VEND.VOUCH.STATS"
   UDTEXECUTE VERB CAPTURING JUNK
   NUMITEMS = FIELD(JUNK<3>," ",1)
   NEWMOD = NUMITEMS / 10
   VERB = "CREATE.FILE HOLD_VEND.VOUCH.STATS_R ":NEWMOD
   UDTEXECUTE VERB
   OPEN "HOLD_VEND.VOUCH.STATS_R" TO HOLD_VEND.VOUCH.STATS_R ELSE
      ERRMSG="UNABLE TO OPEN HOLD_VEND.VOUCH.STATS_R FILE"
      GOSUB 91000
      STOP
   END
*
*
*   BODY OF PROGRAM SECTION
*
   DONE=0
   SELECT PO
   ERRMSG=""
   LOOP
      READNEXT ID ELSE DONE=1
   UNTIL DONE DO
      MATREAD PO.REC FROM PO,ID THEN
         NUM.PROD=DCOUNT(PO.PROD.NUM,@VM)
         IS.BLANK=0
         FOR II = 1 TO NUM.PROD WHILE NOT(IS.BLANK)
            IF PO.PROD.SEQ<1,II> = "" THEN
               IS.BLANK=1
            END
         NEXT II
         IF IS.BLANK THEN
            MSG1 = "PO # ":ID[4,99]:" has seq # missing unable to process"
            IF ERRMSG = "" THEN
               ERRMSG = MSG1
            END ELSE
               ERRMSG := CHAR(10):MSG1
            END
         END
      END
   REPEAT
   IF ERRMSG # "" THEN
      ERRMSG := CHAR(10):"NO RECORDS PROCESSED"
      PRINT ERRMSG
      STOP
   END
   DONE=0
   SELECT PO
   ERRMSG=""
   ID.LIST=""
   LOOP
      READNEXT ID ELSE DONE=1
   UNTIL DONE DO
      MATREAD PO.REC FROM PO,ID THEN
         PO.NO = ID[4,8]
         NUM.PROD=DCOUNT(PO.PROD.NUM,@VM)
         KEY = ID[1,3]:PO.VEND.NO:"!R!":ID[4,99]
         READ XX FROM HOLD_VEND.PO.STATS_R,KEY ELSE
            MATREAD VPS.REC FROM VEND.PO.STATS,KEY THEN
               GOSUB PROCESS.VPS.REC
            END
         END
      END
   REPEAT
   IF ID.LIST # "" THEN
      PRINTER ON
      ID.LIST = "LIST OF POS THAT HAVE A VEND.PO.STATS PROBLEM":CHAR(10):ID.LIST
      PRINT ID.LIST
      PRINTER OFF
   END
   STOP
*
PROCESS.VPS.REC: 
*
   PTR=1; ALREADY.DONE=0
   FND.ATTR=""
   FOR II = 1 TO NUM.PROD
      PRODNO = PO.PROD.NUM<1,II>
      WHSENO = PO.WHSE<1,II>
      CURR.WHSE = PO.WHSE<1,II>:"@":PO.PROD.SEQ<1,II>
      IF PO.PROD.SEQ<1,II> # "" THEN
         ALREADY.DONE=0
         LOCATE PRODNO IN VPS.PROD<1>,PTR SETTING POS THEN
            CONTINUE.SEARCH=1
            FOR KK = POS TO NUM.PROD WHILE CONTINUE.SEARCH
               IF VPS.WHSE<1,II> = WHSENO OR VPS.WHSE<1,II> = CURR.WHSE THEN
                  CONTINUE.SEARCH=0
                  IF VPS.WHSE<1,II>= CURR.WHSE THEN ALREADY.DONE=1
                  VPS.WHSE<1,II>=CURR.WHSE
                  FND.ATTR<1,II>=1
               END
            NEXT KK
         END
         PTR = PTR+1
         IF NOT(ALREADY.DONE) THEN GOSUB PROCESS.VPDS.REC
      END
   NEXT II
*
   ERROR.FND=0

   FOR II = 1 TO NUM.PROD
      IF FND.ATTR<1,II> # 1 THEN
         ERROR.FND=1
      END
   NEXT II
*
   IF ERROR.FND THEN
      ERRMSG="THIS PO HAS PROBLEMS ":ID:" --->":FND.ATTR:" NUM.PROD=":NUM.PROD
      GOSUB 91000

      IF ID.LIST = "" THEN
         ID.LIST=ID
      END ELSE
         ID.LIST:=CHAR(10):ID
      END
   END ELSE
     IF NOT(ALREADY.DONE) THEN
        MATWRITE VPS.REC ON HOLD_VEND.PO.STATS_R,KEY
        MATWRITE VPS.REC ON VEND.PO.STATS,KEY
     END
   END
*
   RETURN
*
*
PROCESS.VPDS.REC: 
*
   VP.KEY = KEY:"!":PRODNO:"!":WHSENO
   NEW.KEY = KEY:"!":PRODNO:"!":CURR.WHSE
   ATSEQ = FIELD(CURR.WHSE,"@",2)
   READ XX FROM HOLD_VEND.PROD.STATS_R,VP.KEY ELSE
      MATREAD VPDS.REC FROM VEND.PROD.STATS,VP.KEY THEN
         MATWRITE VPDS.REC ON VEND.PROD.STATS,NEW.KEY
         MATWRITE VPDS.REC ON HOLD_VEND.PROD.STATS_R,VP.KEY
         DELETE VEND.PROD.STATS,VP.KEY
         FOR V = 1 TO DCOUNT(VPDS.VOU.NO,VM)
           MATREAD VVOS.REC FROM VEND.VOUCH.STATS,FIELD(KEY,"!",1):"!":VPDS.VOU.NO<1,V> THEN
             LOCATE PO.NO IN VVOS.PO.NO<1>,1 SETTING PVFND THEN
                PTTR=PVFND
                LOOP
                  LOCATE PRODNO IN VVOS.PROD<1>,PTTR SETTING PVPFND ELSE PVPFND = 0
                  BEGIN CASE
                  CASE PVPFND = 0
                  CASE WHSENO # VVOS.WHSE<1,PVPFND>
                    PTTR = PVPFND + 1
                  CASE VPDS.QTY<1,V> # VVOS.VOU.QTY<1,PVPFND>
                    PTTR = PVPFND + 1
                  CASE VPDS.UN.COST<1,V> # VVOS.VOU.UN.COST<1,PVPFND>
                    PTTR = PVPFND + 1
                  CASE VVOS.PO.SEQ<1,PVPFND> = ''
                    VVOS.PO.SEQ<1,PVPFND> = ATSEQ
                    PTTR = 0
                  END CASE
                WHILE PTTR AND PVPFND DO
                REPEAT
             END
             IF PVFND AND PVPFND THEN
               MATWRITE VVOS.REC ON VEND.VOUCH.STATS,FIELD(KEY,"!",1):"!":VPDS.VOU.NO<1,V>
             END
           END
         NEXT V
      END
   END
*
   RETURN
*
*
*   STANDARD SUBROUTINES
*
91000 PRINT @(0,23):ERRMSG:CL:
   INPUT XX:
   PRINT @(0,23):CL:
   RETURN
99999 RETURN
END
