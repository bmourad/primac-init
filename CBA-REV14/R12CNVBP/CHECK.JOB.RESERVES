OPEN "","JOB" TO JOB ELSE STOP 201
OPEN "","INV.JOB.STATS.OLD" TO INV.JOB.STATS.OLD ELSE STOP 201
OPEN "","CONVERSION.ERRORS" TO CONVERSION.ERRORS ELSE STOP 201
*
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>JCS.CPYLIB>JOB
*
;*
;* verify if reserves are ok
;*
SELECT INV.JOB.STATS.OLD
DONE=0 ; ERR.CNT=0; ERR=''
LOOP
  READNEXT IJS.ID ELSE DONE=1
UNTIL DONE DO
  ERRMSG=''
  MATREAD INV.JS.REC FROM INV.JOB.STATS.OLD,IJS.ID THEN
    IF IJS.JOB.QTY+0#SUM(IJS.FI.QTY<1>)+0 THEN
      ERRMSG="IJS.JOB.QTY=":IJS.JOB.QTY:" and SUM(IJS.FI.QTY)=":SUM(IJS.FI.QTY<1>)
    END
    CONO=IJS.ID[1,3]
    PROD=OCONV(IJS.ID,"G!1")[4,99]
    WHSE=OCONV(IJS.ID,"G1!1")
    JOB.NO=OCONV(IJS.ID,"G2!1")
    JOB.ID=CONO:JOB.NO
    MATREAD JOB.REC FROM JOB,JOB.ID THEN
      FOUND=0;EOL=0;BEG.POS=1
      LOOP
        LOCATE PROD IN JOB.RESV.MATL<1>,BEG.POS SETTING PPOS THEN
          IF JOB.RESV.WHSE<1,PPOS>=WHSE THEN
            FOUND=1 ;EOL=1
            IF IJS.JOB.QTY+0#JOB.RESV.QTY<1,PPOS>+0 THEN
              ERRMSG<-1>='There is a mismatch between JOB reserves and INV.JOB.STATS reserves '
            END
          END ELSE
*lross      BEG.POS+=1
            BEG.POS=PPOS+1
          END
        END ELSE
          EOL=1
          ERRMSG<-1>='Product/Whse combination ':PROD:'/':WHSE:' could not be found on the job '
        END
      UNTIL EOL DO REPEAT
    END ELSE
      ERRMSG<-1>='Job is missing on the JOB file'
    END
    IF (ERRMSG) THEN
      ERR.CNT+=1
      ERR<ERR.CNT>=ERRMSG
      ERR=INSERT(ERR,ERR.CNT,1,0,IJS.ID)
    END
  END
REPEAT
WRITE ERR ON CONVERSION.ERRORS,"CHECK.JOB.RESERVES"
CALL CONV.RPT("CHECK.JOB.RESERVES")
END
