;* this report will give us all INV.WHSE records for which
;* sum of all IRW.ONAND quantities does not match IWH.ON.HAND qty.
;* mismatch is due to missing serials, serials for which info could 
;* not be found in conversion process.
;*
OPEN '','INV.WHSE' TO INV.WHSE ELSE STOP 201
OPEN '','INV_RECP_WHSE' TO INV_RECP_WHSE ELSE STOP 201
OPEN '','CONVERSION.ERRORS' TO CONVERSION.ERRORS ELSE STOP 201
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*
PRINT @(-1)
PRINT @(1,5):'SELECTING INV.WHSE':@(-4)
SELECT INV.WHSE
PRINT @(1,5):'VERIFYING IWH.ONHAND VS. IRW.ONHAND':@(-4) 
*
DONE=0; ERR=''
CNT=0
LOOP
  READNEXT IWH.ID ELSE DONE=1
UNTIL DONE DO
  CNT+=1                   
  IF REM(CNT,100)=0 THEN   
    PRINT @(45,5):CNT:@(-4)
  END                      
  TOT.ONHAND=0
  MATREAD IWH.REC FROM INV.WHSE,IWH.ID THEN
    RCNT=DCOUNT(IWH.RECP.NO,@VM)
    FOR RR=1 TO RCNT
      CONO=IWH.ID[1,3]
      WHSE=OCONV(IWH.ID,"G1!1")
      IRW.ID = CONO:IWH.RECP.NO<1,RR>:"!":WHSE
      MATREAD IRW.REC FROM INV_RECP_WHSE,IRW.ID THEN
        TOT.ONHAND+=IRW.ON.HAND
      END
    NEXT RR
    IF TOT.ONHAND # IWH.ON.HAND THEN
      ERR<-1>=IWH.ID:@VM:"SUM(IRW.ON.HAND)=":TOT.ONHAND:" AND IWH.ON.HAND=":IWH.ON.HAND
    END
  END
REPEAT
WRITE ERR ON CONVERSION.ERRORS,"IWH.IRW.ONHAND.VERIFY"
CALL CONV.RPT("IWH.IRW.ONHAND.VERIFY")
