OPEN '','INV.JOB.STATS' TO INV.JOB.STATS ELSE STOP 201
OPEN '','INV.JOB.STATS.OLD' TO INV.JOB.STATS.OLD ELSE STOP 201
OPEN '','JOB.MATL' TO JOB.MATL ELSE STOP 201
OPEN '','JOB.MATL.OLD' TO JOB.MATL.OLD ELSE STOP 201
OPEN '','INV.WHSE' TO INV.WHSE ELSE STOP 201
*
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>ICS.CPYLIB>INV.WHSE
;*
;* replace fifo number with receipt number in INV.JOB.STATS
;*
PRINT @(-1)
PRINT @(1,5):'SELECTING INV.JOB.STATS.OLD':@(-4)
SELECT INV.JOB.STATS.OLD
PRINT @(45,5):'REPLACING FIFO POINTER WITH RECEIPT NUMBER':@(-4)
DONE=0 ; CNT=0
LOOP
  READNEXT ID ELSE DONE=1
UNTIL DONE DO
  CNT+=1                   
  IF REM(CNT,100)=0 THEN   
    PRINT @(45,5):CNT:@(-4)
  END                      
  MATREAD INV.JS.REC FROM INV.JOB.STATS.OLD,ID THEN
    IWH.ID=OCONV(ID,"G!2")
    MATREAD IWH.REC FROM INV.WHSE,IWH.ID THEN
      FICNT=DCOUNT(IJS.FI.NO,@VM)
      RECPS=''
      FOR FF=1 TO FICNT
        RPOS=IJS.FI.NO<1,FF>
        RECPS<1,-1>=IWH.RECP.NO<1,RPOS>
      NEXT FF
      IJS.RECP.NO<1>=RECPS
      MATWRITE INV.JS.REC ON INV.JOB.STATS,ID
    END
  END
REPEAT
;*
;* replace fifo number with receipt # in JOB.MATL
;* 
PRINT @(1,5):'SELECTING JOB.MATL':@(-4)
SELECT JOB.MATL.OLD
PRINT @(1,5):'REPLACING FIFO POINTER WITH RECEIPT NUMBER':@(-4)
DONE=0 ; CNT=0
LOOP
  READNEXT ID ELSE DONE=1
UNTIL DONE DO
  CNT+=1                   
  IF REM(CNT,100)=0 THEN   
    PRINT @(45,5):CNT:@(-4)
  END                      
  MATREAD JMT.REC FROM JOB.MATL.OLD,ID THEN
    FICNT=DCOUNT(JMT.PTR<1,1>,@SVM)
    IWH.ID=ID[1,3]:OCONV(ID,"G3!2")
    MATREAD IWH.REC FROM INV.WHSE,IWH.ID THEN
      NEWPTR=''
      FOR FF=1 TO FICNT
        RPOS=JMT.PTR<1,1,FF>
        NEWPTR<1,1,FF>=IWH.RECP.NO<1,RPOS>
        NEWPTR<1,2,FF>=JMT.PTR<1,2,FF>
        NEWPTR<1,3,FF>=JMT.PTR<1,3,FF>
        NEWPTR<1,4,FF>=JMT.PTR<1,4,FF>
      NEXT FF
      JMT.PTR=NEWPTR
      MATWRITE JMT.REC ON JOB.MATL,ID
    END ELSE
;* need these even though INV.WHSE is not there. See CSF 40483
      MATWRITE JMT.REC ON JOB.MATL,ID
    END
  END
REPEAT
