*
* THIS PROGRAM IS A FIRST PART OF THE CONVERSION OF THE INV.WHSE.LOC FILE
* FOR ROLL/SKID ITEMS. IT MOVES ROLL.SKID.INFO RECORDS IN 
* ROLL.SKID.INFO.TEMP FILE AND BUILDS A CROSS REFERENCE RS.PO.XREF
* FROM INV.TRAN.HIST AND JOB.MATL FILE
*
*COPY>ICS.CPYLIB>ROLL.SKID.INFO
*COPY>ICS.CPYLIB>INV.TRAN.HIST
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>CPYLIB>CHAR
*
OPEN '','ROLL.SKID.INFO' TO ROLL.SKID.INFO ELSE STOP 201
OPEN '','ROLL.SKID.INFO.TEMP' TO ROLL.SKID.INFO.TEMP ELSE STOP 201
OPEN '','RS.PO.XREF' TO RS.PO.XREF ELSE STOP 201
OPEN '','INV.TRAN.HIST' TO INV.TRAN.HIST ELSE STOP 201
OPEN '','JOB.MATL.OLD' TO JOB.MATL.OLD ELSE STOP 201
OPEN '','INV.WHSE' TO INV.WHSE ELSE STOP 201
OPEN '','INV.WHSE.OLD' TO INV.WHSE.OLD ELSE STOP 201
*
;* move all RSKI records to temporary file
;*
PRINT @(-1)
PRINT @(1,5): 'COPYING FROM ROLL.SKID.INFO TO ROLL.SKID.INFO.TEMP':@(-4)
CMD='COPY FROM ROLL.SKID.INFO TO ROLL.SKID.INFO.TEMP ALL OVERWRITING'
UDTEXECUTE CMD CAPTURING MSG
;*
;* create RS.PO.XREF
;* this is so we can determine under what po
;* non-barcoded roll was received for the ones that we have RSKI.REC
;*
PRINT @(1,5):'SELECTING INV.TRAN.HIST FILE RECORDS':@(-4)
CMD = 'SELECT INV.TRAN.HIST WITH ITH.TYPE = "R" AND WITH ITH.R.S#""'
UDTEXECUTE CMD  CAPTURING MSG
PRINT @(1,5):'BUILDING RS.PO.XREF':@(-4)
DONE=0 ; CNT=0
LOOP
  READNEXT ITH.ID ELSE DONE=1
UNTIL (DONE) DO 
  CNT+=1
  IF REM(CNT,100)=0 THEN   
    PRINT @(45,5):CNT:@(-4)
  END                      
  MATREAD ITH.REC FROM INV.TRAN.HIST,ITH.ID THEN
    CONO = ITH.ID[1,3]
    RS.CNT = DCOUNT(ITH.R.S,VM)
    FOR RS=1 TO RS.CNT
      REC=""
      IF ITH.TYPE<1,RS> = "R" THEN
        RSPO.ID = CONO:"!":ITH.R.S<1,RS>
        REC<1> = ITH.TRAN<1,RS> ;* PO
        REC<2> = ITH.UN.COST<1,RS> ;* UNIT COST
        WRITE REC ON RS.PO.XREF,RSPO.ID
      END
    NEXT RS
  END
REPEAT
*
PRINT @(1,5):'SELECTING JOB.MATL.OLD':@(-4)
CMD = 'SSELECT JOB.MATL.OLD WITH JMT.RS.ID # "" BY JMT_DATE BY JMT_SEQ'
UDTEXECUTE CMD  CAPTURING MSG
PRINT @(1,5):'BUILDING RS.PO.XREF':@(-4)
DONE=0 ; CNT=0
LOOP
  READNEXT JMT.ID ELSE DONE=1
UNTIL (DONE) DO 
  CNT+=1
  IF REM(CNT,500)=0 THEN   
    PRINT @(45,5):CNT:@(-4)
  END                      
  MATREAD JMT.REC FROM JOB.MATL.OLD,JMT.ID THEN
    CONO = JMT.ID[1,3]
    PROD = OCONV(JMT.ID,"G3!1")
    WHSE = OCONV(JMT.ID,"G4!1")
* Added by LMR 03/15/2006 v to avoid overwrite from receipt.
    RS.PO.ID = CONO:"!":JMT.R.S.ID
    READ DUMMY FROM RS.PO.XREF, RS.PO.ID ELSE
      IWH.ID = CONO:PROD:"!":WHSE
      MATREAD IWH.REC FROM INV.WHSE.OLD,IWH.ID THEN
        REC = ""
        FI = JMT.PTR<1,1,1>
        REC<1> = IWH.PO.NO.FI<1,FI>
        REC<2> = IWH.COST.FI<1,FI>
*       RS.PO.ID = CONO:"!":JMT.R.S.ID
        WRITE REC ON RS.PO.XREF,RS.PO.ID
      END
    END
* ^
  END
REPEAT
END
