*
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - R12CNVBP
* PROGRAM     - CONV.OUTSIDE.PO
* BY          - CHRIS MYKLEBUST - PRIMAC SYSTEMS
* DATE        - 05/06/2002
* DESCRIPTION -
*
*T25755 cmykleb 05/06/2002 * New pgm to conv Outside PO with new fields for
*                            seq number. This number will be to uniquely
*                            identify a line. This new field will be a seq
*                            number that is stored and at the MV1 level and
*                            one that is stored at the attr level for last
*                            seq # used. See OUTSIDE.PO.MAINT.SUB for more
*                            details
*ENDDOC
**********************************************
*   CPYLIBS
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>CPYLIB>CHAR
*
*   FILE OPENS
   OPEN "OUTSIDE.PO" TO OUTSIDE.PO ELSE
      ERRMSG="Unable to open outside.po"
      GOSUB 91000
      STOP
   END
*
*   INITIALIZATION SECTION
*
*   BODY OF PROGRAM SECTION
*
   VERB = "COUNT OUTSIDE.PO"
   UDTEXECUTE VERB CAPTURING JUNK
*
   NUMITEMS = FIELD(JUNK<3>," ",1)
   NUMFRAMES = INT(NUMITEMS / 5)
   VERB = "CREATE.FILE HOLD_OUTSIDE.PO ":NUMFRAMES
   UDTEXECUTE VERB
   OPEN "HOLD_OUTSIDE.PO" TO HOLD_OUTSIDE.PO ELSE
      ERRMSG = "UNABLE TO OPEN HOLD_OUTSIDE.PO"
      GOSUB 91000
      STOP
   END
*CLEARFILE HOLD_OUTSIDE.PO
   CTR=0
   SELECT OUTSIDE.PO
   DONE = 0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      MATREADU OPO.REC FROM OUTSIDE.PO,ID THEN
         READ REC FROM HOLD_OUTSIDE.PO,ID THEN
      *  ALREADY PROCESSED
         END ELSE
            MATWRITE OPO.REC ON HOLD_OUTSIDE.PO,ID
            GOSUB PROCESS.OPO
         END
      END
      CTR += 1
      IF REM(CTR,100) = 0 THEN PRINT CTR
   REPEAT
   STOP
*
*
PROCESS.OPO: 
*
   IF OPO.LAST.SEQ = "" THEN
      OPO.LAST.SEQ=0
      NUM.LINES = DCOUNT(OPO.JOB.NO,@VM)
      FOR II = 1 TO NUM.LINES
         OPO.LAST.SEQ += 1
         OPO.PROD.SEQ<1,II>=OPO.LAST.SEQ
      NEXT II
      MATWRITE OPO.REC ON OUTSIDE.PO,ID
   END
*
*
   RETURN
*
*   STANDARD SUBROUTINES
*
91000 PRINT @(0,23):ERRMSG:CL:
   INPUT XX:
   PRINT @(0,23):CL:
   RETURN
99999 RETURN
END
