********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - INVOICE.REGISTER.DRIVER
* SYSTEM        - ARSBP
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE          - 06/16/85
* DESCRIPTION   -
* This program creates a temporary file for Invoices/Manual Invoices
* to run for INVOICE.REGISTER program.
*T25301 cm 07/03/2000 * Change process to allow multiple users to run at
*                       the same time.
*T28772 lross 01/25/2006 * Allow report to show data only for Division
*                          chosen.
********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>ARS.CPYLIB>MANUAL.INVOICE
*COPY>ARS.CPYLIB>INV.REG.TEMP
*COPY>PMC.CPYLIB>CUSTOMER
DIM HOLD.MIV.REC(MIV.REC.SIZE) ;*T28772
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
     SYS.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
     PROCREAD XX ELSE
        ERRMSG="MUST BE RUN FROM A PROC"
        GOSUB 91000
        GOTO 99999
     END
     CONO = XX<1>
     FILE.FLAG = XX<4>
     FILE.NAME = XX<6> ; * T25301
     DIV.CODE = XX<5> ;*T28772
*
*---- OPEN FILES
*
*T25301 v
*    OPEN '','INV.REG.TEMP' TO INV.REG.TEMP ELSE ERRMSG='INV.REG.TEMP FILE MISSING';GOTO 93000
     OPEN '',FILE.NAME TO INV.REG.TEMP ELSE ERRMSG = FILE.NAME:' FILE IS MISSING';GOTO 93000
*T25301 ^
     OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
     OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
     OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
     BEGIN CASE
     CASE FILE.FLAG = "J" OR FILE.FLAG = "A"
        OPEN '','INVOICE' TO INVOICE ELSE ERRMSG='INVOICE FILE MISSING';GOTO 93000
        FILE.FLAG = "J"
        OPEN 'JOB' TO JOB ELSE ERRMSG='JOB FILE MISSING';GOTO 93000;*T28772
     CASE FILE.FLAG = "M"
        OPEN '','MANUAL.INVOICE' TO INVOICE ELSE ERRMSG='MANUAL.INVOICE FILE MISSING';GOTO 93000
     CASE FILE.FLAG = "O"
        OPEN '','ORDER.INVOICE' TO INVOICE ELSE ERRMSG='ORDER.INVOICE FILE MISSING';GOTO 93000
     CASE 1
        ERRMSG = "INVALID REQUEST PASSED FROM PROC TO PROGRAM"
        GOSUB 91000
        GOTO 99999
     END CASE
*
*---- MAIN PROCESSING
*
10*
     READNEXT MIV.KEY ELSE GOTO 99999
     MATREAD MIV.REC FROM INVOICE, MIV.KEY ELSE 
       GOTO 10
     END
     IRT.KEY=MIV.KEY:"*":FILE.FLAG
     MATREAD CUST.REC FROM CUSTOMER , MIV.KEY[1,3]:MIV.CUST.NO ELSE
        MAT CUST.REC = ""
     END
     IF CUST.NAME # "" AND CUST.NAME # MIV.CUST.NAME THEN
        MIV.CUST.NAME = CUST.NAME
        MIV.ADDR1 = CUST.ADDR1
        MIV.ADDR2 = CUST.ADDR2
        MIV.ADDR3 = CUST.ADDR3
        MIV.ADDR4 = CUST.ADDR4:" ":CUST.ZIP
     END
*T28772 v
     IF FILE.FLAG = 'J' AND DIV.CODE # 'ALL' THEN
        FOR J = DCOUNT(MIV.SLSMAN,VM) TO 1 STEP -1
          READV JDIV FROM JOB,CONO:MIV.SLSMAN<1,J>,6 ELSE JDIV = ''
          IF JDIV # DIV.CODE OR MIV.CHG.CAT<1,J>='SUB' OR MIV.CHG.CAT<1,J>='TOT' THEN
            DEL MIV.CHG.CAT<1,J>
            DEL MIV.CHG.CODE<1,J>
            DEL MIV.QUANTITY<1,J>
            DEL MIV.DESC<1,J>
            DEL MIV.TAX.JURS<1,J>
            DEL MIV.AMOUNT<1,J>
            DEL MIV.SLSMAN<1,J>
          END
        NEXT J
     END
*T28772 ^
     MAT IRT.REC = MAT MIV.REC
     MATWRITE IRT.REC ON INV.REG.TEMP,IRT.KEY
     GOTO 10
*
*---- CALLS FOR SYSCOM
*
91000*
     ERR.TYPE=1
     CALL SYSCOM(MAT SYSCOM.REC)
     RETURN
93000*
     PRINTER OFF
     ERR.TYPE=3
     CALL SYSCOM(MAT SYSCOM.REC)
99999*
     END
