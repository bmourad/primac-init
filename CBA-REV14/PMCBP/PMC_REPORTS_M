*COPY>CPYLIB>COM1
**********************************************
* REVISION    - [12.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - PMC_REPORTS_M
* BY          - ABDULLAH JIBALY; CBA
* DATE        - 03/14/2002
* DESCRIPTION - THIS PROGRAM MAINTAINS PMC_REPORTS
*
*T26090 ajibaly 02/05/2002 * THIS PROGRAM MAINTAINS PMC_REPORTS
*T26090 wyamout 03/28/2002 * Remove Terminal; Combine PDF & RTF into
*                            Email
*T26090 cmykleb 05/21/2002 * Add multi screen flag.
*T27638 wyamout 08/13/2003 * ADD DIV '00' TO FORMS
*ENDDOC
**********************************************
*
*---CPYLIBS
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>PMC_REPORTS
*COPY>PMC.CPYLIB>REPORT.PROC.FILE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*
*---ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---OPEN FILES
*
   OPEN '', 'PMC.SCREENS' TO M.SCREENS ELSE
      ERRMSG = 'PMC.SCREENS FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'PMC_REPORTS' TO PMC_REPORTS ELSE
      ERRMSG = 'PMC_REPORTS FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'REPORT.PROC.FILE' TO REPORT.PROC.FILE ELSE
      ERRMSG = 'REPORT.PROC.FILE FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'COMPANY' TO COMPANY ELSE
      ERRMSG = 'COMPANY FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'DIVISION' TO DIVISION ELSE
      ERRMSG = 'DIVISION FILE IS MISSING'
      GOSUB 91000; RETURN
   END
*
*---INITIALIZATION SECTION
*
   MAT EDIT.COM.DRIVER = ''
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME = 'PMC_REPORTS_M'
   ECD.ACTION=1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   ESN = ECD.SCRN.NO
   LINE.COUNT = 6:@AM:3    ;* 2 SCROLLING AREAS
   SCR.AREA = ''           ;* IS SET TO EITHER 1 OR 2
   PROCREAD IN_BUFFER ELSE IN_BUFFER = ""
   IF IN_BUFFER<1> # "AUTO" THEN IN_BUFFER = ""
   MORE = 1
   LOOP
      GOSUB MAIN
   WHILE MORE REPEAT
   RETURN
*
*---BODY OF PROGRAM SECTION
*
MAIN: 
   REF.NO = '':@AM:''
   LN.NO = 0:@AM:0
   FIELD.COUNT = 0:@AM:0
   MAT SCV.REC = ''
   ECD.ACTION=2;CALL SCRN.EDIT
*---RETRIEVE PMC_REPORTS ID
   LOOP
      IF IN_BUFFER<1> = "" THEN
         GOSUB SET.PRPT.ID
         IF ECD.RET.VALUE = 'END' THEN
            MORE=0; RETURN
         END
      END ELSE
         PRPT.ID = IN_BUFFER<2>
         ECD.NUM = 1
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = PRPT.ID
         ECD.ACTION = 5
         CALL SCRN.EDIT
      END
      MATREAD PRPT.REC FROM PMC_REPORTS,PRPT.ID ELSE
         MAT PRPT.REC = ''
         FOR SUB.OPTION = 1 TO 4
            ON SUB.OPTION GOSUB SET.HEADING, SET.PROMPTS, SET.FLAGS, SET.FORMS
         UNTIL ECD.RET.VALUE = 'END'
         NEXT SUB.OPTION
         IF SUB.OPTION = 1 THEN CONTINUE ELSE EXIT
      END
      SCV.REC(2)<ESN> = PRPT.HEADING
      SCV.REC(3)<ESN> = PRPT.PROMPTS
      SCV.REC(24)<ESN> = PRPT.PROMPTS.DS
      SCV.REC(25)<ESN> = PRPT.PROMPTS.GDS
      SCV.REC(26)<ESN> = PRPT.PROMPTS.MULTI ; * T26090
      SCR.AREA = 1
      FIELD.COUNT<SCR.AREA> = DCOUNT(PRPT.PROMPTS,@VM)
      FOR IDX = 1 TO FIELD.COUNT<SCR.AREA>
         MATREAD RPT.PROC.REC FROM REPORT.PROC.FILE, PRPT.PROMPTS<1,IDX> THEN
            SCV.REC(4)<ESN,IDX> = RPR.DESC
         END ELSE
            SCV.REC(4)<ESN,IDX> = '**INVALID REPORT PROMPT NUMBER**'
         END
      NEXT IDX
      GOSUB SCROLL
      SCV.REC(5)<ESN> = PRPT.VIEW.FLAG
      SCV.REC(7)<ESN> = PRPT.EMAIL.FLAG
      SCV.REC(11)<ESN> = PRPT.FORM.CONO
      SCV.REC(12)<ESN> = PRPT.FORM.DIV
      SCV.REC(13)<ESN> = PRPT.FORM.PATH
      SCV.REC(20)<ESN> = PRPT.RPT.SCRN.FLAG
      SCR.AREA = 2
      FIELD.COUNT<SCR.AREA> = DCOUNT(PRPT.FORM.PATH,@VM)
      GOSUB SCROLL
      ECD.ACTION = 3; CALL SCRN.EDIT
   WHILE PRPT.ID = '' REPEAT
*---GET OPTION
   LOOP
      ECD.NUM = 14
      IF IN_BUFFER<1> # "" THEN
         ECD.VALDAT = "F,E,f,e"
         ECD.PMSG = "Enter an option, (F)ile, or (E)xit"
      END
      ECD.ACTION = 4; CALL SCRN.EDIT
      OPTION = UPCASE(ECD.RET.VALUE)
      BEGIN CASE
         CASE OPTION = "END" OR OPTION = "E"
            IF IN_BUFFER<1> # "" THEN MORE = 0
            RETURN
         CASE OPTION = "F"
            IF IN_BUFFER<1> # "" THEN MORE = 0
            MATWRITE PRPT.REC ON PMC_REPORTS, PRPT.ID
            RETURN
         CASE OPTION = "C"
            COPY.RECORD.CONFIRM = 'Copy report ':PRPT.ID:' to *ID* (y/n):'
            GOSUB COPY.RECORD
         CASE OPTION = "M"
            OLD.PRPT.ID = PRPT.ID
            COPY.RECORD.CONFIRM = 'Move report ':PRPT.ID:' to *ID* (y/n):'
            GOSUB COPY.RECORD
            IF PRPT.ID # OLD.PRPT.ID THEN
               DELETE PMC_REPORTS, OLD.PRPT.ID
            END
         CASE OPTION = "D"
            READ REC FROM PMC_REPORTS, PRPT.ID ELSE
               ERRMSG = 'This report has not been filed yet!'; GOSUB 91000
               CONTINUE
            END
            ECD.NUM = 22
            ECD.PMSG = 'Delete report ':PRPT.ID:' (y/n):'
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'Y' THEN
               DELETE PMC_REPORTS, PRPT.ID
               RETURN
            END
         CASE NUM(OPTION)
            ON OPTION GOSUB SET.HEADING, SET.PROMPTS, SET.FLAGS, SET.FORMS
         CASE 1
            ERRMSG = "INVALID OPTION, PLEASE TRY AGAIN"; GOSUB 91000
      END CASE
   REPEAT
   RETURN
*
REFRESH.SCREEN: 
   ECD.ACTION = 2; CALL SCRN.EDIT
   ECD.ACTION = 3; CALL SCRN.EDIT
   REF.NO = '':@AM:''
   SCR.AREA = 1; GOSUB SCROLL
   SCR.AREA = 2; GOSUB SCROLL
*
SCROLL: 
   CUR.LN = LN.NO<SCR.AREA>
   CUR.LNS = LINE.COUNT<SCR.AREA>
   START.REF.NO = 1 + INT((CUR.LN-1)/CUR.LNS)*CUR.LNS
   IF START.REF.NO = REF.NO<SCR.AREA> THEN RETURN
   REF.NO<SCR.AREA> = START.REF.NO
   CUR.PAGE = CUR.LN/CUR.LNS
   IF INT(CUR.PAGE) # CUR.PAGE OR CUR.PAGE = 0 THEN
      CUR.PAGE = INT(CUR.PAGE)+1
   END
   LAST.PAGE = FIELD.COUNT<SCR.AREA>/CUR.LNS
   IF INT(LAST.PAGE) # LAST.PAGE OR LAST.PAGE = 0 THEN
      LAST.PAGE = INT(LAST.PAGE)+1
   END
   IF CUR.PAGE > LAST.PAGE THEN LAST.PAGE = CUR.PAGE
*---Get around scrolling bug in ECD.ACTION=7 by using ECD.ACTION=5
   BEGIN CASE
      CASE SCR.AREA = 1
         FOR IDX = REF.NO<SCR.AREA> TO REF.NO<SCR.AREA>+CUR.LNS-1
            SCV.REC(23)<ESN,IDX> = IDX
            ECD.SUB.NUM=IDX;ECD.NUM=23;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=3;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=24;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=25;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=4;ECD.ACTION=5;CALL SCRN.EDIT
         NEXT IDX
         ECD.NUM = 9
         SCV.REC(ECD.NUM)<ESN> = CUR.PAGE
         ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 10
         SCV.REC(ECD.NUM)<ESN> = LAST.PAGE
         ECD.ACTION = 5; CALL SCRN.EDIT
      CASE SCR.AREA = 2
         FOR IDX = REF.NO<SCR.AREA> TO REF.NO<SCR.AREA>+CUR.LNS-1
            SCV.REC(19)<ESN,IDX> = IDX
            ECD.SUB.NUM=IDX;ECD.NUM=19;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=11;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=12;ECD.ACTION=5;CALL SCRN.EDIT
            ECD.SUB.NUM=IDX;ECD.NUM=13;ECD.ACTION=5;CALL SCRN.EDIT
         NEXT IDX
         ECD.NUM = 16
         SCV.REC(ECD.NUM)<ESN> = CUR.PAGE
         ECD.ACTION = 5; CALL SCRN.EDIT
         ECD.NUM = 17
         SCV.REC(ECD.NUM)<ESN> = LAST.PAGE
         ECD.ACTION = 5; CALL SCRN.EDIT
   END CASE
   RETURN
*
SCROLL.OPTIONS: 
   BEGIN CASE
      CASE SCROLL.OPTION = "S"
         LN.NO<SCR.AREA> = REF.NO<SCR.AREA> + LINE.COUNT<SCR.AREA>
         IF LN.NO<SCR.AREA> > FIELD.COUNT<SCR.AREA> THEN LN.NO<SCR.AREA> = 1
         GOSUB SCROLL
      CASE SCROLL.OPTION = "SR"
         LN.NO<SCR.AREA> = REF.NO<SCR.AREA> - LINE.COUNT<SCR.AREA>
         IF LN.NO<SCR.AREA> < 1 THEN LN.NO<SCR.AREA> = FIELD.COUNT<SCR.AREA>
         GOSUB SCROLL
      CASE SCROLL.OPTION = "SB"
         LN.NO<SCR.AREA> = FIELD.COUNT<SCR.AREA>
         GOSUB SCROLL
      CASE SCROLL.OPTION = "ST"
         LN.NO<SCR.AREA> = 1
         GOSUB SCROLL
   END CASE
   RETURN
*
COPY.RECORD: 
   LOOP
      ECD.NUM = 21
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = 'END' THEN RETURN
      READ REC FROM PMC_REPORTS,ECD.RET.VALUE THEN
         ERRMSG = 'Report ':ECD.RET.VALUE:' already exists!'; GOSUB 91000
         ECD.RET.VALUE = ''
         CONTINUE
      END
   WHILE ECD.RET.VALUE = '' REPEAT
   NEW.PRPT.ID = ECD.RET.VALUE
   ECD.NUM = 22
   ECD.PMSG = CHANGE(COPY.RECORD.CONFIRM,'*ID*',NEW.PRPT.ID)
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'Y' THEN
      PRPT.ID = NEW.PRPT.ID
      MATWRITE PRPT.REC ON PMC_REPORTS, PRPT.ID
      SCV.REC(1)<ESN> = PRPT.ID
      ECD.NUM = 1; ECD.ACTION = 5; CALL SCRN.EDIT
   END
   RETURN
*
SET.PRPT.ID: 
   LOOP
      ECD.NUM = 1
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = 'END' THEN RETURN
      IF ECD.RET.VALUE = 'N' THEN
         ECD.NUM = 21
         ECD.MINL = 2
         ECD.PMSG = 'Report prefix:'
         ECD.ACTION = 4; CALL SCRN.EDIT
         IF ECD.RET.VALUE = 'END' THEN CONTINUE
         PRPT.PREFIX   = ECD.RET.VALUE[1,4]
         PRPT.PREFIX.A = ECD.RET.VALUE[1,2]
         PRPT.PREFIX.N = ECD.RET.VALUE[3,3]
         IF NOT(NUM(PRPT.PREFIX.N)) OR NOT(ALPHA(PRPT.PREFIX.A)) THEN
            ERRMSG = "Invalid prefix, must be (AANNN), (AANN), (AAN), or (AA)."
            GOSUB 91000
            ECD.RET.VALUE = ''
            CONTINUE
         END
         CMD = "SSELECT PMC_REPORTS WITH @ID LIKE "
         IF LEN(ECD.RET.VALUE) = 5 THEN
            CMD:= PRPT.PREFIX.A:"... AND WITH PRPT_ID_NUM GE ":PRPT.PREFIX.N
         END ELSE IF PRPT.PREFIX.N > 0 THEN
            CMD:= PRPT.PREFIX:"..."
         END ELSE
            CMD:= PRPT.PREFIX.A:"..."
         END
         UDTEXECUTE CMD CAPTURING JUNK
         PRPT.ID = ECD.RET.VALUE"L%5"
         LOOP.CNT = 0
         LOOP
            LOOP.CNT = LOOP.CNT + 1
            PRPT.ID.OLD = PRPT.ID
            READNEXT PRPT.ID ELSE
               IF LOOP.CNT = 1 THEN
                  PRPT.ID = PRPT.ID.OLD
                  EXIT
               END
               PRPT.ID.N = (PRPT.ID.OLD[3,3] + 1)"R%3"
               PRPT.ID = PRPT.PREFIX.A:PRPT.ID.N
               IF PRPT.PREFIX = PRPT.ID[1,LEN(PRPT.PREFIX)] THEN EXIT
               PRPT.ID = ''
               EXIT
            END
            GAP = PRPT.ID[3,3] - PRPT.ID.OLD[3,3]
            IF GAP > 0 AND LOOP.CNT = 1 THEN
               PRPT.ID = PRPT.ID.OLD
               EXIT
            END
            IF GAP > 1 THEN
               PRPT.ID.N = (PRPT.ID.OLD[3,3] + 1)"R%3"
               PRPT.ID = PRPT.PREFIX.A:PRPT.ID.N
               EXIT
            END
         REPEAT
         CLEARSELECT
         IF PRPT.ID = '' THEN
            ERRMSG = "No report numbers are available with that prefix"
            GOSUB 91000
            CONTINUE
         END ELSE
            ECD.ACTION = 2; CALL SCRN.EDIT
            ECD.NUM = 1
            SCV.REC(ECD.NUM)<ESN> = PRPT.ID
            ECD.ACTION = 5; CALL SCRN.EDIT
         END
      END ELSE
         PRPT.ID = ECD.RET.VALUE
      END
   WHILE ECD.RET.VALUE = '' REPEAT
   RETURN
*
SET.HEADING: 
   ECD.NUM = 2
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN PRPT.HEADING = ECD.RET.VALUE
   RETURN
*
SET.PROMPTS: 
   ECD.RET.VALUE = ''
   LOOP
      SCR.AREA = 1
      IF FIELD.COUNT<SCR.AREA> > 0 THEN
         ECD.NUM = 15
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = UPCASE(ECD.RET.VALUE)
      END ELSE
         IF ECD.RET.VALUE # "END" THEN OPTION = "A" ELSE OPTION = "END"
      END
      BEGIN CASE
         CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
            ECD.RET.VALUE = ""
            EXIT
         CASE OPTION[1,1] = "S"
            SCROLL.OPTION = OPTION; GOSUB SCROLL.OPTIONS
         CASE OPTION = "A"
            LN.NO<SCR.AREA> = FIELD.COUNT<SCR.AREA>
            LOOP
               LN.NO<SCR.AREA> += 1
               GOSUB SCROLL
               PROMPT.VALUES = ''
               FOR PROMPT.IDX = 1 TO 4
                  ON PROMPT.IDX GOSUB PROMPT.SUB, PROMPT.DS.SUB, PROMPT.GDS.SUB, PROMPT.MULTI.SUB
               UNTIL ECD.RET.VALUE = 'END'
               NEXT PROMPT.IDX
               IF ECD.RET.VALUE = 'END' THEN EXIT
               PRPT.PROMPTS<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<1>
               PRPT.PROMPTS.DS<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<2>
               PRPT.PROMPTS.GDS<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<3>
*T26090 v
               PRPT.PROMPTS.MULTI<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<4>
*T26090 ^
               FIELD.COUNT<SCR.AREA> += 1
            REPEAT
            SCV.REC(3)<ESN> = DELETE(SCV.REC(3),ESN,LN.NO<SCR.AREA>)
            SCV.REC(24)<ESN> = DELETE(SCV.REC(24),ESN,LN.NO<SCR.AREA>)
            SCV.REC(25)<ESN> = DELETE(SCV.REC(25),ESN,LN.NO<SCR.AREA>)
            LN.NO<SCR.AREA> -= 1
            REF.NO<SCR.AREA> = ''; GOSUB SCROLL
         CASE OPTION = "D"
            ECD.NUM = 18
            ECD.MINV = REF.NO<SCR.AREA>
            ECD.MAXV = MINIMUM((REF.NO<SCR.AREA>+LINE.COUNT<SCR.AREA>-1):@AM:FIELD.COUNT<SCR.AREA>)
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN CONTINUE
            LN = ECD.RET.VALUE
            SCV.REC(3)<ESN> = DELETE(SCV.REC(3),ESN,LN)
            SCV.REC(4)<ESN> = DELETE(SCV.REC(4),ESN,LN)
            SCV.REC(24)<ESN> = DELETE(SCV.REC(24),ESN,LN)
            SCV.REC(25)<ESN> = DELETE(SCV.REC(25),ESN,LN)
            SCV.REC(26)<ESN> = DELETE(SCV.REC(26),ESN,LN) ; * T26090
            LN.NO<SCR.AREA> = LN-1
            PRPT.PROMPTS = SCV.REC(3)<ESN>
            PRPT.PROMPTS.DS = SCV.REC(24)<ESN>
            PRPT.PROMPTS.GDS = SCV.REC(25)<ESN>
            PRPT.PROMPTS.MULTI = SCV.REC(26)<ESN> ; * T26090
            FIELD.COUNT<SCR.AREA> -= 1
            REF.NO<SCR.AREA> = ''
            GOSUB SCROLL
         CASE OPTION = "C"
            ECD.NUM = 18
            ECD.MINV = REF.NO<SCR.AREA>
            ECD.MAXV = MINIMUM((REF.NO<SCR.AREA>+LINE.COUNT<SCR.AREA>-1):@AM:FIELD.COUNT<SCR.AREA>)
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN CONTINUE
            LN = ECD.RET.VALUE
            LN.NO<SCR.AREA> = LN
            SAVE.RPR.DESC = SCV.REC(4)<ESN,LN>
            PROMPT.VALUES = ''
            FOR PROMPT.IDX = 1 TO 4
               ON PROMPT.IDX GOSUB PROMPT.SUB, PROMPT.DS.SUB, PROMPT.GDS.SUB, PROMPT.MULTI.SUB
            UNTIL ECD.RET.VALUE = 'END'
            NEXT PROMPT.IDX
            IF ECD.RET.VALUE = 'END' THEN
               ECD.NUM = 3; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.PROMPTS<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
               ECD.NUM = 24; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.PROMPTS.DS<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
               ECD.NUM = 25; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.PROMPTS.GDS<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
*T26090 v
               ECD.NUM = 26; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.PROMPTS.MULTI<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
*T26090 ^
               ECD.NUM = 4; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = SAVE.RPR.DESC
               ECD.ACTION = 5; CALL SCRN.EDIT
               CONTINUE
            END
            PRPT.PROMPTS<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<1>
            PRPT.PROMPTS.DS<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<2>
            PRPT.PROMPTS.GDS<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<3>
*T26090 v
            PRPT.PROMPTS.MULTI<1,LN.NO<SCR.AREA>> = PROMPT.VALUES<4>
*T26090 ^
         CASE OPTION = "I"
            ECD.NUM = 18
            ECD.MINV = REF.NO<SCR.AREA>
            ECD.MAXV = MINIMUM((REF.NO<SCR.AREA>+LINE.COUNT<SCR.AREA>-1):@AM:FIELD.COUNT<SCR.AREA>)
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN CONTINUE
            LN = ECD.RET.VALUE-1
            LN.NO<SCR.AREA> = LN
            LOOP
               LN.NO<SCR.AREA> += 1
               SCV.REC(3)<ESN> = INSERT(SCV.REC(3),ESN,LN.NO<SCR.AREA>,0,"")
               SCV.REC(4)<ESN> = INSERT(SCV.REC(4),ESN,LN.NO<SCR.AREA>,0,"")
               SCV.REC(24)<ESN> = INSERT(SCV.REC(24),ESN,LN.NO<SCR.AREA>,0,"")
               SCV.REC(25)<ESN> = INSERT(SCV.REC(25),ESN,LN.NO<SCR.AREA>,0,"")
               REF.NO<SCR.AREA> = ''; GOSUB SCROLL
               PROMPT.VALUES = ''
               FOR PROMPT.IDX = 1 TO 4
                  ON PROMPT.IDX GOSUB PROMPT.SUB, PROMPT.DS.SUB, PROMPT.GDS.SUB, PROMPT.MULTI.SUB
               UNTIL ECD.RET.VALUE = 'END'
               NEXT PROMPT.IDX
               IF ECD.RET.VALUE = 'END' THEN EXIT
               PRPT.PROMPTS = SCV.REC(3)<ESN>
               PRPT.PROMPTS.DS = SCV.REC(24)<ESN>
               PRPT.PROMPTS.GDS = SCV.REC(25)<ESN>
               PRPT.PROMPTS.MULTI = SCV.REC(26)<ESN> ; * T26090
               FIELD.COUNT<SCR.AREA> += 1
            REPEAT
            SCV.REC(3)<ESN> = DELETE(SCV.REC(3),ESN,LN.NO<SCR.AREA>)
            SCV.REC(4)<ESN> = DELETE(SCV.REC(4),ESN,LN.NO<SCR.AREA>)
            SCV.REC(24)<ESN> = DELETE(SCV.REC(24),ESN,LN.NO<SCR.AREA>)
            SCV.REC(25)<ESN> = DELETE(SCV.REC(25),ESN,LN.NO<SCR.AREA>)
*T26090 v
            SCV.REC(26)<ESN> = DELETE(SCV.REC(26),ESN,LN.NO<SCR.AREA>)
*T26090 ^
            LN.NO<SCR.AREA> -= 1
            REF.NO<SCR.AREA> = ''; GOSUB SCROLL
      END CASE
   REPEAT
   RETURN
*
PROMPT.SUB: 
   LOOP
      ECD.NUM = 3; ECD.SUB.NUM = LN.NO<SCR.AREA>
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = 'END' THEN EXIT
      LOCATE ECD.RET.VALUE IN PRPT.PROMPTS<1> SETTING DUP.LN THEN
         IF DUP.LN # LN.NO<SCR.AREA> THEN
            ERRMSG = 'THAT PROMPT IS ALREADY AT LINE ':DUP.LN
            GOSUB 91000
            ECD.RET.VALUE = ''
            CONTINUE
         END
      END
      MATREAD RPT.PROC.REC FROM REPORT.PROC.FILE, ECD.RET.VALUE THEN
         ECD.NUM = 4; ECD.SUB.NUM = LN.NO<SCR.AREA>
         SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = RPR.DESC
         ECD.ACTION = 5; CALL SCRN.EDIT
      END ELSE
         ERRMSG = 'THAT PROMPT NUMBER DOES NOT EXIST'; GOSUB 91000
         ECD.RET.VALUE = ''
         CONTINUE
      END
      PROMPT.VALUES<1> = ECD.RET.VALUE
   WHILE ECD.RET.VALUE = '' REPEAT
   RETURN
*
PROMPT.DS.SUB: 
   ECD.NUM = 24; ECD.SUB.NUM = LN.NO<SCR.AREA>
   DIV_FILE = 0
   FCNT = DCOUNT(RPR.FILE,",")
   FOR FNUM = 1 TO FCNT
      IF FIELD(RPR.FILE,',',FNUM) = "DIVISION" THEN
         DIV_FILE = 1
         EXIT
      END
   NEXT FNUM
   IF DIV_FILE = 0 THEN
      SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = ''
      ECD.ACTION = 5; CALL SCRN.EDIT
      RETURN
   END
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN RETURN
   PROMPT.VALUES<2> = ECD.RET.VALUE
   RETURN
*
PROMPT.GDS.SUB: 
   ECD.NUM = 25; ECD.SUB.NUM = LN.NO<SCR.AREA>
   DIV_FILE = 0
   FCNT = DCOUNT(RPR.FILE,",")
   FOR FNUM = 1 TO FCNT
      IF FIELD(RPR.FILE,',',FNUM) = "DIVISION" THEN
         DIV_FILE = 1
         EXIT
      END
   NEXT FNUM
   IF DIV_FILE = 0 THEN
      SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = ''
      ECD.ACTION = 5; CALL SCRN.EDIT
      RETURN
   END
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN RETURN
   PROMPT.VALUES<3> = ECD.RET.VALUE
   RETURN
*T26090 v
*
PROMPT.MULTI.SUB: 
   ECD.NUM = 26; ECD.SUB.NUM = LN.NO<SCR.AREA>
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN RETURN
   PROMPT.VALUES<4> = ECD.RET.VALUE
   RETURN
*T26090 ^
*
SET.FLAGS: 
   ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN PRPT.VIEW.FLAG = ECD.RET.VALUE ELSE RETURN
   ECD.NUM = 7; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN PRPT.EMAIL.FLAG = ECD.RET.VALUE ELSE RETURN
   ECD.NUM = 20; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN PRPT.RPT.SCRN.FLAG = ECD.RET.VALUE
   RETURN
*
SET.FORMS: 
   ECD.RET.VALUE = ''
   LOOP
      SCR.AREA = 2
      IF FIELD.COUNT<SCR.AREA> > 0 THEN
         ECD.NUM = 15
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = UPCASE(ECD.RET.VALUE)
      END ELSE
         IF ECD.RET.VALUE # "END" THEN OPTION = "A" ELSE OPTION = "END"
      END
      BEGIN CASE
         CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
            ECD.RET.VALUE = ""
            EXIT
         CASE OPTION[1,1] = "S"
            SCROLL.OPTION = OPTION; GOSUB SCROLL.OPTIONS
         CASE OPTION = "A"
            LN.NO<SCR.AREA> = FIELD.COUNT<SCR.AREA>
            LOOP
               LN.NO<SCR.AREA> += 1
               GOSUB SCROLL
               FORM.VALUE = ''
               FOR FORM.IDX = 1 TO 3
                  ON FORM.IDX GOSUB FORM.CONO.SUB, FORM.DIV.SUB, FORM.PATH.SUB
               UNTIL ECD.RET.VALUE = 'END'
               NEXT FORM.IDX
               IF ECD.RET.VALUE = 'END' THEN EXIT
               PRPT.FORM.CONO<1,LN.NO<SCR.AREA>> = FORM.VALUE<1>
               PRPT.FORM.DIV<1,LN.NO<SCR.AREA>> = FORM.VALUE<2>
               PRPT.FORM.PATH<1,LN.NO<SCR.AREA>> = FORM.VALUE<3>
               FIELD.COUNT<SCR.AREA> += 1
            REPEAT
            SCV.REC(11)<ESN> = DELETE(SCV.REC(11),ESN,LN.NO<SCR.AREA>)
            SCV.REC(12)<ESN> = DELETE(SCV.REC(12),ESN,LN.NO<SCR.AREA>)
            SCV.REC(13)<ESN> = DELETE(SCV.REC(13),ESN,LN.NO<SCR.AREA>)
            LN.NO<SCR.AREA> -= 1
            REF.NO<SCR.AREA> = ''; GOSUB SCROLL
         CASE OPTION = "D"
            ECD.NUM = 18
            ECD.MINV = REF.NO<SCR.AREA>
            ECD.MAXV = MINIMUM((REF.NO<SCR.AREA>+LINE.COUNT<SCR.AREA>-1):@AM:FIELD.COUNT<SCR.AREA>)
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN CONTINUE
            LN = ECD.RET.VALUE
            SCV.REC(11)<ESN> = DELETE(SCV.REC(11),ESN,LN)
            SCV.REC(12)<ESN> = DELETE(SCV.REC(12),ESN,LN)
            SCV.REC(13)<ESN> = DELETE(SCV.REC(13),ESN,LN)
            LN.NO<SCR.AREA> = LN-1
            PRPT.FORM.CONO = SCV.REC(11)<ESN>
            PRPT.FORM.DIV  = SCV.REC(12)<ESN>
            PRPT.FORM.PATH = SCV.REC(13)<ESN>
            FIELD.COUNT<SCR.AREA> -= 1
            REF.NO<SCR.AREA> = ''
            GOSUB SCROLL
         CASE OPTION = "C"
            ECD.NUM = 18
            ECD.MINV = REF.NO<SCR.AREA>
            ECD.MAXV = MINIMUM((REF.NO<SCR.AREA>+LINE.COUNT<SCR.AREA>-1):@AM:FIELD.COUNT<SCR.AREA>)
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN CONTINUE
            LN = ECD.RET.VALUE
            LN.NO<SCR.AREA> = LN
            FORM.VALUE = ''
            FOR FORM.IDX = 1 TO 3
               ON FORM.IDX GOSUB FORM.CONO.SUB, FORM.DIV.SUB, FORM.PATH.SUB
            UNTIL ECD.RET.VALUE = 'END'
            NEXT FORM.IDX
            IF ECD.RET.VALUE = 'END' THEN
               ECD.NUM = 11; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.FORM.CONO<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
               ECD.NUM = 12; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.FORM.DIV<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
               ECD.NUM = 13; ECD.SUB.NUM = LN
               SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = PRPT.FORM.PATH<1,LN>
               ECD.ACTION = 5; CALL SCRN.EDIT
               CONTINUE
            END
            PRPT.FORM.CONO<1,LN.NO<SCR.AREA>> = FORM.VALUE<1>
            PRPT.FORM.DIV<1,LN.NO<SCR.AREA>> = FORM.VALUE<2>
            PRPT.FORM.PATH<1,LN.NO<SCR.AREA>> = FORM.VALUE<3>
         CASE OPTION = "I"
            ECD.NUM = 18
            ECD.MINV = REF.NO<SCR.AREA>
            ECD.MAXV = MINIMUM((REF.NO<SCR.AREA>+LINE.COUNT<SCR.AREA>-1):@AM:FIELD.COUNT<SCR.AREA>)
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE = 'END' THEN CONTINUE
            LN = ECD.RET.VALUE-1
            LN.NO<SCR.AREA> = LN
            LOOP
               LN.NO<SCR.AREA> += 1
               SCV.REC(11)<ESN> = INSERT(SCV.REC(11),ESN,LN.NO<SCR.AREA>,0,"")
               SCV.REC(12)<ESN> = INSERT(SCV.REC(12),ESN,LN.NO<SCR.AREA>,0,"")
               SCV.REC(13)<ESN> = INSERT(SCV.REC(13),ESN,LN.NO<SCR.AREA>,0,"")
               REF.NO<SCR.AREA> = ''; GOSUB SCROLL
               FORM.VALUE = ''
               FOR FORM.IDX = 1 TO 3
                  ON FORM.IDX GOSUB FORM.CONO.SUB, FORM.DIV.SUB, FORM.PATH.SUB
               UNTIL ECD.RET.VALUE = 'END'
               NEXT FORM.IDX
               IF ECD.RET.VALUE = 'END' THEN EXIT
               PRPT.FORM.CONO = SCV.REC(11)<ESN>
               PRPT.FORM.DIV  = SCV.REC(12)<ESN>
               PRPT.FORM.PATH = SCV.REC(13)<ESN>
               FIELD.COUNT<SCR.AREA> += 1
            REPEAT
            SCV.REC(11)<ESN> = DELETE(SCV.REC(11),ESN,LN.NO<SCR.AREA>)
            SCV.REC(12)<ESN> = DELETE(SCV.REC(12),ESN,LN.NO<SCR.AREA>)
            SCV.REC(13)<ESN> = DELETE(SCV.REC(13),ESN,LN.NO<SCR.AREA>)
            LN.NO<SCR.AREA> -= 1
            REF.NO<SCR.AREA> = ''; GOSUB SCROLL
      END CASE
   REPEAT
   RETURN
*
FORM.CONO.SUB: 
   LOOP
      ECD.NUM = 11; ECD.SUB.NUM = LN.NO<SCR.AREA>
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = 'END' THEN EXIT
      MATREAD COMP.REC FROM COMPANY, ECD.RET.VALUE ELSE
         ERRMSG = 'INVALID COMPANY NUMBER'; GOSUB 91000
         ECD.RET.VALUE = ''
         CONTINUE
      END
      FORM.VALUE<1> = ECD.RET.VALUE
   WHILE ECD.RET.VALUE = '' REPEAT
   RETURN
*
FORM.DIV.SUB: 
   LOOP
      ECD.NUM = 12; ECD.SUB.NUM = LN.NO<SCR.AREA>
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = 'END' THEN EXIT
      FOR IDX = 1 TO DCOUNT(PRPT.FORM.DIV,@VM)
         IF PRPT.FORM.CONO<1,IDX>:PRPT.FORM.DIV<1,IDX> = FORM.VALUE<1>:ECD.RET.VALUE AND IDX # LN.NO<SCR.AREA> THEN
            ERRMSG = 'DUPLICATE COMPANY/DIVISION COMBINATION ON LINE ':IDX
            GOSUB 91000
            SCV.REC(12)<ESN,LN.NO<SCR.AREA>> = ''; ECD.RET.VALUE = ''
         END
      UNTIL ECD.RET.VALUE = ''
      NEXT IDX
      IF ECD.RET.VALUE = '' THEN CONTINUE
*T27638 v
      IF ECD.RET.VALUE = "00" THEN
         MAT DIV.REC = ""
         DIV.DESC = "GENERAL DIVISION"
      END ELSE
*T27638 ^
      MATREAD DIV.REC FROM DIVISION, FORM.VALUE<1>:ECD.RET.VALUE ELSE
         ERRMSG = 'INVALID DIVISION FOR COMPANY ':FORM.VALUE<1>; GOSUB 91000
         ECD.RET.VALUE = ''
         CONTINUE
      END
      END ;* T27638
      FORM.VALUE<2> = ECD.RET.VALUE
   WHILE ECD.RET.VALUE = '' REPEAT
   RETURN
*
FORM.PATH.SUB: 
   LOOP
      ECD.NUM = 13; ECD.SUB.NUM = LN.NO<SCR.AREA>
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = 'END' THEN RETURN
      FORM.VALUE<3> = ECD.RET.VALUE
      ECD.NUM = 13; ECD.SUB.NUM = LN.NO<SCR.AREA>
      SCV.REC(ECD.NUM)<ESN,ECD.SUB.NUM> = FORM.VALUE<3>
      ECD.ACTION = 5; CALL SCRN.EDIT
   WHILE ECD.RET.VALUE = '' REPEAT
   RETURN
*
*
*---STANDARD SUBROUTINES
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
99999 RETURN
END
