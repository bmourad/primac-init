   SUBROUTINE PMC_GETREPORTID(ERRMSG,IN_PARAM,OUT_PARAM,CHECK_FILE,CONTROL_FILE)
*
* EXAMPLE OF IN_PARAM FOR DAILY.TIME
* IN_PARAM<1> = 001
* IN_PARAM<2> = "DAILY.TIME"
* IN_PARAM<3> = "CONTROL"
* IN_PARAM<4> = "DAILY.TIME"
* IN_PARAM<5> = "SEQ"
* IN_PARAM<6> = ""
* IN_PARAM<7> = ""
* IN_PARAM<8> = 10001
* IN_PARAM<9> = 999999
*
* EXAMPLE OF IN_PARAM FOR DAILY.INVOICE
* IN_PARAM<1> = 001
* IN_PARAM<2> = "NEXT.INVOICE.NO"
* IN_PARAM<3> = "CONTROL"
* IN_PARAM<4> = "DAILY.INVOICE"
* IN_PARAM<5> = "SEQADD"
* IN_PARAM<6> = "R%6"
* IN_PARAM<7> = ""
* IN_PARAM<8> = 1
* IN_PARAM<9> = 999999
* IN_PARAM<10> = "" OR "CM" OR "DM"
*
* EXAMPLE OF IN_PARAM FOR SIS_OUTPUT
* IN_PARAM<1> = ""
* IN_PARAM<2> = "SVR_" : SISMSG_SERVER
* IN_PARAM<3> = "SIS_CONTROL"
* IN_PARAM<4> = "SIS_OUTPUT"
* IN_PARAM<5> = "DATE"
* IN_PARAM<6> = ""
* IN_PARAM<7> = "R%6"
* IN_PARAM<8> = ""
* IN_PARAM<9> = ""
* IN_PARAM<10> = ""
* IN_PARAM<11> = SISMSG_SERVER
*
* EXAMPLE OF IN_PARAM FOR PMCProcessStatus
* IN_PARAM<1> = ""
* IN_PARAM<2> = "PMCProcessStatus"
* IN_PARAM<3> = "CONTROL"
* IN_PARAM<4> = "_HOLD_"
* IN_PARAM<5> = "DATE"
* IN_PARAM<6> = ""
* IN_PARAM<7> = "R%6"
* IN_PARAM<8> = ""
* IN_PARAM<9> = ""
* IN_PARAM<10> = ""
*
   ERRMSG = ""
   OUT_PARAM = ""
   CONO = IN_PARAM<1> ;* 001
   SEQ_ID = IN_PARAM<2> ;* CONTROL RECORD ID
   CONTROL_NAME = IN_PARAM<3>
   CHECK_NAME = IN_PARAM<4>
   SEQ_TYPE = IN_PARAM<5>
   SEQ_FMT = IN_PARAM<6>
   SEQ_SFMT = IN_PARAM<7>
   SEQ_MINV = IN_PARAM<8> + 0
   SEQ_MAXV = IN_PARAM<9>
   SEQ_SUFFIX = IN_PARAM<10>
   SEQ_PREFIX = IN_PARAM<11>
   IF FILEINFO(CONTROL_FILE,0) # 1 THEN
      OPEN '',CONTROL_NAME TO CONTROL_FILE ELSE
         ERRMSG = CONTROL_NAME : " FILE IS MISSING"
         GOTO 999999
      END
   END
   IF FILEINFO(CHECK_FILE,0) # 1 THEN
      OPEN '',CHECK_NAME TO CHECK_FILE ELSE
         ERRMSG = CHECK_NAME : " FILE IS MISSING"
         GOTO 999999
      END
   END
   BEGIN CASE
   CASE SEQ_TYPE = "SEQ"
      GOSUB SEQ_SUB
   CASE SEQ_TYPE = "SEQADD"
      GOSUB SEQ_SUB
   CASE SEQ_TYPE = "DATE"
      GOSUB DATE_SUB
   CASE SEQ_TYPE = "DATETIME"
      GOSUB DATETIME_SUB
   CASE 1
      ERRMSG = "INVALID SEQ_TYPE - " : SEQ_TYPE
   END CASE
   GOTO 999999
*
*--- SUBROUTINES
*
SEQ_SUB: 
   READU SEQ_REC FROM CONTROL_FILE, CONO:SEQ_ID ELSE SEQ_REC = SEQ_MINV
   NEWID = ""
   LOOP
   UNTIL NEWID # "" DO
      IF SEQ_TYPE = "SEQADD" THEN
         SEQ_REC<1> += 1
         IF SEQ_MAXV # "" AND SEQ_MAXV > SEQ_MINV THEN
            IF SEQ_REC<1> > SEQ_MAXV THEN SEQ_REC<1> = SEQ_MINV
         END
      END
      NEWID = CONO : SEQ_PREFIX
      IF SEQ_FMT # "" THEN
         NEWID := SEQ_REC<1> SEQ_FMT
      END ELSE
         NEWID := SEQ_REC<1>
      END
      NEWID := SEQ_SUFFIX
      IF SEQ_SFMT # "" THEN
         SUB_ID = 1
         NEWID := SUB_ID SQE_SFMT
      END
      READU DUMMY FROM CHECK_FILE, NEWID THEN
         NEWID = ""
      END LOCKED
         NEWID = ""
      END
      IF SEQ_TYPE # "SEQADD" THEN
         SEQ_REC<1> += 1
         IF SEQ_MAXV # "" AND SEQ_MAXV > SEQ_MINV THEN
            IF SEQ_REC<1> > SEQ_MAXV THEN SEQ_REC<1> = SEQ_MINV
         END
      END
   REPEAT
   DUMMY = ""
   WRITE SEQ_REC ON CONTROL_FILE, CONO:SEQ_ID
   OUT_PARAM = NEWID
   RETURN
*
DATE_SUB: 
   P_DATE = DATE()
   READU SEQ_REC FROM CONTROL_FILE, CONO:SEQ_ID ELSE SEQ_REC = ""
   NEWID = ""
   LOOP
   UNTIL NEWID # "" DO
      IF SEQ_REC<1> = "" OR P_DATE > SEQ_REC<1> THEN
         SEQ_REC<1> = P_DATE
         SEQ_REC<2> = 0
      END
      SEQ_REC<2> += 1
      NEWID = CONO : SEQ_PREFIX : P_DATE : "_"
      IF SEQ_SFMT # "" THEN
         NEWID := SEQ_REC<2> SEQ_SFMT
      END ELSE
         NEWID := SEQ_REC<2>
      END
      READU DUMMY FROM CHECK_FILE, NEWID THEN
         NEWID = ""
      END LOCKED
         NEWID = ""
      END
   REPEAT
   DUMMY = ""
   WRITE SEQ_REC ON CONTROL_FILE, CONO:SEQ_ID
   OUT_PARAM = NEWID
   RETURN
*
DATETIME_SUB: 
   TODAY = DATETIME()
   P_DATE = ICONV(TODAY[10,99], "D")
   P_TIME = ICONV(TODAY[1,8], "MTS")
   P_TIMESTAMP = P_DATE*100000 + P_TIME
   SEQ_REC = ""
   IF SEQ_ID # "" THEN
      READU SEQ_REC FROM CONTROL_FILE, CONO:SEQ_ID ELSE SEQ_REC = ""
   END
   NEWID = ""
   LOOP
   UNTIL NEWID # "" DO
      IF SEQ_REC<1> = "" OR P_TIMESTAMP > SEQ_REC<1> THEN
         SEQ_REC<1> = P_TIMESTAMP
         SEQ_REC<2> = 0
      END
      SEQ_REC<2> += 1
      NEWID = CONO : SEQ_PREFIX : P_TIMESTAMP : "_"
      IF SEQ_SFMT # "" THEN
         NEWID := SEQ_REC<2> SEQ_SFMT
      END ELSE
         NEWID := SEQ_REC<2>
      END
      READU DUMMY FROM CHECK_FILE, NEWID THEN
         NEWID = ""
      END LOCKED
         NEWID = ""
      END
   REPEAT
   DUMMY = ""
   IF SEQ_ID # "" THEN
      WRITE SEQ_REC ON CONTROL_FILE, CONO:SEQ_ID
   END
   OUT_PARAM = NEWID
   RETURN
999999*
   RETURN
END
