*REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
      PROMPT ""
100   PRINT CHAR(12)
      PRINT "DO YOU NEED INSTRUCTIONS (Y/RET)?":;INPUT ANS
      IF ANS ="Y" THEN PRINT CHAR(12); GOTO 101
      IF ANS = "END" THEN STOP
      IF ANS # "" THEN PRINT;GOTO 100
      PRINT
      GOTO 50
*****  FILE MODULO REALLOCATION PROGRAM  *****
*                                            *
*    THIS PROGRAM CALCULATES A NEW MODULO    *
* PARAMETER FOR FILES, USING THE STAT-FILE   *
* DICTIONARY CREATED DURING A FILE-SAVE.     *
* THE PROGRAM PROMPTS WITH: "IS THIS A TEST?"*
* A 'Y' RESPONSE WILL DISPLAY ALL THE FILES  *
* ALONG WITH  THE  PRESENT AND  SUGGESTED    *
* MODULO.  A 'N' RESPONSE, IN ADDITION, WILL *
* PLACE THE REALLOCATION PARAMETERS ON LINE  *
* 13 OF THE FILE DEFINITION ITEM.  FILE      *
* DEFINITION ITEMS WITH A PERIOD ON LINE 13  *
* WILL NOT BE REALLOCATED IN THIS MANNER.    *
* IN ADDITION, A 'O' OR 'B' ENTRY ON LINE 13 *
* WILL NOT BE OVERWRITTEN, BUT WILL BE FOL-  *
* LOWED BY THE REALLOCATION PARAMETERS       *
* (UNLESS FOLLOWED BY A PERIOD, OF COURSE).  *
* THE CHANGE IN THE MODULO WILL TAKE PLACE   *
* WHEN A FILE-RESTORE IS MADE WITH THESE     *
* REALLOCATION PARAMETERS PRESENT.           *
*                                            *
**********************************************
*
101   PRINT "*****  FILE MODULO REALLOCATION PROGRAM  *****"
      PRINT "*                                            *"
      PRINT "*    THIS PROGRAM CALCULATES A NEW MODULO    *"
      PRINT "* PARAMETER FOR FILES, USING THE STAT-FILE   *"
      PRINT "* DICTIONARY CREATED DURING A FILE-SAVE.     *"
      PRINT "* THE PROGRAM PROMPTS WITH: IS THIS A TEST?  *"
      PRINT "* A 'Y' RESPONSE WILL DISPLAY  THE FILES     *"
      PRINT "* ALONG WITH  THE  PRESENT AND  SUGGESTED    *"
      PRINT "* MODULO.  A 'N' RESPONSE, IN ADDITION, WILL *"
      PRINT "* PLACE THE REALLOCATION PARAMETERS ON LINE  *"
      PRINT "* 13 OF THE FILE DEFINITION ITEM.  FILE      *"
      PRINT "* DEFINITION ITEMS WITH A PERIOD ON LINE 13  *"
      PRINT "* WILL NOT BE REALLOCATED IN THIS MANNER.    *"
      PRINT "* IN ADDITION, A 'O' OR 'B' ENTRY ON LINE 13 *"
      PRINT "* WILL NOT BE OVERWRITTEN, BUT WILL BE FOL-  *"
      PRINT "* LOWED BY THE REALLOCATION PARAMETERS       *"
      PRINT "* (UNLESS FOLLOWED BY A PERIOD, OF COURSE).  *"
      PRINT "* THE CHANGE IN THE MODULO WILL TAKE PLACE   *"
      PRINT "* WHEN A FILE-RESTORE IS MADE WITH THESE     *"
      PRINT "* REALLOCATION PARAMETERS PRESENT.           *"
      PRINT "* ONLY THOSE FILES WHOSE MODULO NEEDS TO BE  *"
      PRINT "* ENLARGED WILL BE CHANGED.                  *"
      PRINT "**********************************************"
50    O.ACC='XXXXXXXXXXXXXXXXXXXXXXXX'
      QFILE1='QFILE1'
      QFILE2='QFILE2'
      J=0
      MOD.SUM=0
      DIM STAT(40)
51    PRINT 'IS YOUR SYSTEM A SERIES 18/XXX (Y/N)':
      INPUT FLAG
      BEGIN CASE
         CASE FLAG = "END"
            STOP
         CASE FLAG = "Y"
            FRAME.SIZE = 1000
         CASE FLAG = "N"
            FRAME.SIZE = 500
         CASE 1
            GOTO 51
      END CASE
      PRINT
      PRINT 'IS THIS A TEST (Y/N)':
      INPUT ANS
      PRINT CHAR(12)
      OPEN '','STAT-FILE' TO SFILE ELSE PRINT 'STAT-FILE DOES NOT EXIST';STOP
      SELECT SFILE
      OPEN 'DICT','MD' TO MD ELSE PRINT 'MD DOES NOT EXIST';STOP
      OPEN '','CONTROL' TO CONTROL ELSE PRINT 'CANNOT LOCATE CONTROL';STOP
      AM=CHAR(254)
***** START MAIN LOOP THROUGH FILES *****
1     READNEXT ID ELSE GOTO 99
      MATREAD STAT FROM SFILE,ID ELSE PRINT ID:' NOT ON FILE';GOTO 1
      MOD=STAT(4)
      IF MOD + 0 = 0 THEN GOTO 1
      DESC=STAT(1)
      ACCOUNT=FIELD(DESC,'*',1)
      READ MCNTL FROM CONTROL, 'MENUS.CONTROL' ELSE MCNTL = 'PRIMAC'
      M.LEN = LEN(MCNTL<1>)
      READ OTH.PMC.ACCT FROM CONTROL, "OTHER.PRIMAC.ACCOUNTS" ELSE
           OTH.PMC.ACCT = ""
      END
*     BEGIN CASE
*     CASE ACCOUNT[1,3] = "CBA"
*     CASE ACCOUNT[1,M.LEN] = MCNTL<1>
*     CASE 1
*        LOCATE ACCOUNT IN OTH.PMC.ACCT,1 SETTING FND ELSE GOTO 1
*     END CASE
      IF ANS#'Y' THEN
         IF O.ACC#ACCOUNT THEN
            WRITE 'Q':AM:ACCOUNT:AM:AM:'.' ON MD,QFILE1
            OPEN 'DICT',QFILE1 TO FMD ELSE PRINT ACCOUNT:' NOT FOUND';GOTO 1
            O.ACC=ACCOUNT
         END
      END
      FILE = FIELD(DESC,'*',2)
      I=3
3     DLID=FIELD(DESC,'*',I)
      IF DLID='' THEN GOTO 2
      IF DLID='DL/ID' THEN GOTO 2
      FILE=FILE:'*':DLID
      I=I+1
      GOTO 3
2*COMPUTE NEW MODULO
      IF FILE=ACCOUNT THEN DLID=ACCOUNT; ACCOUNT='SYSTEM'; FILE=''
      SIZE=STAT(7)
      ITEMS=STAT(6)
      SIZE=SIZE/FRAME.SIZE
      IF ITEMS<SIZE THEN SIZE=ITEMS
      SIZE=INT(SIZE)+1
21    HALF=SQRT(SIZE)+1
      FOR X=2 TO HALF
         AP=SIZE/X
         IAP=INT(AP)
         IF IAP=AP THEN SIZE=SIZE+1;GO 21
      NEXT X
      IF MOD>=SIZE THEN GOTO 1
      MOD.SUM=MOD.SUM+SIZE-MOD
      PRINT ACCOUNT:' ':FILE:' ':DLID:' ':MOD:' ---> ':SIZE
      IF ANS#'Y' THEN
         IF DLID#'' THEN
            WRITE 'Q':AM:ACCOUNT:AM:FILE:AM:'.' ON MD,QFILE2
            OPEN 'DICT',QFILE2 TO FILE.DICT ELSE PRINT FILE:' NO FIND';GOTO 1
            READV A1 FROM FILE.DICT,DLID,1 ELSE PRINT DLID:' NOT FOUND';GOTO 1
            IF A1#'D' AND A1#'DY' AND A1#'DX' THEN GOTO 1
            READV A13 FROM FILE.DICT,DLID,13 ELSE PRINT DLID:' NOT FOUND';GOTO 1
            IF INDEX(A13,'.',1) THEN GOTO 1
            A=A13[1,1]
            IF A#'B' AND A#'O' THEN A=''
            MOD.SEP='(':SIZE:',1)'
            WRITEV A:MOD.SEP ON FILE.DICT,DLID,13
         END ELSE
            READV A1 FROM FMD,FILE,1 ELSE PRINT FILE:' NOT FOUND'; GOTO 1
            IF A1#'D' AND A1#'DY' AND A1#'DX' THEN GOTO 1
            READV A13 FROM FMD,FILE,13 ELSE PRINT FILE:' NOT FOUND'; GOTO 1
            IF INDEX(A13,'.',1) THEN GOTO 1
            A=A13[1,1]
            IF A#'B' AND A#'O' THEN A=''
            MOD.SEP='(':SIZE:',1)'
            WRITEV A:MOD.SEP ON FMD,FILE,13
         END
      END
      J=J+1
      GOTO 1
99    PRINT J:' FILES REALLOCATED, A MINIMUM OF ':MOD.SUM "L,":' ADDITIONAL FRAMES REQUIRED'
      PRINT;PRINT;PRINT"   HIT RETURN TO GO BACK TO SYSTEM MAINTENANCE SELECTOR:":;INPUT ANS
   END
