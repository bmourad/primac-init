*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM        - INVOICE.CODE.MAINT
*
* BY             - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE           - 05/31/84
*
* DESCRIPTION    -
*                  This program will be used for invoice code
*                  maintenance.
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>INVOICE.CODE
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
   OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
   OPEN '','INVOICE.CODE' TO INVOICE.CODE ELSE ERRMSG = 'INVOICE.CODE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
   MAT EDIT.COM.DRIVER = ""
   MAT INC.REC = ''
   ERRMSG = ''
*
*---- GET COMPANY NUMBER
*
   CONO = ''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = 'END' THEN GOTO 99999
*
*---- PRINT SCREEN
*
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'INVOICE.CODE.MAINT'
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
   ECD.NUM = 1
   ECD.ACTION = 4; CALL SCRN.EDIT
   INVOICE.CODE.KEY = ECD.RET.VALUE
   BEGIN CASE
      CASE ECD.RET.VALUE = 'END'
         GOTO 99999
      CASE ECD.RET.VALUE # ''
         NEW.INVOICE.CODE = 'NO'
      * T26126 v
         MATREADU INC.REC FROM INVOICE.CODE,CONO:INVOICE.CODE.KEY LOCKED
            ERRMSG = 'INVOICE CODE record is locked by user - ':GETUSERNAME(STATUS())
            GOSUB 91000;GOTO 10 
         END ELSE
      * T26126 ^
            MAT INC.REC = ''
            NEW.INVOICE.CODE = 'YES'
         END
   END CASE
   IF NEW.INVOICE.CODE = 'YES' THEN
      J = 0
      FOR J = 1 TO 3
         ON J GOSUB 20,30,40
         IF ECD.RET.VALUE = 'END' THEN
            RELEASE INVOICE.CODE,CONO:INVOICE.CODE.KEY
            GOSUB 300
            GOTO 10
         END
      NEXT J
   END ELSE
      GOSUB 100
      ECD.ACTION = 3; CALL SCRN.EDIT
   END
   GOSUB 200
   GOSUB 300
   GOTO 10
*
*---- ENTER INVOICE CODE DESCRIPTION
*
20 ECD.NUM = 2
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      INC.DESC = ECD.RET.VALUE
   END
   RETURN
*
*---- ENTER INVOICE CODE CATEGORY
*
30 ECD.NUM = 3
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      INC.CATEGORY = ECD.RET.VALUE
   END
   RETURN
*
*---- ENTER HIDDEN LINE
*
40 ECD.NUM = 5
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      INC.HIDDEN = ECD.RET.VALUE
   END
   RETURN
*
*---- GET ALL VALUES FROM FILE
*
100 SCV.REC(2)<ECD.SCRN.NO,1> = INC.DESC
   SCV.REC(3)<ECD.SCRN.NO,1> = INC.CATEGORY
   SCV.REC(5)<ECD.SCRN.NO> = INC.HIDDEN
   RETURN
*
*---- ENTER OPTIONS
*
200 MORE = 1
   LOOP
      ECD.NUM = 4
      ECD.ACTION = 4; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = 'END' OR OPTION = 'E'
            RELEASE INVOICE.CODE,CONO:INVOICE.CODE.KEY
            MORE = 1
         CASE OPTION = '1'
            GOSUB 20
            MORE = 0
         CASE OPTION = '2'
            GOSUB 30
            MORE = 0
         CASE OPTION = '3'
            GOSUB 40
            MORE = 0
         CASE OPTION = 'F'
            GOSUB 400
            MORE = 1
      END CASE
   WHILE MORE = 0 DO REPEAT
   RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300 ECD.ACTION = 6; CALL SCRN.EDIT
   MAT SCV.REC = ""
   RETURN
*
*---- UPDATE INVOICE.CODES FILE
*
400 MATWRITE INC.REC ON INVOICE.CODE,CONO:INVOICE.CODE.KEY
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
