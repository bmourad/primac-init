      SUBROUTINE CUST.PURGED.INV.INQ(CONO,BEG.PURGE.DATE,END.PURGE.DATE,VALID.PURGED.IDS)
***************************************************************************
* REVISION    - [11.0]
* Copyright 1999 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - R12BP
* PROGRAM       - CUST.PURGED.INV.INQ
* BY            - FROM CUST.INV.INQ CREATED BY JIHAD YAMOUT, C.B.A
* DATE          - 07/20/99
* DESCRIPTION   - THIS SUBROUTINE DISPLAYS ALL PURGED INVOICES FOR ONE
*                 CUSTOMER. (SEE T25763)
*
* LAST COMPILE - 355
*C34331 edvard 09/21/1999 * Fixed the display of the description
*                           problem.
*ENDDOC
***************************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.CUST
*COPY>CPYLIB>EDIT.COM
*COPY>ARS.CPYLIB>OPEN.RECV
*COPY>ARS.CPYLIB>PURGED.OPEN.RECV
*COPY>JCS.CPYLIB>INVOICE 
*COPY>ARS.CPYLIB>MANUAL.INVOICE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
OPEN "","PURGED.OPEN.RECV" TO PURGED.OPEN.RECV ELSE ERRMSG = "CANNOT OPEN PURGED.OPEN.RECV";GOTO 93000
*** INT
BEGIN.PAGE = 7
PAGE.SIZE = 7
LINE.SPACE = 2
LINES = 0
OLD.START.LINE=0
*
**** SETUP FOR SYSTEM ERRMSGS
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
CUST.KEY=SCV.REC(1)<ECD.SCRN.NO>
SCV.REC(2)<ECD.SCRN.NO> = CUST.NAME
ECD.ACTION = 3; CALL SCRN.EDIT
LINES = DCOUNT(VALID.PURGED.IDS, AM)
LN = 1
IF LINES > 0 THEN
   GOSUB 10000
END
MORE = 1
LOOP
   ECD.SCRN.NO = 9
   ECD.NUM = 3 ; SCV.REC(3)<ECD.SCRN.NO> = '' ; ECD.ACTION = 4 ; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = '' OR ECD.RET.VALUE = 'END' OR ECD.RET.VALUE = 'E'
         MORE = 0
      CASE ECD.RET.VALUE = "SR"
         LN = LN - PAGE.SIZE
         IF LN < 1 THEN LN = 1
         GOSUB 10000
      CASE ECD.RET.VALUE = "ST"
         LN = 1
         GOSUB 10000
      CASE ECD.RET.VALUE = "SB"
         LN = LINES
         GOSUB 10000
      CASE ECD.RET.VALUE = "S" OR ECD.RET.VALUE = "SF"
         LN = LN + PAGE.SIZE
         IF LN > LINES THEN LN = 1
         GOSUB 10000
      CASE NUM(ECD.RET.VALUE)
         IF LAST.LINE > LINES THEN ELINE = LINES ELSE ELINE = LAST.LINE
         IF ECD.RET.VALUE >= START.LINE AND ECD.RET.VALUE <= ELINE THEN
            * GET SCREEN WITH DETAIL DISPLAY
            ECD.SCRN.NO = 10
            SCV.REC(1)<ECD.SCRN.NO> = VALID.PURGED.IDS<ECD.RET.VALUE>[4,99]
            SCV.REC(2)<ECD.SCRN.NO> = SCV.REC(1)<9>
            SCV.REC(3)<ECD.SCRN.NO> = SCV.REC(2)<9>
            CALL PURGED.INV.INQ.DET(CONO,VALID.PURGED.IDS<ECD.RET.VALUE>)
            ECD.SCRN.NO = 9
            ECD.ACTION = 2 ; CALL SCRN.EDIT
            OLD.START.LINE = 0;GOSUB 10000
            ECD.ACTION = 3 ; CALL SCRN.EDIT
         END
   END CASE
WHILE MORE DO REPEAT
GOTO 99999
*
***** SCROLL ROUTINE
*
10000*
      START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
      IF START.LINE=OLD.START.LINE THEN GOTO 10001
      OLD.START.LINE=START.LINE
      LAST.LINE=START.LINE + PAGE.SIZE - 1
      CNT=1
      FOR N=START.LINE TO LAST.LINE UNTIL N > LINES
         MATREAD POR.REC FROM PURGED.OPEN.RECV, VALID.PURGED.IDS<N> ELSE
            MAT POR.REC = ""
         END
         SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
         P_X  = 0 ; P_Y = SLN ; P_VALUE = N "L#3"
         P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:TRIM(VALID.PURGED.IDS<N>[4,99]) "L#8"
         P_X  := AM:13 ; P_Y := AM:SLN ; P_VALUE := AM:POR.DIV "L#2"
         LAST.TRAN.DATE = DCOUNT(POR.TR.DATE,VM)
         IF LAST.TRAN.DATE < 1 THEN
            POR.DTP = "????"
         END ELSE
            POR.DTP = POR.TR.DATE<1,LAST.TRAN.DATE> - POR.INV.DATE
         END
         P_X  := AM:17 ; P_Y := AM:SLN ; P_VALUE := AM:POR.DTP "R#4"
         P_X  := AM:23 ; P_Y := AM:SLN ; P_VALUE := AM:POR.JOB "L#8"
         P_X  := AM:36 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(POR.INV.DATE , "D2/") "R#8"
         AMT = POR.INV.AMT<1,1>
         FOR I = 2 TO 6
            IF POR.TYPE<1,I> = "T" OR POR.TYPE<1,I> = "S" OR POR.TYPE<1,I> = "G" OR POR.TYPE<1,I> = "U" OR POR.TYPE<1,I> = "M" THEN
               AMT = AMT + POR.INV.AMT<1,I>
            END
         NEXT I
         P_X := AM:45 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(AMT, "MD2") "R#12"
         P_X  := AM:58 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(AMT - POR.BAL, "MD2") "R#12"
         P_X  := AM:23 ; P_Y := AM:SLN+1 ; P_VALUE := AM:POR.PO "L#12"
         P_X  := AM:36 ; P_Y := AM:SLN+1 ; P_VALUE := AM:POR.INV.DESC "L#34" 
         P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         CNT=CNT + 1
      NEXT N
      FOR M=CNT TO PAGE.SIZE
         SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
         P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT M
10001 RETURN
***** CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
      ECD.ACTION=99;CALL SCRN.EDIT
      RETURN
   END
