  SUBROUTINE DIVISION.ASSIGN(CONO,MAT SEC.REC,USERID)
*COPY>CPYLIB>COM1
*********************************************************************
* REVISION     - [011.0]
* Copyright 1998 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - CBABP
* PROGRAM      - DIVISION.ASSIGN
* BY           - MARK THOMASY, COMPUTER BUSINESS ASSOCIATES
* DATE         - 09/18/1998
* DESCRIPTION  - 
*                This program sets the divisional security per system user
*T25978 adelgado 01/29/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SECURITY
*COPY>PMC.CPYLIB>DIVISION
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
**************
* OPEN FILES *
**************
  OPEN '','DIVISION' TO DIVISION ELSE
    ERRMSG = 'DIVISION FILE IS MISSING'
    GOTO 93000
  END
**************
* INITIALIZE *
**************
1 *
******************
  ERRMSG = ""
  LINE.SPACE = 1
  PAGE.SIZE = 12
  MENU.DEFAULT = ''
  BEGIN.PAGE = 8
  REQUEST = ''
  LINES=0;START.LINE=0;OLD.START.LINE=0;LAST.LINE=0
  ECD.SCRN.NO = 5
*******************
* MAIN PROCESSING *
*******************
100 *
  SCV.REC(8)<ECD.SCRN.NO> = CONO
  SCV.REC(9)<ECD.SCRN.NO> = USERID
  SCV.REC(10)<ECD.SCRN.NO> = SEC.NAME
  ECD.ACTION = 3 ; CALL SCRN.EDIT
  LINES = DCOUNT(SEC.DIVISION,VM)
  LN = 1
  GOSUB 11000
200 *
  ECD.NUM = 5; ECD.ACTION=4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = 'E' OR ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE = 'A'
      LOOP
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 14000
      WHILE LINES > OLD.LINES DO REPEAT
      LN = LINES
    CASE ECD.RET.VALUE = 'C'
      GOSUB 15000
      IF LNO THEN
        LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
        P_X  = 40 ; P_Y = 22 ; P_VALUE = STR('-',39) ; P_OPT = ""
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        DEFAULT = SEC.DIVISION<1,LN>
        GOSUB 14100
      END
    CASE ECD.RET.VALUE = 'D'
      GOSUB 15000
      IF LNO THEN
        LN = LNO
        SEC.DIVISION = DELETE(SEC.DIVISION,1,LN,0)
        LINES = DCOUNT(SEC.DIVISION,VM)
        IF LN > LINES THEN LN = LINES
        IF LN < 1 THEN LN = 1
        OLD.START = 0
      END
      GOSUB 11000
    CASE ECD.RET.VALUE = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
      GOSUB 11000
    * T25978 v             
    CASE ECD.RET.VALUE = 'SR'     
      LN -= PAGE.SIZE      
      IF LN < 1 THEN LN = 1
      GOSUB 11000          
    CASE ECD.RET.VALUE = 'ST'     
      LN = 1               
      GOSUB 11000          
    CASE ECD.RET.VALUE = 'SB'     
      LN = LINES           
      GOSUB 11000          
    * T25978 ^             
  END CASE
  GOTO 200
*
11000 *
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
*      IF START.LINE = OLD.START.LINE THEN GOTO 11999
  OLD.START.LINE = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  CNT = 1
  IF SEC.DIVISION<1> = '' THEN
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
    P_X  = 15 ; P_Y = SLN ; P_VALUE = '' ; P_OPT = ''
    P_X := AM:21 ; P_Y := AM:SLN ; P_VALUE := AM:"ALL"
    P_X := AM:27 ; P_Y := AM:SLN ; P_VALUE := AM:"FULL DIVISION ACCESS"
    CALL VSI_PLINE(P_X, P_Y, P_VALUE, P_OPT)
    GOTO 11999
  END
  IF START.LINE = 1 THEN LCT = START.LINE
  FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 15 ; P_Y = SLN ; P_VALUE = LCT "R#3" ; P_OPT = ""
    P_X  := AM:21 ; P_Y := AM:SLN ; P_VALUE := AM:SEC.DIVISION<1,N>
    MATREAD DIV.REC FROM DIVISION, CONO:SEC.DIVISION<1,N> ELSE
      DIV.DESC = 'DIVISION NOT ON FILE'
    END
    P_X  := AM:27 ; P_Y := AM:SLN ; P_VALUE := AM:DIV.DESC
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    LCT = LCT + 1
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
11999 *
  RETURN
*
*
14000 *
  GOSUB 11000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 15 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
14100*
  X = 21; Y = SLN; TYP = 1; MAXL = 3; HMSG = "Division"; O.R = 'O'
  CALL EDIT.SUB
  IF VALUE = "END" THEN
    IF ECD.RET.VALUE = "A" THEN
      SEC.DIVISION = DELETE(SEC.DIVISION,1,LN,0)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    OLD.START = 0
    GOTO 14999
  END
  IF VALUE = "ALL" THEN
    ECD.ACTION = 6; CALL SCRN.EDIT
    SCV.REC(8)<ECD.SCRN.NO> = CONO
    SCV.REC(9)<ECD.SCRN.NO> = USERID
    SCV.REC(10)<ECD.SCRN.NO> = SEC.NAME
    ECD.ACTION = 3; CALL SCRN.EDIT
    P_X  = 15 ; P_Y = 8 ; P_VALUE = '' ; P_OPT = ''
    P_X := AM:21 ; P_Y := AM:8 ; P_VALUE := AM:"ALL"
    P_X := AM:27 ; P_Y := AM:8 ; P_VALUE := AM:"FULL DIVISION ACCESS"
    CALL VSI_PLINE(P_X, P_Y, P_VALUE, P_OPT)
    SEC.DIVISION = ""; LINES = 0
    GOTO 14999
  END
14200 *
  FOR XYZ = 1 TO LINES
    IF SEC.DIVISION<1,XYZ> = VALUE THEN
      ERRMSG = 'DIVISION CODE ALREADY USED'
      GOSUB 91000 ; GOTO 14100
    END
  NEXT XYZ
  MATREAD DIV.REC FROM DIVISION, CONO:VALUE ELSE
    ERRMSG = 'DIVISION NOT ON FILE'
    GOSUB 91000 ; GOTO 14100
  END
  SEC.DIVISION<1,LN> = VALUE
  P_X  = 27 ; P_Y = SLN ; P_VALUE = DIV.DESC'L#25' ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LINES += 1
*
*
14999*
  RETURN
*
*--- GET LINE NUMBER
*
15000*
  GOSUB 11000
  ECD.MINV = START.LINE
  ECD.MAXV = LAST.LINE
  ECD.NUM = 6
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
  ECD.ACTION = 99; CALL SCRN.EDIT
END
