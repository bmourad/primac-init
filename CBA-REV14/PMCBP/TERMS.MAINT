*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* SOURCE         - PMCBP
*
* PROGRAM        - TERMS.MAINT
*
* BY             - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE           - 02/28/85
*
* DESCRIPTION    -
* This program will maintain and update TERMS file.
*
*T25764 renee 07/19/2001 * Sheridan mod - Add "due days" field (to be
*                          used in calculating the due date in all
*                          invoicing functions and AR aging report).
*T25764 renee 07/19/2001 * Include "Days Due " prompt in initial entry
*                          of a terms code.
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>TERMS
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
  OPEN '','TERMS' TO TERMS ELSE ERRMSG = 'TERMS FILE MISSING';GOTO 93000
  OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  ERRMSG = ""
  UNKNOWN = STR("?",30)
*
*---- GET COMPANY NUMBER
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  MAT EDIT.COM.DRIVER = ""
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'TERMS.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
  SCV.REC(8)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=8
  ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  TERMS.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.TERMS = "NO"
      * T26126 v
      MATREADU TERMS.REC FROM TERMS,CONO:TERMS.KEY LOCKED
        ERRMSG = 'TERMS record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
      * T26126 ^
        MAT TERMS.REC=''
        NEW.TERMS = 'YES'
      END
  END CASE
  IF NEW.TERMS = 'YES' THEN 
* T25764 v Include new field in initial entry
*       FOR S = 1 TO 5
    FOR S = 1 TO 6
*          ON S GOSUB 10,20,30,40,50
      ON S GOSUB 10,20,30,40,50,60
* T25764 ^
      IF ECD.RET.VALUE = 'END' THEN 
        RELEASE TERMS,CONO:TERMS.KEY
        GOSUB 300;GOTO 5
      END
    NEXT S
  END ELSE
    GOSUB 100
  END
  GOSUB 200
  GOSUB 300
  GOTO 5
*
*---- ENTER TERMS DESCRIPTION
*
10*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    TERMS.DESC = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER TERMS PERCENT
*
20*
  ECD.NUM = 3
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    IF ECD.RET.VALUE > "9999" THEN
      ERRMSG = "CANNOT BE GREATER THAN 99.99 %";GOSUB 91000;GOTO 20
    END
    TERMS.PER = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER TERMS DAYS ALLOWED
*
30*
  ECD.NUM = 4
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    TERMS.DAYS.ALLOWED = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER TERMS DAY OF MONTH ALLOWED
*
40*
  ECD.NUM = 5
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    TERMS.D.OF.M.ALLOWED = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER GENERAL LEDGER NUMBER
*
50*
  SCV.REC(6)<ECD.SCRN.NO,1> = ""
  ECD.NUM=6;ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 6
  ECD.MAXL = LEN(CO.ACCT.PIC) ; ECD.PATRN = CO.ACCT.MATCH
  IF TERMS.G.L.CODE # "" THEN ECD.DEFAULT = TERMS.G.L.CODE
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    TERMS.G.L.CODE = ECD.RET.VALUE
    IF ECD.RET.VALUE # "" THEN
      SCV.REC(6)<ECD.SCRN.NO,1> = TERMS.G.L.CODE CO.ACCT.MASK
      ECD.NUM = 6;ECD.ACTION = 5;CALL SCRN.EDIT
      MATREAD COA.REC FROM COA, CONO : TERMS.G.L.CODE ELSE
        ERRMSG = "INVALID GENERAL LEDGER NUMBER";GOSUB 91000;GOTO 50
      END
      SCV.REC(9)<ECD.SCRN.NO,1> = COA.DESC
      ECD.NUM = 9;ECD.ACTION = 5;CALL SCRN.EDIT
    END ELSE
      SCV.REC(6)<ECD.SCRN.NO,1>=""
      ECD.NUM=6;ECD.ACTION=5;CALL SCRN.EDIT
      SCV.REC(9)<ECD.SCRN.NO,1>=""
      ECD.NUM=9;ECD.ACTION=5;CALL SCRN.EDIT
    END
  END ELSE
    SCV.REC(6)<ECD.SCRN.NO,1>=""
    ECD.NUM=6;ECD.ACTION=5;CALL SCRN.EDIT
    SCV.REC(9)<ECD.SCRN.NO,1>=""
    ECD.NUM=9;ECD.ACTION=5;CALL SCRN.EDIT
  END
  RETURN
* T25764 v Enter DUE DAYS
*
*---- ENTER DUE DAYS 
*
60*
  ECD.NUM = 10
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    TERMS.DUE.DAYS = ECD.RET.VALUE
  END 
  RETURN
* T25764 ^
*
*---- GET VALUES FROM FILE
*
100*
  IF TERMS.G.L.CODE # "" THEN
    MATREAD COA.REC FROM COA, CONO : TERMS.G.L.CODE ELSE
      MAT COA.REC = ""; COA.DESC = UNKNOWN
    END
    GL.NUMBER = TERMS.G.L.CODE CO.ACCT.MASK
  END ELSE
    COA.DESC = ""
    GL.NUMBER = ""
  END
  SCV.REC(2)<ECD.SCRN.NO,1> = TERMS.DESC
  SCV.REC(3)<ECD.SCRN.NO,1> = TERMS.PER
  SCV.REC(4)<ECD.SCRN.NO,1> = TERMS.DAYS.ALLOWED
  SCV.REC(5)<ECD.SCRN.NO,1> = TERMS.D.OF.M.ALLOWED
  SCV.REC(6)<ECD.SCRN.NO,1> = GL.NUMBER
  SCV.REC(9)<ECD.SCRN.NO,1> = COA.DESC
  SCV.REC(10)<ECD.SCRN.NO,1> = TERMS.DUE.DAYS     ; * T25764
  ECD.ACTION = 3;CALL SCRN.EDIT
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 0
  LOOP
    ECD.NUM = 7
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE TERMS,CONO:TERMS.KEY   ;* T26126
        MORE = 1
      CASE NUM(OPTION)
        ON OPTION GOSUB 10,20,30,40,50,60   ; * T25764 Added ",60"
      CASE OPTION = "D"
        DELETE TERMS,CONO:TERMS.KEY
        MORE = 1
      CASE OPTION = 'F'
        GOSUB 400
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE TERMS FILE
*
400*
  MATWRITE TERMS.REC ON TERMS,CONO:TERMS.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
