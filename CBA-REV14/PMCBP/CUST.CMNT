   SUBROUTINE CUST.CMNT(CONO,MODE)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.CUST
*T25889 ajibaly 02/04/2002 * MOVE TO REV12
*T23898 randy 05/12/1999 * ADD S, SR, ST, AND SB FUNCTIONS TO COMMENTS
*                          SCREEN
*T23898 randy 05/07/1999 * MOVED THIS PROGRAM FROM REV11 COMMUNICOLOR TO
*                          ALLOW NAHAN TO BE ABLE TO PUT IN COMMENTS
*                          DURING CUSTOMER MAINTENANCE.
**********************************************
* REVISION    - [11.2]
* Copyright 1982 by Vercom Software, Inc.
* SYSTEM      - PRIMAC
* SOURCE      - COMMUNBP
* PROGRAM     - CUST.CMNT
* BY          - RIK REED, VERCOM SOFTWARE
* DATE        - 08/15/98
*T23065 rik 08/15/1998 * ALLOW CUSTOMER COMMENT SCROLLING AND EDITING.
**********************************************
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZE
*
   BEGIN.PAGE=6; PAGE.SIZE=14; LINE.SPACE=1
   LN=1; OLD.START.LINE=0
   LINES=DCOUNT(CUST.COMMENT,VM)
   GOSUB 4000
*
*---- OPTIONS
*
   MORE=1
   LOOP
      IF MODE='M' THEN
         ECD.NUM=30
*T25889 v
      END ELSE IF MODE='I' THEN
         ECD.NUM=34
*T25889 ^
      END ELSE
         ECD.NUM=33
      END
      SCV.REC(ECD.NUM)<ECD.SCRN.NO>=''
      ECD.ACTION=4; CALL SCRN.EDIT
      OPTION=ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION='' OR OPTION='E' OR OPTION='END'
            MORE=0
         CASE OPTION = 'F'
            WRITEV CUST.COMMENT ON CUSTOMER,CONO:SCV.REC(2)<ECD.SCRN.NO>,32
            MORE = 0
         CASE OPTION='A' AND MODE # "I"
            LOOP
               LN=LINES + 1
               OLD.LINES=LINES
               GOSUB 2000
            WHILE LINES > OLD.LINES DO REPEAT
            LN=LINES
            GOSUB 4000
         CASE OPTION='C' AND MODE # "I"
            GOSUB 3000
            IF LN.NO THEN
               LN=LN.NO
               GOSUB 2000
            END
         CASE OPTION='D' AND MODE # "I"
            GOSUB 3000
            IF LN.NO THEN
               LN=LN.NO
               CUST.COMMENT=DELETE(CUST.COMMENT,1,LN,0)
               LINES=DCOUNT(CUST.COMMENT,VM)
               IF LN > 1 AND LN > LINES THEN LN=LN - 1
               IF LN < 1 THEN LN=1
               OLD.START.LINE=0
            END
            GOSUB 4000
         CASE OPTION='I' AND MODE # "I"
            GOSUB 3000
            IF LN.NO THEN
               LN=LN.NO
               CUST.COMMENT=INSERT(CUST.COMMENT,1,LN,0,'')
               LINES=DCOUNT(CUST.COMMENT,VM)
               OLD.START.LINE=0
               GOSUB 2000
               IF VALUE='END' THEN
                  CUST.COMMENT=DELETE(CUST.COMMENT,1,LN,0)
                  LINES=DCOUNT(CUST.COMMENT,VM)
                  OLD.START.LINE=0; GOSUB 4000
               END
            END
         CASE OPTION='S'
            LN=LN+PAGE.SIZE-1
            LN=(PAGE.SIZE*INT(LN/PAGE.SIZE))+1
            IF LN > LINES THEN LN=1
            GOSUB 4000
*T23898 v
         CASE OPTION='SR'
            LN=LN-PAGE.SIZE
            LN=(PAGE.SIZE*INT(LN/PAGE.SIZE))+1
            IF LN < 1 THEN LN=1
            GOSUB 4000
         CASE OPTION='ST'
            LN=1
            GOSUB 4000
         CASE OPTION='SB'
            LN=LINES
            GOSUB 4000
*T23898 ^
      END CASE
   WHILE MORE DO REPEAT
   GOTO 99999
*
*---- ENTER CUSTOMER COMMENTS
*
2000 *
   GOSUB 4000
   SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
   P_X  = 0 ; P_Y = SLN ; P_VALUE = LN 'R#2' ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
2100 *
   X=3; Y=SLN; TYP=1; MAXL=70; O.R='O'
   DEFAULT=CUST.COMMENT<1,LN>
   CALL EDIT.SUB
   IF VALUE='END' THEN
      OLD.START.LINE=0; GOSUB 4000
      GOTO 2999
   END
   CUST.COMMENT<1,LN>=VALUE
   LINES=DCOUNT(CUST.COMMENT,VM)
2999 *
   RETURN
*
*---- GET LINE NUMBER
*
3000 *
   GOSUB 4000
   ECD.NUM=31; SCV.REC(ECD.NUM)<ECD.SCRN.NO>=''
   ECD.MINV=OLD.START.LINE; ECD.MAXV=LAST.LINE
   ECD.ACTION=4; CALL SCRN.EDIT
   LN.NO=ECD.RET.VALUE
   IF LN.NO='' OR LN.NO='END' THEN LN.NO=0
   RETURN
*
*---- SCROLL
*
4000 *
   START.LINE=1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
   LAST.LINE=START.LINE + PAGE.SIZE - 1
   IF LAST.LINE > LINES THEN LAST.LINE=LINES
   IF START.LINE=OLD.START.LINE THEN GOTO 4999
   OLD.START.LINE=START.LINE
   CNT=1
   FOR N=START.LINE TO LAST.LINE
      SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N 'R#2' ; P_OPT = "CL"
      P_X  := AM:3 ; P_Y := AM:SLN ; P_VALUE := AM:CUST.COMMENT<1,N> 'L#70'
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT=CNT + 1
   NEXT N
   FOR M=CNT TO PAGE.SIZE
      SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
4999 *
   RETURN
*
*---- END
*
91000 *
   ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000 *
   ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 *
   ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
   RETURN
END
