****************************************************************
* REVISION    - [08.1]
* COPYRIGHT   - 1982 by C.B.A.   (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - VOID.INVOICE.REG.REF
* AUTHOR      - ANN POWLEY, C.B.A.
* DATE        - 09/09/87
* DESCRIPTION - This program will print voided invoice register.
*T22570 rick 03/31/1998 * Modify date that prints in Void Dte column to
*                         be void/del date
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
****************************************************************
*
**** FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>VOID.INVOICES
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** OPEN FILES
*
      OPEN "","VOID.INVOICES" TO VOID.INVOICES ELSE ERRMSG = "VOID.INVOICES FILE IS MISSING"; GOSUB 91000; GOTO 99999
      OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING"; GOSUB 91000; GOTO 99999
*
**** SETUP FOR SYSTEM ERROR MESSAGES
*
      SYS.TYPE=1
      CALL SYSCOM(MAT SYSCOM.REC)
*
**** PERFORM PROCREAD
*
      PROCREAD BUFFER ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOSUB 91000; GOTO 99999
      CONO = BUFFER<1>
      CO.NAME = BUFFER<2>
      FROM.DTE = BUFFER<3>
      TO.DTE = BUFFER<4>
      REPORT = BUFFER<5>
*T26943 v
*     REPORT.NAME = "VOIDED ": REPORT :" INVOICE REGISTER BY REFERENCE"
*     REPORT.NUMBER = "XXXX"
      CO.NAME = ""
      REPORT.NAME = ""
      REPORT.NUMBER = BUFFER<2>
*T26493 ^
      REPORT.DATE = DATE()
      HD1 = "";HD2 = "";PG.NO = 0
      CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** INTIALIZATION
*
      LINE.CNT = 99
      SP = SPACE(1)
      HD3 = SPACE(63) : "FROM " : FROM.DTE : " TO " : TO.DTE
      HD4 = " REF #   REF DATE CUST #            CUSTOMER            SLS PO/ORD/JOB VOID DTE"
      HD5 = "-------- -------- ------ ------------------------------ --- ---------- --------"
*
**** MAIN PROCESSING
*
      DATA = 1
      PRINTER ON
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         MATREAD VOI.REC FROM VOID.INVOICES, ID ELSE MAT VOI.REC = ''
         MATREAD CUST.REC FROM CUSTOMER , ID[1,3]:VOI.CUST.NO ELSE
            MAT CUST.REC = ""
         END
         IF CUST.NAME # "" AND VOI.CUST.NAME # CUST.NAME THEN
            VOI.CUST.NAME = CUST.NAME
         END
         GOSUB 200
      REPEAT
      GOTO 99999
*
**** PROCESS THE JOB
*
200*
      IF LINE.CNT > 55 THEN GOSUB 300
      REF = ID[4,99]
      IF VOI.JOB.NO # '' THEN JOB.PO = VOI.JOB.NO ELSE JOB.PO = VOI.PO
      H.LINE = ""
      H.LINE = H.LINE : REF "L#8" : SP
      H.LINE = H.LINE : OCONV(VOI.DATE, "D2/") "R#8" : SP
      H.LINE = H.LINE : VOI.CUST.NO "L#6" : SP
      H.LINE = H.LINE : VOI.CUST.NAME "L#30" : SP
      H.LINE = H.LINE : VOI.SLSMAN "L#3" : SP
      H.LINE = H.LINE : JOB.PO "L#10" : SP
*T22570 v
****      H.LINE = H.LINE : OCONV(VOI.DATE, "D2/") "R#8"
      H.LINE = H.LINE : OCONV(VOI.DEL.DATE, "D2/") "R#8"
*T22570 ^
      PRINT H.LINE
      LINE.CNT = LINE.CNT + 1
      RETURN
*
**** PRINT HEADINGS
*
300*
      PG.NO = PG.NO + 1
      PRINT CHAR(12)
      PRINT HD1 : PG.NO "R#4"
      PRINT HD2
      PRINT HD3
      PRINT
      PRINT HD4
      PRINT HD5
      LINE.CNT = 6
      RETURN
*
**** CALLS FOR SYSCOM
*
91000*
      ERR.TYPE=1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      PRINTER OFF
      ERR.TYPE=3
      CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF PROGRAM
99999*
      PRINTER OFF
      PRINT @(-1):
   END
