   SUBROUTINE REPORT.SCRN.NOTICE (RET_VALUE, IN_HEAD, IN_MESSAGE)
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - PMCBP
* PROGRAM       - REPORT.SCRN.NOTICE
* BY            - JIHAD YAMOUT, C.B.A
* DATE          - 01/28/86
* DESCRIPTION
*
* This program display the Warning message.
*ENDDOC
*********************************************************************
*
*---- FILE INSERTS
*
*COPY>CPYLIB>COM1
*COPY>CPYLIB>TCC
*COPY>CPYLIB>EDIT.COM
*COPY>PMC.CPYLIB>PMC_PROCESS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SETUP SYSTEM ERRMSGS
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- SET SCREEN TOP HEADING
*
   IF IN_HEAD = "" THEN
     IN_HEAD = "HEADING WAS NOT DEFINED"
   END
   H.LEN = LEN(IN_HEAD)
   IF H.LEN * 2 > 56 THEN
     SCRN.HEAD = IN_HEAD
   END ELSE
     SCRN.HEAD = IN_HEAD[1,1]
     FOR S = 2 TO H.LEN
       SCRN.HEAD = SCRN.HEAD:" ":IN_HEAD[S,1]
     NEXT S
   END
   POS = 56 - LEN(SCRN.HEAD)
   POS1 = INT(POS/2)
   IN_HEAD = SPACE(POS1):SCRN.HEAD
*
*---- MAIN PROCESSING
*
   IF PC.PORT.TYPE = "termulator" THEN
     TYPE = "DLG.WARNING"
     BUTTONS = "Continue":VM:"Exit"
     RETURNS = "Y":VM:"N"
     TITLE = SCRN.HEAD
     TU_FUNC = "TU.FORM.DIALOG":TU_VERNO
     CALL @TU_FUNC(VALUE, TYPE, IN_MESSAGE, BUTTONS, RETURNS, TITLE, ERROR)
     IF NOT(ERROR) THEN
       RET_VALUE = VALUE
     END ELSE
       RET_VALUE = "END"
     END
     GOTO 99999
   END
   FOR II = 1 TO SCV.REC.SIZE
     SCV.REC(II)<ECD.SCRN.NO> = ""
   NEXT II
   ECD.ACTION = 2 ; CALL SCRN.EDIT
*
*---- LOAD AND PRINT SCREEN
*
   ECD.NUM = 1; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = IN_HEAD
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 2; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = DATE()
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 4
   LINES = DCOUNT(IN_MESSAGE<1>,VM)
   FOR II = 1 TO LINES
     SCV.REC(ECD.NUM)<ECD.SCRN.NO,II> = IN_MESSAGE<1,II>
   NEXT II
   ECD.SUB.NUM = 1
   ECD.ACTION = 7; CALL SCRN.EDIT
   MORE = 1
   LOOP
     ECD.NUM = 3
     ECD.ACTION = 4; CALL SCRN.EDIT
     OPTION = ECD.RET.VALUE
     BEGIN CASE
       CASE OPTION = "E" OR OPTION = "END"
         RET_VALUE = "END"
         MORE = 0
       CASE OPTION = "Y"
         RET_VALUE = OPTION
         MORE = 0
       CASE OPTION = "N"
         RET_VALUE = OPTION
         MORE = 0
     END CASE
   WHILE MORE = 1 DO REPEAT
   GOTO 99999
*
*---- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
   RETURN
 END
