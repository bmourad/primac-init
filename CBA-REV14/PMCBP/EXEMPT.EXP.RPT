*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - EXEMPT.EXP.RPT
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC
* DATE        - 03/30/92
* DESCRIPTION -
*ENDDOC
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>PMC.CPYLIB>TAX
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- OPEN FILES
*
      OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG = "CUSTOMER IS MISSING"; GOTO 93000
      OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL IS MISSING"; GOTO 93000
      OPEN '','TAX' TO TAX ELSE ERRMSG = "TAX IS MISSING"; GOTO 93000
      OPEN '','SHIP.TO' TO SHIP.TO ELSE ERRMSG = "SHIP.TO IS MISSING"; GOTO 93000
      OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY IS MISSING"; GOTO 93000
*
*---- DEFINE HEADINGS
*
      PROCREAD BUFFER ELSE
         ERRMSG = 'CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      CONO = BUFFER<1>
      EXP.DAYS = BUFFER<3>
*T26493 v
*     CONO.NAME = BUFFER<2>
*     REPORT.NAME = "TAX EXEMPT EXPIRATION REPORT"
*     REPORT.NUMBER = "XXXX"
      CONO.NAME = ""
      REPORT.NAME = ""
      REPORT.NUMBER = BUFFER<2>
*T26493 ^
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
      S.HD1 = "CUST # SHP              NAME               TELEPHONE             TAX JURISDICTION            EXEMPT NUMBER  EXP DATE"
      S.UN1 = "------ --- ------------------------------ ------------ ------------------------------------ --------------- --------"
*
*---- INITIALIZATION
*
      FIRST.SHP = 1
      FIRST.CUST = 1
      LINE.CNT = 99
      UNKNOWN = STR('?',30)
      SP1 = SPACE(1)
      PAGE.NO = 0
      PRINTER ON
*
*---- MAIN PROCESSING 
*
      TODAY = DATE()
      EXP.DATE = TODAY + EXP.DAYS
      DONE = 0
      LOOP
         READNEXT ID ELSE DONE = 1
      UNTIL DONE DO
         FIRST.CUST = 1
         FIRST.SHP = 1
         MATREAD CUST.REC FROM CUSTOMER, ID ELSE
            MAT CUST.REC = ""
            CUST.NAME = UNKNOWN
         END
         CUSTNO = ID[4,99]
         GOSUB 500
         FOR T = 1 TO CUST.SHIP.TO
            SHPNO = STR("0",3-LEN(T)):T
            MATREAD SPT.REC FROM SHIP.TO, CONO:CUSTNO:"!":SHPNO THEN
               GOSUB 600
            END ELSE
               MAT SPT.REC = ""
            END
         NEXT T
         IF FIRST.CUST = 0 THEN
            PRINT; LINE.CNT = LINE.CNT + 1
         END
      REPEAT
      GOTO 99999
*
*---- PRINT DETAIL LINES
500 *
      TXCNT = DCOUNT(CUST.TAX.JUR,VM)
      FOR X = 1 TO TXCNT
         IF LINE.CNT > "55" THEN GOSUB 1000
         IF FIRST.CUST = 1 THEN
            LINE = CUSTNO "L#6" :SPACE(5)
            LINE = LINE : CUST.NAME "L#30" : SP1 
            LINE = LINE : CUST.PHONE "L#12" : SP1
         END ELSE
            LINE = SPACE(55)
         END
         IF CUST.EXMT.DATE<1,X> <= EXP.DATE AND CUST.EXMT.DATE<1,X> # "" THEN
            LINE = LINE : CUST.TAX.JUR<1,X> "L#5" : SP1
            MATREAD TAX.REC FROM TAX, CONO:CUST.TAX.JUR<1,X> ELSE
               TAX.JUR = UNKNOWN
            END
            LINE = LINE : TAX.JUR "L#30" : SP1
            LINE = LINE : CUST.EXEMPT<1,X> "L#15" : SP1
            LINE = LINE : OCONV(CUST.EXMT.DATE<1,X>,"D2/")"R#8" 
            LINE.CNT = LINE.CNT + 1
            PRINT LINE
            FIRST.CUST = 0
         END
      NEXT X
599 *
      RETURN
*
*---- PRINT SHIP TO LINES
600 *
      TXCNT = DCOUNT(SPT.TAX.JUR,VM)
      FOR X = 1 TO TXCNT
         IF LINE.CNT > "55" THEN GOSUB 1000
         IF FIRST.CUST THEN
            LINE = CUSTNO "L#6" : SP1
         END ELSE
            LINE = SPACE(7)
         END
         IF FIRST.SHP = 1 THEN
            LINE = LINE : SHPNO "L#3" :SP1
            LINE = LINE : SPT.NAME "L#30" : SP1 
            LINE = LINE : SPACE(13) 
         END ELSE
            LINE = SPACE(55)
         END
         IF SPT.EXMT.DATE<1,X> <= EXP.DATE AND SPT.EXMT.DATE<1,X> # "" THEN
            LINE = LINE : SPT.TAX.JUR<1,X> "L#5" : SP1
            MATREAD TAX.REC FROM TAX, CONO:SPT.TAX.JUR<1,X> ELSE
               TAX.JUR = UNKNOWN
            END
            LINE = LINE : TAX.JUR "L#30" : SP1
            LINE = LINE : SPT.EXEMPT<1,X> "L#15" : SP1
            LINE = LINE : OCONV(SPT.EXMT.DATE<1,X>,"D2/")"R#8" 
            LINE.CNT = LINE.CNT + 1
            FIRST.SHP = 0
            FIRST.CUST = 0
            PRINT LINE
         END
      NEXT X
699 *
      RETURN
*** PRINT HEADINGS
1000 *
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PRINT S.HD1
      PRINT S.UN1
      LINE.CNT = 5
      RETURN
*
* 91000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT X :
*       PRINT @(0,23) : CL :
*       RETURN
* 93000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT X :
*       PRINT @(0,23) : CL :
*
*--- CALLS FOR SYSCOM
*
91000 *
      ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
      ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
      ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
*
99999 PRINTER OFF
      END
