********************************************************************
* REVISION       - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM         - PRIMAC
* PROGRAM        - VEND.LISTING
* BY             - JIHAD YAMOUT ,C.B.A
* DATE           - 05/29/84
* DESCRIPTION    - This program print vendor listing or vendor analysis.
*T26493 cmykleb 03/27/2002 * Change pgm to use report # from proc.
********************************************************************
*
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
**** SET UP FOR SYSCOM
   SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
**** PROC READ CONO
   PROCREAD X ELSE ERRMSG = "THIS REPORT MUST BE RUN FROM PROC (VEND.LISTING)";GOTO 93000
   CONO = X<1>
   CONO.NAME = X<2>
   FLAG = X<3>
*
**** INIT
*
   TOT.OPEN = 0 ; TOT.MTD = 0 ; TOT.YTD = 0 ; TOT.LYTD = 0
   PAGE.NO = 0
   LINE.COUNT = 99
   SP1 = " "
*
**** SETUP REPORT HEADING
*
   IF FLAG = "A" THEN
*T26493     REPORT.NAME = "VENDOR ANALYSIS"
      HD3="VENDOR #       VENDOR NAME AND ADDRESS          OPEN A/P      PAID MTD     PAID YTD      PAID L'YR   LAST PMT CHECK#    AMOUNT"
      HD4="-------- ------------------------------------ ------------- ------------ ------------- ------------- -------- ------ ------------"
      HD5=SPACE(46):"============= ============ ============= ============="
   END ELSE
*T26493     REPORT.NAME = "VENDOR LISTING"
      HD3="VENDOR #       VENDOR NAME AND ADDRESS          PHONE & CONTACT    NET  %   DAYS ST   AUTO-G/L #   1099 S.S# & TAX# SV"
      HD4="-------- ------------------------------------ -------------------- --- ---- ---- -- -------------- ---- ----------- --"
   END
*T26493 v
*  REPORT.NUMBER = "XXXX"
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = X<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** OPEN FILES
*
   OPEN "","VEND" TO VEND ELSE ERRMSG="VEND FILE IS MISSING";GOTO 93000
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING";GOTO 93000
*
**** READ COMPANY RECORD
*
   MATREAD COMP.REC FROM COMPANY , CONO ELSE ERRMSG = "COMPANY RECORD IS MISSING" ; GOTO 93000
*** START OF PRINT ***
   PRINTER ON
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA = 1 DO
      MATREAD VEND.REC FROM VEND , ID ELSE GOTO 111
      IF FLAG = "A" THEN
         GOSUB 1000
      END ELSE
         GOSUB 2000
      END
111 REPEAT
   IF FLAG = "A" THEN GOSUB 8000
   GOTO 99999
*
**** PRINT VENDOR ANALYSIS LINES
*
1000*
   IF LINE.COUNT + 5 >= 53 THEN
      GOSUB 9000
   END
   P.LINE = ""
   P.LINE = P.LINE :ID[4,8] "L#8":SP1
   P.LINE = P.LINE :VEND.DESC "L#36":SP1
   P.LINE = P.LINE :OCONV(VEND.OP.BAL, "MD2,") "R#13":SP1
   P.LINE = P.LINE :OCONV(VEND.PD.M.T.D, "MD2,") "R#12":SP1
   P.LINE = P.LINE :OCONV(VEND.PD.Y.T.D, "MD2,") "R#13":SP1
   P.LINE = P.LINE :OCONV(VEND.PD.L.YR, "MD2,") "R#13":SP1
   P.LINE = P.LINE :OCONV(VEND.DATE.PD, "D2/") "R#8":SP1
   P.LINE = P.LINE :VEND.CK.NO.PD "R#6":SP1
   P.LINE = P.LINE :OCONV(VEND.PA.AMT, "MD2,") "R#12"
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   P.LINE = ""
   P.LINE = P.LINE :SPACE(9)
   P.LINE = P.LINE :VEND.ADDR1 "L#36"
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   IF VEND.ADDR2 # "" THEN
      P.LINE = ""
      P.LINE = P.LINE:SPACE(9)
      P.LINE = P.LINE:VEND.ADDR2 "L#36"
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT + 1
   END
   P.LINE = ""
   P.LINE = P.LINE:SPACE(9)
   P.LINE = P.LINE:VEND.CT.ST "L#25":SP1
   P.LINE = P.LINE:VEND.ZIP
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   IF LINE.COUNT < 54 THEN 
      PRINT
      LINE.COUNT = LINE.COUNT + 1
   END
   TOT.OPEN = TOT.OPEN + VEND.OP.BAL
   TOT.MTD = TOT.MTD + VEND.PD.M.T.D
   TOT.YTD = TOT.YTD + VEND.PD.Y.T.D
   TOT.LYTD = TOT.LYTD + VEND.PD.L.YR
   RETURN
*
**** PRINT VENDOR LISTING LINES
*
2000*
   IF LINE.COUNT >= 53 THEN
      GOSUB 9000
   END
   P.LINE = ""
   P.LINE = P.LINE:ID[4,8] "R#8":SP1
   P.LINE = P.LINE:VEND.DESC "L#36":SP1
   P.LINE = P.LINE:VEND.PHONE "L#20":SP1
   P.LINE = P.LINE:VEND.TERMS<1,2> "L#3":SP1
   P.LINE = P.LINE:OCONV(VEND.TERMS<1,1>[1,4], "MD2") "R#4":SP1
   P.LINE = P.LINE:VEND.TERMS<1,1>[5,2] "R#4":SP1
   P.LINE = P.LINE:VEND.STATUS "L#2":SP1
   P.LINE = P.LINE:VEND.AUTO.DIST CO.ACCT.MASK:SP1
   P.LINE = P.LINE:VEND.1099.FLAG "L#4":SP1
   P.LINE = P.LINE:VEND.S.S.NO "L#11":SP1
   P.LINE = P.LINE:VEND.SHIP.VIA "L#2"
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   P.LINE = ""
   P.LINE = P.LINE :SPACE(9)
   P.LINE = P.LINE :VEND.ADDR1 "L#36":SP1
   P.LINE = P.LINE :VEND.CONTACT "L#20"
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   IF VEND.ADDR2 # "" THEN
      P.LINE = ""
      P.LINE = P.LINE:SPACE(9)
      P.LINE = P.LINE:VEND.ADDR2 "L#36"
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT + 1
   END
   P.LINE = ""
   P.LINE = P.LINE:SPACE(9)
   P.LINE = P.LINE:VEND.CT.ST "L#25":SP1
   P.LINE = P.LINE:VEND.ZIP
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT + 1
   IF LINE.COUNT < 54 THEN 
      PRINT
      LINE.COUNT = LINE.COUNT + 1
   END
   RETURN
*
**** PRINT TOTAL LINE FOR VENDOR ANALYSIS
*
8000*
   IF LINE.COUNT + 2 >= 53 THEN
      GOSUB 9000
   END
   PRINT HD5
   T.LINE = ""
   T.LINE = T.LINE :SPACE(9)
   T.LINE = T.LINE :"* T O T A L S *":SPACE(22)
   T.LINE = T.LINE :OCONV(TOT.OPEN, "MD2,") "R#13":SP1
   T.LINE = T.LINE :OCONV(TOT.MTD, "MD2,") "R#12":SP1
   T.LINE = T.LINE :OCONV(TOT.YTD, "MD2,") "R#13":SP1
   T.LINE = T.LINE :OCONV(TOT.LYTD, "MD2,") "R#13"
   PRINT T.LINE
   RETURN
*
**** PRINT HEADING
*
9000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT HD3
   PRINT HD4
   LINE.COUNT = 5
   RETURN
***** CALLS FOR SYSCOM
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999 PRINTER OFF
END
