      SUBROUTINE OVERNIGHT.REPORT.REVIEW(MAT OR.REC, OPTION)
*********************************************************************
*
* Copyright 1989 by PRISYS Pty Ltd (Australasia)
*
* SYSTEM        - PRIMAC
*
* PROGRAM       - OVERNIGHT.REPORT.REVIEW
*
* AUTHOR        - BERN (PRISYS Pty Ltd)
*
* DATE          - 22 NOV 1989
*
* MODIFIED      - 24 MAR 1992 by NICK AMENDOLA (VERCOM)
*
* DESCRIPTION
*
* This program is used to review the overnight report request options
* and values, and is called from program OVERNIGHT.PROCESSOR
*
*********************************************************************
*
*  COPY statements
*
*COPY>CPYLIB>SCOMMON1
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>OVERNIGHT.REQUESTS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*  Open all files
*
      OPEN "","REPORT.PROC.FILE" TO REPORT.PROC.FILE ELSE
         ERRMSG = "CANNOT OPEN REPORT.PROC.FILE"
         GOSUB 90000
         STOP
      END
*
*  Initialization
*
      SCREEN DEFINE;OVERNIGHT.REPORT.REVIEW
      SCREEN FORMAT
      SCREEN COUNT;;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      LINE.CNT = 0
      REF.NO = ""
      CURR.REF.NO = ""
*
*  Main processing
*
      GOSUB 80000
      LINE.CNT = DCOUNT(OR.FIELDS<1,OPTION>,SM)
      GOSUB 81000
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 50000
*
*  Get operator reply
*
500 *
      S$DEFAULT = ""
      SCREEN FIELD;;21
      SCREEN INPUT;;21
      OPT = S$VALUE
510 *
      BEGIN CASE
      CASE OPT = "" OR OPT = "E" OR OPT = "END"
         GOTO 99999
      CASE OPT = "S" OR OPT = "SF"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > LINE.CNT THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "SR"
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "ST"
         REF.NO = 1
         GOSUB 50000
      CASE OPT = "SB"
         REF.NO = LINE.CNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      END CASE
      GOTO 500
*
*  Multi-line scroll routine
*
50000 *
      START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = DCOUNT(S$DATA(11)<S$SCR>,VM)
      SCREEN MULTI;;C;11;12;13;14
      RETURN
*
*  Load data fields and display
*
80000 *
      S$DATA(1)<S$SCR> = DATE()
      S$DATA(2)<S$SCR> = OR.REPORT.NAME<1,OPTION>
      S$DATA(3)<S$SCR> = OR.MD.PROC<1,OPTION>
      S$DATA(4)<S$SCR> = OR.USER.ID<1,OPTION>
      S$DATA(5)<S$SCR> = OR.COMPANY.NO<1,OPTION>
      S$DATA(6)<S$SCR> = OR.ACCOUNT.NAME<1,OPTION>
      IF OR.START.DATE<1,OPTION> = "" THEN
         S$DATA(7)<S$SCR> = ""
         S$DATA(8)<S$SCR> = ""
      END ELSE
         S$DATA(7)<S$SCR> = OCONV(OR.START.DATE<1,OPTION>,"D2/")
         S$DATA(8)<S$SCR> = OCONV(OR.START.TIME<1,OPTION>,"MTS")
      END
      IF OR.END.DATE<1,OPTION> = "" THEN
         S$DATA(9)<S$SCR>  = ""
         S$DATA(10)<S$SCR> = ""
      END ELSE
         S$DATA(9)<S$SCR>  = OCONV(OR.END.DATE<1,OPTION>,"D2/")
         S$DATA(10)<S$SCR> = OCONV(OR.END.TIME<1,OPTION>,"MTS")
      END
      BEGIN CASE
      CASE OR.STATUS<1,OPTION> = "A"
         S$DATA(30)<S$SCR> = "** ABORTED **"
      CASE OR.STATUS<1,OPTION> = "C"
         S$DATA(30)<S$SCR> = "** COMPLETED **"
      CASE OR.STATUS<1,OPTION> = "D"
         S$DATA(30)<S$SCR> = "** DELETED **"
      CASE OR.STATUS<1,OPTION> = "H"
         S$DATA(30)<S$SCR> = "** HOLD **"
      CASE OR.STATUS<1,OPTION> = "I"
         S$DATA(30)<S$SCR> = "** IN-PROCESS **"
      CASE OR.STATUS<1,OPTION> = "S"
         S$DATA(30)<S$SCR> = "** SCHEDULED **"
      END CASE
80050 *
      SCREEN DISPLAY;;ALL
      RETURN
*
*  Load multi-line screen data
*
81000 *
      FOR I = 1 TO LINE.CNT
         READV DESC FROM REPORT.PROC.FILE,OR.FIELDS<1,OPTION,I>,1 ELSE DESC = "UNKNOWN OPTION"
         S$DATA(11)<S$SCR,I> = OR.FIELDS<1,OPTION,I>
         S$DATA(12)<S$SCR,I> = DESC
         S$DATA(13)<S$SCR,I> = ":"
         S$DATA(14)<S$SCR,I> = OR.PARAMETERS<1,OPTION,I>
      NEXT I
      RETURN
*
*  Error routine
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*  End of program
*
99999 *
      SCREEN CLEAR;;D
      RETURN
   END
