***************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - GET.PROC.CONO
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/18/85
*
* REVISION - A.1.0
*
* DESCRIPTION
*
* This program will derive the company ID for processing. If the company
* ID cannot be derived, a request will be made for input and the company
* will be validated, And also will return company number in the first 
* attribute and the total heading in second attribute or the conpany name.
*
*T25901 cmykleb 02/20/2002 * Include user id of person printing the report
*                            in the heading.
***************************************************************************
*
*---- COPY LIBRARY
*
*COPY>PMC.CPYLIB>SECURITY
*COPY>PMC.CPYLIB>REPORTS
*COPY>CPYLIB>CHAR
*
*---- GET REPORT NUMBER AND REPORT NAME
*
   PROCREAD X ELSE X = ""
   IF X<2> = "" THEN X = ""
   IF X # "" THEN
      REPORT.NAME = X<1>
      REPORT.NUMBER = X<2>
   END
   USER.ID = UPCASE(@LOGNAME) ; * T25901
*
*---- INITIALIZATION
*
   PROMPT ""
   PORT = "TTY"
   CALL SYSVARS.SUB(PORT)
   CONO = ""
   CO.NAME = "????????????????????"
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      CONO = "END"
      GOTO 99999
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      CONO = "END"
      GOTO 99999
   END
   OPEN "","REPORTS" TO REPORTS ELSE
      CONO = "END"
      GOTO 99999
   END
*
*---- MAIN PROCESSING
*
   READV CONO FROM CONTROL, "MASTER",1 ELSE NULL
   IF CONO # "" THEN GOTO 2000
   OPEN "","SECURITY" TO SECURITY ELSE GOTO 1000
   READV CONO FROM SECURITY, "R.":PORT,1 ELSE GOTO 1000
   IF CONO # "" THEN GOTO 2000
*
*---- REQUEST COMPANY ID
*
1000*
   PRINT @(-1):
1010 PRINT @(20,10):"Company : ###":@(30,10):
   INPUT CONO,3  _:
   IF CONO = "END" THEN GOTO 99999
   IF CONO = "" THEN
      CONO = "001"
   END ELSE
      CONO = STR("0",3-LEN(CONO)):CONO
   END
2000*
   READV CO.NAME FROM COMPANY, CONO,1 ELSE
      PRINT @(-1):@(0,23):"Invalid Company ID (":CONO:")":
      GOTO 1010
   END
*
*---- BUILD HEADINGS AND RETURN
*
99999*
   LINE = X
   LINE<1> = CONO
   IF X # "" AND CONO # "END" THEN
      MATREAD RPT.REC FROM REPORTS, CONO:REPORT.NAME ELSE
         MAT RPT.REC = ""
      END
      IF RPT.TITLE # "" THEN
         REPORT.NAME = RPT.TITLE
      END
      IF RPT.NUMBER # "" THEN
         REPORT.NUMBER = RPT.NUMBER
      END 
      PTR = INDEX(REPORT.NAME,"(",1)
      IF PTR = 0 THEN
         REPORT.NAME = TRIM(REPORT.NAME)
         REPORT.SUFFIX = ""
      END ELSE
         REPORT.SUFFIX = TRIM(REPORT.NAME[PTR,99])
         REPORT.NAME = TRIM(REPORT.NAME[1,PTR-1])
      END
      IHL = LEN(REPORT.NAME)
      SAVE.REPORT.NAME = ""
      FOR P = 1 TO IHL
         SAVE.REPORT.NAME = SAVE.REPORT.NAME:" ":REPORT.NAME[P,1]
      NEXT P
      IF LEN(SAVE.REPORT.NAME) < 81 THEN
         REPORT.NAME = SAVE.REPORT.NAME[2,80]
      END
      HLINE1 = "RUN D - T: ":OCONV(DATE(), "D2/"):" - ":OCONV(TIME(), "MTS")
      HLINE1 = HLINE1 "L#33"
      HLINE2 = SPACE(INT((85-LEN(CO.NAME))/2)):CO.NAME
      HLINE2 = HLINE2 "L#85"
      HLINE3 = SPACE(3):"PAGE 'PL'"
      HLINE = HLINE1:HLINE2:HLINE3
*T25901 v
*     HLINE4 = "REPORT # : ":REPORT.NUMBER
      HLINE4 = "RPT #-BY : ":REPORT.NUMBER:"-":USER.ID
*T25901 ^
      HLINE4 = HLINE4 "L#33"
      HLINE5 = SPACE(INT((85-LEN(REPORT.NAME))/2)):REPORT.NAME
      HLINE5 = HLINE5:"   ":REPORT.SUFFIX
      HLINEA = HLINE4:HLINE5
      HLINE = HLINE:HLINEA
      LINE<2> = HLINE
   END ELSE
      LINE<2> = CO.NAME
   END
   PROCWRITE LINE
END
