*COPY>CPYLIB>COM1
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - TAX.JUR.MAINT
* SOURCE   - PMCBP
* AUTHOR   - JIHAD YAMOUT , C.B.A
* DATE     - 01/23/85
* DESCRIPTION
* This program will add and maintain tax jurisdiction table file entries.
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>TAX
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN ALL FILES
*
  OPEN "","PMC.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN PMC.SCREENS FILE"
    GOTO 93000
  END
  OPEN "","TAX" TO TAX ELSE
    ERRMSG = "CANNOT OPEN TAX FILE"
    GOTO 93000
  END
  OPEN "","COA" TO COA ELSE
    ERRMSG = "CANNOT OPEN COA FILE"
    GOTO 93000
  END
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOTO 93000
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOTO 93000
  END
*
*---- INITIALIZATION
*
  MAT EDIT.COM.DRIVER = ""
  ERRMSG = ""
*
*---- READ COMPANY RECORD
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  IN.ACCT.LEN  = LEN(CO.ACCT.PIC)
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "TAX.JUR.MAINT"
  ECD.ACTION = 1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
100*
  MAT SCV.REC = ""
  ECD.ACTION = 6;CALL SCRN.EDIT
  SCV.REC(1)<1> = DATE()
  ECD.NUM = 1;ECD.ACTION = 5;CALL SCRN.EDIT
  KEY = ""
  NEW.REC = ""
  MAT TAX.REC = ""
  ECD.NUM = 4
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 99999
  KEY = ECD.RET.VALUE
  * T26126 v
  MATREADU TAX.REC FROM TAX, CONO:KEY LOCKED
    ERRMSG = 'TAX record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 100 
  END ELSE
  * T26126 ^
    ECD.NUM = 5
    ECD.ACTION = 4;CALL SCRN.EDIT
    IF ECD.RET.VALUE = "N" THEN 
      RELEASE TAX,CONO:KEY
      GOTO 100
    END
    NEW.REC = "Y"
  END
  IF NEW.REC = "Y" THEN
    FOR X = 1 TO 3
      ON X GOSUB 10300,10400,10500
      IF ECD.RET.VALUE = "END" THEN GOTO 100
    NEXT X
  END ELSE
    MATREAD COA.REC FROM COA , CONO:TAX.ACCT ELSE
      MAT COA.REC = ""
      COA.DESC = "?????????????????"
    END
    SCV.REC(2)<1> = TAX.JUR
    SCV.REC(7)<1> = TAX.ACCT CO.ACCT.MASK
    SCV.REC(8)<1> = COA.DESC
    SCV.REC(9)<1> = TAX.RATE
    ECD.ACTION = 3;CALL SCRN.EDIT
  END
10110*
  ECD.NUM = 6
  ECD.ACTION = 4;CALL SCRN.EDIT
  BEGIN CASE
    CASE NUM(ECD.RET.VALUE)
      ON ECD.RET.VALUE GOSUB 10300,10400,10500
    CASE ECD.RET.VALUE = "E" OR ECD.RET.VALUE = "END"
      RELEASE TAX,CONO:KEY
      GOTO 100
    CASE ECD.RET.VALUE = "F"
      MATWRITE TAX.REC ON TAX, CONO:KEY
      GOTO 100
  END CASE
  GOTO 10110
*
*---- GET TAX JURISDICTION
*
10300*
  ECD.NUM = 2
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN TAX.JUR = ECD.RET.VALUE
  RETURN
*
*---- GET COA ACCT NUMBER
*
10400*
  SCV.REC(7)<1> = ""
  ECD.NUM = 7;ECD.ACTION = 5;CALL SCRN.EDIT
  SCV.REC(8)<1> = ""
  ECD.NUM = 8;ECD.ACTION = 5;CALL SCRN.EDIT
  SCV.REC(7)<1> = TAX.ACCT
  ECD.NUM = 7
  ECD.MAXL = IN.ACCT.LEN ; ECD.MINL = IN.ACCT.LEN
  ECD.ACTION = 4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END" AND TAX.ACCT = ""
      MAT COA.REC = ""
    CASE ECD.RET.VALUE = "END"
      MATREAD COA.REC FROM COA , CONO:TAX.ACCT ELSE
        MAT COA.REC = ""
        COA.DESC = "?????????????????"
      END
    CASE 1
      MATREAD COA.REC FROM COA , CONO:ECD.RET.VALUE ELSE
        ERRMSG = "Account not on file " ; GOSUB 91000 ; GOTO 10400
      END
      TAX.ACCT = ECD.RET.VALUE
  END CASE
  SCV.REC(7)<1> = TAX.ACCT CO.ACCT.MASK
  ECD.NUM = 7;ECD.ACTION = 5;CALL SCRN.EDIT
  SCV.REC(8)<1> = COA.DESC
  ECD.NUM = 8;ECD.ACTION = 5;CALL SCRN.EDIT
  RETURN
*
*---- GET TAX RATE
*
10500*
  ECD.NUM = 9
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN TAX.RATE = ECD.RET.VALUE
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
