*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [11.0]
* Copyright 1999 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - CUST.DELETE.PURGED.INV
* BY          - RANDY MORPHEW, PRIMAC
* DATE        - 07/27/99
* DESCRIPTION - See T25763
* This program deletes PURGED.OPEN.RECV records and cross references to
* those records in the appropriate PURGED.OR.XREF records.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>ARS.CPYLIB>PURGED.OPEN.RECV
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD XX ELSE ERRMSG="MUST BE RUN FROM PROC";GOSUB 91000;GOTO 99999
   CONO=XX<1>
*
*---- OPEN FILES
*
   OPEN 'CONTROL' TO CONTROL ELSE
      ERRMSG = 'CONTROL FILE IS MISSING'
      GOTO 93000
   END
   OPEN 'PMC.SCREENS' TO M.SCREENS ELSE
      ERRMSG = 'PMC.SCREENS FILE IS MISSING'
      GOTO 93000
   END
   OPEN 'PURGED.OPEN.RECV' TO PURGED.OPEN.RECV ELSE
      ERRMSG = 'PURGED.OPEN.RECV FILE IS MISSING'
      GOTO 93000
   END
   OPEN 'PURGED.OR.XREF' TO PURGED.OR.XREF ELSE
      ERRMSG = 'PURGED.OR.XREF FILE IS MISSING'
      GOTO 93000
   END
*
*---- DISPLAY SCREEN AND GET RESPONSE
*     IF CUSTOMER ENTERS "Y", THEN PROCEED WITH DELETE.
*     FOR ANY OTHER ENTRY, EXIT PROGRAM WITHOUT DELETING ANY RECORDS.
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME = "CUST.DELETE.PURGED.INV"
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1; ECD.ACTION = 2; CALL SCRN.EDIT
   VALID.ENTRY = 0
   LOOP
      ECD.NUM = 1
      SCV.REC(ECD.NUM)<1> = ''
      ECD.ACTION = 5
      CALL SCRN.EDIT
      ECD.ACTION = 4
      CALL SCRN.EDIT
      TEST.VAL = TRIM(ECD.RET.VALUE)
      BEGIN CASE
         CASE TEST.VAL = "END"
            VALID.ENTRY = 1
            OK.TO.PROCEED = 0
         CASE TEST.VAL = "Y"
            VALID.ENTRY = 1
            OK.TO.PROCEED = 1
         CASE TEST.VAL = "N"
            OK.TO.PROCEED = 0
            VALID.ENTRY = 1
      END CASE
   UNTIL VALID.ENTRY DO
   REPEAT
*
* IF 'Y' WAS NOT THE ANSWER, THEN EXIT THE PROGRAM BEFORE DELETING ANY
* RECORDS.
*
   IF NOT(OK.TO.PROCEED) THEN GOTO 99999
*
*---- MAIN PROCESSING
*
*
*---- GET FISCAL BEGIN DATE FOR THIS COMPANY
*     THEN SUBTRACT 1095 DAYS TO GET CUTOFF DATE FOR RECORD DELETION
*     (ANY INVOICE DATE LESS THAN THIS DATE WILL BE DELETED FROM THE
*     PURGED FILE.)
*     IF THERE IS NO RECORD, THEN THIS DATE WILL BE CALCULATED TO BE
*     THE FIRST DAY OF THE CURRENT MONTH.
*
   READ CREC FROM CONTROL,CONO:"ARSFISCAL" THEN
      FISCAL.BEG.DATE = CREC<2,0,0>
   END ELSE
      TEMP.DATE = OCONV(DATE(),"D4-")
      FISCAL.BEG.DATE = ICONV(TEMP.DATE[1,3]:"01":TEMP.DATE[6,5],"D")
   END
   CUTOFF.DATE = FISCAL.BEG.DATE - 1095
*
*---- START CHECKING THE PURGED.OPEN.RECV FILE
*
   DONE = 0
   POR.KEYS = ""
   SELECT PURGED.OPEN.RECV TO POR.KEYS
   LOOP
      READNEXT POR.KEY FROM POR.KEYS ELSE DONE = 1
   UNTIL DONE DO
      MATREAD POR.REC FROM PURGED.OPEN.RECV,POR.KEY THEN
         IF POR.INV.DATE < CUTOFF.DATE THEN
         * DETERMINE IF AN XREF RECORD EXISTS
            XREF.KEY = CONO:POR.CUST
            READ XREF.REC FROM PURGED.OR.XREF,XREF.KEY THEN
               XREF.FOUND = 1
            END ELSE
               XREF.FOUND = 0
            END
         * IF AN XREF RECORD WAS FOUND, DETERMINE IF THE PURGED ITEM
         * IS CONTAINED IN THE XREF RECORD.
            IF XREF.FOUND THEN
               LOCATE POR.KEY IN XREF.REC<1> SETTING REC.LOC ELSE REC.LOC = 0
               IF XREF.FOUND AND REC.LOC > 0 THEN
                  XREF.REC = DELETE(XREF.REC,REC.LOC,0,0)
               END
               WRITE XREF.REC ON PURGED.OR.XREF,XREF.KEY
            END
            DELETE PURGED.OPEN.RECV,POR.KEY
         END
      END
   REPEAT
   GOTO 99999
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYP=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000*
   ERR.TYP=3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
   ECD.ACTION = 99; CALL SCRN.EDIT
END
