   SUBROUTINE PMC_EMAIL_SUB (EMAIL_PARAM, ERRMSG)
*************************************************************
* REVISION      - [12.0]
* Copyright 1997 by Vercom Software, Inc.
* SOURCE        - CBABP
* PROGRAM       - PMC_EMAIL_SUB
* DESCRIPTION   -
*
* PROGRAMS REQUIRED FOR MAIL FEATURE:
*  -GHOSTSCRIPT, MPACK (UNIX)
*  -GHOSTSCRIPT, BLAT  (WINDOWS)
*************************************************************
*
*COPY>CPYLIB>CHAR
   DEFFUN PMC_SEND_MAIL(ADDRESSES,SUBJECT,TEXT,ATTACH)
*
*---- Initialization
*
   ERRMSG = ""
   OPEN '',"_HOLD_" TO HOLD.FILE ELSE
      ERRMSG = "HOLD FILE IS MISSING"
      GOTO 999999
   END
*
*---- Read in hold file record
*
   CONO = EMAIL_PARAM<1>
   CO.NAME = EMAIL_PARAM<2>
   HF.NAME = EMAIL_PARAM<3>
   EMAIL.FORMAT = EMAIL_PARAM<4>
   EMAIL.ADDRESS = EMAIL_PARAM<5>
   SUBJECT = EMAIL_PARAM<6>
*  EMAIL.PLATFORM = UPCASE(SYSTEM(33))[1,7]
   EMAIL.PLATFORM = EMAIL_PARAM<7>
*
   READ HOLD.REC FROM HOLD.FILE, HF.NAME ELSE HOLD.REC = ''
*
   BEGIN CASE
   CASE EMAIL.FORMAT = 1
     EMAIL.ATTACH = "ASCII":@AM:"_HOLD_/":HF.NAME:".rtf"
     FORMATSTR = ''
     FORMATSTR<1,1> = "WIDE REPORT"
     FORMATSTR<1,2> = "FONT SIZE"
     FORMATSTR<2,2> = 9
     CALL CONVERT.TO.RTF(CO.NAME,"_HOLD_",HF.NAME,SUBJECT,FORMATSTR)
     EMAIL.DESC = "This email contains a report attachment in RTF format, "
     EMAIL.DESC:= "which can be opened by most word-processors including "
     EMAIL.DESC:= "Microsoft Word. You can download a Word viewer from "
     EMAIL.DESC:= "http://office.microsoft.com/downloads/2000/wd97vwr32.aspx"
   CASE EMAIL.FORMAT = 2
     EMAIL.ATTACH = "BIN":@AM:"_HOLD_/":HF.NAME:".pdf"
     CALL CONVERT.TO.PDF("_HOLD_",HF.NAME,'')
     EMAIL.DESC = "This email contains a report attachment in Adobe PDF "
     EMAIL.DESC:= "format. To open it you must have Adobe Acrobat Reader, "
     EMAIL.DESC:= "which you can download at http://www.acrobat.com"
   END CASE
   TEXT.FILE = "./_HOLD_/":HF.NAME:".dsc"
   OSWRITE EMAIL.DESC ON TEXT.FILE
   EMAIL.ERROR = PMC_SEND_MAIL(EMAIL.ADDRESS,SUBJECT,TEXT.FILE,EMAIL.ATTACH)
   IF EMAIL.ERROR THEN
     ERRMSG = EMAIL.ERROR<2>
   END
*
   DELETE HOLD.FILE, HF.NAME
   DELETE HOLD.FILE, HF.NAME:".rtf"
   DELETE HOLD.FILE, HF.NAME:".dsc"
   DELETE HOLD.FILE, HF.NAME:".ps" 
   DELETE HOLD.FILE, HF.NAME:".pdf"
999999*
   RETURN
 END
