  SUBROUTINE COMP.POS.SUB(CO.ID)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.FICA.TABLE
*************************************************************
*   REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*   SUBROUTINE COMP.POS FOR COMPANY.NAME PROGRAM
*T24231 edvard 08/03/1999 * Added two new fields that control if PO
*                           requisition process is used and how many
*                           levels of approvers are going to be used in
*                           requisition process.
*T26152 alex 10/26/2001 * Add new fields for maintaining the Accrue
*                         field in Regular, Outside, & Misc. P/O.
*ENDDOC
*************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2 
*
*
  SCV.REC(5)<ECD.SCRN.NO> = CO.ID
  SCV.REC(6)<ECD.SCRN.NO> = CO.NAME
*24231 v
  SCV.REC(11)<ECD.SCRN.NO> = CO.PO.REQ.FLAG
  SCV.REC(12)<ECD.SCRN.NO> = CO.PO.APP.LEVELS
  SCV.REC(13)<ECD.SCRN.NO> = CO.PO.ACCRUE.FLAG<1,1>   ;* T26152
  SCV.REC(14)<ECD.SCRN.NO> = CO.PO.ACCRUE.MAINT<1,1>  ;* T26152
  SCV.REC(15)<ECD.SCRN.NO> = CO.PO.ACCRUE.FLAG<1,2>   ;* T26152
  SCV.REC(16)<ECD.SCRN.NO> = CO.PO.ACCRUE.MAINT<1,2>  ;* T26152
  SCV.REC(17)<ECD.SCRN.NO> = CO.PO.ACCRUE.FLAG<1,3>   ;* T26152
  SCV.REC(18)<ECD.SCRN.NO> = CO.PO.ACCRUE.MAINT<1,3>  ;* T26152
  GOSUB ACTION.2; GOSUB ACTION.3
*
*
  DONE = 0
  LOOP
    ECD.NUM = 10; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''; GOSUB ACTION.4
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE NUM(OPTION) AND OPTION > 0
        ;* T26152 v
              * ON OPTION GOSUB ENTER.FLAG, ENTER.LEVELS
        ON OPTION GOSUB ENTER.FLAG, ENTER.LEVELS, ENTER.REG.ACCRUE, ENTER.OUT.ACCRUE, ENTER.MISC.ACCRUE
        ;* T26152 ^
      CASE OPTION = '' OR OPTION = 'E' OR OPTION = 'END'
        DONE = 1
    END CASE
  UNTIL DONE DO REPEAT
  GOTO 999999
***********
ENTER.FLAG: 
***********
  SCV.REC(11)<ECD.SCRN.NO> =  CO.PO.REQ.FLAG
  ECD.NUM = 11 ; GOSUB ACTION.4
  BEGIN CASE
    CASE ECD.RET.VALUE = "N" 
      CMD = "COUNT REG.REQ"
      UDTEXECUTE CMD CAPTURING MSG1
      CMD = "COUNT OUT.REQ"
      UDTEXECUTE CMD CAPTURING MSG2
      CMD = "COUNT MISC.REQ"
      UDTEXECUTE CMD CAPTURING MSG3
      IF MSG1<3>[1,1] > 0 OR MSG2<3>[1,1] > 0 OR MSG3<3>[1,1] > 0 THEN 
        ERRMSG = "Requisitions still exist. Cannot change."
        GOSUB 91000
        SCV.REC(11)<ECD.SCRN.NO> = CO.PO.REQ.FLAG
        GOSUB ACTION.5
      END ELSE
        SCV.REC(11)<ECD.SCRN.NO> = ECD.RET.VALUE
        CO.PO.REQ.FLAG = ECD.RET.VALUE
      END
    CASE ECD.RET.VALUE = "Y" 
      SCV.REC(11)<ECD.SCRN.NO> = ECD.RET.VALUE
      CO.PO.REQ.FLAG = ECD.RET.VALUE
  END CASE
  RETURN
*************
ENTER.LEVELS: 
*************
  SCV.REC(12)<ECD.SCRN.NO> = CO.PO.APP.LEVELS
  ECD.NUM = 12 ; GOSUB ACTION.4
  IF ECD.RET.VALUE # "END" THEN 
    SCV.REC(12)<ECD.SCRN.NO> = ECD.RET.VALUE
    CO.PO.APP.LEVELS = ECD.RET.VALUE
  END
  RETURN
* T26152 v
*****************
ENTER.REG.ACCRUE: 
*****************
  SCV.REC(13)<ECD.SCRN.NO> = CO.PO.ACCRUE.FLAG<1,1>
  ECD.NUM = 13 ; GOSUB ACTION.4
  IF ECD.RET.VALUE # "END" THEN 
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
    CO.PO.ACCRUE.FLAG<1,1> = ECD.RET.VALUE
    ECD.NUM = 14
    IF ECD.RET.VALUE = '' THEN
      CO.PO.ACCRUE.MAINT<1,1> = ECD.RET.VALUE
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
      GOSUB ACTION.5
    END ELSE
      GOSUB GET.ACCRUE.MAINT
    END
  END
  RETURN
*****************
ENTER.OUT.ACCRUE: 
*****************
  SCV.REC(15)<ECD.SCRN.NO> = CO.PO.ACCRUE.FLAG<1,2>
  ECD.NUM = 15 ; GOSUB ACTION.4
  IF ECD.RET.VALUE # "END" THEN 
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
    CO.PO.ACCRUE.FLAG<1,2> = ECD.RET.VALUE
    ECD.NUM = 16
    IF ECD.RET.VALUE = '' THEN
      CO.PO.ACCRUE.MAINT<1,2> = ECD.RET.VALUE
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
      GOSUB ACTION.5
    END ELSE
      GOSUB GET.ACCRUE.MAINT
    END
  END
  RETURN
******************
ENTER.MISC.ACCRUE: 
******************
  SCV.REC(17)<ECD.SCRN.NO> = CO.PO.ACCRUE.FLAG<1,3>
  ECD.NUM = 17 ; GOSUB ACTION.4
  IF ECD.RET.VALUE # "END" THEN 
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
    CO.PO.ACCRUE.FLAG<1,3> = ECD.RET.VALUE
    ECD.NUM = 18
    IF ECD.RET.VALUE = '' THEN
      CO.PO.ACCRUE.MAINT<1,3> = ECD.RET.VALUE
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
      GOSUB ACTION.5
    END ELSE
      GOSUB GET.ACCRUE.MAINT
    END
  END
  RETURN
*****************
GET.ACCRUE.MAINT: 
*****************
  BEGIN CASE
    CASE ECD.NUM = 14 ; XPOS = 1
    CASE ECD.NUM = 16 ; XPOS = 2
    CASE ECD.NUM = 18 ; XPOS = 3
  END CASE
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.PO.ACCRUE.MAINT<1,XPOS>
  GOSUB ACTION.4
  IF ECD.RET.VALUE # "END" THEN 
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
    CO.PO.ACCRUE.MAINT<1,XPOS> = ECD.RET.VALUE
  END
  RETURN
* T26152 ^
*************
ACTION.1: 
*************
  ECD.ACTION = 1; CALL SCRN.EDIT; RETURN
*************
ACTION.2: 
*************
  ECD.ACTION = 2; CALL SCRN.EDIT; RETURN
*************
ACTION.3: 
*************
  ECD.ACTION = 3; CALL SCRN.EDIT; RETURN
*************
ACTION.4: 
*************
  ECD.ACTION = 4; CALL SCRN.EDIT; RETURN
*************
ACTION.5: 
*************
  ECD.ACTION = 5; CALL SCRN.EDIT; RETURN
**
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN 
*
999999*
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
