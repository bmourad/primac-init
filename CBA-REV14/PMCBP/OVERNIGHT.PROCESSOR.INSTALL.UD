*********************************************************************
*
* SYSTEM        - PRIMAC
*
* PROGRAM       - OVERNIGHT.PROCESSOR.INSTALL
*
* AUTHOR        - NICK AMENDOLA - COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 03/27/92
*
* DESCRIPTION
*
* This program is used to install the overnight report processor
* into a specified base account.
*
* The following Q-pointer are created in the relavent accounts.
*
*      PMC.SCREENS     - points to the CBA account
*      OVERNIGHT.PROCS - points to the main account
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>MENUS.CONTROL
*
*---- OPEN ALL FILE
*
      OPEN "","VOC" TO VOC ELSE
         ERRMSG = "CANNOT OPEN VOC FILE"
         GOSUB 90000
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         STOP
      END
      MATREAD MENU.REC FROM CONTROL, "MENUS.CONTROL" ELSE
         ERRMSG = "MENUS.CONTROL RECORD IS MISSING FROM CONTROL FILE"
         GOSUB 90000
         STOP
      END
*
*---- INITIALIZATION
*
10 *
      PROMPT ""
*     PORT = SYSTEM(18)
      PORT = @TTY
      PRINT @(-1)
      SCRN = ""
      SCRN = SCRN:@(20,00):"OVERNIGHT PROCESSOR INSTALLATION"
      SCRN = SCRN:@(00,01):STR("-",80)
      SCRN = SCRN:@(00,20):STR("-",80)
      SCRN = SCRN:@(00,22):STR("-",80)
      SCRN = SCRN:@(05,05):"This program is used to install the Overnight Processor."
      SCRN = SCRN:@(05,07):"The following file should be created prior to running this process:"
      SCRN = SCRN:@(05,09):"     OVERNIGHT.PROCS - created in the base account (modulo = 3)."
      SCRN = SCRN:@(05,12):"The following Q-pointer are created in the relevant accounts:"
      SCRN = SCRN:@(05,14):"     PMC.SCREENS     - points to the CBA account"
      SCRN = SCRN:@(05,15):"     OVERNIGHT.PROCS - points to the main account"
      PRINT SCRN:
*
*---- MAIN PROCESSING
*
100 *
      PRINT @(0,21):@(-4):"Base Account Name : ":
      INPUT ACCT,30:_
      WARNMSG = ""
      BEGIN CASE
      CASE ACCT = "END"
      CASE ACCT = CHAR(27)
      CASE ACCT = "^"
      CASE ACCT = ""
      CASE INDEX(ACCT,"-",1) > 0
         ERRMSG = "Invalid account name format"
         GOSUB 90000
         GOTO 100
      CASE ACCT # M.ACCOUNT
         ERRMSG = "Invalid base account name"
         GOSUB 90000
         GOTO 100
      CASE 1
         BASEACCT = FIELD(ACCT,"-",1)
         GOSUB 1000
         GOTO 100
      END CASE
      GOTO 99999
*
*---- CREATE Q-POINTERS
*
1000 *
      PRINT @(0,21):@(-4):" Processing ":
      PRINT ".":
      QREC = "F"
      QREC<2> = PRIMAC.PATH:"/VOC"
      QREC<3> = PRIMAC.PATH:"/D_VOC"
      WRITE QREC ON VOC, "OVERNIGHT.INSTALL.":PORT
      OPEN "","OVERNIGHT.INSTALL.":PORT TO QFILE ELSE
         DELETE VOC, "OVERNIGHT.INSTALL.":PORT
         ERRMSG = "Invalid account name - ":QREC<2>
         GOSUB 90000
         GOTO 1010
      END
*
      FID = "PMC.SCREENS"
      READU QREC FROM QFILE, FID ELSE QREC = ""
      IF QREC = "" OR QREC<1> = "F" THEN
         QREC = "F"
         QREC<2> = CBA.PATH:"/":FID
         QREC<3> = CBA.PATH:"/D_":FID
      END
      WRITE QREC ON QFILE, FID
*
      FID = "OVERNIGHT.PROCS"
      READ MREC FROM QFILE, FID ELSE
         ERRMSG = "WARNING - ":FID:" file not found in ":BASEACCT:" account!"
         GOSUB 90000
      END
1010 *
      ACNT = DCOUNT(VALID.SYS,VM)
      FOR A = 1 TO ACNT
         PRINT ".":
         QREC = "F"
         QREC<2> = PRIMAC.PATH:"-":VALID.SYS<1,A>:"/VOC"
         QREC<3> = PRIMAC.PATH:"-":VALID.SYS<1,A>:"/D_VOC"
         WRITE QREC ON VOC, "OVERNIGHT.INSTALL.":PORT
         OPEN "","OVERNIGHT.INSTALL.":PORT TO QFILE ELSE
            DELETE VOC, "OVERNIGHT.INSTALL.":PORT
            ERRMSG = "Invalid account name - ":QREC<2>
            GOSUB 90000
            GOTO 1080
         END
*
         FID = "PMC.SCREENS"
         READU QREC FROM QFILE, FID ELSE QREC = ""
         IF QREC = "" OR QREC<1> = "F" THEN
            QREC = "F"
            QREC<2> = CBA.PATH:"/":FID
            QREC<3> = CBA.PATH:"/D_":FID
         END
         WRITE QREC ON QFILE, FID
*
         FID = "OVERNIGHT.PROCS"
         READU QREC FROM QFILE, FID ELSE QREC = ""
         IF QREC = "" OR QREC<1> = "F" THEN
            QREC = "F"
            QREC<2> = PRIMAC.PATH:"/":FID
            QREC<3> = PRIMAC.PATH:"/D_":FID
         END
         WRITE QREC ON QFILE, FID
*
1080  NEXT APTR
      DELETE VOC, "OVERNIGHT.INSTALL.":PORT
      IF WARNMSG # "" THEN
         ERRMSG = WARNMSG
         GOSUB 90000
      END
      ERRMSG = "Installation complete. Press <RETURN> to continue..."
      GOSUB 90000
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      PRINT @(0,23):@(-4):CHAR(7):ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      PRINT @(-1)
   END
