*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.FICA.TABLE
*COPY>PMC.CPYLIB>COM.COMP.OPS
*********************************************************************
* REVISION    - [08.0]
*Copyright 1982 by Computer Business Associates (Vercom Software Inc)
* PROGRAM     - COMP.MAINT                                         
* DATE        - 01/16/84                                           
* BY          - WALID YAMOUT, C.B.A.                               
* DESCRIPTION
* SCREEN INPUT AND UPDATE FOR COMPANY FILE.    
* MOD           - TASK 15719 RWW 1.2.91 FICA/MEDICARE FLDS
* 
* TASK 17927 GAT 10/31/94 ADD FLAG FOR INVOICE ALLOCATION IN JCS
* TASK 18606 LLH 03/08/95 ADD KIT DETAIL FLAG
*T21360 lanny 12/23/1996 * Mod for MCD purge periods.
*T23359 rik 01/05/1999 * ADD WIP TRACKING FLAG TO CONTROL FILE.
*T23955 cm 05/17/1999 * CORRECT GUI DISPLAY PROBLEM
*T25159 alex 05/11/2000 * Add PMC.CPYLIB>COUNTRY.CODE, since the
*                         COUNTRY.CODE file can hold multiple attributes
*                         of data.
*T26126 adelgado 02/26/2002 * Implement the LOCKED clause for READU.
*T26497 adelgado 03/21/2002 * Add open of WAREHOUSE file for validation
*                             of inter-division whse.
*T26090 wyamout 03/28/2002 * Add Email Format & COMP.PMC.SUB
*T26090 wyamout 05/13/2002 * ADD PMC FLAG
*T26572 cmykleb 04/26/2002 * Correct JIS screen problem.
*T26949 ajibaly 10/28/2002 * Add a field for telephone #.
*T27935 cmykleb 03/31/2004 * Add a new field for purging old journals.
*T27920 cmykleb 08/18/2004 * Add a new field for cancelling po open qtys.
*T28977 bmourad 09/01/2006 * Add a new field to APS sub to allow different
*                            language.
*T29032 cmyklebu 12/28/2006 * Move rev12 to rev14.
*********************************************************************
*
*** INSERT EQUATE FILE FROM CPYLIB
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FICA.TABLE
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>COUNTRY.CODE           ;*T25159
*COPY>CPYLIB>FILE.VARS
*** INITIALIZATION
*** SET UP ERRMSG ROUTINE FOR SYSCOM
   SYS.TYPE = 1
   RO.CNTR = ''
   CALL SYSCOM(MAT SYSCOM.REC)
*** OPEN THE FILES
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "Cannot locate the COMPANY file"; GOTO 93000
   OPEN "","FICA.TABLE" TO FICA.TABLE ELSE ERRMSG = "Cannot locate the FICA.TABLE file"; GOTO 93000
   OPEN "","PMC.SCREENS" TO M.SCREENS ELSE ERRMSG = "Cannot locate the PMC.SCREENS file"; GOTO 93000
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "Cannot locate the CONTROL file"; GOTO 93000
   OPEN "","PREFIX" TO PREFIX ELSE ERRMSG = "Cannot locate the PREFIX file"; GOTO 93000
   OPEN "","XREF.DATA" TO XREF.DATA ELSE ERRMSG = "Cannot locate the XREF.DATA file"; GOTO 93000
   OPEN "","DIVISION" TO DIVISION ELSE ERRMSG = "Cannot locate the DIVISION file"; GOTO 93000
   OPEN "","JOB.CATEGORY" TO JOB.CATEGORY ELSE ERRMSG = "Cannot locate the JOB.CATEGORY file"; GOTO 93000
   OPEN "","COUNTRY.CODE" TO COUNTRY.CODE ELSE ERRMSG = "Cannot locate the COUNTRY.CODE file"; GOTO 93000
   OPEN "","WAREHOUSE" TO WAREHOUSE ELSE ERRMSG = "Cannot locate the WAREHOUSE file"; GOTO 93000          ;* T26497
*T29032 v
*  OPEN "","REV12A.SCREENS" TO SECOND.SCREENS ELSE ERRMSG = "Cannot locate the REV12A.SCREENS file"; GOTO 93000
*T29032 ^
*
   MAT GEN.XREF.REC = ""
   MAT EDIT.COM.DRIVER = ""
   ECD.SCRN.CNT = 13
   ECD.SCRN.NAME = "COMP.MAINT"
   ECD.SCRN.NAME<2> = "COMP.ARS.SUB"
   ECD.SCRN.NAME<3> = "COMP.APS.SUB"
* T29032 v
*  ECD.SCRN.FLAG<3> = 2 ; * PAPS55
* T29032 ^
   ECD.SCRN.NAME<4> = "COMP.PRS.SUB"
   ECD.SCRN.NAME<5> = "COMP.FAS.SUB"
   ECD.SCRN.NAME<6> = "COMP.GLS.SUB"
* T29032 v
*  ECD.SCRN.FLAG<6> = 2 ; * T27935
* T29032 ^
   ECD.SCRN.NAME<7> = "COMP.SAS.SUB"
   ECD.SCRN.NAME<8> = "COMP.ICS.SUB"
* T29032 v
*  ECD.SCRN.FLAG<8> = 2 ; * T27920
* T29032 ^
   ECD.SCRN.NAME<9> = "COMP.POS.SUB"
   ECD.SCRN.NAME<10> = "COMP.OPS.SUB"
   ECD.SCRN.NAME<11> = "COMP.JES.SUB"
   ECD.SCRN.NAME<12> = "COMP.JCS.SUB"
* T29032 v
*  ECD.SCRN.FLAG<12> = 2 ; * T29010
* T29032 ^
   ECD.SCRN.NAME<13> = "COMP.PSS.SUB"
*T26572 v
* ECD.SCRN.NAME<14> = "COMP.JIS.SUB"
  ECD.SCRN.NAME<19> = "COMP.JIS.SUB"
*T26572 ^
   ECD.SCRN.NAME<14> = "COMP.WTR.SUB"
   ECD.SCRN.NAME<15> = "PR4.COMP.PRS.SUB"
   ECD.SCRN.NAME<16> = "EPS.COMP.PRS.SUB"
   ECD.SCRN.NAME<17> = "EP4.COMP.PRS.SUB"
* T26090 v
   ECD.SCRN.NAME<18> = "COMP.PMC.SUB"
   CO.EMAIL_FORMAT.ATT = 1
* T26090 ^
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   GOSUB 62000
*** ENTER COMPANY NUMBER
100 MAT SCV.REC = ""
   MAT COMP.REC = ""; MAT FIC.REC = ""; MAT OPCO.REC = ""
   ECD.ACTION = 6; CALL SCRN.EDIT
   ECD.NUM = 5
   ECD.VALDAT.CODE = "4"
   ECD.VALDAT.FILE = COMPANY
   GOSUB 64000
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 999999
      CASE ECD.RET.VALUE # "" AND ECD.VALDAT.ITEM # ""
      * T26126 v
         READU TMP.REC FROM COMPANY, ECD.RET.VALUE LOCKED
            ERRMSG = 'COMPANY record is locked by user - ':GETUSERNAME(STATUS())
            GOSUB 91000;GOTO 100 
         END ELSE NULL
      * T26126 ^
         CO.ID = ECD.RET.VALUE
         READU RL.SK.CONTROL FROM CONTROL,CO.ID:"RL.SK.CONTROL" ELSE RL.SK.CONTROL =''
         READU WIP.TRACKING FROM CONTROL,CO.ID:"WIP.TRACKING" ELSE WIP.TRACKING =''     ;*T23359
*T21360 v
         READU MCD.PURGE.PDS FROM CONTROL, CO.ID:"MCD.PURGE.PERIODS" ELSE MCD.PURGE.PDS = 1
*T21360 ^
* T26090 v
         READU EMAIL_FORMAT.REC FROM CONTROL, CO.ID:"EMAIL_FORMAT" ELSE
            EMAIL_FORMAT.REC = ""
         END
* T26090 ^
         FOR J = 1 TO COMP.REC.SIZE
            COMP.REC(J) = ECD.VALDAT.ITEM<J>
         NEXT J
         MATREAD FIC.REC FROM FICA.TABLE, CO.ID ELSE MAT FIC.REC = ""
         MATREAD OPCO.REC FROM CONTROL, CO.ID:"OPS" ELSE
            MAT OPCO.REC = ""
         END
         SCV.REC(5)<1> = CO.ID
         SCV.REC(7)<1> = CO.NAME
         SCV.REC(8)<1> = CO.ADDRESS
         SCV.REC(9)<1> = CO.CITY
         SCV.REC(10)<1> = CO.STATE
         SCV.REC(11)<1> = CO.ZIP
         SCV.REC(44)<1> = CO.PHONE ;* T26949
         SCV.REC(12)<1> = CO.ACCT.PIC
         SCV.REC(13)<1> = CO.ACCT.MATCH
         SCV.REC(14)<1> = CO.ACCT.STRUC
         SCV.REC(15)<1> = CO.CURR.YR.EARN
         SCV.REC(19)<1> = CO.CUST.ROLDX
         SCV.REC(20)<1> = CO.VEND.ROLDX
         SCV.REC(21)<1> = CO.ARS
         SCV.REC(22)<1> = CO.APS
         SCV.REC(23)<1> = CO.PRS
         SCV.REC(24)<1> = CO.FAS
         SCV.REC(25)<1> = CO.GLS
         SCV.REC(26)<1> = CO.SAS
         SCV.REC(27)<1> = CO.ICS
         SCV.REC(28)<1> = CO.POS
         SCV.REC(29)<1> = CO.OPS
         SCV.REC(30)<1> = CO.JES
         SCV.REC(31)<1> = CO.JCS
         SCV.REC(32)<1> = CO.PSS
         SCV.REC(33)<1> = CO.JIS
         SCV.REC(40)<1> = CO.401K
         SCV.REC(42)<1> = WIP.TRACKING    ;*T23359
         SCV.REC(43)<1> = "Y" ;*T26090
         IF OPCO.CNTY = "" THEN
            COUNTRY = ""
         END ELSE
        *T25159 v
        * READ COUNTRY FROM COUNTRY.CODE, CO.ID:OPCO.CNTY ELSE
            MATREAD CTY.REC FROM COUNTRY.CODE, CO.ID:OPCO.CNTY THEN
               COUNTRY = CTY.DESC
            END ELSE
        *T25159 ^
               COUNTRY = OPCO.CNTY
            END
         END
         SCV.REC(41)<1> = COUNTRY
         GOSUB 63000
      CASE ECD.RET.VALUE # "" AND ECD.VALDAT.ITEM = ""
         CO.ID = ECD.RET.VALUE
         READU RL.SK.CONTROL FROM CONTROL,CO.ID:"RL.SK.CONTROL" ELSE RL.SK.CONTROL =''
         READU WIP.TRACKING FROM CONTROL,CO.ID:"WIP.TRACKING" ELSE WIP.TRACKING =''     ;*T23359
*T21360 v
         READU MCD.PURGE.PDS FROM CONTROL, CO.ID:"MCD.PURGE.PERIODS" ELSE MCD.PURGE.PDS = 1
*T21360 ^
* T26090 v
         READU EMAIL_FORMAT.REC FROM CONTROL, CO.ID:"EMAIL_FORMAT" ELSE
            EMAIL_FORMAT.REC = ""
         END
* T26090 ^
*T23359 v
*        FOR OPTION = 1 TO 24 WHILE ECD.RET.VALUE # "END"
*              ON OPTION GOSUB 101,102,103,104,105,106,107,108,109,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127
         ;* T26949 Add SET.PHONE option
         FOR OPTION = 1 TO 27 WHILE ECD.RET.VALUE # "END"
            ON OPTION GOSUB 101,102,103,104,105,SET.PHONE,106,107,108,109,113,114,131,115,116,117,118,119,120,121,122,123,124,125,126,127,130
*T23359 ^
         NEXT OPTION
         IF ECD.RET.VALUE = "END" THEN GOTO 100
* T26090 v
*     FOR OPTION = 1 TO 14
*       ON OPTION GOSUB 1001,1002,1003,1004,1005,1006,1007,1008,1009,1010,1011,1012,1013,1014
         FOR OPTION = 1 TO 15
            ON OPTION GOSUB 1018,1001,1002,1003,1004,1005,1006,1007,1008,1009,1010,1011,1012,1013,1014
* T26090 ^
         NEXT OPTION
         GOSUB 62000;GOSUB 63000
      CASE 1
         GOTO 100
   END CASE
*** OPTION ROUTINE
   MORE = 1
   LOOP
      ECD.NUM = 37
      SCV.REC(ECD.NUM)<1> = ""
      GOSUB 64000
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "E" OR OPTION = "END"
            RELEASE CONTROL, CO.ID      ;* T26126
            RELEASE CONTROL,CO.ID:"RL.SK.CONTROL"
            RELEASE CONTROL,CO.ID:"WIP.TRACKING"  ;*T23359
            RELEASE CONTROL,CO.ID:"MCD.PURGE.PERIODS"   ;*T21360
* T26090 v
            RELEASE CONTROL, CO.ID:"EMAIL_FORMAT"
* T26090 ^
            MORE = 0
*T23359 v
*           CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 25
*              ON OPTION GOSUB 101,102,103,104,105,106,107,108,109,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127
            ;* T26949 Add SET.PHONE option
         CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 27
            ON OPTION GOSUB 101,102,103,104,105,SET.PHONE,106,107,108,109,113,114,115,116,117,118,119,120,121,122,123,124,125,126,127,130
*T23359 ^
         CASE OPTION = "F"
            MATWRITE COMP.REC ON COMPANY,CO.ID
            MATWRITE FIC.REC ON FICA.TABLE,CO.ID
            MATWRITE OPCO.REC ON CONTROL, CO.ID:"OPS"
            WRITE RL.SK.CONTROL ON CONTROL,CO.ID:"RL.SK.CONTROL"
            WRITE WIP.TRACKING ON CONTROL,CO.ID:"WIP.TRACKING"  ;*T23359
*T21360 v
            WRITE MCD.PURGE.PDS ON CONTROL,CO.ID:"MCD.PURGE.PERIODS"
*T21360 ^
* T26090 v
            WRITE EMAIL_FORMAT.REC ON CONTROL, CO.ID:"EMAIL_FORMAT"
* T26090 ^
            HD = ""
            L = LEN(CO.NAME)
            FOR I = 1 TO L
               HD = HD : " " : CO.NAME[I,1]
            NEXT I
            HD = SPACE(INT((132-LEN(HD))/2)) : HD
            WRITE HD ON CONTROL, CO.ID : "HEADING"
            IF CO.ID = "001" THEN
*T23955 v
*                 X = 0 ; Y = 21 ; MAXL = 1 ; TYP = 8 
               X = 3 ; Y = 21 ; MAXL = 1 ; TYP = 8 
*T23955 ^
               PMSG = ' IS "001" YOUR ONLY COMPANY (Y/N) : '
               CALL EDIT.SUB
               IF VALUE = "N" THEN
                  DELETE CONTROL, "MASTER"
               END ELSE
                  WRITE CO.ID ON CONTROL, "MASTER"
                  ECD.ACTION = 99 ; CALL SCRN.EDIT
                  GOTO 999999
               END
            END ELSE
               DELETE CONTROL, "MASTER"
            END
            MORE = 0
         CASE OPTION = "ARS"
            GOSUB 1001
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "APS"
            GOSUB 1002
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "PRS"
            GOSUB 1003
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "FAS"
            GOSUB 1004
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "GLS"
            GOSUB 1005
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "SAS"
            GOSUB 1006
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "ICS"
            GOSUB 1007
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "POS"
            GOSUB 1008
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "OPS"
            GOSUB 1009
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "JES"
            GOSUB 1010
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "JCS"
            GOSUB 1011
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "PSS"
            GOSUB 1012
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "JIS"
            GOSUB 1014
            GOSUB 62000; GOSUB 63000
         CASE OPTION = "WTR"
            GOSUB 1013
            GOSUB 62000; GOSUB 63000
* T26090 v
         CASE OPTION = "PMC"
            GOSUB 1018
            GOSUB 62000; GOSUB 63000
* T26090 ^
         CASE 1
            ERRMSG = "* * INVALID SELECTION * *"; GOSUB 91000
      END CASE
   WHILE MORE DO REPEAT
   GOTO 100
101 ECD.NUM = 7; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.NAME = ECD.RET.VALUE
   RETURN
102 ECD.NUM = 8; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.ADDRESS = ECD.RET.VALUE
   RETURN
103 ECD.NUM = 9; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.CITY = ECD.RET.VALUE
   RETURN
104 ECD.NUM = 10; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.STATE = ECD.RET.VALUE
   RETURN
105 ECD.NUM = 11; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN
      CO.ZIP = ECD.RET.VALUE
      GOSUB 301
   END
   RETURN
*T26949 v
SET.PHONE: 
   ECD.NUM = 44; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.PHONE = ECD.RET.VALUE
   RETURN
*T26949 ^
106 ECD.NUM = 12; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.ACCT.PIC = ECD.RET.VALUE
   RETURN
107 ECD.NUM = 13; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.ACCT.MATCH = ECD.RET.VALUE
   RETURN
108 ECD.NUM = 14; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN
      CO.ACCT.STRUC = ECD.RET.VALUE
      CO.ACCT.MASK = ""
      SCNT = COUNT(CO.ACCT.STRUC,"-") + 1
      FOR S = 1 TO SCNT
         IF NOT(NUM(FIELD(CO.ACCT.STRUC,"-",S))) THEN GOTO 108
         CO.ACCT.MASK = CO.ACCT.MASK : STR("#",FIELD(CO.ACCT.STRUC,"-",S)) : "-"
      NEXT S
      CO.ACCT.MASK = CO.ACCT.MASK[1,LEN(CO.ACCT.MASK)-1]
      CO.ACCT.MASK = CO.ACCT.MASK : STR("#",14-LEN(CO.ACCT.MASK))
   END
   RETURN
109 ECD.NUM = 15; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.CURR.YR.EARN = ECD.RET.VALUE
   RETURN
113 ECD.NUM = 19; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.CUST.ROLDX = ECD.RET.VALUE
   RETURN
114 ECD.NUM = 20; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.VEND.ROLDX = ECD.RET.VALUE
   RETURN
115 ECD.NUM = 21; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.ARS = ECD.RET.VALUE
   RETURN
116 ECD.NUM = 22; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.APS = ECD.RET.VALUE
   RETURN
117 ECD.NUM = 23; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.PRS = ECD.RET.VALUE
   IF ECD.RET.VALUE = "Y" OR ECD.RET.VALUE = 'E' THEN
      ECD.NUM = 40; GOSUB 64000
      IF ECD.RET.VALUE # "END" THEN CO.401K = ECD.RET.VALUE
   END
   IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "N" THEN CO.401K = "N"
   RETURN
118 ECD.NUM = 24; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.FAS = ECD.RET.VALUE
   RETURN
119 ECD.NUM = 25; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.GLS = ECD.RET.VALUE
   RETURN
120 ECD.NUM = 26; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN
      CO.SAS = ECD.RET.VALUE
      IF CO.SAS = "N" THEN CO.SAS.INVOICE = CO.SAS
   END
   RETURN
121 ECD.NUM = 27; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.ICS = ECD.RET.VALUE
   RETURN
122 ECD.NUM = 28; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.POS = ECD.RET.VALUE
   RETURN
123 ECD.NUM = 29; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.OPS = ECD.RET.VALUE
   RETURN
124 ECD.NUM = 30; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.JES = ECD.RET.VALUE
   RETURN
125 ECD.NUM = 31; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.JCS = ECD.RET.VALUE
*     CO.OIS = CO.JCS
   RETURN
126 ECD.NUM = 32; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.PSS = ECD.RET.VALUE
   RETURN
127 ECD.NUM = 33; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN CO.JIS = ECD.RET.VALUE
   RETURN
*T23359 v    (WIP TRACKING FLAG)
130 ECD.NUM = 42; GOSUB 64000
   IF ECD.RET.VALUE # "END" THEN WIP.TRACKING = ECD.RET.VALUE
   RETURN
*T23359 ^
*T26090 v
131 ECD.NUM = 43
   SCV.REC(ECD.NUM)<1> = "Y"
   GOSUB 65000
   RETURN
*T26090 ^
301*
   ECD.NUM = 41; SCV.REC(ECD.NUM)<1> = OPCO.CNTY
   GOSUB 65000; ECD.MAXL = 3; GOSUB 64000
   BEGIN CASE
      CASE ECD.RET.VALUE="END"
         GOTO 309
      CASE ECD.RET.VALUE = ""
         COUNTRY = ""
      CASE 1
         IF ECD.RET.VALUE = "???" THEN
            GXR.CO = CO.ID
            GXR.NAME = "GEN.CODE1"
            GXR.SORT.FILE = "COUNTRY.CODE"
            GXR.FILE = COUNTRY.CODE
            CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
            GOSUB 62000
            SCV.REC(ECD.NUM)<1> = GXR.ID
            GOSUB 63000
            IF GXR.ID = "" THEN GOTO 301
            ECD.RET.VALUE = GXR.ID
         END
      *T25159 v
      * READ COUNTRY FROM COUNTRY.CODE,CO.ID:ECD.RET.VALUE ELSE
         MATREAD CTY.REC FROM COUNTRY.CODE,CO.ID:ECD.RET.VALUE THEN
            COUNTRY = CTY.DESC         
         END ELSE
      *T25159 ^
            ERRMSG="Cannot locate country code # ":ECD.RET.VALUE
            GOSUB 91000; GOTO 301
         END
   END CASE
   OPCO.CNTY = ECD.RET.VALUE
   ECD.NUM = 41; SCV.REC(ECD.NUM)<1>=COUNTRY
   GOSUB 65000
309*
   RETURN
1001 IF CO.ARS = "Y" THEN
      ECD.SCRN.NO = 2
      CALL COMP.ARS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1002 IF CO.APS = "Y" THEN
      ECD.SCRN.NO = 3
      CALL COMP.APS.SUB(CO.ID,MCD.PURGE.PDS) ;*T21360
      ECD.SCRN.NO = 1
   END
   RETURN
1003 BEGIN CASE
      CASE CO.PRS = "E" AND CO.401K = "Y"
         ECD.SCRN.NO = 17
         CALL EP4.COMP.PRS.SUB(CO.ID)
         ECD.SCRN.NO = 1
      CASE CO.PRS = "E" AND CO.401K = "N"
         ECD.SCRN.NO = 16
         CALL EPS.COMP.PRS.SUB(CO.ID)
         ECD.SCRN.NO = 1
      CASE CO.PRS = "Y" AND CO.401K = "Y"
         ECD.SCRN.NO = 15
         CALL PR4.COMP.PRS.SUB(CO.ID)
         ECD.SCRN.NO = 1
      CASE CO.PRS = "Y" AND CO.401K = "N"
         ECD.SCRN.NO = 4
         CALL COMP.PRS.SUB(CO.ID)
         ECD.SCRN.NO = 1
   END CASE
   RETURN
1004 IF CO.FAS = "Y" THEN
      ECD.SCRN.NO = 5
      CALL COMP.FAS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1005 IF CO.GLS = "Y" THEN
      ECD.SCRN.NO = 6
      CALL COMP.GLS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1006 IF CO.SAS = "Y" THEN
      ECD.SCRN.NO = 7
      CALL COMP.SAS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1007 IF CO.ICS = "Y" THEN
      ECD.SCRN.NO = 8
      CALL COMP.ICS.SUB(CO.ID,RL.SK.CONTROL)
      ECD.SCRN.NO = 1
   END
   RETURN
1008 IF CO.POS = "Y" THEN
      ECD.SCRN.NO = 9
      CALL COMP.POS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1009 IF CO.OPS = "Y" THEN
      ECD.SCRN.NO = 10
      CALL COMP.OPS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1010 IF CO.JES = "Y" THEN
      ECD.SCRN.NO = 11
      CALL COMP.JES.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1011 IF CO.JCS = "Y" THEN
      ECD.SCRN.NO = 12
      CALL COMP.JCS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1012 IF CO.PSS = "Y" THEN
      ECD.SCRN.NO = 13
      CALL COMP.PSS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1013 IF WIP.TRACKING = "Y" THEN
      ECD.SCRN.NO = 14
      CALL COMP.WTR.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1014 IF CO.JIS = "Y" THEN
*T26572 v
*   ECD.SCRN.NO = 14
      ECD.SCRN.NO = 19
*T26572 ^
      CALL COMP.JIS.SUB(CO.ID)
      ECD.SCRN.NO = 1
   END
   RETURN
1018*
   COMP_PMC_FLAGS = ""
   COMP_PMC_FLAGS<CO.EMAIL_FORMAT.ATT> = EMAIL_FORMAT.REC<1>
   ECD.SCRN.NO = 18
   CALL COMP.PMC.SUB(CO.ID, COMP_PMC_FLAGS)
   ECD.SCRN.NO = 1
   EMAIL_FORMAT.REC<1> = COMP_PMC_FLAGS<CO.EMAIL_FORMAT.ATT>
   RETURN
*** CALLS FOR SCRN.EDIT
61000 ECD.ACTION = 1; CALL SCRN.EDIT
   RETURN
62000 ECD.ACTION = 2; CALL SCRN.EDIT
   RETURN
63000 ECD.ACTION = 3; CALL SCRN.EDIT
   RETURN
64000 ECD.ACTION = 4; CALL SCRN.EDIT
   RETURN
65000 ECD.ACTION = 5; CALL SCRN.EDIT
   RETURN
*
*** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999 *
*       PRINT @(-1):
END
