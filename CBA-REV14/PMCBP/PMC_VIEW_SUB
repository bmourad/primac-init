   SUBROUTINE PMC_VIEW_SUB (EMAIL_PARAM, ERRMSG)
*************************************************************
* REVISION      - [12.0]
* Copyright 1997 by Vercom Software, Inc.
* SOURCE        - CBABP
* PROGRAM       - PMC_VIEW_SUB
* DESCRIPTION   -
*
* PROGRAMS REQUIRED FOR MAIL FEATURE:
*  -GHOSTSCRIPT, MPACK (UNIX)
*  -GHOSTSCRIPT, BLAT  (WINDOWS)
*************************************************************
*
*COPY>CPYLIB>CHAR
*
*---- Initialization
*
   ERRMSG = ""
   OPEN '',"_HOLD_" TO HOLD.FILE ELSE
      ERRMSG = "HOLD FILE IS MISSING"
      GOTO 999999
   END
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 999999
*
*---- Read in hold file record
*
   CONO = EMAIL_PARAM<1>
   CO.NAME = EMAIL_PARAM<2>
   HF.NAME = EMAIL_PARAM<3>
   EMAIL.FORMAT = EMAIL_PARAM<4>
   EMAIL.ADDRESS = EMAIL_PARAM<5>
   SUBJECT = EMAIL_PARAM<6>
   EMAIL.PLATFORM = EMAIL_PARAM<7>
   PPSR_ID = EMAIL_PARAM<8>
   RB.FLAG = EMAIL_PARAM<9>
   VIEW_ID = ""
*
   IF PPSR_ID = "" THEN
      P_DATE = DATE()
      READU XXX_REC FROM HOLD.FILE, CONO:"PMC_VIEW" ELSE XXX_REC = ""
      PPSR_ID = ""
      LOOP
      UNTIL PPSR_ID # "" DO
         IF XXX_REC<1> = "" OR P_DATE > XXX_REC<1> THEN
            XXX_REC<1> = P_DATE
            XXX_REC<2> = 0
         END
         XXX_REC<2> += 1
         PPSR_ID = CONO : P_DATE : "!" : XXX_REC<2>
         READU DUMMY FROM HOLD.FILE, PPSR_ID THEN
            PPSR_ID = ""
         END LOCKED
            PPSR_ID = ""
         END
      REPEAT
      DUMMY = ""
      WRITE XXX_REC ON HOLD.FILE, CONO:"PMC_VIEW"
   END
*
   READ HOLD.REC FROM HOLD.FILE, HF.NAME ELSE HOLD.REC = ''
   WRITE HOLD.REC ON HOLD.FILE, PPSR_ID
*
   BEGIN CASE
      CASE EMAIL.FORMAT = 1
         FORMATSTR = ''
         FORMATSTR<1,1> = "WIDE REPORT"
         FORMATSTR<1,2> = "FONT SIZE"
         FORMATSTR<2,2> = 9
         CALL CONVERT.TO.RTF(CO.NAME,"_HOLD_",PPSR_ID,SUBJECT,FORMATSTR)
         VIEW_ID = PPSR_ID : ".rtf"
         IF EMAIL.PLATFORM = "WINDOWS" THEN
            UDTEXECUTE "!wordpad _HOLD_\":VIEW_ID
         END
      CASE EMAIL.FORMAT = 2
         CALL CONVERT.TO.PDF("_HOLD_",PPSR_ID,'')
         VIEW_ID = PPSR_ID : ".pdf"
         IF EMAIL.PLATFORM = "WINDOWS" AND RB.FLAG = "" THEN
            UDTEXECUTE "!acrobat _HOLD_\":VIEW_ID
         END
   END CASE
*
   IF RB.FLAG # "" THEN
      STMT2 = "copy FROM _HOLD_ TO LOCAL_REPORTS "
      STMT2 := VIEW_ID : "," : VIEW_ID
     STMT2 := " DELETING"
      UDTEXECUTE STMT2 CAPTURING MESG
   END
*
*   DELETE HOLD.FILE, HF.NAME
*   DELETE HOLD.FILE, PPSR_ID
*   DELETE HOLD.FILE, VIEW_ID
*   DELETE HOLD.FILE, HF.NAME:".rtf"
*   DELETE HOLD.FILE, HF.NAME:".dsc"
*   DELETE HOLD.FILE, HF.NAME:".pdf"
999999*
   RETURN
END
