*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - PMCBP
* PROGRAM     - SALES.CODE.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 08/14/84
* DESCRIPTION -
*               This program will be used for Sales Code
*               Maintenance.
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*T26973 adelgado 10/31/2002 * Add a new field for description.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SALES.CODE
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
  OPEN '','SALES.CODE' TO SALES.CODE ELSE ERRMSG = 'SALES.CODE FILE MISSING';GOTO 93000
  OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING';GOTO 93000
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
*---- INITIALIZATION
*
  MAT EDIT.COM.DRIVER = ""
  MAT SLC.REC = ''
  ERRMSG = ''
  IN.ACCT.LEN = LEN(CO.ACCT.PIC)
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'SALES.CODE.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  SALES.CODE.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.SALES.CODE = 'NO'
      * T26126 v
      MATREADU SLC.REC FROM SALES.CODE,CONO:SALES.CODE.KEY LOCKED
        ERRMSG = 'SALES CODE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE
      * T26126 ^
        MAT SLC.REC = ''
        NEW.SALES.CODE = 'YES'
      END
  END CASE
  IF NEW.SALES.CODE = 'YES' THEN 
* T26973 v
    FOR REQUEST = 1 TO 4
      ON REQUEST GOSUB GET.DESC,20,30,40
* T26973 ^
      IF ECD.RET.VALUE = 'END' THEN 
        RELEASE SALES.CODE,CONO:SALES.CODE.KEY
        GOSUB 300
        GOTO 10
      END
    NEXT REQUEST
  END ELSE
    GOSUB 100
    ECD.ACTION = 3; CALL SCRN.EDIT
  END
  GOSUB 200
  GOSUB 300
  GOTO 10
* T26973 v
*
GET.DESC: 
*
  ECD.NUM = 9 ; ECD.ACTION = 4
  CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SLC.DESC = ECD.RET.VALUE
  END
  RETURN
* T26973 ^
*
*---- ENTER SALES CODE GENERAL LEDGER NUMBER
*
20*
  ECD.NUM = 2 ; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = "" ;ECD.ACTION = 5 ; CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN ;ECD.VALDAT.CODE = 2;ECD.VALDAT.FILE = COA;ECD.PREFIX.ID = CONO
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SLC.GL.ACCT ; ECD.PATRN = CO.ACCT.MATCH ;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SLC.GL.ACCT = ECD.RET.VALUE
    FOR L = 1 TO COA.REC.SIZE ; COA.REC(L) = ECD.VALDAT.ITEM<L> ; NEXT L
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SLC.GL.ACCT CO.ACCT.MASK ;ECD.ACTION = 5;CALL SCRN.EDIT
    ECD.NUM = 3 ;SCV.REC(ECD.NUM)<ECD.SCRN.NO> = COA.DESC ;ECD.ACTION = 5;CALL SCRN.EDIT
  END
  RETURN
*
*---- ENTER RET & ALLOW G/L NUMBER
*
30*
  ECD.NUM = 4 ; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = "" ;ECD.ACTION = 5 ; CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN ;ECD.VALDAT.CODE = 2;ECD.VALDAT.FILE = COA;ECD.PREFIX.ID = CONO
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SLC.RT.ACCT ; ECD.PATRN = CO.ACCT.MATCH ;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SLC.RT.ACCT = ECD.RET.VALUE
    FOR L = 1 TO COA.REC.SIZE ; COA.REC(L) = ECD.VALDAT.ITEM<L> ; NEXT L
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SLC.RT.ACCT CO.ACCT.MASK ;ECD.ACTION = 5;CALL SCRN.EDIT
    ECD.NUM = 5 ;SCV.REC(ECD.NUM)<ECD.SCRN.NO> = COA.DESC ;ECD.ACTION = 5;CALL SCRN.EDIT
  END
  RETURN
*
*---- ENTER DISCOUNT G/L NUMBER
*
40*
  ECD.NUM = 6 ; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = "" ;ECD.ACTION = 5 ; CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN ;ECD.VALDAT.CODE = 2;ECD.VALDAT.FILE = COA;ECD.PREFIX.ID = CONO
  SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SLC.DS.ACCT ; ECD.PATRN = CO.ACCT.MATCH ;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SLC.DS.ACCT = ECD.RET.VALUE
    FOR L = 1 TO COA.REC.SIZE ; COA.REC(L) = ECD.VALDAT.ITEM<L> ; NEXT L
    SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SLC.DS.ACCT CO.ACCT.MASK ;ECD.ACTION = 5;CALL SCRN.EDIT
    ECD.NUM = 7 ;SCV.REC(ECD.NUM)<ECD.SCRN.NO> = COA.DESC ;ECD.ACTION = 5;CALL SCRN.EDIT
  END
  RETURN
*
*---- GET ALL VALUES FROM FILE
*
100*
  SCV.REC(2)<ECD.SCRN.NO> = SLC.GL.ACCT CO.ACCT.MASK
  MATREAD COA.REC FROM COA,CONO:SLC.GL.ACCT ELSE COA.DESC = "UNKNOWN"
  SCV.REC(3)<ECD.SCRN.NO> = COA.DESC
  SCV.REC(4)<ECD.SCRN.NO> = SLC.RT.ACCT CO.ACCT.MASK
  MATREAD COA.REC FROM COA,CONO:SLC.RT.ACCT ELSE COA.DESC = "UNKNOWN"
  SCV.REC(5)<ECD.SCRN.NO> = COA.DESC
  SCV.REC(6)<ECD.SCRN.NO> = SLC.DS.ACCT CO.ACCT.MASK
  MATREAD COA.REC FROM COA,CONO:SLC.DS.ACCT ELSE COA.DESC = "UNKNOWN"
  SCV.REC(7)<ECD.SCRN.NO> = COA.DESC
  SCV.REC(9)<ECD.SCRN.NO> = SLC.DESC          ;* T26973
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 1
  LOOP
    ECD.NUM = 8
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE SALES.CODE,CONO:SALES.CODE.KEY
        MORE = 1
      CASE NUM(OPTION) AND OPTION > "0"
        ON OPTION GOSUB 20,30,40
        MORE = 0
      CASE OPTION = 'D'            ;* T26973
        GOSUB GET.DESC             ;* T26973
        MORE = 0                   ;* T26973
      CASE OPTION = 'F'
        GOSUB 400
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE SALES.CODES FILE
*
400*
  MATWRITE SLC.REC ON SALES.CODE,CONO:SALES.CODE.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000*
  ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
