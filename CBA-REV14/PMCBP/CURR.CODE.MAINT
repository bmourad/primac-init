*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMXBP
* PROGRAM     - CURR.CODE.MAINT
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC
* DATE        - 11/14/91
* DESCRIPTION -
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>CURR.CODE
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
  OPEN '','CURR.CODE' TO CURR.CODE ELSE ERRMSG = 'CURR.CODE FILE MISSING';GOTO 93000
  OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  MAT EDIT.COM.DRIVER = ""
  ERRMSG = ''
  UNKNOWN = STR("?",25)
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
  MATREAD COMP.REC FROM COMPANY, CONO ELSE
    MAT COMP.REC = ""
  END
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'CURR.CODE.MAINT'
  ECD.ACTION = 1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
  MAT CRC.REC = ""
  ECD.NUM = 1;ECD.ACTION = 4;CALL SCRN.EDIT
  CRC.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE CRC.KEY = 'END'
      GOTO 99999
    CASE CRC.KEY # ''
      NEW.CRC = 'NO'
      * T26126 v
      MATREADU CRC.REC FROM CURR.CODE, CONO:CRC.KEY LOCKED
        ERRMSG = 'CURR CODE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE
      * T26126 ^
        NEW.CRC = 'YES';MAT CRC.REC = ''
      END
  END CASE
  IF NEW.CRC = 'YES' THEN
    FOR W = 1 TO 3 UNTIL ECD.RET.VALUE = "END"
      ON W GOSUB 20,30,40
    NEXT W
    IF ECD.RET.VALUE = 'END' THEN
      GOSUB 300;GOTO 10
    END
  END ELSE
    SCV.REC(3)<ECD.SCRN.NO,1> = CRC.DESC
    SCV.REC(5)<ECD.SCRN.NO,1> = CRC.EX.RATE
    SCV.REC(6)<ECD.SCRN.NO,1> = CRC.COA CO.ACCT.MASK
    MATREAD COA.REC FROM COA, CONO:CRC.COA ELSE
      MAT COA.REC = ""
      COA.DESC = UNKNOWN
    END
    SCV.REC(20)<ECD.SCRN.NO,1> = COA.DESC
    ECD.ACTION = 3;CALL SCRN.EDIT
  END
  GOSUB 200
  GOSUB 300
  GOTO 10
*
*---- ENTER CURR.CODE DESCRIPTION
*
20*
  ECD.NUM = 3;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    CRC.DESC = ECD.RET.VALUE
  END
  RETURN
*
*---- ENTER CURR.CODE EX.RATE
*
30*
  ECD.NUM = 5;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    CRC.EX.RATE = ECD.RET.VALUE
  END
  RETURN
*
*---- ENTER CURR.CODE COA
*
40*
  ECD.NUM = 6;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    MATREAD COA.REC FROM COA, CONO:ECD.RET.VALUE ELSE
      ERRMSG = "INVALID ACCOUNT"
      GOSUB 91000
      GOTO 40
    END
    IF COA.TYPE # "A" THEN
      ERRMSG = "THIS IS NOT AN ASSET ACCOUNT"
      GOSUB 91000
      GOTO 40
    END
    CRC.COA = ECD.RET.VALUE
    SCV.REC(6)<ECD.SCRN.NO,1> = CRC.COA CO.ACCT.MASK
    ECD.NUM = 6; ECD.ACTION = 5; CALL SCRN.EDIT
    SCV.REC(20)<ECD.SCRN.NO,1> = COA.DESC
    ECD.NUM = 20; ECD.ACTION = 5; CALL SCRN.EDIT
  END
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 1
  LOOP
    ECD.NUM = 19;ECD.ACTION = 4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        MORE = 1
      CASE NUM(OPTION)
        ON OPTION GOSUB 20,30,40
        MORE = 0
      CASE OPTION = 'F'
        GOSUB 400
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN & RE-INITIALIZE
*
300*
  ECD.ACTION = 6;CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE CURR.CODE FILE
*
400*
  MATWRITE CRC.REC ON CURR.CODE , CONO:CRC.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
END
