  SUBROUTINE TAX.JUR.MAINT(CONO,CUST.KEY,MODE)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.CUST
*************************************************************
* REVISION      - [08.2]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - PMCBP
* PROGRAM       - TAX.JUR.MAINT
* BY            - JULIANNE RIVERA, VERCOM SOFTWARE, INC.
* DATE          - 07/16/92
* DESCRIPTION   - This program updates and maintains CUSTOMER file 
*                 tax jurisdiction information.
*T25978 adelgado 01/28/2002 * Add the use of prompts (S,SR,SB,ST).
*T26069 ajibaly 02/05/2002 * ADD INQUIRY ONLY MODE
*************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>TAX
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>CHAR
*
*---- CALL FOR SYSCOM
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- INITIALIZATION
*
  TODAY = DATE()
  LINE.SPACE = 1
  PAGE.SIZE = 14
  BEGIN.PAGE = 6
  OLD.START.LINE = 0
  LINES = 0
  START.LINE = 0
  OLD.LINES = 0
  OPTION = ''
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
  UNKNOWN = STR('?',30)
  ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
  SCV.REC(2)<ECD.SCRN.NO> = CUST.KEY
  ECD.NUM = 2; ECD.ACTION = 5; CALL SCRN.EDIT
  SCV.REC(3)<ECD.SCRN.NO> = CUST.NAME
  ECD.NUM = 3; ECD.ACTION = 5; CALL SCRN.EDIT
  LINES = DCOUNT(CUST.TAX.JUR,VM)
  *T26069> IF LINES > 0 THEN
  IF LINES > 0 OR MODE = 'I' THEN
    LN = 1
    OLD.START.LINE = 0
  END ELSE
    REQUEST = "A"
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 100
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END
  GOSUB 600
  GOSUB 3000
  GOTO 99999
*
*---- ENTER TAX SCREEN
*
100 *
  GOSUB 600
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
  P_X  := AM:0 ; P_Y := AM:SLN ; P_VALUE := AM:LN "R#2"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
200 *
  X = 4; Y = SLN; TYP = 1; MAXL = 5
  IF CUST.TAX.JUR<1,LN> # "" THEN
    DEFAULT = CUST.TAX.JUR<1,LN>
    O.R = "O"
  END
  HMSG = "ENTER TAX JURISDICTION CODE"
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = ''
      IF REQUEST = "C" THEN GOTO 200
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 599
    CASE VALUE = "END"
      IF REQUEST = 'A' THEN
        CUST.TAX.JUR = DELETE(CUST.TAX.JUR,1,LN,0)
        CUST.TAXABLE = DELETE(CUST.TAXABLE,1,LN,0)
        CUST.EXEMPT = DELETE(CUST.EXEMPT,1,LN,0)
        CUST.EXMT.DATE = DELETE(CUST.EXMT.DATE,1,LN,0)
        P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END ELSE
        P_X  = 4 ; P_Y = SLN ; P_VALUE = CUST.TAX.JUR<1,LN>"L#5" ; P_OPT = ""
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        MATREAD TAX.REC FROM TAX,CONO:CUST.TAX.JUR<1,LN> ELSE TAX.JUR = UNKNOWN
        P_X  = 10 ; P_Y = SLN ; P_VALUE = TAX.JUR "L#30" ; P_OPT = ""
        P_X  := AM:45 ; P_Y := AM:SLN ; P_VALUE := AM:CUST.TAXABLE<1,LN>"L#1"
        P_X  := AM:51 ; P_Y := AM:SLN ; P_VALUE := AM:CUST.EXEMPT<1,LN>"L#15"
        P_X  := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(CUST.EXMT.DATE<1,LN>,"D2/")"L#8"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      GOTO 599
    CASE VALUE = "???"
      GXR.NAME = "GEN.CODE"
      GXR.SORT.FILE = "TAX"
      GXR.FILE = TAX
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION = 2; CALL SCRN.EDIT
      ECD.ACTION = 3; CALL SCRN.EDIT
      OLD.START.LINE = 0
      IF GXR.ID = "" THEN GOTO 100
      GOSUB 600; VALUE = GXR.ID
      SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#2" ; P_OPT = ""
      P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:VALUE"L#5"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END CASE
  MATREAD TAX.REC FROM TAX,CONO:VALUE ELSE
    ERRMSG = "TAX JURISDICTION CODE IS INVALID";GOSUB 91000;GOTO 200
  END
  CUST.TAX.JUR<1,LN> = VALUE
  P_X  = 10 ; P_Y = SLN ; P_VALUE = TAX.JUR "L#30" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
300 *
  X = 45;Y = SLN;TYP = 8;MAXL = 1
  IF CUST.TAXABLE<1,LN> # "" THEN
    DEFAULT = CUST.TAXABLE<1,LN>
    O.R = "O"
  END
  HMSG = "ENTER TAXABLE (Y/N)"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 200
  IF VALUE = '' THEN GOTO 300
  CUST.TAXABLE<1,LN> = VALUE
400 *
  IF CUST.TAXABLE<1,LN> = "Y" THEN
    CUST.EXEMPT<1,LN> = ''
    CUST.EXMT.DATE<1,LN> = ""
    P_X  = 51 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 599
  END
  X = 51;Y = SLN;TYP = 1;MAXL = 15;MAXV = '';MINV = ''
  IF CUST.EXEMPT<1,LN> # "" THEN
    DEFAULT = CUST.EXEMPT<1,LN>;O.R = "O"
  END
  HMSG = "ENTER TAX EXEMPT NUMBER"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 200
  IF VALUE = "" THEN GOTO 400
  CUST.EXEMPT<1,LN> = VALUE
500 *
  X = 68;Y = SLN;TYP = 6;MAXL = 8
  IF CUST.EXMT.DATE<1,LN> # "" THEN
    DEFAULT = OCONV(CUST.EXMT.DATE<1,LN>,"D2/");O.R = "O"
  END
  HMSG = "ENTER TAX EXEMPT EXPIRATION DATE"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 200
  CUST.EXMT.DATE<1,LN> = VALUE
599 *
  LINES = DCOUNT(CUST.TAX.JUR,VM)
  RETURN
*
*---- SCROLL ROUTINE
*
600 *
  START.LINE = 1 + INT((LN-1) / PAGE.SIZE) * PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN GOTO 169
  OLD.START.LINE = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR L = START.LINE TO LAST.LINE UNTIL L > LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(L-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = L "R#2" ; P_OPT = ""
    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:CUST.TAX.JUR<1,L>"L#5"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    MATREAD TAX.REC FROM TAX,CONO:CUST.TAX.JUR<1,L> ELSE TAX.JUR = UNKNOWN
    P_X  = 10 ; P_Y = SLN ; P_VALUE = TAX.JUR "L#30" ; P_OPT = ""
    P_X  := AM:45 ; P_Y := AM:SLN ; P_VALUE := AM:CUST.TAXABLE<1,L>"L#1"
    P_X  := AM:51 ; P_Y := AM:SLN ; P_VALUE := AM:CUST.EXEMPT<1,L>"L#15"
    P_X  := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(CUST.EXMT.DATE<1,L>,"D2/")"L#8"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT L
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
169 *
  RETURN
*
*---- GET LINE NUMBER
*
700 *
  GOSUB 600
  ECD.NUM = 29
  IF LAST.LINE < LINES THEN
    ECD.MAXV = LAST.LINE
  END ELSE
    ECD.MAXV = LINES
  END
  SCV.REC(29)<ECD.SCRN.NO,1> = ''
  ECD.ACTION = 4;CALL SCRN.EDIT
  P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LNO = ECD.RET.VALUE
  IF LNO = '' OR LNO = "END" THEN LNO = 0
  RETURN
*
*---- OPTIONS FOR TAX SCREEN
*
3000 *
  AGAIN = 0
  LOOP
    ECD.NUM = 34
    IF MODE = 'I' THEN ECD.NUM = 42 ;*T26069
    SCV.REC(ECD.NUM)<ECD.SCRN.NO,1> = ''
    ECD.ACTION = 4;CALL SCRN.EDIT
    REQUEST = ECD.RET.VALUE
    BEGIN CASE
      CASE REQUEST = ''
        AGAIN = 1
      CASE REQUEST = 'END'
        AGAIN = 1
      CASE REQUEST = "A"
        LINES = COUNT(CUST.TAX.JUR,VM) + (CUST.TAX.JUR # '')
        LOOP
          LN = LINES + 1
          OLD.LINES = LINES
          GOSUB 100
        WHILE LINES > OLD.LINES DO REPEAT
        LN = LINES
        GOSUB 600
      CASE REQUEST = "S"
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 600
      * T25978 v
      CASE REQUEST = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 600
      CASE REQUEST = 'ST'
        LN = 1
        GOSUB 600
      CASE REQUEST = 'SB'
        LN = LINES
        GOSUB 600
     * T25978 ^
      CASE REQUEST = "C"
        GOSUB 700
        IF LNO THEN
          LN = LNO
          SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          GOSUB 200
        END
        GOSUB 600
      CASE REQUEST = "D"
        GOSUB 700
        IF LNO THEN
          LN = LNO
          CUST.TAX.JUR = DELETE(CUST.TAX.JUR,1,LN,0)
          CUST.TAXABLE = DELETE(CUST.TAXABLE,1,LN,0)
          CUST.EXEMPT = DELETE(CUST.EXEMPT,1,LN,0)
          CUST.EXMT.DATE = DELETE(CUST.EXMT.DATE,1,LN,0)
          LINES = COUNT(CUST.TAX.JUR,VM) + (CUST.TAX.JUR # '')
          IF LN > 1 AND LN > LINES THEN LN = LN - 1
          IF LN < 1 THEN LN = 1
          OLD.START.LINE = 0
        END
        GOSUB 600
      CASE 1
        ERRMSG = "INVALID OPTION";GOSUB 91000;GOTO 3000
    END CASE
3100 *
  WHILE AGAIN = 0 DO REPEAT
  RETURN
*
*--- REPRINT SCREEN
*
*
*---- CALLS FOR SYSCOM
*
91000 *
  ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000 *
  ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 *
  ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
