      SUBROUTINE FIELD.EDIT(PS,LN,MAXL,DVALUE,DEFAULT)
*******************************************************************
* Program Id:   FIELD.EDIT
* Written By:   Duane E. Green
* 
* Comments:
*     This program is called from SCREEN.INPUT, SCRN.EDIT, and 
*     EDIT.SUB and handles cursor navigation in pure alphanumeric
*     fields which meet the minimum length as set in the CONTROL 
*     file.
* DEG - 11/30/94 - ADDED vt220 Support
*T21177 diane 11/06/1996 * REV11 UPG
*******************************************************************
*IF @LOGNAME = "diane" OR @LOGNAME = "ziad" THEN
*CRT @(0,23):"IN FIELD EDIT- PS/LN/MAXL/DVALUE/DEFAULT = (":PS:'/':LN:'/':MAXL:'/':DVALUE:'/':DEFAULT:')'
**DEBUG
*END
* Reset The Backspace Key Mapping
      CMD = "PTERM -ERASE '|'"
      EXECUTE CMD CAPTURING XXX
* Setup Program Variables
      TRMNL = SYSTEM(7)
      OPS = PS
      OLN = LN
      EOSKEY = CHAR(5)
      DLKY = CHAR(4)
      INSKY = CHAR(9)
      BSKEY = CHAR(8)
      FDKEY = CHAR(11)
*
      IF TRMNL # 'prism4' AND TRMNL # 'p4' THEN
         CRT @(OPS,LN):@(-13):DVALUE:@(-14):
         CRT @(-13):@((OPS+LEN(DVALUE)),LN):STR('_',(MAXL-LEN(DVALUE))):@(-14):
      END ELSE
         CRT @(OPS,LN):DVALUE
         CRT @((OPS+LEN(DVALUE)),LN):STR('_',(MAXL-LEN(DVALUE))):
      END
*
* Display The Help Message Line
      IF LN # 23 THEN
         CRT @(0,23):@(-4):"'->' '<-', CTL+: K (Del Fld), I (Ins), D (Del Char), E (Del-EOS)":
      END
*
      FOR A = 1 TO MAXL
10       CRT @(PS,LN):
         INPUT INFLD,1:
         IF TRMNL[1,3] = 'ibm' THEN
            IF INFLD = CHAR(27) THEN
               INPUT @(PS,LN):INFLD2,1:
               IF INFLD2 = 'D' THEN
                  INFLD = BSKEY
               END
               IF INFLD2 = 'C' THEN
                  INFLD = CHAR(12)
               END
               CRT @(OPS,LN):DVALUE:
               IF INFLD2 = '' THEN
                  INFLD = ''
               END
            END
         END
**The Next Statement is for vt100 on macintosh.
*         IF TRMNL = 'vt100' THEN
*            IF INFLD = CHAR(155) THEN
*               INPUT @(PS,LN):INFLD2,1:
*               IF INFLD2 = 'D' THEN
*                  INFLD = BSKEY
*               END
*               IF INFLD2 = 'C' THEN
*                  INFLD = CHAR(12)
*               END
*               CRT @(OPS,LN):DVALUE:
*               IF INFLD2 = '' THEN
*                  INFLD = ''
*               END
*            END
*         END
*         IF TRMNL = 'vt220' THEN
         IF TRMNL[1,2] = 'vt' THEN
            IF INFLD = CHAR(27) THEN
               INPUT @(PS,LN):INFLD2,2:
               IF INFLD2 = '[D' THEN
                  INFLD = BSKEY
               END
               IF INFLD2 = '[C' THEN
                  INFLD = CHAR(12)
               END
               CRT @(OPS,LN):SPACE(MAXL):
               CRT @(OPS,LN):DVALUE:
               IF INFLD2 = '' THEN
*                      INFLD = ''
               END
            END
         END
* This Is The Escape Key
         IF INFLD = CHAR(27) THEN 
            FMT = 'L#':MAXL
            CRT @(OPS,LN):DVALUE FMT:SPACE(1)
*            DVALUE = 'END'
            IF DEFAULT = '' THEN DVALUE = 'END'
            GO 99999
         END
* This Is The Delete To EOS Key
         IF INFLD = EOSKEY THEN
            DVALUE = DVALUE[1,(PS-OPS)]
            DEFAULT = DVALUE
            CRT @(OPS,LN):DVALUE:
            GO 99999
         END
* This Is The Backspace Key
         IF INFLD = BSKEY THEN
            IF A < 2 THEN GO 10
* The Next Two Lines Are Commented Out So Backspace Will Not Be Destructive
*            CRT @(PS,LN):' ':
*            DVALUE[A,1] = ' '
            PS = PS - 1
            A = A - 1
            GO 10
         END
* Delete A Single Character
         IF INFLD = DLKY THEN
            FOR B = A TO MAXL
               DVALUE[B,1] = DVALUE[(B+1),1]
            NEXT B
            CRT @(OPS,LN):DVALUE:
            GOTO 10
         END
* Delete Data In The Entire Field
         IF INFLD = FDKEY THEN
            DVALUE = ''
            DEFAULT = ''
            JFYR = "L#":MAXL
            CRT @(OPS,LN):DVALUE JFYR:
            GO 99999
         END
* Insert Routine
         IF INFLD = INSKY THEN
            CRT @(0,23):@(-4):'Tap Enter To Exit Insert Mode':
15          FOR B = MAXL TO A STEP - 1
               DVALUE[B,1] = DVALUE[(B-1),1]
            NEXT B
            DVALUE[A,1] = ' '
            CRT @(OPS,LN):DVALUE:
20          CRT @(PS,LN):
            INPUT INFLD,1:
            IF (INFLD GE CHAR(32)) AND (INFLD LE CHAR(125)) THEN
               DVALUE[A,1] = INFLD
               PS += 1
               A += 1
               GOTO 15
            END
* Redisplay The Help Message Line
            IF INFLD =  '' THEN 
               CRT @(0,23):@(-4):"'->' '<-', CTL+: K (Del Fld), I (Ins), D (Del Char), E (Del-EOS)":
               GO 10
            END
            GO 20
         END
* Entry Complete - Exit Routine
         IF INFLD = '' THEN
            GO 99999
         END
* Forward Cursor One Position
         IF INFLD = CHAR(12) OR INFLD = CHAR(6) THEN
            PS += 1
            GOTO 99
         END
* If Valid ASCII CHARACTER Update The Data Field
         IF (INFLD GE CHAR(32)) AND (INFLD LE CHAR(125)) THEN
            DVALUE[A,1] = INFLD
            PS += 1
            GOTO 99
         END
         PS = PS-1
         A = A - 1
         GO 10
*
99    NEXT A
*
99999 DVALUE = TRIMB(DVALUE)
      PS = OPS ; LN = OLN
      CRT @(0,23):@(-4):
      CMD = "PTERM -ERASE ":"'":BSKEY:"'"
      EXECUTE CMD CAPTURING XXX
*IF @LOGNAME = "diane" OR @LOGNAME = "ziad" THEN
*CRT @(0,23):"OUT OF FIELD EDIT- PS/LN/MAXL/DVALUE/DEFAULT = (":PS:'/':LN:'/':MAXL:'/':DVALUE:'/':DEFAULT:')'
*END
      RETURN
