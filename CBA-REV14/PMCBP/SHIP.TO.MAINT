  SUBROUTINE SHIP.TO.MAINT(CONO,CUSTNO,SHPNO,OPCO.CNTY,MODE)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - FGSBP
* PROGRAM       - SHIP.TO.MAINT
* BY            - Ziad Yamout, VERCOM Software Inc.
* DATE          - 01/24/90
* DESCRIPTION   - This pgm is the same as CUST.SHIP.TO.MAINT except the
*                 commons are different.
*                 They both share the same screen so if a change is made
*                 to this pgm it needs to also be changed in the pgm
*                 CUST.SHIP.TO.MAINT
*T25159 alex 05/11/2000 * Add PMC.CPYLIB>COUNTRY.CODE, since the
*                         COUNTRY.CODE file can hold multiple attributes
*                         of data.
*T25978 adelgado 01/30/2002 * Add the use of prompts (SF,SR,SB,ST).
*T26069 ajibaly 02/05/2002 * ADD INQUIRY READ ONLY MODE
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
********************************************************************
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>PMC.CPYLIB>TAX
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>FOB
*COPY>PMC.CPYLIB>COUNTRY.CODE         ;*T25159
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
  LN = 1
  LINE.SPACE=1
  PAGE.SIZE=2
  C.PAGE.SIZE=3
  BEGIN.PAGE=18
  C.BEGIN.PAGE=12
  LINES=0
  START.LINE=0
  OLD.LINES=0
  OPTION=''
  OLD.NAME=''
  NEW.NAME=''
  UNKNOWN = "Unknown"
*
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
*
*---- Display screen
*
  ESN = ECD.SCRN.NO
  SCV.REC(2)<ESN> = CUSTNO
  SCV.REC(4)<ESN> = CUST.NAME
  SCV.REC(6)<ESN> = CUST.ADDR1
  SCV.REC(8)<ESN> = CUST.ADDR2
  SCV.REC(10)<ESN> = CUST.ADDR3
  SCV.REC(13)<ESN>=FIELD(CUST.ADDR4,",",1) "L#20":" ,  "
  SCV.REC(13)<ESN>=SCV.REC(13)<ESN>:TRIM(FIELD(CUST.ADDR4,",",2,99))
  SCV.REC(16)<ESN> = CUST.ZIP
  IF CUST.ADDL.OPS<1,4> = "" THEN
    COUNTRY = ""
  END ELSE
    *T25159 v
    * READ COUNTRY FROM COUNTRY.CODE,CONO:CUST.ADDL.OPS<1,4> ELSE
    MATREAD CTY.REC FROM COUNTRY.CODE,CONO:CUST.ADDL.OPS<1,4> THEN
      COUNTRY = CTY.DESC
    END ELSE
    *T25159 ^
      COUNTRY = CUST.ADDL.OPS<1,4>
    END
  END
  SCV.REC(54)<ESN>=COUNTRY
  IF SHPNO = "" THEN
    GOSUB 8050
  END ELSE
    SCV.REC(1)<ESN> = SHPNO
    GOSUB 8050
    ECD.NUM = 1; ECD.RET.VALUE = SHPNO
    GOTO 110
  END
*
*---- Ship to
100*
  ECD.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
110*
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOSUB 8900; GOTO 99999
    CASE ECD.RET.VALUE = "N"
      MAT SPT.REC = ""
      FOR OPTION = 1 TO 13 WHILE ECD.RET.VALUE # "END"
        ON OPTION GOSUB 1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200,2300
      NEXT OPTION
      IF ECD.RET.VALUE = "END" THEN
        GOSUB 8900; GOSUB 8000; GOTO 100
      END
      SHPNO = "N"; GOTO 500
    CASE ECD.RET.VALUE = "" OR ECD.RET.VALUE = "???"
      BEGIN CASE
        CASE CUST.SHIP.TO < 1
          ERRMSG = "No ship to selected for this customer"
          GOSUB 91000; GOTO 100
        CASE CUST.SHIP.TO = 1
          SHPNO = CUST.SHIP.TO "R%3"
          ECD.RET.VALUE = SHPNO
        CASE 1
IF @LOGNAME = "thompson" THEN DEBUG
          GXR.NAME = "SHIP.TO"
          GXR.FILE = SHIP.TO
          GXR.SRCH.ID = CUSTNO:" : ":CUST.NAME
          GXR.IDLIST = CUSTNO:"!001"
          FOR SNO = 2 TO CUST.SHIP.TO
            GXR.IDLIST<1,SNO> = CUSTNO:"!":SNO "R%3"
          NEXT SNO
          CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
          ECD.ACTION = 2; CALL SCRN.EDIT
          SCV.REC(1)<ESN> = GXR.ID
          IF GXR.ID = "" THEN GOTO 100
          SHPNO = FIELD(GXR.ID,"!",2)
      END CASE
    CASE 1
      SHPNO = ECD.RET.VALUE "R%3"
  END CASE
  * T26126 v
  MATREADU SPT.REC FROM SHIP.TO, CONO:CUSTNO:"!":SHPNO LOCKED
    ERRMSG = 'SHIP TO record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 100 
  END ELSE
  * T26126 ^
    SCV.REC(ECD.NUM)<ESN> = ""
    IF ECD.RET.VALUE = "" THEN GOSUB 8050
    RELEASE SHIP.TO, CONO:CUSTNO:"!":SHPNO
    GOTO 100
  END
  GOSUB 8100; GOSUB 8050
  LINES = DCOUNT(SPT.TAX.JUR,VM)
  OLD.START.LINE = 0; GOSUB 20000
*
*---- Prompt line
500*
  MORE = 1
  LOOP
    ECD.NUM = 51
    IF MODE = 'I' THEN ECD.NUM = 55 ;*T26069
    SCV.REC(ECD.NUM)<ESN> = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "END" OR OPTION = "E"
        RELEASE SHIP.TO, CONO:CUSTNO:"!":SHPNO
        MORE = 0
      CASE OPTION = "F"
        IF SHPNO = "N" THEN
          CUST.SHIP.TO = CUST.SHIP.TO + 1
          SHPNO = CUST.SHIP.TO "R%3"
          ERRMSG = "NOTE NEW SHIP TO NUMBER : ":SHPNO
          GOSUB 91000
        END
        MATWRITE SPT.REC ON SHIP.TO, CONO:CUSTNO:"!":SHPNO
        MATWRITEU CUST.REC ON CUSTOMER, CONO:CUSTNO
        MORE = 0
      CASE OPTION = "T"
        LINES = DCOUNT(SPT.TAX.JUR,VM)
        LN = 1; OLD.START.LINE = 0
        GOSUB 20000
        GOSUB 20500
        LN = 1;OLD.START.LINE = 0;LINES = 0
      CASE NOT(NUM(OPTION))
        ERRMSG = "Invalid entry, please re-enter"
        GOSUB 91000
      CASE OPTION < 1 OR OPTION > 13
        ERRMSG = "Out of range, please re-enter"
        GOSUB 91000
      CASE 1
        ON OPTION GOSUB 1100,1200,1300,1400,1500,1600,1700,1800,1900,2000,2100,2200,2300
    END CASE
  WHILE MORE DO REPEAT
  GOSUB 8900; GOSUB 8000; GOTO 100
*
*---- Name
1100*
  ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SPT.NAME = ECD.RET.VALUE
  END
  RETURN
*
*---- 1st address
1200*
  ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SPT.ADDR1 = ECD.RET.VALUE
  END
  RETURN
*
*---- 2nd address
1300*
  ECD.NUM = 7; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SPT.ADDR2 = ECD.RET.VALUE
  END
  RETURN
*
*---- City & State
1400*
  ECD.NUM = 11; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    TEMP = ECD.RET.VALUE
    ECD.NUM = 12; ECD.ACTION = 4; CALL SCRN.EDIT
    IF ECD.RET.VALUE = "END" THEN GOTO 1400
    SPT.CITY = TEMP
    SPT.STATE = ECD.RET.VALUE
  END
  RETURN
*
*---- Zip
1500*
  ECD.NUM = 15; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1509
  SPT.ZIP = ECD.RET.VALUE
  IF OPCO.CNTY = "" THEN GOTO 1509
1505*
  ECD.NUM = 9; SCV.REC(ECD.NUM)<ESN> = SPT.CNTY
  ECD.ACTION = 5; CALL SCRN.EDIT
  IF SPT.CNTY = "" THEN
    IF CUST.ADDL.OPS<1,4> = "" THEN
      ECD.DEFAULT = OPCO.CNTY
    END ELSE
      ECD.DEFAULT = CUST.ADDL.OPS<1,4>
    END
  END
  ECD.MAXL = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE="END"
      GOTO 1509
    CASE ECD.RET.VALUE = "" OR ECD.RET.VALUE = OPCO.CNTY
      ECD.RET.VALUE = ""; COUNTRY = ""
    CASE 1
      IF ECD.RET.VALUE = "???" THEN
        GXR.NAME = "GEN.CODE1"
        GXR.SORT.FILE = "COUNTRY.CODE"
        GXR.FILE = COUNTRY.CODE
        CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
        ECD.ACTION = 2;CALL SCRN.EDIT
        SCV.REC(ECD.NUM)<ESN> = GXR.ID
        GOSUB 8050
        IF GXR.ID = "" THEN GOTO 1505
        ECD.RET.VALUE = GXR.ID
      END
      IF ECD.RET.VALUE = OPCO.CNTY THEN
        ECD.RET.VALUE = ""; COUNTRY = ""
      END ELSE
        *T25159 v
        * READ COUNTRY FROM COUNTRY.CODE,CONO:ECD.RET.VALUE ELSE
        MATREAD CTY.REC FROM COUNTRY.CODE,CONO:ECD.RET.VALUE ELSE
        *T25159 ^
          ERRMSG="Cannot locate country code # ":ECD.RET.VALUE
          GOSUB 91000; GOTO 1505
        END
        COUNTRY = CTY.DESC         ;*T25159
      END
  END CASE
  SPT.CNTY=ECD.RET.VALUE
  ECD.NUM = 9; SCV.REC(ECD.NUM)<ESN>=COUNTRY
  ECD.ACTION=5;CALL SCRN.EDIT
1509*
  RETURN
*
*---- Attention
1600*
  ECD.NUM = 19; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SPT.ATTN = ECD.RET.VALUE
  END
  RETURN
*
*---- Salesman
1700*
  IF SPT.SLSMN = "" AND CUST.SALESMAN # "" THEN
    ECD.DEFAULT = CUST.SALESMAN
  END
  ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 1709
    CASE ECD.RET.VALUE = "???"
      GXR.NAME = "GEN.CODE1"
      GXR.SORT.FILE = "SALESMAN"
      GXR.FILE = SALESMAN
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION = 2; CALL SCRN.EDIT
      SCV.REC(ECD.NUM)<ESN> = GXR.ID
      GOSUB 8050; OLD.START.LINE = 0; GOSUB 20000
      IF GXR.ID = "" THEN GOTO 1700
      ECD.RET.VALUE = GXR.ID
  END CASE
  MATREAD SALESMAN.REC FROM SALESMAN, CONO:ECD.RET.VALUE ELSE
    ERRMSG = "Cannot locate salesman # ":ECD.RET.VALUE
    GOSUB 91000; GOTO 1700
  END
  SPT.SLSMN = ECD.RET.VALUE
  ECD.NUM = 22; SCV.REC(ECD.NUM)<ESN> = SALS.NAME
  ECD.ACTION = 5; CALL SCRN.EDIT
1709*
  RETURN
*
*---- Ship Via
1800*
  IF SPT.SHIP.VIA = "" AND CUST.SHIP.VIA # "" THEN
    ECD.DEFAULT = CUST.SHIP.VIA
  END
  ECD.NUM = 23; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 1809
    CASE ECD.RET.VALUE = ""
      SPT.SHIP.VIA = ""
      GOTO 1809
    CASE ECD.RET.VALUE = "??"
      GXR.NAME = "GEN.CODE1"
      GXR.SORT.FILE = "SHIP.VIA"
      GXR.FILE = SHIP.VIA
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION = 2;CALL SCRN.EDIT
      SCV.REC(ECD.NUM)<ESN> = GXR.ID
      GOSUB 8050; OLD.START.LINE = 0; GOSUB 20000
      IF GXR.ID = "" THEN GOTO 1800
      ECD.RET.VALUE = GXR.ID
  END CASE
  MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:ECD.RET.VALUE ELSE
    ERRMSG = "Cannot locate Ship Via # ":ECD.RET.VALUE
    GOSUB 91000; GOTO 1800
  END
  SPT.SHIP.VIA = ECD.RET.VALUE
  ECD.NUM = 27; SCV.REC(ECD.NUM)<ESN> = SHPV.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
1809*
  RETURN
*
*---- Ship Terms
1900*
  ECD.NUM = 30
  IF SPT.SHP.TERMS = "" AND CUST.ADDL.OPS<1,3> # "" THEN
    ECD.DEFAULT = CUST.ADDL.OPS<1,3>
  END
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SPT.SHP.TERMS = ECD.RET.VALUE
  END
  RETURN
*
*---- Freight On Board
2000*
  IF SPT.FOB = "" AND CUST.ADDL.OPS<1,2> # "" THEN
    ECD.DEFAULT = CUST.ADDL.OPS<1,2>
  END
  ECD.NUM = 29; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END" 
      SCV.REC(ECD.NUM)<ESN> = SPT.FOB
      ECD.ACTION = 5; CALL SCRN.EDIT
      GOTO 2009
    CASE ECD.RET.VALUE = "???"
      GXR.NAME = "GEN.CODE1"
      GXR.SORT.FILE = "FOB"
      GXR.FILE = FOB
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION = 2; CALL SCRN.EDIT
      SCV.REC(ECD.NUM)<ESN> = GXR.ID
      GOSUB 8050; OLD.START.LINE = 0; GOSUB 20000
      IF GXR.ID = "" THEN GOTO 2000
      ECD.RET.VALUE = GXR.ID
  END CASE
  IF ECD.RET.VALUE = "" THEN
    MAT FOB.REC = ""
  END ELSE
    MATREAD FOB.REC FROM FOB, CONO:ECD.RET.VALUE ELSE
      ERRMSG = "Cannot locate F.O.B # ":ECD.RET.VALUE
      GOSUB 91000; GOTO 2000
    END
  END
  SPT.FOB = ECD.RET.VALUE
  ECD.NUM = 28; SCV.REC(ECD.NUM)<ESN> = FOB.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
2009*
  RETURN
*
*---- Phone Number
2100*
  ECD.NUM = 17; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN 
    SPT.PHONE = ECD.RET.VALUE
    IF LEN(SPT.PHONE) = 10 THEN
      SPT.PHONE = SPT.PHONE[1,3]:"-":SPT.PHONE[4,3]:"-":SPT.PHONE[7,4]
      SCV.REC(17)<ESN> = SPT.PHONE
      ECD.ACTION = 5; CALL SCRN.EDIT
    END
  END
  RETURN
*
*---- Fax Number
2200*
  ECD.NUM = 18; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SPT.FAX = ECD.RET.VALUE
    IF LEN(SPT.FAX) = 10 THEN
      SCV.REC(18)<ESN> = SPT.FAX[1,3]:"-":SPT.FAX[4,3]:"-":SPT.FAX[7,4]
      ECD.ACTION = 5; CALL SCRN.EDIT
    END
  END
  RETURN
*
*---- Status & Date
2300*
  ECD.NUM = 25; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    TEMP = ECD.RET.VALUE
    ECD.NUM = 26; ECD.ACTION = 4; CALL SCRN.EDIT
    IF ECD.RET.VALUE = "END" THEN GOTO 1800
    SPT.STATUS = TEMP
    SPT.STAT.DATE = ECD.RET.VALUE
  END
  RETURN
*
*---- ENTER TAX SCREEN
*
5000*
  GOSUB 20000
  SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#2" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
5001*
  FILL="#";X=4;Y=SLN;TYP=1;MAXL=5;O.R="O";MAXV='';MINV=''
  DEFAULT=SPT.TAX.JUR<1,LN>;HMSG="ENTER TAX JURISDICTION CODE"
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE=''
      IF REQUEST="C" THEN GOTO 5001
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 5009
    CASE VALUE="END"
      IF SPT.TAXABLE<1,LN>='' THEN
        SPT.TAX.JUR = DELETE(SPT.TAX.JUR,1,LN,0)
        SPT.TAXABLE = DELETE(SPT.TAXABLE,1,LN,0)
        SPT.EXEMPT = DELETE(SPT.EXEMPT,1,LN,0)
        SPT.EXMT.DATE = DELETE(SPT.EXMT.DATE,1,LN,0)
        P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END ELSE
        P_X  = 4 ; P_Y = SLN ; P_VALUE = SPT.TAX.JUR<1,LN>"L#5" ; P_OPT = ""
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        MATREAD TAX.REC FROM TAX,CONO:SPT.TAX.JUR<1,LN> ELSE TAX.JUR=UNKNOWN
        P_X  = 10 ; P_Y = SLN ; P_VALUE = TAX.JUR "L#30" ; P_OPT = ""
        P_X  := AM:45 ; P_Y := AM:SLN ; P_VALUE := AM:SPT.TAXABLE<1,LN>"L#1"
        P_X  := AM:51 ; P_Y := AM:SLN ; P_VALUE := AM:SPT.EXEMPT<1,LN>"L#15"
        P_X  := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(SPT.EXMT.DATE<1,LN>,"D2/")"L#8"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      GOTO 5009
    CASE VALUE = "???"
      GXR.NAME = "GEN.CODE1"
      GXR.SORT.FILE = "TAX"
      GXR.FILE = TAX
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION = 2; CALL SCRN.EDIT
      GOSUB 8050; OLD.START.LINE = 0
      IF GXR.ID = "" THEN GOTO 5000
      GOSUB 20000; VALUE = GXR.ID
      SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#2" ; P_OPT = ""
      P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:VALUE"L#5"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END CASE
  IF VALUE # "" THEN
    MATREAD TAX.REC FROM TAX, CONO:VALUE ELSE
      ERRMSG="TAX JURISDICTION CODE IS INVALID"
      GOSUB 91000;GOTO 5001
    END
    SPT.TAX.JUR<1,LN>=VALUE
    P_X  = 10 ; P_Y = SLN ; P_VALUE = TAX.JUR "L#30" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END
5002*
  X=45;Y=SLN;TYP=8;MAXL=1;O.R="O";MAXV='';MINV=''
  DEFAULT=SPT.TAXABLE<1,LN>;HMSG="ENTER TAXABLE (Y/N)"
  CALL EDIT.SUB
  IF VALUE="END" THEN GOTO 5001
  IF VALUE='' THEN GOTO 5002
  SPT.TAXABLE<1,LN>=VALUE
5003*
  IF SPT.TAXABLE<1,LN>="Y" THEN
    SPT.EXEMPT<1,LN>=''
    SPT.EXMT.DATE<1,LN>=""
    P_X  = 51 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 5009
  END
  X=51;Y=SLN;TYP=1;MAXL=15;MAXV='';MINV='';DEFAULT=SPT.EXEMPT<1,LN>;O.R="O"
  HMSG="ENTER TAX EXEMPT NUMBER"
  CALL EDIT.SUB
  IF VALUE="END" THEN GOTO 5001
  IF VALUE="" THEN GOTO 5003
  SPT.EXEMPT<1,LN>=VALUE
  X=68;Y=SLN;TYP=6;MAXL=8;DEFAULT=OCONV(SPT.EXMT.DATE<1,LN>,"D2/");O.R="O"
  HMSG="ENTER TAX EXEMPT EXPIRATION DATE"
  CALL EDIT.SUB
  IF VALUE="END" THEN GOTO 5001
  SPT.EXMT.DATE<1,LN>=VALUE
5009*
  LINES=DCOUNT(SPT.TAX.JUR,VM)
  RETURN
*
*---- SCROLL ROUTINE
*
20000*
  START.LINE=1 + INT((LN-1) / PAGE.SIZE) * PAGE.SIZE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  IF START.LINE=OLD.START.LINE THEN GOTO 20200
  OLD.START.LINE=START.LINE
  ECD.NUM = 41
  SCV.REC(ECD.NUM)<ESN> = INT(LAST.LINE/PAGE.SIZE+.9) "R%2"
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.NUM = 42
  SCV.REC(ECD.NUM)<ESN> = INT(LINES/PAGE.SIZE+.9) "R%2"
  ECD.ACTION = 5; CALL SCRN.EDIT
  CNT=1
  FOR L=START.LINE TO LAST.LINE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(L-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = L "R#2" ; P_OPT = ""
    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:SPT.TAX.JUR<1,L>"L#5"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    MATREAD TAX.REC FROM TAX,CONO:SPT.TAX.JUR<1,L> ELSE TAX.JUR=UNKNOWN
    P_X  = 10 ; P_Y = SLN ; P_VALUE = TAX.JUR "L#30" ; P_OPT = ""
    P_X  := AM:45 ; P_Y := AM:SLN ; P_VALUE := AM:SPT.TAXABLE<1,L>"L#1"
    P_X  := AM:51 ; P_Y := AM:SLN ; P_VALUE := AM:SPT.EXEMPT<1,L>"L#15"
    P_X  := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(SPT.EXMT.DATE<1,L>,"D2/")"L#8"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT=CNT + 1
  NEXT L
  FOR M=CNT TO PAGE.SIZE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
20200*
  RETURN
*
*---- OPTIONS FOR TAX SCREEN
*
20500*
  AGAIN=0
  LOOP
    SCV.REC(52)<ECD.SCRN.NO,1>=''
    ECD.NUM=52
    IF MODE = 'I' THEN ECD.NUM = 56 ;*T26069
    ECD.ACTION=4;CALL SCRN.EDIT
    REQUEST=ECD.RET.VALUE
    BEGIN CASE
      CASE REQUEST=''
        AGAIN=1
      CASE REQUEST='END'
        AGAIN=1
      CASE REQUEST="A"
        LINES=DCOUNT(SPT.TAX.JUR,VM)
        LOOP
          LN=LINES + 1
          OLD.LINES=LINES
          GOSUB 5000
        WHILE LINES > OLD.LINES DO REPEAT
        LN=LINES
        OLD.START.LINE = 0
        GOSUB 20000
      CASE REQUEST="S"
        LN=LN + PAGE.SIZE
        IF LN > LINES THEN LN=1
        GOSUB 20000
      * T25978 v
      CASE REQUEST = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 20000
      CASE REQUEST = 'ST'
        LN = 1
        GOSUB 20000
      CASE REQUEST = 'SB'
        LN = LINES
        GOSUB 20000
      * T25978 ^
      CASE REQUEST="C"
        GOSUB 3000
        IF LNO THEN
          LN=LNO
          SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          GOSUB 5000
        END
      CASE REQUEST="D"
        GOSUB 3000
        IF LNO THEN
          LN=LNO
          SPT.TAX.JUR=DELETE(SPT.TAX.JUR,1,LN,0)
          SPT.TAXABLE=DELETE(SPT.TAXABLE,1,LN,0)
          SPT.EXEMPT=DELETE(SPT.EXEMPT,1,LN,0)
          SPT.EXMT.DATE=DELETE(SPT.EXMT.DATE,1,LN,0)
          LINES=DCOUNT(SPT.TAX.JUR,VM) 
          IF LN > 1 AND LN > LINES THEN LN=LN - 1
          IF LN < 1 THEN LN=1
          OLD.START.LINE=0
          GOSUB 20000
        END
      CASE 1
        ERRMSG="INVALID OPTION";GOSUB 91000;GOTO 20500
    END CASE
20510*
  WHILE AGAIN=0 DO REPEAT
  RETURN
*
*
*---- GET LINE NUMBER
*
3000*
  ECD.NUM=53
  ECD.MINV = OLD.START.LINE
  ECD.MAXV = LAST.LINE
  SCV.REC(53)<ECD.SCRN.NO,1>=''
  ECD.ACTION=4;CALL SCRN.EDIT
  P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LNO=ECD.RET.VALUE
  IF LNO='' OR LNO="END" THEN LNO=0
  RETURN
*
*---- Display data
8000*
  ECD.ACTION = 6; CALL SCRN.EDIT
8050*
  ECD.ACTION = 3; CALL SCRN.EDIT
  RETURN
*
*---- Load ship to
8100*
  SCV.REC(1)<ESN> = SHPNO
  SCV.REC(3)<ESN> = SPT.NAME
  SCV.REC(5)<ESN> = SPT.ADDR1
  SCV.REC(7)<ESN> = SPT.ADDR2
  IF SPT.CNTY = "" THEN
    COUNTRY = ""
  END ELSE
    *T25159 v
    * READ COUNTRY FROM COUNTRY.CODE,CONO:SPT.CNTY ELSE
    MATREAD CTY.REC FROM COUNTRY.CODE,CONO:SPT.CNTY THEN
      COUNTRY = CTY.DESC
    END ELSE
    *T25159 ^
      COUNTRY = SPT.CNTY
    END
  END
  SCV.REC(9)<ESN>=COUNTRY
  SCV.REC(11)<ESN> = SPT.CITY
  SCV.REC(12)<ESN> = SPT.STATE
  SCV.REC(15)<ESN> = SPT.ZIP
  SCV.REC(17)<ESN> = SPT.PHONE
  SCV.REC(18)<ESN> = SPT.FAX
  SCV.REC(19)<ESN> = SPT.ATTN
  SCV.REC(21)<ESN> = SPT.SLSMN
  MATREAD SALESMAN.REC FROM SALESMAN, CONO:SPT.SLSMN ELSE
    MAT SALESMAN.REC = ""; SALS.NAME = "Unknown"
  END
  SCV.REC(22)<ESN> = SALS.NAME
  SCV.REC(25)<ESN> = SPT.STATUS
  SCV.REC(26)<ESN> = SPT.STAT.DATE
  MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:SPT.SHIP.VIA ELSE
    MAT SHIP.VIA.REC = ""
  END
  SCV.REC(27)<ESN> = SHPV.DESC
  MATREAD FOB.REC FROM FOB,CONO:SPT.FOB ELSE
    MAT FOB.REC = ""
  END
  SCV.REC(28)<ESN> = FOB.DESC
  SCV.REC(29)<ESN> = SPT.FOB
  SCV.REC(30)<ESN> = SPT.SHP.TERMS
  SCV.REC(23)<ESN> = SPT.SHIP.VIA
  RETURN
*
*---- Clear SCV.REC
8900*
  SCV.REC(1)<ESN> = "";SCV.REC(3)<ESN> = "";SCV.REC(5)<ESN> = ""
  SCV.REC(7)<ESN> = "";SCV.REC(9)<ESN> = "";SCV.REC(11)<ESN> = ""
  SCV.REC(12)<ESN> = "";SCV.REC(14)<ESN> = "";SCV.REC(15)<ESN> = ""
  FOR I = 17 TO 75
    SCV.REC(I)<ESN> = ""
  NEXT I
  RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT XX :
*       PRINT @(0,23) : CL :
*       RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000 PRINT @(0,23) : ERRMSG : CL :
*       INPUT XX :
*       PRINT @(0,23) : CL :
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
