      SUBROUTINE DESC.REFORMAT.2(OLD.DESC,NEW.LEN,NEW.DESC)
**********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - PMCBP
* PROGRAM       - DESC.REFORMAT.NEW
* BY            - L ROSS
* DATE          - 03/28/97
* DESCRIPTION   - This pgm will take a multi-valued string of chars and 
*               - reformat to a new multi-valued string with each lemgth =
*               - NEW.LEN and with no breaks in words.
**********************************************************************
*ENDDOC
**COPY>CPYLIB>CHAR
      EQU AM  TO CHAR(254)
      EQU VM  TO CHAR(253)
      EQU SVM TO CHAR(252)
      EQU BEL TO CHAR(007)
      CS = CHAR(12)
      CL = CHAR(27):CHAR(75)
      DESC = ""
      NEW.DESC=''
      X = 0
      IF NEW.LEN = "" OR NEW.LEN < 15 THEN NEW.LEN = 15
      IF NEW.LEN > 120 THEN NEW.LEN = 120
      DESC.CNT = COUNT(OLD.DESC,VM) + (OLD.DESC # "")
      FOR I = 1 TO DESC.CNT
        FMT = 'L#':LEN(OLD.DESC<1,I>)
        DESC = OLD.DESC<1,I> FMT
        LOOP
        WHILE DESC # "" DO
           X = X+1
           FND = 0
           IF (DESC[NEW.LEN,1] # " " AND DESC[NEW.LEN,1] # "") AND (DESC[NEW.LEN+1,1] # " " AND DESC[NEW.LEN+1,1] # "") THEN
             FOR II = NEW.LEN TO 1 STEP - 1 UNTIL FND
               IF DESC[II-1,1] = " " THEN
                 NEW.DESC<1,X> = DESC[1,II-1]
                 FND = 1
                 DESC = TRIM(DESC[II,9999])
               END ELSE
                 IF II = 1 THEN
                   NEW.DESC<1,X> = DESC[1,NEW.LEN]
                   FND = 1
                   DESC = TRIM(DESC[NEW.LEN+1,9999])
                 END
               END
             NEXT II
           END ELSE
             NEW.DESC<1,X> = DESC[1,NEW.LEN]
             DESC = TRIM(DESC[NEW.LEN+1,9999])
           END
        REPEAT
      NEXT I
RETURN
