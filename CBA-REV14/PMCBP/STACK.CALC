      SUBROUTINE STACK.CALC(STACK,ERRMSG)
      EQU AM  TO CHAR(254)
      OPERS = "^":AM:"/":AM:"*":AM:"-":AM:"+":AM:"PWR":AM:"REM":AM:"RND":AM:"RNDU":AM:"RNDD"
      FUNCS = "INT":AM:"ABS":AM:"SQRT":AM:"SUM"
      SPTR = 1
      LOOP WHILE STACK<2> # "" AND STACK<SPTR> # "" DO
*        IF NUM(STACK<SPTR>) THEN
         IF NUM(STACK<SPTR>) AND STACK<SPTR> # "-" AND STACK<SPTR> # "+" THEN
            SPTR = SPTR + 1
         END ELSE
            LOCATE STACK<SPTR> IN OPERS SETTING OP THEN
               IF SPTR < 3 THEN
                  ERRMSG = "Not enough expressions for function (":STACK<SPTR>:"), zero used."
                  STACK = 0
               END ELSE
                  P1 = SPTR - 1
                  P2 = SPTR - 2
                  ON OP GOSUB 1100,1200,1300,1400,1500,2100,2200,2300,2300,2300
                  STACK = DELETE(STACK,SPTR,0,0)
                  STACK = DELETE(STACK,P2,0,0)
                  SPTR = SPTR - 1
               END
            END ELSE
               LOCATE STACK<SPTR> IN FUNCS SETTING OP THEN
                  IF SPTR < 2 THEN
                     ERRMSG = "Not enough expressions for function (":STACK<SPTR>:"), zero used."
                     STACK = 0
                  END ELSE
                     P1 = SPTR - 1
                     ON OP GOSUB 5100,5200,5300,5400
                     STACK = DELETE(STACK,SPTR,0,0)
                  END
               END ELSE
                  ERRMSG = "Invalid argumnet (":STACK<SPTR>:") in stack, zero used."
                  STACK = 0
               END
            END
         END
      REPEAT
      IF NOT(NUM(STACK)) THEN
         ERRMSG = "Invalid stack, zero used."
         STACK = 0
      END
      GOTO 99999
1100  STACK<P1> = STACK<P1> ^ STACK<P2>
      RETURN
1200  IF STACK<P2> = 0 THEN
         STACK<P1> = 0
      END ELSE
         STACK<P1> = STACK<P1> / STACK<P2>
      END
      RETURN
1300  STACK<P1> = STACK<P1> * STACK<P2>
      RETURN
1400  STACK<P1> = STACK<P1> - STACK<P2>
      RETURN
1500  STACK<P1> = STACK<P1> + STACK<P2>
      RETURN
2100  STACK<P1> = PWR(STACK<P1>,STACK<P2>)
      RETURN
2200  STACK<P1> = MOD(STACK<P1>,STACK<P2>)
      RETURN
2300  BEGIN CASE
      CASE STACK<P2> > 0 AND STACK<P2> < 5
         IF STACK<P1> < 0 THEN
            RND = 0 - 1
         END ELSE
            RND = 1
         END
         OP = OP - 7
         MLTP = PWR(10,STACK<P2>-1)
         ON OP GOSUB 2311,2312,2313
      CASE STACK<P2> > -7 AND STACK<P2> < 0
         IF STACK<P1> < 0 THEN
            RND = 0 - 1
         END ELSE
            RND = 1
         END
         OP = OP - 7
         STACK<P2> = 0 - STACK<P2>
         ON OP GOSUB 2321,2322,2323
      END CASE
      RETURN
2311  STACK<P1> = INT((STACK<P1> + RND*(5/PWR(10,STACK<P2>))) * MLTP) / MLTP
      RETURN
2312  STACK<P1> = INT((STACK<P1> + RND*(9/PWR(10,STACK<P2>))) * MLTP) / MLTP
      RETURN
2313  STACK<P1> = INT(STACK<P1> * MLTP) / MLTP
      RETURN
2321  STACK<P1> = INT(STACK<P1> / PWR(10,STACK<P2>) + RND*.5)
      RETURN
2322  STACK<P1> = INT(STACK<P1> / PWR(10,STACK<P2>) + RND*.9)
      RETURN
2323  STACK<P1> = INT(STACK<P1> / PWR(10,STACK<P2>))
      RETURN
5100  STACK<P1> = INT(STACK<P1>)
      RETURN
5200  STACK<P1> = ABS(STACK<P1>)
      RETURN
5300  STACK<P1> = SQRT(STACK<P1>)
      RETURN
5400  STACK<P1> = SUM(STACK<P1>)
      RETURN
99999 RETURN
   END
