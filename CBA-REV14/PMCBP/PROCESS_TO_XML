   PROGRAM PROCESS_TO_XML
*COPY>CPYLIB>COM1
**********************************************
* REVISION    - [12.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - PROCESS_TO_XML
* BY          - ABDULLAH JIBALY; CBA
* DATE        - 04/10/2002
* DESCRIPTION - THIS PROGRAM MAINTAINS CONVERT PMC_PROCESS RECORDS
*               TO XML RECORDS IN THE PMC_PROCESS_XML FILE
*
*ENDDOC
**********************************************
*
*---CPYLIBS
*
*COPY>PMC.CPYLIB>PMC_PROCESS
*COPY>PMC.CPYLIB>PMC_REPORTS
*COPY>PMC.CPYLIB>REPORT.PROC.FILE
*COPY>CPYLIB>SYSCOM
*
*---ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---OPEN FILES
*
   OPEN '', 'PMC_PROCESS_XML' TO PMC_PROCESS_XML ELSE
      ERRMSG = 'PMC_PROCESS_XML FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'PMC_PROCESS' TO PMC_PROCESS ELSE
      ERRMSG = 'PMC_PROCESS FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'PMC_REPORTS' TO PMC_REPORTS ELSE
      ERRMSG = 'PMC_REPORTS FILE IS MISSING'
      GOSUB 91000; RETURN
   END
   OPEN '', 'REPORT.PROC.FILE' TO REPORT.PROC.FILE ELSE
      ERRMSG = 'REPORT.PROC.FILE FILE IS MISSING'
      GOSUB 91000; RETURN
   END
*
*---INITIALIZATION SECTION
*
   DEFFUN XML_FORMAT_CDATA(STR)
   PROCREAD BUFFER ELSE
      ERRMSG = 'CANNOT PERFORM PROCREAD'
      GOSUB 91000; RETURN
   END
   CONO    = BUFFER<1>
   RPT.NUM = BUFFER<2>
   GOSUB MAIN
   RETURN
*
MAIN: 
   LINEFEED = CHAR(10)
   NULL.ERR.CNT = 0
   NULL.ERR.REC = ''
   LOOP
      READNEXT PPS.ID ELSE EXIT
      MATREAD PPS.REC FROM PMC_PROCESS, PPS.ID ELSE
         NULL.ERR.CNT = NULL.ERR.CNT + 1
         NULL.ERR.MSG = NULL.ERR.CNT:"- ERROR READING PMC_PROCESS, "
         NULL.ERR.MSG:= QUOTE(PPS.ID)
         NULL.ERR.REC<-1> = NULL.ERR.MSG
         CONTINUE
      END
      PRPT.ID = PPS.REPORTS<1,1>[1,5]
      MATREAD PRPT.REC FROM PMC_REPORTS, PRPT.ID ELSE
         NULL.ERR.CNT = NULL.ERR.CNT + 1
         NULL.ERR.MSG = NULL.ERR.CNT:"- ERROR READING PMC_REPORTS, "
         NULL.ERR.MSG:= QUOTE(PRPT.ID):" REFERENCED BY PMC_PROCESS, "
         NULL.ERR.MSG:= QUOTE(PPS.ID)
         NULL.ERR.REC<-1> = NULL.ERR.MSG
         CONTINUE
      END
      NUM.FIELDS = DCOUNT(PRPT.PROMPTS,@VM)
      ;*
      ;* Remove runtime-include headings of this format ^PROMPT#^
      ;*
      HEADING = ''
      FOR HEADING.IDX = 1 TO DCOUNT(PRPT.HEADING,"^") STEP 2
        HEADING:= FIELD(PRPT.HEADING,"^",HEADING.IDX)
      NEXT HEADING.IDX
      HEADING = OCONV(HEADING,"MCT") ;* CONVERT TO INITIAL CAPS
*
      XML.REC = ""
      XML.REC<-1> = '<?xml version="1.0"?>'
      XML.REC<-1> = '<Page>'
      XML.REC<-1> = '<NumCols>2</NumCols>'
      XML.REC<-1> = '<NumRows>':NUM.FIELDS:'</NumRows>'
      XML.REC<-1> = '<ScreenName>':XML_FORMAT_CDATA(HEADING):'</ScreenName>'
      XML.REC<-1> = '<ReportNumber>':PRPT.ID:'</ReportNumber>'
      XML.REC<-1> = '<Table LookUp="NO" Color="M" Report="NO">'
      XML.REC<-1> = 'PMC:ReportSelectionTable'
      FOR FIELD.IDX = 1 TO NUM.FIELDS
         RPR.ID = PRPT.PROMPTS<1,FIELD.IDX>
         GOSUB XML.ADD.FIELD
      NEXT FIELD.IDX
      XML.REC<-1> = '</Table>'
      XML.REC<-1> = '</Page>'
*
      SWAP @AM WITH LINEFEED IN XML.REC
      OSWRITE XML.REC ON "PMC_PROCESS_XML/":PPS.ID:".XML"
   REPEAT
   IF NULL.ERR.CNT > 0 THEN
      ERRMSG = "ENCOUNTERED ":NULL.ERR.CNT:" ERRORS DURING CONVERSION. "
      ERRMSG:= "SEE THE 'CONVERT.ERRORS' RECORD IN PMC_PROCESS_XML."
      SWAP @AM WITH LINEFEED IN NULL.ERR.REC
      OSWRITE NULL.ERR.REC ON "PMC_PROCESS_XML/CONVERT.ERRORS"
   END ELSE
      ERRMSG = "ALL RECORDS SUCCESSFULLY CONVERTED TO PMC_PROCESS_XML FILE."
      DELETE PMC_PROCESS_XML, "CONVERT.ERRORS"
   END
   GOSUB 91000
   RETURN
*
XML.ADD.FIELD: 
   MATREAD RPT.PROC.REC FROM REPORT.PROC.FILE, RPR.ID ELSE
      NULL.ERR.CNT = NULL.ERR.CNT + 1
      NULL.ERR.MSG = NULL.ERR.CNT:"- ERROR READING REPORT.PROC.FILE, "
      NULL.ERR.MSG:= QUOTE(RPR.ID):" REFERENCED BY PMC_PROCESS, "
      NULL.ERR.MSG:= QUOTE(PPS.ID)
      NULL.ERR.REC<-1> = NULL.ERR.MSG
      RETURN
   END
   FIELD.NAME = "PPDField":FIELD.IDX'R%2'
   IF RPR.O.R = "O" THEN FIELD.O.R = "O" ELSE FIELD.O.R = "R"
   XML.REC.ADD = ''
   BEGIN CASE
      CASE RPR.TYPE = 8
         FIELD.INPUT.TYPE = "Radio"
         XML.REC.ADD<-1> = "<RadioText>Yes#No`Y#N</RadioText>"
         XML.REC.ADD = @AM:XML.REC.ADD
      CASE RPR.VALID # ''
         FIELD.INPUT.TYPE = "DropDown"
         IF DCOUNT(RPR.VAL.HEAD,",") = DCOUNT(RPR.VALID,",") THEN
            DD.DESCS = RPR.VAL.HEAD
         END ELSE
            DD.DESCS = RPR.VALID
         END
         XML.REC.ADD<-1> = "<ComboType>H</ComboType>"
         XML.REC.ADD<-1> = "<DropDownCodes>":RPR.VALID:"</DropDownCodes>"
         XML.REC.ADD<-1> = "<DropDownDescs>":DD.DESCS:"</DropDownDescs>"
         XML.REC.ADD = @AM:XML.REC.ADD
      CASE 1
         FIELD.INPUT.TYPE = "Text"
   END CASE
   XML.REC<-1> = "<Field>"
   XML.CDATA = RPR.DESC
   XML.CDATA = CHANGE(XML.CDATA,"#","No.")
   XML.CDATA = CHANGE(XML.CDATA," or (END)","")
   XML.REC<-1> = "<Label>":XML_FORMAT_CDATA(XML.CDATA):"</Label>"
   XML.REC<-1> = "<InputType>":FIELD.INPUT.TYPE:"</InputType>"
   XML.REC<-1> = "<Name>":FIELD.NAME:"</Name>"
   XML.REC<-1> = "<TypeField>":RPR.TYPE:"</TypeField>"
   XML.REC<-1> = "<Length>":RPR.MAXL:"</Length>"
   XML.REC<-1> = "<Opt_Req>":FIELD.O.R:"</Opt_Req>"
   XML.REC<-1> = "<Help>":XML_FORMAT_CDATA(RPR.HMSG):"</Help>"
   XML.REC = XML.REC:XML.REC.ADD
   XML.REC<-1> = "</Field>"
   RETURN
*
*---STANDARD SUBROUTINES
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
99999 RETURN
END
