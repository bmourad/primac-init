  SUBROUTINE CUST.JOB.INQ(CONO)
***************************************************************************
* REVISION      - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - BP
* PROGRAM       - CUST.JOB.INQ
* BY            - JIHAD YAMOUT, C.B.A
* DATE          - 09/28/84
* DESCRIPTION   - THIS SUBROUTINE DISPLAYS ALL INVOICES AND JOBS FOR ONE
* CUSTOMER.
*
* LAST COMPILE - 355
* T20532 JR EXPAND INVOICE AMOUNT TO 8.2
*
*T25978 adelgado 01/28/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
***************************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.CUST
*COPY>CPYLIB>EDIT.COM
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*** INT
*
  BEGIN.PAGE = 6
  PAGE.SIZE = 14
  LINE.SPACE = 1
  LINES = 0
  OLD.START.LINE=0
*
**** SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  SCV.REC(2)<ECD.SCRN.NO> = CUST.NAME
  ECD.ACTION = 3; CALL SCRN.EDIT
  LINES = DCOUNT(CUST.JOB,VM)
  LN = 1
  IF LINES > 0 THEN
    GOSUB 10000
  END
  MORE = 1
  LOOP
    ECD.NUM = 3 ; SCV.REC(3)<ECD.SCRN.NO> = '' ; ECD.ACTION = 4 ; CALL SCRN.EDIT
    BEGIN CASE
      CASE ECD.RET.VALUE = '' OR ECD.RET.VALUE = 'END'
        MORE = 0
      CASE ECD.RET.VALUE = 'S'
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 10000
      * T25978 v
      CASE ECD.RET.VALUE = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 10000
      CASE ECD.RET.VALUE = 'SB'
        LN = LINES
        GOSUB 10000
      CASE ECD.RET.VALUE = 'ST'
        LN = 1
        GOSUB 10000
     * T25978 ^
    END CASE
  WHILE MORE DO REPEAT
  GOTO 99999
*
***** SCROLL ROUTINE
*
10000 *
  START.LINE=1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF START.LINE=OLD.START.LINE THEN GOTO 10001
  OLD.START.LINE=START.LINE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  CNT=1
  FOR N=START.LINE TO LAST.LINE UNTIL N > LINES
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = N "L#3" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    MATREAD JOB.REC FROM JOB, CONO:CUST.JOB<1,N> ELSE
      MAT JOB.REC = ""
      JOB.STATUS = 99
    END
    P_X  = 4 ; P_Y = SLN ; P_VALUE = CUST.JOB<1,N> "L#8" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*C26491 v
    CALL JOB.STATUS.SUB(JOB.STATUS,JOB.TRACK.DATE,STATUS)
*C26491 ^
    P_X  = 13 ; P_Y = SLN ; P_VALUE = STATUS "L#15" ; P_OPT = ""
    P_X  := AM:29 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(JOB.STAT.DATE<1,1>,"D2/") "L#8"
    P_X  := AM:38 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(JOB.CONF.AMT,"MD2Z,") "R#12"
    P_X  := AM:51 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(JOB.TOT.INV,"MD2Z,<") "R#13"
    P_X  := AM:65 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(CUST.JOB.BAL<1,N>,"MD2Z,") "R#12"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT=CNT + 1
  NEXT N
  FOR M=CNT TO PAGE.SIZE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
10001 RETURN
***** CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
