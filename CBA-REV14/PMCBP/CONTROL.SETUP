*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - CONTROL.SETUP
* BY          - WALID YAMOUT, C.B.A
* DATE        - 10/11/84
* DESCRIPTION -
*T21643 larryt 05/15/1997 * Allow access to existing company for viewing
*                          only.
*T23278 markt 09/18/1998  * Add user security at the division level
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*T26090 wyamout 03/27/2002 * CHANGE DIVISIONAL SECURITY PROMPTS
*ENDDOC
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SET UP ERROR ROUTINE
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN FILES
*
  MAT FILE.VARS = ""
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"; GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = "PMC.SCREENS FILE IS MISSING"; GOTO 93000
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 11
  ECD.SCRN.NAME<1> = "CONTROL.SETUP"
  ECD.SCRN.NAME<2> = "CONTROL.ARS.SUB"
  ECD.SCRN.NAME<3> = "CONTROL.APS.SUB"
  ECD.SCRN.NAME<4> = "CONTROL.PRS.SUB"
  ECD.SCRN.NAME<5> = "CONTROL.GLS.SUB"
  ECD.SCRN.NAME<6> = "CONTROL.ICS.SUB"
  ECD.SCRN.NAME<7> = "CONTROL.POS.SUB"
  ECD.SCRN.NAME<8> = "CONTROL.JES.SUB"
  ECD.SCRN.NAME<9> = "CONTROL.JCS.SUB"
  ECD.SCRN.NAME<10> = "CONTROL.PSS.SUB"
  ECD.SCRN.NAME<11> = "CONTROL.OPS.SUB"
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
100*
  EDIT.MODE = 1
  MAT SCV.REC = ""
  ECD.ACTION = 6; CALL SCRN.EDIT
  ECD.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 999999
    CASE NUM(ECD.RET.VALUE) AND ECD.RET.VALUE > 0
      MAT COMP.REC = ""
      CONO = ECD.RET.VALUE
      * T26126 v
      MATREADU COMP.REC FROM COMPANY, CONO LOCKED
        ERRMSG = 'COMPANY record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 100 
      END THEN
      * T26126 ^
*T21643 v
        EDIT.MODE = 0
*          ERRMSG = "COMPANY ALREADY ON FILE. YOU CANNOT SET UP CONTROL FILE"
*          GOSUB 91000
*          RELEASE COMPANY, CONO
*          GOTO 100
      END
*T21643 ^
*T23278 v
      READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE SECURITY.REC = "N"
      SCV.REC(5)<1> = SECURITY.REC<1>
      SCV.REC(6)<1> = SECURITY.REC<2>
      ECD.ACTION = 3; CALL SCRN.EDIT
*T23278 ^
    CASE 1
      ERRMSG = "INVALID COMPANY NUMBER"; GOSUB 91000; GOTO 100
  END CASE
*
*--- ENTER OPTION
*
500*
*T21643 v
  IF EDIT.MODE THEN
    ECD.NUM = 3
  END ELSE
    ECD.NUM = 4
  END
*     ECD.NUM = 2; SCV.REC(2)<1> = ""; ECD.ACTION = 4; CALL SCRN.EDIT
  SCV.REC(2)<1> = ""; ECD.ACTION = 4; CALL SCRN.EDIT
*T21643 ^
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "E" OR OPTION = "END"
      RELEASE COMPANY, CONO
      GOTO 100
    CASE OPTION = "P"
      CALL CONTROL.SETUP.LIST(CONO)
      ECD.ACTION = 2; CALL SCRN.EDIT
      ECD.ACTION = 3; CALL SCRN.EDIT
*T23278 v
*T26090 v
*   CASE OPTION = "D"
    CASE OPTION = "13"
*T26090 ^
      IF EDIT.MODE THEN 
        ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
        SECURITY.REC<1> = ECD.RET.VALUE
        IF ECD.RET.VALUE = "N" THEN
          SECURITY.REC<2> = "N"
          SCV.REC(6)<1> = "N"
          ECD.NUM = 6; ECD.ACTION = 5; CALL SCRN.EDIT
        END
        WRITE SECURITY.REC ON CONTROL, CONO:"DIV.SECURITY"
      END ELSE
        ERRMSG = "YOU MAY NOT CHANGE EXISTING COMPANY DIVISION SECURITY FLAG"
        GOSUB 91000
      END
*T26090 v
*   CASE OPTION = "C"
    CASE OPTION = "14"
*T26090 ^
      IF EDIT.MODE THEN
        IF SECURITY.REC<1> = "Y" THEN 
          ECD.NUM = 6; ECD.ACTION = 4; CALL SCRN.EDIT
          IF ECD.RET.VALUE # "END" THEN
            SECURITY.REC<2> = ECD.RET.VALUE
            WRITE SECURITY.REC ON CONTROL, CONO:"DIV.SECURITY"
          END
        END ELSE
          ERRMSG = "YOU CANNOT CLOSE BY DIVISION IF DIVISION SECURITY IS NOT ENABLED"
          GOSUB 91000
        END
      END ELSE
        ERRMSG = "YOU MAY NOT CHANGE EXISTING COMPANY DIVISION CLOSING FLAG"
        GOSUB 91000
      END
*T23278 ^
    CASE NUM(OPTION) AND OPTION > 0
*T21643 v
      IF EDIT.MODE THEN
        ON OPTION GOSUB 10100,10200,10300,1000,10500,1000,10700,10800,10900,11000,11100,1000,1000
        ECD.SCRN.NO = 1; ECD.ACTION = 3; CALL SCRN.EDIT
      END ELSE
        ERRMSG = "YOU MAY NOT EDIT AN EXISTING COMPANY CONTROL RECORD"
        GOSUB 91000
      END
*T21643 ^
  END CASE
  GOTO 500
*
*--- INVALID
*
1000*
  ERRMSG = "NO SETUP NEEDED FOR THIS MODULE"; GOSUB 91000
  RETURN
*
*--- ARS SETUP
*
10100*
  ECD.SCRN.NO = 2
  CALL CONTROL.ARS.SUB(CONO)
  RETURN
*
*--- APS SETUP
*
10200*
  ECD.SCRN.NO = 3
  CALL CONTROL.APS.SUB(CONO)
  RETURN
*
*--- PRS SETUP
*
10300*
  ECD.SCRN.NO = 4
  CALL CONTROL.PRS.SUB(CONO)
  RETURN
*
*--- GLS SETUP
*
10500*
  ECD.SCRN.NO = 5
  CALL CONTROL.GLS.SUB(CONO)
  RETURN
*
*--- OPS SETUP
*
10700*
  ECD.SCRN.NO = 11
  CALL CONTROL.OPS.SUB(CONO)
  RETURN
*
*--- ICS SETUP
*
10800*
  ECD.SCRN.NO = 6
  CALL CONTROL.ICS.SUB(CONO)
  RETURN
*
*--- POS SETUP
*
10900*
  ECD.SCRN.NO = 7
  CALL CONTROL.POS.SUB(CONO)
  RETURN
*
*--- JES SETUP
*
11000*
  ECD.SCRN.NO = 8
  CALL CONTROL.JES.SUB(CONO)
  RETURN
*
*--- JCS SETUP
*
11100*
  ECD.SCRN.NO = 9
  CALL CONTROL.JCS.SUB(CONO)
  RETURN
*
*--- PSS SETUP
*
11200*
*  ECD.SCRN.NO = 10
*  CALL CONTROL.PSS.SUB(CONO)
  RETURN
*
*---   ERROR ROUTINE
*
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999*
*       PRINT @(-1)
END
