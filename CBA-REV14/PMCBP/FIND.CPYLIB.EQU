  PROGRAM FIND.CPYLIB.EQU
********************************************************************
* REVISION - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PMCBP
* PROGRAM  - FIND.CPYLIB.EQU
* BY       - Abdullah Jibaly, COMPUTER BUSINESS ASSOCIATES
* DATE     - 12/19/02
*
* DESCRIPTION
*
* Find what equates a program uses from a certain cpylib
*
*********************************************************************
  ;*
  ;* Initialization
  ;*
  EQU.FORM = "L#20"
  ;*
  ;* Get the cpylib directory
  ;*
  PROMPT ""
  CRT "Enter the cpylib directory: " :
  INPUT CPYLIB.DIR
  IF TRIM(CPYLIB.DIR) = "" OR CPYLIB.DIR = "END" THEN RETURN
  OPEN CPYLIB.DIR TO CPYLIB.FILE ELSE
    CRT "That directory does not exist."
    RETURN
  END
  ;*
  ;* Get the cpylib record
  ;*
  CRT "Enter the cpylib record: " :
  INPUT CPYLIB.REC.NAME
  IF TRIM (CPYLIB.REC.NAME) = "" OR CPYLIB.REC.NAME = 'END' THEN RETURN
  READ CPYLIB.REC FROM CPYLIB.FILE, CPYLIB.REC.NAME ELSE
    CRT "That record does not exist."
    RETURN
  END
  ;*
  ;* Get the program directory
  ;*
  PROMPT ""
  CRT "Enter the program directory: " :
  INPUT PROG.DIR
  IF TRIM(PROG.DIR) = "" OR PROG.DIR = "END" THEN RETURN
  OPEN PROG.DIR TO PROG.FILE ELSE
    CRT "That directory does not exist."
    RETURN
  END
  ;*
  ;* Get the program record
  ;*
  CRT "Enter the program record: " :
  INPUT PROG.REC.NAME
  IF TRIM (PROG.REC.NAME) = "" OR PROG.REC.NAME = 'END' THEN RETURN
  READ PROG.REC FROM PROG.FILE, PROG.REC.NAME ELSE
    CRT "That record does not exist."
    RETURN
  END
  ;*
  ;* Find out whether to print or display
  ;*
  CRT "Print or display output ([P]/D): " :
  INPUT OUT.TYPE
  IF UPCASE(OUT.TYPE) # 'D' THEN
    OUT.TYPE = 'P'
    PRINTER ON
  END ELSE
    OUT.TYPE = 'D'
    PRINTER OFF
  END
  ;*
  ;* Parse the cpylib rec and extract all equates
  ;*
  PRINT "Begin extracting " : CPYLIB.REC.NAME : " from " : PROG.REC.NAME : "."
  FOR CPYLIB.IDX = 1 TO DCOUNT(CPYLIB.REC,@AM)
    CPYLIB.LINE = TRIM(CPYLIB.REC<CPYLIB.IDX>)
    IF CPYLIB.LINE[1,3] # "EQU" THEN CONTINUE
    CPYLIB.EQUATE = FIELD(CPYLIB.LINE," ",2)
    FINDSTR CPYLIB.EQUATE IN PROG.REC SETTING LOC THEN
      OUT.STR = CPYLIB.EQUATE EQU.FORM : "| @" : LOC "L#5"
      OUT.STR:= TRIM(PROG.REC<LOC>)
    END ELSE
      OUT.STR = CPYLIB.EQUATE EQU.FORM : "| Not found."
    END
    PRINT OUT.STR
  NEXT CPYLIB.IDX
  PRINT "Completed."
  RETURN
*
