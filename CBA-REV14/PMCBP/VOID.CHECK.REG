*********************************************************************
* REVISION    - [08.1]
* SYSTEM     - PRIMAC
* PROGRAM    - VOID.CHECK.REG
* BY         - NATALIE BLAKE, C.B.A
* DATE       - 02/01/89
* DESCRIPTION- This program will print a report of voided and unused
*              checks for APS and PRS.
* T20269 JR SELECT REPORT BY DIVISION
* T26493 cmykleb 04/02/2002 * Change pgm to change heading.
*********************************************************************
*
*--- INSERT FILES EQUATES
*
*COPY>CPYLIB>CHAR
*COPY>PRS.CPYLIB>PRSCHECK.STATS
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>COMPANY
*
*--- SETUP ERRMSG ROUTINE
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---OPEN FILES
*
   CLEAR
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOTO 93000
   END
   OPEN "","CHECK.STATS" TO CHECK.STATS ELSE
      ERRMSG = "CANNOT OPEN CHECK.STATS FILE"
      GOTO 93000
   END
   OPEN "","COA" TO COA ELSE
      ERRMSG = "CANNOT OPEN COA FILE"
      GOTO 93000
   END
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOTO 93000
   END
   PROCREAD BUFF ELSE
      ERRMSG = "PROGRAM MUST RUN FROM A PROC"
      GOTO 93000
   END
   CONO = BUFF<1>
   FLG = BUFF<6>
   DIV.OWNER = BUFF<5>; * TASK 20269
   IF FLG = "C" THEN
      CLR.FLG = 1
   END ELSE
      CLR.FLG = 0
   END
   MATREAD COMP.REC FROM COMPANY,CONO ELSE MAT COMP.REC = ""
   HD1 = ""; HD2 = ""
*T26493 v
*  REPORT.NAME = "VOIDED CHECK REGISTER"
   REPORT.NUMBER = "XXXX"
   CO.NAME = ""
   REPORT.NAME= ""
   REPORT.NUMBER = BUFF<2>
*T26493 ^
   REPORT.DATE = DATE()
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   PRINTER ON
   SUB.HEAD0 = "DIVISION : ":DIV.OWNER; * T20269
   SUB.HEAD1 = "                                           START    LAST    START   LAST  LAST USED  CHECK"
   SUB.HEAD2 = "    GL ACCT              ACCT DESC         UNUSED  UNUSED   VOID    VOID  CHECK NO. RUN DATE"
   UL1 =       "---------------  ------------------------- ------  ------  ------  ------ --------- --------"
* T20269
*     HEADING CHAR(12):HD1:"'PL'":HD2:"'LL'":SUB.HEAD1:"'L'":SUB.HEAD2:"'L'":UL1:"'L'"
   HEADING HD1:"'PL'":HD2:"'LL'":SUB.HEAD0:"'L'":SUB.HEAD1:"'L'":SUB.HEAD2:"'L'":UL1:"'L'"
* T20269 ^
   EOF=0
   LOOP
      READNEXT ID ELSE EOF=1
   WHILE EOF=0
      MATREAD PCS.REC FROM CHECK.STATS,ID ELSE GOTO 100
* T20269 v
*     MATREAD COA.REC FROM COA,ID ELSE COA.DESC = "??????"
*     PRINT ID[4,14] "L#15  ":
      C.KEY = FIELD(ID,"!",1)
      MATREAD COA.REC FROM COA,C.KEY ELSE COA.DESC = "??????"
      PRINT C.KEY[4,99] "L#15  ":
* T20269 ^
      PRINT COA.DESC "L#25 ":
      UCNT = DCOUNT(PCS.START.UNUSED,VM)
      FOR I = 1 TO UCNT
         IF I > 1 THEN PRINT SPACE(43):
         FOR PP = 2 TO 5
            IF PCS.REC(PP)<1,I> # "" THEN
               PCS.REC(PP)<1,I>=(STR("0",6-LEN(PCS.REC(PP)<1,I>)):PCS.REC(PP)<1,I>)
            END
         NEXT PP
         PRINT PCS.START.UNUSED<1,I> "R#6  ":
         PRINT PCS.END.UNUSED<1,I> "R#6  ":
         PRINT PCS.START.VOID<1,I> "R#6  ":
         PRINT PCS.END.VOID<1,I> "R#6 ":
         IF I=UCNT THEN
            PRINT "   ":(STR("0",6-LEN(PCS.LST.CHK.NO)):PCS.LST.CHK.NO) "R#6 ":
         END ELSE
            PRINT SPACE(10):
         END
         PRINT OCONV(PCS.VOID.DATE<1,I>,"D2/") "L#8"
      NEXT I
      IF UCNT=0 THEN
         PRINT SPACE(34):(STR("0",6-LEN(PCS.LST.CHK.NO)):PCS.LST.CHK.NO) "R#6"
      END
      PRINT
      IF CLR.FLG THEN
         FOR CLR = 2 TO 8
            PCS.REC(CLR) = ""
         NEXT CLR
         MATWRITE PCS.REC ON CHECK.STATS,ID
      END
100 *
   REPEAT
500*
   GOTO 99999
*---CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER CLOSE
   PRINTER OFF
END
