  SUBROUTINE CONVERT.TO.RTF(CO.NAME,OPEN.FILE,ITEM.NAME,TITLE,FORMATSTR)
*COPY>CPYLIB>COM1
*********************************************************************
* REVISION     - [12.0]
* Copyright 2001 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - PMCBP
* PROGRAM      - CONVERT.TO.RTF
* BY           - ABDULLAH JIBALY, COMPUTER BUSINESS ASSOCIATES
* DATE         - 10/03/2001
* DESCRIPTION  - This subroutine converts a unidata file to RTF format
*              - SEE TASK 26206
*ENDDOC
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>SECURITY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- Setup system error messages
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
**************
* OPEN FILES *
**************
  OPEN '',OPEN.FILE TO WORK.FILE ELSE
    ERRMSG = 'CANNOT OPEN THE ' : OPEN.FILE : ' FILE!'
    GOTO 93000
  END
**************
* INITIALIZE *
**************
*
  READ FILE.REC FROM WORK.FILE, ITEM.NAME ELSE
    ERRMSG = "ITEM ": ITEM.NAME :" DOES NOT EXIST IN ": OPEN.FILE
    GOTO 93000
  END
  PAGE.FORMAT = ''
  LOCATE "WIDE REPORT" IN FORMATSTR<1> SETTING PTR THEN
    PAGE.FORMAT = "\landscape\paperw15840\paperh12240"
  END
  FONT.SIZE = 10
  LOCATE "FONT SIZE" IN FORMATSTR<1> SETTING PTR THEN
    FONT.SIZE = FORMATSTR<2,PTR>
  END
  FONT.SIZE = "\fs":INT(FONT.SIZE * 2)
  MARGIN.SIZE = 0.5 * 1440
  LOCATE "MARGIN SIZE" IN FORMATSTR<1> SETTING PTR THEN
    MARGIN.SIZE = INT(FORMATSTR<2,PTR> * 1440)
  END
  MARGIN.SIZE = CHANGE("\margl*\margr*\margt*\margb*",'*',MARGIN.SIZE)
  LINES = DCOUNT(FILE.REC,AM)
  NEW.FILE.REC = "{\rtf1\ansi\ansicpg1252\deff0{\fonttbl"
  NEW.FILE.REC:= "{\f0\fmodern\fprq1\fcharset0 Courier New;}}"
  NEW.FILE.REC:= "\viewkind4\uc1\viewzk2\pard"
  NEW.FILE.REC:= PAGE.FORMAT:"\f0":FONT.SIZE:MARGIN.SIZE
  NEW.FILE.REC:= "\sl204\slmult1"
  NEW.FILE.REC:= "{\info{\title TITLE}{\author ":@LOGNAME:"}"
  NEW.FILE.REC:= "{\company ":CO.NAME:"}} "
  FOR INDEX = 1 TO LINES
    FILE.REC<INDEX> = CHANGE(FILE.REC<INDEX>,"\","\\")
    FILE.REC<INDEX> = CHANGE(FILE.REC<INDEX>,"{","\{")
    FILE.REC<INDEX> = CHANGE(FILE.REC<INDEX>,"}","\}")
    IF INDEX # 1 THEN
      FILE.REC<INDEX> = CHANGE(FILE.REC<INDEX>,NEW.PAGE,"\page ")
    END
    NEW.FILE.REC := FILE.REC<INDEX>:"\par "
  NEXT INDEX
  NEW.FILE.REC := "}"
  OSWRITE NEW.FILE.REC ON "./":OPEN.FILE:"/":ITEM.NAME:".rtf"
*
GOTO 99999
*
********************
* CALLS FOR SYSCOM *
********************
91000*
ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
*
99999 *
  RETURN
END
