*COPY>CPYLIB>COM1
****************************************************************
* REVISION      - [10A.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - PRODUCTION.DATE.RPT
* SYSTEM        - PMCBP
* BY
* DATE          - 5/95
* DESCRIPTION   - This report will list the period begining and ending dates.
* TASK
*    18573 LLH 5/95 1-52 ACCOUNTING PERIODS
*T22468 lanny 10/25/99 * Allow 2 years setup.
* T26493 cmykleb 04/01/2002 * Change pgm to get the rpt # from the proc.
****************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>COMPANY
*
*---- DIMENSION VARIABLES
*
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
*T22468 v
   PROCREAD XX ELSE
      ERRMSG='Must be run from PROC'; GOTO 93000
   END
   PYEAR = XX<3>
*T22468 ^
*
*---- OPEN FILES
*
   OPEN 'COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN 'CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
*
*---- GET COMPANY NUMBER
*
   CONO = ""
   CALL GET.CONO(CONO, MAT COMP.REC)
*
*---- INITIALIZATION
*
   READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = ""
      PERIOD.REC<1> = 12
   END
   NUM.PERIODS = PERIOD.REC<1>
   PG.NO=1
   ERRMSG=""
   SP2 = "  "
   SP3 = "   "
   NODATA=1
*T26493 v
*  CONAME = CO.NAME
*  REPORT.NAME = "PRODUCTION DATE INFORMATION FOR YEAR ":PYEAR
*  REPORT.NUMBER = "XXXX"
   CONAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = XX<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
*** GET HEADING
   CALL GET.PROG.HEAD(CONO,CONAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   HEAD1 = HD1
   HEAD2 = HD2
   HEAD3 = "PRODUCTION WEEK NUMBER    ENDING DATE    QUARTER"
   HEAD4 = "----------------------    -----------    -------"
*
*---- GET PERIOD INFORMATION
*
*T22468 READ PRODUCTION.REC FROM CONTROL, CONO:"PRODUCTION.WEEKS" ELSE
   READ PRODUCTION.REC FROM CONTROL, CONO:PYEAR:"PRODUCTION.WEEKS" ELSE
      PRODUCTION.REC = ""
   END
   NUM.PRODUCTION.WEEKS = DCOUNT(PRODUCTION.REC<1>,VM)
*
* MAIN PROCESSING
*
   PRINTER ON
   GOSUB 100  
   FOR PR = 1 TO NUM.PRODUCTION.WEEKS
      END.DATE = OCONV(PRODUCTION.REC<1,PR>,"D2/")
      QTR      = PRODUCTION.REC<2,PR>
      P.LINE   = PR"R#8":SPACE(18):END.DATE"R#11":SPACE(4):QTR"R#4"
      IF LINE.CNT > 66 THEN
         GOSUB 100
      END
      PRINT P.LINE
      LINE.CNT = LINE.CNT + 1
   NEXT PR
   GOTO 99999
*
100 * PRINT HEADINGS
*
   PRINT HEAD1:SPACE(1):PG.NO"R#2"
   PRINT HEAD2
   PRINT
   PRINT HEAD3
   PRINT HEAD4
*
   LINE.CNT = 5
   PG.NO = PG.NO + 1
   RETURN
*
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER CLOSE
   PRINTER OFF
END
