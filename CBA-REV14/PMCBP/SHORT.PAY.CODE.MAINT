*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* SOURCE         - PMCBP
*
* PROGRAM        - SHORT.PAY.CODE.MAINT
*
* BY             - ABDULLAH JIBALY, COMPUTER BUSINESS ASSOCIATES
*
* DATE           - 07/17/01
*
* DESCRIPTION    -
*                  This program will be used for short pay code
*                  maintenance.
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>ARS.CPYLIB>SHORT.PAY.CODE
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
  OPEN '','SHORT.PAY.CODE' TO SHORT.PAY.CODE ELSE ERRMSG = 'SHORT.PAY.CODE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  MAT EDIT.COM.DRIVER = ""
  ERRMSG = ''
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'SHORT.PAY.CODE.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
  SCV.REC(5)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=5
  ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  SHORT.PAY.CODE.KEY = UPCASE(ECD.RET.VALUE)
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.SHORT.PAY.CODE = 'NO'
      * T26126 v
      MATREADU SHORT.PAY.REC FROM SHORT.PAY.CODE,CONO:SHORT.PAY.CODE.KEY LOCKED
        ERRMSG = 'SHORT PAY CODE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE
      * T26126 ^
        MAT SHORT.PAY.REC = ''
        NEW.SHORT.PAY.CODE = 'YES'
      END
  END CASE
  IF NEW.SHORT.PAY.CODE = 'YES' THEN 
    GOSUB 20
    IF ECD.RET.VALUE = 'END' THEN GOTO 10
  END ELSE
    SCV.REC(2)<ECD.SCRN.NO,1>=SHORT.PAY.DESC
    ECD.NUM=2;ECD.ACTION = 5; CALL SCRN.EDIT
  END
  GOSUB 100
  GOSUB 200
  GOTO 10
*
*---- ENTER SHORT PAY CODE DESCRIPTION
*
20*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SHORT.PAY.DESC = ECD.RET.VALUE
  END ELSE
    RELEASE SHORT.PAY.CODE,CONO:SHORT.PAY.CODE.KEY
    GOSUB 200
    GOTO 25
  END
25*
  RETURN
*
*---- ENTER OPTIONS
*
100*
  MORE = 0
  LOOP
    ECD.NUM = 4
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = UPCASE(ECD.RET.VALUE)
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE SHORT.PAY.CODE,CONO:SHORT.PAY.CODE.KEY   ;* T26126
        MORE = 1
      CASE OPTION = '1'
        GOSUB 20
      CASE OPTION = 'F'
        GOSUB 300
        MORE = 1
      CASE OPTION = 'D'
        ECD.NUM=6 ; CALL SCRN.EDIT
        IF UPCASE(ECD.RET.VALUE) = 'Y' THEN
          DELETE SHORT.PAY.CODE,CONO:SHORT.PAY.CODE.KEY
          MORE = 1
        END
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
200*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE SHORT.PAY.CODE FILE
*
300*
  MATWRITE SHORT.PAY.REC ON SHORT.PAY.CODE,CONO:SHORT.PAY.CODE.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
