*REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*COPY>OPS.CPYLIB>ORDER
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>CHAR
OPEN '','ORDER' TO ORDER ELSE STOP
OPEN '','CUSTOMER' TO CUSTOMER ELSE STOP
P_X  = 0 ; P_Y = 23 ; P_VALUE = "CLEAR CUSTOMER ORDER INFO" ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
SELECT CUSTOMER
DATA = 1
LOOP
  READNEXT ID ELSE DATA = 0
WHILE DATA DO
  CONO = ID[1,3]
  CUST.NUM = ID[4,99]
  MATREADU CUST.REC FROM CUSTOMER, CONO:CUST.NUM THEN
    CUST.ORD.NUM = ''
    CUST.ORD.BAL = ''
    MATWRITE CUST.REC ON CUSTOMER, CONO:CUST.NUM
  END ELSE
    RELEASE CUSTOMER, CONO:CUST.NUM
  END
REPEAT
***
P_X  = 0 ; P_Y = 23 ; P_VALUE = "UPDATE CUSTOMER ORDER INFO" ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
SENTENCE = "SSELECT ORDER BY ORD.DATE BY ORD.NO"
PERFORM SENTENCE RTNLIST ORD.LIST
DATA = 1
LOOP
  READNEXT ID FROM ORD.LIST ELSE DATA = 0
WHILE DATA DO
  CONO = ID[1,3]
  ORD.NUM = ID[4,99]
  MATREAD ORD.REC FROM ORDER, ID THEN
    MATREADU CUST.REC FROM CUSTOMER, CONO:ORD.CUST THEN
      LOCATE ORD.NUM IN CUST.ORD.NUM<1>,1 SETTING O ELSE
        CUST.ORD.NUM<1,O> = ORD.NUM
      END
      ORD.BALANCE = ORD.TOT.AMT - ORD.TOT.INV
      IF ORD.BALANCE < 0 THEN ORD.BALANCE = 0
      CUST.ORD.BAL<1,O> = ORD.BALANCE
      MATWRITE CUST.REC ON CUSTOMER, CONO:ORD.CUST
    END ELSE
      RELEASE CUSTOMER, CONO:ORD.CUST
    END
  END
REPEAT
***
P_X  = 0 ; P_Y = 23 ; P_VALUE = "DELETE OLD CUSTOMER ORDER INFO" ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
SELECT CUSTOMER
DATA = 1
LOOP
  READNEXT CUST.ID ELSE DATA = 0
WHILE DATA DO
  MATREADU CUST.REC FROM CUSTOMER,CUST.ID THEN
    OCNT = DCOUNT(CUST.ORD.NUM,VM)
    IF OCNT > 250 THEN
      FOR O = OCNT TO 1 STEP -1
        MATREAD ORD.REC FROM ORDER, CONO : CUST.ORD.NUM<1,O> THEN
          IF ORD.STATUS<1,1> = "CANCEL" THEN
            DEL CUST.ORD.NUM<1,O>
            DEL CUST.ORD.BAL<1,O>
          END ELSE
            IF ORD.TOT.INV+0 = 0 AND ORD.INV.NO # "" THEN
              DEL CUST.ORD.NUM<1,O>
              DEL CUST.ORD.BAL<1,O>
            END
          END
        END ELSE
          DEL CUST.ORD.NUM<1,O>
          DEL CUST.ORD.BAL<1,O>
        END
      NEXT O
    END
    MATWRITE CUST.REC ON CUSTOMER,CUST.ID
  END ELSE
    RELEASE CUSTOMER, CUST.ID
  END
REPEAT
END
