*COPY>CPYLIB>COM1
****************************************************************
* REVISION      - [08.X]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - SHIP.TO.LIST
* SYSTEM        - PMXBP
* BY            - CURTIS WILLIAMS, C.B.A.
* DATE          - 04/23/88
* DESCRIPTION   - This program produces a SHIP.TO report.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
****************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>FOB
*COPY>PMC.CPYLIB>TAX
*COPY>CPYLIB>SYSCOM
*
*---- SETUP FOR SYSTEM ERROR MESSAGES

      SYS.TYPE=1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
      PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM A PROC";GOSUB 91000;GOTO 99999
      CONO = ID<1>
*
*---- OPEN FILES
*
      OPEN 'COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
      OPEN 'CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
      OPEN 'SHIP.TO' TO SHIP.TO ELSE ERRMSG='SHIP.TO FILE MISSING';GOTO 93000
      OPEN 'TAX' TO TAX ELSE ERRMSG='TAX FILE MISSING';GOTO 93000
      OPEN 'FOB' TO FOB ELSE ERRMSG='FOB FILE MISSING';GOTO 93000
      OPEN 'CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
*
*---- GET COMPANY NUMBER
*
      MATREAD COMP.REC FROM COMPANY, CONO ELSE GOTO 99999
*
*---- INITIALIZATION
*
      PG.NO=0
      LINE.CNT=99
*T26493 v
*     REPORT.NAME = "SHIP TO LISTING"
*     REPORT.NUMBER = "XXXX"
      CONO.NAME = ""
      REPORT.NAME = ""
      REPORT.NUMBER = ID<2>
*T26493 ^
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
*** GET HEADING
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
      HEAD4 = "SHP FREIGHT ON BOARD      ATTENTION       TAX JUR     DESCRIPTION       EXEMPT NUMBER  EXP DATE A   DATE     PHONE #       FAX #"
      HEAD5 = "--- ---------------- -------------------- ------- -------------------- --------------- -------- - -------- ------------ ------------"
*
*---- MAIN PROCESSING
*
      PRINTER ON
      READNEXT ID ELSE GOTO 99999
      PREV.CUST = FIELD(ID,"!",1)[4,99]
      GOSUB 100
      DATA = 1
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         CUST.NO = FIELD(ID,"!",1)[4,99]
         IF PREV.CUST # CUST.NO THEN
            LINE.CNT = 99
            PREV.CUST = CUST.NO
         END
         GOSUB 100
      REPEAT
      GOTO 99999
*
*---- PRINT ROUTINE
*
100   IF LINE.CNT > 55 THEN GOSUB 8000
      MATREAD SPT.REC FROM SHIP.TO, ID ELSE GOTO 199
      DCNT = DCOUNT(SPT.TAX.JUR,VM)
      LINE = "SHIP TO : ":FIELD(ID,"!",2)"L#4"
      LINE = LINE:SPT.NAME "L#35"
      PRINT LINE
      LINE = SPACE(14):SPT.ADDR1 "L#35":SPACE(21)
      PRINT LINE
      LINE = SPACE(14):SPT.ADDR2 "L#35":SPACE(21)
      PRINT LINE
      SPT.ADDR3 = SPT.CITY:" ":SPT.STATE:" ":SPT.ZIP
      LINE = SPACE(14):SPT.ADDR3"L#35":SPACE(21)
      PRINT LINE
      PRINT
      LINE.CNT = LINE.CNT + 6
      PRINT HEAD4; PRINT HEAD5
      GOSUB 200
      PRINT
199   RETURN
      GOTO 99999
200 *
      LN.CNT = 1
      LINE = SPT.SHP.TERMS"L#3":SPACE(1)
      MATREAD FOB.REC FROM FOB, CONO:SPT.FOB ELSE
         MAT FOB.REC = ""
      END
      LINE = LINE:FOB.DESC"L#15":SPACE(2)
      LINE = LINE:SPT.ATTN"L#21"
      IF SPT.TAX.JUR<1,1> # "" THEN
         LINE = LINE:SPT.TAX.JUR<1,LN.CNT> "L#8"
         MATREAD TAX.REC FROM TAX,CONO:SPT.TAX.JUR<1,LN.CNT> ELSE TAX.JUR = "???????????????????????"
         LINE = LINE:TAX.JUR "L#21"
      END ELSE
         LINE = LINE:SPACE(29)
      END
      LINE = LINE:SPT.EXEMPT<1,LN.CNT> "L#16"
      LINE = LINE:OCONV(SPT.EXMT.DATE<1,LN.CNT>,"D2/") "L#9"
      LINE = LINE:SPT.STATUS "L#2"
      LINE = LINE:OCONV(SPT.STAT.DATE,"D2/") "L#9"
      LINE = LINE:SPT.PHONE"L#13":SPT.FAX"L#13"
      PRINT LINE
      FOR LN.CNT = 2 TO DCNT
         LINE = SPACE(42)
         LINE = LINE:SPT.TAX.JUR<1,LN.CNT> "L#8"
         MATREAD TAX.REC FROM TAX,CONO:SPT.TAX.JUR<1,LN.CNT> ELSE TAX.JUR = "???????????????????????"
         LINE = LINE:TAX.JUR "L#21"
         LINE = LINE:SPT.EXEMPT<1,LN.CNT> "L#16"
         LINE = LINE:OCONV(SPT.EXMT.DATE<1,LN.CNT>,"D2/") "L#9"
         PRINT LINE
      NEXT LN.CNT
      LINE.CNT = LINE.CNT + DCNT + 2
      RETURN
*
*---- PRINT THE HEADINGS
*
8000 *
      PRINT CHAR(12)
      MATREAD CUST.REC FROM CUSTOMER,CONO:PREV.CUST ELSE
         CUST.NAME = "???????????????"
      END
      PG.NO=PG.NO + 1
      PRINT HD1:PG.NO
      PRINT HD2
      PRINT
      PRINT "FOR CUSTOMER : ":PREV.CUST:" ":CUST.NAME
      PRINT 
      LINE.CNT = 5
      RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
      ERR.TYPE=1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE=3
      CALL SYSCOM(MAT SYSCOM.REC)
99999 *
      PRINTER OFF
      END
