*
PRECISION 2
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - GET.PROG.HEAD
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 10/13/85
*
* REVISION - A.1.0
*
* DESCRIPTION
*
* This subroutine is called upon to format headings from a report.
*
*********************************************************************
*
*--- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>REPORTS
*COPY>CPYLIB>CHAR
*
*---- SET-UP
*
      HLINE1 = "END"
      HLINE2 = "END"
*
*--- OPEN ALL FILES
*
      IF CONO = "" THEN
         OPEN "","CONTROL" TO CONTROL ELSE
            ERRMSG = "CANNOT OPEN CONTROL FILE"
            GOSUB 90000; GOTO 99999
         END
      END
      IF CNAME = "" THEN
         OPEN "","COMPANY" TO COMPANY ELSE
            ERRMSG = "CANNOT OPEN COMPANY FILE"
            GOSUB 90000; GOTO 99999
         END
      END
      IF RNAME = "" THEN
         RNAME = "PROGRAM NAME NOT ASSIGN"
      END
      OPEN "","REPORTS" TO REPORTS ELSE
         ERRMSG = "CANNOT OPEN REPORTS FILE"
         GOSUB 90000 ; GOTO 99999
      END
      IF RNUMBER = "" THEN
         RNUMBER = "XXXX"
      END
*
*---- MAIN PROCESSING
*
      IF CONO = "" THEN
         READV CONO FROM CONTROL,"MASTER",1 ELSE CONO = "001"
      END
      IF CNAME = "" THEN
         MATREAD COMP.REC FROM COMPANY, CONO ELSE
            MAT COMP.REC = ""
         END
         CNAME = CO.NAME
      END
      MATREAD RPT.REC FROM REPORTS , CONO:RNAME ELSE
          MAT RPT.REC = ""
      END
      IF RPT.TITLE # "" THEN
         RNAME = RPT.TITLE
      END
      IF RPT.NUMBER # "" THEN
         RNUMBER = RPT.NUMBER
      END
      PROG.HEAD1 = ""
      INPUT.HEAD = TRIM(RNAME)
      PTR = INDEX(INPUT.HEAD,"(",1)
      IF PTR = 0 THEN
         INPUT.HEAD1 = TRIM(INPUT.HEAD)
         INPUT.HEAD2 = ""
      END ELSE
         INPUT.HEAD1 = TRIM(INPUT.HEAD[1,PTR-1])
         INPUT.HEAD2 = TRIM(INPUT.HEAD[PTR,99])
      END
      IHL = LEN(INPUT.HEAD1)
      FOR P = 1 TO IHL
         IF INPUT.HEAD1[P,1] # "." THEN
            PROG.HEAD1 = PROG.HEAD1:" ":INPUT.HEAD1[P,1]
         END ELSE
            PROG.HEAD1 = PROG.HEAD1:"  "
         END
      NEXT P
      IF LEN(PROG.HEAD1) > 80 THEN
         PROG.HEAD1 = INPUT.HEAD1
      END ELSE
         PROG.HEAD1 = PROG.HEAD1[2,80]
      END
*
*---- BUILD REPORT HEADING LINES
*
      IF RDATE = "" THEN RDATE = DATE()
      HLINE1A = "RUN D - T: ":OCONV(RDATE,"D2/"):" - ":OCONV(TIME(), "MTS")
      HLINE1A = HLINE1A "L#33"
      HLINE1B = SPACE(INT((85-LEN(CNAME))/2)):CNAME
      HLINE1B = HLINE1B "L#85"
      HLINE1C = SPACE(3):"PAGE "
      HLINE1 = HLINE1A:HLINE1B:HLINE1C
      HLINE2A = "REPORT # : ":RNUMBER
      HLINE2A = HLINE2A "L#33"
      HLINE2B = SPACE(INT((85-LEN(PROG.HEAD1))/2)):PROG.HEAD1
      HLINE2B = HLINE2B:"   ":INPUT.HEAD2
      HLINE2 = HLINE2A:HLINE2B
      HLINE2 = HLINE2 "L#132"
      GOTO 99999
*
*--- ERROR ROUTINE
*
90000*
      PRINT ERRMSG
      RETURN
*
*--- END OF PROGRAM
*
99999*
      RETURN
   END
