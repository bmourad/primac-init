*COPY>CPYLIB>COM1
************************************************************************
*   REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
*   SYSTEM     - PRIMAC
*
*   PROGRAM    - MAIL
*
*   DATE       - 10/20/83
*
*   BY         - EVAN PRICE, COMPUTER BUSINESS ASSOCIATES
*
*   DESCRIPTION
*
*   This program will allow the entry of user to user mail and the
*   viewing by a user of any mail for him in the system.
*
*ENDDOC
************************************************************************
*
*--- DIMENSION
*
*COPY>PMC.CPYLIB>SECURITY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*----  DEFINE PORT AND ACCOUNT
*
    DASHES = STR("-",80)
    PROMPT ''
*    PORT.ACCT = OCONV(0,"U50BB")
*    PORT.NO = FIELD(PORT.ACCT," ",1)
*    ACCT.NM = FIELD(PORT.ACCT," ",2)
*   PORT.NO = SYSTEM(18)
PORT.NO = "TTY"
CALL SYSVARS.SUB(PORT.NO)
*IND.POS = INDEX(PORT.NO,'/',1)
*IF IND.POS = 0 THEN
*   PORT.NO = PORT.NO[4,(LEN(PORT.NO)-3)]
*END ELSE
*   PORT.NO = PORT.NO[4,IND.POS-4]
*END
REM PORT.NO = @TTY[4,2]
*    ACCT.NM = SYSTEM(19)
     ACCT.NM = @WHO
* DLG
    PORT.ACCT = PORT.NO:" ":ACCT.NM
    TYP = 0
    CALL EDIT.SUB
*
*---- OPEN FILES
*
    OPEN '','USER.MAIL' TO USER.MAIL ELSE
       ERRMSG = 'USER.MAIL FILE MISSING'
       GOTO 80000
    END
    OPEN '','SECURITY' TO SECURITY ELSE
       ERRMSG = 'SECURITY FILE MISSING'
       GOTO 80000
    END
*
*--- MAIN PROCESSING
*
      PRINT @(-1):
     X=0;Y=0
     TYP = 1
     MINL = 1
     MAXL = 1
     VALDAT = "R":VM:"E"
     PMSG = "Do you wish to (R)eceive or (E)nter mail? "
     CALL EDIT.SUB
     IF VALUE = "END" THEN GOTO 90000
     BEGIN CASE
        CASE VALUE = "R"
           GOSUB 1000
        CASE VALUE = "E"
           GOSUB 2000
     END CASE
     GOTO 90000
*
*---- RECEIVE MAIL
*
1000*
      READ USER.REC FROM SECURITY, "R.":PORT.NO ELSE
        GOTO 90000
      END
      CONO = USER.REC<1>
      USER.ID = USER.REC<2>
      READ MAIL.REC FROM USER.MAIL, CONO:USER.ID ELSE
P_X  = 0 ; P_Y = 1 ; P_VALUE = "Sorry, your mail box is empty" ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         RQM;RQM
         GOTO 90000
      END
       PRINT @(-1):
      SCRN.LINE = (-1)
      NO.ATTRIB = COUNT(MAIL.REC,AM) + (MAIL.REC # "")
      FOR NOAM = 1 TO NO.ATTRIB
         NO.VALUES = COUNT(MAIL.REC<NOAM>,VM) + (MAIL.REC<NOAM> # "")
         FOR NOVM = 1 TO NO.VALUES
            SCRN.LINE = SCRN.LINE + 1
            IF SCRN.LINE > 20 THEN
P_X  = 0 ; P_Y = 23 ; P_VALUE = "Hit return to see more mail " ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               INPUT X:
               SCRN.LINE = (-1)
*                PRINT @(-1):
            END
P_X  = 0 ; P_Y = SCRN.LINE ; P_VALUE = MAIL.REC<NOAM,NOVM>[1,80] ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         NEXT NOVM
      NEXT NOAM
      X=0;Y=22
      TYP = 8
      PMSG = "Do you wish to delete your mail? (Y or N) "
      CALL EDIT.SUB
      IF VALUE = "Y" THEN
         DELETE USER.MAIL, CONO:USER.ID
      END
     GOTO 90000
*
*----  ENTER MAIL FOR ANOTHER USER
*
2000*
      READ USER.REC FROM SECURITY, "R.":PORT.NO ELSE
        GOTO 90000
      END
      CONO = USER.REC<1>
      USER.ID = USER.REC<2>
     X=0;Y=1
     TYP = 1
     MAXL = 20
     PMSG = "To which user? :"
     CALL EDIT.SUB
     IF VALUE = "END" THEN GOTO 90000
     RECIPIENT.ID = VALUE
     MATREAD SEC.REC FROM SECURITY, CONO : RECIPIENT.ID ELSE
P_X  = 0 ; P_Y = 2 ; P_VALUE = "Sorry, this user is not on the system " ; P_OPT = "CL"
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        GOTO 90000
     END
     READ MAIL.REC FROM USER.MAIL, CONO:RECIPIENT.ID ELSE
        MAIL.REC = ""
     END
     NO.ATT = COUNT(MAIL.REC,AM) + (MAIL.REC # "")
     MAIL.REC<NO.ATT + 1> = "*** From user ":USER.ID:" on ":OCONV(DATE(),"D2/"):" at ":OCONV(TIME(),"MTHS"):VM
     FOR SCRN.LINE = 2 TO 21
        TYP = 1
        X=0;Y=SCRN.LINE
        MAXL = 78
        PMSG = ">"
        O.R = "O"
        CALL EDIT.SUB
        IF VALUE = "END" OR VALUE = "" THEN GOTO 2500
        VALUE = VALUE:VM
        MAIL.REC<NO.ATT + 1> = MAIL.REC<NO.ATT + 1>:VALUE
     NEXT SCRN.LINE
2500*
     LEN.MAIL = LEN(MAIL.REC<NO.ATT + 1>)
     MAIL.REC<NO.ATT + 1> = MAIL.REC<NO.ATT + 1>[1,LEN.MAIL - 1]
     WRITE MAIL.REC ON USER.MAIL, CONO:RECIPIENT.ID
     GOTO 90000
*
*---- UNRECOVERABLE ERROR
*
80000*
P_X  = 0 ; P_Y = 23 ; P_VALUE = ERRMSG ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
     STOP
*
*---- END OF JOB
*
90000*
     STOP
   END
