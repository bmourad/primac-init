  SUBROUTINE BUILD.ACCESS.BY.USER(CONO, USER.ID, MAT SEC.REC, EASY.MENUS, ACCESS_BY_USER)
************************************************************************
*
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
*   PROGRAM    - BUILD.ACCESS.BY.USER
*
*   DATE       - 08/05/2002
*
*   BY         - ALEJANDRO DELGADO
*
*   DESCRIPTION
*
*   This program will build the file 'ACCESS_BY_USER'.  The new file
*   will hold by user all menu access the user holds.
*
*T26784 adelgado 08/06/2002 * Original Code.
************************************************************************
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>MENUS.CONTROL
*COPY>PMC.CPYLIB>SECURITY
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC = '' ; SYS.TYPE = 2
*
*---- MAIN PROCESSING
*
100*
  ACCESS.REC = ''
  CNT = DCOUNT(SEC.MENU.PROC<1>, VM)
  IF SEC.MENU.FLAG = 'A' AND SEC.MENU.LEVEL LT 4 AND CNT GT 0 THEN
    ACCESS.REC = SEC.MENU.PROC
  END ELSE
    MASTER = 'M.MASTER'
    GOSUB GET.MENU.NAMES
    MENU.CNT = COUNT(MENU.NAMES,AM) + (MENU.NAMES # "")
    FOR MENU.PTR = 1 TO MENU.CNT
      MENU.ID = MENU.NAMES<MENU.PTR>
      IF MENU.ID[1,2] # "M." THEN CONTINUE
      IF MENU.ID = "M.PRS" THEN MENU.ID = "M.EP4"
      READ MENU FROM EASY.MENUS, MENU.ID ELSE CONTINUE
      GOSUB 2000
    NEXT MENU.PTR
  END
  WRITE ACCESS.REC ON ACCESS_BY_USER, CONO:USER.ID
  GOTO 99999
*
*---- PROCESS SELECTED MENU
*
2000*
  LOOP.DONE = 0 ; CHECK.MENUS = ''
  PRIOR.MENU = MENU.ID ; VAL.ERR = 0
  LOOP
    IF PRIOR.MENU = 'M.MASTER' THEN
      LOOP.DONE = 1
    END ELSE
      READ WORK.MENU FROM EASY.MENUS, PRIOR.MENU THEN
        CHECK.MENUS<1,-1> = PRIOR.MENU
        PRIOR.MENU = WORK.MENU<1,1>
      END ELSE
        LOOP.DONE = 1
      END
    END
  UNTIL LOOP.DONE DO REPEAT
    *
  IF SEC.MENU.LEVEL LT 4 THEN
    CCNT = DCOUNT(CHECK.MENUS,VM)
    FOR CPTR = 1 TO CCNT
      CHECK.SEL.MENU.ID = CHECK.MENUS<1,CPTR>
      LOCATE CHECK.SEL.MENU.ID IN SEC.MENU.PROC<1>,1 SETTING FND THEN
        VAL.ERR = 1
      END
    NEXT CPTR
  END
  IF VAL.ERR = 0 THEN
    ACCESS.REC<1,-1> = MENU.ID
  END
  RETURN
*
GET.MENU.NAMES: 
*
  DIM MENU.REC(50)
  MATREAD MENU.REC FROM EASY.MENUS, MASTER ELSE
    ERRMSG = 'CANNOT LOCATE MENU (':MASTER:') TO BUILD USER ACCESS MENUS'
    GOSUB 91000
  END
  MENU.NAMES = MASTER
  MENU.LST = MASTER
  MENU.PTR = 1
1000 *
  MENU.PTR<1> = MENU.PTR<1> + 1
  IF MENU.PTR<1> > 33 THEN
    MENU.LST = DELETE(MENU.LST,1,0,0)
    MENU.PTR = DELETE(MENU.PTR,1,0,0)
    MENU.NAME = MENU.LST<1>
    IF MENU.NAME = "M.PRS" THEN MENU.NAME = "M.EPS"
    IF MENU.NAME # "" THEN
      MATREAD MENU.REC FROM EASY.MENUS, MENU.NAME ELSE
        ERRMSG = 'CANNOT RE-READ MENU (':MENU.NAME:') TO BUILD USER ACCESS MENUS'
        GOSUB 91000
      END
    END
    RETURN
  END
  SEL = MENU.REC(MENU.PTR<1>)<1,1>
  IF SEL = "M.PRS" THEN SEL = "M.EP4"
  SEL.DESC = MENU.REC(MENU.PTR<1>)<1,3>
  BEGIN CASE
    CASE SEL[1,2] = "M."
      MENU.NAME = SEL
      MATREAD MENU.REC FROM EASY.MENUS, MENU.NAME ELSE
        GOTO 1000
      END
      MENU.NAMES<-1> = MENU.NAME
      MENU.LST = INSERT(MENU.LST,1,0,0,MENU.NAME)
      MENU.PTR = INSERT(MENU.PTR,1,0,0,1)
      GOSUB 1000
    CASE FIELD(SEL,".",2) = "LOGTO"
      MENU.NAME = "M.":FIELD(SEL,".",1)
      IF MENU.NAME = "M.PRS" THEN MENU.NAME = "M.EP4"
      MATREAD MENU.REC FROM EASY.MENUS, MENU.NAME ELSE
        GOTO 1000
      END
      MENU.NAMES<-1> = MENU.NAME
      MENU.LST = INSERT(MENU.LST,1,0,0,MENU.NAME)
      MENU.PTR = INSERT(MENU.PTR,1,0,0,1)
      GOSUB 1000
  END CASE
  GOTO 1000
  *
  RETURN
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)       
*
*---- END OF PROGRAM
*
99999*
  RETURN
END
