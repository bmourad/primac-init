*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* SOURCE         - PMCBP
*
* PROGRAM        - SHIP.VIA.MAINT
*
* BY             - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE           - 01/25/85
*
* DESCRIPTION    -
* This program will be used for Ship Via Maintenance.
*
*T22533 stefanie 03/17/1998 * GL number prompt does not hold default.
*T26126 adelgado 02/22/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE MISSING';GOTO 93000
  OPEN '','SHIP.VIA' TO SHIP.VIA ELSE ERRMSG = 'SHIP.VIA FILE MISSING';GOTO 93000
  OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  ERRMSG = ""
  UNKNOWN = "???????????????"
*
*---- GET COMPANY NUMBER
*
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  MAT EDIT.COM.DRIVER = ""
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'SHIP.VIA.MAINT'
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
  SCV.REC(5)<ECD.SCRN.NO>=DATE()
  ECD.NUM=5
  ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 1
  ECD.ACTION = 4; CALL SCRN.EDIT
  SHIP.VIA.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      NEW.SHIP.VIA = "NO"
      * T26126 v
      MATREADU SHIP.VIA.REC FROM SHIP.VIA,CONO:SHIP.VIA.KEY LOCKED
        ERRMSG = 'SHIP VIA record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
      * T26126 ^
        MAT SHIP.VIA.REC=''
        NEW.SHIP.VIA = 'YES'
      END
  END CASE
  IF NEW.SHIP.VIA = 'YES' THEN 
    FOR S = 1 TO 3
      ON S GOSUB 10,20,30
      IF ECD.RET.VALUE = 'END' THEN 
        RELEASE SHIP.VIA,CONO:SHIP.VIA.KEY
        GOSUB 300;GOTO 5
      END
    NEXT S
  END ELSE
    GOSUB 100
  END
  GOSUB 200
  GOSUB 300
  GOTO 5
*
*---- ENTER CONTACT CODE DESCRIPTION
*
10*
  ECD.NUM = 2
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SHPV.DESC = ECD.RET.VALUE
  END 
  RETURN
*
*---- ENTER GENERAL LEDGER NUMBER
*
20*
  SCV.REC(6)<ECD.SCRN.NO> = ""
  ECD.MAXL = LEN(CO.ACCT.PIC)     ;* T22533 <
  ECD.NUM=6;ECD.ACTION=5;CALL SCRN.EDIT
  ECD.NUM = 6
  ECD.DEFAULT = SHPV.GL.NO       ;* T22533 <
  ECD.MAXL = LEN(CO.ACCT.PIC) ; ECD.PATRN = CO.ACCT.MATCH
  ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    SHPV.GL.NO = ECD.RET.VALUE
    SCV.REC(6)<ECD.SCRN.NO> = SHPV.GL.NO CO.ACCT.MASK
    ECD.NUM = 6;ECD.ACTION = 5;CALL SCRN.EDIT
    MATREAD COA.REC FROM COA, CONO : SHPV.GL.NO ELSE
      ERRMSG = "INVALID GENERAL LEDGER NUMBER";GOSUB 91000;GOTO 20
    END
    SCV.REC(7)<ECD.SCRN.NO> = COA.DESC
    ECD.NUM = 7;ECD.ACTION = 5;CALL SCRN.EDIT
  END
  RETURN
*
*---- ENTER OWN TRUCK FLAG
*
30*
  ECD.NUM = 8
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    SHPV.TRUCK.FLG = ECD.RET.VALUE
  END 
  RETURN
*
*---- GET VALUES FROM FILE
*
100*
  IF SHPV.GL.NO # "" THEN
    MATREAD COA.REC FROM COA, CONO : SHPV.GL.NO ELSE
      MAT COA.REC = ""; COA.DESC = UNKNOWN
    END
    GL.NUMBER = SHPV.GL.NO CO.ACCT.MASK
  END ELSE
    COA.DESC = ""
    GL.NUMBER = ""
  END
  SCV.REC(2)<ECD.SCRN.NO> = SHPV.DESC
  SCV.REC(6)<ECD.SCRN.NO> = GL.NUMBER
  SCV.REC(7)<ECD.SCRN.NO> = COA.DESC
  SCV.REC(8)<ECD.SCRN.NO> = SHPV.TRUCK.FLG
  ECD.ACTION = 3;CALL SCRN.EDIT
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 0
  LOOP
    ECD.NUM = 4
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE SHIP.VIA,CONO:SHIP.VIA.KEY   ;* T26126
        MORE = 1
      CASE NUM(OPTION)
        ON OPTION GOSUB 10,20,30
      CASE OPTION = "D"
        ECD.NUM=9 ; CALL SCRN.EDIT
        IF ECD.RET.VALUE = 'Y' THEN
          DELETE SHIP.VIA,CONO:SHIP.VIA.KEY
          MORE = 1
        END
      CASE OPTION = 'F'
        GOSUB 400
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
  ECD.ACTION = 6; CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE SHIP.VIA FILE
*
400*
  MATWRITE SHIP.VIA.REC ON SHIP.VIA,CONO:SHIP.VIA.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1):
END
