*COPY>CPYLIB>COM1
****************************************************************
* REVISION      - [10A.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - PERIOD.DATE.RPT
* SYSTEM        - PMCBP
* BY
* DATE          - 5/95
* DESCRIPTION   - This report will list the period begining and ending dates.
* TASK
* T26493 cmykleb 04/01/2002 * Change pgm to get the rpt # from the proc.
****************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>COMPANY
*
*---- DIMENSION VARIABLES
*
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST BE RUN FROM A PROC"
      GOSUB 91000
      GOTO 99999
   END
*
*---- OPEN FILES
*
   OPEN 'COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN 'CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
*
*---- GET COMPANY NUMBER
*
   CONO = ""
   CALL GET.CONO(CONO, MAT COMP.REC)
*
*---- INITIALIZATION
*
   READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = ""
      PERIOD.REC<1> = 12
   END
   NUM.PERIODS = PERIOD.REC<1>
   PG.NO=1
   ERRMSG=""
   SP2 = "  "
   SP3 = "   "
   NODATA=1
*T26493 v
*     CONAME = CO.NAME
*     REPORT.NAME = "ACCOUNTING PERIOD INFORMATION"
*     REPORT.NUMBER = "XXXX"
   CONAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
*** GET HEADING
   CALL GET.PROG.HEAD(CONO,CONAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   HEAD1 = HD1
   HEAD2 = HD2
   HEAD3 = "PERIOD NUMBER    BEGINNING DATE   ENDING DATE    QUARTER    NUMBER OF PRODUCTION WEEKS "
   HEAD4 = "-------------    --------------   -----------    -------    -------------------------- "
*
*---- GET PERIOD INFORMATION
*
   READ OPENDATES.REC FROM CONTROL, CONO:"OPENDATES" ELSE
      ERRMSG = "Cannot locate OPENDATE record in CONTROL"
      GOSUB 91000; GOTO 99999
   END
   MATREAD SALESDATES.REC FROM CONTROL,CONO:"SALESDATES" ELSE
      ERRMSG = 'CONTROL RECORD "SALESDATES" IS MISSING'
      GOSUB 91000
      GOTO 99999
   END
*
* MAIN PROCESSING
*
   PRINTER ON
   GOSUB 100  
   FOR PR = 1 TO NUM.PERIODS
      BEG.DATE = OCONV(OPENDATES.REC<PR>,"D2/")
      END.DATE = OCONV(SALESDATES.REC(PR+1)<1,1>,"D2/")
      QTR      = SALESDATES.REC(PR+1)<1,3>
      P.WEEKS  = SALESDATES.REC(PR+1)<1,4>
      P.LINE   = PR"R#8":SPACE(9):BEG.DATE"R#14":SPACE(3):END.DATE"R#11":SPACE(7):QTR"R#4":SPACE(4):P.WEEKS"R#5"
      IF LINE.CNT > 66 THEN
         GOSUB 100
      END
      PRINT P.LINE
      LINE.CNT = LINE.CNT + 1
   NEXT PR
   GOTO 99999
*
100 * PRINT HEADINGS
*
   PRINT HEAD1:SPACE(1):PG.NO"R#2"
   PRINT HEAD2
   PRINT
   PRINT HEAD3
   PRINT HEAD4
*
   LINE.CNT = 5
   PG.NO = PG.NO + 1
   RETURN
*
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER CLOSE
   PRINTER OFF
END
