*
*   > COMMON STATEMENTS
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - REMOVE.COMPANY
* BY          - rick; CBA
* DATE        - 07/10/1996
* DESCRIPTION -
*
*T20442 rick 07/10/1996 * Modify pgm to work accross accounts
*ENDDOC
**********************************************
*   CPYLIBS
*
*   FILE OPENS
*
OPEN "CONTROL" TO CONTROL ELSE 
  ERRMSG="UNABLE TO OPEN CONTROL FILE"
  GOSUB 91000
  STOP
END
OPEN "VOC" TO VOC ELSE 
  ERRMSG="UNABLE TO OPEN VOC FILE"
  GOSUB 91000
  STOP
END
OPEN "DICT","VOC" TO DICTVOC ELSE
  ERRMSG="UNABLE TO OPEN DICT VOC FILE"
  GOSUB 91000
  STOP
END
OPEN "COMPANY" TO COMPANY ELSE 
  ERRMSG="UNABLE TO OPEN COMPANY FILE"
  GOSUB 91000
  STOP
END
*
*
*   INITIALIZATION SECTION
*
*   BODY OF PROGRAM SECTION
*
GETITEMVERB = 'SSELECT REMOTE_VOC WITH F1 LIKE "F..." AND WITH F2 UNLIKE ".../..." AND WITH @ID = F2'
*
READ F1REC FROM DICTVOC,"F1" THEN
  READ F2REC FROM DICTVOC,"F2" ELSE
    ERRMSG="UNABLE TO CONTINUE PROCESS NO F1 IN PMC MODULE"
    GOSUB 91000
    STOP
  END
END ELSE
  ERRMSG="UNABLE TO CONTINUE PROCESS NO F2 IN PMC MODULE"
  GOSUB 91000
  STOP
END
READ MENUREC FROM CONTROL,"MENUS.CONTROL" THEN
  LIST.OF.ACCTS = "PMC":@VM:MENUREC<6>
END ELSE
  LIST.OF.ACCTS = "PMC"
END
*
PMC.PATH = @PATH
NUM.PARTS = DCOUNT(PMC.PATH,"/")
*
BASEPATH=''
FOR I = 1 TO NUM.PARTS
  IF I = NUM.PARTS THEN
    PMCNAME = FIELD(PMC.PATH,"/",I)
  END ELSE
    IF BASEPATH = "" THEN
      BASEPATH="/"
    END ELSE
      IF BASEPATH = "/" THEN BASEPATH = ""
      BASEPATH=BASEPATH:"/":FIELD(PMC.PATH,"/",I)
    END
  END
NEXT I
*
*  determine company number to remove
*
*
VALIDCO=0
LOOP
UNTIL VALIDCO DO
  RESP=''
  PRINT 
  PRINT
  PRINT "Enter Company number to Remove or 'X' to exit:":
  INPUT CONO
  IF CONO = 'X' THEN
    VALIDCO=1
  END ELSE
    READ REC FROM COMPANY,CONO THEN
      PRINT "Are you sure (Y/N/X)":
      INPUT RESP
      IF RESP = "Y" OR RESP = "N" OR RESP = "X" THEN
        VALIDCO=1
      END
    END ELSE
      PRINT "Not a valid company number ":CONO:" try again."
    END
  END
REPEAT
IF CONO = "X" THEN STOP
IF RESP[1,1] # "Y" THEN STOP
*
*    
*    
NUMACCTS = DCOUNT(LIST.OF.ACCTS,@VM)
*
FOR ACCTPTR = 1 TO NUMACCTS
  * build a ptr to the voc
  *
  PRINT LIST.OF.ACCTS<1,ACCTPTR>
  *
  READ XX FROM VOC,"REMOTE_VOC" THEN
    DELETE VOC,"REMOTE_VOC"
  END
  IF LIST.OF.ACCTS<1,ACCTPTR> = "PMC" THEN
    CURRBASEPATH=BASEPATH:"/":PMCNAME
    CURVOCPATH=CURRBASEPATH:"/VOC"
  END ELSE
    CURRBASEPATH = BASEPATH:"/":PMCNAME:"-":LIST.OF.ACCTS<1,ACCTPTR>
    CURVOCPATH=CURRBASEPATH:"/VOC"
  END
  SETFILEVERB = "SETFILE ":CURVOCPATH:" REMOTE_VOC OVERWRITING"
  UDTEXECUTE SETFILEVERB CAPTURING JUNK
  OPEN "REMOTE_VOC" TO REMOTE.VOC THEN
    OPEN "DICT","REMOTE_VOC" TO DICTREMOTE.VOC THEN
      READ REM.F1REC FROM DICTREMOTE.VOC,"F1" ELSE
        WRITE F1REC ON DICTREMOTE.VOC,"F1"
      END
      READ REM.F2REC FROM DICTREMOTE.VOC,"F2" ELSE
        WRITE F2REC ON DICTREMOTE.VOC,"F2"
      END
    END
  END
  *  process through the remote voc
  UDTEXECUTE GETITEMVERB CAPTURING JUNK RETURNING RESULTS
  ITMCNT = RESULTS<1,2>
  FILELIST=''
  FILECNT=0
  IF NUM(ITMCNT) THEN
    IF ITMCNT > 0 THEN
      DONE=0
      LOOP
        READNEXT ID ELSE DONE = 1
      UNTIL DONE DO
        IF FILELIST = "" THEN
          FILELIST = ID
          FILECNT=1
        END ELSE
          FILELIST<-1> = ID
          FILECNT = FILECNT + 1
        END
      REPEAT
    END
  END    
  DONE=0
  LOOP
    FILEID = FILELIST<1>
    IF FILELIST = "" THEN DONE=1
  UNTIL DONE DO
    READ XX FROM REMOTE.VOC,"REMOTE_FILE_PTR" THEN
      DELETE REMOTE.VOC,"REMOTE_FILE_PTR"
    END    
    *  now we have a file name
    PATHTOFILE = CURRBASEPATH:"/":FILEID
    VERB = "SETFILE ":PATHTOFILE:" REMOTE_FILE_PTR OVERWRITING"
    UDTEXECUTE VERB CAPTURING JUNK
    OPEN "REMOTE_FILE_PTR" TO REMOTE.FILE.PTR THEN
      PRINT "  ":FILEID
      *  now we can select the file and process the list
      VERB = "SELECT REMOTE_FILE_PTR"
      UDTEXECUTE VERB CAPTURING JUNK RETURNING RESULTS
      RESRECCNT = RESULTS<1,2>
      RECLIST=''
      RECCNT=0
      IF NUM(RESRECCNT) THEN
        IF RESRECCNT > 0 THEN
          RESDONE=0
          LOOP
            READNEXT ID ELSE RESDONE = 1
          UNTIL RESDONE DO
            IF RECLIST = "" THEN
              RECLIST = ID
              RECCNT=1
            END ELSE
              RECLIST<-1> = ID
              RECCNT = FILECNT + 1
            END
          REPEAT
        END
      END
      DONEREC=0
      RECCTR=0
      CONOA = CONO:"A"
      LOOP
        RECID = RECLIST<1>
        IF RECLIST = "" THEN DONEREC=1
      UNTIL DONEREC DO
        TTEMP = RECID[1,3]:"A"
        IF TTEMP = CONOA THEN
          * this is a record to delete
          DELETE REMOTE.FILE.PTR,RECID
          PRINT "    DELETE ":RECID
        END
        RECCTR=RECCTR+1
        IF REM(RECCTR,1000)=0 THEN PRINT "        ":RECCTR
        RECLIST=DELETE(RECLIST,1,0,0)
      REPEAT         
      *   
    END
    FILELIST = DELETE(FILELIST,1,0,0)
  REPEAT
NEXT ACCTPTR
STOP
*
*   STANDARD SUBROUTINES
*
91000 PRINT @(0,23):ERRMSG:CL:
      INPUT XX:
      PRINT @(0,23):CL:
      RETURN
99999 RETURN
   END
