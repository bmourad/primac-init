*COPY>CPYLIB>COM1
**************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SOURCE     - PMCBP
* PROGRAM    - PREFIX.MAINT
* BY         - WALID YAMOUT , C.B.A
* DATE       - 10/31/85
* DESCRIPTION-
* LAST COMPILE  - 449
*T25978 adelgado 01/30/2002 * Add the use of prompts (SF,SR,SB,ST).
*T26126 adelgado 02/26/2002 * Implement the LOCKED clause for READU.
*ENDDOC
**************************************************************************
*
*--- INSERT FILES EQUETES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
  ERRMSG = ""
*
*--- GET COMPANY
*
  MAT FILE.VARS = ""
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING'; GOSUB 91000; GOTO 999999
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING'; GOSUB 91000; GOTO 999999
  CONO = ""
  MAT COMP.REC = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 999999
*
*--- OPEN FILES
*
  OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG = 'PMC.SCREENS FILE IS MISSING'; GOSUB 91000; GOTO 999999
  OPEN '','PREFIX' TO PREFIX ELSE ERRMSG = 'PREFIX FILE IS MISSING'; GOSUB 91000; GOTO 999999
*
*--- INITIALIZATION
*
  LINE.SPACE = 1
  BEGIN.PAGE = 6
  PAGE.SIZE = 14
  LINES = 0
  LN = 1
  OLD.START = 0
*
*--- SCREEN
*
  MAT EDIT.COM.DRIVER = ''
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'PREFIX.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
*
*--- MAIN PROCESS
*
100*
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
  * T26126 v
  READU NAMES FROM PREFIX, CONO LOCKED
    ERRMSG = 'KEYWORD EXCLUSION record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 99990 
  END ELSE
    NAMES = ""
  END
  * T26126 ^
  IF NAMES<1> = "" THEN
    OPTION = "A"
    LOOP
      LN = LINES + 1 
      OLD.LINES = LINES
      GOSUB 10000
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END ELSE
    LINES = COUNT(NAMES<1>,VM) + (NAMES<1> # "")
  END
  GOSUB 80000
*
*--- ENTER OPTIONS
*
500*
  ECD.NUM = 1
  SCV.REC(ECD.NUM)<1> = ''
  ECD.ACTION=4;CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "END" OR OPTION = "E"
      RELEASE PREFIX, CONO
      GOTO 99990
    CASE OPTION = "A"
      LOOP
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 10000
      WHILE LINES > OLD.LINES DO REPEAT
      LN = LINES
    CASE OPTION = "C"
      IF LINES = 1 THEN LNO = 1 ELSE GOSUB 30000
      IF LNO THEN
        LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
        GOSUB 10100
      END
    CASE OPTION = "D"
      GOSUB 30000
      IF LNO THEN
        LN = LNO
        NAMES = DELETE(NAMES,1,LN,0)
        LINES = COUNT(NAMES<1>,VM) + (NAMES<1> # "")
        IF LN > LINES THEN LN = LINES
        IF LN < 1 THEN LN = 1
        OLD.START = 0
      END
    CASE OPTION = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
    * T25978 v
      GOSUB 80000
    CASE OPTION = 'SR'
      LN -= PAGE.SIZE
      IF LN < 1 THEN LN = 1
      GOSUB 80000
    CASE OPTION = 'ST'
      LN = 1
      GOSUB 80000
    CASE OPTION = 'SB'
      LN = LINES
      GOSUB 80000
    * T25978 ^
    CASE OPTION = "F"
      WRITE NAMES ON PREFIX, CONO
      GOTO 99990
  END CASE
  GOSUB 80000
  GOTO 500
*---------------*
*  SUBROUTINES  *
*---------------*
*
*--- LINE ENTRY
*
10000*
  GOSUB 80000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LINES = LINES + 1
10100*
  X = 5; Y = SLN; TYP = 1; MAXL = 25
  IF NAMES<1,LN> # "" THEN
    DEFAULT = NAMES<1,LN>
    O.R = "O"
  END
  CALL EDIT.SUB
  IF VALUE = 'END' THEN
    IF OPTION = 'A' THEN
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NAMES = DELETE(NAMES,1,LN,0)
    END ELSE
      P_X  = 5 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      P_X  := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:NAMES<1,LN> "L#25"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    GOTO 19999
  END
  IF VALUE = "" THEN GOTO 10100
  LOCATE VALUE IN NAMES<1>,1 SETTING FND ELSE FND = 0
  IF FND AND FND # LN THEN
    ERRMSG = "THIS WORD EXISTS ON LINE - ":FND
    GOSUB 91000; GOTO 10100
  END
  NAMES<1,LN> = VALUE
19999*
  LINES = COUNT(NAMES<1>,VM) + (NAMES<1> # "")
  RETURN
*
*--- GET LINE NUMBER
*
30000*
  GOSUB 80000
  ECD.MINV = ST.LINE
  ECD.MAXV = LST.LINE
  ECD.NUM = 4
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*--- SCROLL
*
80000*
  ST.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LST.LINE = ST.LINE + PAGE.SIZE - 1
  IF LST.LINE > LINES THEN LST.LINE = LINES
  IF ST.LINE = OLD.START THEN RETURN
  OLD.START = ST.LINE
  CNT = 1
  FOR N = ST.LINE TO LST.LINE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SSLN ; P_VALUE = N "R#3" ; P_OPT = "CL"
    P_X  := AM:5 ; P_Y := AM:SSLN ; P_VALUE := AM:NAMES<1,N> "L#25"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
  RETURN
*   
*--- ERROR ROUTINE
*   
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
99990 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
999999 
END   
