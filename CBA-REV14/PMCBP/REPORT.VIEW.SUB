   SUBROUTINE REPORT.VIEW.SUB(ERRMSG,THIS_IN,THIS_OUT,HOLD.FILE,MAT FILE.VARS)
*************************************************************
* REVISION      - [12.0]
* Copyright 1997 by Vercom Software, Inc.
* SOURCE        - CBABP
* PROGRAM       - REPORT.VIEW.SUB
* DESCRIPTION   - This program displays a report in PDF.
*
* PROGRAMS REQUIRED FOR MAIL FEATURE:
*  -GHOSTSCRIPT
*T28073 wyamout 04/20/2004 * VIEW REPORTS IN GUI
*T28191 thompson 04/07/05 * Move task 28073 to Rev12A
*************************************************************
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- Initialization
*
   ERRMSG = ""
   THIS_OUT = ""
   DOWNLOAD_PATH = "C:\PRIMAC_REPORTS\"
   DOWNLOAD_DIR = "C:\PRIMAC_REPORTS"
   CONO = THIS_IN<1>
   CO.NAME = THIS_IN<2>
   HF.NAME = THIS_IN<3>
   IN_SUBJECT = THIS_IN<4>
   IN_PORT_TYPE = THIS_IN<5>
   VIEW_FORMAT = THIS_IN<6>
*
*---- Setup system error messages
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- Open files
*
   IF FILEINFO(HOLD.FILE,0) # 1 THEN
      OPEN '','_HOLD_' TO HOLD.FILE ELSE
         ERRMSG = 'CANNOT LOCATE THE _HOLD_ FILE!'
         GOSUB 91000
         GOTO 99999
      END
   END
   IF FILEINFO(CONTROL,0) # 1 THEN
      OPEN '','CONTROL' TO CONTROL ELSE
         ERRMSG = 'CANNOT LOCATE THE CONTROL FILE!'
         GOSUB 91000
         GOTO 99999
      END
   END
   IF CO.NAME = "" THEN
      IF FILEINFO(COMPANY,0) # 1 THEN
         OPEN '','COMPANY' TO COMPANY ELSE
            ERRMSG = 'CANNOT LOCATE THE COMPANY FILE!'
            GOSUB 91000
            GOTO 99999
         END
      END
   END
*
*---- Read in hold file record
*
   READ CR FROM CONTROL,HF.NAME ELSE NULL
   DEFAULT.PTR = CR<2>
   DEFAULT.OPTION = CR<3>
   READ HOLD.REC FROM HOLD.FILE, HF.NAME ELSE HOLD.REC = ''
*
*---- Main processing
*
   IF HOLD.REC = '' THEN
      STMT = 'SETPTR ,,,,,1,BRIEF,NOMESSAGE'
      UDTEXECUTE STMT CAPTURING TEXT
      ERRMSG = 'There is no data to display!  <RETURN> to Exit.'
      GOSUB 91000
      GOTO 99999
   END
   ERRMSG = ""
   IN_PARAM = CONO
   IN_PARAM<2> = "PMCProcessStatus"
   OUT_PARAM = ""
   CALL PMC_GETREPORTID(ERRMSG,IN_PARAM,OUT_PARAM,HOLD.FILE,CONTROL)
   IF ERRMSG # "" THEN
      GOSUB 91000
      GOTO 99999
   END
   PPSR_ID = OUT_PARAM
   WRITE HOLD.REC ON HOLD.FILE, PPSR_ID
   READ REC FROM CONTROL, CONO:"EMAIL_FORMAT" ELSE REC = ''
   READ VIEW_REC FROM CONTROL, CONO:"VIEW_CONTROL" ELSE VIEW_REC = ''
   BEGIN CASE
      CASE VIEW_FORMAT = "PDF"
      CASE VIEW_FORMAT = "RTF"
      CASE VIEW_FORMAT = "SLK"
      CASE 1
         VIEW_FORMAT = REC<1>
   END CASE
   STATUS = 0
   SBVERSION = ""
   CALL TU.GET.VERSION(SBVERSION, STATUS)
* TEMP v
* THIS LOGIC IS TEMPORARY BECAUSE WINTEGRATE IS CORRUPTING PDF FILES
*   IF SBVERSION<1,1> = "" AND VIEW_FORMAT # "RTF" THEN
*      VIEW_FORMAT = "RTF"
*   END
* TEMP ^
   BEGIN CASE
      CASE VIEW_FORMAT = "RTF"
         GOSUB RTF_SUB
      CASE VIEW_FORMAT = "PDF"
         GOSUB PDF_SUB
      CASE VIEW_FORMAT = "SLK"
         GOSUB SLK_SUB
      CASE 1
         GOSUB PDF_SUB
   END CASE
   IF SBVERSION<1,1> = "" THEN
      GOSUB WINTEGRATE_SUB
   END ELSE
      GOSUB SBCLIENT_SUB
   END
   DELETE HOLD.FILE, PPSR_ID
   DELETE HOLD.FILE, VIEW_ID
   GOTO 99999
*
*--- SUBROUTINES ---*
*
RTF_SUB: 
   PCFILE = DOWNLOAD_PATH:PPSR_ID:".RTF"
   VIEW_ID = PPSR_ID:".rtf"
   APP_NAME = "WINWORD.EXE"
   IF CO.NAME = "" THEN
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         MAT COMP.REC = ""
         CO.NAME = "NOT AVAILABLE"
      END
   END
   IF IN_SUBJECT # "" THEN
      IN_SUBJECT = QUOTE(IN_SUBJECT)
   END ELSE
      IN_SUBJECT = "REPORT"
   END
   FORMATSTR = ''
   FORMATSTR<1,1> = "WIDE REPORT"
   FORMATSTR<1,2> = "FONT SIZE"
   FORMATSTR<2,2> = 9
   CALL CONVERT.TO.RTF(CO.NAME,"_HOLD_",PPSR_ID,IN_SUBJECT,FORMATSTR)
   RETURN
*
PDF_SUB: 
   PCFILE = DOWNLOAD_PATH:PPSR_ID:".PDF"
   APP_NAME = "ACRORD32.EXE"
   VIEW_ID = PPSR_ID:".pdf"
   CALL CONVERT.TO.PDF("_HOLD_",PPSR_ID,'')
   RETURN
*
SLK_SUB: 
   PCFILE = DOWNLOAD_PATH:PPSR_ID:".SLK"
   APP_NAME = "EXCEL.EXE"
   VIEW_ID = PPSR_ID:".slk"
   CALL CONVERT.TO.SLK("_HOLD_",PPSR_ID,'')
   RETURN
*
SBCLIENT_SUB: 
   SLEEP 1
   CLIENTVERSION = FIELD(FIELD(FIELD(SBVERSION<1,1,1>,"/",1),"-",3),".",1,3)
   HOSTVERSION = FIELD(SBVERSION<2,1,1>,".",1,3)
*BRM   CLIENTVERSION = FIELD(FIELD(FIELD(SBVERSION<1,1,1>,"/",1),"-",3),".",1,2)
*BRM   HOSTVERSION = FIELD(SBVERSION<2,1,1>,".",1,2)
   IF CLIENTVERSION # HOSTVERSION THEN
      TU_VERNO = "_" : CHANGE(CLIENTVERSION,".","")
   END ELSE
      TU_VERNO = ""
   END
   OSREAD HOSTFILE FROM "./_HOLD_/":VIEW_ID THEN
      SLEEP 1
*     OPTSX    = "OHB"
      VIEW_VAR = "SBCLIENT_OPTIONS"
      GOSUB GET_VIEW_CONTROL
      OPTSX    = VIEW_RESP
      DESCRIPT = "Hold File Transfer"
      STATUS  = ""
      TU_FUNC = "TU.DOWNLOAD":TU_VERNO
      CALL @TU_FUNC(HOSTFILE,PCFILE,OPTSX,DESCRIPT,STATUS)
      IF STATUS = 2 THEN
         TU_FUNC = "TU.CREATE.DIRECTORY":TU_VERNO
         STATUS = ""
         CALL @TU_FUNC(DOWNLOAD_DIR,STATUS)
         IF STATUS = 0 THEN
            STATUS  = ""
            TU_FUNC = "TU.DOWNLOAD":TU_VERNO
            CALL @TU_FUNC(HOSTFILE,PCFILE,OPTSX,DESCRIPT,STATUS)
         END ELSE
            STATUS = 2
         END
      END
   END ELSE
      STATUS = VIEW_ID : " FILE MISSING"
   END
   IF STATUS = 0 THEN
      SLEEP 2
      APP_OPTIONS = "5"
      APP_STATUS = ""
      APP_NAME := " " : PCFILE
      TU_FUNC = "TU.LAUNCH.APP":TU_VERNO
      CALL @TU_FUNC(APP_NAME, APP_OPTIONS, APP_STATUS)
      SLEEP 2
      IF IN_PORT_TYPE = "termulator" THEN
         TU_FUNC = "TU.CHECK.APP":TU_VERNO
         APP_STATUS = 0
         FOR II = 1 TO 10
            SLEEP 1
            CALL @TU_FUNC (APP_NAME, APP_STATUS)
            IF APP_STATUS # 0 THEN EXIT
         NEXT II
      END ELSE
         VIEW_VAR = "DELETE_PC_FILE"
         GOSUB GET_VIEW_CONTROL
         IF VIEW_RESP # "NO" THEN
            ERRMSG = "HIT <RETURN> TO DELETE THE PC FILE"
            GOSUB 91000
            TU_FUNC = "TU.DELETE.FILE":TU_VERNO
            CALL @TU_FUNC(PCFILE, APP_STATUS)
         END
      END
   END ELSE
      ERRMSG = "FILE TRANSFER STATUS = " : STATUS
      GOSUB 91000
   END
   RETURN
*
WINTEGRATE_SUB: 
   SLEEP 1
   FILE = "_HOLD_"
   ITEMS = VIEW_ID
   FIELDS = "*"
   OPTS = ""
   OPTS = "Raw"
   OPTS<3> = "capture"
   OPTS<4> = "yes"
   OPTS<6> = "All"
   OPTS<7> = "\255,\r\n,\254,\r\n"
   STATUS = ""
   CALL WIN.IMPORT(PCFILE,FILE,ITEMS,FIELDS,OPTS,STATUS)
   IF STATUS = "Error" THEN
      CALL WIN.PCMKDIR(DOWNLOAD_DIR)
      STATUS  = ""
      CALL WIN.IMPORT(PCFILE,FILE,ITEMS,FIELDS,OPTS,STATUS)
   END
   IF STATUS = "OK" THEN
      APP_OPTIONS = PCFILE
      CALL WIN.PCRUN(APP_NAME, APP_OPTIONS)
      SLEEP 2
      VIEW_VAR = "DELETE_PC_FILE"
      GOSUB GET_VIEW_CONTROL
      IF VIEW_RESP # "NO" THEN
         ERRMSG = "HIT <RETURN> TO DELETE THE PC FILE"
         GOSUB 91000
         CALL WIN.PCDELETE(PCFILE)
      END
   END ELSE
      ERRMSG = "FILE TRANSFER STATUS = " : STATUS
      GOSUB 91000
   END
   RETURN
*
GET_VIEW_CONTROL: 
   VIEW_RESP = ""
   FOR II = 1 TO DCOUNT(VIEW_REC,AM)
      IF VIEW_VAR:"=" = VIEW_REC<II>[1,LEN(VIEW_VAR)]:"=" THEN
         VIEW_RESP = FIELD(VIEW_REC<II>,"=",2)
         EXIT
      END
   NEXT II
   BEGIN CASE
      CASE VIEW_RESP # ""
      CASE VIEW_VAR = "FORMAT"
         VIEW_RESP = "RTF"
      CASE VIEW_VAR = "SBCLIENT_OPTIONS"
         VIEW_RESP = "OHB"
      CASE 1
   END CASE
   RETURN
*
*---- Calls for syscom
*
91000* 
   ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000* 
   ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
*
99999 *
*
*   DELETE HOLD.FILE, HF.NAME
*   DELETE CONTROL, HF.NAME
   STMT = 'SETPTR ,,,,,1,BRIEF,DEST ':DEFAULT.PTR:',NOMESSAGE'
   UDTEXECUTE STMT CAPTURING TEXT
   RETURN
END
