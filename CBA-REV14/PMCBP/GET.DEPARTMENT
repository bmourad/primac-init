  SUBROUTINE GET.DEPARTMENT (CONO,DX,DY,DDX,DEPT,PDEPT,FIRST.LINE,LINE.COUNT,MAT DIV.REC,MAT DEPT.REC,DEPARTMENT,STATUS)
*COPY>CPYLIB>COM3
***************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - GET.DEPARTMENT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 10/08/85
*
* REVISION - A.1.0
*
* DESCRIPTION
*
* This subroutine will prompt for department at the coordinates specified.
* Full cross-referencing capability is provided.
*
* STATUS is returned as follows:
*
*     00 = valid division, screen undisturbed
*     01 = valid division, re-display screen
*     10 = invalid division, screen undisturbed
*     11 = invalid division, re-display screen
*
*T25978 adelgado 01/31/2002 * Add the use of prompts (S,SR,SB,ST).
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>EDIT.COM
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- INITIALIZATION
*
  STATUS = "00"
  UNKNOWN = STR("?",10)
*
*---- MAIN PROCESSING
*
100*
  X = DX; Y = DY; TYP = 1; MAXL = 2
  O.R = "O"; DEFAULT = PDEPT
  CALL EDIT.SUB
  IF VALUE = "E" THEN VALUE = "END"
*     IF PDEPT # "" THEN PRINT @(0,23):CL:"DEFAULT- ":PDEPT:
*     PRINT @(DX,DY):"##":@(DX,DY):
*     INPUT VALUE,2  _:
*     IF PDEPT # "" THEN PRINT @(0,23):CL:
*    IF VALUE = "^" THEN VALUE = "END"
*     IF VALUE = CHAR(27) OR VALUE = "^" OR VALUE = "E" THEN VALUE = "END"
  IF NUM(VALUE) AND VALUE = 0 THEN
    DEPT = "00"
  END ELSE
    DEPT = VALUE
  END
  BEGIN CASE
    CASE DEPT = "" AND PDEPT # ""
      DEPT = PDEPT
    CASE DEPT = "" OR DEPT = SPACE(LEN(DEPT))
      IF DIV.DEPT = "" THEN GOTO 100
      S.LINES = COUNT(DIV.DEPT,VM) + 1
      S.NO = ''
      S.DESC = ''
      II = 0
      FOR I = 1 TO S.LINES
        IF LEN(DIV.DEPT<1,I>) = 2 THEN
          II = II + 1
          S.NO<II> = DIV.DEPT<1,I>
          MATREAD DEPT.REC FROM DEPARTMENT, CONO:S.NO<II> ELSE DEPT.DESC = UNKNOWN
          S.DESC<II> = DEPT.DESC
        END
      NEXT I
      IF II < 1 THEN GOTO 100
      S.LINES = II
      SLN = 1
      CURR.LINE = ""
      GOSUB 55000
      GOSUB 50000
  END CASE
  IF DEPT # "" AND DEPT # "END" AND DEPT # "00" THEN
    LOCATE DEPT IN DIV.DEPT<1>,1 SETTING FND ELSE
      STATUS = "1":STATUS[2,1]
      ERRMSG = "INVALID DEPARTMENT FOR SPECIFIED DIVISION"
      GOSUB 90000
      GOTO 199
    END
    MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE
      STATUS = "1":STATUS[2,1]
      ERRMSG = "INVALID DEPARTMENT - ":DEPT
      GOSUB 90000
      GOTO 199
    END
  END
  IF DEPT = "" THEN STATUS = "1":STATUS[2,1]
199 RETURN
*
*---- GET OPERATOR REPLY
*
50000*
  X = 0; Y = 23; TYP = 1; MAXL = 3; O.R = "O"
  * T25978 v
  * PMSG = "Enter selection or (S)croll"
  PMSG = "Enter selection or (S,SR,SB,ST) to Scroll"
  * T25978 ^
  CALL EDIT.SUB
*     PRINT @(0,23):CL:PMSG:" ###":@(LEN(PMSG)+1,23):
*     INPUT VALUE,3  _:
  OPTION = VALUE
  BEGIN CASE
    CASE OPTION = "END" OR OPTION = ""
      DEPT = ''
      S.DESC = ''
    CASE NUM(OPTION)
      IF OPTION < START.LINE OR OPTION > LAST.LINE THEN
        ERRMSG = "** OUT OF RANGE **"
        GOSUB 90000
        GOTO 50000
      END ELSE
        DEPT = S.NO<OPTION>
        S.DESC = ''
      END
    CASE OPTION = "S"
      SLN = SLN + LINE.COUNT
      IF SLN > S.LINES THEN SLN = 1
      GOSUB 55000
      GOTO 50000
    * T25978 v
    CASE OPTION = 'SR'
      SLN -= LINE.COUNT
      IF SLN < 1 THEN SLN = 1
      GOSUB 55000
      GOSUB 50000
    CASE OPTION = 'ST'
      SLN = 1
      GOSUB 55000
      GOSUB 50000
    CASE OPTION = 'SB'
      SLN = S.LINES
      GOSUB 55000
      GOSUB 50000
    * T25978 ^
  END CASE
*     PRINT @(0,23):CL:
  RETURN
*
*---- DISPLAY MULTI-LINE AREA
*
55000*
  STATUS = STATUS[1,1]:"1"
  START.LINE = 1 + INT((SLN-1)/LINE.COUNT) * LINE.COUNT
  IF START.LINE = CURR.LINE THEN RETURN
  CURR.LINE = START.LINE
  LAST.LINE = START.LINE + LINE.COUNT - 1
  IF LAST.LINE > S.LINES THEN LAST.LINE = S.LINES
  FOR N = START.LINE TO START.LINE+LINE.COUNT-1
    YY = FIRST.LINE + MOD(N-1,LINE.COUNT)
    IF N > LAST.LINE THEN
      P_X  = 0 ; P_Y = YY ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END ELSE
      P_X  = 0 ; P_Y = YY ; P_VALUE = N "R#3" ; P_OPT = "CL"
      P_X  := AM:DX ; P_Y := AM:YY ; P_VALUE := AM:S.NO<N> "L#2"
      P_X  := AM:DDX ; P_Y := AM:YY ; P_VALUE := AM:S.DESC<N> "L#30"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
  NEXT N
  RETURN
*
*--- CALLS FOR SYSCOM
*
90000*
91000 *
  ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
  ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
  ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
END
