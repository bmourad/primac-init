*COPY>CPYLIB>COM1
***************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - SEC.MENU.MAINT
*
* BY       - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 08/02/84
*
* REVISION - A.1.0
*
* DESCRIPTION
*
* This program is used to maintain valid users of the system. Security
* is provided at both the company and menu option levels.
*
***************************************************************************
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SECURITY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- DEFINE VARIABLES
*
      DIM MENU(40)
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         PRINT "CANNOT OPEN COMPANY FILE"
         STOP
      END
      OPEN "","PMC.SCREENS" TO M.SCREENS ELSE
         PRINT "CANNOT OPEN PMC.SCREENS FILE"
         STOP
      END
      OPEN "","SECURITY" TO SECURITY ELSE
         PRINT "CANNOT OPEN SECURITY FILE"
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         PRINT "CANNOT OPEN CONTROL FILE"
         STOP
      END
      OPEN "","EASY.MENUS" TO EASY.MENUS ELSE
         PRINT "CANNOT OPEN EASY.MENUS FILE"
         STOP
      END
*
*---- INITIALIZATION
*
      MAT SEC.REC = ""
      MAT SCV.REC = ""
      READ SECURITY.PW FROM CONTROL, "SECURITY.PW" ELSE
         SECURITY.PW = "PW"
      END
      MULTI.CO = 0
      READV CONO FROM CONTROL, "MASTER",1 ELSE MULTI.CO = 1
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.NAME = "SEC.MENU.MAINT"
      ECD.ACTION=1;CALL SCRN.EDIT
      ECD.SCRN.NO = 1
      ECD.ACTION=2;CALL SCRN.EDIT
      SCV.REC(8)<1> = DATE()
      ECD.NUM = 8
      ECD.ACTION=5;CALL SCRN.EDIT
      CURR.REF.NO = ""
      LINE.COUNT = 12
      GOSUB 2100
      IF ECD.RET.VALUE = "END" THEN
         PRINT @(-1):
         STOP
      END
      CURR.PW = FIELD(ECD.RET.VALUE,"/",1)
      NEW.PW = FIELD(ECD.RET.VALUE,"/",2)
      IF CURR.PW # SECURITY.PW THEN
         ERRMSG = "** Illegal Password **"
         GOSUB 90000
         STOP
      END
      GOTO 110
*
*---- MAIN PROCESSING
*
100*
      RELEASE
      MAT SEC.REC = ""
      MAT SCV.REC = ""
      ECD.ACTION=6;CALL SCRN.EDIT
110*
      SCV.REC(8)<1> = DATE()
      ECD.NUM = 8
      ECD.ACTION=5;CALL SCRN.EDIT
      GOSUB 1000
      IF ECD.RET.VALUE = "END" THEN
         PRINT @(-1):
         STOP
      END
      GOSUB 1010
      IF ECD.RET.VALUE = "END" THEN
         IF MULTI.CO THEN
            GOTO 100
         END ELSE
            PRINT @(-1):
            STOP
         END
      END
      SEC.KEY = CONO:USER.ID
      NEW.REC = 0
      MATREADU SEC.REC FROM SECURITY, SEC.KEY ELSE
         MAT SEC.REC = ""
         NEW.REC = 1
      END
      IF NOT(NEW.REC) THEN
         GOSUB 9000
         ECD.ACTION=3;CALL SCRN.EDIT
         CURR.REF.NO = 1
         GOTO 500
      END
120*
      ECD.NUM = 16
      ECD.ACTION=4;CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "A"
            FOR N = 1 TO 3
               ON N GOSUB 1100,1150,1200
               IF ECD.RET.VALUE = "END" THEN GOTO 100
            NEXT N
            REF.CNT = 0
            IF SEC.MENU.LEVEL # 3 THEN
               CURR.REF.NO = 1
               DONE = 0
               FOR REF.NO = 1 TO 999 UNTIL DONE
                  GOSUB 10000
                  GOSUB 6000
                  IF ECD.RET.VALUE = "END" THEN
                     SEC.MENU.NAME = DELETE(SEC.MENU.NAME,1,REF.NO,0)
                     SEC.MENU.PROC = DELETE(SEC.MENU.PROC,1,REF.NO,0)
                     SEC.MENU.DESC = DELETE(SEC.MENU.DESC,1,REF.NO,0)
                     SCV.REC(13) = DELETE(SCV.REC(13),1,REF.NO,0)
                     SCV.REC(5) = DELETE(SCV.REC(5),1,REF.NO,0)
                     REF.CNT = REF.NO - 1
                     DONE = 1
                  END ELSE
                     REF.CNT = REF.NO
                  END
               NEXT N
               REF.NO = REF.CNT
               CURR.REF.NO = ""
               GOSUB 10000
            END
         CASE OPTION = "D"
            IF MULTI.CO THEN
               ECD.NUM = 17
               ECD.O.R = "O"; ECD.DEFAULT = CONO
               ECD.ACTION=4;CALL SCRN.EDIT
               IF ECD.RET.VALUE = "END" THEN GOTO 100
               DUP.CONO = ECD.RET.VALUE
            END ELSE
               DUP.CONO = CONO
            END
            ECD.NUM = 18
            ECD.O.R = "O"; ECD.DEFAULT = USER.ID
            ECD.ACTION=4;CALL SCRN.EDIT
            IF ECD.RET.VALUE = "END" THEN GOTO 100
            DUP.USER.ID = ECD.RET.VALUE
            MATREAD SEC.REC FROM SECURITY, DUP.CONO:DUP.USER.ID ELSE
               ERRMSG = "User not on file"
               GOSUB 90000
               GOTO 120
            END
            GOSUB 9000
            ECD.ACTION=3;CALL SCRN.EDIT
            CURR.REF.NO = 1
         CASE 1
            GOTO 100
      END CASE
*
*---- GET OPERATOR REQUEST
*
500*
      GOSUB 5000
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE NUM(ACTION) AND ACTION >= 1 AND ACTION <= 3
            N = ACTION
            ON N GOSUB 1100,1150,1200
         CASE ACTION = "A"
            DONE = 0
            FOR REF.NO = REF.CNT + 1 TO 999 UNTIL DONE
               GOSUB 10000
               GOSUB 6000
               IF ECD.RET.VALUE = "END" THEN
                  SEC.MENU.NAME = DELETE(SEC.MENU.NAME,1,REF.NO,0)
                  SEC.MENU.PROC = DELETE(SEC.MENU.PROC,1,REF.NO,0)
                  SEC.MENU.DESC = DELETE(SEC.MENU.DESC,1,REF.NO,0)
                  SCV.REC(13) = DELETE(SCV.REC(13),1,REF.NO,0)
                  SCV.REC(5) = DELETE(SCV.REC(5),1,REF.NO,0)
                  REF.CNT = REF.NO - 1
                  DONE = 1
               END ELSE
                  REF.CNT = REF.NO
               END
            NEXT REF.NO
            REF.NO = REF.CNT
            CURR.REF.NO = ""
            GOSUB 10000
         CASE ACTION = "C" AND REF.CNT > 0
            ECD.NUM = 9
            SCV.REC(9)<1> = ""
            ECD.MINV = CURR.REF.NO; ECD.MAXV = CURR.REF.NO + LINE.COUNT - 1
            IF ECD.MAXV > REF.CNT THEN ECD.MAXV = REF.CNT
            ECD.ACTION=4;CALL SCRN.EDIT
            IF NUM(ECD.RET.VALUE) AND ECD.RET.VALUE # "" THEN
               REF.NO = ECD.RET.VALUE
               GOSUB 6000
            END
         CASE ACTION = "D" AND REF.CNT > 0
            ECD.NUM = 9
            SCV.REC(9)<1> = ""
            ECD.MINV = CURR.REF.NO; ECD.MAXV = CURR.REF.NO + LINE.COUNT - 1
            IF ECD.MAXV > REF.CNT THEN ECD.MAXV = REF.CNT
            ECD.ACTION=4;CALL SCRN.EDIT
            IF NUM(ECD.RET.VALUE) AND ECD.RET.VALUE # "" THEN
               REF.NO = ECD.RET.VALUE
               SEC.MENU.NAME = DELETE(SEC.MENU.NAME,1,REF.NO,0)
               SEC.MENU.PROC = DELETE(SEC.MENU.PROC,1,REF.NO,0)
               SEC.MENU.DESC = DELETE(SEC.MENU.DESC,1,REF.NO,0)
               SCV.REC(13) = DELETE(SCV.REC(13),1,REF.NO,0)
               SCV.REC(5) = DELETE(SCV.REC(5),1,REF.NO,0)
               REF.CNT = REF.CNT - 1
               IF REF.NO > 1 THEN REF.NO = REF.NO - 1
               CURR.REF.NO = ""
               GOSUB 10000
            END
         CASE ACTION = "E" OR ACTION = "END"
            GOTO 100
         CASE ACTION = "F"
            SEC.PASSWORD = SCV.REC(2)<1>
            SEC.MENU.LEVEL = SCV.REC(12)<1>
            SEC.MENU.FLAG = SCV.REC(3)<1>
            MATWRITE SEC.REC ON SECURITY, SEC.KEY
            IF NEW.PW # "" AND NEW.PW # CURR.PW THEN
               WRITE NEW.PW ON CONTROL, "SECURITY.PW"
            END
            GOTO 100
         CASE ACTION = "I" AND REF.CNT > 0
            ECD.NUM = 9
            ECD.MINV = CURR.REF.NO; ECD.MAXV = CURR.REF.NO + LINE.COUNT - 1
            IF ECD.MAXV > REF.CNT THEN ECD.MAXV = REF.CNT
            ECD.ACTION=4;CALL SCRN.EDIT
            IF NUM(ECD.RET.VALUE) AND ECD.RET.VALUE # "" THEN
               REF.NO = ECD.RET.VALUE
               SEC.MENU.NAME = INSERT(SEC.MENU.NAME,1,REF.NO,0,"")
               SEC.MENU.PROC = INSERT(SEC.MENU.PROC,1,REF.NO,0,"")
               SEC.MENU.DESC = INSERT(SEC.MENU.DESC,1,REF.NO,0,"")
               SCV.REC(13) = INSERT(SCV.REC(13),1,REF.NO,0,"")
               SCV.REC(5) = INSERT(SCV.REC(5),1,REF.NO,0,"")
               REF.CNT = REF.CNT + 1
               CURR.REF.NO = ""
               GOSUB 10000
               GOSUB 6000
               IF ECD.RET.VALUE = "END" THEN
                  SEC.MENU.NAME = DELETE(SEC.MENU.NAME,1,REF.NO,0)
                  SEC.MENU.PROC = DELETE(SEC.MENU.PROC,1,REF.NO,0)
                  SEC.MENU.DESC = DELETE(SEC.MENU.DESC,1,REF.NO,0)
                  SCV.REC(13) = DELETE(SCV.REC(13),1,REF.NO,0)
                  SCV.REC(5) = DELETE(SCV.REC(5),1,REF.NO,0)
                  REF.CNT = REF.CNT - 1
                  CURR.REF.NO = ""
                  GOSUB 10000
               END
            END
         CASE ACTION = "P"
            ECD.NUM = 10
            SCV.REC(10)<1> = ""
            ECD.ACTION=4;CALL SCRN.EDIT
            IF ECD.RET.VALUE = "Y" THEN
               DELETE SECURITY, SEC.KEY
               GOTO 100
            END
         CASE ACTION = "S" AND REF.CNT > 0
            REF.NO = CURR.REF.NO + LINE.COUNT
            IF REF.NO > REF.CNT THEN REF.NO = 1
            GOSUB 10000
      END CASE
      GOTO 500
*
*---- GET COMPANY ID
*
1000*
      IF MULTI.CO THEN
         ECD.NUM = 14
         ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE = "END" THEN RETURN
         CONO = ECD.RET.VALUE
      END ELSE
         ECD.RET.VALUE = CONO
         SCV.REC(14)<1> = CONO
         ECD.NUM = 14
         ECD.ACTION=5;CALL SCRN.EDIT
      END
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         ERRMSG = "Company not on file"
         GOSUB 90000
         GOTO 1000
      END
      SCV.REC(15)<1> = CO.NAME
      ECD.NUM = 15
      ECD.ACTION=5;CALL SCRN.EDIT
      RETURN
*
*---- GET USER ID
*
1010*
      ECD.NUM = 1
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      USER.ID = ECD.RET.VALUE
      RETURN
*
*---- GET PASSWORD
*
1100*
      ECD.NUM = 2
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      SEC.PASSWORD = ECD.RET.VALUE
      RETURN
1150*
      ECD.NUM = 12
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      SEC.MENU.LEVEL = ECD.RET.VALUE
      RETURN
*
*---- GET ALLOW/PROTECT OPTION
*
1200*
      ECD.NUM = 3
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      SEC.MENU.FLAG = ECD.RET.VALUE
      RETURN
*
*---- GET PASSWORD
*
2100*
      ECD.NUM = 11
      ECD.ACTION=4;CALL SCRN.EDIT
      RETURN
*
*---- GET OPERATOR REQUEST
*
5000*
      ECD.NUM = 6
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      RETURN
*
*---- GET MENU NAME
*
6000*
      ECD.NUM = 13
      ECD.SUB.NUM = REF.NO
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      MATREAD MENU FROM EASY.MENUS, ECD.RET.VALUE ELSE
         ERRMSG = "CANNOT LOCATE MENU - ":ECD.RET.VALUE
         GOSUB 90000
         GOTO 6000
      END
      MENU.NAME = ECD.RET.VALUE
      SEC.MENU.NAME<1,REF.NO> = ECD.RET.VALUE
*
*---- GET PROCEDURE NAME
*
6010*
      ECD.NUM = 5
      ECD.SUB.NUM = REF.NO
      ECD.DEFAULT = SCV.REC(13)<1,REF.NO>
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = "END" THEN RETURN
      IF ECD.RET.VALUE = MENU.NAME THEN
         SEC.MENU.DESC<1,REF.NO> = MENU(1)<1,3>
      END ELSE
         PROC.ERR = 1
         FOR MP = 2 TO 40 WHILE PROC.ERR
            IF MENU(MP) # "" THEN
               IF MENU(MP)<1,1> = ECD.RET.VALUE THEN
                  PROC.ERR = 0
                  SEC.MENU.DESC<1,REF.NO> = MENU(MP)<1,3>
               END
            END
         NEXT MP
         IF PROC.ERR THEN
            ERRMSG = "CANNOT LOCATE ENTRY ON SPECIFIED MENU"
            GOSUB 90000
            GOTO 6010
         END
      END
      SEC.MENU.PROC<1,REF.NO> = ECD.RET.VALUE
      RETURN
*
*---- SET SCREEN VALUES
*
9000*
      SCV.REC(2)<1> = SEC.PASSWORD
      SCV.REC(12)<1> = SEC.MENU.LEVEL
      SCV.REC(3)<1> = SEC.MENU.FLAG
      REF.CNT = COUNT(SEC.MENU.NAME,VM) + (SEC.MENU.NAME # "")
      FOR N = 1 TO REF.CNT
         SCV.REC(13)<1,N> = SEC.MENU.NAME<1,N>
         SCV.REC(5)<1,N> = SEC.MENU.PROC<1,N>
      NEXT N
      RETURN
*
*---- DISPLAY MULTI-LINE FIELDS
*
10000*
      START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      ECD.NUM = 4
      ECD.SUB.NUM = START.REF.NO
      ECD.ACTION=7;CALL SCRN.EDIT
      ECD.NUM = 13
      ECD.SUB.NUM = START.REF.NO
      ECD.ACTION=7;CALL SCRN.EDIT
      ECD.NUM = 5
      ECD.SUB.NUM = START.REF.NO
      ECD.ACTION=7;CALL SCRN.EDIT
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF PROGRAM
*
99999*
      STOP
   END
