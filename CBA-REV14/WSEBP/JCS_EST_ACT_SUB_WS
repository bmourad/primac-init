   SUBROUTINE JCS_EST_ACT_SUB_WS(CONO,JOBNO)
*
* THIS SUBROUTINE WILL REBUILD THE ESTIMATE TO ACTUAL RECORD
* FOR A SPECIFIC JOB
*
*  CALLED BY EST.BOOK.SUB, JOB.BOOK.SUB
*
$INCLUDE JCS.CPYLIB JOB
$INCLUDE JCS.CPYLIB JOB.TIME
$INCLUDE JCS.CPYLIB JOB.MATL
$INCLUDE JCS.CPYLIB JOB.OSP
$INCLUDE JCS.CPYLIB JOB.SHIP
$INCLUDE JCS.CPYLIB JOB.MISC
$INCLUDE JES.CPYLIB EST.JOB.ACT
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB CATEGORY
$INCLUDE CPYLIB FILE.VARS
$INCLUDE CPYLIB CHAR
   OPEN '','JOB' TO JOB ELSE RETURN
   OPEN '','JOB.TIME' TO JOB.TIME ELSE RETURN
   OPEN '','JOB.MATL' TO JOB.MATL ELSE RETURN
   OPEN '','JOB.OSP' TO JOB.OSP ELSE RETURN
   OPEN '','JOB.SHIP' TO JOB.SHIP ELSE RETURN
   OPEN '','JOB.MISC' TO JOB.MISC ELSE RETURN
   OPEN '','ESTIMATE.JOB' TO ESTIMATE.JOB ELSE RETURN
   OPEN '','EST.JOB.ACT' TO EST.JOB.ACT ELSE RETURN
   OPEN '','PB.JOB.TIME' TO PB.JOB.TIME ELSE RETURN
   OPEN '','PB.JOB.MATL' TO PB.JOB.MATL ELSE RETURN
   OPEN '','PB.EST.JOB.ACT' TO PB.EST.JOB.ACT ELSE RETURN
   OPEN '','INVENTORY' TO INVENTORY ELSE RETURN
   OPEN '','CATEGORY' TO CATEGORY ELSE RETURN
*
* PROCESS JOB
*
   TODAY = DATE()
   ID = CONO:JOBNO
   MATREAD JOB.REC FROM JOB, ID THEN
      MATREADU ESTJA.REC FROM EST.JOB.ACT, ID ELSE MAT ESTJA.REC = ""
      MATREAD ESTJA.REC FROM ESTIMATE.JOB, ID THEN
         GOSUB 2000
         GOSUB 3000
         GOSUB 4000
         GOSUB 5000
         GOSUB 6000
         MATWRITE ESTJA.REC ON EST.JOB.ACT, ID
         MATWRITE ESTJA.REC ON PB.EST.JOB.ACT, ID
      END ELSE
         DELETE EST.JOB.ACT, ID
         DELETE PB.EST.JOB.ACT, ID
      END
   END
   GOTO 99999
*
* LABOR
*
2000 *
   LCNT = DCOUNT(JOB.LB.SEQ,VM)
   FOR L = 1 TO LCNT
      JLB.DEPT = JOB.LB.DEPT<1,L>
      JLB.CCTR = JOB.LB.CCTR<1,L>
      FOR S = 1 TO JOB.LB.SEQ<1,L>
         JLB.ID = ID:"!":JLB.DEPT:"!":JLB.CCTR:"!":S
         MATREADU JLB.REC FROM JOB.TIME, JLB.ID THEN
            PTR = 1
            MATCHED = 0
            LOOP
               LOCATE JLB.DEPT IN ESTJA.LB.DEPT<1>,PTR BY "AR" SETTING D THEN
                  BEGIN CASE
                  CASE JLB.CCTR = ESTJA.LB.CCTR<1,D>
                     BEGIN CASE
                     CASE JLB.OPER = ESTJA.LB.OPER<1,D>
                        PTR = 0
                        MATCHED = 1
                     CASE JLB.OPER < ESTJA.LB.OPER<1,D>
                        PTR = 0
                     CASE 1
                        PTR = D + 1
                     END CASE
                  CASE JLB.CCTR < ESTJA.LB.CCTR<1,D>
                     PTR = 0
                  CASE 1
                     PTR = D + 1
                  END CASE
               END ELSE
                  PTR = 0
               END
            WHILE PTR DO REPEAT
            IF MATCHED = 0 THEN
               ESTJA.LB.DEPT = INSERT(ESTJA.LB.DEPT,1,D,0,JLB.DEPT)
               ESTJA.LB.CCTR = INSERT(ESTJA.LB.CCTR,1,D,0,JLB.CCTR)
               ESTJA.LB.OPER = INSERT(ESTJA.LB.OPER,1,D,0,JLB.OPER)
               ESTJA.LB.HRS = INSERT(ESTJA.LB.HRS,1,D,0,"0")
               ESTJA.LB.QTY = INSERT(ESTJA.LB.QTY,1,D,0,"0")
               ESTJA.LB.DCOST = INSERT(ESTJA.LB.DCOST,1,D,0,"")
               ESTJA.LB.COST = INSERT(ESTJA.LB.COST,1,D,0,"0")
               ESTJA.LB.SALE = INSERT(ESTJA.LB.SALE,1,D,0,"0")
               ESTJA.LB.ACT.HRS = INSERT(ESTJA.LB.ACT.HRS,1,D,0,"0")
               ESTJA.LB.ACT.QTY = INSERT(ESTJA.LB.ACT.QTY,1,D,0,"0")
               ESTJA.LB.ACT.COST = INSERT(ESTJA.LB.ACT.COST,1,D,0,"0")
               ESTJA.LB.ACT.SALE = INSERT(ESTJA.LB.ACT.SALE,1,D,0,"0")
            END
            ESTJA.LB.ACT.HRS<1,D> = ESTJA.LB.ACT.HRS<1,D> + JLB.HRS
            ESTJA.LB.ACT.QTY<1,D> = ESTJA.LB.ACT.QTY<1,D> + JLB.IMP
            ESTJA.LB.ACT.COST<1,D> = ESTJA.LB.ACT.COST<1,D> + JLB.COST
            ESTJA.LB.ACT.SALE<1,D> = ESTJA.LB.ACT.SALE<1,D>+ JLB.SALE
            IF JLB.EST.UPD = "" THEN
               JLB.EST.UPD = TODAY
               MATWRITE JLB.REC ON JOB.TIME, JLB.ID
               MATWRITE JLB.REC ON PB.JOB.TIME, JLB.ID
            END
         END
         RELEASE JOB.TIME, JLB.ID
      NEXT S
   NEXT L
   RETURN
*
* MATL
*
3000 *
   LCNT = DCOUNT(JOB.MT.SEQ,VM)
   FOR L = 1 TO LCNT
      JMT.DEPT = JOB.MT.DEPT<1,L>
      JMT.CCTR = JOB.MT.CCTR<1,L>
      JMT.PROD = JOB.MT.PROD<1,L>
      JMT.WHSE = JOB.MT.WHSE<1,L>
      FOR S = 1 TO JOB.MT.SEQ<1,L>
         JMT.ID = ID:"!":JMT.DEPT:"!":JMT.CCTR:"!":JMT.PROD:"!":JMT.WHSE:"!":S
         MATREADU JMT.REC FROM JOB.MATL, JMT.ID THEN
            PTR = 1
            MATCHED = 0
            LOOP
               LOCATE JMT.DEPT IN ESTJA.MT.DEPT<1>,PTR BY "AR" SETTING D THEN
                  BEGIN CASE
                  CASE JMT.CCTR = ESTJA.MT.CCTR<1,D>
                     BEGIN CASE
                     CASE JMT.PROD = ESTJA.MT.PROD<1,D>
                        PTR = 0
                        MATCHED = 1
                     CASE JMT.PROD < ESTJA.MT.PROD<1,D>
                        PTR = 0
                     CASE 1
                        PTR = D + 1
                     END CASE
                  CASE JMT.CCTR < ESTJA.MT.CCTR<1,D>
                     PTR = 0
                  CASE 1
                     PTR = D + 1
                  END CASE
               END ELSE
                  PTR = 0
               END
            WHILE PTR DO REPEAT
            IF MATCHED = 0 THEN
               ESTJA.MT.DEPT = INSERT(ESTJA.MT.DEPT,1,D,0,JMT.DEPT)
               ESTJA.MT.CCTR = INSERT(ESTJA.MT.CCTR,1,D,0,JMT.CCTR)
               ESTJA.MT.OPER = INSERT(ESTJA.MT.OPER,1,D,0,JMT.PROD)
               ESTJA.MT.PROD = INSERT(ESTJA.MT.PROD,1,D,0,JMT.PROD)
               ESTJA.MT.DESC = INSERT(ESTJA.MT.DESC,1,D,0,"")
               ESTJA.MT.QTY = INSERT(ESTJA.MT.QTY,1,D,0,"0")
               MATREAD INV.REC FROM INVENTORY, CONO:JMT.PROD THEN
                  ESTJA.MT.TYPE = INSERT(ESTJA.MT.TYPE,1,D,0,INV.UNIT<1,3>)
                  ESTJA.MT.PLINE = INSERT(ESTJA.MT.PLINE,1,D,0,INV.LINE)
                  ESTJA.MT.MLINE = INSERT(ESTJA.MT.MLINE,1,D,0,INV.M.LINE)
                  MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE THEN
                     BEGIN CASE
                       CASE CATG.TYPE = "L" OR CATG.TYPE = "LR" OR CATG.TYPE = "PC"
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MR")
                       CASE CATG.TYPE = "S"
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MS")
                       CASE CATG.TYPE = "I"
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MI")
                       CASE CATG.TYPE = "F"
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MF")
                       CASE CATG.TYPE = "O"
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MO")
                       CASE CATG.TYPE = "P"
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"MP")
                       CASE 1
                         ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"")
                     END CASE
                  END ELSE
                     ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"")
                  END
               END ELSE
                  ESTJA.MT.TYPE = INSERT(ESTJA.MT.TYPE,1,D,0,"0")
                  ESTJA.MT.PLINE = INSERT(ESTJA.MT.PLINE,1,D,0,"")
                  ESTJA.MT.MLINE = INSERT(ESTJA.MT.MLINE,1,D,0,"")
                  ESTJA.MT.PROD.CLS = INSERT(ESTJA.MT.PROD.CLS,1,D,0,"")
               END
               ESTJA.MT.DCOST = INSERT(ESTJA.MT.DCOST,1,D,0,"")
               ESTJA.MT.COST = INSERT(ESTJA.MT.COST,1,D,0,"0")
               ESTJA.MT.SALE = INSERT(ESTJA.MT.SALE,1,D,0,"0")
               ESTJA.MT.ACT.QTY = INSERT(ESTJA.MT.ACT.QTY,1,D,0,"0")
               ESTJA.MT.ACT.COST = INSERT(ESTJA.MT.ACT.COST,1,D,0,"0")
               ESTJA.MT.ACT.SALE = INSERT(ESTJA.MT.ACT.SALE,1,D,0,"0")
            END
            ESTJA.MT.ACT.QTY<1,D> = ESTJA.MT.ACT.QTY<1,D> + JMT.QTY
            ESTJA.MT.ACT.COST<1,D> = ESTJA.MT.ACT.COST<1,D> + JMT.COST
            ESTJA.MT.ACT.SALE<1,D> = ESTJA.MT.ACT.SALE<1,D>+ JMT.SALE
            IF JMT.EST.UPD = "" THEN
               JMT.EST.UPD = TODAY
               MATWRITE JMT.REC ON JOB.MATL, JMT.ID
               MATWRITE JMT.REC ON PB.JOB.MATL, JMT.ID
            END
         END
         RELEASE JOB.MATL, JMT.ID
      NEXT S
   NEXT L
   RETURN
*
* OUTSIDE
*
4000 *
   LCNT = DCOUNT(JOB.OS.SEQ,VM)
   FOR L = 1 TO LCNT
      JOS.DEPT = JOB.OS.DEPT<1,L>
      JOS.CCTR = JOB.OS.CCTR<1,L>
      JOS.PLINE = JOB.OS.PLINE<1,L>
      FOR S = 1 TO JOB.OS.SEQ<1,L>
         JOS.ID = ID:"!":JOS.DEPT:"!":JOS.CCTR:"!":JOS.PLINE:"!":S
         MATREADU JOS.REC FROM JOB.OSP, JOS.ID THEN
            PTR = 1
            MATCHED = 0
            LOOP
               LOCATE JOS.DEPT IN ESTJA.OS.DEPT<1>,PTR BY "AR" SETTING D THEN
                  BEGIN CASE
                  CASE JOS.CCTR = ESTJA.OS.CCTR<1,D>
                     BEGIN CASE
                     CASE JOS.VEND = ESTJA.OS.OPER<1,D>
                        BEGIN CASE
                        CASE JOS.PLINE = ESTJA.OS.PLINE<1,D>
                           PTR = 0
                           MATCHED = 1
                        CASE JOS.PLINE < ESTJA.OS.PLINE<1,D>
                           PTR = 0
                        CASE 1
                           PTR = D + 1
                        END CASE
                     CASE JOS.VEND < ESTJA.OS.OPER<1,D>
                        PTR = 0
                     CASE 1
                        PTR = D + 1
                     END CASE
                  CASE JOS.CCTR < ESTJA.OS.CCTR<1,D>
                     PTR = 0
                  CASE 1
                     PTR = D + 1
                  END CASE
               END ELSE
                  PTR = 0
               END
            WHILE PTR DO REPEAT
            IF MATCHED = 0 THEN
               ESTJA.OS.DEPT = INSERT(ESTJA.OS.DEPT,1,D,0,JOS.DEPT)
               ESTJA.OS.CCTR = INSERT(ESTJA.OS.CCTR,1,D,0,JOS.CCTR)
               ESTJA.OS.OPER = INSERT(ESTJA.OS.OPER,1,D,0,JOS.VEND)
               ESTJA.OS.DESC = INSERT(ESTJA.OS.DESC,1,D,0,JOS.DESC)
               ESTJA.OS.PLINE = INSERT(ESTJA.OS.PLINE,1,D,0,JOS.PLINE)
               ESTJA.OS.VEND = INSERT(ESTJA.OS.VEND,1,D,0,JOS.VEND)
               ESTJA.OS.QTY = INSERT(ESTJA.OS.QTY,1,D,0,"0")
               ESTJA.OS.DCOST = INSERT(ESTJA.OS.DCOST,1,D,0,"")
               ESTJA.OS.COST = INSERT(ESTJA.OS.COST,1,D,0,"0")
               ESTJA.OS.SALE = INSERT(ESTJA.OS.SALE,1,D,0,"0")
               ESTJA.OS.ACT.QTY = INSERT(ESTJA.OS.ACT.QTY,1,D,0,"0")
               ESTJA.OS.ACT.COST = INSERT(ESTJA.OS.ACT.COST,1,D,0,"0")
               ESTJA.OS.ACT.SALE = INSERT(ESTJA.OS.ACT.SALE,1,D,0,"0")
            END
            ESTJA.OS.ACT.QTY<1,D> = ESTJA.OS.ACT.QTY<1,D> + JOS.QTY
            ESTJA.OS.ACT.COST<1,D> = ESTJA.OS.ACT.COST<1,D> + JOS.COST
            ESTJA.OS.ACT.SALE<1,D> = ESTJA.OS.ACT.SALE<1,D>+ JOS.SALE
            IF JOS.EST.UPD = "" THEN
               JOS.EST.UPD = TODAY
               MATWRITE JOS.REC ON JOB.OSP, JOS.ID
            END
         END
         RELEASE JOB.OSP, JOS.ID
      NEXT S
   NEXT L
   RETURN
*
* SHIPPING
*
5000 *
   LCNT = DCOUNT(JOB.SP.SEQ,VM)
   FOR L = 1 TO LCNT
      JSP.DEPT = JOB.SP.DEPT<1,L>
      JSP.CCTR = JOB.SP.CCTR<1,L>
      FOR S = 1 TO JOB.SP.SEQ<1,L>
         JSP.ID = ID:"!":JSP.DEPT:"!":JSP.CCTR:"!":S
         MATREADU JSP.REC FROM JOB.SHIP, JSP.ID THEN
            PTR = 1
            MATCHED = 0
            LOOP
               LOCATE JSP.DEPT IN ESTJA.SP.DEPT<1>,PTR BY "AR" SETTING D THEN
                  BEGIN CASE
                  CASE JSP.CCTR = ESTJA.SP.CCTR<1,D>
                     BEGIN CASE
                     CASE JSP.OPER = ESTJA.SP.OPER<1,D>
                        PTR = 0
                        MATCHED = 1
                     CASE JSP.OPER < ESTJA.SP.OPER<1,D>
                        PTR = 0
                     CASE 1
                        PTR = D + 1
                     END CASE
                  CASE JSP.CCTR < ESTJA.SP.CCTR<1,D>
                     PTR = 0
                  CASE 1
                     PTR = D + 1
                  END CASE
               END ELSE
                  PTR = 0
               END
            WHILE PTR DO REPEAT
            IF MATCHED = 0 THEN
               ESTJA.SP.DEPT = INSERT(ESTJA.SP.DEPT,1,D,0,JSP.DEPT)
               ESTJA.SP.CCTR = INSERT(ESTJA.SP.CCTR,1,D,0,JSP.CCTR)
               ESTJA.SP.OPER = INSERT(ESTJA.SP.OPER,1,D,0,JSP.OPER)
               ESTJA.SP.DESC = INSERT(ESTJA.SP.DESC,1,D,0,JSP.DESC)
               ESTJA.SP.VIA = INSERT(ESTJA.SP.VIA,1,D,0,JSP.VIA)
               ESTJA.SP.DCOST = INSERT(ESTJA.SP.DCOST,1,D,0,"")
               ESTJA.SP.COST = INSERT(ESTJA.SP.COST,1,D,0,"0")
               ESTJA.SP.SALE = INSERT(ESTJA.SP.SALE,1,D,0,"0")
               ESTJA.SP.ACT.COST = INSERT(ESTJA.SP.ACT.COST,1,D,0,"0")
               ESTJA.SP.ACT.SALE = INSERT(ESTJA.SP.ACT.SALE,1,D,0,"0")
            END
            ESTJA.SP.ACT.COST<1,D> = ESTJA.SP.ACT.COST<1,D> + JSP.COST
            ESTJA.SP.ACT.SALE<1,D> = ESTJA.SP.ACT.SALE<1,D>+ JSP.SALE
            IF JSP.EST.UPD = "" THEN
               JSP.EST.UPD = TODAY
               MATWRITE JSP.REC ON JOB.SHIP, JSP.ID
            END
         END
         RELEASE JOB.SHIP, JSP.ID
      NEXT S
   NEXT L
   RETURN
*
* MISCELLANEOUS
*
6000 *
   LCNT = DCOUNT(JOB.MS.SEQ,VM)
   FOR L = 1 TO LCNT
      JMS.DEPT = JOB.MS.DEPT<1,L>
      JMS.CCTR = JOB.MS.CCTR<1,L>
      FOR S = 1 TO JOB.MS.SEQ<1,L>
         JMS.ID = ID:"!":JMS.DEPT:"!":JMS.CCTR:"!":S
         MATREADU JMS.REC FROM JOB.MISC, JMS.ID THEN
            PTR = 1
            MATCHED = 0
            LOOP
               LOCATE JMS.DEPT IN ESTJA.MS.DEPT<1>,PTR BY "AR" SETTING D THEN
                  BEGIN CASE
                  CASE JMS.CCTR = ESTJA.MS.CCTR<1,D>
                     BEGIN CASE
                     CASE JMS.OPER = ESTJA.MS.OPER<1,D>
                        PTR = 0
                        MATCHED = 1
                     CASE JMS.OPER < ESTJA.MS.OPER<1,D>
                        PTR = 0
                     CASE 1
                        PTR = D + 1
                     END CASE
                  CASE JMS.CCTR < ESTJA.MS.CCTR<1,D>
                     PTR = 0
                  CASE 1
                     PTR = D + 1
                  END CASE
               END ELSE
                  PTR = 0
               END
            WHILE PTR DO REPEAT
            IF MATCHED = 0 THEN
               ESTJA.MS.DEPT = INSERT(ESTJA.MS.DEPT,1,D,0,JMS.DEPT)
               ESTJA.MS.CCTR = INSERT(ESTJA.MS.CCTR,1,D,0,JMS.CCTR)
               ESTJA.MS.OPER = INSERT(ESTJA.MS.OPER,1,D,0,JMS.OPER)
               ESTJA.MS.DESC = INSERT(ESTJA.MS.DESC,1,D,0,JMS.DESC<1,1>)
               ESTJA.MS.DCOST = INSERT(ESTJA.MS.DCOST,1,D,0,"")
               ESTJA.MS.COST = INSERT(ESTJA.MS.COST,1,D,0,"0")
               ESTJA.MS.SALE = INSERT(ESTJA.MS.SALE,1,D,0,"0")
               ESTJA.MS.ACT.COST = INSERT(ESTJA.MS.ACT.COST,1,D,0,"0")
               ESTJA.MS.ACT.SALE = INSERT(ESTJA.MS.ACT.SALE,1,D,0,"0")
            END
            ESTJA.MS.ACT.COST<1,D> = ESTJA.MS.ACT.COST<1,D> + JMS.COST
            ESTJA.MS.ACT.SALE<1,D> = ESTJA.MS.ACT.SALE<1,D>+ JMS.SALE
            IF JMS.EST.UPD = "" THEN
               JMS.EST.UPD = TODAY
               MATWRITE JMS.REC ON JOB.MISC, JMS.ID
            END
         END
         RELEASE JOB.MISC, JMS.ID
      NEXT S
   NEXT L
   RETURN
*
99999 *
   RETURN
END
