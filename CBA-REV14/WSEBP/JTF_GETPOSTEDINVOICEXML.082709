SUBROUTINE JTF_GETPOSTEDINVOICEXML(CONO,STARTINV,LASTINV,SCHEMA.ONLY,ERRMSG,INVXML)
*************************************************************
* REVISION      - [14.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - JTF_GETPOSTEDINVOICEXML
* SYSTEM        - WSEBP
* BY            -
* DATE          - 07/02/09
* DESCRIPTION   -
* This program get invoices into XML format.
*ENDDOC
*************************************************************
*
SCHEMA.ONLY = "1"
*
*---- FILE EQUATES
*
$INCLUDE JCS.CPYLIB DAILY.INVOICE
$INCLUDE PMC.CPYLIB INVOICE.CODE
$INCLUDE CPYLIB CHAR
$INCLUDE JCS.CPYLIB JOB
$INCLUDE PMC.CPYLIB COMPANY
$INCLUDE PMC.CPYLIB SALESMAN
$INCLUDE PMC.CPYLIB CUSTOMER
$INCLUDE PMC.CPYLIB SALES.CODE
$INCLUDE PMC.CPYLIB SHIP.VIA
$INCLUDE PMC.CPYLIB TERMS
$INCLUDE PMC.CPYLIB COUNTRY.CODE
*
*---- OPEN FILES
*
OPEN '','CONTROL' TO CONTROL ELSE
  ERRMSG='CONTROL FILE MISSING';GOTO 99999
END
OPEN "","INVOICE" TO DAILY.INVOICE ELSE
  ERRMSG="INVOICE FILE MISSING"; GOTO 99999
END
OPEN '','INVOICE.CODE' TO INVOICE.CODE ELSE
  ERRMSG='INVOICE.CODE FILE MISSING';GOTO 99999
END
OPEN '','SALESMAN' TO SALESMAN ELSE
  ERRMSG='SALESMAN FILE MISSING';GOTO 99999
END
OPEN '','CUSTOMER' TO CUSTOMER ELSE
  ERRMSG='CUSTOMER FILE MISSING';GOTO 99999
END
OPEN '','COMPANY' TO COMPANY ELSE
  ERRMSG='COMPANY FILE MISSING';GOTO 99999
END
OPEN '','JOB' TO JOB ELSE
  ERRMSG='JOB FILE MISSING';GOTO 99999
END
OPEN '','SALES.CODE' TO SALES.CODE ELSE
  ERRMGS='SALES CODE FILE IS MISSING'; GOTO 99999
END
OPEN '','SHIP.VIA' TO SHIP.VIA ELSE
  ERRMGS='SHIP VIA FILE IS MISSING'; GOTO 99999
END
OPEN '','TERMS' TO TERMS ELSE
  ERRMGS='TERMS FILE IS MISSING'; GOTO 99999
END
OPEN '','COUNTRY.CODE' TO COUNTRY.CODE ELSE
  ERRMGS='COUNTRY.CODE FILE IS MISSING'; GOTO 99999
END
OPEN "","IVC_XML" TO IVC_XML ELSE
  ERRMSG="IVC_XML FILE IS MISSING" ; GOTO 99999
END
*
*---- INITIALIZATION
*
MAT DI.REC=''
MAT JOB.REC=''
MAT CUST.REC=''
MAT SALESMAN.REC=''
LINE.CNT=0
SP1=" ";SP2="  "
SP=STR(' ',71)
XS=STR('X',48)
UNKNOWN=STR("?",30)
DESC=""
UM=''
LINE=STR("-",13)
TOTAL=0
HIDDEN.FLAG=0
PRINTED.TOT=0
SCHEMA.ONLY = "0"
INVXML = "<Invoices>"
*
*---- MAIN PROCESSING
*
FIRST.TIME=''
FIRST.ID = CONO:STARTINV
LAST.ID = CONO:LASTINV
STMT = "SELECT INVOICE WITH @ID GE '":FIRST.ID:"' AND WITH @ID LE '":LAST.ID:"' AND WITH @ID UNLIKE '...CM' AND WITH @ID UNLIKE '...DM' AND WITH @ID UNLIKE '...PB'"
UDTEXECUTE STMT CAPTURING JUNK
IVC.CNT = FIELD(JUNK<2>," ",1)
INVXML := "<InvoiceCount>":IVC.CNT:"</InvoiceCount>"
LOOP
  READNEXT DI.KEY ELSE EXIT
  INVOICE.NUM=DI.KEY[4,99]
  MATREADU DI.REC FROM DAILY.INVOICE,DI.KEY ELSE
    RELEASE DAILY.INVOICE,DI.KEY
    CONTINUE
  END
  MATREAD CUST.REC FROM CUSTOMER,CONO:DI.CUST.NO ELSE MAT CUST.REC=""
  MATREAD JOB.REC FROM JOB,CONO:DI.JOB.NO ELSE MAT JOB.REC=""
  MATREAD SLC.REC FROM SALES.CODE,CONO:JOB.SALES.CODE ELSE
    MAT SLC.REC=''
  END
  MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE
    SALS.NAME=UNKNOWN[1,20]
  END
  MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:JOB.SHIP.VIA ELSE
    MAT SHIP.VIA.REC=''
  END
  MATREAD TERMS.REC FROM TERMS,CONO:DI.TERMS ELSE MAT TERMS.REC=''
  CHG.CODE.CNT=DCOUNT(DI.CHG.CODE,VM)
  IF DI.CHG.CODE = '' THEN CHG.CODE.CNT = 0
  GOSUB FORM.TOP.PRINT
  XML.DET=""
  FOR CODE.IDX=1 TO CHG.CODE.CNT
    IF DI.HIDDEN<1,CODE.IDX> = 'Y' THEN CONTINUE
    CODE=DI.CHG.CODE<1,CODE.IDX>
    NEXT.CODE=DI.CHG.CODE<1,CODE.IDX+1>
    CHG.CODE=DI.CHG.CAT<1,CODE.IDX>
    HIDDEN.AMT=0;HIDDEN.QTY=0;HIDDEN.PRICE=0
    FOR IDX = CODE.IDX+1 TO CHG.CODE.CNT
      IF DI.HIDDEN<1,IDX> # 'Y' THEN EXIT
      HIDDEN.AMT   += DI.AMOUNT<1,IDX>
      HIDDEN.QTY   += DI.QUANTITY<1,IDX>
      HIDDEN.PRICE += DI.UNIT.PRICE<1,IDX>
    NEXT IDX
    AMT = DI.AMOUNT<1,CODE.IDX>+HIDDEN.AMT
    QUANTITY = DI.QUANTITY<1,CODE.IDX>
    UNIT.PRICE = DI.UNIT.PRICE<1,CODE.IDX>+HIDDEN.PRICE
    UM = DI.UM<1,CODE.IDX>
    IF HIDDEN.AMT+0 # 0 AND QUANTITY+0 # 0 THEN
      BEGIN CASE
        CASE UM='M'
          FACTOR=1000
        CASE UM='C'
          FACTOR=100
        CASE 1
          FACTOR=1
      END CASE
      UNIT.PRICE=ICONV((AMT/100) / (QUANTITY/FACTOR),'MD4')
    END
    DESC=DI.DESC<1,CODE.IDX>
    IF AMT="" OR AMT=0 THEN
      AMOUNT=""
    END ELSE
      IF CODE = "SUB" OR CODE = "SUBT" OR CODE = "TOT" THEN
        AMOUNT = OCONV(AMT,"MD2,$")
      END ELSE
        AMOUNT = OCONV(AMT,"MD2,")
      END
    END
    IF CODE # 'SUB' AND CODE # 'SUBT' AND CODE # 'TOT' THEN
      TOTAL += AMT
    END
    GOSUB BODY.INVOICE.PRINT
  NEXT CODE.IDX
  DI.PRT.DATE=DATE()
*  MATWRITE DI.REC ON DAILY.INVOICE,DI.KEY
  IF CUST.XML.FLAG = "Y" OR CUST.XML.FLAG = "B" THEN
    GOSUB XML.SUB
  END
  TOTAL=0
  PRINTED.TOT=0
REPEAT
INVXML:="</Invoices>"
TD = OCONV(DATE(),'D4/')
TODAY = TD[1,2]:TD[4,2]:TD[7,4]
TIMENOW = OCONV(TIME(),'MTS')
TIMESTMP = TODAY:"-":TIMENOW
IVCXKEY="INVOICES-":TIMESTMP:".XML"
WRITE INVXML ON IVC_XML, IVCXKEY
GOTO 99999
*
*---- CREATE INVOICE TOP
*
FORM.TOP.PRINT: 
BILL.TO.CNTY=CUST.ADDL.OPS<1,4>
IF BILL.TO.CNTY # '' THEN
  MATREAD CTY.REC FROM COUNTRY.CODE, CONO:BILL.TO.CNTY THEN
    BILL.TO.CNTY=CTY.DESC
  END
END
RETURN
*
*---- INVOICE BODY
*
BODY.INVOICE.PRINT: 
IF QUANTITY=0 THEN QUANTITY=""
IF UNIT.PRICE=0 THEN UNIT.PRICE=""
DEC.POS=INDEX(AMOUNT,".",1)
IF DEC.POS=0 THEN AMOUNT=OCONV(AMOUNT,"MD2")
XML.DET<CODE.IDX>="<LineItem>"
XML.DET<CODE.IDX>:="<ItemNo>":CODE.IDX:"</ItemNo>"
XML.DET<CODE.IDX>:="<Description>":DESC:"</Description>"
XML.DET<CODE.IDX>:="<Quantity>":QUANTITY:"</Quantity>"
XML.DET<CODE.IDX>:="<InvoiceCode>":DI.CHG.CODE<1,CODE.IDX>:"</InvoiceCode>"
XML.DET<CODE.IDX>:="<InvoiceCodeCatg>":DI.CHG.CAT<1,CODE.IDX>:"</InvoiceCodeCatg>"
XML.DET<CODE.IDX>:="<TaxJurisdiction>":DI.TAX.JURS<1,CODE.IDX>:"</TaxJurisdiction>"
XML.DET<CODE.IDX>:="<TaxRate>":DI.TAX.RATE<1,CODE.IDX>:"</TaxRate>"
XML.DET<CODE.IDX>:="<JobToBill>":DI.CHG.JOB<1,CODE.IDX>:"</JobToBill>"
XML.DET<CODE.IDX>:="<Hidden>":DI.HIDDEN<1,CODE.IDX>:"</Hidden>"
XML.DET<CODE.IDX>:="<UOM>":UM:"</UOM>"
XML.DET<CODE.IDX>:="<UnitPrice>":OCONV(UNIT.PRICE,'MD4'):"</UnitPrice>"
XML.DET<CODE.IDX>:="<Amount>":AMOUNT:"</Amount>"
XML.DET<CODE.IDX>:="</LineItem>"
RETURN
*
*---- XML BUILD
*
XML.SUB: 
DI.PRT.DATE=OCONV(DI.PRT.DATE,"D2-")
DI.PRT.DATE=DI.PRT.DATE[1,2]:DI.PRT.DATE[4,2]:DI.PRT.DATE[7,2]
INVXML:="<Invoice>"
INVXML:="<CompanyNo>":CONO:"</CompanyNo>"
INVXML:="<InvoiceNo>":INVOICE.NUM:"</InvoiceNo>"
INVXML:="<InvoiceDate>":DI.PRT.DATE:"</InvoiceDate>"
INVXML:="<InvoiceShipDate>":DI.SHP.DATE:"</InvoiceShipDate>"
INVXML:="<InvoicePONo>":DI.PO.NO:"</InvoicePONo>"
INVXML:="<InvoiceSalesNo>":JOB.SLSMN:"</InvoiceSalesNo>"
INVXML:="<InvoiceSalesName>":SALS.NAME:"</InvoiceSalesName>"
INVXML:="<JobNo>":DI.JOB.NO:"</JobNo>"
INVXML:="<LoginId>":DI.USER.ID:"</LoginId>"
INVXML:="<ProcessDate>":OCONV(DI.PROC.DATE,'D2/'):"</ProcessDate>"
INVXML:="<DueDate>":OCONV(DI.DUE.DATE,'D2/'):"</DueDate>"
INVXML:="<ShipVia>":CUST.SHIP.VIA:"</ShipVia>"
INVXML:="<ShipViaDesc>":SHPV.DESC:"</ShipViaDesc>"
INVXML:="<Terms>":DI.TERMS:"</Terms>"
INVXML:="<TermsDesc>":TERMS.DESC:"</TermsDesc>"
INVXML:="<CustomerId>":DI.CUST.NO:"</CustomerId>"
INVXML:="<CustomerName>":CUST.NAME:"</CustomerName>"
INVXML:="<Address1>":DI.ADDR1:"</Address1>"
INVXML:="<Address2>":DI.ADDR2:"</Address2>"
INVXML:="<Address3>":DI.ADDR3:"</Address3>"
*ADDR4 = CUST.ADDR4:"  ":CUST.ZIP:"  ":BILL.TO.CNTY
CST.CITY = FIELD(CUST.ADDR4,',',1)
CST.STATE = FIELD(CUST.ADDR4,',',2)
INVXML:="<CustomerCity>":CST.CITY:"</CustomerCity>"
INVXML:="<CustomerState>":CST.STATE:"</CustomerState>"
INVXML:="<CustomerZip>":CUST.ZIP:"</CustomerZip>"
*INVXML:="<Address4>":ADDR4:"</Address4>"
INVXML:="<Attention>":DI.ATTENTION:"</Attention>"
INVXML:="<ShiptoAddress1>":DI.SHP.ADDR1:"</ShiptoAddress1>"
INVXML:="<ShiptoAddress2>":DI.SHP.ADDR2:"</ShiptoAddress2>"
INVXML:="<ShiptoAddress3>":DI.SHP.ADDR3:"</ShiptoAddress3>"
*INVXML:="<ShiptoAddress4>":DI.ADDR4:"</ShiptoAddress4>"
SHP.CITY = FIELD(DI.ADDR4,',',1)
SHP.STATE= FIELD(TRIM(FIELD(DI.ADDR4,',',2)),' ',1)
SHP.ZIP = FIELD(TRIM(FIELD(DI.ADDR4,',',2)),' ',2)
INVXML:="<ShipToCity>":SHP.CITY:"</ShipToCity>"
INVXML:="<ShipToState>":SHP.STATE:"</ShipToState>"
INVXML:="<ShipToZip>":SHP.ZIP:"</ShipToZip>"
INVXML:="<ShiptoAttention>":DI.SHP.NAME:"</ShiptoAttention>"
INVXML:="<CustomerAccount>":CUST.ACCT:"</CustomerAccount>"
INVXML:="<CustomerStartDate>":OCONV(CUST.START.DATE,'D2/'):"</CustomerStartDate>"
INVXML:="<CustomerCredit>":CUST.CREDIT:"</CustomerCredit>"
INVXML:="<CustomerCreditLimit>":CUST.CR.LIMIT:"</CustomerCreditLimit>"
INVXML:="<CustomerCreditDesc>":CUST.CREDIT.DESC:"</CustomerCreditDesc>"
INVXML:="<CustomerType>":CUST.TYPE:"</CustomerType>"
INVXML:="<CustomerStatementCode>":CUST.STMT.CODE:"</CustomerStatementCode>"
INVXML:="<CustomerEmail>":CUST.EMAIL:"</CustomerEmail>"
INVXML:="<CustomerStatementSendFlag>":CUST.STMT.SEND.FLAG:"</CustomerStatementSendFlag>"
INVXML:="<CustomerInvoiceCode>":CUST.IVC.CODE:"</CustomerInvoiceCode>"
INVXML:="<CustomerPriceCode>":CUST.PRICE.CODE:"</CustomerPriceCode>"
INVXML:="<CustomerAdditionalOps>":CUST.ADDL.OPS<1,1>:"</CustomerAdditionalOps>"
INVXML:="<CustomerMiscCode>":CUST.COMMISSION:"</CustomerMiscCode>"
INVXML:="<LineItems>"
INVXML:="<ItemTotal>":CHG.CODE.CNT:"</ItemTotal>"
IF CHG.CODE.CNT > 0 THEN
  FOR XX = 1 TO CHG.CODE.CNT
    INVXML:=XML.DET<XX>
  NEXT XX
END
INVXML:= "</LineItems>"
INVXML:="<Total>":OCONV(TOTAL,'MD2,$'):"</Total>"
INVXML:="</Invoice>"
RETURN
*
*---- CALLS FOR SYSCOM
*
99999*
IF ERRMSG <> "" THEN
  ERRMSG = "ERROR FROM JTF_GETPOSTEDINVOICEXML " : ERRMSG
  CALL WRITELOG(ERRMSG)
END
*
END
