   FUNCTION RFT_DIVISION.POSITION.L2(CONO,CONTROL,DIV.CODE)
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PMCBP
* PROGRAM     - GET.DIV.POS
* BY          - edvard; CBA
* DATE        - 08/21/2001
* DESCRIPTION -
*RET.VALUE<1,1> VALUES:
*1. '' NO ERROR,  RET.VALUE<1,2> CONTAINS RESULT 
*2. 0 WARNING,   RET.VALUE<1,2> CONTAINS ERRMSG
*3. -1 ERROR,     RET.VALUE<1,2> CONTAINS ERRMSG
*4. -2 FATAL ERROR, RET.VALUE<1,2> CONTAINS ERRMSG
*T25740 edvard 08/21/2001 * REV12
*ENDDOC
**********************************************
   RET.VALUE=''
   IF FILEINFO(CONTROL,0)=0 THEN
      OPEN '','CONTROL' TO CONTROL ELSE
         RET.VALUE<1,1>="-2"
         RET.VALUE<1,2>="CONTROL file is missing."
         GOTO 99999
      END
   END
   READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE    
      RET.VALUE<1,1>="-2"
      RET.VALUE<1,2> = "DIVISIONS CONTROL RECORD IS MISSING"         
      GOTO 99999                                             
   END                                                      
   READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE 
      SECURITY.REC = 'N'                                     
      SECURITY.REC<2> = 'N'                                  
   END                                                      
   IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
         RET.VALUE<1,1>="-1"
         RET.VALUE<1,2> = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
         GOTO 99999
      END
      LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING DIV.POS ELSE
         RET.VALUE<1,1>="-2"
         RET.VALUE<1,2> = "Division ":DIV.CODE:" not found in DIVISIONS Control Record"
         GOTO 99999
      END
   END ELSE
      DIV.POS=1
   END
   RET.VALUE<1,1>=''
   RET.VALUE<1,2>=DIV.POS
99999 
   RETURN RET.VALUE
