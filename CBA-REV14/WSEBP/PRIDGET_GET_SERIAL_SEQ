FUNCTION PRIDGET_GET_SERIAL_SEQ(CONO,CONTROL,INV_SERIAL)
*************************************************************
* SYSTEM      - PRIDGET
* SOURCE      - WSEBP
* PROGRAM     - GET_SERIAL_SEQ
* BY          - YAKUB ON 06/16/2009
* DESCRIPTION - This program gets the next available key for the
*               INV_SERIAL file.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
$INCLUDE CPYLIB CHAR
IF FILEINFO(INV_SERIAL_TEMP,0)=0 THEN 
  OPEN '','INV_SERIAL_TEMP' TO INV_SERIAL_TEMP ELSE STOP "INV_SERIAL_TEMP FILE IS MISSING"
END
IF FILEINFO(INV_SERIAL_DELETED,0)=0 THEN 
  OPEN '','INV_SERIAL_DELETED' TO INV_SERIAL_DELETED ELSE STOP "INV_SERIAL_DELETED FILE IS MISSING"
END
IF FILEINFO(CONTROL,0)=0 THEN 
  OPEN '','CONTROL' TO CONTROL ELSE STOP "CONTROL FILE IS MISSING"
END
READU SERIAL.SEQ FROM CONTROL, CONO:"ISTK.SEQ" ELSE
  SERIAL.SEQ = 0
END
FND=0
LOOP
  SERIAL.SEQ+=1
  IF SERIAL.SEQ > 9999999 THEN SERIAL.SEQ = 1
  SERIAL.SEQ =  SERIAL.SEQ"R%7"
  CALL PRIDGET_CHECK_DIGIT("C",SERIAL.SEQ,"10RL",CKDIG,VALID)
  SERIAL.NO=SERIAL.SEQ:CKDIG
  ISTK.ID=CONO:SERIAL.NO
  IF VALID THEN
    IF RECORDLOCKED(INV_SERIAL_TEMP,ISTK.ID)=0 THEN   
      DELETE INV_SERIAL_TEMP,ISTK.ID                  
    END                                               
    READU CHECKREC FROM INV_SERIAL_TEMP,ISTK.ID LOCKED
      NULL                                            
    END ELSE                                          
      READU CHECKREC FROM INV_SERIAL,ISTK.ID THEN     
        RELEASE INV_SERIAL, ISTK.ID                   
        RELEASE INV_SERIAL_TEMP,ISTK.ID               
      END ELSE                                        
        READ CHECK.REC FROM INV_SERIAL_DELETED,ISTK.ID THEN
          RELEASE INV_SERIAL, ISTK.ID                   
          RELEASE INV_SERIAL_TEMP,ISTK.ID               
        END ELSE
          FND = 1                                       
        END                                             
      END                                               
    END
  END
UNTIL FND DO
  RELEASE INV_SERIAL,ISTK.ID
REPEAT
WRITE SERIAL.SEQ ON CONTROL,CONO:"ISTK.SEQ"
RETURN SERIAL.NO
