   SUBROUTINE BUILD.IWH.FI(IWH.ID,MAT IWH.REC, PERIOD,ERR.FLG,ERRMSG,OPEN.FLAG)
*
*COPY>CPYLIB>COM1
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - BUILD.IWH.FI
* BY          - epitka; CBA
* DATE        - 11/30/2001
* DESCRIPTION -
*
* THIS SUB BUILDS INV.WHSE FIFO buckets from
*                           INV_RECP_WHSE file.
*                           USE THIS PROGRAM ONLY IF NO UPDATES OF 
*                           INV.WHSE FILE ARE GOING TO TAKE PLACE
*                           OTHERWISE USE ICS.IWH.SUB
*                           CALL WITH ACTION=1 
*ERR.FLG=2 TERMINAL ERROR; GOSUB 93000 IN CALLING PROGRAM
*ERR.FLG=1 ; GOSUB 91000 IN CALLING PROGRAM
*T25740 epitka 11/30/2001  REV12
*T26676 epitka 06/19/2002 * EXCLUDE TRANSACTIONS OCCURING IN THE FUTURE
*                           PERIOD WHEN BUILDING FIFO BUCKETS FOR SOME
*                           REPORTS BASED ON THE FLAG (FIFO VALUE
*                           REPORT).
*T28431 lross 02/07/2005 * Correction for RECP.COST.ADJ made after
*                          cutoff.
*T28762 lross 01/10/2006 * Add Transfers to logic for T28431 above.
*T28978 lross 08/30/2006 * Read of INV.REC out of place.
*ENDDOC
**********************************************
*
*
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE ICS.CPYLIB INVENTORY
$INCLUDE ICS.CPYLIB INV_RECEIPTS
$INCLUDE ICS.CPYLIB INV_RECP_WHSE 
$INCLUDE ICS.CPYLIB INV_AUDIT_HIST
$INCLUDE CPYLIB CHAR
$INCLUDE CPYLIB FILE.VARS
*
   IF PERIOD<1,2>='EXCLUDE' THEN EXCLUDE=1 ELSE EXCLUDE=0
   IF PERIOD<1,1>='' THEN PERIOD<1,1>='9999999'
*
   IF (OPEN.FLAG) THEN
      OPEN.FLAG=0
      IF FILEINFO(INV_RECEIPTS,0)=0 THEN
         OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE               
            ERRMSG="INV_RECEIPTS FILE IS MISSING"
            ERR.FLG=2; GOTO 99999
         END                                                       
      END
      IF FILEINFO(INV_RECEIPTS_TEMP,0)=0 THEN
         OPEN '','INV_RECEIPTS_TEMP' TO INV_RECEIPTS_TEMP ELSE     
            ERRMSG="INV_RECEIPTS_TEMP FILE IS MISSING"
            ERR.FLG=2; GOTO 99999
         END                                                       
      END
      IF FILEINFO(INV_RECP_WHSE,0)=0 THEN
         OPEN '','INV_RECP_WHSE' TO INV_RECP_WHSE ELSE             
            ERRMSG="INV_RECP_WHSE FILE IS MISSING"
            ERR.FLG=2; GOTO 99999
         END                                                       
      END
      IF FILEINFO(INV_RECP_WHSE_TEMP,0)=0 THEN
         OPEN '','INV_RECP_WHSE_TEMP' TO INV_RECP_WHSE_TEMP ELSE   
            ERRMSG="INV_RECP_WHSE_TEMP FILE IS MISSING"
            ERR.FLG=2; GOTO 99999
         END                                                       
      END
      IF FILEINFO(INVENTORY,0)=0 THEN ;*T28431
         OPEN '','INVENTORY' TO INVENTORY ELSE             
            ERRMSG="INVENTORY FILE IS MISSING"
            ERR.FLG=2; GOTO 99999
         END                                                       
      END
      IF (EXCLUDE) THEN
         IF FILEINFO(INV_AUDIT_HIST,0)=0 THEN
            OPEN '','INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE
               ERRMSG='INV_AUDIT_HIST FILE IS MISSING'
               ERR.FLG=2; GOTO 99999
            END
         END
      END
   END
   ;*
   ;*build IWH.REC information from receipts.
   ;*do not include future period receipts in receipt array
   ;* if PERIOD variable passed. If not then include all receipts, 
   ;* current and future.
   ;* If PERIOD<1,2>='EXCLUDE' then exclude
   ;* transactions that occur in the future period
   ;* when building fifo buckets.
   ;*
*
   RECP.NO=IWH.RECP.NO<1>
   RECP.PERIOD=IWH.RECP.PERIOD<1>
*
   IWH.ORG.FI = ""
   IWH.RSV.FI = ""
   IWH.QTY.FI = ""
   IWH.COST.FI=""
   IWH.VDR.FI=''
   IWH.PO.NO.FI=''
   IWH.PO.LN.FI=''
   IWH.RECV.FI=''
   IWH.DATE.FI=''
   IWH.ACT.COST=''
   IWH.COST.FI=''
   IWH.SALE.FI=''
   IWH.RECP.NO=''
   IWH.RECP.PERIOD=''
   IWH.RECP.ENT.DATE=''
*
   CONO=IWH.ID[1,3]
   WHSE = OCONV(IWH.ID,"G1!1")
   INV.RFLG=0 ;*T28431
   RCNT=0
   RECP.CNT = DCOUNT(RECP.NO<1>,VM)
   FOR RR = 1 TO RECP.CNT
      RECP=RECP.NO<1,RR>
      IF RECP.PERIOD<1,RR> <= PERIOD<1,1> THEN
         INVR.ID=CONO:RECP
         MATREAD INVR.REC FROM INV_RECEIPTS,INVR.ID ELSE CONTINUE
*T28431 v
         IF NOT(INV.RFLG) THEN
           MATREAD INV.REC FROM INVENTORY,CONO:INVR.PROD ELSE CONTINUE
           INV.RFLG=1
         END
*T28431 ^
         IRW.ID=CONO:RECP:"!":WHSE
         MATREAD IRW.REC FROM INV_RECP_WHSE,IRW.ID ELSE CONTINUE
         RCNT+=1
         IWH.ORG.FI<1,RCNT>=IRW.ORG.QTY   
         IWH.RSV.FI<1,RCNT>=IRW.RSVB.QTY   
         IWH.QTY.FI<1,RCNT>=IRW.CUR.QTY        
         IWH.COST.FI<1,RCNT>=IRW.UNIT.COST   
         IWH.VDR.FI<1,RCNT> = INVR.VEND          
         IWH.PO.NO.FI<1,RCNT>=INVR.PO            
         IWH.PO.LN.FI<1,RCNT>=INVR.PO.LN         
         IWH.RECV.FI<1,RCNT>=INVR.ENT.DATE       
         IWH.DATE.FI<1,RCNT>=INVR.POST.DATE      
         IWH.COST.FI<1,RCNT>=INVR.UNIT.COST   
         IWH.SALE.FI<1,RCNT>=INVR.UN.SALE           
         IWH.RECP.PERIOD<1,RCNT>=INVR.PERIOD     
         IWH.RECP.ENT.DATE<1,RCNT>=INVR.ENT.DATE 
         IWH.RECP.NO<1,RCNT>=RECP             
      END ELSE
         IF (EXCLUDE) THEN
            GOSUB EXCLUDE.FUTURE.TRANSACTIONS
         END
      END
   NEXT RR
   IF (EXCLUDE) THEN 
      RECP.CNT = DCOUNT(IWH.RECP.NO<1>,VM)
      FOR RR = 1 TO RECP.CNT
         RECP=IWH.RECP.NO<1,RR>
         GOSUB EXCLUDE.FUTURE.TRANSACTIONS
      NEXT RR
   END
*
   GOTO 99999
*
*************************************************************************
***** S U B R O U T I N E S *********************************************
*************************************************************************
*
****************************
EXCLUDE.FUTURE.TRANSACTIONS: 
****************************
*
   INVR.ID=CONO:RECP
   MATREAD INVR.REC FROM INV_RECEIPTS,INVR.ID ELSE CONTINUE
   IRW.ID=CONO:RECP:"!":WHSE
   MATREAD IRW.REC FROM INV_RECP_WHSE,IRW.ID ELSE CONTINUE
   HCNT=DCOUNT(INVR.AUDIT.NO,VM)
*T28978 v
   IF NOT(INV.RFLG) THEN
     MATREAD INV.REC FROM INVENTORY,CONO:INVR.PROD ELSE CONTINUE
     INV.RFLG=1
   END
*T28978 ^
*T28431 v
   TOT.RECP.QTY=0
   TOT.RECP.COST=0
   FUTR.COST.ADJ=''
*T28431 ^
   FOR HH=1 TO HCNT
      INAH.ID=CONO:INVR.AUDIT.NO<1,HH>
      MATREAD INAH.REC FROM INV_AUDIT_HIST,INAH.ID THEN
         IF INAH.PERIOD>PERIOD<1,1> THEN
            IF INAH.WHSE=WHSE THEN
               BEGIN CASE
                  CASE INAH.DEPL.RECP#''
                     DPCNT=DCOUNT(INAH.DEPL.RECP<1>,VM)
                     FOR DD=1 TO DPCNT
                        LOCATE INAH.DEPL.RECP<1,DD> IN IWH.RECP.NO<1> SETTING RPOS THEN
                           IF IWH.RECP.PERIOD<1,RPOS><=PERIOD<1,1> THEN
                              IWH.QTY.FI<1,RPOS>-=INAH.DEPL.QTY<1,DD>
                           END
                        END
                     NEXT DD
                  CASE 1
                     LOCATE INAH.RECP.NO IN IWH.RECP.NO<1> SETTING RPOS THEN
                        IWH.QTY.FI<1,RPOS>-=INAH.QTY
                     END
               END CASE
*T28431 v
               IF INAH.SRC = 'IC' THEN
                 FUTR.COST.ADJ = 1
               END
            END
*        END
         END ELSE
            IF INAH.WHSE=WHSE THEN
*T28762 v     IF (INAH.TYPE = 'R' OR INAH.TYPE = 'A') AND INAH.SRC # 'IS' THEN
              IF (INAH.TYPE = 'R' OR INAH.TYPE = 'A' OR INAH.TYPE='I') AND INAH.SRC # 'IS' THEN
                TOT.RECP.QTY += INAH.QTY
                TOT.RECP.COST += INAH.EXT.COST
              END
            END
*T28431 ^
         END
      END
   NEXT HH
*T28431 v
   IF FUTR.COST.ADJ THEN
     IF INV.COST.WT+0 = 0 THEN INV.COST.WT = 100
     IF TOT.RECP.QTY # 0 THEN
       UN.COST = INT((TOT.RECP.COST / TOT.RECP.QTY * INV.COST.WT * 1000) +.5)
       IWH.COST.FI<1,RR> = UN.COST
     END
   END
*T28431 ^
   RETURN
*
99999* 
END
