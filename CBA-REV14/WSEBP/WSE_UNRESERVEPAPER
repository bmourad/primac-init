SUBROUTINE WSE_UNRESERVEPAPER(CONO, JOBID, PAPERID, WHSEID, QTYRESERVED, CURRDATE, ERRMSG)
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE JCS.CPYLIB JOB
OPEN '','JOB' TO JOB ELSE
  ERRMSG = 'JOB FILE IS MISSING'
  RETURN
END
OPEN '','INV.WHSE' TO INV.WHSE ELSE
  ERRMSG = 'INV.WHSE FILE IS MISSING'
  RETURN
END

NCNT = DCOUNT(PAPERID,@VM)
MATREAD JOB.REC FROM JOB, CONO : JOBID THEN
  FOR I = 1 TO NCNT
    BFND = 0
    CNT = DCOUNT(JOB.RESV.MATL,@VM)
    FOR J = 1 TO CNT
      IF JOB.RESV.MATL<1,J> = PAPERID<1,I> AND JOB.RESV.WHSE<1,J> = WHSEID<1,I> THEN BFND = J
    NEXT J
    IF BFND # 0 THEN
      IF JOB.RESV.QTY<1,BFND> LE ICONV(QTYRESERVED<1,I>,'MD2') * 10 THEN
        JOB.RESV.MATL = DELETE(JOB.RESV.MATL,1,BFND,0)
        JOB.RESV.WHSE = DELETE(JOB.RESV.WHSE,1,BFND,0)
        JOB.RESV.DATE = DELETE(JOB.RESV.DATE,1,BFND,0)
        JOB.ALOC.QTY = DELETE(JOB.ALOC.QTY,1,BFND,0)
        JOB.RESV.QTY = DELETE(JOB.RESV.QTY,1,BFND,0)
        JOB.USED.QTY = DELETE(JOB.USED.QTY,1,BFND,0)
      END ELSE
        JOB.RESV.MATL<1,BFND> = JOB.RESV.MATL<1,BFND>
        JOB.RESV.WHSE<1,BFND> = JOB.RESV.WHSE<1,BFND>
        JOB.RESV.DATE<1,BFND> = ICONV(CURRDATE,'D2/')
        JOB.ALOC.QTY<1,BFND> = JOB.ALOC.QTY<1,BFND>
        JOB.RESV.QTY<1,BFND> -= ICONV(QTYRESERVED<1,I>,'MD2') * 10
        JOB.USED.QTY<1,BFND> = JOB.USED.QTY<1,BFND>
      END
    END
    IWH.ID = CONO:PAPERID<1,I>:'!':WHSEID<1,I>
    READVU IWH.RESV FROM INV.WHSE, IWH.ID, 8 THEN
      IF IWH.RESV LE ICONV(QTYRESERVED<1,I>,'MD2') * 10 THEN
        IWH.RESV = 0
      END ELSE
        IWH.RESV -= ICONV(QTYRESERVED<1,I>,'MD2') * 10
      END
      WRITEV IWH.RESV ON INV.WHSE, IWH.ID, 8
    END
    RELEASE INV.WHSE, IWH.ID
  NEXT I
  WRITEV JOB.RESV.MATL ON JOB, CONO:JOBID, 31
  WRITEV JOB.RESV.WHSE ON JOB, CONO:JOBID, 32
  WRITEV JOB.RESV.DATE ON JOB, CONO:JOBID, 33
  WRITEV JOB.ALOC.QTY ON JOB, CONO:JOBID, 34
  WRITEV JOB.RESV.QTY ON JOB, CONO:JOBID, 35
  WRITEV JOB.USED.QTY ON JOB, CONO:JOBID, 36
END ELSE
  ERRMSG = 'CANNOT FIND JOB ':CONO:JOBID
  RETURN
END
RETURN
