  SUBROUTINE RFT_INV.AVG.COST.MAINT.T2(MAT IWH.REC,COST.WT,CUR.PERIOD)
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.AVG.COST.MAINT
* BY          - WALID YAMOUT , C.B.A
* DATE        - 05/15/84
* DESCRIPTION
* This program calculates the average inventory receipt cost based
* upon all receipts for this product as stored in the FIFO buckets.
*
*  ADDED LINES 35 THRU 35 TO FIX AVERAGE COST. THE PROGRAM WOULD NOT
*  UPDATE AVERAGE COST IF THERE WAS ONLY ONE RECEIPT
*  RLB...6/23/95
*
* PROBLEM 538 7/12/95 RECALCULATE AVG COST WITH 1 RECEIPT
*T26767 epitka 07/23/2002 * calculation of average price should be based
*                           on original rather than on current qty
*T26767 epitka 07/24/2002 * BASE CALCULATION ON CURRENT QTY FOR CURRENT
*                           AND PAST PERIOD AND ON ORIGINAL FOR FUTURE
*                           PERIODS.
*********************************************************************
*
*---- INSERT EQUATE FROM CPYLIB
*
$INCLUDE ICS.CPYLIB INV.WHSE
$INCLUDE CPYLIB CHAR
*
  ;*
  TOT.QTY = 0
  TOT.COST = 0
  IF COST.WT + 0 = 0 THEN COST.WT = 100
  ;*
  CNT=DCOUNT(IWH.RECP.NO,VM)
  IF CNT = 0 THEN CNT = 1     ;* P538 RLB
  FOR I = 1 TO CNT
    IF IWH.RECP.PERIOD<1,I><= CUR.PERIOD THEN
      TOT.QTY +=(IWH.QTY.FI<1,I>/1000)
      TOT.COST+=((IWH.COST.FI<1,I>/10000)*(IWH.QTY.FI<1,I>/1000))
    END ELSE
      TOT.QTY +=(IWH.ORG.FI<1,I>/1000)
      TOT.COST+=((IWH.COST.FI<1,I>/10000)*(IWH.ORG.FI<1,I>/1000))
    END
  NEXT I
  BEGIN CASE
    CASE TOT.QTY < 0
      IWH.AVG.COST = INT(((TOT.COST / TOT.QTY) * 10000) - 0.5)
    CASE TOT.QTY > 0
      IWH.AVG.COST = INT(((TOT.COST / TOT.QTY) * 10000) + 0.5)
  END CASE
999999*
  RETURN
END
