*COPY>CPYLIB>COM1
**************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - ORDER.INV.EDIT.LIST.F4
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC.
* DATE        - 07/29/92
* DESCRIPTION -
* This program produces an Invoice edit listing for Invoice Form # 4.
*
* TASK
*    19394 7/95 LLH DIVISIONALIZE
* TASK 20532 JR EXPAND INVOICE AMOUNT TO 8.2
*
*T23278 markt 12/22/1998 * Add check for division security.
*T24502 alex 11/05/1999 * FIX ARRAY PROBLEM ON PRINT LINE LIKE JCS
*                       * CHANGED PL & PR FROM DIM. TO DYNAMIC ARRAY
*T25756 lanny 04/17/2001 * Insert date in DI_LIST_DATE req'd for posting.
*T25764 cmykleb 02/22/2002 * Add Invoice Due Date to report.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T26090 cmykleb 04/11/2002 * If report is (V)iewed do not update records
*                            as printed.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>OPS.CPYLIB>DAILY.ORDER.INVOICE
*COPY>PMC.CPYLIB>COMPANY
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD XX ELSE
      ERRMSG = "MUST BE READ FROM A PROC"
      GOSUB 91000;GOTO 99999
   END
   DIV.NO = XX<3>
   PRINT.FLAG = XX<4> ; * T26090
   TYPE.FLAG = XX<5>
   IF TYPE.FLAG # "I" AND TYPE.FLAG # "C" AND TYPE.FLAG # "D" THEN
      ERRMSG = "INVALID INFORMATION PASSED TO PROGRAM"
      GOSUB 91000
      GOTO 99999
   END
*
*---- DIMENSION VARIABLES
*
   PR = '' ; PL = '' ; *T24502
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','DAILY.ORDER.INVOICE' TO DAILY.ORDER.INVOICE ELSE ERRMSG='DAILY.ORDER.INVOICE FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
   PG.NO=0
   LINE.CNT=0
   ERRMSG=''
   SP=" "
   SP2="  "
   SP10=STR(" ",10)
   SP42=STR(" ",42)
   DSH=STR("-",44)
   DSH40=DSH[1,40]
   DSH30=DSH[1,30]
   DSH13=DSH[1,13]
   EQL13=STR("=",13)
   DSH14=DSH[1,14]; * T20532
   EQL14=STR("=",14); * T20532
   DSH10=DSH[1,10]
   DSH8=DSH[1,8]
   DSH5=DSH[1,5]
   DSH4=DSH[1,4]
   DSH3=DSH[1,3]
   UNKNOWN=STR("?",30)
   INV.TTL=0
   GRAND.INV.TTL=0
   G.PRINT.TOT=0
   G.SHIP.TOT=0
   G.TAX.TOT=0
   NODATA=1
   LL = 5
   PRINTER ON
*
*---- GET COMPANY NUMBER
*
   CONO=''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO='END' THEN GOTO 99999
*
*---- GET REPORT HEADING (FIRST TWO LINES)
*
*T26493 v
*  IF TYPE.FLAG = "I" THEN RNAME="INVOICE EDIT LISTING"
*  IF TYPE.FLAG = "C" THEN RNAME="CREDIT MEMO EDIT LISTING"
*  IF TYPE.FLAG = "D" THEN RNAME="DEBIT MEMO EDIT LISTING"
*  RPT.NO="XXXX"
   RNAME = ""
   RPT.NO = XX<2>
   CONO.NAME = ""
*T26493 ^
   RDATE=DATE()
   CALL GET.PROG.HEAD(CONO,CONO.NAME,RNAME,RPT.NO,RDATE,HLINE1,HLINE2)
   HLINE3 = "FOR DIVISION ":DIV.NO
*
*---- MAIN PROCESSING
*
   DIV.CODE = DIV.NO
   READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE 
      ERRMSG = "DIV.SECURITY CONTROL RECORD MISSING"
      GOTO 93000
   END
   IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
         ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
         GOSUB 91000; GOTO 99999
      END
   END
   GOSUB 20
10 *
   READNEXT INV.ID ELSE
      IF NODATA THEN GOTO 99999
      GOTO 12
   END
   MATREADU DI.REC FROM DAILY.ORDER.INVOICE,INV.ID ELSE RELEASE; GOTO 10
   NODATA=0
   INV.NUM = INV.ID[4,99] "L#8"
   INV.DATE = OCONV(DI.DATE,"D2/") "R#8"
   CUST.NUM = DI.CUST.NO "L#6"
   GOSUB 30
   RR = 0
   CODE.CNT=COUNT(DI.CHG.CODE,VM) + (DI.CHG.CODE # '')
   FOR I=1 TO CODE.CNT
      CODE = DI.CHG.CODE<1,I> "L#4"
      CATG = DI.CHG.CAT<1,I>
      J.S = DI.TAX.JURS<1,I> "L#5"
      IF DI.CHG.ORDER = "" THEN
         SUB.ORDER = DI.ORDER.NO "L#8"
      END ELSE
         SUB.ORDER = DI.CHG.ORDER<1,I> "L#8"
      END
      QTY = DI.QUANTITY<1,I> "R#8"
      UM = DI.UM<1,I> "R#3"
      U.PRICE = OCONV(DI.UNIT.PRICE<1,I>, "MD4") "R#10"
      DESC = DI.DESC<1,I> "L#30"
      IF TYPE.FLAG = "C" THEN
         AMOUNT = DI.AMOUNT<1,I> * (-1)
         IF DI.CHG.CODE<1,I> # "SUB" AND DI.CHG.CODE<1,I> # "TOT" AND DI.CHG.CODE<1,I> # "SUBT" THEN
            INV.TTL = INV.TTL + AMOUNT
            GRAND.INV.TTL = GRAND.INV.TTL + AMOUNT
         END
         IF AMOUNT + 0 = 0 THEN AMOUNT = ""
         AMT = OCONV(AMOUNT,"MD2,-") "R#14"
      END ELSE
         IF DI.CHG.CODE<1,I> # "SUB" AND DI.CHG.CODE<1,I> # "TOT" AND DI.CHG.CODE<1,I> # "SUBT" THEN
            INV.TTL = INV.TTL + DI.AMOUNT<1,I>
            GRAND.INV.TTL = GRAND.INV.TTL + DI.AMOUNT<1,I>
         END
         IF DI.AMOUNT<1,I> + 0 = 0 THEN DI.AMOUNT<1,I> = ""
         AMT = OCONV(DI.AMOUNT<1,I>,"MD2,-") "R#14"
      END
      IF DI.CHG.CODE<1,I> # "SUB" AND DI.CHG.CODE<1,I> # "TOT" AND DI.CHG.CODE<1,I> # "SUBT" THEN
         BEGIN CASE
            CASE CATG = "SHP"
               IF TYPE.FLAG = "C" THEN
                  G.SHIP.TOT = G.SHIP.TOT + AMOUNT
               END ELSE
                  G.SHIP.TOT = G.SHIP.TOT + DI.AMOUNT<1,I>
               END
            CASE CATG = "TAX"
               IF TYPE.FLAG = "C" THEN
                  G.TAX.TOT = G.TAX.TOT + AMOUNT
               END ELSE
                  G.TAX.TOT = G.TAX.TOT + DI.AMOUNT<1,I>
               END
            CASE 1
               IF TYPE.FLAG = "C" THEN
                  G.PRINT.TOT = G.PRINT.TOT + AMOUNT
               END ELSE
                  G.PRINT.TOT = G.PRINT.TOT + DI.AMOUNT<1,I>
               END
         END CASE
      END
      GOSUB 40
   NEXT I
   GOSUB 50
   MATWRITE DI.REC ON DAILY.ORDER.INVOICE, INV.ID
   GOTO 10
12 *
   GOSUB 60
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20 *
   PRINT CHAR(12)
   PG.NO=PG.NO + 1
   PRINT HLINE1:PG.NO
   PRINT HLINE2
   PRINT SPACE(68):HLINE3
   PRINT
   PRINT SPACE(11):"INVOICE / CUSTOMER":SPACE(12):"CODE  J/S   ORDER#  QUANTITY":SP:"U/M":SP2:"U/PRICE":SPACE(12):"DESCRIPTION":SPACE(15):"AMOUNT"
   PRINT DSH40:SP:DSH4:SP:DSH5:SP:DSH8:SP:DSH8:SP:DSH3:SP:DSH10:SP:DSH30:SP:DSH14
   PRINT
   LINE.CNT=7
   RETURN
*
*---- FORMAT THE LEFT SIDE OF REPORT
*
30 *
   PL<1> = "INVOICE#: ":INV.NUM:"  ":INV.DATE
   PL<2> = "CUST #  : ":CUST.NUM
   PL<3> = SP10:DI.CUST.NAME "L#30"
   PL<4> = SP10:DI.ADDR1 "L#30"
   LL = 4
   IF DI.ADDR2 # "" THEN
      LL = LL + 1
      PL<LL> = SP10:DI.ADDR2 "L#30"
   END
   IF DI.ADDR3 # "" THEN
      LL = LL + 1
      PL<LL> = SP10:DI.ADDR3 "L#30"
   END
   IF DI.ADDR4 # "" THEN
      LL = LL + 1
      PL<LL> = SP10:DI.ADDR4 "L#30"
   END
   RETURN
*
*---- FORMAT THE RIGHT SIDE OF REPORT
*
40 *
   RR=RR + 1
   PR<RR> = CODE:SP:J.S:SP:SUB.ORDER:SP:QTY:SP:UM:SP:U.PRICE:SP:DESC:SP:AMT
   AMOUNT = "";AMT = ""
   RETURN
*
*---- PRINT INVOICE AND INVOICE TOTAL
*
50 *
   IF LL > RR THEN
      END.LINE = LL
   END ELSE
      END.LINE = RR
   END
   FOR GG = 1 TO END.LINE
      IF LINE.CNT >= 50 THEN GOSUB 20
      PRINT PL<GG> "L#41":PR<GG>
      LINE.CNT = LINE.CNT + 1
   NEXT GG
   INV.TOTAL = OCONV(INV.TTL,"MD2,-") "R#14"
*T25764 v
*  PRINT SPACE(116):DSH14
   PRINT "DUE DATE: ":OCONV(DI.DUE.DATE,"D2/")"L#8":SPACE(98):DSH14
*T25764 ^
   PRINT SPACE(107):"TOTAL :  ":INV.TOTAL 
   PRINT
   INV.TOTAL = 0
   INV.TTL = 0
   LINE.CNT=LINE.CNT + 3
   IF LINE.CNT >= 50 THEN GOSUB 20
   PR = '' ; PL = '' ; *T24502
*T26090 v
*  DI.LIST.DATE = DATE() ;*T25756
   IF PRINT.FLAG # "V" THEN DI.LIST.DATE = DATE()
*T26090 ^
   RETURN
*
*---- PRINT GRAND TOTAL
*
60 *
   IF LINE.CNT + 6 >= 54 THEN GOSUB 20
   GRAND.PRINT = OCONV(G.PRINT.TOT,"MD2,-") "R#14"
   GRAND.SHIP = OCONV(G.SHIP.TOT,"MD2,-") "R#14"
   GRAND.TAX = OCONV(G.TAX.TOT,"MD2,-") "R#14"
   GRAND.INV.TOTAL = OCONV(GRAND.INV.TTL,"MD2,-") "R#14"
   PRINT SPACE(116):EQL14
   PRINT SPACE(98):"PRINTING TOTAL :  ":GRAND.PRINT
   PRINT SPACE(92):"TRANSPORTATION TOTAL :  ":GRAND.SHIP
   PRINT SPACE(103):"TAX TOTAL :  ":GRAND.TAX
   PRINT SPACE(116):EQL14
   PRINT SPACE(101):"GRAND TOTAL :  ":GRAND.INV.TOTAL 
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   PRINTER OFF
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 *
END
