*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*********************************************************************
* REVISION    - [09.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - ORDER.PURGE
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC.
* DATE        - 09/16/92
* DESCRIPTION - 
*CSF 25475 - ALSO PURGE BOL_TAG IF IT EXISTS.
* CSF 22204 PURGEING EVERYTHING THAT IS COMPLETED OR CANCELED OR CLOSED
* REGARDLESS OF DATE
* TASK 19086 MWS 5/25/95 - REV10ABUG, REMOVE MULITI-VALUES FROM 
*            CUST.ORD.NUM FIELD IN CUSTOMER FILE.
*
* LLH KITTING
*
*T22288 rick 10/15/1997 * Year 2000 compliance. Make sure that date
*                         calcs use 4 digit years.
*T23278 markt 11/03/1998 * Enable division-level EOP processing.
*                          Correlate fiscal month to division.
*T26493 cmykleb 04/03/2002 * Change pgm to get the rpt # from the proc.
*T28396 lross 01/12/2005 * ORDER.PERIOD may be incorrect for users not
*                          on calendar FISCAL.
*********************************************************************
*--- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>FISCAL
*COPY>OPS.CPYLIB>ORDER
*COPY>OPS.CPYLIB>ORDER.DETAIL
*COPY>OPS.CPYLIB>ORDER.DETAIL.INQ
*COPY>OPS.CPYLIB>ORDER.INVOICE
*COPY>ICS.CPYLIB>FNGD.STATS
*COPY>ICS.CPYLIB>FNGD.ORDER.STATS
*COPY>OPS.CPYLIB>ORDER.RELEASE
*COPY>OPS.CPYLIB>BOL
*COPY>ARS.CPYLIB>OPEN.RECV
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*** SETUP FOR SYSTEM ERRMSG
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*** OPEN ALL FILES
*
   OPEN 'COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING' ; GOTO 93000
   OPEN 'CUSTOMER' TO CUSTOMER ELSE ERRMSG = 'CUSTOMER FILE IS MISSING' ; GOTO 93000
   OPEN 'ORDER' TO ORDER ELSE ERRMSG = 'ORDER FILE IS MISSING' ; GOTO 93000
   OPEN 'ORDER.DETAIL' TO ORDER.DETAIL ELSE ERRMSG = 'ORDER.DETAIL FILE IS MISSING' ; GOTO 93000
   OPEN 'CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING' ; GOTO 93000
   OPEN 'FNGD.STATS' TO FNGD.STATS ELSE ERRMSG = 'FNGD.STATS IS MISSING' ; GOTO 93000
   OPEN 'FNGD.ORDER.STATS' TO FNGD.ORDER.STATS ELSE ERRMSG = 'FNGD.ORDER.STATS IS MISSING' ; GOTO 93000
   OPEN 'FNGD.JOB.STATS' TO FNGD.JOB.STATS ELSE ERRMSG = 'FNGD.JOB.STATS IS MISSING' ; GOTO 93000
   OPEN 'JOB.FNGD.STATS' TO JOB.FNGD.STATS ELSE ERRMSG = 'JOB.FNGD.STATS IS MISSING' ; GOTO 93000
   OPEN 'ORDER.RELEASE' TO ORDER.RELEASE ELSE ERRMSG = 'ORDER.RELEASE IS MISSING' ; GOTO 93000
   OPEN 'BOL' TO BOL ELSE ERRMSG = 'BOL IS MISSING' ; GOTO 93000
   OPEN 'BOL_TAG' TO BOL_TAG ELSE ERRMSG = 'BOL_TAG IS MISSING' ; GOTO 93000
   OPEN 'PICK.TICKET' TO PICK.TICKET ELSE ERRMSG = 'PICK.TICKET IS MISSING' ; GOTO 93000
   OPEN 'ORDER.INVOICE' TO ORDER.INVOICE ELSE ERRMSG = 'ORDER.INVOICE IS MISSING' ; GOTO 93000
*
*** GET CONO FROM PROC
*
   PROCREAD KEY ELSE
      ERRMSG = 'THIS PROCESS SHOULD BE RUN FROM A PROC';GOTO 93000
   END
   CONO = KEY<1>
   MODE = KEY<4>
*T28396 v   DIV.CODE = KEY<99>;* T23278
   DIV.CODE = KEY<3>
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = 'COMPANY RECORD IS MISSING' ; GOTO 93000
   END
   MATREAD FISCAL.REC FROM CONTROL, CONO:"OPFISCAL" ELSE
      ERRMSG = "CANNOT LOCATE CONTROL, OPS FISCAL PERIOD"
      GOTO 93000
   END
***** T23278 v
   READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
      ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
      GOTO 93000
   END
   READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
      GOTO 93000
   END
*
   IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
         ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
         GOSUB 91000; GOTO 99999
      END
      LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING POS ELSE
         ERRMSG = "Division ":DIV.CODE:"  not found in Control File DIVISIONS Record."
         GOTO 93000
      END
   END ELSE
      POS = 1
   END
*T28396 v
   READ SALESDATES.REC FROM CONTROL, CONO:"SALESDATES" ELSE
     ERRMSG='Cannot read SALESDATES CONTROL record.'
     GOTO 93000
   END
   READ NUM.PERIODS FROM CONTROL, CONO:"ACCT.PERIODS" ELSE NUM.PERIODS = 12
   MONS='JAN':VM:'FEB':VM:'MAR':VM:'APR':VM:'MAY':VM:'JUN':VM
   MONS:='JUL':VM:'AUG':VM:'SEP':VM:'OCT':VM:'NOV':VM:'DEC'
*T28396 ^
***** T23278 ^
   IF CO.ARS = "Y" THEN
      OPEN "","OPEN.RECV" TO OPEN.RECV ELSE ERRMSG="OPEN.RECV FILE MISSING";GOTO 93000
   END
   IF MODE = "LIST" THEN
*T26493 v
*     RPT.NAME = 'ORDER PURGE LISTING'
*     RPT.NUM = 'XXXX'
      RPT.NAME = ""
      RPT.NUM = KEY<2>
*T26493 ^
      RPT.DATE = DATE()
      HD1 = ''
      HD2 = ''
      CALL GET.PROG.HEAD(CONO,'',RPT.NAME,RPT.NUM,RPT.DATE,HD1,HD2)
      PRINTER ON
      PG = 0
      LINES = 99
   END
*
*** MAIN PROCESS
*
   UPDATE.FLG = 0
   MAT EDIT.COM = ""
   TYP = 0 ; CALL EDIT.SUB ; FILL = "#"
   READV NO.DATE FROM CONTROL, CONO:"DET.DATES",1 ELSE
      NO.DATE = ""
   END
*
*** ENTER CUTOFF DAYS
10 *
   MATREAD OPCO.REC FROM CONTROL, CONO:"OPS" ELSE
      ERRMSG = "OPS COMPANY MAINTENANCE IS NOT SETUP"
      GOTO 93000
   END
   CUTOFF.PER = OPCO.PRG.DAY
   IF NOT(NUM(CUTOFF.PER)) THEN CUTOFF.PER = 3
*T28396IF CUTOFF.PER > 12 THEN
   IF CUTOFF.PER > NUM.PERIODS THEN
*T28396YY = INT(CUTOFF.PER / 12)
      YY = INT(CUTOFF.PER / NUM.PERIODS)
*T28396MM = MOD(CUTOFF.PER,12)
      MM = MOD(CUTOFF.PER,NUM.PERIODS)
   END ELSE
      YY = 0
      MM = CUTOFF.PER
   END
********** T23278 v
*  IF FR.CURR.PER[5,2] > MM THEN
*      CUTOFF.PER = FR.CURR.PER - MM
*      CUTOFF.PER = (FR.CURR.PER[1,4] - YY) : CUTOFF.PER[5,2]
*  END ELSE
*      YY = YY + 1
*      CUTOFF.PER = (FR.CURR.PER[5,2] + 12) - MM
*      CUTOFF.PER = STR("0",2-LEN(CUTOFF.PER)) : CUTOFF.PER
*      CUTOFF.PER = (FR.CURR.PER[1,4] - YY) : CUTOFF.PER
*  END
**********
   IF FR.CURR.PER<1,POS>[5,2] > MM THEN
      CUTOFF.PER = FR.CURR.PER<1,POS> - MM
      CUTOFF.PER = (FR.CURR.PER<1,POS>[1,4] - YY) : CUTOFF.PER[5,2]
   END ELSE
      YY = YY + 1
*T28396CUTOFF.PER = (FR.CURR.PER<1,POS>[5,2] + 12) - MM
      CUTOFF.PER = (FR.CURR.PER<1,POS>[5,2] + NUM.PERIODS) - MM
      CUTOFF.PER = STR("0",2-LEN(CUTOFF.PER)) : CUTOFF.PER
      CUTOFF.PER = (FR.CURR.PER<1,POS>[1,4] - YY) : CUTOFF.PER
   END
********** T23278 ^
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREADU ORD.REC FROM ORDER, ID ELSE
         RELEASE ORDER, ID
         GOTO 111
      END
      CONO = ID[1,3]
      ORDNO = ID[4,99]
      ICNT = DCOUNT(ORD.INV.NO,VM)
*CSF22204
      DT=OCONV(ORD.DATE,"D4/") ;*T22288 4 digit year
      YR=DT[7,4] ;*T22288 4 digit year
      MO=DT[1,2]
*T22288  IF YR > 70 AND YR <= 99 THEN
*T22288    YR="19":YR
*T22288  END ELSE
*T22288    YR="20":YR
*T22288  END
*T28396 v
      MON = MONS<1,MO>
      FIND MON IN SALESDATES.REC,1 SETTING FNDA,FNDV,FNDS THEN
        IF FNDA = 1 THEN
          FIND MON IN SALESDATES.REC,2 SETTING FNDA,FNDV,FNDS THEN
            MO = FNDA - 1'R%2'
          END
        END ELSE
          MO = FNDA - 1'R%2'
        END
      END
*T28396 ^
      ORDER.PERIOD=YR:MO
***
      BEGIN CASE
         CASE ORD.STATUS<1,1> # "CLOSED" AND ORD.STATUS<1,1> # "CANCEL" AND ORD.STATUS<1,1> # "COMPLETE"
            GOTO 111
         CASE SUM(ORD.INV.BAL) > 0
            GOTO 111
*CSF22204
         CASE ORDER.PERIOD >= CUTOFF.PER
            GOTO 111
***
         CASE ICNT > 0
            FND = 0
            FOR I = 1 TO ICNT UNTIL FND = 1
               IF CO.ARS = "Y" THEN
                  MATREAD OR.REC FROM OPEN.RECV, CONO:ORD.INV.NO<1,I> THEN FND = 1
               END
               IF NOT(FND) THEN
*CSF22204
                  MATREAD IVC.REC FROM ORDER.INVOICE, CONO:ORD.INV.NO<1,I> ELSE MAT IVC.REC=""
*        MATREAD IVC.REC FROM ORDER.INVOICE, ORD.INV.NO<1,I> THEN
*          IF IVC.ID[LEN(IVC.ID)-1,2] = "PB" THEN
*            IF IVC.PRE.BILL.MON = "" OR IVC.PRE.BILL.MON >= CUTOFF.PER THEN
*              FND = 1
*            END
*          END ELSE
*            IF IVC.PROC.MON >= CUTOFF.PER THEN
*              FND = 1
*            END
*          END
*        END
                  IF IVC.PROC.MON >= CUTOFF.PER THEN
                     FND = 1
                  END
               END
***
            NEXT I
            IF FND = 1 THEN GOTO 111
      END CASE
*
      SHPNO = "ALL"; STATUS = "L"
      CALL ORDER.LINE.UPD(CONO,ORDNO,SHPNO,STATUS)
      CONT.PROCESS = 1
      RCNT = DCOUNT(ORD.REL.NO,VM)
      BCNT = DCOUNT(ORD.BOL,VM)
      PCNT = DCOUNT(ODQ.PROD,VM)
      BEGIN CASE
         CASE SUM(ODQ.A.QTY) # 0
            CONT.PROCESS = 0
         CASE SUM(ODQ.R.QTY) # 0
            CONT.PROCESS = 0
         CASE RCNT # 0 OR BCNT # 0
            FOR R = 1 TO RCNT UNTIL NOT(CONT.PROCESS)
               MATREAD ORR.REC FROM ORDER.RELEASE, CONO:ORD.REL.NO<1,R> THEN
                  IF ORR.STATUS<1,1> # "COMPLETED" THEN
                     CONT.PROCESS = 0
                  END
               END
            NEXT R
            FOR B = 1 TO BCNT UNTIL NOT(CONT.PROCESS)
               MATREAD BOL.REC FROM BOL, CONO:ORD.BOL<1,B> THEN
                  IF BOL.STATUS # "CANCEL" AND BOL.INVOICE = "" THEN
                     CONT.PROCESS = 0
                  END
               END
            NEXT B
      END CASE
      FOR P = 1 TO PCNT UNTIL NOT(CONT.PROCESS)
         IWH.ID = CONO:ODQ.PROD<1,P>:"!":ODQ.WHSE<1,P>
         MATREAD FOS.REC FROM FNGD.ORDER.STATS, IWH.ID:"!":ORDNO:"!":ODQ.PROD.SEQ<1,P>:"!":ODQ.KIT<1,P> THEN
            IF FOS.A.QTY + FOS.R.QTY > 0 OR FOS.S.QTY < FOS.O.QTY THEN
               CONT.PROCESS = 0
            END
         END
      NEXT P
*PRINT "at 219 and CONT.PROCESS = ":CONT.PROCESS
*INPUT WAIT
      IF NOT(CONT.PROCESS) THEN
         GOTO 111
      END
*
      IF MODE = "LIST" THEN
         IF LINES > 55 THEN
            PG = PG + 1
            PRINT HD1:PG
            PRINT HD2
            PRINT
            PRINT "CONO  ORDER     DATE      STATUS"
            PRINT "----  --------  --------  ----------"
            LINES = 5
         END
         PRINT CONO"L#6":ORDNO"L#10":OCONV(ORD.DATE,"D2/")"L#10":ORD.STATUS<1,1>
         LINES = LINES + 1
      END ELSE
*
*---- Delete FNGD.ORDER.STATS
*
         FOR P = 1 TO PCNT
            IWH.ID = CONO:ODQ.PROD<1,P>:"!":ODQ.WHSE<1,P>
            MATREADU FGS.REC FROM FNGD.STATS, IWH.ID THEN
               PTR = 1
               LOOP
                  LOCATE ORDNO IN FGS.ORDER<1>,PTR SETTING OLOC THEN
                     FGS.ORDER = DELETE(FGS.ORDER,1,OLOC,0)
                     FGS.O.QTY = DELETE(FGS.O.QTY,1,OLOC,0)
                     FGS.B.QTY = DELETE(FGS.B.QTY,1,OLOC,0)
                  END ELSE
                     PTR = 0
                  END
               UNTIL PTR = 0 DO
                  PTR = 1
               REPEAT
               IF FGS.ORDER # "" OR FGS.JOB # "" THEN
                  MATWRITE FGS.REC ON FNGD.STATS, IWH.ID
               END ELSE
                  DELETE FNGD.STATS, IWH.ID
               END
            END ELSE
               RELEASE FNGD.STATS, IWH.ID
            END
            MATREADU FOS.REC FROM FNGD.ORDER.STATS, IWH.ID:"!":ORDNO:"!":ODQ.PROD.SEQ<1,P>:"!":ODQ.KIT<1,P> THEN
               DELETE FNGD.ORDER.STATS, IWH.ID:"!":ORDNO:"!":ODQ.PROD.SEQ<1,P>:"!":ODQ.KIT<1,P>
            END ELSE
               RELEASE FNGD.ORDER.STATS, IWH.ID:"!":ORDNO:"!":ODQ.PROD.SEQ<1,P>:"!":ODQ.KIT<1,P>
               MATREAD FOS.REC FROM FNGD.ORDER.STATS, IWH.ID:"!":ORDNO THEN
                  DELETE FNGD.ORDER.STATS, IWH.ID:"!":ORDNO
               END
               RELEASE FNGD.ORDER.STATS, IWH.ID:"!":ORDNO
            END
         NEXT P
*
*---- Delete ORDER.DETAIL
*
         XCNT = DCOUNT(ORD.SHIP.TO,VM)
         FOR X = 1 TO XCNT
            SHP.KEY = CONO:ORDNO:"!":ORD.SHIP.TO<1,X>
            MATREADU ORD.DET.REC FROM ORDER.DETAIL, SHP.KEY THEN
               DELETE ORDER.DETAIL, SHP.KEY
            END ELSE
               RELEASE ORDER.DETAIL, SHP.KEY
            END
         NEXT X
*
*---- Delete ORDER.RELEASE
*
         XCNT = DCOUNT(ORD.REL.NO,VM)
         FOR X = 1 TO XCNT
            REL.KEY = CONO:ORD.REL.NO<1,X>
            READU REC FROM ORDER.RELEASE, REL.KEY THEN
               DELETE ORDER.RELEASE, REL.KEY
            END ELSE
               RELEASE ORDER.RELEASE, REL.KEY
            END
         NEXT X
*
*---- Delete PICK.TICKET
*
         XCNT = DCOUNT(ORD.PICK.NO,VM)
         FOR X = 1 TO XCNT
            PICK.KEY = CONO:ORD.PICK.NO<1,X>
            READU REC FROM PICK.TICKET, PICK.KEY THEN
               DELETE PICK.TICKET, PICK.KEY
            END ELSE
               RELEASE PICK.TICKET, PICK.KEY
            END
         NEXT X
*
*---- Delete BOL
*
         XCNT = DCOUNT(ORD.BOL,VM)
         FOR X = 1 TO XCNT
            BOL.KEY = CONO:ORD.BOL<1,X>
            READU REC FROM BOL, BOL.KEY THEN
               DELETE BOL, BOL.KEY
*CSF 25475 v
               DELETE BOL_TAG, BOL.KEY
*CSF 25475 ^
            END ELSE
               RELEASE BOL, BOL.KEY
            END
         NEXT X
*TASK19086
*
*----Delete order number from customer file
*
*
         FND = 1
         OFND = ""
         MATREADU CUST.REC FROM CUSTOMER,CONO:ORD.CUST ELSE
            RELEASE CUSTOMER,CONO:ORD.CUST
            FND = 0
         END
         IF FND THEN
            LOCATE ORDNO IN CUST.ORD.NUM<1>,1 SETTING OFND ELSE OFND = 0
            IF OFND # 0 THEN
               CUST.ORD.NUM = DELETE(CUST.ORD.NUM,1,OFND,0)
               CUST.ORD.BAL = DELETE(CUST.ORD.BAL,1,OFND,0)
            END
            MATWRITE CUST.REC ON CUSTOMER,CONO:ORD.CUST
         END
*** ^
*---- Delete ORDER
*
         DELETE ORDER, ID
      END
      UPDATE.FLG = 1
111 *
      RELEASE ORDER, ID
   REPEAT
   IF UPDATE.FLG = 0 THEN
      PROCWRITE "END"
   END
   GOTO 99999
*
*** PRINT ERRMSG
*
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   PRINTER OFF
   PRINTER CLOSE
   PRINT @(-1)
END
