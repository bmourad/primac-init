*COPY>CPYLIB>COM1
**************************************************************************
* REVISION    - [00.0]
* COPYRIGHT   - 1993 Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPS
* PROGRAM     - UNPRICED.FNGD.RPT
* AUTHOR      - S.L.Fritz
* DATE        - 04/30/93
* DESCRIPTION - Print the report for unpriced FNGD records.
*
*T26484 cmykleb 03/11/2002 * Change report to work with new rev12
*                            changes.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
**************************************************************************
*
*---- DATA Structure Libraries
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- File open
*
   OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG = 'CUSTOMER';GOTO 93000
   OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE ERRMSG = 'INV_RECEIPTS';GOTO 93000
   OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG = 'INVENTORY';GOTO 93000
*
*---- Initialize variables
*
   TRUE = 1
   OTHERS = 1
   VOID = ''
   NULL = ''
   FALSE = 0
   PAGE.NO = 0
   PAGE.NO = 0
   LINES.THIS.PAGE = 0
   PAGE.HEADING.SIZE = 5
   PAGE.SIZE = 55
   EQU CONO         TO BUFFER<1>
   EQU COMPANY.NAME TO BUFFER<2>
   EQU CUSTOMER.NUM TO BUFFER<3>
   SHD1 = 'RECEIPT #'
   SHD1 = SHD1:'JOB-NO   '
   SHD1 = SHD1:'CUST-# '
   SHD1 = SHD1:'NAME                           '
   SHD1 = SHD1:'PRODUCT         '
   SHD1 = SHD1:'DESCRIPTION                    '
   SHD1 = SHD1:'WHSE '
   SHD2 = STR('-',9):SPACE(1)
   SHD2 = SHD2:STR('-',8):SPACE(1)
   SHD2 = SHD2:STR('-',6):SPACE(1)
   SHD2 = SHD2:STR('-',30):SPACE(1)
   SHD2 = SHD2:STR('-',15):SPACE(1)
   SHD2 = SHD2:STR('-',30):SPACE(1)
   SHD2 = SHD2:STR('-',4):SPACE(1)
   PRINTER ON
   PROCREAD BUFFER ELSE
      ERRMSG = 'Must run this program from a REPORT.SCRN PROC.'
      GOTO 93000
   END
*T26493 v
*  RPT.NAME = 'UNPRICED FINISHED GOODS REPORT'
*  RPT.NUM = 'XXXX'
   RPT.NAME = ""
   RPT.NUM = BUFFER<2>
*T26493 ^
   RPT.DATE = DATE()
   HD1 = ''
   HD2 = ''
   CALL GET.PROG.HEAD(CONO,VOID,RPT.NAME,RPT.NUM,RPT.DATE,HD1,HD2)
   GOSUB 20000
*
10000 *
*
   READNEXT IR.KEY ELSE GOTO 99999
   RECEIPT.NO = IR.KEY[4,9]
   MATREAD INVR.REC FROM INV_RECEIPTS, IR.KEY ELSE
      ERRMSG = 'Can not read the ':RECEIPT.NO:' record from the INV_RECEIPTS file.'
      GOTO 10000
   END
   IF INVR.ORDER<1,2> # "" THEN GOTO 10000 ; * HAS AN ORDER #
   READV CUST.NAME FROM CUSTOMER, CONO:INVR.CUST, 1 ELSE
      CUST.NAME = 'Customer record missing'
   END
   MATREAD INV.REC FROM INVENTORY, CONO:INVR.PROD ELSE
      INV.FULL.DESC = 'Inventory records description missing'
   END
   IF INV.BASE.SKU # '' THEN
      READV DESCRIPTION.ONE FROM INVENTORY, CONO:INV.BASE.SKU, 2 ELSE
         DESCRIPTION.ONE = "Inventory description missing from file!"
      END
      DESCRIPTION.TWO = INV.FULL.DESC
   END ELSE
      DESCRIPTION.ONE = INV.FULL.DESC
      DESCRIPTION.TWO = ''
   END
   LINES.THIS.PAGE = LINES.THIS.PAGE + 1
   IF LINES.THIS.PAGE > PAGE.SIZE THEN
      GOSUB 20000
      LINES.THIS.PAGE = LINES.THIS.PAGE + 1
   END
   PRINT.LINE = RECEIPT.NO'L#9':SPACE(1)
   PRINT.LINE = PRINT.LINE:INVR.JOB'L#8':SPACE(1)
   PRINT.LINE = PRINT.LINE:INVR.CUST'L#6':SPACE(1)
   PRINT.LINE = PRINT.LINE:CUST.NAME'L#30':SPACE(1)
   PRINT.LINE = PRINT.LINE:INVR.PROD'L#15':SPACE(1)
   PRINT.LINE = PRINT.LINE:DESCRIPTION.ONE'L#30':SPACE(1)
   PRINT.LINE = PRINT.LINE:INVR.ORG.WHSE'L#4':SPACE(1)
   PRINT PRINT.LINE
   IF DESCRIPTION.TWO # '' THEN
      LINES.THIS.PAGE = LINES.THIS.PAGE + 1
      PRINT SPACE(70):DESCRIPTION.TWO'L#30'
   END
   GOTO 10000
*
20000 * Print the heading
*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT SHD1
   PRINT SHD2
   LINES.THIS.PAGE = PAGE.HEADING.SIZE
   RETURN
93000 CRT ERRMSG ; INPUT ANS
99999 PRINTER OFF
END
