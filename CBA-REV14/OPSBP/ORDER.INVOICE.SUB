DO NOT USE - SEE OPS.INVOICE.SUB
 SUBROUTINE ORDER.INVOICE.SUB (CONO,ORDNO,IVC.NO,MENU,PROG.FLAG)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
***COPY>JCS.CPYLIB>COM.INVOICE
*********************************************************************
* REVISION - [08.1]
* Copyright 1982 by Vercom Software, Inc. (dba CBA)
* SOURCE   - OPSBP
* PROGRAM  - ORDER.INVOICE.SUB
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 11/17/86
* DESCRIPTION
* This program is used to create and maintain order related invoices
*********************************************************************
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>INVOICE.CODE
*COPY>PMC.CPYLIB>TAX
*COPY>PMC.CPYLIB>SALES.CODE
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>VOID.INVOICES
*COPY>OPS.CPYLIB>ORDER
*COPY>OPS.CPYLIB>ORDER.DETAIL.INQ
*COPY>JCS.CPYLIB>INVOICE
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
 MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
 ESN = ECD.SCRN.NO
 FOR SCV = 1 TO SCV.REC.SIZE
   SCV.REC(SCV)<ESN> = ""
 NEXT SCV
*
 REF.CNT = 0
 RNO = ""
 CURR.RNO = ""
 LINE.COUNT = 6
 INVOICE.TOTAL = 0
 N.IVC.NO = ""
 MAT EDIT.COM = ""
 MAT GEN.XREF.REC = ""
 TYP = 0
 CALL EDIT.SUB
 FILL = "#"
 ERRMSG = ""
 MODE = ""
 TMP.TAX.RATE = ""
 TMP.TAX.CALC = ""
 TMP.CUST.PO = ORD.PO
 CURR.ORDNO = ORDNO
 DESC.LEN = 40; **********  SCREEN DEPENDENT  **********
 IF PROG.FLAG # "I" THEN
   GOSUB 21000
 END
 IF IVC.CHG.JOB = "" THEN
   ICODE = "JOB"
   MAT INC.REC = ""; INC.CATEGORY = "OTH"
   UN.PRC = 3000; U.M = "M"; FACTOR = 1000
   QTY = SUM(ODQ.S.QTY) - SUM(ODQ.I.QTY)
   IVC.CHG.JOB<1,-1> = ORDNO
   IVC.CHG.CODE<1,-1> = ICODE
   IVC.CHG.CAT<1,-1> = INC.CATEGORY
   IVC.TAX.JURS<1,-1> = ""
   IVC.QUANTITY<1,-1> = QTY
   IVC.UM<1,-1> = U.M
   IVC.DESC<1,-1> = "ORDER FINISHED GOODS"
   IVC.UNIT.PRICE<1,-1> = UN.PRC
   IVC.HIDDEN<1,-1> = INC.HIDDEN
   IVC.TAXABLE<1,-1> = ""
   IF UN.PRC # "" THEN
     AMT = (QTY/FACTOR) * UN.PRC
     IF AMT > 0 THEN
       AMT = INT(AMT/10+0.5)
     END ELSE
       AMT = INT(AMT/10-0.5)
     END
   END ELSE
     AMT = 0
   END
   IVC.AMOUNT<1,-1> = AMT
 END
*
*---- MAIN PROCESSING
100 *
 SCV.REC(27)<ESN> = DATE()
 ECD.NUM = 27; ECD.ACTION = 5; CALL SCRN.EDIT
 GOSUB 9000
 ECD.ACTION = 3; CALL SCRN.EDIT
 RNO = 1
 CURR.RNO = 1
 IF REF.CNT > 0 THEN GOSUB 6200
*
*---- GET OPERATOR REQUEST
500 *
 GOSUB 5000
 ACTION = ECD.RET.VALUE
502 *
 BEGIN CASE
   CASE NUM(ACTION) AND ACTION >=  1 AND ACTION <=  5
     N = ACTION
     ON N GOSUB 1100,1200,1300,1400,1500
   CASE ACTION = "A"
     MODE = "A"
     DONE = 0
     FOR RNO = REF.CNT + 1 TO 99 UNTIL DONE
       IF REF.CNT > 0 THEN GOSUB 10000
       GOSUB 6000
       IF ECD.RET.VALUE = "END" THEN
         REF.CNT = RNO - 1
         AREF = RNO
         AREF.CNT = DCOUNT(IVC.CHG.CODE,VM)
         FOR RNO = AREF.CNT TO AREF STEP -1
           GOSUB 600
         NEXT RNO
         DONE = 1
       END ELSE
         REF.CNT = RNO
       END
       GOSUB 6200
     NEXT RNO
     RNO = REF.CNT
     CURR.RNO = ""
     GOSUB 10000
   CASE ACTION = "C" AND REF.CNT > 0
     MODE = "C"
     GOSUB 5100
     IF NUM(ECD.RET.VALUE) AND ECD.RET.VALUE # "" THEN
       RNO = ECD.RET.VALUE
       GOSUB 6000
       GOSUB 6200
     END
     GOSUB 10000
   CASE ACTION = "D" AND REF.CNT > 0
     MODE = "D"
     GOSUB 5100
     IF NUM(ECD.RET.VALUE) AND ECD.RET.VALUE # "" THEN
       RNO = ECD.RET.VALUE
       GOSUB 600
       GOSUB 6200
       REF.CNT = REF.CNT - 1
       IF RNO > 1 THEN RNO = RNO - 1
       CURR.RNO = ""
       GOSUB 10000
     END
   CASE ACTION = "I" AND REF.CNT > 0
     MODE = "I"
     GOSUB 5100
     IF NUM(ECD.RET.VALUE) AND ECD.RET.VALUE # "" THEN
       RNO = ECD.RET.VALUE
       IVC.CHG.JOB = INSERT(IVC.CHG.JOB,1,RNO,0,"")
       IVC.CHG.CODE = INSERT(IVC.CHG.CODE,1,RNO,0,"")
       IVC.CHG.CAT = INSERT(IVC.CHG.CAT,1,RNO,0,"")
       IVC.TAX.JURS = INSERT(IVC.TAX.JURS,1,RNO,0,"")
       IVC.QUANTITY = INSERT(IVC.QUANTITY,1,RNO,0,"")
       IVC.UM = INSERT(IVC.UM,1,RNO,0,"")
       IVC.DESC = INSERT(IVC.DESC,1,RNO,0,"")
       IVC.UNIT.PRICE = INSERT(IVC.UNIT.PRICE,1,RNO,0,"")
       IVC.AMOUNT = INSERT(IVC.AMOUNT,1,RNO,0,"")
       IVC.HIDDEN = INSERT(IVC.HIDDEN,1,RNO,0,"")
       IVC.TAXABLE = INSERT(IVC.TAXABLE,1,RNO,0,"")
       TMP.TAX.RATE = INSERT(TMP.TAX.RATE,1,RNO,0,"")
       TMP.TAX.CALC = INSERT(TMP.TAX.CALC,1,RNO,0,"")
       SCV.REC(13) = INSERT(SCV.REC(13),ESN,RNO,0,"")
       SCV.REC(14) = INSERT(SCV.REC(14),ESN,RNO,0,"")
       SCV.REC(30) = INSERT(SCV.REC(30),ESN,RNO,0,"")
       SCV.REC(15) = INSERT(SCV.REC(15),ESN,RNO,0,"")
       SCV.REC(31) = INSERT(SCV.REC(31),ESN,RNO,0,"")
       SCV.REC(17) = INSERT(SCV.REC(17),ESN,RNO,0,"")
       REF.CNT = REF.CNT + 1
       CURR.RNO = ""
       GOSUB 10000
       GOSUB 6000
       IF ECD.RET.VALUE = "END" THEN
         GOSUB 600
         REF.CNT = REF.CNT - 1
         CURR.RNO = ""
         GOSUB 10000
       END ELSE
         GOSUB 6200
         GOSUB 10000
       END
     END
   CASE ACTION = "S" AND REF.CNT > 0
     RNO = CURR.RNO + LINE.COUNT
     IF RNO > REF.CNT THEN RNO = 1
     GOSUB 10000
   CASE ACTION = "SF" AND REF.CNT > 0
     RNO = CURR.RNO + LINE.COUNT
     IF RNO > REF.CNT THEN RNO = 1
     GOSUB 10000
   CASE ACTION = "ST" AND REF.CNT > 0
     RNO = 1
     GOSUB 10000
   CASE ACTION = "SR" AND REF.CNT > 0
     RNO = CURR.RNO - LINE.COUNT
     IF RNO < 1 THEN RNO = 1
     GOSUB 10000
   CASE ACTION = "SB" AND REF.CNT > 0
     RNO = REF.CNT
     GOSUB 10000
   CASE ACTION = "N"
     SHPNO = ""
     ECD.SCRN.NO = 3
     CALL INVOICE.LINE.SUB(CONO, ORDNO, SHPNO)
     ECD.SCRN.NO = ESN; ECD.ACTION = 3; CALL SCRN.EDIT
   CASE ACTION = "P"
     ECD.NUM = 22; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE = "Y" THEN
       JCNT = DCOUNT(O.NOS,AM)
       FOR J = 1 TO JCNT
         MATREADU ORD.REC FROM ORDER, CONO:O.NOS<J> ELSE
           RELEASE ORDER, CONO:O.NOS<J>
           ERRMSG = "CANNOT LOCATE ORDER RECORD - ":O.NOS<J>
           GOSUB 90000; GOTO 504
         END
         LOCATE IVC.NO IN ORD.INV.NO<1>,1 SETTING FND ELSE FND = 0
         IF FND = 0 THEN
           RELEASE ORDER, CONO:O.NOS<J>
         END ELSE
           IF ORD.INV.DATE<1,FND> = "" THEN
             ORD.INV.NO = DELETE(ORD.INV.NO,1,FND,0)
             ORD.INV.DATE = DELETE(ORD.INV.DATE,1,FND,0)
             ORD.INV.CAT = DELETE(ORD.INV.CAT,1,FND,0)
             ORD.INV.AMT = DELETE(ORD.INV.AMT,1,FND,0)
             ORD.INV.BAL = DELETE(ORD.INV.BAL,1,FND,0)
           END
           MATWRITE ORD.REC ON JOB, CONO:O.NOS<J>
         END
504    NEXT J
       MAT VOI.REC = ""
       VOI.DATE = IVC.DATE
       VOI.ORDNO = IVC.JOB.NO
       VOI.CUST.NO = IVC.CUST.NO
       VOI.CUST.NAME = IVC.CUST.NAME
       VOI.ADDR1 = IVC.ADDR1
       VOI.ADDR2 = IVC.ADDR2
       VOI.ADDR3 = IVC.ADDR3
       VOI.ADDR4 = IVC.ADDR4
       MATREAD ORD.REC FROM JOB, CONO:ORDNO ELSE MAT ORD.REC = ""
       VOI.PO = ORD.PO
       VOI.SLSMAN = ORD.SLSMN
       VOI.DEL.DATE = DATE()
       MATWRITE VOI.REC ON VOID.INVOICES, CONO:IVC.NO
       DELETE DAILY.INVOICE, CONO:IVC.NO
       IVC.NO = ""
       GOTO 99999
     END
   CASE ACTION = "E" OR ACTION = "END"
     GOTO 99999
   CASE ACTION = "F" AND INVOICE.TOTAL < 0
     ERRMSG = "Invalid Total Amount - Must be positive"
     GOSUB 90000
   CASE ACTION = "F" AND IVC.PROC.DATE = ""
     REF.CNT = DCOUNT(IVC.CHG.CODE,VM)
     BLK = 0
     FOR I = 1 TO REF.CNT UNTIL BLK
       IF IVC.CHG.CAT<1,I> # "CMT" THEN BLK = 1
     NEXT I
     IF BLK = 0 THEN
       ERRMSG = "One Valid Invoice Line Entry is Required Before Filling"
       GOSUB 90000; GOTO 599
     END
     BEGIN CASE
       CASE MENU = "CREDIT" OR MENU = "DEBIT"
         IVC.LAST = "N"
       CASE MENU = "PREBILL"
         IVC.LAST = "N"
       CASE 1
         GOSUB 21000; GOSUB 22000
         IF MATCH.ERROR THEN
           X = 0; Y = 21; TYP = 8
           PMSG = "Continue (Y/N)"
           CALL EDIT.SUB
           IF VALUE = "N" THEN GOTO 500
         END
         X = 0; Y = 21; TYP = 8
         PMSG = "Is this the last invoice (Y/N)"
         CALL EDIT.SUB
         IF VALUE = "END" THEN GOTO 500
         IVC.LAST = VALUE
     END CASE
     BEGIN CASE
       CASE MENU = "CREDIT" OR MENU = "DEBIT"
         IVC.PRE.BILL = "N"
         IVC.WIP.TYPE = "ALL"
         IVC.WIP.DATE = "ALL"
         IVC.WIP.PRCNT = 0
       CASE MENU = "PREBILL"
         IVC.PRE.BILL = "N"
         IVC.WIP.TYPE = "ALL"
         IVC.WIP.DATE = "ALL"
         IVC.WIP.PRCNT = 0
       CASE IVC.LAST = "Y"
         IVC.PRE.BILL = "Y"
         IVC.WIP.TYPE = "ALL"
         IVC.WIP.DATE = "ALL"
         IVC.WIP.PRCNT = 10000
       CASE 1
         PRE.BILL.PRESENT = 0
         ICNT = DCOUNT(IVC.CHG.CODE,VM)
         FOR I = 1 TO ICNT UNTIL PRE.BILL.PRESENT
           JB = IVC.CHG.JOB<1,I>
           IF JB # "" THEN
             LOCATE JB IN O.NOS,1 SETTING FND ELSE FND = 0
             IF FND AND PRE.BILL.BAL<1,FND> = "Y" THEN PRE.BILL.PRESENT = 1
           END
         NEXT I
         IF PRE.BILL.PRESENT THEN
           X = 0; Y = 21; TYP = 8
           PMSG = "Credit sales for pre-bills (Y/N)"
           CALL EDIT.SUB
           IF VALUE = "END" THEN GOTO 500
           IVC.PRE.BILL = VALUE
         END ELSE
           IVC.PRE.BILL = "N"
         END
         CALL INVOICE.WIP.SUB(CONO, WIP.FLAG, IVC.NO, MENU, PROG.FLAG)
         IF WIP.FLAG = "END" OR IVC.NO = "N" THEN
           ECD.SCRN.NO = ESN; ECD.ACTION = 3; CALL SCRN.EDIT
         END
         IF WIP.FLAG = "END" THEN GOTO 500
     END CASE
     IF IVC.NO = "N" THEN
550    READU IVC.NO FROM CONTROL, CONO:"NEXT.INVOICE.NO" ELSE IVC.NO = 1
       N.IVC.NO = IVC.NO + 1
       IVC.NO = STR("0",6-LEN(IVC.NO)):IVC.NO
       WRITE N.IVC.NO ON CONTROL, CONO:"NEXT.INVOICE.NO"
       BEGIN CASE
         CASE MENU = "CREDIT"
           IVC.NO = IVC.NO:"CM"
         CASE MENU = "DEBIT"
           IVC.NO = IVC.NO:"DM"
         CASE MENU = "PREBILL"
           IVC.NO = IVC.NO:"PB"
       END CASE
       READ REC FROM DAILY.INVOICE,CONO:IVC.NO ELSE GOTO 560
       GOTO 550
560    READ REC FROM INVOICE,CONO:IVC.NO ELSE GOTO 565
       GOTO 550
565    READ REC FROM VOID.INVOICES,CONO:IVC.NO ELSE GOTO 570
       GOTO 550
570    IF CO.ARS = "Y" THEN
         READ REC FROM OPEN.RECV,CONO:IVC.NO ELSE GOTO 580
         GOTO 550
580    END
       ECD.NUM = 7; SCV.REC(7)<ESN> = IVC.NO
       ECD.ACTION = 5; CALL SCRN.EDIT
       ERRMSG = "Please note the new number"; GOSUB 90000
     END
     JCNT = DCOUNT(IVC.CHG.JOB,VM)
     FOR J = 1 TO JCNT
       IF IVC.CHG.JOB<1,J> # "" THEN
         MATREADU ORD.REC FROM ORDER, CONO:IVC.CHG.JOB<1,J> ELSE
           RELEASE ORDER, IVC.CHG.JOB<1,J>
           ERRMSG = "CANNOT LOCATE ORDER RECORD - ":IVC.CHG.JOB<1,J>
           GOSUB 90000; GOTO 590
         END
         LOCATE IVC.NO IN ORD.INV.NO<1>,1 SETTING PTR ELSE
           PTR = DCOUNT(ORD.INV.NO,VM) + 1
           ORD.INV.NO<1,PTR> = IVC.NO
         END
         ORD.PO = TMP.CUST.PO
         MATWRITE ORD.REC ON JOB, CONO:IVC.CHG.JOB<1,J>
       END
590  NEXT J
     MATWRITE IVC.REC ON DAILY.INVOICE, CONO:IVC.NO
     GOTO 99999
599 *
 END CASE
 GOTO 500
*
*---- DELETE SPECIFIC LINE
600 *
 IVC.CHG.JOB = DELETE(IVC.CHG.JOB,1,RNO,0)
 IVC.CHG.CODE = DELETE(IVC.CHG.CODE,1,RNO,0)
 IVC.CHG.CAT = DELETE(IVC.CHG.CAT,1,RNO,0)
 IVC.TAX.JURS = DELETE(IVC.TAX.JURS,1,RNO,0)
 IVC.TAXABLE = DELETE(IVC.TAXABLE,1,RNO,0)
 IVC.QUANTITY = DELETE(IVC.QUANTITY,1,RNO,0)
 IVC.UM = DELETE(IVC.UM,1,RNO,0)
 IVC.DESC = DELETE(IVC.DESC,1,RNO,0)
 IVC.UNIT.PRICE = DELETE(IVC.UNIT.PRICE,1,RNO,0)
 IVC.AMOUNT = DELETE(IVC.AMOUNT,1,RNO,0)
 IVC.HIDDEN = DELETE(IVC.HIDDEN,1,RNO,0)
 TMP.TAX.RATE = DELETE(TMP.TAX.RATE,1,RNO,0)
 TMP.TAX.CALC = DELETE(TMP.TAX.CALC,1,RNO,0)
 SCV.REC(13) = DELETE(SCV.REC(13),ESN,RNO,0)
 SCV.REC(14) = DELETE(SCV.REC(14),ESN,RNO,0)
 SCV.REC(30) = DELETE(SCV.REC(30),ESN,RNO,0)
 SCV.REC(15) = DELETE(SCV.REC(15),ESN,RNO,0)
 SCV.REC(31) = DELETE(SCV.REC(31),ESN,RNO,0)
 SCV.REC(17) = DELETE(SCV.REC(17),ESN,RNO,0)
 RETURN
*- GET CUSTOMER ID AND XREF FOR BILL TO
1100 *
 SCV.REC(1)<ESN> = IVC.CUST.NO
 ECD.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IF ECD.RET.VALUE = "???" THEN
   ECD.NUM = 2; ECD.SUB.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 1100
   MAT GEN.XREF.REC = ""
   GXR.CO = CONO
   GXR.NAME = "CUSTOMER"
   GXR.XREF = CUSTOMER.XREF
   GXR.FILE = CUSTOMER
   GXR.SRCH.ID = ECD.RET.VALUE
   CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
   IF GXR.ACTION # "X" THEN
     ECD.ACTION = 2; CALL SCRN.EDIT
     ECD.ACTION = 3; CALL SCRN.EDIT
   END
   IF GXR.ID = "" THEN
     SCV.REC(1)<ESN> = IVC.CUST.NO
     ECD.NUM = 1; ECD.ACTION = 5; CALL SCRN.EDIT
     SCV.REC(2)<ESN,1> = IVC.CUST.NAME
     ECD.NUM = 2; ECD.SUB.NUM = 1; ECD.ACTION = 5; CALL SCRN.EDIT
     GOTO 1100
   END
   ECD.RET.VALUE = GXR.ID
   SCV.REC(1)<ESN> = GXR.ID
   ECD.NUM = 1; ECD.ACTION = 5; CALL SCRN.EDIT
 END
 MATREAD CUST.REC FROM CUSTOMER, CONO:ECD.RET.VALUE ELSE
   ERRMSG = "Invalid Customer ID"
   GOSUB 90000
   GOTO 1100
 END
 IVC.CUST.NO = ECD.RET.VALUE
 IVC.CUST.NAME = CUST.NAME
 IVC.ADDR1 = CUST.ADDR1
 IVC.ADDR2 = CUST.ADDR2
 IVC.ADDR3 = CUST.ADDR3
 IVC.ADDR4 = CUST.ADDR4:" ":CUST.ZIP
 IVC.ATTENTION = CUST.ATTENTION
 SCV.REC(1)<ESN> = IVC.CUST.NO
 SCV.REC(2)<ESN,1> = IVC.CUST.NAME
 SCV.REC(2)<ESN,2> = IVC.ADDR1
 SCV.REC(2)<ESN,3> = IVC.ADDR2
 SCV.REC(2)<ESN,4> = IVC.ADDR3
 SCV.REC(2)<ESN,5> = IVC.ADDR4
 SCV.REC(28)<ESN> = IVC.ATTENTION
 ECD.NUM = 1; ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 2; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 28; ECD.ACTION = 5; CALL SCRN.EDIT
 RETURN
*
*---- GET CUSTOMER NAME & ADDRESS
1200 *
 ECD.NUM = 2; ECD.SUB.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IF ECD.RET.VALUE = "" THEN GOTO 1200
 IVC.CUST.NAME = ECD.RET.VALUE
 ECD.NUM = 2; ECD.SUB.NUM = 2; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.ADDR1 = ECD.RET.VALUE
 ECD.NUM = 2; ECD.SUB.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.ADDR2 = ECD.RET.VALUE
 ECD.NUM = 2; ECD.SUB.NUM = 4; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.ADDR3 = ECD.RET.VALUE
 ECD.NUM = 2; ECD.SUB.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.ADDR4 = ECD.RET.VALUE
 RETURN
*
*---- GET ATTENTION OF
1300 *
 ECD.NUM = 28; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.ATTENTION = ECD.RET.VALUE
 RETURN
*
*---- GET CUSTOMER P/O NUMBER
1400 *
 ECD.NUM = 9; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 TMP.CUST.PO = ECD.RET.VALUE
 RETURN
*
*---- GET INVOICE DATE
1500 *
* ECD.MINV = DATE()-365
 ECD.NUM = 8; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.DATE = ECD.RET.VALUE
 RETURN
*
*---- GET OPERATOR REQUEST
5000 *
 IF PROG.FLAG = "I" OR IVC.PROC.DATE#"" THEN
   ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
 END ELSE
   ECD.NUM = 20; ECD.ACTION = 4; CALL SCRN.EDIT
 END
 IF ECD.RET.VALUE = "END" THEN RETURN
 RETURN
*
*---- GET LINE NUMBER
5100 *
 SCV.REC(19)<ESN> = ""
 ECD.MINV = CURR.RNO; ECD.MAXV = CURR.RNO + LINE.COUNT - 1
 IF ECD.MAXV > REF.CNT THEN ECD.MAXV = REF.CNT
 ECD.NUM = 19; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 RETURN
*
*---- GET MULTI-LINE DATA
6000 *
 ECD.NUM = 12; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
 IF SCV.REC(22)<ESN,RNO> # "" THEN
   SCV.REC(22)<ESN,RNO> = ""
   SCV.REC(23)<ESN,RNO> = ""
   SCV.REC(24)<ESN,RNO> = ""
   ECD.NUM = 22; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 23; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 24; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
 END
*---- GET INVOICE CODE
6010 *
 SCV.REC(13)<ESN,RNO> = IVC.CHG.CODE<1,RNO>
 ECD.NUM = 13; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 ICODE = ECD.RET.VALUE
 BEGIN CASE
   CASE ICODE = "JOB1" OR ICODE = "JOB"
     LOCATE "TOT" IN IVC.CHG.CODE<1>,1 SETTING FND ELSE FND = 999
     IF FND < RNO THEN
       ERRMSG = "Only comment lines may be entered after total line"
       GOSUB 90000; GOTO 6010
     END
     MAT INC.REC = ""
     INC.CATEGORY = "OTH"
   CASE ICODE = "SUB" OR ICODE = "SUBT"
     LOCATE "TOT" IN IVC.CHG.CODE<1>,1 SETTING FND ELSE FND = 999
     IF FND < RNO THEN
       ERRMSG = "Only comment lines may be entered after total line"
       GOSUB 90000; GOTO 6010
     END
     MAT INC.REC = ""
     INC.CATEGORY = "CMT"
     INC.DESC = "SUB TOTAL"
     INC.DESC = SPACE(DESC.LEN-LEN(INC.DESC)-4):INC.DESC
   CASE ICODE = "TOT"
     FOR TN = RNO+1 TO REF.CNT
       IF IVC.CHG.CAT<1,TN> # "CMT" THEN
         ERRMSG = "Total line can only preceed comment lines"
         GOSUB 90000; GOTO 6010
       END
     NEXT TN
     MAT INC.REC = ""
     INC.CATEGORY = "CMT"
     INC.DESC = "TOTAL"
     INC.DESC = SPACE(DESC.LEN-LEN(INC.DESC)-4):INC.DESC
   CASE ICODE = "CMT"
     MAT INC.REC = ""
     INC.CATEGORY = "CMT"
   CASE 1
     MATREAD INC.REC FROM INVOICE.CODE, CONO:ICODE ELSE
       ERRMSG = "Invalid code"
       GOSUB 90000; GOTO 6010
     END
     IF INC.HIDDEN = "Y" AND RNO = 1 THEN
       ERRMSG = "Hidden line cannot be first line entered"
       GOSUB 90000; GOTO 6010
     END
     IF INC.CATEGORY # "CMT" THEN
       LOCATE "TOT" IN IVC.CHG.CODE<1>,1 SETTING FND ELSE FND = 999
       IF FND < RNO THEN
         ERRMSG = "Only comment lines may be entered after total line"
         GOSUB 90000; GOTO 6010
       END
     END
 END CASE
 IF IVC.CHG.CODE<1,RNO>#ICODE THEN DESC.FLAG = 1
 IVC.CHG.CODE<1,RNO> = ICODE
 IVC.CHG.CAT<1,RNO> = INC.CATEGORY
 IVC.HIDDEN<1,RNO> = INC.HIDDEN
*---- GET JOB NUMBER
6012 *
 BEGIN CASE
   CASE INC.CATEGORY = "CMT"
     IVC.CHG.JOB<1,RNO> = ""
   CASE 1
     IVC.CHG.JOB<1,RNO> = ORDNO
 END CASE
 BEGIN CASE
   CASE ICODE = "JOB1"
*INC.DESC = ORD.DESC<1,1>[1,DESC.LEN]
     INC.DESC = ""
   CASE ICODE = "JOB"
     NEW.DESC = "FINISHED GOODS"
     CALL DESC.REFORMAT (NEW.DESC, DESC.LEN, NEW.DESC)
     MAT INC.REC = ""
     INC.CATEGORY = "OTH"
     INC.DESC = NEW.DESC<1,1>
     IF MODE = "A" THEN
       DCNT = DCOUNT(NEW.DESC,VM)
       FOR D = 2 TO DCNT
         IVC.CHG.CODE<1,RNO+D-1> = "CMT"
         IVC.CHG.CAT<1,RNO+D-1> = "CMT"
         IVC.DESC<1,RNO+D-1> = NEW.DESC<1,D>
         SCV.REC(13)<ESN,RNO+D-1> = "CMT"
         SCV.REC(15)<ESN,RNO+D-1> = NEW.DESC<1,D>
       NEXT D
       CURR.RNO = ""
       GOSUB 10000
     END
 END CASE
*---- GET TAX JURISDICTION OR SHIP VIA IF NECESSARY
6015 *
 BEGIN CASE
   CASE IVC.CHG.CAT<1,RNO> = "TAX"
     SCV.REC(25)<ESN> = "Tax Jur :"
     ECD.NUM = 25; ECD.ACTION = 5; CALL SCRN.EDIT
     SCV.REC(23)<ESN> = IVC.TAX.JURS<1,RNO>
     SCV.REC(24)<ESN> = ""
     ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
     ECD.NUM = 23; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE = "END" THEN RETURN
     MATREAD TAX.REC FROM TAX, CONO:ECD.RET.VALUE ELSE
       ERRMSG = "Jurisdiction ":ECD.RET.VALUE:" is not on file"
       GOSUB 90000
       GOTO 6015
     END
     LOCATE ECD.RET.VALUE IN O.JURS<1>,1 SETTING FND ELSE FND = 0
     IF FND = 0 THEN
       ERRMSG = "No shipment present for this tax jurisdiction"
       GOSUB 90000
     END
     IVC.TAX.JURS<1,RNO> = ECD.RET.VALUE
     TMP.TAX.RATE<1,RNO> = TAX.RATE
     TMP.TAX.CALC<1,RNO> = ""
     SCV.REC(24)<ESN> = TAX.JUR
     ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
   CASE IVC.CHG.CAT<1,RNO> = "SHP"
     SCV.REC(25)<ESN> = "Ship Via :"
     ECD.NUM = 25; ECD.ACTION = 5; CALL SCRN.EDIT
     SCV.REC(23)<ESN> = IVC.TAX.JURS<1,RNO>
     SCV.REC(24)<ESN> = ""
     ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
     ECD.NUM = 23; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE = "END" THEN RETURN
     MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:ECD.RET.VALUE ELSE
       ERRMSG = "Ship Via ":ECD.RET.VALUE:" is not on file"
       GOSUB 90000
       GOTO 6015
     END
     LOCATE ECD.RET.VALUE IN O.SHIP<1>,1 SETTING FND ELSE FND = 0
     IF FND = 0 THEN
       ERRMSG = "No shipment present for this carrier"
       GOSUB 90000
     END
     IVC.TAX.JURS<1,RNO> = ECD.RET.VALUE
     IVC.TAXABLE<1,RNO> = ""
     TMP.TAX.RATE<1,RNO> = ""
     TMP.TAX.CALC<1,RNO> = ""
     SCV.REC(24)<ESN> = SHPV.DESC
     ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
   CASE IVC.CHG.CAT<1,RNO> = "DSC" OR IVC.CHG.CAT<1,RNO> = "MSC"
     SCV.REC(25)<ESN> = "Sales CD :"
     ECD.NUM = 25; ECD.ACTION = 5; CALL SCRN.EDIT
     SCV.REC(23)<ESN> = IVC.TAX.JURS<1,RNO>
     SCV.REC(24)<ESN> = ""
     ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
     ECD.NUM = 23; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE = "END" THEN RETURN
     MATREAD SLC.REC FROM SALES.CODE, CONO:ECD.RET.VALUE ELSE
       ERRMSG = "Sales Code ":ECD.RET.VALUE:" is not on file"
       GOSUB 90000
       GOTO 6015
     END
     IVC.TAX.JURS<1,RNO> = ECD.RET.VALUE
     IVC.TAXABLE<1,RNO> = ""
     TMP.TAX.RATE<1,RNO> = ""
     TMP.TAX.CALC<1,RNO> = ""
     ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
   CASE 1
     IVC.TAX.JURS<1,RNO> = ""
     IVC.TAXABLE<1,RNO> = ""
     TMP.TAX.RATE<1,RNO> = ""
     TMP.TAX.CALC<1,RNO> = ""
 END CASE
 IF MODE = "A" AND INC.DESC # "" THEN
   SCV.REC(15)<ESN,RNO> = INC.DESC
   ECD.NUM = 15; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
 END
*---- GET QUANTITY
6020 *
 BEGIN CASE
   CASE INC.CATEGORY = "OTH" OR INC.CATEGORY = "MSC"
     ECD.NUM = 14; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE = "END" THEN RETURN
   CASE 1
     ECD.RET.VALUE = ""
     SCV.REC(14)<ESN,RNO> = ""
     ECD.NUM = 14; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
 END CASE
 IVC.QUANTITY<1,RNO> = ECD.RET.VALUE
*---- GET U/M
6025 *
 BEGIN CASE
   CASE IVC.QUANTITY<1,RNO> # ""
     ECD.NUM = 30; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE = "END" THEN RETURN
   CASE 1
     ECD.RET.VALUE = ""
     SCV.REC(30)<ESN,RNO> = ""
     ECD.NUM = 30; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
 END CASE
 IVC.UM<1,RNO> = ECD.RET.VALUE
*---- GET DESCRIPTION
6030 *
 ECD.DEFAULT = INC.DESC
 ECD.NUM = 15; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 IVC.DESC<1,RNO> = ECD.RET.VALUE
*---- GET AMOUNT
6040 *
 IF IVC.QUANTITY<1,RNO> = "" OR IVC.UM<1,RNO> = "" THEN
   SCV.REC(31)<ESN,RNO> = ""
   ECD.NUM = 31; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
   IVC.UNIT.PRICE<1,RNO> = ""
 END
 BEGIN CASE
   CASE IVC.QUANTITY<1,RNO> # ""
     IF IVC.UM<1,RNO> = "" THEN
       ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
       IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
     END ELSE
       ECD.NUM = 31; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
       IF ECD.RET.VALUE # "END" THEN
         IVC.UNIT.PRICE<1,RNO> = ECD.RET.VALUE
         BEGIN CASE
           CASE IVC.UNIT.PRICE<1,RNO> = ""
             ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
             IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
           CASE IVC.UM<1,RNO> = "M"
             FACTOR = "1000"
           CASE IVC.UM<1,RNO> = "C"
             FACTOR = "100"
           CASE 1
             FACTOR = "1"
         END CASE
         IF IVC.UNIT.PRICE<1,RNO> # "" THEN
           IVC.AMOUNT<1,RNO> = (IVC.QUANTITY<1,RNO>/FACTOR) * IVC.UNIT.PRICE<1,RNO>
           IF IVC.AMOUNT<1,RNO> > 0 THEN
             IVC.AMOUNT<1,RNO> = INT(IVC.AMOUNT<1,RNO>/10+0.5)
           END ELSE
             IVC.AMOUNT<1,RNO> = INT(IVC.AMOUNT<1,RNO>/10-0.5)
           END
           SCV.REC(17)<ESN,RNO> = IVC.AMOUNT<1,RNO>
           ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
         END
       END
     END
   CASE INC.CATEGORY = "TAX"
     TAX.REF = RNO
     GOSUB 6400
     ECD.O.R = "O"; ECD.DEFAULT = TAMT
     SCV.REC(17)<ESN,RNO> = ""
     ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
     IF ECD.RET.VALUE # "END" THEN
       IF ECD.RET.VALUE = "" THEN
         ECD.RET.VALUE = TAMT
         SCV.REC(17)<ESN,RNO> = TAMT
         ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
       END
       IF ECD.RET.VALUE = TAMT THEN
         TMP.TAX.CALC<1,RNO> = "C"
         IVC.TAXABLE<1,RNO> = AMT.TAX
       END ELSE
         TMP.TAX.CALC<1,RNO> = "E"
         BEGIN CASE
           CASE TMP.TAX.RATE<1,RNO> + 0 = 0
           CASE ECD.RET.VALUE < 0
             AMT.TAX = INT((ECD.RET.VALUE*100)/(TMP.TAX.RATE<1,RNO>/1000)-0.5)
           CASE 1
             AMT.TAX = INT((ECD.RET.VALUE*100)/(TMP.TAX.RATE<1,RNO>/1000)+0.5)
         END CASE
6045     DEFAULT = OCONV(AMT.TAX, "MD2"); O.R = "O"
         X = 0; Y = 21; TYP = 4; SCALER = 2; MINV = 0; MAXV = 999999999
         MAXL = 10; PMSG = "Enter Taxable amount : "
         CALL EDIT.SUB
         P_X  = 0 ; P_Y = 21 ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         IF VALUE = "END" THEN GOTO 6040
         IF VALUE = "" THEN GOTO 6045
         IVC.TAXABLE<1,RNO> = VALUE
       END
       IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
     END
   CASE IVC.CHG.CODE<1,RNO> = "SUB"
     GOSUB 6200
     ECD.RET.VALUE = AMT.SUB
     SCV.REC(17)<ESN,RNO> = AMT.SUB
     ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
     IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
   CASE IVC.CHG.CODE<1,RNO> = "SUBT"
     GOSUB 6200
     ECD.RET.VALUE = IVC.AMOUNT<1,RNO>
     ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
   CASE IVC.CHG.CODE<1,RNO> = "TOT"
     GOSUB 6200
     ECD.RET.VALUE = AMT.TOT
     SCV.REC(17)<ESN,RNO> = AMT.TOT
     ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
     IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
   CASE INC.CATEGORY = "CMT"
     ECD.RET.VALUE = ""
     SCV.REC(17)<ESN,RNO> = ""
     ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 5; CALL SCRN.EDIT
     IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
   CASE 1
     ECD.NUM = 17; ECD.SUB.NUM = RNO; ECD.ACTION = 4; CALL SCRN.EDIT
     IVC.AMOUNT<1,RNO> = ECD.RET.VALUE
 END CASE
 IF SCV.REC(23)<ESN> # "" THEN
   SCV.REC(23)<ESN> = ""
   SCV.REC(24)<ESN> = ""
   SCV.REC(25)<ESN> = ""
   ECD.NUM = 23; ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 24; ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 25; ECD.ACTION = 5; CALL SCRN.EDIT
 END
 RETURN
*
*---- CALCULATE AND DISPLAY TOTAL
6200 *
 AMT.SUB = 0
 AMT.TOT = 0
 AMT.SUBT = 0
 FOR REF = 1 TO REF.CNT + 1
   CODE = IVC.CHG.CODE<1,REF>
   CATG = IVC.CHG.CAT<1,REF>
   AMT = IVC.AMOUNT<1,REF>
   BEGIN CASE
     CASE CODE = "SUB"
       IF AMT.SUB # AMT THEN
         IVC.AMOUNT<1,REF> = AMT.SUB
         SCV.REC(17)<ESN,REF> = AMT.SUB
         IF AMT # "" THEN CURR.RNO = ""
       END
     CASE CODE = "SUBT"
       IF AMT.SUBT # AMT THEN
         IVC.AMOUNT<1,REF> = AMT.SUBT
         SCV.REC(17)<ESN,REF> = AMT.SUBT
         IF AMT # "" THEN CURR.RNO = ""
       END
       AMT.SUBT = 0
     CASE CODE = "TOT"
       IF AMT.TOT # AMT THEN
         IVC.AMOUNT<1,REF> = AMT.TOT
         SCV.REC(17)<ESN,REF> = AMT.TOT
         IF AMT # "" THEN CURR.RNO = ""
       END
     CASE CATG = "TAX"
       TAX.REF = REF
       GOSUB 6400
       IF TAMT # AMT OR TMP.TAX.RATE<1,REF> + 0 = 0 THEN
         IF TMP.TAX.CALC<1,REF> = "E" THEN
           BEGIN CASE
             CASE MODE = ""
             CASE MODE = "A"
             CASE MODE = "C" AND RNO >=  REF
             CASE MODE = "D" AND RNO > REF
             CASE MODE = "I" AND RNO > REF
             CASE 1
               ERRMSG = "TAX ENTRY ON LINE ":REF:" MAY BE AFFECTED"
               GOSUB 90000
           END CASE
         END ELSE
           AMT = TAMT
           IVC.AMOUNT<1,REF> = AMT
           IVC.TAXABLE<1,REF> = AMT.TAX
           SCV.REC(17)<ESN,REF> = AMT
           IF AMT # "" THEN CURR.RNO = ""
         END
       END
       AMT.SUB = AMT.SUB + AMT
       AMT.TOT = AMT.TOT + AMT
       AMT.SUBT = AMT.SUBT + AMT
     CASE 1
       AMT.SUB = AMT.SUB + AMT
       AMT.TOT = AMT.TOT + AMT
       AMT.SUBT = AMT.SUBT + AMT
   END CASE
 NEXT REF
 INVOICE.TOTAL = AMT.TOT
 SCV.REC(18)<ESN> = INVOICE.TOTAL
 ECD.NUM = 18; ECD.ACTION = 5; CALL SCRN.EDIT
 RETURN
*
*---- CALCULATE SALES TAX
6400 *
 AMT.TAX = 0
 TDONE = 0
 FOR TREF = TAX.REF-1 TO 1 STEP -1 UNTIL TDONE
   IF IVC.CHG.CAT<1,TREF> = "TAX" THEN
     IF AMT.TAX > 0 THEN TDONE = 1
   END ELSE
     IF IVC.CHG.CODE<1,TREF> # "SUBT" THEN
       AMT.TAX = AMT.TAX + IVC.AMOUNT<1,TREF>
       IF IVC.CHG.CODE<1,TREF> = "SUB" THEN
         TDONE = 1
       END
     END
   END
 NEXT TREF
 TAMT = INT(AMT.TAX*(TMP.TAX.RATE<1,TAX.REF>/1000)/100+0.5)
 RETURN
*
*---- SET SCREEN VALUES
9000 *
 SCV.REC(1)<ESN> = IVC.CUST.NO
 SCV.REC(2)<ESN,1> = IVC.CUST.NAME
 SCV.REC(2)<ESN,2> = IVC.ADDR1
 SCV.REC(2)<ESN,3> = IVC.ADDR2
 SCV.REC(2)<ESN,4> = IVC.ADDR3
 SCV.REC(2)<ESN,5> = IVC.ADDR4
 SCV.REC(28)<ESN> = IVC.ATTENTION
*********SCV.REC(6)<ESN> = IVC.JOB.NO
 SCV.REC(6)<ESN> = ORDNO
 SCV.REC(7)<ESN> = IVC.NO
 SCV.REC(8)<ESN> = IVC.DATE
 SCV.REC(9)<ESN> = TMP.CUST.PO
 SCV.REC(29)<ESN> = IVC.SHP.DATE
 IF IVC.PROC.DATE = "" THEN
   IF IVC.PRT.DATE # "" THEN
     SCV.REC(10)<ESN> = "PRINTED"
     SCV.REC(11)<ESN> = IVC.PRT.DATE
   END
 END ELSE
   SCV.REC(10)<ESN> = "PROCESSED"
   SCV.REC(11)<ESN> = IVC.PROC.DATE
 END
 REF.CNT = DCOUNT(IVC.CHG.CODE,VM)
 FOR REF = 1 TO REF.CNT
   SCV.REC(13)<ESN,REF> = IVC.CHG.CODE<1,REF>
   SCV.REC(14)<ESN,REF> = IVC.QUANTITY<1,REF>
   SCV.REC(30)<ESN,REF> = IVC.UM<1,REF>
   SCV.REC(15)<ESN,REF> = IVC.DESC<1,REF>
   SCV.REC(31)<ESN,REF> = IVC.UNIT.PRICE<1,REF>
   SCV.REC(17)<ESN,REF> = IVC.AMOUNT<1,REF>
   IF IVC.CHG.CAT<1,REF> = "TAX" THEN
     MATREAD TAX.REC FROM TAX, CONO:IVC.TAX.JURS<1,REF> ELSE
       MAT TAX.REC = ""
       ERRMSG = "CANNOT LOCATE TAX JURISDICTION - ":IVC.TAX.JURS
       GOSUB 90000
     END
     TMP.TAX.RATE<1,REF> = TAX.RATE
     TAX.REF = REF
     GOSUB 6400
     IF IVC.AMOUNT<1,REF> = TAMT THEN
       TMP.TAX.CALC<1,REF> = "C"
     END ELSE
       TMP.TAX.CALC<1,REF> = "E"
     END
   END
 NEXT REF
 SCV.REC(18)<ESN> = INVOICE.TOTAL
 RETURN
*
*---- DISPLAY MULTI-LINE FIELDS
10000 *
 START.RNO = 1 + INT((RNO-1)/LINE.COUNT)*LINE.COUNT
 IF START.RNO = CURR.RNO THEN RETURN
 CURR.RNO = START.RNO
 ECD.NUM = 12; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 13; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 14; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 30; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 15; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 31; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 ECD.NUM = 17; ECD.SUB.NUM = START.RNO; ECD.ACTION = 7; CALL SCRN.EDIT
 RETURN
*
*---- GET JOB SHIPPING INFORMATION
21000 *
 O.JURS = ""; O.SHIP = ""
 JJCNT = 0; JSCNT = 0
 O.NOS = ORDNO
 PRE.BILL.BAL = ""
 SHIP.WIP = 0
 JCNT = DCOUNT(O.NOS,AM)
 RETURN
*
*---- MATCH TAX JURISDICTIONS
22000 *
 SHIP.AMT = 0
 JJMAT = ""; JSMAT = ""
 LCNT = DCOUNT(IVC.CHG.CODE,VM)
 FOR L = 1 TO LCNT
   CATG = IVC.CHG.CAT<1,L>
   BEGIN CASE
     CASE CATG = "TAX"
       J = IVC.TAX.JURS<1,L>
       LOCATE J IN O.JURS<1>,1 SETTING FND ELSE FND = 0
       IF FND > 0 THEN JJMAT<1,FND> = "X"
     CASE CATG = "SHP"
       SHIP.AMT = SHIP.AMT + IVC.AMOUNT<1,L>
       S = IVC.TAX.JURS<1,L>
       LOCATE S IN O.SHIP<1>,1 SETTING FND ELSE FND = 0
       IF FND > 0 THEN JSMAT<1,FND> = "X"
   END CASE
 NEXT L
*
 MATCH.ERROR = 0
 ERR = 0
 FOR J = 1 TO JJCNT UNTIL ERR
   IF JJMAT<1,J> = "" THEN
     ERR = 1
     MATCH.ERROR = 1
     ERRMSG = "Taxes not entered for all tax jurisdictions on job"
     GOSUB 90000
   END
 NEXT J
 ERR = 0
 FOR S = 1 TO JSCNT UNTIL ERR
   IF JSMAT<1,S> = "" THEN
     ERR = 1
     MATCH.ERROR = 1
     ERRMSG = "Shipping charges not entered for all carriers used on job"
     GOSUB 90000
   END
 NEXT S
*
 IF SHIP.AMT > SHIP.WIP THEN
   MATCH.ERROR = 1
   ERRMSG = "Shipping charges entered exceeds WIP by ":OCONV((SHIP.AMT-SHIP.WIP),"MD2,$")
   GOSUB 90000
 END
 IF SHIP.WIP > SHIP.AMT THEN
   MATCH.ERROR = 1
   ERRMSG = "WIP exceeds shipping charges entered by ":OCONV((SHIP.WIP-SHIP.AMT),"MD2,$")
   GOSUB 90000
 END
 RETURN
*
*---- ERROR ROUTINE
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
* PRINT @(0,23):CL:ERRMSG:
* INPUT REPLY,1:
* PRINT @(0,23):CL:
* RETURN
*
*---- END OF PROGRAM
99999 *
 ECD.ACTION=99;CALL SCRN.EDIT
 RETURN
END
