SUBROUTINE PKTKT.PRINT.DATE(DATE.LIST,INID)
**********************************************
* REVISION    - [10.B]
* Copyright 1996 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - PKTKT.PRINT.DATE
* BY          - RENEE YANEZ; CBA
* DATE        - 01/25/99
* DESCRIPTION - this sub will figure out the correct date to use when
*               selecting the picking tickets that will be printed. If the
*               order has a release, then use the release requested date.
*               If the order does not have a release, the use the order
*               due date.
*
*T23580 renee 01/25/1999 * New dictionary subroutine.
*ENDDOC
**********************************************
*COPY>OPS.CPYLIB>ORDER
*COPY>OPS.CPYLIB>ORDER.RELEASE
*COPY>OPS.CPYLIB>ORDER.DETAIL
*
IF @USER1 = "" THEN
  ORDER.FLAG = "" ; RELEASE.FLAG = ""
  OPEN "ORDER" TO @USER2 THEN
    ORDER.FLAG='1'
  END
  OPEN "ORDER.RELEASE" TO @USER3 THEN
    RELEASE.FLAG='1'
  END
  @USER0=''
  IF ORDER.FLAG='1' AND RELEASE.FLAG = "1" THEN
    @USER0='1'
  END
  @USER1='1'
END ELSE
  IF @USER0='1' THEN
    * CONTINUE
  END ELSE
    DATE.LIST=''
    RETURN
  END
END
DATE.LIST=''
*
CONO=INID[1,3]
ORDER.ID = FIELD(INID,"!",1)
MATREAD ORD.REC FROM @USER2,ORDER.ID THEN
  IF ORD.REL.NO NE "" THEN
    REL.CNT = DCOUNT(ORD.REL.NO,@VM)
    FOR I = 1 TO REL.CNT
      MATREAD ORR.REC FROM @USER3, CONO:ORD.REL.NO<1,I> ELSE
        MAT ORR.REC = ""
      END
      IF ORR.REQ.DATE NE "" THEN
        * Only include in list if a pick ticket has not been printed alrdy
        LOCATE ORD.REL.NO<1,I> IN ORD.PICK.NO<1>,1 SETTING PICKPOS ELSE
          DATE.LIST<1,-1> = ORR.REQ.DATE
        END
      END
    NEXT I
  END ELSE
    DATE.LIST=ORD.DUE
  END
END
RETURN
