 SUBROUTINE OPS.RETURN.LINE.DET(ACTION,CONO,MAT IVC.REC,MAT TMP.REC,RNO)
*COPY>CPYLIB>COM1
*********************************************************************
* REVISION - [08.1]
* Copyright 1982 by Vercom Software, Inc. (dba CBA)
* SOURCE   - OPSBP
* PROGRAM  - OPS.RETURN.LINE.DET
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 07/21/92
* DESCRIPTION
* This program is used to return merchandise.
*T25740 epitka 02/01/2002 * REV12
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>OPS.CPYLIB>BOL
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>OPS.CPYLIB>ORDER.INVOICE
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
 MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- DIMENSIONED VARIABLES
*
 DIM TMP.REC(20)
* EQU TMP.PALLET      TO TMP.REC(1)
 EQU TMP.ORDER       TO TMP.REC(2)
 EQU TMP.PROD        TO TMP.REC(3)
 EQU TMP.DESC        TO TMP.REC(4)
 EQU TMP.SHP.QTY     TO TMP.REC(5)
 EQU TMP.REF.QTY     TO TMP.REC(6)
 EQU TMP.UM          TO TMP.REC(7)
 EQU TMP.COST.UNIT   TO TMP.REC(8)
 EQU TMP.UNIT.PRICE  TO TMP.REC(9)
 EQU TMP.AMOUNT      TO TMP.REC(10)
*
 EQU TMP.RTN.QTY     TO TMP.REC(11)
 EQU TMP.RTN.PRICE   TO TMP.REC(12)
 EQU TMP.RTN.WHSE    TO TMP.REC(13)
 EQU TMP.RTN.LOC     TO TMP.REC(14)
* EQU TMP.RTN.PALLET  TO TMP.REC(15)
*
 EQU TMP.LPTR        TO TMP.REC(19)
 EQU TMP.SPTR        TO TMP.REC(20)
*
*---- INITIALIZATION
*
 ESN = ECD.SCRN.NO
 LPTR = TMP.LPTR<1,RNO>
 SPTR = TMP.SPTR<1,RNO>
*
*---- MAIN PROCESSING
*
100 *
 IF TMP.RTN.QTY<1,RNO> = "" THEN
   TMP.RTN.QTY<1,RNO> = 0
   TMP.RTN.WHSE<1,RNO> = IVC.WHSE<1,LPTR,SPTR>
   TMP.RTN.LOC<1,RNO> = "HOLD"
*   TMP.RTN.PALLET<1,RNO> = IVC.PALLET<1,LPTR,SPTR>
   GOSUB 9000
   ECD.ACTION=3; CALL SCRN.EDIT
   FOR REF = 1 TO 4
     ON REF GOSUB 1100, 1200, 1300, 1400
     IF ECD.RET.VALUE = "END" THEN GOTO 99999
   NEXT REF
 END ELSE
   GOSUB 9000
   ECD.ACTION=3; CALL SCRN.EDIT
 END
*
*---- GET OPERATOR REQUEST
*
500 *
 ECD.NUM=21; ECD.ACTION=4; CALL SCRN.EDIT
 OPTION=ECD.RET.VALUE
 BEGIN CASE
   CASE NUM(OPTION)
     ON OPTION GOSUB 1100, 1200, 1300, 1400
   CASE OPTION="E" OR OPTION="END"
     GOTO 99999
 END CASE
 GOTO 500
*
*---- GET REFUND QUANTITY
*
1100 *
 ECD.MAXV=TMP.SHP.QTY<1,RNO>
 ECD.NUM=13; ECD.ACTION=4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 TMP.REF.QTY<1,RNO> = ECD.RET.VALUE
 GOSUB 9500
 RETURN
*
*---- GET REFUND PRICE
*
1200 *
 ECD.NUM=14; ECD.ACTION=4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 TMP.UNIT.PRICE<1,RNO> = ECD.RET.VALUE
 GOSUB 9500
 RETURN
*
*---- GET RESTOCK QUANTITY
*
1300 *
 ECD.DEFAULT=0
 ECD.NUM=16; ECD.ACTION=4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 TMP.RTN.QTY<1,RNO> = ECD.RET.VALUE
 RETURN
*
*---- GET RESTOCK LOCATION
*
1400 *
 IF TMP.RTN.QTY<1,RNO>+0 = 0 THEN RETURN
 ECD.NUM=17; ECD.ACTION=4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 TMP.RTN.WHSE<1,RNO> = ECD.RET.VALUE
*
 ECD.NUM=18; ECD.ACTION=4; CALL SCRN.EDIT
 IF ECD.RET.VALUE = "END" THEN RETURN
 TMP.RTN.LOC<1,RNO> = ECD.RET.VALUE
*
* ECD.NUM=19; ECD.ACTION=4; CALL SCRN.EDIT
* IF ECD.RET.VALUE = "END" THEN RETURN
* TMP.RTN.PALLET<1,RNO> = ECD.RET.VALUE
* RETURN
*
*---- DISPLAY DATA
*
9000 *
 SCV.REC(1)<ESN>   = DATE()
 SCV.REC(2)<ESN>   = TMP.PROD<1,RNO>
 SCV.REC(3)<ESN>   = TMP.DESC<1,RNO>
*
 SCV.REC(11)<ESN>  = TMP.SHP.QTY<1,RNO>
 SCV.REC(12)<ESN>  = TMP.UM<1,RNO>
 SCV.REC(13)<ESN>  = TMP.REF.QTY<1,RNO>
 SCV.REC(14)<ESN>  = TMP.UNIT.PRICE<1,RNO>
 SCV.REC(15)<ESN>  = TMP.AMOUNT<1,RNO>
 SCV.REC(16)<ESN>  = TMP.RTN.QTY<1,RNO>
 SCV.REC(17)<ESN>  = TMP.RTN.WHSE<1,RNO>
 SCV.REC(18)<ESN>  = TMP.RTN.LOC<1,RNO>
* SCV.REC(19)<ESN>  = TMP.RTN.PALLET<1,RNO>
 RETURN
*
*---- CALCULATE RETURN AMOUNT
*
9500 *
 QTY = TMP.REF.QTY<1,RNO>
 COST.UNIT = TMP.COST.UNIT<1,RNO>
 UNIT.PRICE = TMP.UNIT.PRICE<1,RNO>
 TMP.AMOUNT<1,RNO> = INT(QTY/COST.UNIT*(UNIT.PRICE/10)+0.5)
 SCV.REC(15)<ESN> = TMP.AMOUNT<1,RNO>
 ECD.NUM=15; ECD.ACTION=5; CALL SCRN.EDIT
 RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*---- END OF PROGRAM
*
99999 *
 ECD.ACTION=99;CALL SCRN.EDIT
 RETURN
END
