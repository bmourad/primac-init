*COPY>CPYLIB>COM1
*************************************************************
* REVISION      - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - FNGD.RECEIPT.LIST
* SYSTEM        - OPSBP
* AUTHOR        - NICK AMENDOLA
* DATE          - 08/09/93
* DESCRIPTION
* TASK
*     19404 LLH DIVISION
*
*T25740 epitka 01/14/2002 * REV12
*T26090 ajibaly 03/26/2002 * CONVERT TO R12 REPORT.SCRN
*T26556 adelgado 04/23/2002 * Expand Receipt field by 2 char.
*T26556 cmykleb 05/06/2002 * Hard code the rpt # into rpt.
*                            use the hard coded rpt #.
*************************************************************
*
*---- FILE EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>OPS.CPYLIB>DAILY_FNGD_RECEIPT
*COPY>ICS.CPYLIB>INVENTORY
*COPY>OPS.CPYLIB>ORDER
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN "","DAILY_FNGD_RECEIPT" TO DAILY_FNGD_RECEIPT ELSE ERRMSG = "DAILY_FNGD_RECEIPT FILE MISSING";GOTO 93000
   OPEN "","INVENTORY" TO INVENTORY ELSE ERRMSG = "INVENTORY FILE MISSING";GOTO 93000
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
   OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE MISSING";GOTO 93000
   OPEN "","SHIP.TO" TO SHIP.TO ELSE ERRMSG = "SHIP.TO FILE MISSING";GOTO 93000
   OPEN "","ORDER" TO ORDER ELSE ERRMSG = "ORDER FILE MISSING";GOTO 93000
*
*---- GET COMPANY NUMBER
*
   CONO = ""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
   PORT.NO = "TTY"; CALL SYSVARS.SUB(PORT.NO)
*
*---- PROC READ
*
   PROCREAD INBUFF ELSE 
      INBUFF = ""
   END
   DIV = INBUFF<10>
   PRINT.FLAG = INBUFF<11> ; * T26090
*
*---- INITIALIZATION
*
   DIM D.LINE(6)
   MAT D.LINE = ""
   LINE.CNT = 0
   PAGE.NO = 0
   SP = " "
   SP2 = "  "
   SP3 = "   "
   TOT.QTY = 0
   P.LINE = ""
   R.LINE = ""
   UNKNOWN = STR("?",45)
   ERRMSG = ""
   SHD1 = "ORDER #  SHP           SHIP-TO NAME            QUANTITY "
   SUN1 = "-------  ---  ------------------------------  ----------"
   TUN1 = "                                              ----------"
*T26090 v
*  RPT.NAME = "FINISHED GOODS RECEIPT EDIT LISTING"
   RPT.NAME = ""
   CO.NAME = ""
   RPT.NO = "IC201"
*T26090 ^
   RPT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   HD3 = "DIVISION : ":DIV
   CALL GET.PROG.HEAD(CONO,CO.NAME,RPT.NAME,RPT.NO,RPT.DATE,HD1,HD2)
*
*---- MAIN PROCESSING
*
   TEST.FLAG=""
   READNEXT ID ELSE
      READ REC FROM CONTROL, CONO:"FNGD.RECEIPT.LIST":"!":PORT.NO ELSE GOTO 99999
      ID = CONO:REC<1>
      MAT DFR.REC = ""
      FOR N = 1 TO DFR.REC.SIZE
         DFR.REC(N) = REC<N+1>
      NEXT N
      PRINTER ON
      TEST.FLAG="."
      GOSUB 3000
      GOSUB 110
      PRINTER OFF
      PRINTER CLOSE
      GOTO 99999
   END
   PRINTER ON
   TEST.FLAG=".."
   GOSUB 3000
   GOSUB 100
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      GOSUB 100
   REPEAT
   PRINTER OFF
   PRINTER CLOSE
   GOTO 99999
*
*---- PRINT LINES
100 *
   MATREADU DFR.REC FROM DAILY_FNGD_RECEIPT, ID ELSE
      RELEASE DAILY_FNGD_RECEIPT, ID
      GOTO 199
   END
*T26090 v
*  DFR.PRT.DATE = DATE()
   IF PRINT.FLAG # "V" THEN DFR.PRT.DATE = DATE()
*T26090 ^
   MATWRITE DFR.REC ON DAILY_FNGD_RECEIPT, ID
110 *
   MATREAD CUST.REC FROM CUSTOMER, CONO:DFR.CUST ELSE
      MAT CUST.REC = ""
      CUST.NAME = UNKNOWN
   END
   RCNT = 0
   OCNT = DCOUNT(DFR.ORDER,VM)
   FOR OPTR = 1 TO OCNT
*T26090v
* IF DFR.QTY+0 # 0 THEN RCNT = RCNT + 1
      IF DFR.QTY<1,OPTR>+0 # 0 THEN RCNT = RCNT + 1
*T26090^
   NEXT OPTR
   IF LINE.CNT + RCNT + 10 > 55 THEN GOSUB 3000
*
   D.LINE(1) = "RECEIPT # : ":ID[4,99]"L#9"    ;* T26556
   D.LINE(2) = "CUSTOMER  : ":DFR.CUST"L#20":CUST.NAME"L#30"
   MATREAD INV.REC FROM INVENTORY, CONO:DFR.PROD ELSE
      MAT INV.REC = ""
      INV.FULL.DESC = STR("?",45)
   END
   GOSUB 9000
   D.LINE(3) = "PRODUCT   : ":DFR.PROD"L#20":INV.FULL.DESC"L#45"
   D.LINE(4) = "JOB NUM   : ":DFR.JOB"L#20":"WHSE   : ":DFR.WHSE"L#10":"LOC   : ":DFR.LOC"L#10"
   D.LINE(4) = D.LINE(4):"TOTAL UNITS  : ":OCONV(INT(DFR.TOT.QTY / ICR.DV1 * ICR.MT1 / ICR.DV2 + .5), ICR.CNV1)"L#10"
   D.LINE(5) = "RECV DATE : ":OCONV(DFR.RECP.DATE,"D2/")"L#20"
   D.LINE(5) = D.LINE(5):"PERIOD : ":DFR.PERIOD "L#6"
   D.LINE(6) = "STATUS    : ":DFR.STATUS<1,1>
   FOR P = 1 TO 6
      PRINT D.LINE(P)
      LINE.CNT = LINE.CNT + 1
   NEXT P
*
   IF RCNT > 0 THEN
      PRINT
      PRINT SHD1
      PRINT SUN1
      LINE.CNT = LINE.CNT + 3
      OCNT = DCOUNT(DFR.ORDER,VM)
      FOR OPTR = 1 TO OCNT
         QTY = DFR.QTY<1,OPTR>+0
         IF QTY # 0 THEN
            ORDNO = DFR.ORDER<1,OPTR>
            SHPNO = DFR.SHIP.TO<1,OPTR>
            IF ORDNO = "*FNGD*" THEN
               MAT ORD.REC = ""
               MAT SPT.REC = ""
            END ELSE
               MATREAD ORD.REC FROM ORDER, CONO:ORDNO ELSE MAT ORD.REC = ""
               MATREAD SPT.REC FROM SHIP.TO, CONO:ORD.CUST:"!":SHPNO ELSE
                  MAT SPT.REC = ""
                  SPT.NAME = "???????????????"
               END
            END
            R.LINE = ""
            R.LINE = DFR.ORDER<1,OPTR>"L#7"
            R.LINE = R.LINE : SP2 : SHPNO"R#3"
            R.LINE = R.LINE : SP2 : SPT.NAME"L#30"
            R.LINE = R.LINE : SP2 : OCONV(INT(QTY / ICR.DV1 * ICR.MT1 / ICR.DV2 + .5), ICR.CNV1)"R#10"
            PRINT R.LINE
            LINE.CNT = LINE.CNT + 1
         END
      NEXT OPTR
      PRINT TUN1
      QTY = SUM(DFR.QTY)
      T.LINE = SPACE(46):OCONV(INT(QTY / ICR.DV1 * ICR.MT1 / ICR.DV2 + .5), ICR.CNV1)"R#10"
      PRINT T.LINE
      LINE.CNT = LINE.CNT + 2
      PRINT;PRINT
      LINE.CNT = LINE.CNT + 2
   END
199 *
   RETURN
*
*---- PRINT HEADINGS
3000 *
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO:TEST.FLAG
   PRINT HD2
   PRINT HD3
   PRINT;PRINT
   LINE.CNT = 5
   RETURN
9000 *
*COPY>ICSBP>INV.UM.CNV
   RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   PRINTER OFF
END
