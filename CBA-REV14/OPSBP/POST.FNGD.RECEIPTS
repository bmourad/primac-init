*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST      
*COPY>OPS.CPYLIB>COM.ORDER     
*COPY>ICS.CPYLIB>COM.INV.CNV   
*COPY>OPS.CPYLIB>COM.OPS.LINK  
*********************************************************************
* REVISION    - [08.0X]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - POST.FNGD.RECEIPTS
* AUTHOR      - NICK AMENDOLA
* DATE        - 08/09/93
* DESCRIPTION
*
*T25420 cm 09/13/2000 * Do not change the DFR.PERIOD in this pgm.
*T25424 cm 09/14/2000 * Enable division level posting.
*T25740 epitka 01/14/2002 * REV12
*********************************************************************
*
*---- Data structure libraries
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>FISCAL
*COPY>JCS.CPYLIB>JOB
*COPY>OPS.CPYLIB>DAILY_FNGD_RECEIPT
*COPY>OPS.CPYLIB>FNGD.WIP.HIST
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>WAREHOUSE
*
*---- SYSCOM setup
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- Open files
*
   OPEN "","DAILY_FNGD_RECEIPT" TO DAILY_FNGD_RECEIPT ELSE
      ERRMSG = "DAILY_FNGD_RECEIPT"; GOTO 93000
   END
   OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE
      ERRMSG = "INV_RECEIPTS"; GOTO 93000
   END
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "COMPANY"; GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL"; GOTO 93000
   END
   OPEN "","CUSTOMER" TO CUSTOMER ELSE
      ERRMSG = "CUSTOMER"; GOTO 93000
   END
   OPEN "","INVENTORY" TO INVENTORY ELSE
      ERRMSG = "INVENTORY"; GOTO 93000
   END
   OPEN "","CATEGORY" TO CATEGORY ELSE
      ERRMSG = "CATEGORY"; GOTO 93000
   END
   OPEN "","INV.WHSE" TO INV.WHSE ELSE
      ERRMSG = "INV.WHSE"; GOTO 93000
   END
   OPEN "","INV.WHSE.LOC" TO INV.WHSE.LOC ELSE
      ERRMSG = "INV.WHSE.LOC"; GOTO 93000
   END
   OPEN "","FNGD.STATS" TO FNGD.STATS ELSE
      ERRMSG = "FNGD.STATS"; GOTO 93000
   END
   OPEN "","FNGD.JOB.STATS" TO FNGD.JOB.STATS ELSE
      ERRMSG = "FNGD.JOB.STATS"; GOTO 93000
   END
   OPEN "","FNGD.WIP.HIST" TO FNGD.WIP.HIST ELSE
      ERRMSG = "FNGD.WIP.HIST"; GOTO 93000
   END
   OPEN "","FNGD_WIP_HIST_TAG" TO FNGD_WIP_HIST_TAG ELSE
      ERRMSG = "FNGD_WIP_HIST_TAG"; GOTO 93000
   END
   OPEN "","FNGD_AUDIT_TAG" TO FNGD_AUDIT_TAG ELSE
      ERRMSG = "FNGD_AUDIT_TAG"; GOTO 93000
   END
   OPEN "","FNGD.ORDER.STATS" TO FNGD.ORDER.STATS ELSE
      ERRMSG = "FNGD.ORDER.STATS"; GOTO 93000
   END
   OPEN "","JOB" TO JOB ELSE
      ERRMSG = "JOB"; GOTO 93000
   END
   OPEN "","ORDER" TO ORDER ELSE
      ERRMSG = "ORDER"; GOTO 93000
   END
   OPEN "","ORDER.DETAIL" TO ORDER.DETAIL ELSE
      ERRMSG = "ORDER.DETAIL"; GOTO 93000
   END
   OPEN "","ORDER.RELEASE" TO ORDER.RELEASE ELSE
      ERRMSG = "ORDER.RELEASE"; GOTO 93000
   END
   OPEN "","JOB.FNGD.STATS" TO JOB.FNGD.STATS ELSE
      ERRMSG = "JOB.FNGD.STATS"; GOTO 93000
   END
   OPEN "","PICK.TICKET.PRT" TO PICK.TICKET.PRT ELSE
      ERRMSG = "PICK.TICKET.PRT"; GOTO 93000
   END
   OPEN "","WAREHOUSE" TO WAREHOUSE ELSE
      ERRMSG = "WAREHOUSE"; GOTO 93000
   END
*
*---- Main Processing
*
   READNEXT DFR.ID ELSE GOTO 99999
   CONO = DFR.ID[1,3]
   DFRNO = DFR.ID[4,99]
   MATREAD COMP.REC FROM COMPANY, CONO ELSE GOTO 99999
   MATREAD OPCO.REC FROM CONTROL, CONO:"OPS" ELSE
      MAT OPCO.REC = ""
   END
   READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
      PERIOD.REC = ""
      PERIOD.REC<1> = 12
   END
   NUM.PERIODS = PERIOD.REC<1>
   MATREAD FISCAL.REC FROM CONTROL, CONO:"ICFISCAL" ELSE
      ERRMSG = "Cannot locate Inventory Control Fiscal Period !!"
      GOSUB 91000; GOTO 99999
   END
   READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
      ERRMSG = "DIVISIONS CONTROL FILE RECORD IS MISSING"
      GOTO 93000
   END
   READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
      ERRMSG = "DIV.SECURITY CONTROL FILE RECORD IS MISSING"
      GOTO 93000
   END
   MATREADU DFR.REC FROM DAILY_FNGD_RECEIPT, DFR.ID ELSE
      RELEASE DAILY_FNGD_RECEIPT, DFR.ID
      GOTO 99999
   END
   GOSUB 1000
   DATA = 1
   LOOP
      READNEXT DFR.ID ELSE DATA = 0
   WHILE DATA DO
      IF CONO # DFR.ID[1,3] THEN GOTO 999
      DFRNO = DFR.ID[4,99]
      MATREADU DFR.REC FROM DAILY_FNGD_RECEIPT, DFR.ID ELSE
         RELEASE DAILY_FNGD_RECEIPT, DFR.ID
         GOTO 99999
      END
      GOSUB 1000
999*
   REPEAT
   GOTO 99999
*
*---- Process a receipt
1000*
   MATREAD WHSE.REC FROM WAREHOUSE, CONO:DFR.WHSE ELSE MAT WHSE.REC = ""
   IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      LOCATE WHS.DIV IN DIVISION.REC<1>,1 SETTING POS ELSE
         ERRMSG = "Division ":WHS.DIV:" not found in DIVISIONS Control File Record."
         GOTO 93000
      END
   END ELSE
      POS = 1
   END
   IF LEN(FR.CURR.PER<1,POS>) # 6 THEN
      ERRMSG = "ERROR in the Inventory Control Fiscal Period (":FR.CURR.PER<1,POS>:") !!"
      GOSUB 91000; GOTO 99999
   END
   IF FR.CURR.PER<1,POS>[5,2] < 1 OR FR.CURR.PER<1,POS>[5,2] > NUM.PERIODS THEN
      ERRMSG = "ERROR in the Inventory Control Fiscal Period (":FR.CURR.PER<1,POS>:") !!"
      GOSUB 91000; GOTO 99999
   END
   DFR.STATUS = ""
   READU DUMMY FROM INV_RECEIPTS, DFR.ID THEN
      DFR.STATUS = "Receipt already posted"
   END ELSE
      MATREAD JOB.REC FROM JOB, CONO:DFR.JOB THEN
         MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST THEN
            CALL FNGD.RECP.UPDATE(DFR.ID,OPCO.COST.SALE)
         END ELSE
            DFR.STATUS = "Cannot locate Customer # ":JOB.CUST
         END
      END ELSE
         DFR.STATUS = "Cannot locate Job # ":DFR.JOB
      END
   END
   IF DFR.STATUS='' THEN
      MATREADU FWH.REC FROM FNGD.WIP.HIST, CONO:DFR.JOB ELSE MAT FWH.REC=""
      PPTR = 1
      LOOP
         LOCATE DFR.PROD IN FWH.PROD<1>,PPTR SETTING FND THEN
            IF DFR.WHSE = FWH.WHSE<1,FND> THEN PPTR = 0
         END ELSE
            FWH.PROD<1,FND> = DFR.PROD
            FWH.WHSE<1,FND> = DFR.WHSE
            PPTR = 0
         END
      WHILE PPTR DO
         PPTR = FND + 1
      REPEAT
      PPTR = FND
      LOCATE DFR.PERIOD IN FWH.PERIOD<1,PPTR>,1 SETTING MPTR THEN
         FWH.RECP<1,PPTR,MPTR> = FWH.RECP<1,PPTR,MPTR>:"!":DFRNO
      END ELSE
         FWH.PERIOD<1,PPTR,MPTR> = DFR.PERIOD
         FWH.RECP<1,PPTR,MPTR> = DFRNO
      END
      MATWRITE FWH.REC ON FNGD.WIP.HIST, CONO:DFR.JOB
      WRITE "" ON FNGD_WIP_HIST_TAG, CONO:DFR.JOB
      DELETE DAILY_FNGD_RECEIPT, DFR.ID
      GOTO 1099
   END ELSE
      MATWRITE DFR.REC ON DAILY_FNGD_RECEIPT, DFR.ID
   END
1099*
   RETURN
*
*---- SYSCOM Calls
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   ERRMSG = "Cannot locate the ":ERRMSG:" file"
   CALL SYSCOM(MAT SYSCOM.REC)
99999 END
