 SUBROUTINE BOL.ORDER.SUB(CONO,ORDNO,SHPNO)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>PMC.CPYLIB>COM.SHIP.TO
********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - OPSBP
* PROGRAM       - BOL.ORDER.SUB
* BY            - Ziad Yamout, VERCOM Software Inc.
* DATE          - 01/23/90
* DESCRIPTION   -
*T25740 epitka 01/22/2002 * REV12
********************************************************************
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>OPS.CPYLIB>ORDER
*COPY>OPS.CPYLIB>ORDER.DETAIL
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>FNGD.STATS
*COPY>ICS.CPYLIB>FNGD.ORDER.STATS
*COPY>ICS.CPYLIB>FNGD.JOB.STATS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
 MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- Display screen
*
 GEN.SHPNO = "000"
 TODAY = DATE()
 LINE.SPACE = 2
 PAGE.SIZE = 5
 BEGIN.PAGE = 10
*
 ESN = ECD.SCRN.NO
 ECD.NUM = 1; SCV.REC(ECD.NUM)<ESN> = ORDNO
 ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 2; SCV.REC(ECD.NUM)<ESN> = ORD.CUST
 ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 3; SCV.REC(ECD.NUM)<ESN> = CUST.NAME
 ECD.ACTION = 5; CALL SCRN.EDIT
*
 ECD.NUM = 4; SCV.REC(ECD.NUM)<ESN> = SHPNO
 ECD.ACTION = 5; CALL SCRN.EDIT
 IF SHPNO = GEN.SHPNO THEN
   MAT SPT.REC = ""
   SPT.NAME = CUST.NAME
   SPT.ADDR1 = CUST.ADDR1
   SPT.ADDR2 = CUST.ADDR2
   SPT.STATE = TRIM(FIELD(CUST.ADDR4,",",2))
   SPT.CITY = TRIM(FIELD(CUST.ADDR4,",",1))
   SPT.ZIP = CUST.ZIP
   SPT.TAX.JUR = CUST.TAX.JUR
   SPT.TAXABLE = CUST.TAXABLE
   SPT.EXEMPT = CUST.EXEMPT
   SPT.EXMT.DATE = CUST.EXMT.DATE
   SPT.SLSMN = CUST.SALESMAN
   SPT.FOB = CUST.ADDL.OPS<1,3>
   SPT.SHP.TERMS = CUST.ADDL.OPS<1,4>
 END ELSE
   MATREAD SPT.REC FROM SHIP.TO, CONO:ORD.CUST:"!":SHPNO ELSE
     ERRMSG = "Cannot locate ship to # ":SHPNO
     GOSUB 91000; GOTO 100
   END
 END
 ECD.NUM = 8; SCV.REC(ECD.NUM)<ESN> = SPT.NAME
 ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 10; SCV.REC(ECD.NUM)<ESN> = SPT.ADDR1
 ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 52; SCV.REC(ECD.NUM)<ESN> = SPT.CITY
 ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 53; SCV.REC(ECD.NUM)<ESN> = SPT.STATE
 ECD.ACTION = 5; CALL SCRN.EDIT
 ECD.NUM = 56; SCV.REC(ECD.NUM)<ESN> = SPT.ZIP
 ECD.ACTION = 5; CALL SCRN.EDIT
 MATREAD ORD.DET.REC FROM ORDER.DETAIL, CONO:ORDNO:"!":SHPNO ELSE
   MAT ORD.DET.REC = ""
 END
 ECD.NUM = 9; SCV.REC(ECD.NUM)<ESN> = OSD.PO
 ECD.ACTION = 5; CALL SCRN.EDIT
 LINES = DCOUNT(OSD.PROD,VM); LN = 0
 OLD.START.LINE = 0
 GOSUB 7900
*
*---- Prompt line
*
100*
 MORE = 1
 LOOP
   ECD.NUM = 41; SCV.REC(ECD.NUM)<ESN> = ""
   ECD.ACTION = 4; CALL SCRN.EDIT
   OPTION = ECD.RET.VALUE
   BEGIN CASE
     CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
       MORE = 0
     CASE OPTION = "S" OR OPTION = "SF"
       LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
       IF LN > LINES THEN LN = 1
       GOSUB 7900
     CASE OPTION = "SR"
       LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
       IF LN < 1 THEN LN = 1
       GOSUB 7900
     CASE OPTION = "ST"
       LN = 1
       GOSUB 7900
     CASE OPTION = "SB"
       LN = LINES
       GOSUB 7900
     CASE OPTION[1,1] = "S" AND NUM(OPTION[2,99])
       LN.NO = OPTION[2,99] + 0
       IF LN.NO < 1 OR LN.NO > LINES THEN
         ERRMSG = "** Invalid selection **"
         GOSUB 91000
       END ELSE
         LN = LN.NO
         GOSUB 7900
       END
     CASE 1
       ERRMSG = "Invalid entry, please re-enter"
       GOSUB 91000
   END CASE
 WHILE MORE > 0 DO REPEAT
 IF MORE < 0 THEN
   GOTO 100
 END ELSE
   GOTO 99999
 END
*
*---- Display scrolling lines
7900*
 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
 LAST.LINE = START.LINE + PAGE.SIZE - 1
 IF LAST.LINE > LINES THEN LAST.LINE = LINES
 IF START.LINE = OLD.START.LINE THEN GOTO 7990
 OLD.START.LINE = START.LINE
 ECD.NUM = 31
 SCV.REC(ECD.NUM)<ESN> = INT(LAST.LINE/PAGE.SIZE+.9) "R%2"
 ECD.ACTION = 5; CALL SCRN.EDIT
 CNT = 1
 FOR N = START.LINE TO LAST.LINE
   SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
   GOSUB 7910
   CNT = CNT + 1
 NEXT N
 FOR M = CNT TO PAGE.SIZE
   SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
   P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
 NEXT M
7990*
 RETURN
*
*---- Display a single line
7910*
7911*
 MATREAD INV.REC FROM INVENTORY, CONO:OSD.PROD<1,N> ELSE
   MAT INV.REC = ""; INV.FULL.DESC = "Unknown"
 END
 GOSUB 9000
 P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R%2" ; P_OPT = "CL"
 P_X  := AM:3 ; P_Y := AM:SLN ; P_VALUE := AM:OSD.PROD<1,N> "L#15"
 P_X  := AM:19 ; P_Y := AM:SLN ; P_VALUE := AM:OSD.WHSE<1,N> "L#4"
 P_X  := AM:24 ; P_Y := AM:SLN ; P_VALUE := AM:INV.UNIT<1,3> "L#3"
 CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
 MATREAD IWH.REC FROM INV.WHSE, CONO:OSD.PROD<1,N>:"!":OSD.WHSE<1,N> ELSE
   MAT IWH.REC = ""
 END
 P_X  = 28 ; P_Y = SLN ; P_VALUE = OCONV(INT(((OSD.G.QTY<1,N> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) "R#10" ; P_OPT = ""
 P_X  := AM:39 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((OSD.R.QTY<1,N> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) "R#10"
 P_X  := AM:50 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((OSD.S.QTY<1,N> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1) "R#10"
 CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
 P_X  = 3 ; P_Y = SLN+1 ; P_VALUE = INV.FULL.DESC "L#45" ; P_OPT = ""
 CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
 IF OSD.JOB<1,N,2> # "" THEN
   P_X  = 50 ; P_Y = SLN+1 ; P_VALUE = "MULTI" "L#8" ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
 END ELSE
   P_X  = 50 ; P_Y = SLN+1 ; P_VALUE = OSD.JOB<1,N> "L#8" ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
 END
 RETURN
*
*---- Display data
8000*
 ECD.ACTION = 3; CALL SCRN.EDIT
 OLD.START.LINE = 0; GOSUB 7900
 RETURN
*
9000*
*COPY>ICSBP>INV.UM.CNV
 RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
* PRINT @(0,23) : ERRMSG : CL :
* INPUT XX :
* PRINT @(0,23) : CL :
* RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000*
* PRINT @(0,23) : ERRMSG : CL :
* INPUT XX :
* PRINT @(0,23) : CL :
99999*
 ECD.ACTION=99;CALL SCRN.EDIT
 RETURN
END
