*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE        - OPSBP
* PROGRAM       - ORDER.INQ.RPT
* BY            - JULIANNE RIVERA, VERCOM Software Inc.
* DATE          - 04/05/90
* DESCRIPTION   -
* P.S           - A work file will be required to replace ORD.DET.SUM
********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>OPS.CPYLIB>ORDER
*COPY>OPS.CPYLIB>ORDER.DETAIL
*COPY>OPS.CPYLIB>ORDER.DETAIL.INQ
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- Get buffer
*
      PROCREAD BUFFER ELSE
         ERRMSG = "Must be executed from a PROC"
         GOSUB 91000; GOTO 99999
      END
      CONO = BUFFER<1>
      HD = BUFFER<2>
      ORDER.PRINT = BUFFER<3>
      START.NO = BUFFER<4>
      END.NO = BUFFER<5>
      START.DATE = BUFFER<6>
      END.DATE = BUFFER<7>
      SHIP.TO.BREAK = BUFFER<8>
*
*---- Open files
*
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "Cannot locate the CONTROL file";GOTO 93000
      END
      OPEN "","JOB" TO JOB ELSE
         ERRMSG = "Cannot locate the JOB file";GOTO 93000
      END
      OPEN "","CUSTOMER" TO CUSTOMER ELSE
         ERRMSG = "Cannot locate the CUSTOMER file";GOTO 93000
      END
      OPEN "","INVENTORY" TO INVENTORY ELSE
         ERRMSG = "Cannot locate the INVENTORY file";GOTO 93000
      END
      OPEN "","INV.WHSE" TO INV.WHSE ELSE
         ERRMSG = "Cannot locate the INV.WHSE file";GOTO 93000
      END
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "Cannot locate the COMPANY file";GOTO 93000
      END
      OPEN "","SHIP.TO" TO SHIP.TO ELSE
         ERRMSG = "Cannot locate the SHIP.TO file";GOTO 93000
      END
      OPEN "","ORDER" TO ORDER ELSE
         ERRMSG = "Cannot locate the ORDER file";GOTO 93000
      END
      OPEN "","ORDER.DETAIL" TO ORDER.DETAIL ELSE
         ERRMSG = "Cannot locate the ORDER.DETAIL file";GOTO 93000
      END
*
*---- INITIALIZATION
*
      LINE.CNT = 99
      NEW.ORD.NO = ""
      IF ORDER.PRINT = "N" THEN
         ORDER.PRT = SPACE(2):"For New Orders"
      END ELSE
         ORDER.PRT = "For Printed Orders"
      END
      HD4 = SPACE(8):"Item":SPACE(26):"Description":SPACE(20):"Whse"
      HD4 = HD4:SPACE(6):"Order":SPACE(6)
      HD4 = HD4:"Allocate":SPACE(5):"Manufact":SPACE(5)
      HD4 = HD4:"Job No"
      HD5 = SPACE(3):STR("-",15):SPACE(3)
      HD5 = HD5:STR("-",45):SPACE(3):"----"
      HD5 = HD5:SPACE(3):STR("-",10):SPACE(3)
      HD5 = HD5:STR("-",10):SPACE(3):STR("-",10)
      HD5 = HD5:SPACE(3):STR("-",8)
*
*---- Get order number
100*
      PRINTER ON
      DATA = 1
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         MATREAD ORD.REC FROM ORDER, ID ELSE
            GOTO 199
         END
         ORDNO = ID[4,8]
         IF LINE.CNT > 50 OR ORDNO # NEW.ORD.NO THEN GOSUB 2000
         NEW.ORD.NO = ORDNO
         PRINT
         PRINT
         PRINT SPACE(25):"ORDER NO : ":ORDNO"L#8":SPACE(25):"ORDER DATE : ":OCONV(ORD.DATE,"D2/")"L#8"
         MATREAD CUST.REC FROM CUSTOMER, CONO:ORD.CUST ELSE
            MAT CUST.REC = ""; CUST.NAME = "Unknown"
         END
         PRINT
         PRINT SPACE(25):"CUST ID  : ":ORD.CUST"L#10":SPACE(23):"DUE DATE   : ":OCONV(ORD.DUE,"D2/")"L#8"
         PRINT
         PRINT SPACE(25):CUST.NAME"L#30"
         PRINT SPACE(25):CUST.ADDR1"L#30"
         IF CUST.ADDR2 # "" THEN
            PRINT SPACE(25):CUST.ADDR2"L#30"
            LINE.CNT = LINE.CNT + 1
         END
         IF CUST.ADDR3 # "" THEN
            PRINT SPACE(25):CUST.ADDR3"L#30"
            LINE.CNT = LINE.CNT + 1
         END
         PRINT SPACE(25):CUST.ZIP"L#9"
         PRINT
         LINE.CNT = LINE.CNT + 10
         GOSUB 3000
         IF SHIP.TO.BREAK = "P" THEN
            CALL ORDER.SHIP.TO.SUB(CONO,HD,ORD.CUST,ORDNO)
         END
199   REPEAT
      GOTO 99999
*
*---- HEADINGS
2000*
      HDT = HD:" 'L'":SPACE(66):ORDER.PRT
      LINE.CNT = 3
      IF START.NO # "ALL" THEN
         HDT = HDT:" 'L'":SPACE(57):"With Numbers Between ":START.NO:" And ":END.NO
         LINE.CNT = LINE.CNT + 1
      END
      IF START.DATE # "ALL" THEN
         HDT = HDT:" 'L'":SPACE(57):"Starting ":START.DATE:" And Ending ":END.DATE
         LINE.CNT = LINE.CNT + 1
      END
      HEADING HDT
      RETURN
*
*---- Build ORD.DET.INQ & ORD.DET.SUM
3000*
      STATUS = "L"; SHPNO = "ALL"
      CALL ORDER.LINE.UPD(CONO,ORDNO,SHPNO,STATUS)
      SHPNO = ""
      L.CNT = 99
      DCNT = DCOUNT(ODQ.PROD,VM)
      FOR D = 1 TO DCNT
         IF L.CNT > 50 THEN GOSUB 8000
         MATREAD INV.REC FROM INVENTORY, CONO:ODQ.PROD<1,D> ELSE
            MAT INV.REC = ""; INV.FULL.DESC = "UNKNOWN"
         END
*COPY>ICSBP>INV.UM.CNV
         LINE = SPACE(3):ODQ.PROD<1,D>"L#15"
         LINE = LINE:SPACE(3):INV.FULL.DESC"L#45"
         LINE = LINE:SPACE(3):ODQ.WHSE<1,D>"L#4"
         LINE = LINE:SPACE(3):OCONV(INT(((ODQ.O.QTY<1,D> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)"R#10"
         LINE = LINE:SPACE(3):OCONV(INT(((ODQ.R.QTY<1,D> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)"R#10"
         LINE = LINE:SPACE(3):OCONV(INT(((ODQ.A.QTY<1,D> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)"R#10"
         IF ODQ.JOB<1,D,2> = "" THEN
            LINE = LINE:SPACE(3):ODQ.JOB<1,D>"L#8"
         END ELSE
            LINE = LINE:SPACE(3):"MULTI" "L#8"
         END
         PRINT LINE
         L.CNT = L.CNT + 1
      NEXT D
      RETURN
8000*
      HD1 = HD:" 'L' ":SPACE(55):"O R D E R   L I N E   I T E M   L I S T"
      HEADING HD1
      PRINT
      PRINT
      PRINT HD4
      PRINT HD5
      PRINT
      L.CNT = 8
      RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : ERRMSG : @(-4) :
*       INPUT XX :
*       PRINT @(0,23) : @(-4) :
*       RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000 PRINT @(0,23) : ERRMSG : @(-4) :
*       INPUT XX :
*       PRINT @(0,23) : @(-4) :
99999
      PRINTER OFF
      END
