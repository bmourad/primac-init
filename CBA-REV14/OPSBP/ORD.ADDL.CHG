  SUBROUTINE ORD.ADDL.CHG(CONO,ORDNO,SEL)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>ICS.CPYLIB>COM.INV.CNV
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - ORD.ADDL.CHG
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC.
* DATE        - 06/01/92
* DESCRIPTION -
*
* TASK 20532 JR EXPAND INVOICE AMOUNT TO 8.2
*T22267 renee 11/04/1997 * When the default description is changed, the
*                          system reverts back to the default description 
*                          when editing the line.
*T22344 rik 01/02/1998 * ALLOW INVOICE CODE AS A HIDDEN LINE ON LINE # 1
*T23280 lanny 09/29/1998 * Closed or Cancelled Orders Add'l chg info not
*                          showing on display.
*T25978 adelgado 02/18/2002 * Add the use of prompts (S,SR,SB,ST).
*T28908 lross 05/09/2006 * Allow display of Add'l Chgs regardless of
*                          Order Status.
*********************************************************************
*
*--- INSERT FILES EQUETES
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>OPS.CPYLIB>ORDER
*COPY>PMC.CPYLIB>SALES.CODE
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>INVOICE.CODE
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- INITIALIZATION
*
  ESN = ECD.SCRN.NO
  ECD.ACTION=2;CALL SCRN.EDIT
  FOR X = 1 TO 75
    SCV.REC(X)<ESN> = ""
  NEXT X
  ERRMSG = ""
  LINE.SPACE = 1
  BEGIN.PAGE = 9
  PAGE.SIZE = 10
  LINES = 0
  LN = 1
  OLD.START = 0
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
  SCV.REC(1)<ESN> = ORDNO
  ECD.NUM = 1;ECD.ACTION=5;CALL SCRN.EDIT
  SCV.REC(2)<ESN> = ORD.CUST
  ECD.NUM = 2;ECD.ACTION=5;CALL SCRN.EDIT
  SCV.REC(3)<ESN> = CUST.NAME
  ECD.NUM = 1;ECD.ACTION=5;CALL SCRN.EDIT
*T23280 v
  LINES = DCOUNT(ORD.AC.CHG.CD,VM)
  GOSUB 80000
*T23280 ^
  IF SEL = "M" THEN
    P.NUM = 21
    GOTO 110
  END ELSE
*T28908 v
    LINES = DCOUNT(ORD.AC.CHG.CD,VM)
    LN = 1
    GOSUB 80000
*T28908 ^
    P.NUM = 23
    GOTO 500
  END
*
*--- PRINT PRODUCT AND DESCRIPTION
*
110 *
  LINES = DCOUNT(ORD.AC.CHG.CD,VM)
  IF LINES = 0 THEN
    ACTION = "A"
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 10000
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END ELSE
    ACTION = ""
    LN = 1
  END
  GOSUB 80000
*
*--- ENTER ACTION
*
500 *
  ECD.NUM = P.NUM
  ECD.ACTION = 4; CALL SCRN.EDIT
  ACTION = ECD.RET.VALUE
  BEGIN CASE
    CASE ACTION = "E" OR ACTION = "END" OR ACTION = ""
      GOTO 99999
    CASE ACTION = "A"
      LOOP
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 10000
      WHILE LINES > OLD.LINES DO REPEAT
      LN = LINES
      GOSUB 12000
    CASE ACTION = "C"
      GOSUB 30000
      IF LNO THEN
        LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
        GOSUB 10100
        GOSUB 12000
      END
    CASE ACTION = "D"
      GOSUB 30000
      IF LNO THEN
        LN = LNO
        ORD.AC.CHG.CD = DELETE(ORD.AC.CHG.CD,1,LN,0)
        ORD.AC.CHG.CAT = DELETE(ORD.AC.CHG.CAT,1,LN,0)
        ORD.AC.SLC = DELETE(ORD.AC.SLC,1,LN,0)
        ORD.AC.DESC = DELETE(ORD.AC.DESC,1,LN,0)
        ORD.AC.AMOUNT = DELETE(ORD.AC.AMOUNT,1,LN,0)
        ORD.AC.HIDDEN = DELETE(ORD.AC.HIDDEN,1,LN,0)
        ORD.AC.INV = DELETE(ORD.AC.INV,1,LN,0)
        LINES = DCOUNT(ORD.AC.CHG.CD,VM)
        IF LN > LINES THEN LN = LINES
        IF LN < 1 THEN LN = 1
        OLD.START = 0
        GOSUB 12000
      END
    CASE ACTION = "I"
      GOSUB 30000
      IF LNO THEN
        LN = LNO
        ORD.AC.CHG.CD = INSERT(ORD.AC.CHG.CD,1,LN,0,"")
        ORD.AC.CHG.CAT = INSERT(ORD.AC.CHG.CAT,1,LN,0,"")
        ORD.AC.SLC = INSERT(ORD.AC.SLC,1,LN,0,"")
        ORD.AC.DESC = INSERT(ORD.AC.DESC,1,LN,0,"")
        ORD.AC.AMOUNT = INSERT(ORD.AC.AMOUNT,1,LN,0,"")
        ORD.AC.HIDDEN = INSERT(ORD.AC.HIDDEN,1,LN,0,"")
        ORD.AC.INV = INSERT(ORD.AC.INV,1,LN,0,"")
        LINES = DCOUNT(ORD.AC.CHG.CD,VM)
        IF LN > LINES THEN LN = LINES
        IF LN < 1 THEN LN = 1
        OLD.START = 0
        GOSUB 12000
        GOSUB 10000
      END
    CASE ACTION = "S"
      LN = LN + PAGE.SIZE
      IF LN > LINES THEN LN = 1
    * T25978 v
    CASE ACTION = 'SR'
      LN -= PAGE.SIZE
      IF LN < 1 THEN LN = 1
    CASE ACTION = 'ST'
      LN = 1
    CASE ACTION = 'SB'
      LN = LINES
    * T25978 ^
  END CASE
  GOSUB 80000
  GOTO 500
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- LINE ENTRY
*
10000 *
  GOSUB 80000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#2" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10100 *
  X = 3; Y = SLN; TYP = 1; MAXL = 4; O.R = "O"; HMSG = "Enter Invoice Code or <RETURN> for Cross Reference"
  IF ORD.AC.CHG.CD<1,LN> # "" THEN
    DEFAULT = ORD.AC.CHG.CD<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN
    IF ACTION = "A" THEN
      ORD.AC.CHG.CD = DELETE(ORD.AC.CHG.CD,1,LN,0)
      ORD.AC.CHG.CAT = DELETE(ORD.AC.CHG.CAT,1,LN,0)
      ORD.AC.AMOUNT = DELETE(ORD.AC.AMOUNT,1,LN,0)
      ORD.AC.DESC = DELETE(ORD.AC.DESC,1,LN,0)
      ORD.AC.AMOUNT = DELETE(ORD.AC.AMOUNT,1,LN,0)
      ORD.AC.HIDDEN = DELETE(ORD.AC.HIDDEN,1,LN,0)
      ORD.AC.INV = DELETE(ORD.AC.INV,1,LN,0)
      LINES = DCOUNT(ORD.AC.CHG.CD,VM)
    END
    OLD.START = 0
    GOTO 19999
  END
  IF VALUE = "" OR VALUE = "???" THEN
    MAT GEN.XREF.REC = ""
    GXR.CO = CONO
    GXR.SORT.FILE = "INVOICE.CODE"; GXR.FILE = INVOICE.CODE
    GXR.NAME = "GEN.CODE.0"
    CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
    ECD.ACTION = 2; CALL SCRN.EDIT
    GOSUB 30500
    P_X  = 0 ; P_Y = SLN ; P_VALUE = LN"R#2" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    IF GXR.ID = "" THEN
      GOTO 10100
    END
    VALUE = GXR.ID
    P_X  = 3 ; P_Y = SLN ; P_VALUE = GXR.ID"L#4" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END 
  LOCATE VALUE IN ORD.AC.CHG.CD<1>,1 SETTING FND ELSE FND = 0
  IF FND THEN
    IF FND # LN THEN
      ERRMSG = "* * ":VALUE:" ALREADY EXIST ON LINE # ":FND:" * *"
      GOSUB 91000; GOTO 10100
    END
  END
  MATREAD INC.REC FROM INVOICE.CODE, CONO : VALUE ELSE
    ERRMSG = "Invalid Invoice Code"
    GOSUB 91000; GOTO 10100
  END
  BEGIN CASE
*T22344 v
*  CASE INC.HIDDEN = "Y" AND LN = 1
*    ERRMSG = "Hidden line cannot be first line entered"
*    GOSUB 91000; GOTO 10100
*T22344 ^
    CASE INC.CATEGORY = "TAX"
      ERRMSG = "Cannot utilize a TAX code for the additional charges"
      GOSUB 91000; GOTO 10100
  END CASE
  ORD.AC.CHG.CD<1,LN> = VALUE
  ORD.AC.CHG.CAT<1,LN> = INC.CATEGORY
  ORD.AC.HIDDEN<1,LN> = INC.HIDDEN
  LINES = DCOUNT(ORD.AC.CHG.CD,VM)
10200*
  IF INC.CATEGORY = "CMT" THEN
    ORD.AC.SLC<1,LN> = ""
    P_X  = 8 ; P_Y = SLN ; P_VALUE = ORD.AC.SLC<1,LN>"L#3" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 10300
  END
10201*
  X = 8; Y = SLN; TYP = 1; O.R = "O"
  IF ORD.AC.CHG.CAT<1,LN> = "SHP" THEN
    MAXL = 8
    HMSG = "Enter Ship Via or <RETURN> for Cross Reference"
  END ELSE
    MAXL = 3
    HMSG = "Enter Sales Code or <RETURN> for Cross Reference"
  END
  IF ORD.AC.SLC<1,LN> # "" THEN
    DEFAULT = ORD.AC.SLC<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  IF VALUE = "" OR VALUE = "???" THEN
    MAT GEN.XREF.REC = ""
    GXR.CO = CONO
    IF ORD.AC.CHG.CAT<1,LN> = "SHP" THEN
      GXR.SORT.FILE = "SHIP.VIA"; GXR.FILE = SHIP.VIA
      GXR.NAME = "GEN.CODE1"
    END ELSE
      GXR.SORT.FILE = "SALES.CODE"; GXR.FILE = SALES.CODE
      GXR.NAME = "GEN.CODE.0"
    END
    CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
    ECD.ACTION = 2; CALL SCRN.EDIT
    P_X  = 8 ; P_Y = SLN ; P_VALUE = " " ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOSUB 30500
    IF GXR.ID = "" THEN
      GOTO 10201
    END
    VALUE = GXR.ID
    P_X  = 8 ; P_Y = SLN ; P_VALUE = GXR.ID"L#8" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END 
  IF VALUE # "" THEN
    IF ORD.AC.CHG.CAT<1,LN> = "SHP" THEN
      MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO : VALUE ELSE
        ERRMSG = "Cannot locate Ship Via # ":VALUE; GOSUB 91000; GOTO 10201
      END
    END ELSE
      MATREAD SLC.REC FROM SALES.CODE, CONO : VALUE ELSE
        ERRMSG = "Cannot locate Sales Code # ":VALUE; GOSUB 91000; GOTO 10201
      END
    END
  END
  ORD.AC.SLC<1,LN> = VALUE
*
*---- GET DESCRIPTION
10300*
  X = 17; Y = SLN; TYP = 1; MAXL = 45; O.R = "O"; HMSG = "Enter Description"
  O.R = "O"
* T22267 v   Check if screen variable first, if null then pull default
  DEFAULT = ORD.AC.DESC<1,LN>
  IF DEFAULT = "" THEN DEFAULT = INC.DESC
* T22267 ^
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  ORD.AC.DESC<1,LN> = VALUE
*
*---- GET AMOUNT
10400*
  IF INC.CATEGORY # "CMT" THEN
    X = 63; Y = SLN; TYP = 4; SCALER = 2; MAXL = 12; HMSG = "Enter Amount" ;* T20532
    IF ORD.AC.AMOUNT<1,LN> # "" THEN
      DEFAULT = OCONV(ORD.AC.AMOUNT<1,LN>,"MD2")
      O.R = "O"
    END
    CALL EDIT.SUB
    IF VALUE = "END" THEN GOTO 10100
    ORD.AC.AMOUNT<1,LN> = VALUE
  END
  GOSUB 12000
*
*---- PRINT INVOICE FLAG
10500*
  IF ORD.AC.INV<1,LN> = "" THEN ORD.AC.INV<1,LN> = "N"
  P_X  = 77 ; P_Y = SLN ; P_VALUE = ORD.AC.INV<1,LN>"L#1" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  IF SCV.REC(23)<ESN> # "" THEN
    FOR ECD.NUM = 23 TO 25
      SCV.REC(ECD.NUM)<ESN> = ""
      ECD.ACTION = 5;CALL SCRN.EDIT
    NEXT ECD.NUM
  END
  LINES = DCOUNT(ORD.AC.CHG.CD,VM)
19999 *
  RETURN
*
*---- CALCULATE AND DISPLAY TOTAL
12000*
  AMT.TOT = 0
  FOR REF = 1 TO LINES
    AMT.TOT = AMT.TOT + ORD.AC.AMOUNT<1,REF>
  NEXT REF
  SCV.REC(19)<ESN> = AMT.TOT
  ECD.NUM = 19;ECD.ACTION = 5;CALL SCRN.EDIT
  RETURN
30000 *
  GOSUB 80000
  ECD.MINV = ST.LINE
  ECD.MAXV = LST.LINE
  ECD.NUM = 22
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*--- SCROLL
*
80000 *
  ST.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LST.LINE = ST.LINE + PAGE.SIZE -1
  IF LST.LINE > LINES THEN LST.LINE = LINES
  IF ST.LINE = OLD.START THEN RETURN
  OLD.START = ST.LINE
  CNT = 1
  FOR N = ST.LINE TO LST.LINE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SSLN ; P_VALUE = N "R#2" ; P_OPT = "CL"
    P_X  := AM:3 ; P_Y := AM:SSLN ; P_VALUE := AM:ORD.AC.CHG.CD<1,N> "L#4"
    P_X  := AM:8 ; P_Y := AM:SSLN ; P_VALUE := AM:ORD.AC.SLC<1,N> "L#8"
    P_X  := AM:17 ; P_Y := AM:SSLN ; P_VALUE := AM:ORD.AC.DESC<1,N> "L#45"
    P_X  := AM:63 ; P_Y := AM:SSLN ; P_VALUE := AM:OCONV(ORD.AC.AMOUNT<1,N>, "MD2") "R#12" ;* T20532
    P_X  := AM:77 ; P_Y := AM:SSLN ; P_VALUE := AM:ORD.AC.INV<1,N> "L#1"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
  GOSUB 12000
  RETURN
30500 ECD.ACTION = 3; CALL SCRN.EDIT
  OLD.START = 0; GOSUB 80000
30999 RETURN
*
*--- ERROR ROUTINE
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 *
*       PRINT @(0,23) : ERRMSG : CL :
*       INPUT REPLY :
*       PRINT @(0,23) : CL :
*       RETURN
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
