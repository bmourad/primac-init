   SUBROUTINE OPS.EOM.POST.FG2(L,ORD.DIV)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.EOM.POST
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.COMP.OPS
*COPY>OPS.CPYLIB>XCOM.OPS.INVOICEX
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - OPS.EOM.POST.FG2
* BY          - WALID YAMOUT, C.B.A
* DATE        - 02/28/87
* DESCRIPTION -
*T25740 epitka 02/01/2002 * REV12
*ENDDOC
*********************************************************************
*
*---- INSERT FILE EQUATE
*
*COPY>JCS.CPYLIB>EOM.ACCT
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>PMC.CPYLIB>POST.REJECTS
*COPY>OPS.CPYLIB>ORDER.INVOICE
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INTITIALIZATION
*
   NEW.REC = 0
   ORDNO = IVC.CHG.ORDER<1,L>
   PCNT = DCOUNT(IVC.PROD<1,L>,SVM)
   FOR P = 1 TO PCNT
      PROD = IVC.PROD<1,L,P>
      AMT = IVC.SHP.COST<1,L,P>
      MATREAD INV.REC FROM INVENTORY, CONO:PROD ELSE
         MAT PRR.REC = ""
         PRR.JOB = ORDNO
         PRR.FILE = "INVENTORY"
         PRR.ID = PROD
         PRR.ERR = "CANNOT LOCATE"
         PRR.SEQ = PRR.SEQ + 1
         MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
         GOTO 999
      END
      MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
         MAT PRR.REC = ""
         PRR.JOB = ORDNO
         PRR.FILE = "CATEGORY"
         PRR.ID = INV.LINE
         PRR.ERR = "CANNOT LOCATE"
         PRR.SEQ = PRR.SEQ + 1
         MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
         GOTO 999
      END
      BEGIN CASE
         CASE CATG.COG = MT.COG.ACCT
         CASE CATG.COG # ""
            CATG.COG = CATG.COG : STR("0",IN.ACCT.LEN-LEN(CATG.COG))
            CATG.COG = CATG.COG[1,IN.ACCT.LEN]
            MATREAD COA.REC FROM COA, CONO : CATG.COG ELSE COA.LEVEL = 0
            MT.COG.ACCT = CATG.COG
            MT.COG.LEVEL = COA.LEVEL
         CASE GLTB.MT.DIR.COG = MT.COG.ACCT
         CASE 1
            MATREAD COA.REC FROM COA, CONO : GLTB.MT.DIR.COG ELSE COA.LEVEL = 0
            MT.COG.ACCT = GLTB.MT.DIR.COG
            MT.COG.LEVEL = COA.LEVEL
      END CASE
      BEGIN CASE
         CASE MT.COG.LEVEL < 1
            T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : MT.COG.ACCT
         CASE 1
            T.ACCT = ORD.DIV : GEN.DEPT : GEN.CCTR : MT.COG.ACCT
      END CASE
      BEGIN CASE
         CASE AMT + 0 > 0
            TRAN.AMT = AMT
            GOSUB 1000
         CASE AMT + 0 < 0
            TRAN.AMT = 0 - AMT
            GOSUB 2000
      END CASE
*     BEGIN CASE
*     CASE SHIP.WIP.LEVEL < 1
*        T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : SHIP.WIP.ACCT
*     CASE 1
*        T.ACCT = ORD.DIV : GEN.DEPT : GEN.CCTR : SHIP.WIP.ACCT
*     END CASE
      BEGIN CASE
         CASE CATG.SHIP.WIP = ''
            BEGIN CASE
               CASE SHIP.WIP.LEVEL < 1
                  T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : SHIP.WIP.ACCT
               CASE 1
                  T.ACCT = ORD.DIV : GEN.DEPT : GEN.CCTR : SHIP.WIP.ACCT
            END CASE
         CASE 1
            BEGIN CASE
               CASE SHIP.WIP.LEVEL < 1
                  T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : CATG.SHIP.WIP
               CASE 1
                  T.ACCT = ORD.DIV : GEN.DEPT : GEN.CCTR : CATG.SHIP.WIP
            END CASE
      END CASE
      BEGIN CASE
         CASE AMT + 0 > 0
            TRAN.AMT = AMT
            GOSUB 2000
         CASE AMT + 0 < 0
            TRAN.AMT = 0 - AMT
            GOSUB 1000
      END CASE
999 *
   NEXT P
   GOTO 99999
*
*---- DEBIT
*
1000 *
   ETR.ID = CONO : T.ACCT : ORDNO : "!FG-D*" : NEW.REC
   MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
      MAT ETR.REC = ""
      ETR.REF = IVC.CUST.NO
   END
   ETR.AMT = ETR.AMT + TRAN.AMT
   PTR=DCOUNT(ETR.TRAN<1>,VM)+1
   ETR.TRAN<1,PTR>=''            
   ETR.DATE<1,PTR>=IVC.PROC.DATE 
   ETR.TAMT<1,PTR>=(TRAN.AMT)   
   IF PTR > 99 THEN NEW.REC = NEW.REC+1
   MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
   RETURN
*
*---- CREDIT
*
2000 *
   ETR.ID = CONO : T.ACCT : ORDNO : "!FG-C*" : NEW.REC
   MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
      MAT ETR.REC = ""
      ETR.REF = IVC.CUST.NO
   END
   ETR.AMT = ETR.AMT - TRAN.AMT
   PTR=DCOUNT(ETR.TRAN<1>,VM) +1
   ETR.TRAN<1,PTR>=''
   ETR.DATE<1,PTR>=IVC.PROC.DATE
   ETR.TAMT<1,PTR>=-(TRAN.AMT)
   IF PTR > 99 THEN NEW.REC = NEW.REC+1
   MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
   RETURN
*
*---- END
*
99999 *
   RETURN
END
