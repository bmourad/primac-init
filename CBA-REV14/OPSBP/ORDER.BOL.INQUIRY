   SUBROUTINE SHIPMENT.INQ(CONO,ORDER.NO)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>ICS.CPYLIB>COM.INV.CNV
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - OPSBP
* PROGRAM     - ORDER.BOL.INQUIRY
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DATE        - 02/07/2002
* TASK #      - 25952
* DESCRIPTION - This subroutine display BOL history for one order.
*T27359 lhelms 04/09/2003 * QTY DISPLAY WRONG
*ENDDOC
*********************************************************************
*
*---- INSERT FILES EQUATES
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>OPS.CPYLIB>BOL
*COPY>OPS.CPYLIB>ORDER
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>PMC.CPYLIB>SHIP.VIA
*
*---- OPEN FILES
*
   OPEN "BOL" TO BOL ELSE
      ERRMSG = "CANNOT LOCATE THE BOL FILE"; GOTO 93000
   END
   OPEN "SHIP.TO" TO SHIP.TO ELSE
      ERRMSG = "CANNOT LOCATE THE SHIP.TO FILE"; GOTO 93000
   END
   OPEN "SHIP.VIA" TO SHIP.VIA ELSE
      ERRMSG = "CANNOT LOCATE THE SHIP.VIA FILE"; GOTO 93000
   END
   OPEN "INV.WHSE" TO INV.WHSE ELSE
      ERRMSG = "CANNOT LOCATE THE INV.WHSE FILE"; GOTO 93000
   END
*
*---- INITIALIZATION
*
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
   ESN = ECD.SCRN.NO
   FOR SCV = 1 TO SCV.REC.SIZE
      SCV.REC(SCV)<ECD.SCRN.NO>=""
   NEXT SCV
   ECD.ACTION=2;CALL SCRN.EDIT
   REF.CNT = 0
   REF.NO = ''
   CURR.REF.NO = ''
   LINE.COUNT = 5
   REF.CNT = ''
   SHIP.TOTAL = ''
*
*---- BUILD DATA ARRAY TO DISPLAY
*
   BCNT = DCOUNT(ORD.BOL<1>,VM)
   FOR B = 1 TO BCNT
      BOL.ID = ORD.BOL<1,B>
      MATREAD BOL.REC FROM BOL, CONO : BOL.ID ELSE MAT BOL.REC = ''
      IF BOL.STATUS = 'CANCEL' THEN GO 2
      MATREAD SPT.REC FROM SHIP.TO, CONO:BOL.CUST:'!':BOL.SHIP.TO ELSE
         MAT SPT.REC = ''
      END
      MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:BOL.SHIP.VIA ELSE
         MAT SHIP.VIA.REC = ''
      END
      REF.CNT = REF.CNT + 1
      SCV.REC(7)=INSERT(SCV.REC(7),ESN,1,0,BOL.ID)
      SCV.REC(8)=INSERT(SCV.REC(8),ESN,1,0,BOL.SHP.DATE)
      SCV.REC(9)=INSERT(SCV.REC(9),ESN,1,0,SPT.ADDR1)
      SCV.REC(11)=INSERT(SCV.REC(11),ESN,1,0,REF.CNT)
      PCNT = DCOUNT(BOL.ORDER<1>,VM); PROD.TOTAL = 0 ; LOADED = 0
      FOR P = 1 TO PCNT
         LOC.CNT = 0 ;* T27359
         LOC.CNT = DCOUNT(BOL.QTY<1,P>,SVM) ;* T27359
         IF ORDER.NO = BOL.ORDER<1,P> THEN
            FOR L = 1 TO LOC.CNT
               PROD.TOTAL = PROD.TOTAL + SUM((BOL.QTY<1,P,L>)/1000) ;* T27359
               SHIP.TOTAL = SHIP.TOTAL + SUM((BOL.QTY<1,P,L>)/1000) ;* T27359
            NEXT L
            READ IWH.DATA FROM INV.WHSE, CONO:BOL.PROD<1,P>:'!':BOL.WHSE<1,P> THEN
               IF LOADED = 0 THEN
                  LOCATE BOL.FI.NO<1,P,1> IN IWH.DATA<56>,1 SETTING BFN THEN
                     SCV.REC(12)=INSERT(SCV.REC(12),ESN,1,0,IWH.DATA<38,BFN>)
                     LOADED = 1
                  END
               END
            END
         END
      NEXT P
      SCV.REC(14)=INSERT(SCV.REC(14),ESN,1,0,OCONV(PROD.TOTAL,'MD0,'))
      SCV.REC(15)=INSERT(SCV.REC(15),ESN,1,0,SPT.NAME)
      SCV.REC(16)=INSERT(SCV.REC(16),ESN,1,0,SHPV.DESC)
      SCV.REC(17)=INSERT(SCV.REC(17),ESN,1,0,BOL.INVOICE)
2 *
   NEXT B
   SCV.REC(1)<ESN> = ORDER.NO
   ECD.NUM=1;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(6)<ESN> = BOL.CUST
   ECD.NUM=6;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(13)<ESN> = CUST.NAME
   ECD.NUM=13;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(21)<ESN> = OCONV(SHIP.TOTAL,'MD0,')
   ECD.NUM=21;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(32)<ESN> = INT(REF.CNT / LINE.COUNT + .9)
   ECD.NUM=32;ECD.ACTION=5;CALL SCRN.EDIT
   IF REF.CNT GT 0 THEN GOSUB 1900
   LOOP
      ECD.NUM = 41; SCV.REC(ECD.NUM)<ESN> = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE ACTION = "" OR ACTION = "END"
            ACTION = "END"
         CASE ACTION = "S" AND REF.CNT GT 0
            REF.NO=CURR.REF.NO + LINE.COUNT
            IF REF.NO > REF.CNT THEN REF.NO=1
            GOSUB 1900
         CASE ACTION = "SF" AND REF.CNT GT 0
            REF.NO=CURR.REF.NO + LINE.COUNT
            IF REF.NO > REF.CNT THEN REF.NO=1
            GOSUB 1900
         CASE ACTION = "SR" AND REF.CNT GT 0
            REF.NO=CURR.REF.NO - LINE.COUNT
            IF REF.NO < 1 THEN REF.NO=1
            GOSUB 1900
         CASE ACTION = "ST"
            REF.NO = 1
            GOSUB 1900
         CASE ACTION = "SB"
            REF.NO = REF.CNT
            GOSUB 1900
         CASE NOT(NUM(ACTION))
            ERRMSG = "Invalid entry, please re-enter"
            GOSUB 91000
         CASE ACTION LT 1 OR ACTION GT REF.CNT
            ERRMSG = "Out of range, please re-enter"
            GOSUB 91000
         CASE 1
            LOCATE ACTION IN SCV.REC(11)<ESN>,1 SETTING BCNT THEN
               BOL.PRINT.NO = SCV.REC(7)<ESN,BCNT>
               CALL SHIPTO.BOL.PRINT.SUB(CONO,BOL.PRINT.NO,'P')
            END
      END CASE
   WHILE ACTION # "END" DO REPEAT
   GOTO 99999
*
*---- Display Multi Line fields
1900*
   START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)* LINE.COUNT
   IF START.REF.NO = CURR.REF.NO THEN RETURN
   CURR.REF.NO = START.REF.NO
   IF CURR.REF.NO LT LINE.COUNT THEN
      PG.NO = 1
   END ELSE
      PG.NO = INT(CURR.REF.NO / LINE.COUNT) + 1
   END
   ECD.NUM=11;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
*  ECD.NUM=12;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=13;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=14;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=15;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=16;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=17;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=6;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=7;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=8;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=9;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   SCV.REC(31)<ESN> = PG.NO
   ECD.NUM=31;ECD.ACTION=5;CALL SCRN.EDIT
   RETURN
*
*---- Error message routine
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- End of job
*
99999*
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
