*COPY>CPYLIB>COMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* PROGRAM       - GLS.DICT.BUILD
* BY            - WALID YAMOUT, C.B.A
* DATE          - 01/30/86
* DESCRIPTION
*********************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>CPYLIB>DICT.FILE
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SETUP FOR SYSTEM ERRMSGS
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
      OPEN '','CPYLIB' TO CPYLIB ELSE
         ERRMSG = 'CPYLIB FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','M.SCREENS' TO M.SCREENS ELSE
         ERRMSG = 'M.SCREENS FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','GLS.DICT' TO GLS.DICT ELSE
         ERRMSG = 'GLS.DICT FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','GLS.DICT' TO GLS.DICT ELSE
         ERRMSG = 'GLS.DICT FILE IS MISSING'
         GOTO 93000
      END
*
**** MAIN PROCESSING
*
      MAT EDIT.COM.DRIVER = ''
      MAT EDIT.COM = ''
      TYP = 0 ; CALL EDIT.SUB
      FILL = "#"
      ECD.SCRN.CNT = 20
      ECD.SCRN.NAME<1> = "GLS.DICT.BUILD"
      ECD.SCRN.NAME<2> = "DICT.BUILD.SUB0"
      ECD.SCRN.NAME<3> = "DICT.BUILD.SUBA"
      ECD.SCRN.NAME<4> = "DICT.BUILD.SUBC"
      ECD.SCRN.NAME<5> = "DICT.BUILD.SUBD"
      ECD.SCRN.NAME<6> = "DICT.BUILD.SUBD1"
      ECD.SCRN.NAME<7> = "DICT.BUILD.SUBD2"
      ECD.SCRN.NAME<8> = "DICT.BUILD.SUBF"
      ECD.SCRN.NAME<9> = "DICT.BUILD.SUBG"
      ECD.SCRN.NAME<10> = "DICT.BUILD.SUBMD"
      ECD.SCRN.NAME<11> = "DICT.BUILD.SUBMF"
      ECD.SCRN.NAME<12> = "DICT.BUILD.SUBMP"
      ECD.SCRN.NAME<13> = "DICT.BUILD.SUBMT"
      ECD.SCRN.NAME<14> = "DICT.BUILD.SUBMX"
      ECD.SCRN.NAME<15> = "DICT.BUILD.SUBT"
      ECD.SCRN.NAME<16> = "DICT.BUILD.SUBTF"
      ECD.SCRN.NAME<17> = "DICT.BUILD.SUBU"
      ECD.SCRN.NAME<18> = "DICT.BUILD.SUBV"
      ECD.SCRN.NAME<19> = "GLS.DICT.TYPE.SUB1"
      ECD.SCRN.NAME<20> = "GLS.DICT.TYPE.SUB2"
      ECD.ACTION=1;CALL SCREEN.EDIT
      NO.TYPE = ""
      NO.FILE = ""
      NO.NAME = ""
      ALL.TYPE = "00"
      FOR LN = 1 TO 28
         ALL.TYPE<-1> = STR("0",2-LEN(LN)) : LN
      NEXT LN
      ALL.TYPE<-1> = "32"
      ALL.TYPE<-1> = "33"
      ALL.TYPE<-1> = "34"
*--- SELECT GLS.DICT FILE
      SELECT GLS.DICT
      DATA = 1
      LOOP
      IF DATA = 0 THEN GOTO 99999
      READNEXT ID ELSE DATA = 0
      WHILE DATA DO
      TEMP.TYPE = ID[1,2]
      TEMP.FILE = FIELD(ID,"!",1)
      TEMP.FILE = TEMP.FILE[3,99]
      TEMP.NAME = FIELD(ID,"!",2)
      LOCATE TEMP.FILE IN NO.FILE,1 SETTING PTR ELSE PTR = 0
      IF PTR THEN
         LOCATE TEMP.NAME IN NO.NAME<PTR>,1 SETTING SPTR ELSE SPTR = 0
         IF SPTR THEN
            IF TEMP.TYPE # NO.TYPE<PTR,SPTR> THEN
               ERRMSG = "CHECK GLS.DICT ":ID
               GOSUB 91000; DATA = 0
            END
         END ELSE
            NO.NAME<PTR,-1> = TEMP.NAME
            NO.TYPE<PTR,-1> = TEMP.TYPE
         END
      END ELSE
         NO.FILE<-1> = TEMP.FILE
         NO.TYPE<-1> = TEMP.TYPE
         NO.NAME<-1> = TEMP.NAME
      END
      REPEAT
**** PRINT SCREEN
1*
      ECD.SCRN.NO = 1
      MAT DICT.REC = ""
      S.NAME = ""
      S.CONV = ""
      S.CORR = ""
      ECD.RET.VALUES<ECD.SCRN.NO> = ''
      ECD.ACTION=6;CALL SCREEN.EDIT
*
**** ENTER FILE NAME
*
5*
      ECD.NUM = 1
      IF FILE.NAME # "" THEN ECD.DEFAULT = FILE.NAME
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = "END" THEN GOTO 99999
      FILE.NAME = ECD.RET.VALUE
      OPEN '',FILE.NAME TO F.NAME ELSE
         ERRMSG = FILE.NAME:" IS MISSING "
         GOSUB 91000 ; GOTO 5
      END
      OPEN 'DICT',FILE.NAME TO D.F.NAME ELSE
         ERRMSG = "DICT ":FILE.NAME:" IS MISSING"
         GOSUB 91000 ; GOTO 5
      END
*
**** ENTER ITEM NAME
*
7*
      ECD.NUM = 2
      ECD.ACTION = 4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 1
      ITEM.NAME = ECD.RET.VALUE
*--- GET TYPE
      NEW = 1
      FND = 1
      LOCATE FILE.NAME IN NO.FILE,1 SETTING PTR ELSE PTR = 0
      IF PTR THEN
         LOCATE ITEM.NAME IN NO.NAME<PTR>,1 SETTING SPTR ELSE SPTR = 0
         IF SPTR THEN
            ITEM.TYPE = NO.TYPE<PTR,SPTR>
            ECD.NUM = 17; ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM> = ITEM.TYPE
            ECD.ACTION = 5; CALL SCREEN.EDIT
            NEW = 0
         END
      END
      IF NEW THEN
8*
         ECD.NUM = 17; ECD.ACTION = 4; CALL SCREEN.EDIT
         IF ECD.RET.VALUE = "END" THEN GOTO 1
         IF ECD.RET.VALUE = "" THEN
            GOSUB 7000
            IF ECD.RET.VALUE = "N" OR ECD.RET.VALUE = "END" THEN GOTO 8
            ECD.NUM = 17; ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM> = ECD.RET.VALUE
            ECD.ACTION = 5; CALL SCREEN.EDIT
         END
         LOCATE ECD.RET.VALUE IN ALL.TYPE,1 SETTING PTR ELSE
            ERRMSG = "INVALID TYPE"; GOSUB 91000; GOTO 8
         END
         ITEM.TYPE = ECD.RET.VALUE
      END ELSE
         FND = 0
         MATREAD DICT.REC FROM GLS.DICT, ITEM.TYPE:FILE.NAME:"!":ITEM.NAME ELSE FND = 1
      END
      IF FND THEN
         FND = 0
         MATREAD DICT.REC FROM D.F.NAME , ITEM.NAME ELSE FND = 1
      END
      IF FND THEN
         FOR X = 1 TO 7 UNTIL ECD.RET.VALUE = "END"
            ON X GOSUB 100,200,300,400,500,600,700
         NEXT X
         IF ECD.RET.VALUE = "END" THEN GOTO 1
      END ELSE
         GOSUB 1000
      END
*
**** ENTER OPTIONS
*
10*
      ECD.NUM = 16
      ECD.RET.VALUES<1,ECD.NUM> = ''
      ECD.ACTION=4;CALL SCREEN.EDIT
      REQUEST = ECD.RET.VALUE
      BEGIN CASE
         CASE REQUEST = 'END' OR REQUEST = 'E'
            IF NEW THEN ERRMSG = "NEW RECORD WAS NOT FILED"; GOSUB 91000
            GOTO 1
         CASE NUM(REQUEST)
            ON REQUEST GOSUB 100,200,300,400,500,600,700
         CASE REQUEST = 'D'
            X=0;Y=21;O.R='R';TYP=8
            PMSG='YOU ARE ABOUT TO DELETE THIS ITEM OK (Y/N)'
            CALL EDIT.SUB
            PRINT @(0,21):CL:
            IF VALUE = "Y" THEN
               IF NEW = 0 THEN
                 DELETE GLS.DICT, ITEM.TYPE:FILE.NAME:"!":ITEM.NAME
                 LOCATE FILE.NAME IN NO.FILE,1 SETTING PTR ELSE PTR = 0
                 IF PTR THEN
                   LOCATE ITEM.NAME IN NO.NAME<PTR>,1 SETTING SPTR ELSE SPTR = 0
                   IF SPTR THEN
                     IF ITEM.TYPE = NO.TYPE<PTR,SPTR> THEN
                       NO.TYPE = DELETE(NO.TYPE,PTR,SPTR,0)
                       NO.NAME = DELETE(NO.NAME,PTR,SPTR,0)
                       IF NO.TYPE<PTR> = "" OR NO.NAME<PTR> = "" THEN
                         NO.TYPE = DELETE(NO.TYPE,PTR,0,0)
                         NO.NAME = DELETE(NO.NAME,PTR,0,0)
                         NO.FILE = DELETE(NO.FILE,PTR,0,0)
                       END
                     END
                   END
                 END
               END
               GOTO 1
            END
         CASE REQUEST = 'F'
            L.VALUE = LEN(DICT.S.NAME)
            V.VALUE = DICT.S.NAME
            GOSUB 4000
            DICT.S.NAME = FV.VALUE
            L.VALUE = LEN(DICT.CONV)
            V.VALUE = DICT.CONV
            GOSUB 4000
            DICT.CONV = V.VALUE
            L.VALUE = LEN(DICT.CORR)
            V.VALUE = DICT.CORR
            GOSUB 4000
            DICT.CORR = FV.VALUE
            IF NEW THEN
               LOCATE FILE.NAME IN NO.FILE,1 SETTING PTR ELSE PTR = 0
               IF PTR THEN
                  LOCATE ITEM.NAME IN NO.NAME<PTR>,1 SETTING SPTR ELSE SPTR = 0
                  IF SPTR THEN
                     IF ITEM.TYPE # NO.TYPE<PTR,SPTR> THEN
                        ERRMSG = "TYPE SHOULD BE ":NO.TYPE<PTR,SPTR>
                        GOSUB 91000; GOTO 10
                     END
                  END ELSE
                     NO.NAME<PTR,-1> = ITEM.NAME
                     NO.TYPE<PTR,-1> = ITEM.TYPE
                  END
               END ELSE
                  NO.FILE<-1> = FILE.NAME
                  NO.NAME<-1> = ITEM.NAME
                  NO.TYPE<-1> = ITEM.TYPE
               END
            END
            MATWRITE DICT.REC ON GLS.DICT, ITEM.TYPE:FILE.NAME:"!":ITEM.NAME
            GOTO 1
      END CASE
      GOTO 10
*
**** ENTER D.CODE
*
100*
      ECD.NUM = 3
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         DICT.D.CODE = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER A.AMC
*
200*
      ECD.NUM = 4
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         DICT.A.AMC = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER FIRST HEADING LINE
*
300*
      ECD.NUM = 5
      ECD.DEFAULT = ITEM.NAME
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 399
      IF ECD.RET.VALUE = '' THEN ECD.RET.VALUE = ' '
      S.NAME<1,1> = ECD.RET.VALUE
*
**** ENTER SECOND HEADING LINE
*
      ECD.NUM = 6
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 399
      S.NAME<1,2> = ECD.RET.VALUE
*
**** ENTER THIRD HEADING LINE
*
      ECD.NUM = 7
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 399
      S.NAME<1,3> = ECD.RET.VALUE
399*
      DICT.S.NAME = S.NAME<1,1>:S.NAME<1,2>:S.NAME<1,3>
      ECD.NUM=5;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.S.NAME[1,59];ECD.ACTION=5;CALL SCREEN.EDIT
      ECD.NUM=6;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.S.NAME[60,59];ECD.ACTION=5;CALL SCREEN.EDIT
      ECD.NUM=7;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.S.NAME[120,59];ECD.ACTION=5;CALL SCREEN.EDIT
      RETURN
*
**** ENTER FIRST CONV LINE
*
400*
      ECD.NUM = 8
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 499
      IF ECD.RET.VALUE = "???" THEN
         GOSUB 5000;GOTO 400
      END
      S.CONV<1,1> = ECD.RET.VALUE
*
**** SECOND CONV LINE
*
      ECD.NUM = 9
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 499
      IF ECD.RET.VALUE = "???" THEN
         GOSUB 5000;GOTO 400
      END
      S.CONV<1,2> = ECD.RET.VALUE
*
**** ENTER THIRD CONV LINE
*
      ECD.NUM = 10
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 499
      IF ECD.RET.VALUE = "???" THEN
         GOSUB 5000;GOTO 400
      END
      S.CONV<1,3> = ECD.RET.VALUE
499*
      DICT.CONV = S.CONV<1,1>:S.CONV<1,2>:S.CONV<1,3>
      ECD.NUM=8;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.CONV[1,59];ECD.ACTION=5;CALL SCREEN.EDIT
      ECD.NUM=9;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.CONV[60,59];ECD.ACTION=5;CALL SCREEN.EDIT
      ECD.NUM=10;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.CONV[120,59];ECD.ACTION=5;CALL SCREEN.EDIT
      RETURN
*
**** ENTER FIRST CORR LINE
*
500*
      ECD.NUM = 11
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 599
      IF ECD.RET.VALUE = "???" THEN
         GOSUB 5000;GOTO 500
      END
      S.CORR<1,1> = ECD.RET.VALUE
*
**** ENTER SECOND CORR LINE
*
      ECD.NUM = 12
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 599
      IF ECD.RET.VALUE = "???" THEN
         GOSUB 5000;GOTO 500
      END
      S.CORR<1,2> = ECD.RET.VALUE
*
**** ENTER THIRD CORR LINE
*
      ECD.NUM = 13
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE = 'END' THEN GOTO 599
      IF ECD.RET.VALUE = "???" THEN
         GOSUB 5000;GOTO 500
      END
      S.CORR<1,3> = ECD.RET.VALUE
599*
      DICT.CORR = S.CORR<1,1>:S.CORR<1,2>:S.CORR<1,3>
      ECD.NUM=11;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.CORR[1,59];ECD.ACTION=5;CALL SCREEN.EDIT
      ECD.NUM=12;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.CORR[60,59];ECD.ACTION=5;CALL SCREEN.EDIT
      ECD.NUM=13;ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,1> = DICT.CORR[120,59];ECD.ACTION=5;CALL SCREEN.EDIT
      RETURN
*
**** ENTER JUSTIFY
*
600*
      ECD.NUM = 14
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE # "END" THEN
         DICT.JUSTIFY = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER MAXL LENGTH
*
700*
      ECD.NUM = 15
      SAVE.S.NAME = DICT.S.NAME<1,1>:DICT.S.NAME<1,2>:DICT.S.NAME<1,3>
      SAVE.S.NAME = FIELD(SAVE.S.NAME,"}",1)
      ECD.DEFAULT = LEN(SAVE.S.NAME)
      ECD.ACTION=4;CALL SCREEN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         DICT.MAX.LEN = ECD.RET.VALUE
      END
      RETURN
*
**** GET ALL VALUES FROM FILE
*
1000*
      L.VALUE = LEN(DICT.S.NAME)
      V.VALUE = DICT.S.NAME
      GOSUB 3000
      DICT.S.NAME = FV.VALUE
      L.VALUE = LEN(DICT.CONV)
      V.VALUE = DICT.CONV
      GOSUB 3000
      DICT.CONV = FV.VALUE
      L.VALUE = LEN(DICT.CORR)
      V.VALUE = DICT.CORR
      GOSUB 3000
      DICT.CORR = FV.VALUE
      ECD.RET.VALUES<ECD.SCRN.NO,3,1> = DICT.D.CODE
      ECD.RET.VALUES<ECD.SCRN.NO,4,1> = DICT.A.AMC
      ECD.RET.VALUES<ECD.SCRN.NO,5,1> = DICT.S.NAME[1,59]
      ECD.RET.VALUES<ECD.SCRN.NO,6,1> = DICT.S.NAME[60,59]
      ECD.RET.VALUES<ECD.SCRN.NO,7,1> = DICT.S.NAME[120,59]
      ECD.RET.VALUES<ECD.SCRN.NO,8,1> = DICT.CONV[1,59]
      ECD.RET.VALUES<ECD.SCRN.NO,9,1> = DICT.CONV[60,59]
      ECD.RET.VALUES<ECD.SCRN.NO,10,1> = DICT.CONV[120,59]
      ECD.RET.VALUES<ECD.SCRN.NO,11,1> = DICT.CORR[1,59]
      ECD.RET.VALUES<ECD.SCRN.NO,12,1> = DICT.CORR[60,59]
      ECD.RET.VALUES<ECD.SCRN.NO,13,1> = DICT.CORR[120,59]
      ECD.RET.VALUES<ECD.SCRN.NO,14,1> = DICT.JUSTIFY
      ECD.RET.VALUES<ECD.SCRN.NO,15,1> = DICT.MAX.LEN
      ECD.ACTION = 3;CALL SCREEN.EDIT
      RETURN
*
**** CHANGE ALL VALUE MARK TO ]
*
3000*
      FV.VALUE = ''
      FOR LL = 1 TO L.VALUE
         IF V.VALUE[LL,1] = VM THEN
            FV.VALUE = FV.VALUE:"}"
         END ELSE
            FV.VALUE = FV.VALUE:V.VALUE[LL,1]
         END
      NEXT LL
      RETURN
*
**** CHANGE ALL ] TO VALUE MARK
*
4000*
      FV.VALUE = ''
      FOR LL = 1 TO L.VALUE
         IF V.VALUE[LL,1] = "}" THEN
            FV.VALUE = FV.VALUE:VM
         END ELSE
            FV.VALUE = FV.VALUE:V.VALUE[LL,1]
         END
      NEXT LL
      RETURN
*
**** PRINT PRIMARY XREF SCREEN
*
5000*
      ECD.SCRN.NO = 2 ; ECD.ACTION = 2 ; CALL SCREEN.EDIT
      GOSUB 50000
      ECD.SCRN.NO = 1 ; ECD.ACTION = 2 ; CALL SCREEN.EDIT
      ECD.ACTION = 3 ; CALL SCREEN.EDIT
      RETURN
*
**** PRINT SECONDARY XREF SCREENS
*
6000*
      ECD.ACTION = 2 ; CALL SCREEN.EDIT
      ERRMSG = "PRESS <RETURN> TO CONTINUE :" ; GOSUB 91000
      ECD.SCRN.NO = 2 ; ECD.ACTION = 2 ; CALL SCREEN.EDIT
      RETURN
*
*--- TYPE XREF
*
7000*
      ECD.SCRN.NO = 19; ECD.ACTION = 2; CALL SCREEN.EDIT
7001*
      IF ECD.SCRN.NO = 20 THEN PRINT @(0,23):CL:"* * * LAST SCREEN * * *":
      ECD.NUM = 1; ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM> = ""
      ECD.ACTION = 4; CALL SCREEN.EDIT
      REQUEST = ECD.RET.VALUE
      BEGIN CASE
        CASE REQUEST = "END" OR REQUEST = "N"
          GOTO 7009
        CASE REQUEST = "F"
          IF ECD.SCRN.NO = 19 THEN
            ECD.SCRN.NO = 20; ECD.ACTION = 2; CALL SCREEN.EDIT
          END ELSE
            ERRMSG = "THIS IS THE LAST SCREEN"; GOSUB 91000
          END
        CASE REQUEST = "B"
          IF ECD.SCRN.NO = 20 THEN
            ECD.SCRN.NO = 19; ECD.ACTION = 2; CALL SCREEN.EDIT
          END ELSE
            ERRMSG = "THIS IS THE FIRST SCREEN"; GOSUB 91000
          END
        CASE 1
          GOTO 7009
      END CASE
      GOTO 7001
7009*
      ECD.SCRN.NO = 1; ECD.ACTION = 3; CALL SCREEN.EDIT
      RETURN
*
**** PRINT CODES DEFINITION
*
50000*
      HELP = 0
      LOOP
      ECD.NUM = 1
      ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM> = ''
      ECD.ACTION=4;CALL SCREEN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = 'END' OR OPTION = ''
            HELP = 1
         CASE OPTION = 'A'
            ECD.SCRN.NO = 3; GOSUB 6000
         CASE OPTION = 'C'
            ECD.SCRN.NO = 4; GOSUB 6000
         CASE OPTION = 'D'
            ECD.SCRN.NO = 5; GOSUB 6000
         CASE OPTION = 'D1'
            ECD.SCRN.NO = 6; GOSUB 6000
         CASE OPTION = 'D2'
            ECD.SCRN.NO = 7; GOSUB 6000
         CASE OPTION = 'F'
            ECD.SCRN.NO = 8; GOSUB 6000
         CASE OPTION = 'G'
            ECD.SCRN.NO = 9; GOSUB 6000
         CASE OPTION = 'MD'
            ECD.SCRN.NO = 10; GOSUB 6000
         CASE OPTION = 'MF'
            ECD.SCRN.NO = 11; GOSUB 6000
         CASE OPTION = 'MP'
            ECD.SCRN.NO = 12; GOSUB 6000
         CASE OPTION = 'MT'
            ECD.SCRN.NO = 13; GOSUB 6000
         CASE OPTION = 'MX'
            ECD.SCRN.NO = 14; GOSUB 6000
         CASE OPTION = 'T'
            ECD.SCRN.NO = 15; GOSUB 6000
         CASE OPTION = 'TFILE'
            ECD.SCRN.NO = 16; GOSUB 6000
         CASE OPTION = 'U'
            ECD.SCRN.NO = 17; GOSUB 6000
         CASE OPTION = 'V'
            ECD.SCRN.NO = 18; GOSUB 6000
      END CASE
      WHILE HELP = 0 DO REPEAT
      RETURN
*
**** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999 PRINT @(-1):
   END
