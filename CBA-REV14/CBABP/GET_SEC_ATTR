SUBROUTINE GET_SEC_ATTR(SER,ATTR_NUM,SEC_ATTR)
*********************************************************************
*
* REVISION    - [14.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
*   SUBROUTINE    - GET_SEC_ATTR
*
*   AUTHOR     - Suhail Hussain S, Vercom Software, Inc
*
*   DATE       - 01/04/2007
*
*   DESCRIPTION
*
*   TT D219 - Generate 100 unique numbers from the passed serial number and
*   return the required attribute in SEC_ATTR.
*
*
*********************************************************************
*
*---- Initialization
*
   LSER=LEN(SER)
   CS1=0;CS2=0
   TS1=1;TS2=1
   MAXNUM = 9999
   SEC_STR = ""
*
*---- Main Processing
*
   FOR N=1 TO LSER
      IF MOD(N,2) THEN
         CS1=CS1+SEQ(SER[N,1])
      END ELSE
         CS2=CS2+SEQ(SER[N,1])
      END
   NEXT N
*
   FOR N=1 TO LSER
      IF MOD(N,2) THEN
         TS1=TS1*(CS2+SEQ(SER[N,1]))
      END ELSE
         TS2=TS2*(CS1+SEQ(SER[N,1]))
      END
   NEXT N
   SID=TS2:TS1
   SER = SID
   LSER = LEN(SER)
*
   CS1=0;CS2=0;RES = ""
   FOR E = 1 TO 100
      FOR N = 1 TO LSER
         IF MOD(N,2) THEN
            CS1=CS1+SEQ(SER[N,1])
         END ELSE
            CS2=CS2+SEQ(SER[N,1])
         END
      NEXT N
      S = CS1:CS2
      RES<1,E> = (S + E)
   NEXT E
*
   FOR E = 1 TO 100
      NXT_NUM = RES<1,E> 
      LOOP
         SUB_SUM = 0
         FOR J = 1 TO LEN(NXT_NUM)
            SUB_SUM += NXT_NUM[J,1]
         NEXT J
         NXT_NUM = SUB_SUM
      WHILE LEN(NXT_NUM) > 2 DO REPEAT

      NXT_STR = MAXNUM - NXT_NUM
      SEC_STR<E+1> = NXT_STR "R%4"
      MAXNUM = NXT_STR
   NEXT E
   SEC_STR<1> = SID
   SEC_ATTR = SEC_STR<ATTR_NUM>
*
*---- End of Program
*
9999*
   RETURN
