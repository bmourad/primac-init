      SUBROUTINE LS.BP.SUB(LINE,ERRMSG,PGM.MAP,ADD.LINE)
*
      DIM BP.FL(18)
      BP.FL(1) = "CBABP"
      BP.FL(2) = "UTLBP"
      BP.FL(3) = "PMCBP"
      BP.FL(4) = "APSBP"
      BP.FL(5) = "ARSBP"
      BP.FL(6) = "FASBP"
      BP.FL(7) = "GLSBP"
      BP.FL(8) = "ICSBP"
      BP.FL(9) = "JCSBP"
      BP.FL(10) = "JESBP"
      BP.FL(11) = "MLSBP"
      BP.FL(12) = "POSBP"
      BP.FL(13) = "PRSBP"
      BP.FL(14) = "PSSBP"
      BP.FL(15) = "PSSBP"
      BP.FL(16) = "TCA.BP"
      BP.FL(17) = "EPSBP"
      BP.FL(18) = "EP4BP"
      ERRMSG = ""
      SPC = SPACE(1)
      S.C = ";"
      MLEN = LEN(LINE)
      INDX = "CALL "
      LOC = 1
      PRINT
      PRINT LINE
      LOOP
         POS = INDEX(LINE,INDX,LOC)
      WHILE POS DO
         LIMIT = POS - 1
         MIN = 1; VALID = 1
         FOR P = LIMIT TO MIN STEP -1
            CHR = LINE[P,1]
            BEGIN CASE
            CASE CHR = S.C
               MIN = POS
            CASE CHR # SPC
               VALID = 0; MIN = POS
            END CASE
         NEXT P
         IF VALID THEN GOSUB 2100
         LOC = LOC + 1
      REPEAT
      GOTO 99999
*
*---- CALL statement conversion change from long program name to short name
*
2100*
      POS = POS + 5
      GOSUB 3000;*Find next char
      IF NOT(XPOS) THEN GOTO 2199
      POS1 = XPOS
      POS = XPOS + 1
      GOSUB 3100;*Find the name
      IF NOT(XPOS) THEN GOTO 2199
      OLD.SUB = LINE[POS1,XPOS-POS1+1]
      BEGIN CASE
         CASE OLD.SUB[1,1] = "@"
            ADD.LINE = "CALL LSC.BP.SUB(":OLD.SUB[2,LEN(OLD.SUB)]:")"
         CASE LEN(OLD.SUB) < 13 
            GOTO 2199
         CASE 1
            LIMIT = 18
            FOR I = 1 TO LIMIT
               READ ITEM FROM PGM.MAP, BP.FL(I):"/":OLD.SUB THEN
                  LIMIT = 0
                  NEW.SUB = ITEM<1>
               END
            NEXT I
            IF LIMIT THEN
               ERRMSG = "Could not locate short name for - ":OLD.SUB
               GOTO 2199
            END ELSE
               LINE = LINE[1,POS1-1]:NEW.SUB:LINE[XPOS+1,999]
            END
            ERRMSG = "CNV"
      END CASE
2199*
      RETURN
*
*---- Find the next non space character
3000*
      LIMIT = MLEN; XPOS = 0; EOL = 1
      FOR I = POS TO LIMIT
         CHR = LINE[I,1]
         BEGIN CASE
         CASE CHR = S.C
            LIMIT = 0
         CASE CHR # SPC
            XPOS = I; EOL = 0; LIMIT = 0
         END CASE
      NEXT I
      RETURN
*
*---- Find a specified char
3100*
      LIMIT = MLEN; XPOS = 0; EOL = 1
      FOR I = POS TO LIMIT
         CHR = LINE[I,1]
         BEGIN CASE
         CASE CHR = SPC
            XPOS = I - 1; EOL = 0; LIMIT = 0
         CASE CHR = "("
            XPOS = I - 1; EOL = 0; LIMIT = 0
         CASE CHR = S.C
            XPOS = I - 1; LIMIT = 0
         END CASE
      NEXT I
      IF NOT(XPOS) AND POS < LIMIT THEN XPOS = LIMIT
      RETURN
99999*
   RETURN
   END
