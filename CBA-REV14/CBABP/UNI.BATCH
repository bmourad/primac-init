*COPY>CPYLIB>COM1
*COPY>CPYLIB>COM.DICT.VAL
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SOURCE      - CBABP
* PROGRAM     - UNI.BATCH
* BY          - Julianne Rivera, Vercom Software, Inc.
* DATE        _ 09/11/90
* DESCRIPTION
*************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>DICT.VALUES
*COPY>CPYLIB>UNI.DICT
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>DICT.HLD.FILE
   MAT SFR = ''
   MAT POS = ''
   MAT SF1 = ''
   MAT SF2 = ''
   MAT SF3 = ''
   MAT DICT.REC = ''
   COMPANY.POINTER = ''
   NEW.ITEM = ''
*
*---- INITIAL
*
   ERRMSG = ''
   DASHES = STR('-',80)
   PROMPT ""
   OPEN.FLAG = 0
*
*---- OPEN FILES
*
   OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'CONTROL FILE IS MISSING'
      GOSUB 91000
      GOTO 999999
   END
   OPEN '','VOC' TO VOC ELSE
      ERRMSG = 'VOC FILE IS MISSING'
      GOSUB 91000
      GOTO 999999
   END
   OPEN '','HOLD.FILE' TO HOLD.FILE ELSE
      ERRMSG = 'HOLD.FILE IS MISSING'
      GOSUB 91000
      GOTO 999999
   END
   PERFORM 'WHERE' CAPTURING CURR.ACCT
   L=LEN(CURR.ACCT)
   CURR.ACCT=CURR.ACCT[1,L-1]
10*
*
*---- PRINT SCREEN
*
   SCRN = CS
   SCRN = SCRN : @(0,0):'VERCOM'
   SCRN = SCRN : @(28,0):'DICTIONARIES ADJUSTMENT'
   SCRN = SCRN : @(0,1):DASHES
   SCRN = SCRN : @(25,5):'CURRENT ACCOUNT : ' : CURR.ACCT
   SCRN = SCRN : @(25,6):'FULL PATH : ' 
   SCRN = SCRN : @(25,7):'ADJUST ACCOUNT : '
   SCRN = SCRN : @(25,11):'NOW PROCESSING'
   SCRN = SCRN : @(25,13):'FILE :'
   SCRN = SCRN : @(25,15):'DICT :'
   SCRN = SCRN : @(0,22):DASHES
   PRINT SCRN :
   PRINT @(36,6) : CL :
   INPUT NEW.PATH :
   IF NEW.PATH = 'END' THEN GOTO 999999
   IF NEW.PATH = '' THEN GOTO 999999
   PRINT @(36,6) : NEW.PATH
   PRINT @(43,7) : CL :
   INPUT NEW.ACCT :
   IF NEW.ACCT = 'END' THEN GOTO 999999
   IF NEW.ACCT = '' THEN NEW.ACCT = CURR.ACCT
   PRINT @(43,7) : NEW.ACCT
   COMPANY.POINTER<1> = 'F'
   COMPANY.POINTER<2> = NEW.PATH:"/":NEW.ACCT:"/COMPANY"
   COMPANY.POINTER<3> = NEW.PATH:"/":NEW.ACCT:"/D_COMPANY"
   WRITE COMPANY.POINTER ON VOC, 'COMPANY.POINTER'
   OPEN '','COMPANY.POINTER' TO COMPANY ELSE
      ERRMSG = 'COMPANY FILE IS MISSING'
      GOSUB 91000
      GOTO 999999
   END
   PRINT @(0,23):CL:'INPUT THE COMPANY NUMBER ':
   INPUT COMPANY.NO,3_:
   PRINT @(0,23):CL:
   IF COMPANY.NO = 'END' OR COMPANY.NO = '' THEN GOTO 999999
   PRINT @(25,3) : COMPANY.NO
   MATREAD COMP.REC FROM COMPANY, COMPANY.NO ELSE
      ERRMSG = 'COMPANY':COMPANY.NO:'IS NOT ON FILE'
      GOSUB 91000
      GOTO 999999
   END
   PRINT @(25,3):COMPANY.NO:'  ':CO.NAME
   PREV.FILE = ''
*
*---- CHECK COMPANY PARAMETER
*
   CONO.LEN = 3
   ST.CONO = CONO.LEN + 1
   DIV.LEN = 2
   DEP.LEN = 2
   CTR.LEN = 3
   CCTR.LEN = CONO.LEN + DIV.LEN + DEP.LEN + CTR.LEN
   ST.CCTR = CCTR.LEN + 1
   GL.LEN = 0
   STRUC.CNT = COUNT(CO.ACCT.STRUC,'-')
   SF.CNT = STRUC.CNT + 1
   FOR I = 1 TO SF.CNT
      SFR(I) = FIELD(CO.ACCT.STRUC,'-',I)
      IF NUM(SFR(I)) THEN
         IF SFR(I) > 0 THEN
            SFR(I) = SFR(I) + 0
            POS(I) = GL.LEN + 1
            GL.LEN = GL.LEN + SFR(I)
         END ELSE
            ERRMSG = 'SUFFIX #':I:' IS < 1'
            GOSUB 91000
            GOTO 999999
         END
      END ELSE
         ERRMSG = 'SUFFIX # ':I:' IS NOT NUMERIC'
         GOSUB 91000
         GOTO 999999
      END
   NEXT I
   IF GL.LEN <> LEN(CO.ACCT.PIC) THEN
      ERRMSG = 'GL.LEN <> LEN(CO.ACCT.PIC)'
      GOSUB 91000
      GOTO 999999
   END
   IF SF.CNT > 1 THEN
      SUFFIX.POS = POS(2)
      SUFFIX.LEN = GL.LEN - SFR(1)
   END ELSE
      SUFFIX.LEN = 0
      SUFFIX.POS = GL.LEN + 1
   END
   STRUC.LEN = STRUC.CNT + GL.LEN
   TOT.LEN = CCTR.LEN + GL.LEN
   EN.GL.LEN1 = ST.CONO + GL.LEN
   EN.GL.LEN2 = ST.CCTR + GL.LEN
*
*---- PROCESS ALL DICT
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MAT DICT.REC = ''
      MATREAD HOLD.REC FROM HOLD.FILE, ID ELSE GOTO 9999
      IF NOT(NUM(HOLD.TYPE)) THEN
         ERRMSG = HOLD.TYPE:' IS NOT A VALID TYPE'
         GOSUB 91000
         GOTO 9999
      END
      FILE.NAME = FIELD(ID,'!',1)
      IF FILE.NAME # PREV.FILE THEN
         OPEN.FLAG = 1
         PREV.FILE = FILE.NAME
         READ ITEM FROM VOC, FILE.NAME ELSE
            ERRMSG = 'FILE ':FILE.NAME:' IS MISSING'
            GOSUB 91000
            OPEN.FLAG = 0
            GOTO 199
         END
         LPOS = COUNT(ITEM<2>,'/')
         ADIR = FIELD(ITEM<2>,'/',LPOS)
         ACCT = ''
         ACCT = FIELD(ADIR,'-',2)
         IF ACCT = '' THEN
            UPD.ACCT = NEW.ACCT
         END ELSE
            UPD.ACCT = NEW.ACCT:"-":ACCT
         END
         NEW.ITEM<1> = 'F'
         NEW.ITEM<2> = NEW.PATH:"/":UPD.ACCT:"/":FILE.NAME
         NEW.ITEM<3> = NEW.PATH:"/":UPD.ACCT:"/":"D_":FILE.NAME
         WRITE NEW.ITEM ON VOC, 'UNI.BATCH.POINTER'
         OPEN 'DICT','UNI.BATCH.POINTER' TO D.F.NAME ELSE
            ERRMSG = FILE.NAME:' FILE IS MISSING'
            GOSUB 91000
            OPEN.FLAG = 0
            GOTO 199
         END
         PRINT @(32,13):FILE.NAME:CL:
199*
      END
      IF OPEN.FLAG = 0 THEN GOTO 9999
      DICT.NAME = FIELD(ID,'!',2)
      IF DICT.NAME = 'DL/ID' THEN GOTO 9999
      PRINT @(32,15):DICT.NAME:CL:
      CALL CREATE.DICT(FILE.NAME)
      MATWRITE DICT.REC ON D.F.NAME,DICT.NAME
9999*
   REPEAT
   DELETE VOC,'COMPANY.POINTER'
   DELETE VOC,'UNI.BATCH.POINTER'
   GOTO 999999
91000*
   PRINT @(0,23):ERRMSG:CL:
   INPUT REPLY,1:
   PRINT @(0,23):CL:
   RETURN
999999*
*  PRINT @(-1):
END
