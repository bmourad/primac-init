     SUBROUTINE GEN.XREF2 (CONO,GXR.FILE,GXR.SRCH.ID,GXR.XREF,GXR.ID,GXR.ACTION,GXR.TOP.LINE,PREFIX,PROG.ID)
****************************************************************
*SOURCE  - CBABP
*PROGRAM - GEN.XREF2
*AUTHOR  - GRAHAM JARVIS, PACRIM SOFTWARE PTY LTD
*DATE    - 07 MAR 1992
*DESC'N  - Provides cross reference details for files based
*          on details stored in GEN.PROG.XREF
*MODIFIED- 07 MAR 1992
*          Changed to re-read display details for each scroll
*          action to avoid item size error.
****************************************************************
*COPY>CPYLIB>PROG.XREF.DAT
*COPY CPYLIB>CHAR
      OPEN '','GEN.PROG.XREF' TO GEN.PROG.XREF ELSE PRINT "CANNOT OPEN GEN.PROG.XREF"; STOP
      MATREAD PROG.XREF.DAT FROM GEN.PROG.XREF,PROG.ID ELSE
         TOP = 8
         LEFT = 1
         RIGHT = 78
         BOTTOM = 21
      END
      GXR.CO=CONO
      PANEL.WIDTH=RIGHT-LEFT-1
      FILL = '#'
      REC.IDS = ''
      GXR.ACTION = ''
      IF XR.HEADING = '' OR GXR.SRCH.ID = '' THEN
         GXR.ACTION = 'X'
         GOTO 99999
      END ELSE
         DISP.CNT = COUNT(XR.HEADING,VM) + 1
      END
      SCRN.SIZE = BOTTOM-TOP-6 
      BEG.SCRN = TOP+4 
      END.SCRN = BEG.SCRN + SCRN.SIZE - 1 
      SCRN.LINES = ''
      ALL.NAMES = ''
      CALL GET.NAME(GXR.SRCH.ID,ALL.NAMES,PREFIX)
      NAME.CNT = DCOUNT(ALL.NAMES,VM)
      REC.IDS = ''
      IF NAME.CNT THEN
         READ ITEM FROM GXR.XREF, GXR.CO : ALL.NAMES<1,1> ELSE ITEM = ""
         REC.IDS = ITEM<XR.LOC>
      END
      FOR MLP = 2 TO NAME.CNT UNTIL REC.IDS = ''
         READ ITEM FROM GXR.XREF, GXR.CO : ALL.NAMES<1,MLP> ELSE ITEM = ''
         IF ITEM # '' THEN
            CNT = COUNT(REC.IDS<1>,VM) + (REC.IDS<1> # "")
            IF CNT THEN
               FOR SLP = CNT TO 1 STEP - 1
                  LOCATE REC.IDS<1,SLP> IN ITEM<XR.LOC>,1 SETTING FND ELSE
                     REC.IDS = DELETE(REC.IDS,1,SLP,0)
                  END
               NEXT SLP
*           END ELSE
*              REC.IDS = ''
            END
*        END ELSE
*           REC.IDS = ''
         END
      NEXT MLP
      IF REC.IDS = '' THEN
         GXR.ACTION = 'X'
         GOTO 99999
      END
      IF GXR.TOP.LINE="" THEN GXR.TOP.LINE=XREF.DSC
      GXR.TOP.LINE=GXR.TOP.LINE:BG:" (":PROG.ID:")":FG
      GXR.TOP.LINE=(SPACE((PANEL.WIDTH-LEN(GXR.TOP.LINE<1,1>))/2):GXR.TOP.LINE<1,1>)
      REC.CNT = COUNT(REC.IDS,VM) + 1
      HDR.LINE = 'SEL'
      DSH.LINE = '---'
      FOR DLP = 1 TO DISP.CNT
         FORMAT.MASK = "L#" : XR.LEN<1,DLP>
         HDR.LINE = HDR.LINE : ' ' : XR.HEADING<1,DLP>FORMAT.MASK
         DSH.LINE = DSH.LINE : ' ' : STR('-',XR.LEN<1,DLP>)
      NEXT DLP
      BOX.SUB="BOX.":TCC.REC<1>
      CALL @BOX.SUB(TCC.REC,TOP,LEFT,BOTTOM,RIGHT) 
      PRINT @(LEFT+1,TOP+1):GXR.TOP.LINE
      PRINT @(LEFT+1,TOP+2):BG:HDR.LINE:FG:
      PRINT @(LEFT+1,TOP+3):BG:DSH.LINE:FG:
      SCREN = 1
      TOTAL.SCRENS = INT((REC.CNT / SCRN.SIZE) + .99)
1000  *
      START.LINE = ((SCREN - 1) * SCRN.SIZE) + 1
      END.LINE = START.LINE + SCRN.SIZE - 1
      IF END.LINE > REC.CNT THEN END.LINE = REC.CNT
      FOR PRT.LINE = BEG.SCRN TO END.SCRN
         PRINT @(LEFT+2,PRT.LINE) : STR(" ",PANEL.WIDTH-1) :
      NEXT PRT.LINE
      PRT.LINE = BEG.SCRN
      FOR PLP = START.LINE TO END.LINE
            READ ITEM FROM GXR.FILE, GXR.CO : REC.IDS<1,PLP> ELSE
               XR.ITEM = ''
               FOR DLP = 1 TO DISP.CNT
                  ITEM<XR.ATT<1,DLP,1>,1> = STR('?',XR.LEN<1,DLP>)
               NEXT DLP
            END
            SCRN.LINES<1,PLP> = PLP "R#3"
            TEXT.LINE=''
            FOR DLP = 1 TO DISP.CNT
               ITEM.DESC=ITEM<XR.ATT<1,DLP,1>,1>
               FORMAT.MASK = "L#" : XR.LEN<1,DLP>
               IF XR.OCONV<1,DLP> # '' THEN
                  IF XR.OCONV<1,DLP>[1,2]="T;" THEN
                     QXR.FILE=FIELD(XR.OCONV<1,DLP>,';',2)
                     QXR.KEY =FIELD(XR.OCONV<1,DLP>,';',3)
                     QXR.ATT =FIELD(XR.OCONV<1,DLP>,';',4)
                     QXR.CNV =FIELD(XR.OCONV<1,DLP>,';',5)
                     OPEN '',QXR.FILE TO QXRFILE ELSE STOP
                     READV QKEY FROM XFILE,GXR.CO:ITEM.DESC,QXR.KEY ELSE QKEY=''
                     READV ITEM.DESC FROM QXRFILE,GXR.CO:QKEY,QXR.ATT ELSE DESC=''
                     IF QXR.CNV#"" THEN 
                        ITEM.DESC=OCONV(ITEM.DESC,QXR.CNV)
                     END
                  END ELSE
                        ITEM.DESC=OCONV(ITEM.DESC,XR.OCONV<1,DLP>)
                  END
               END
               IF XR.ATT<1,DLP> = 0 THEN
                  SCRN.LINES<1,PLP> = SCRN.LINES<1,PLP> : ' ' : REC.IDS<1,PLP>FORMAT.MASK
               END ELSE
                  TEXT.LINE = TEXT.LINE:' ':ITEM.DESC FORMAT.MASK
               END
            NEXT DLP
         PRINT @(LEFT+2,PRT.LINE) : SCRN.LINES<1,PLP>:TEXT.LINE:
         PRT.LINE=PRT.LINE+1
      NEXT PLP
2000  *
      IF SCREN = TOTAL.SCRENS THEN
         PRINT @(LEFT+2,BOTTOM-1) : '** LAST screen **' : STR(" ",PANEL.WIDTH-20) :
      END ELSE
         PRINT @(LEFT+2,BOTTOM-1) : STR(" ",PANEL.WIDTH-1) :
      END
      PAGE.CNT=TOTAL.SCRENS
      PAGE.NO=SCREN
      PRINT @(RIGHT-20,BOTTOM):' Page: ':PAGE.NO "R#3":' of ':PAGE.CNT "L#3"
      PRINT @(LEFT+1,BOTTOM-2):BG:'Enter #,(N),(S),(B),(P#) :':FG:'####':@(LEFT+29):     
      INPUT VALUE,4:
      IF VALUE = CHAR(27) OR VALUE = CHAR(251) THEN VALUE = "END"
      BEGIN CASE
      CASE VALUE = 'END' OR VALUE = 'N' OR VALUE="X" 
         GXR.ACTION = 'N'
         GOTO 99999
      CASE VALUE[1,1] = "P"
         SCREN = VALUE[2,3]
         IF NOT(NUM(SCREN)) THEN
            ERRMSG = '** Invalid Selection **'
            GOSUB 90000
            GOTO 2000
         END 
         IF SCREN > TOTAL.SCRENS THEN
            SCREN=TOTAL.SCRENS
         END
         IF SCREN < 1 THEN SCREN = 1
         GOTO 1000
      CASE VALUE = 'S' OR VALUE ="SF"
         IF SCREN = TOTAL.SCRENS THEN
            ERRMSG = '** This is the LAST screen **'
            GOSUB 90000
         END ELSE
            SCREN = SCREN + 1
            GOTO 1000
         END
      CASE VALUE = 'B'
         IF SCREN = 1 THEN
            ERRMSG = '** This is the FIRST screen **'
            GOSUB 90000
         END ELSE
            SCREN = SCREN - 1
            GOTO 1000
         END
      CASE NUM(VALUE)
         IF VALUE >= 1 AND VALUE <= REC.CNT THEN
            GXR.ID = REC.IDS<1,VALUE>
            GOTO 99999
         END ELSE
            ERRMSG = '** Invalid Selection **'
            GOSUB 90000
         END
      CASE 1
         ERRMSG = '** Invalid Selection **'
         GOSUB 90000
      END CASE
      GOTO 2000
      *
      **** PRINT ERRMSG
      *
90000 *
      PRINT @(LEFT+2,BOTTOM-1):ERRMSG:STR(" ",PANEL.WIDTH-LEN(ERRMSG)-1): 
      INPUT REPLY:
      PRINT @(LEFT+2,BOTTOM-1):STR(" ",PANEL.WIDTH-1):
      RETURN
99999 *
      RETURN
      END
