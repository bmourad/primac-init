***************************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM - MENU.DEMO
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 02/04/84
*
* DESCRIPTION
*
* This program display the menus specified in the CONTROL file item
* "MENU.TBL". The delay after each menu is displayed is requested at
* program execution time.
*
*ENDDOC
***************************************************************************
*
*---- DEFINE CONSTANTS
*
      ACCT.NAME = "C.B.A"
      SYSTEM.NAME = "P R I M A C"
      P = INT((80 - LEN(SYSTEM.NAME)) / 2)
      SYSTEM.NAME = @(P,0) : SYSTEM.NAME
      CS = CHAR(12)
      CL = "K"
      DASHES = STR("-",80)
*
*---- DEFINE DIMENSIONED VARIABLES
*
      DIM MENU.TBL(50)
      DIM MENU(40)
*
*---- OPEN ALL FILES
*
      OPEN "","CONTROL" TO CONTROL ELSE
         STOP "CANNOT OPEN CONTROL FILE"
      END
      OPEN "","PRIMAC.MENUS" TO PRIMAC.MENUS ELSE
         STOP "CANNOT OPEN PRIMAC.MENUS FILE"
      END
*
*---- INITIALIZATION
*
      ITEM.CNT = 0
      MENU.ID = ""
      MATREAD MENU.TBL FROM CONTROL, "MENU.TBL" ELSE
         STOP "CANNOT LOCATE MENU.TBL RECORD ON CONTROL FILE"
      END
      PRINT CHAR(12):
10*
      PRINT "DELAY IN SECONDS":
      INPUT DELAY
      IF NOT(NUM(DELAY)) THEN
         PRINT "INVALID INPUT"
         GOTO 10
      END
      DELAY2 = DELAY * 2
*
*---- MAIN PROCESSING
*
100*
      DONE = 0
      FOR N = 1 TO 50 UNTIL DONE
         MENU.ID = MENU.TBL(N)
         IF MENU.ID = "" THEN
            DONE = 1
            GOTO 190
         END
         MATREAD MENU FROM PRIMAC.MENUS, MENU.ID ELSE GOTO 190
         ITEM.CNT = 0
         FOR I = 2 TO 33
            IF MENU(I) = '' THEN
               I = 99
            END ELSE
               ITEM.CNT = ITEM.CNT + 1
            END
         NEXT I
         GOSUB 1000
         IF MENU.ID = "M.MASTER" THEN
*            DUMMY = ICONV(DELAY2,"U407A")
            RQM DELAY2
         END ELSE
*            DUMMY = ICONV(DELAY,"U407A")
            RQM DELAY
         END
190*
      NEXT N
      GOTO 100
*
*--- DISPLAY SELECTED MENU
*
1000*
      PRINT @(-1) :
      PRINT @(0,0) : CL : ACCT.NAME :
      PRINT SYSTEM.NAME :
      PRINT @(62,0) : OCONV(DATE(),"D2/") :
      PRINT @(72,0) : OCONV(TIME(),"MTS") :
      PRINT @(0,1) :DASHES :
      P = INT((80 - LEN(MENU(1)<1,3>)) / 2)
      PRINT @(P,3) : MENU(1)<1,3> :
      XREF = ""
      SEL.CNT = 0
      BEGIN CASE
      CASE ITEM.CNT > 16
      I2 = INT((ITEM.CNT + 1) / 2)
      Y = 5 + INT((16 - I2) / 2)
      FOR I = 1 TO I2
         IF MENU(I+1)<1,1> = "C" THEN
            PRINT @(0,Y) : MENU(I+1)<1,3>[1,33]
         END ELSE
            SEL.CNT = SEL.CNT + 1
            XREF<SEL.CNT> = I + 1
            PRINT @(0,Y) : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3>[1,33]
         END
         Y = Y + 1
      NEXT I
      Y = 5 + INT((16 - I2) / 2)
      FOR I = I2 + 1 TO ITEM.CNT
         IF MENU(I+1)<1,1> = "C" THEN
            PRINT @(40,Y) : MENU(I+1)<1,3>[1,33]
         END ELSE
            SEL.CNT = SEL.CNT + 1
            XREF<SEL.CNT> = I + 1
            PRINT @(40,Y) : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3>[1,33]
         END
         Y = Y + 1
      NEXT I
      CASE ITEM.CNT > 8
      Y = 5 + INT((16 - ITEM.CNT) / 2)
      FOR I = 1 TO ITEM.CNT
         IF MENU(I+1)<1,1> = "C" THEN
            PRINT @(25,Y) : MENU(I+1)<1,3>[1,40]
         END ELSE
            SEL.CNT = SEL.CNT + 1
            XREF<SEL.CNT> = I + 1
            PRINT @(25,Y) : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3>[1,40]
         END
         Y = Y + 1
      NEXT I
      CASE 1
      Y = 5 + INT((16 - 2 * ITEM.CNT) / 2)
      FOR I = 1 TO ITEM.CNT
         IF MENU(I+1)<1,1> = "C" THEN
            PRINT @(25,Y) : MENU(I+1)<1,3>[1,40]
         END ELSE
            SEL.CNT = SEL.CNT + 1
            XREF<SEL.CNT> = I + 1
            PRINT @(25,Y) : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3>[1,40]
         END
         Y = Y + 2
      NEXT I
      END CASE
      PRINT @(0,22) : DASHES :
      RETURN
   END
