*COPY>CPYLIB>COM1
***************************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM - MENU.MAINT
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/05/84
*
* DESCRIPTION
*
* This program provides for the creation and maintenance of a menu.
*
*T21177 diane 11/06/1996 * REV11 UPG
*T21177 diane 12/03/1996 * Update MENUFORM with modifications.
*T21897 diane 05/08/1997 * Correct unit var at line 297
*T22426 stefanie 02/03/1998 * Allow reports to print to the screen.
*T23903 diane 04/26/1999 * FIX scrolling
*T26090 ajibaly 04/12/2002 * Remove report print option (T22426)
*                            now moved to REPORT.SCRN
***************************************************************************
*COPY>PMC.CPYLIB>PMC_PROCESS
*COPY>PMC.CPYLIB>PMC_PROCESS_XREF
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- DEFINE VARIABLES
*
   DIM MENU(33)
   DIM OLD.MENU(33)
*
*---- OPEN ALL FILES
*
   OPEN "","M.SCREENS" TO M.SCREENS ELSE
      ERRMSG = "CANNOT OPEN M.SCREENS FILE"
      GOTO 93000
   END
   PROCESS.ONFILE = 1
   OPEN "","PMC_PROCESS_XREF" TO PMC_PROCESS_XREF ELSE
      PROCESS.ONFILE = 0
      ERRMSG = "CANNOT OPEN PMC_PROCESS_XREF FILE"
      GOTO 93000
   END
   OPEN "","PMC_PROCESS" TO PMC_PROCESS ELSE
      PROCESS.ONFILE = 0
      ERRMSG = "CANNOT OPEN PMC_PROCESS FILE"
      GOTO 93000
   END
*
*---- INITIALIZATION
*
   MAT EDIT.COM.DRIVER = ""
   ECD.SCRN.NAME = "MENU.MAINT"
   ECD.ACTION=1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   ECD.ACTION=2; CALL SCRN.EDIT
   MAT SCV.REC = ""
   SCV.REC(10)<1> = OCONV(DATE(),"D2/")
   ECD.NUM = 10
   ECD.ACTION=5; CALL SCRN.EDIT
   MENU.FILE = "PRIMAC.MENUS"
   PRIOR.FILE = ""
   LINE.COUNT = 8
*
*---- MAIN PROCESSING
*
100*
   MAT SCV.REC = ""
   ECD.NUM = 1; ECD.DEFAULT = MENU.FILE
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN
      PRINT @(-1):
      STOP
   END
   OPEN "",ECD.RET.VALUE TO MFILE ELSE
      ERRMSG = "INVALID FILE NAME"
      GOSUB 90000
      GOTO 100
   END
   MENU.FILE = ECD.RET.VALUE
*
*---- GET MENU NAME
*
200*
   ECD.NUM = 2
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 100
   MENU.NAME = ECD.RET.VALUE
   NEW.MENU = 0
   MATREAD MENU FROM MFILE, MENU.NAME THEN
      MAT OLD.MENU = MAT MENU
      OLD.REF.CNT = 0
      CURR.REF.NO = 0 ;* T23903 4/26/99
      REF.NO = 0 ;* T23903 4/26/99
      FOR N = 33 TO 2 STEP -1 UNTIL OLD.REF.CNT > 0
         IF MENU(N)<1,1> # "" OR MENU(N)<1,3> # "" THEN
            OLD.REF.CNT = N - 1
         END
      NEXT N
   END ELSE
      MAT OLD.MENU = ''
      MAT MENU = ""
      NEW.MENU = 1
      OLD.REF.CNT = 0
   END
   BEGIN CASE
      CASE NEW.MENU
         GOSUB 9000
         CURR.REF.NO = ""
         FOR N = 1 TO 2
            ON N GOSUB 3000,4000
            IF ECD.RET.VALUE = "END" THEN
               ECD.ACTION=6; CALL SCRN.EDIT
               GOTO 100
            END
         NEXT N
         DONE = 0
         REF.CNT = 0
         FOR REF.NO = 1 TO 32 UNTIL DONE
            GOSUB 10000
            GOSUB 6000
            IF ECD.RET.VALUE = "END" THEN
               REF.CNT = REF.NO - 1
               DONE = 1
            END ELSE
               REF.CNT = REF.NO
            END
         NEXT N
         REF.NO = REF.CNT
         GOSUB 10000
      CASE 1
         GOSUB 9000
         ECD.ACTION=3; CALL SCRN.EDIT
*T23903         CURR.REF.NO = 1
         CURR.REF.NO = 0 ;* T23903 4/26/99
         GOSUB 10000  ;*T23903 4/26/99
   END CASE
*
*---- GET OPERATION REQUEST
*
500*
   ECD.NUM = 9
   ECD.ACTION=4; CALL SCRN.EDIT
   ACTION = ECD.RET.VALUE
   BEGIN CASE
      CASE ACTION = "E" OR ACTION = "END"
         ECD.ACTION=6; CALL SCRN.EDIT
         GOTO 100
      CASE ACTION = "F"
         MAT MENU = ""
         MENU(1)<1,1> = SCV.REC(3)<1>
         MENU(1)<1,3> = SCV.REC(4)<1>
         FOR N = 1 TO REF.CNT
            MENU(N+1)<1,3> = SCV.REC(6)<1,N>
            MENU(N+1)<1,1> = SCV.REC(7)<1,N>
            MENU(N+1)<1,2> = SCV.REC(8)<1,N>
         NEXT N
         MATWRITE MENU ON MFILE, MENU.NAME
         IF NEW.MENU THEN
            GOSUB 11000
            GOSUB 13000
         END ELSE
            GOSUB 12000
            GOSUB 11000
            GOSUB 13000
         END
         ECD.ACTION=6; CALL SCRN.EDIT
         GOTO 100
      CASE ACTION = "S"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > REF.CNT THEN REF.NO = 1
         GOSUB 10000
      CASE ACTION = "A"
         DONE = 0
         FOR REF.NO = REF.CNT + 1 TO 32 UNTIL DONE
            GOSUB 10000
            GOSUB 6000
            IF ECD.RET.VALUE = "END" THEN
               REF.CNT = REF.NO - 1
               DONE = 1
            END ELSE
               REF.CNT = REF.NO
            END
         NEXT REF.NO
         REF.NO = REF.CNT
         GOSUB 10000
      CASE ACTION = "C"
         ECD.NUM = 11
         ECD.MINV = 1; ECD.MAXV = REF.CNT
         ECD.ACTION=4; CALL SCRN.EDIT
         IF NUM(ECD.RET.VALUE) THEN
            REF.NO = ECD.RET.VALUE
            GOSUB 10000
            GOSUB 6000
         END
      CASE ACTION = "D"
         ECD.NUM = 11
         ECD.MINV = 1; ECD.MAXV = REF.CNT
         ECD.ACTION = 4; CALL SCRN.EDIT
         IF NUM(ECD.RET.VALUE) THEN
            REF.NO = ECD.RET.VALUE
            SCV.REC(5) = DELETE(SCV.REC(5),1,REF.NO,0)
            SCV.REC(6) = DELETE(SCV.REC(6),1,REF.NO,0)
            SCV.REC(7) = DELETE(SCV.REC(7),1,REF.NO,0)
            SCV.REC(8) = DELETE(SCV.REC(8),1,REF.NO,0)
            REF.CNT = REF.CNT - 1
            CURR.REF.NO = ""
            GOSUB 10000
         END
      CASE ACTION = "I" AND REF.CNT = 32
         ERRMSG = "An entry must be deleted before any more may be added"
         GOSUB 90000
      CASE ACTION = "I"
         ECD.NUM = 11
         ECD.MINV = 1; ECD.MAXV = REF.CNT
         ECD.ACTION=4; CALL SCRN.EDIT
         IF NUM(ECD.RET.VALUE) THEN
            REF.NO = ECD.RET.VALUE
            SCV.REC(5) = INSERT(SCV.REC(5),1,REF.NO,0,"")
            SCV.REC(6) = INSERT(SCV.REC(6),1,REF.NO,0,"")
            SCV.REC(7) = INSERT(SCV.REC(7),1,REF.NO,0,"")
            SCV.REC(8) = INSERT(SCV.REC(8),1,REF.NO,0,"")
            REF.CNT = REF.CNT + 1
            CURR.REF.NO = ""
            GOSUB 10000
            GOSUB 6000
         END
      CASE NUM(ACTION)
         BEGIN CASE
            CASE ECD.RET.VALUE = 1
               GOSUB 3000
            CASE ECD.RET.VALUE = 2
               GOSUB 4000
         END CASE
   END CASE
   GOTO 500
*
*---- GET PRIOR MENU ID
*
3000*
   ECD.NUM = 3
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      MENU(1)<1,1> = ECD.RET.VALUE
   END
   RETURN
*
*---- GET MENU NAME
*
4000*
   ECD.NUM = 4
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      MENU(1)<1,3> = ECD.RET.VALUE
   END
   RETURN
*
*---- DISPLAY REFERENCE NUMBER
*
5000*
   ECD.NUM = 5
   ECD.SUB.NUM = REF.NO
   ECD.ACTION=5; CALL SCRN.EDIT
   RETURN
*
*---- GET SELECTION NAME
*
6000*
   ECD.NUM = 6
   ECD.SUB.NUM = REF.NO
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN RETURN
*
*---- GET MENU.PROC NAME
*
7000*
   ECD.NUM = 7
   ECD.SUB.NUM = REF.NO
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 6000
   BEGIN CASE
      CASE ECD.RET.VALUE = "C"
      CASE ECD.RET.VALUE[1,2] = "M."
      CASE ECD.RET.VALUE[1,3] = "*M."
      CASE 1
         IF PROCESS.ONFILE THEN
            PMCID = ECD.RET.VALUE     ;* T21897
            IF ECD.RET.VALUE[1,1] = "*" THEN PMCID = ECD.RET.VALUE[2,99]
            READ DUMMY FROM PMC_PROCESS_XREF, PMCID ELSE
               ERRMSG = "WARNING ! PROCESS ":PMCID:" NOT ON FILE"
               GOSUB 91000
*               GOTO 7000
            END
         END
   END CASE
*
*---- GET DOCUMENTATION NAME
*
8000*
   IF ECD.RET.VALUE = "C" THEN
      ECD.RET.VALUE = ""
      SCV.REC(8)<1,REF.NO> = ""
      ECD.NUM = 8
      ECD.SUB.NUM = REF.NO
      ECD.ACTION=5; CALL SCRN.EDIT
      GOTO 8090
   END
   ECD.NUM = 8
   ECD.SUB.NUM = REF.NO
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 7000
8090*
   RETURN
*
*---- SET SCREEN VALUES
*
9000*
   SCV.REC(3)<1> = MENU(1)<1,1>
   SCV.REC(4)<1> = MENU(1)<1,3>
   REF.CNT = 0
   FOR N = 33 TO 2 STEP -1 UNTIL REF.CNT > 0
      IF MENU(N)<1,1> # "" OR MENU(N)<1,3> # "" THEN
         REF.CNT = N - 1
      END
   NEXT N
   FOR N = 1 TO REF.CNT
      SCV.REC(5)<1,N> = N ;*T23903 4/26/99
      SCV.REC(6)<1,N> = MENU(N+1)<1,3>
      SCV.REC(7)<1,N> = MENU(N+1)<1,1>
      SCV.REC(8)<1,N> = MENU(N+1)<1,2>
   NEXT N
   RETURN
*
*---- DISPLAY MULTI-LINE FIELDS
*
10000*
   START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
   IF START.REF.NO = CURR.REF.NO THEN RETURN
   CURR.REF.NO = START.REF.NO
   ECD.NUM = 5
   ECD.SUB.NUM = START.REF.NO
   ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM = 6
   ECD.SUB.NUM = START.REF.NO
   ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM = 7
   ECD.SUB.NUM = START.REF.NO
   ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM = 8
   ECD.SUB.NUM = START.REF.NO
   ECD.ACTION=7; CALL SCRN.EDIT
   RETURN
*
*---- UPDATE PROCESSES FOR NEW MENU
*
11000 *
   FOR N = 1 TO REF.CNT
      PPSX.ID = MENU(N+1)<1,1>
      MATREADU PPSX.REC FROM PMC_PROCESS_XREF, PPSX.ID THEN
         LOCATE MENU.NAME IN PPSX.MENU<1>,1 SETTING PTR ELSE
            PPSX.MENU = INSERT(PPSX.MENU,1,PTR,0,MENU.NAME)
            MATWRITE PPSX.REC ON PMC_PROCESS_XREF, PPSX.ID
         END
         MATREADU PPS.REC FROM PMC_PROCESS, PPSX.PROCESS.ID THEN
            LOCATE MENU.NAME IN PPS.MENU<1>,1 SETTING PTR ELSE
               PPS.MENU = INSERT(PPS.MENU,1,PTR,0,MENU.NAME)
               MATWRITE PPS.REC ON PMC_PROCESS, PPSX.PROCESS.ID
            END
         END ELSE
            RELEASE PMC_PROCESS, PPSX.PROCESS.ID
         END
      END ELSE
         RELEASE PMC_PROCESS_XREF, PPSX.ID
      END
   NEXT N
   RETURN
*
*---- UPDATE PROCESSES FOR OLD MENU
*
12000 *
   FOR N = 1 TO OLD.REF.CNT
      PPSX.ID = OLD.MENU(N+1)<1,1>
      MATREADU PPSX.REC FROM PMC_PROCESS_XREF, PPSX.ID THEN
         LOCATE MENU.NAME IN PPSX.MENU<1>,1 SETTING PTR THEN
            PPSX.MENU = DELETE(PPSX.MENU,1,PTR,0)
            MATWRITE PPSX.REC ON PMC_PROCESS_XREF, PPSX.ID
         END
         MATREADU PPS.REC FROM PMC_PROCESS, PPSX.PROCESS.ID THEN
            LOCATE MENU.NAME IN PPS.MENU<1>,1 SETTING PTR THEN
               PPS.MENU = DELETE(PPS.MENU,1,PTR,0)
               MATWRITE PPS.REC ON PMC_PROCESS, PPSX.PROCESS.ID
            END
         END ELSE
            RELEASE PMC_PROCESS, PPSX.PROCESS.ID
         END
      END ELSE
         RELEASE PMC_PROCESS_XREF, PPSX.ID
      END
   NEXT N
   RETURN
*
*---- UPDATE MENUFORM
13000 *
   OK = 0
   BEGIN CASE
      CASE MENU.NAME = "M.MASTER"
*      CASE MENU.NAME = "M.FS"
*      CASE MENU.NAME = "M.INV"
*      CASE MENU.NAME = "M.JC"
*      CASE MENU.NAME = "M.PMC"
      CASE 1
         OK = 1
   END CASE
   IF OK THEN
*---- Determine Form File (EASY.MENUFORMS or PRIMAC.MENUFORMS)
      FORM.FILE = FIELD(MENU.FILE,".",1):".MENUFORMS"
      BYPASS = 1
      CALL MENU.MAINT.TUF(MENU.NAME,FORM.FILE,MENU.FILE,BYPASS)
   END ELSE
      ERRMSG = "CANNOT UPDATE GUI FORM, USE FORM PAINTER"
      GOSUB 91000
   END
   RETURN
*
*--- CALLS FOR SYSCOM
*
90000*
91000 *
   ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
   ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
   ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
END
