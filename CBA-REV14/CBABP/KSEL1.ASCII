      SUBROUTINE KSEL1.ASCII(MAT TCC.REC, MAT GRAPH.REC, MAT VIDEO.REC, MAT KSEL.REC, MAT KSL.REC, MAT KSL.YX)
*
*COPY>CPYLIB>TCC
*COPY>CPYLIB>KSEL
*COPY>CPYLIB>CHAR
*
      DIM KSL.XY(40)
      MAT KSL.XY = ""
      FOR I = 1 TO 25
         FOR J = 1 TO KSL.COLS
            IF KSL.YX(I)<J> # "" THEN
               KSL.XY(J)<-1> = I
            END
         NEXT J
      NEXT I
      BEGIN CASE
      CASE KSL.COL = "B"
         KSL.COL = KSL.COLS
      CASE KSL.COL > KSL.COLS
         KSL.COL = KSL.COLS
      END CASE
      MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
      BEGIN CASE
      CASE KSL.ROW = "T"
         LNO = 1
      CASE KSL.ROW = "B"
         LNO = MAX.LNO
      CASE 1
         LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
            IF LNO > 1 THEN LNO = LNO - 1
         END
      END CASE
      KSL.ROW = KSL.XY(KSL.COL)<LNO>
      DX = KSL.YX(KSL.ROW)<KSL.COL>
*
      MAT HBOX.REC = ""; HBOX.ROW = 23
      HBOX.ID = "KSEL.SUB"; HBOX.SEP = "AM"
      HBOX.FILE = "SCREEN.HELP.FILE"; HBOX.MSG = "&OPENFILE&"
*
      IF KSL.HROW # "" THEN
         IF KSL.HMSG = "" THEN
            IF KSL.VALDAT = "" THEN
               KSL.HMSG = "Use"
            END ELSE
               VC = BOX.ON:VERT.CHAR:BOX.OFF
               KSL.HMSG = "Use ":VC
               ML = 0; LIMIT = COUNT(KSL.VALDAT,AM) + 1
               FOR H = 1 TO LIMIT
                  L = LEN(KSL.VALDAT<H>)
                  IF L + ML + 1 > 41 THEN
                     LIMIT = 0
                  END ELSE
                     KSL.HMSG = KSL.HMSG:KSL.VALDAT<H>:VC
                     ML = ML + L + 1
                  END
               NEXT H
            END
            KSL.HMSG = KSL.HMSG:ARROWS.MSG:", - = / , SPACE , ESC "
            IF KSL.HROW = "B" THEN
               IF KSEL.SLO = "" THEN
                  KSL.HMSG = @(0,23):ETX.HHD.OFF:KSL.HMSG:ETX.HHD.OFF:@(0,23):ETX.HHD
               END ELSE
                  KSL.HMSG = KSEL.SLO:ETX.HHD:KSL.HMSG:ETX.HHD.OFF:KSEL.SLF
               END
            END ELSE
               KSL.HMSG = @(0,KSL.HROW):ETX.HHD.OFF:KSL.HMSG:ETX.HHD.OFF:@(0,KSL.HROW):ETX.HHD
            END
         END
         PRINT KSL.HMSG:
      END
*
      MORE = 1
      BUFFER = ""
      PRINT KSEL.KOF:
      LOOP
         PRINT @(DX,KSL.ROW):KSL.ETX.SEL:@(DX,KSL.ROW):
         ECHO OFF
         INPUT KSL.SEL,1:
         ECHO ON
         PRINT @(DX,KSL.ROW):KSL.ETX.OFF:
*        PRINT @(0,23):"X":SEQ(KSL.SEL):"X":
110*
         BEGIN CASE
         CASE KSL.SEL = KSEL.SEL
            MORE = 0
         CASE KSL.SEL = KSL.ESC
            MORE = 0
         CASE KSL.SEL = KSEL.LF
            BEGIN CASE
            CASE BUFFER # ""
               KSL.SEL = BUFFER
               MORE = 0
            CASE LNO < MAX.LNO
               LNO = LNO + 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.SCROLL AND KSL.COL = KSL.COLS
               IF KSL.PGNO < KSL.PAGES THEN
                  KSL.ROW = "T"; KSL.COL = 1
                  KSL.PGNO = KSL.PGNO + 1
                  KSL.SEL = "SF"
                  MORE = 0
               END
            CASE KSL.COLS = 1
               LNO = 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE 1
               IF KSL.COL < KSL.COLS THEN
                  KSL.COL = KSL.COL + 1
               END ELSE
                  KSL.COL = 1
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.UPK
            BUFFER = ""
            BEGIN CASE
            CASE LNO > 1
               LNO = LNO - 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.SCROLL AND KSL.COL = 1
               IF KSL.PGNO > 1 THEN
                  KSL.ROW = "B"
                  KSL.PGNO = KSL.PGNO - 1
                  KSL.SEL = "SR"
                  MORE = 0
               END
            CASE KSL.COLS = 1
               LNO = MAX.LNO
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE 1
               IF KSL.COL < KSL.COLS THEN
                  KSL.COL = KSL.COL + 1
               END ELSE
                  KSL.COL = 1
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = MAX.LNO
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.ACK
            BUFFER = ""
            IF KSL.COL < KSL.COLS THEN
               KSL.COL = KSL.COL + 1
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.NAK
            BUFFER = ""
            IF KSL.COL > 1 THEN
               KSL.COL = KSL.COL - 1
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.SOH
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO > 1
               KSL.PGNO = 1; KSL.COL = 1
               KSL.ROW = "T"
               KSL.SEL = "ST"
               MORE = 0
            CASE LNO = 1
               IF KSL.COL > 1 THEN
                  KSL.COL = 2; KSL.SEL = KSEL.NAK
                  GOTO 110
               END
            CASE KSL.COL = 1
               LNO = 2; KSL.SEL = KSEL.UPK
               GOTO 110
            CASE 1
               KSL.COL = 1; LNO = 2; KSL.SEL = KSEL.UPK
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               GOTO 110
            END CASE
         CASE KSL.SEL = KSEL.ETX
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO < KSL.PAGES
               KSL.PGNO = KSL.PAGES
               KSL.ROW = "B"; KSL.COL = "B"
               KSL.SEL = "SB"
               MORE = 0
            CASE KSL.COL = KSL.COLS
               IF LNO < MAX.LNO THEN
                  LNO = MAX.LNO - 1; KSL.SEL = KSEL.LF
                  GOTO 110
               END
            CASE 1
               KSL.COL = KSL.COLS; KSL.SEL = KSEL.LF
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = MAX.LNO - 1
               GOTO 110
            END CASE
         CASE KSL.SEL = KSEL.PUP
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO > 1
               KSL.PGNO = KSL.PGNO - 1
               KSL.ROW = "B"; KSL.COL = 1
               KSL.SEL = "SR"
               MORE = 0
            CASE LNO > 1
               LNO = 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.COL > 1
               KSL.COL = 1
               LNO = 1; KSL.ROW = KSL.XY(KSL.COL)<LNO>
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.PDN
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO < KSL.PAGES
               KSL.ROW = "T"; KSL.COL = 1
               KSL.PGNO = KSL.PGNO + 1
               KSL.SEL = "SF"
               MORE = 0
            CASE LNO < MAX.LNO
               LNO = MAX.LNO
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.COL < KSL.COLS
               KSL.COL = KSL.COLS
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = MAX.LNO; KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.PFW
            BUFFER = ""
            IF KSL.COL < KSL.COLS THEN
               KSL.COL = KSL.COLS
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.PBK
            BUFFER = ""
            IF KSL.COL > 1 THEN
               KSL.COL = 1
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.HLP
            CALL @TCC.HLP.SUB(MAT TCC.REC, MAT GRAPH.REC, MAT VIDEO.REC, MAT KSEL.REC, MAT HBOX.REC)
            IF BOX.HROW # "" THEN PRINT KSL.HMSG:
         CASE KSL.VALDAT # ""
            LOCATE KSL.SEL IN KSL.VALDAT,1 SETTING FND THEN
               MORE = 0
            END ELSE
               BUFFER = BUFFER:KSL.SEL
            END
         CASE 1
            BUFFER = BUFFER:KSL.SEL
         END CASE
      WHILE MORE DO REPEAT
      PRINT KSEL.KON:
      BEGIN CASE
      CASE KSL.HROW = ""
      CASE KSL.HROW = "B"
         IF KSEL.SLO = "" THEN
            PRINT @(0,23):CL:
         END ELSE
            PRINT KSEL.SLO:
            PRINT "":
            PRINT KSEL.SLF:
         END
      CASE 1
         PRINT @(0,KSL.HROW):CL:
      END CASE
99999*
      RETURN
   END
