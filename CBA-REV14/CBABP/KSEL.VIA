      SUBROUTINE KSEL.VIA
*
*COPY>CPYLIB>COMMON3
*COPY>CPYLIB>COM.KSEL
*COPY>CPYLIB>TCC
*COPY>CPYLIB>KSEL
*COPY>CPYLIB>CHAR
*
      DIM KSL.XY(40)
      MAT KSL.XY = ""
      FOR I = 1 TO 25
         FOR J = 1 TO KSL.COLS
            IF KSL.YX(I)<J> # "" THEN
               KSL.XY(J)<-1> = I
            END
         NEXT J
      NEXT I
      BEGIN CASE
      CASE KSL.COL = "B"
         KSL.COL = KSL.COLS
      CASE KSL.COL > KSL.COLS
         KSL.COL = KSL.COLS
      END CASE
      MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
      BEGIN CASE
      CASE KSL.ROW = "T"
         LNO = 1
      CASE KSL.ROW = "B"
         LNO = MAX.LNO
      CASE 1
         LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
            IF LNO > 1 THEN LNO = LNO - 1
         END
      END CASE
      KSL.ROW = KSL.XY(KSL.COL)<LNO>
      DX = KSL.YX(KSL.ROW)<KSL.COL>
*
      HBOX.ID = "KSEL.SUB"; HBOX.SEP = "AM"
      HBOX.FILE = "SCREEN.HELP.FILE"; HBOX.MSG = "&OPENFILE&"
*
*---- Verify if mouse is on
*
      PCCMD = CHAR(27):CHAR(8); ENDCMD = CHAR(0) ; ***** Unidata may not support CHAR(0) *****
      IF MOUSE.BUTTONS THEN
         MOUSEON = PCCMD:"MOUSE /O-/L-/P-/R-/C-/D+/I64 ON ":ENDCMD
         MOUSEOFF = PCCMD:"MOUSE OFF ":ENDCMD
      END
*
      IF KSL.HROW # "" THEN
         IF KSL.HMSG # "" THEN
            HCNT = DCOUNT(KSL.HPOS,AM)
         END ELSE
            VC = BOX.ON:VERT.CHAR:BOX.OFF
            HCNT = 0
            IF KSL.VALDAT = "" THEN
               KSL.HPOS = ""
               KSL.HMSG = ETX.OFF:"Use"
            END ELSE
               KSL.HMSG = ETX.OFF:"Use ":VC
               ML = 0; LIMIT = COUNT(KSL.VALDAT,AM) + 1
               FOR H = 1 TO LIMIT
                  L = LEN(KSL.VALDAT<H>)
                  IF L + ML + 1 > 12 THEN
                     LIMIT = 0
                  END ELSE
                     KSL.HMSG = KSL.HMSG:KSL.VALDAT<H>:VC
                     HCNT = HCNT + 2
                     KSL.HPOS<HCNT-1> = 5 + ML
                     ML = ML + L + 1
                     KSL.HPOS<HCNT> = 5 + ML
                  END
               NEXT H
            END
            KSL.HMSG = KSL.HMSG:ARROWS.MSG:", - = / , SPACE , ESC "
            IF KSL.HROW = "B" THEN KSL.HROW = 23
            IF MOUSE.BUTTONS THEN
               KSL.HMSG = KSL.HMSG:@(49,KSL.HROW):VC:"Help":VC:"Home":VC:"PgUp":VC:"PgDn":VC:"End":VC:"Esc":VC
            END
            KSL.HMSG = KSL.HMSG:ETX.OFF:@(0,KSL.HROW):ETX.MHD
         END
         PRINT @(0,KSL.HROW):
         PRINT PCCMD:"COLOR ":BOX.BCOLOR:" ":ENDCMD:
         PRINT @(0,KSL.HROW):KSL.HMSG:
         PRINT @(DX,KSL.ROW):
         PRINT PCCMD:"COLOR ":BOX.WCOLOR:" ":ENDCMD:
      END
*
      IF MOUSE.BUTTONS THEN PRINT MOUSEON:
      MORE = 1
      BUFFER = ""
      LOOP
         PRINT @(DX,KSL.ROW):KSL.ETX.SEL:@(DX,KSL.ROW):
         ECHO OFF
         INPUT KSL.SEL,1:
         ECHO ON
         PRINT @(DX,KSL.ROW):KSL.ETX.OFF:
*        PRINT @(0,23):"X":SEQ(KSL.SEL):"X":
110*
         BEGIN CASE
         CASE MOUSE.BUTTONS AND KSL.SEL = "@"
            ECHO OFF
            INPUT MOUSE.SEL:
            ECHO ON
            M.ACTION = FIELD(MOUSE.SEL,' ',1)
            BEGIN CASE
* Bouble Click Action
            CASE M.ACTION = 6 OR M.ACTION = 5
               BUTTON = FIELD(MOUSE.SEL,' ',2)
               BUTTON = BUTTON-INT(BUTTON/4)*4
* Check for the left Button
               IF BUTTON = 1 THEN
                  ROW = FIELD(MOUSE.SEL,' ',4)
                  BEGIN CASE
                  CASE ROW = KSL.HROW
                     COL = FIELD(MOUSE.SEL,' ',3)
                     BEGIN CASE
                     CASE COL > 49 AND COL < 54
                        KSL.SEL = KSEL.HLP
                        GOTO 110
                     CASE COL > 54 AND COL < 59
                        KSL.SEL = KSEL.SOH
                        GOTO 110
                     CASE COL > 59 AND COL < 64
                        KSL.SEL = KSEL.PUP
                        GOTO 110
                     CASE COL > 64 AND COL < 69
                        KSL.SEL = KSEL.PDN
                        GOTO 110
                     CASE COL > 69 AND COL < 73
                        KSL.SEL = KSEL.ETX
                        GOTO 110
                     CASE COL > 73 AND COL < 77
                        KSL.SEL = KSL.ESC
                        GOTO 110
                     CASE HCNT
                        LIMIT = HCNT
                        FOR M = 1 TO LIMIT STEP 2
                           IF COL > KSL.HPOS<M> AND COL < KSL.HPOS<M+1> THEN
                              KSL.SEL = KSL.VALDAT<INT(M/2+0.5)>
                              LIMIT = 0; MORE = 0
                           END
                        NEXT M
                     END CASE
                  CASE ROW < 1 OR ROW > 25
                  CASE KSL.YX(ROW) # ""
                     COL = FIELD(MOUSE.SEL,' ',3)
                     LIMIT = KSL.COLS; BX = 1
                     FOR MX = LIMIT TO BX STEP -1
                        CX = KSL.YX(ROW)<MX>
                        IF COL > CX AND CX # "" THEN
                           BX = KSL.COLS + 1
                           IF MX # KSL.COL THEN
                              KSL.COL = MX
                              MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
                           END
                        END
                     NEXT MX
                     IF BX # 1 THEN
                        MX = KSL.YX(ROW)<KSL.COL>
                        IF MX = DX AND ROW = KSL.ROW THEN
                           KSL.SEL = KSEL.SEL
                           MORE = 0
                        END ELSE
                           KSL.ROW = ROW
                           LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                              IF LNO > 1 THEN LNO = LNO - 1
                           END
                           DX = MX
                        END
                     END
                  END CASE
               END
* Check if the MOUSE is on, Not applicable here.
            CASE M.ACTION = 0
* Response to WHERE or WAIT actions, Not applicable here.
            CASE M.ACTION = 1
* Check if the MOUSE was pressed, Not applicable here.
            CASE M.ACTION = 2
               PCOL = FIELD(MOUSE.SEL,' ',5)
               PROW = FIELD(MOUSE.SEL,' ',6)
* Check if the MOUSE was released, Not applicable here.
            CASE M.ACTION = 3
* Check if the MOUSE was pressed and released, Not applicable here.
            CASE M.ACTION = 4
            END CASE
111*
         CASE KSL.SEL = KSEL.SEL
            MORE = 0
         CASE KSL.SEL = KSL.ESC
            MORE = 0
         CASE KSL.SEL = KSEL.LF
            BEGIN CASE
            CASE BUFFER # ""
               KSL.SEL = BUFFER
               MORE = 0
            CASE LNO < MAX.LNO
               LNO = LNO + 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.SCROLL AND KSL.COL = KSL.COLS
               IF KSL.PGNO < KSL.PAGES THEN
                  KSL.ROW = "T"; KSL.COL = 1
                  KSL.PGNO = KSL.PGNO + 1
                  KSL.SEL = "SF"
                  MORE = 0
               END
            CASE KSL.COLS = 1
               LNO = 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE 1
               IF KSL.COL < KSL.COLS THEN
                  KSL.COL = KSL.COL + 1
               END ELSE
                  KSL.COL = 1
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.UPK
            BUFFER = ""
            BEGIN CASE
            CASE LNO > 1
               LNO = LNO - 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.SCROLL AND KSL.COL = 1
               IF KSL.PGNO > 1 THEN
                  KSL.ROW = "B"
                  KSL.PGNO = KSL.PGNO - 1
                  KSL.SEL = "SR"
                  MORE = 0
               END
            CASE KSL.COLS = 1
               LNO = MAX.LNO
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE 1
               IF KSL.COL < KSL.COLS THEN
                  KSL.COL = KSL.COL + 1
               END ELSE
                  KSL.COL = 1
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = MAX.LNO
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.ACK
            BUFFER = ""
            IF KSL.COL < KSL.COLS THEN
               KSL.COL = KSL.COL + 1
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.NAK
            BUFFER = ""
            IF KSL.COL > 1 THEN
               KSL.COL = KSL.COL - 1
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.SOH
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO > 1
               KSL.PGNO = 1; KSL.COL = 1
               KSL.ROW = "T"
               KSL.SEL = "ST"
               MORE = 0
            CASE LNO = 1
               IF KSL.COL > 1 THEN
                  KSL.COL = 2; KSL.SEL = KSEL.NAK
                  GOTO 110
               END
            CASE KSL.COL = 1
               LNO = 2; KSL.SEL = KSEL.UPK
               GOTO 110
            CASE 1
               KSL.COL = 1; LNO = 2; KSL.SEL = KSEL.UPK
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               GOTO 110
            END CASE
         CASE KSL.SEL = KSEL.ETX
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO < KSL.PAGES
               KSL.PGNO = KSL.PAGES
               KSL.ROW = "B"; KSL.COL = "B"
               KSL.SEL = "SB"
               MORE = 0
            CASE KSL.COL = KSL.COLS
               IF LNO < MAX.LNO THEN
                  LNO = MAX.LNO - 1; KSL.SEL = KSEL.LF
                  GOTO 110
               END
            CASE 1
               KSL.COL = KSL.COLS; KSL.SEL = KSEL.LF
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = MAX.LNO - 1
               GOTO 110
            END CASE
         CASE KSL.SEL = KSEL.PUP
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO > 1
               KSL.PGNO = KSL.PGNO - 1
               KSL.ROW = "B"; KSL.COL = 1
               KSL.SEL = "SR"
               MORE = 0
            CASE LNO > 1
               LNO = 1
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.COL > 1
               KSL.COL = 1
               LNO = 1; KSL.ROW = KSL.XY(KSL.COL)<LNO>
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.PDN
            BUFFER = ""
            BEGIN CASE
            CASE KSL.PGNO < KSL.PAGES
               KSL.ROW = "T"; KSL.COL = 1
               KSL.PGNO = KSL.PGNO + 1
               KSL.SEL = "SF"
               MORE = 0
            CASE LNO < MAX.LNO
               LNO = MAX.LNO
               KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            CASE KSL.COL < KSL.COLS
               KSL.COL = KSL.COLS
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               LNO = MAX.LNO; KSL.ROW = KSL.XY(KSL.COL)<LNO>
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END CASE
         CASE KSL.SEL = KSEL.PFW
            BUFFER = ""
            IF KSL.COL < KSL.COLS THEN
               KSL.COL = KSL.COLS
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.PBK
            BUFFER = ""
            IF KSL.COL > 1 THEN
               KSL.COL = 1
               LOCATE KSL.ROW IN KSL.XY(KSL.COL),1 BY "AR" SETTING LNO ELSE
                  IF LNO > 1 THEN LNO = LNO - 1
                  KSL.ROW = KSL.XY(KSL.COL)<LNO>
               END
               MAX.LNO = DCOUNT(KSL.XY(KSL.COL),AM)
               DX = KSL.YX(KSL.ROW)<KSL.COL>
            END
         CASE KSL.SEL = KSEL.HLP
            IF MOUSE.BUTTONS THEN PRINT MOUSEOFF:
            CALL @TCC.HLP.SUB
            IF MOUSE.BUTTONS THEN PRINT MOUSEON:
         CASE KSL.VALDAT # ""
            LOCATE KSL.SEL IN KSL.VALDAT,1 SETTING FND THEN
               MORE = 0
            END ELSE
               BUFFER = BUFFER:KSL.SEL
            END
         CASE 1
            BUFFER = BUFFER:KSL.SEL
         END CASE
      WHILE MORE DO REPEAT
      IF MOUSE.BUTTONS THEN PRINT MOUSEOFF:
      IF KSL.HROW # "" THEN
         PRINT @(0,KSL.HROW):CL:
      END
99999*
      RETURN
   END
