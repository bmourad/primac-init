*COPY>CPYLIB>COM1
**************************************************************************
*
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM - BUILD.MENU.PROCESS
*
* BY      - ALEJANDRO DELGADO
*
* DATE    - 06/05/02
*
* DESCRIPTION
*
* THIS PROGRAM REBUILDS THE PMC_PROCESS RECORDS WITH THE MENU POINTER.
*
*T26630 adelgado 06/03/2002 * Original Code.
**************************************************************************
*COPY>PMC.CPYLIB>PMC_PROCESS
*COPY>PMC.CPYLIB>PMC_PROCESS_XREF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
  OPEN "","PMC_PROCESS_XREF" TO PMC_PROCESS_XREF ELSE
    ERRMSG = "CANNOT OPEN PMC_PROCESS_XREF FILE"
    GOTO 93000
  END
  OPEN "","PMC_PROCESS" TO PMC_PROCESS ELSE
    ERRMSG = "CANNOT OPEN PMC_PROCESS FILE"
    GOTO 93000
  END
  OPEN "","PRIMAC.eMENUS" TO PRIMAC.eMENUS ELSE
    ERRMSG = "CANNOT OPEN PRIMAC.eMENUS FILE"
    GOTO 93000
  END
  OPEN "","ePRIMAC.MENUS" TO ePRIMAC.MENUS ELSE
    ERRMSG = "CANNOT OPEN ePRIMAC.MENUS FILE"
    GOTO 93000
  END
*
  DIM MENU.REC(33) ; DIM eMENU.REC(33)
  MAT MENU.REC = '' ; MAT eMENU.REC = ''
  ID.ARRAY = '' ; SEQ.ARRAY = ''
  PMC.ARRAY = ''
  NAMES = '' ; SEQS = '' ; ACTIONS = ''
*
*  CLEAR MENU POINTER FROM PMC_PROCESS_XREF
*
  CMD = 'SSELECT PMC_PROCESS_XREF'
  PERFORM CMD CAPTURING MSG
  LOOP.DONE = 0
  LOOP
    READNEXT ID ELSE LOOP.DONE = 1
  UNTIL (LOOP.DONE) DO
    MATREADU PPSX.REC FROM PMC_PROCESS_XREF, ID THEN
      PPSX.MENU = ''
      MATWRITE PPSX.REC ON PMC_PROCESS_XREF, ID
    END
  REPEAT
*
*  CLEAR MENU POINTER FROM PMC_PROCESS
*
  CMD = 'SSELECT PMC_PROCESS'
  PERFORM CMD CAPTURING MSG
  LOOP.DONE = 0
  LOOP
    READNEXT ID ELSE LOOP.DONE = 1
  UNTIL (LOOP.DONE) DO
    MATREADU PPS.REC FROM PMC_PROCESS, ID THEN
      PPS.MENU = ''
      PPS.MENU.SEQ = ''
      PPS.MODULE = ''
      PPS.MODULE.SEQ = ''
      PPS.MODULE.MENUS = ''
      MATWRITE PPSX.REC ON PMC_PROCESS, ID
    END
  REPEAT
*
* BUILD PROCESS FROM PRIMAC.MENUS
*
  CMD = 'SSELECT PRIMAC.eMENUS'
  PERFORM CMD CAPTURING MSG
  LOOP.DONE = 0
  LOOP
    READNEXT ID ELSE LOOP.DONE = 1
    MATREAD MENU.REC FROM PRIMAC.eMENUS, ID THEN
      PTR = 0
      FOR POS = 1 TO 33
        PMCID = MENU.REC(POS)<1,1>
        eMENU.ID = MENU.REC(POS)<1,5>
        BEGIN CASE
          CASE PMCID = 'C' OR PMCID = ''
          CASE PMCID[1,2] = 'M.' ; PTR += 1
          CASE PMCID[1,3] = '*M.' ; PTR += 1
          CASE 1 ; PTR += 1
            IF eMENU.ID = '' THEN
              ERRMSG = 'CANNOT FIND eMENU FOR MENU (':ID:') AND PROC (':PMCID:')'
              GOSUB 91000
            END ELSE
              MATREAD eMENU.REC FROM ePRIMAC.MENUS, eMENU.ID THEN
                ePTR = 0 ; ePMCID = eMENU.REC(ZZZ)<1,5>
                FOUND = 0
                FOR ZZZ = 1 TO 33 UNTIL (FOUND)
                  BEGIN CASE
                    CASE ePMCID = 'C' OR ePMCID = ''    
                    CASE ePMCID[1,2] = 'M.' ; ePTR += 1 
                    CASE ePMCID[1,3] = '*M.' ; ePTR += 1
                    CASE ePMCID = PMCID ; ePTR += 1 ; FOUND = 1
                    CASE 1 ; ePTR += 1                 
                  END CASE
                NEXT ZZZ
                IF NOT(FOUND) THEN
                  ERRMSG = 'CANNOT FIND PROC (':PMCID:') IN eMENU (':eMENU.ID:')'
                  GOSUB 91000
                END ELSE
                  PMC.ARRAY<1,-1> = PMCID
                  ID.ARRAY<1,-1> = ID
                  SEQ.ARRAY<1,-1> = PTR
                  ID.ARRAY<2,-1> = eMENU.ID
                  SEQ.ARRAY<2,-1> = ePTR
                END
              END ELSE
                ERRMSG = 'eMENU DOES NOT EXIST FOR MENU (':ID:') AND PROC (':PMCID:')'
                GOSUB 91000
              END
            END
        END CASE
      NEXT POS
    END
  UNTIL (LOOP.DONE) DO
  REPEAT
*
* REBUILD MENU POINTER TO PMC_PROCESS & PMC_PROCESS_XREF
*
  PCNT = DCOUNT(PMC.ARRAY<1>,VM)
  SCNT = DCOUNT(SEQ.ARRAY<1>,VM)
  ICNT = DCOUNT(ID.ARRAY<2>,VM)
  IF PCNT = SCNT AND SCNT = ICNT THEN
    ACTIONS<1> = 'B' ; ACTIONS<2> = 'A'
    FOR XX = 1 TO PCNT
      ERRMSG = ''
      NAMES<1> = ID.ARRAY<1,XX>
      NAMES<2> = ID.ARRAY<2,XX>
      SEQS<1> = SEQ.ARRAY<1,XX>
      SEQS<2> = SEQ.ARRAY<2,XX>
      CALL MENU.PROCESS.UPD(PMC.ARRAY<1,XX>,ACTIONS,NAMES,SEQS,ERRMSG)
    NEXT XX
  END ELSE
    ERRMSG = 'CORRUPTION IN THE REBUILD ARRAYS HAS OCCURRED.'
    GOSUB 91000
  END
  STOP
*
91000 *
  ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
  ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
  ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
END
