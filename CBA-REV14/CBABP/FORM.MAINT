*********************************************************************
*
* Copyright 1982 by Vercom Software Inc.
*
* REVISION - [11.0]
*
* PROGRAM  - FORM.MAINT
*
* AUTHOR   - DIANE TOVAR, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 09/26/96
*
* DESCRIPTION
*
* Maintains the SBClient form generated by screen maintenance
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
* *COPY>CPYLIB>SAMPLE
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZE SYSCOM
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOTO 93000
   END
   OPEN "","M.SCREENS" TO M.SCREENS ELSE
      ERRMSG = "CANNOT OPEN M.SCREENS FILE"
      GOTO 93000
   END
*
*---- INITIALIZATION
*
   CONO = ""
   MAT COMP.REC = ""
   CALL GET.CONO (CONO, MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
   MAT SCV.REC = ""
   MAT EDIT.COM.DRIVER = ""
   ECD.SCRN.CNT = 2
   ECD.SCRN.NAME<1> = "FORM.MAINT"
   ECD.SCRN.NAME<2> = "FORM.MAINT.SUB"
   ECD.ACTION=1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   ESN = ECD.SCRN.NO
   ECD.ACTION=2; CALL SCRN.EDIT
*
   BEGIN.PAGE = 7
   PAGE.SIZE = 13
   LINE.SPACE = 1
   LINES = 0
   LN = ""
   OLD.START = ""
   FORM.ID = ""
   TEMP.LN = ""
   TEMP.REF = ""
   TEMP.CLASS = ""
   TEMP.NAME = ""
   TEMP.STRING = ""
   GOTO 110
   GOTO 110
*
*---- MAIN PROCESSING
*
100 *
   ECD.ACTION=6; CALL SCRN.EDIT
110 *
   GOSUB 1000
   IF ECD.RET.VALUE = "END" THEN GOTO 99999
   NEW.REC = 0
   READU FORM.REC FROM FORM, ECD.RET.VALUE THEN
      FORM.ID = ECD.RET.VALUE
      LOCATE "title" IN FORM.REC<3>,1 SETTING PTR THEN
         FORM.TITLE = FORM.REC<4,PTR>
      END ELSE
         FORM.TITLE = ""
      END
   END
   FPTR = 1
   FCNT = DCOUNT(FORM.REC,AM)
   GOSUB 81000
   GOSUB 80000
   LN = 1
*
*---- GET OPERATOR REPLY
*
500 *
   ECD.NUM = 20
   ECD.ACTION=4; CALL SCRN.EDIT
   OPT = ECD.RET.VALUE
510 *
   BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 100
      CASE OPT = "D"
         GOSUB 600
         IF ECD.RET.VALUE # "" AND ECD.RET.VALUE # "END" THEN
            LN = ECD.RET.VALUE
            SEL.REF = FORM.REC<2,2,LN>
            SEL.LINE = FORM.REC<SEL.REF>
            SEL.LINE<2> = FORM.REC<SEL.REF+1>
            SEL.LINE<3> = FORM.REC<SEL.REF+2>
            SEL.LINE<4> = FORM.REC<SEL.REF+3>
            ECD.SCRN.NO = 2
            CALL FORM.MAINT.SUB(SEL.REF,SEL.LINE,FORM.FILE,FORM.TITLE,FORM.ID)
            FORM.REC<SEL.REF+2> = SEL.LINE<3>
            FORM.REC<SEL.REF+3> = SEL.LINE<4>
            ECD.SCRN.NO = 1
            ECD.ACTION=2; CALL SCRN.EDIT
            GOSUB 80050
            OLD.START = ""
            GOSUB 50000
         END
      CASE OPT = "S" OR OPT = "SF"
         LN = OLD.START + PAGE.SIZE
         IF LN > LINES THEN LN = 1
         GOSUB 50000
      CASE OPT = "SR"
         LN = OLD.START - PAGE.SIZE
         IF LN < 1 THEN LN = 1
         GOSUB 50000
      CASE OPT = "ST"
         LN = 1
         GOSUB 50000
      CASE OPT = "SB"
         LN = LINES
         IF LN < 1 THEN LN = 1
         GOSUB 50000
      CASE OPT = "F"
         FORM.REC<1> =  'formclass ':OCONV(TIME(),'MTS'):'  ':OCONV(DATE(),'D')
         WRITE FORM.REC ON FORM, FORM.ID
         GOTO 100
   END CASE
   GOTO 500
*
*---- GET LINE NUMBER
*
600 *
   GOSUB 50000
   ECD.NUM = 21
   ECD.MINV = START.LINE
   ECD.MAXV = LAST.LINE
   ECD.NUM=21; ECD.ACTION=4; CALL SCRN.EDIT
   RETURN
*
*---- Get file
1000 *
   ECD.NUM = 1
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN RETURN
   FORM.FILE = ECD.RET.VALUE
   OPEN "",FORM.FILE TO FORM ELSE
      ERRMSG = "CANNOT OPEN ":FORM.FILE:" FILE"
      GOTO 93000
   END
   ECD.NUM = 2
   ECD.VALDAT.FILE = FORM
   ECD.VALDAT.CODE = 2
   ECD.ACTION = 4 ; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN RETURN
   RETURN
*
*---- GET SAMPLE ITEM ID
*
1010 *
   ECD.NUM = 2
   ECD.ACTION=4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN RETURN
   FORM.ID = ECD.RET.VALUE
   RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
   START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   IF LAST.LINE > LINES THEN LAST.LINE = LINES
   IF START.LINE = OLD.START THEN RETURN
   OLD.START = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
*      PRINT @(0,SLN) : CL : SCV.REC(10)<ESN,N> "R#3" :
*      PRINT @(4,SLN):SCV.REC(11)<ESN,N>:
*      PRINT @(8,SLN):SCV.REC(12)<ESN,N>:
*      PRINT @(24,SLN):SCV.REC(13)<ESN,N>:
*      PRINT @(40,SLN):SCV.REC(14)<ESN,N>:
      P_X  = 0 ; P_Y = SLN ; P_VALUE = SCV.REC(10)<ESN,N>"R#3" ; P_OPT = "CL"
      P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:SCV.REC(11)<ESN,N>
      P_X  := AM:8 ; P_Y := AM:SLN ; P_VALUE := AM:SCV.REC(12)<ESN,N>
      P_X  := AM:24 ; P_Y := AM:SLN ; P_VALUE := AM:SCV.REC(13)<ESN,N>
      P_X  := AM:40 ; P_Y := AM:SLN ; P_VALUE := AM:SCV.REC(14)<ESN,N>
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR N = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
*      PRINT @(0,SLN) : @(-4) :
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT N
   RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
   SCV.REC(1)<ESN> = FORM.FILE
   SCV.REC(2)<ESN> = FORM.ID
   SCV.REC(3)<ESN> = FORM.TITLE
80050 *
   ECD.ACTION=3; CALL SCRN.EDIT
   RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
   SCV.REC(11)<ESN> = CHANGE(FORM.REC<2,2>,SVM,VM)            ;* REF
   TEMP.REF = CHANGE(FORM.REC<2,2>,SVM,VM)
   LCNT = DCOUNT(FORM.REC<2,2>,SVM)
   LINES = LCNT
   FOR LN = 1 TO LCNT
      FPTR = FORM.REC<2,2,LN>
      TEMP.LN<1,LN> = LN                  ;* SCROLL LINE NUMBER
      TEMP.CLASS<1,LN> = FORM.REC<FPTR>      ;* CLASS
      TEMP.NAME<1,LN> = FORM.REC<FPTR+1,3>  ;* NAME
      LOCATE "string" IN FORM.REC<FPTR+2>,1 SETTING PTR THEN
         TEMP.STRING<1,LN> = FORM.REC<FPTR+3,PTR>
      END ELSE
         TEMP.STRING<1,LN> = ""
      END
   NEXT LN
   SCV.REC(10)<ESN> = TEMP.LN                  ;* SCROLL LINE NUMBER
   SCV.REC(12)<ESN> = TEMP.CLASS      ;* CLASS
   SCV.REC(13)<ESN> = TEMP.NAME  ;* NAME
   SCV.REC(14)<ESN> = TEMP.STRING  ;* STRING
   LN = 1
   RETURN
*
*---- ERROR ROUTINE
*
91000 ERR.TYPE=1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 ERR.TYPE=2; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE=3; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
*
*---- END OF PROGRAM
*
99999 *
*   PRINT @(-1)
END
