*COPY>CPYLIB>COM1
*
*COPY>CPYLIB>TCC
*COPY>CPYLIB>ALT.BUILD.SCREEN
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- DEFINE VARIABLES
*
*
*---- OPEN ALL FILES
*
      MAT FILE.VARS = ""
      OPEN "","VOC" TO MD ELSE
         ERRMSG = "CANNOT OPEN VOC FILE"
         GOTO 93000
      END
      OPEN "","PCNAME_XREF" TO PCNAME_XREF ELSE
         ERRMSG = "CANNOT OPEN PCNAME_XREF FILE"
         GOTO 93000
      END
      OPEN "","XREF.DATA" TO XREF.DATA ELSE
         ERRMSG = "CANNOT OPEN XREF.DATA FILE"
         GOTO 93000
      END
      OPEN "","M.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "CANNOT OPEN M.SCREENS FILE"
         GOTO 93000
      END
*
*---- INITIALIZATION
*
10*
      MAT EDIT.COM.DRIVER = ""
      ECD.SCRN.CNT = 2
      ECD.SCRN.NAME = "SCRN_BLD_CNV"
      ECD.ACTION = 1
      CALL SCRN.EDIT
      ECD.SCRN.NO = 1
*
      SFILE = ""
      SNAME = ""
      PCNAME = ""
      NEXT_SNAME = ""
      PAGE.SIZE = 8
      BEGIN.PAGE = 12
      LINE.SPACE = 1
      ECD.ACTION = 2
      CALL SCRN.EDIT
      MAT SCV.REC = ""
      OPTION = ""
*
*---- MAIN PROCESSING
*
100*
*
*---- Enter FILE Name
      IF SFILE[LEN(SFILE)-7,8] = ".SCREENS" THEN
         ECD.DEFAULT = SFILE[1,LEN(SFILE)-8]
      END ELSE
         ECD.DEFAULT = SFILE
      END
      ECD.NUM = 1
      ECD.ACTION = 4
      CALL SCRN.EDIT
      BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 99999
      CASE ECD.RET.VALUE = ""
         PCNAME = ""
         GOSUB 3000
         IF ECD.RET.VALUE = "END" THEN GOTO 100
      CASE 1
         CHK_FILE = ECD.RET.VALUE
         GOSUB 1000
         IF ERRMSG # "" THEN
            GOSUB 91000
            GOTO 100
         END
         IF CHK_FILE # SFILE AND SFILE # "" THEN
            CLEARSELECT
            SNAME = ""
            NEXT_SNAME = ""
         END
         SFILE = CHK_FILE
         GOSUB 2000
         IF ECD.RET.VALUE = "END" THEN GOTO 100
      END CASE
150*
      ECD.SCRN.NAME<2> = SNAME
      ECD.SCRN.FLAG<2> = 2
*
*---- PROCESS ALL PROBLEM FIELDS
*
      ALL_FIELDS = ""
      LN.NO = 0
      OPTION = ""
      LOOP
         LN.NO = LN.NO + 1
      UNTIL LN.NO > LINES OR OPTION = "END" DO
         BEGIN CASE
         CASE SCV.REC(12)<ECD.SCRN.NO,LN.NO> = ""
         CASE SCV.REC(13)<ECD.SCRN.NO,LN.NO> = ""
         CASE 1
            ECD.NUM = SCV.REC(12)<ECD.SCRN.NO,LN.NO>
            LOCATE ECD.NUM IN ALL_FIELDS,1 SETTING FND ELSE
               ALL_FIELDS<FND> = ECD.NUM
               ECD.SCRN.NO = 2
               ECD.ACTION = 4
               CALL SCRN.EDIT
               ECD.SCRN.NO = 1
               GOSUB 2500
               IF ERRMSG # "" THEN
                  GOSUB 91000
                  OPTION = "END"
               END
               LN.NO = 0
            END
         END CASE
      REPEAT
*---- RETURNED FROM THE PMSG
      BEGIN CASE
      CASE ALL_FIELDS = ""
      CASE PC.PORT.TYPE = "termulator" AND ECD.SCRN.PC<ECD.SCRN.NO>
         ECD.SCRN.NO = 2
         ECD.ACTION = 99
         CALL SCRN.EDIT
         ECD.SCRN.NO = 1
      CASE 1
         ECD.ACTION = 3
         CALL SCRN.EDIT
      END CASE
      LN = 1
      OLD.START.LINE = 0
      GOSUB 80000
*
*---- REQUEST
*
      LOOP
         ECD.NUM = 6
         ECD.MAXV = LAST.LINE
         ECD.MINV = START.LINE
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
         ECD.ACTION = 4
         CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
         CASE OPTION = "END"
         CASE OPTION = "E"
            OPTION = "END"
         CASE OPTION = "V"
            GOSUB 30000
            BEGIN CASE
            CASE LN.NO = 0
            CASE SCV.REC(12)<ECD.SCRN.NO,LN.NO> > 0
               GOSUB 10000
            END CASE
         CASE OPTION = "N"
         CASE NUM(OPTION) AND OPTION > 0
            IF SCV.REC(12)<ECD.SCRN.NO,OPTION> > 0 THEN
               LN.NO = OPTION
               GOSUB 10000
            END
         CASE OPTION = "S" OR OPTION = "SF"
            LN = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE + PAGE.SIZE
            IF LN > LINES THEN LN = 1
            GOSUB 80000
         CASE OPTION = "SR"
            LN = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE - PAGE.SIZE
            IF LN < 1 THEN LN = LINES
            IF LN < 1 THEN LN = 1
            GOSUB 80000
         CASE OPTION = "ST"
            LN = 1
            GOSUB 80000
         CASE OPTION = "SB"
            LN = LINES
            IF LN < 1 THEN LN = 1
            GOSUB 80000
         END CASE
      UNTIL OPTION = "END" OR OPTION = "N" DO
      REPEAT
      IF NEXT_SNAME = SNAME THEN NEXT_SNAME = ""
      MAT SCV.REC = ""
      ECD.ACTION = 6
      CALL SCRN.EDIT
      ECD.NUM = 1
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = SFILE
      ECD.ACTION = 5
      CALL SCRN.EDIT
      GOSUB 2000
      IF ECD.RET.VALUE = "END" THEN
         SCV.REC(1)<ECD.SCRN.NO> = ""
         GOTO 100
      END ELSE
         GOTO 150
      END
*
*---- OPEN SCREEN FILE
1000*
      ERRMSG = ""
      IF INDEX(CHK_FILE,".",1) = 0 THEN
         CHK_FILE = CHK_FILE:".SCREENS"
         ECD.NUM = 1
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CHK_FILE
         ECD.ACTION = 5
         CALL SCRN.EDIT
      END
      IF FIELD(CHK_FILE,".",2) # "SCREENS" THEN
         ERRMSG = 'Screen file name must end with ".SCREENS"'
      END ELSE
         READ REC FROM MD, CHK_FILE THEN
            IF REC<1> # "F" THEN
               ERRMSG = "INVALID FILE NAME - ":CHK_FILE
            END ELSE
               OPEN '',CHK_FILE TO SECOND.SCREENS ELSE
                  ERRMSG = "INVALID FILE NAME - ":CHK_FILE
               END
            END
         END ELSE
            ERRMSG = "INVALID FILE NAME - ":CHK_FILE
         END
      END
      RETURN
*
*---- Enter SCREEN Name
2000*
      IF NEXT_SNAME = "" THEN
         READNEXT NEXT_SNAME ELSE NEXT_SNAME = ""
         BEGIN CASE
         CASE NEXT_SNAME = ""
         CASE NEXT_SNAME[LEN(NEXT_SNAME)-3,4] = "*FLD"
            NEXT_SNAME = NEXT_SNAME[1,LEN(NEXT_SNAME)-4]
         CASE 1
            NEXT_SNAME = ""
            GOTO 2000
         END CASE
      END
      IF OPTION = "N" AND NEXT_SNAME # "" THEN
         ECD.NUM = 2
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = NEXT_SNAME
         ECD.ACTION = 5
         CALL SCRN.EDIT
         ECD.RET.VALUE = NEXT_SNAME
      END ELSE
         BEGIN CASE
         CASE NEXT_SNAME # ""
            ECD.DEFAULT = NEXT_SNAME
         CASE 1
            ECD.DEFAULT = SNAME
         END CASE
         ECD.NUM = 2
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
         ECD.ACTION = 4
         CALL SCRN.EDIT
      END
      BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         RETURN
      CASE ECD.RET.VALUE = "*"
         IF NEXT_SNAME = "" THEN
            SELECT SECOND.SCREENS
         END
         GOTO 2000
      CASE ECD.RET.VALUE = "N"
         NEXT_SNAME = ""
         GOTO 2000
      CASE ECD.RET.VALUE[LEN(ECD.RET.VALUE)-3,4] = "*FLD"
         SCRN_ID = ECD.RET.VALUE
         ECD.RET.VALUE = ECD.RET.VALUE[1,LEN(ECD.RET.VALUE)-4]
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ECD.RET.VALUE
         ECD.ACTION = 5
         CALL SCRN.EDIT
      CASE 1
         SCRN_ID = ECD.RET.VALUE : "*FLD"
      END CASE
      GOSUB 2510
      IF ERRMSG # "" THEN
         GOSUB 91000
         IF OPTION = "N" THEN NEXT_SNAME = ""
         GOTO 2000
      END
      SNAME = ECD.RET.VALUE
      PCNAME = BLD.PCNAME
      ECD.NUM = 3
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = BLD.PCNAME
      ECD.ACTION = 5
      CALL SCRN.EDIT
      RETURN
*
*---- FIND ERRORS
*
2500*
      SCV.REC(11)<ECD.SCRN.NO> = ""
      SCV.REC(12)<ECD.SCRN.NO> = ""
      SCV.REC(13)<ECD.SCRN.NO> = ""
      SCV.REC(14)<ECD.SCRN.NO> = ""
2510*
      ERRMSG = ""
      LINES = 0
      MATREAD ABLD.SCRN FROM SECOND.SCREENS, SCRN_ID ELSE
         MAT ABLD.SCRN = ""
         ERRMSG = "Invalid screen.   Try again!"
         GOTO 2599
      END
      MCNT = 0
      MPCNT = 0
      PSTACK = 0
      FOR FC = 1 TO BLD.FIELD.CNT
         EXT.REF = BLD.EXT.REF<1,FC>
         FDEF = ABLD.SCRN(FC)
*---- CHECKING FOR 'M' OR 'MP'
         BEGIN CASE
         CASE FDEF<1,B.FLDTYPE> = "M"
            MCNT = MCNT + 1
         CASE FDEF<1,B.FLDTYPE> = "MP"
            MPCNT = MPCNT + 1
         END CASE
*---- CHECKING PSTACK
         BEGIN CASE
         CASE FDEF<1,B.PSTACK> = ""
         CASE FDEF<1,B.PSTACK> = 1
            PSTACK = PSTACK + 1
         CASE 1
            LINES = LINES + 1
            SCV.REC(12)<ECD.SCRN.NO,LINES> = EXT.REF
            SCV.REC(13)<ECD.SCRN.NO,LINES> = "***"
            SCV.REC(14)<ECD.SCRN.NO,LINES> = "PSTACK = ":FDEF<1,B.PSTACK>
         END CASE
*---- CHECKING IF FLDTYPE = 'M', 'MP' OR 'VP' WHEN PMSG EXIST
         BEGIN CASE
         CASE FDEF<1,B.PMSG> = ""
         CASE FDEF<1,B.TYP> = 8
         CASE FDEF<1,B.FLDTYPE> = "VP"
            LINES = LINES + 1
            SCV.REC(12)<ECD.SCRN.NO,LINES> = EXT.REF
            SCV.REC(13)<ECD.SCRN.NO,LINES> = ""
            SCV.REC(14)<ECD.SCRN.NO,LINES> = FDEF<1,B.PMSG>
         CASE FDEF<1,B.FLDTYPE> = "P" OR FDEF<1,B.FLDTYPE> = "MP"
*---- CHECK VALDAT & TOOLBAR
            BEGIN CASE
            CASE FDEF<1,B.VALDAT> # ""
               LINES = LINES + 1
               SCV.REC(12)<ECD.SCRN.NO,LINES> = EXT.REF
               SCV.REC(13)<ECD.SCRN.NO,LINES> = ""
               SCV.REC(14)<ECD.SCRN.NO,LINES> = FDEF<1,B.PMSG>
            CASE FDEF<1,B.TYP> = 3 AND FDEF<1,B.USERDATA> = "^VALUE^"
               LINES = LINES + 1
               SCV.REC(12)<ECD.SCRN.NO,LINES> = EXT.REF
               SCV.REC(13)<ECD.SCRN.NO,LINES> = ""
               SCV.REC(14)<ECD.SCRN.NO,LINES> = FDEF<1,B.PMSG>
            CASE 1
               LINES = LINES + 1
               SCV.REC(12)<ECD.SCRN.NO,LINES> = EXT.REF
               SCV.REC(13)<ECD.SCRN.NO,LINES> = "val"
               SCV.REC(14)<ECD.SCRN.NO,LINES> = FDEF<1,B.PMSG>
*              SCV.REC(14)<ECD.SCRN.NO,LINES> = "VALDAT IS NOT SETUP"
            END CASE
         CASE 1
            LINES = LINES + 1
            SCV.REC(12)<ECD.SCRN.NO,LINES> = EXT.REF
            SCV.REC(13)<ECD.SCRN.NO,LINES> = "'P'"
            SCV.REC(14)<ECD.SCRN.NO,LINES> = FDEF<1,B.PMSG>
*           SCV.REC(14)<ECD.SCRN.NO,LINES> = "FLDTYPE MUST BE 'P', 'MP' OR 'VP'"
         END CASE
      NEXT FC
      IF PSTACK < 1 THEN
         ERRMSG = "NO VALID '1' PSTACK IS SET"
         LINES = LINES + 1
         INS "" BEFORE SCV.REC(12)<ECD.SCRN.NO,1>
         INS "***" BEFORE SCV.REC(13)<ECD.SCRN.NO,1>
         INS ERRMSG BEFORE SCV.REC(14)<ECD.SCRN.NO,1>
      END
      BEGIN CASE
      CASE MCNT > 1
         ERRMSG = "MORE THAN ONE 'M' FIELDS"
         LINES = LINES + 1
         INS "" BEFORE SCV.REC(12)<ECD.SCRN.NO,1>
         INS "***" BEFORE SCV.REC(13)<ECD.SCRN.NO,1>
         INS ERRMSG BEFORE SCV.REC(14)<ECD.SCRN.NO,1>
      CASE MCNT < 1 AND MPCNT < 1
         ERRMSG = "NO 'M' OR 'MP' FIELDS"
         LINES = LINES + 1
         INS "" BEFORE SCV.REC(12)<ECD.SCRN.NO,1>
         INS "***" BEFORE SCV.REC(13)<ECD.SCRN.NO,1>
         INS ERRMSG BEFORE SCV.REC(14)<ECD.SCRN.NO,1>
      CASE 1
      END CASE
      ERRMSG = ""
2599*
      RETURN
*
*---- Enter PCNAME
3000*
      ECD.NUM = 3
      ECD.VALDAT.FILE = PCNAME_XREF
      ECD.VALDAT.CODE = 2
      ECD.PREFIX.ID = ""
      ECD.ACTION = 4
      CALL SCRN.EDIT
      BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         RETURN
      CASE 1
         ECD.NUM = 1
         SCV.REC(ECD.NUM)<1> = ECD.VALDAT.ITEM<1>
         ECD.ACTION = 5
         CALL SCRN.EDIT
         ECD.NUM = 2
         SCV.REC(ECD.NUM)<1> = ECD.VALDAT.ITEM<2>
         ECD.ACTION = 5
         CALL SCRN.EDIT
         CHK_FILE = ECD.VALDAT.ITEM<1>
         SCRN_ID = ECD.VALDAT.ITEM<2> : "*FLD"
         GOSUB 1000
         IF ERRMSG # "" THEN
            GOSUB 91000
            GOTO 3000
         END
         IF CHK_FILE # SFILE AND SFILE # "" THEN
            CLEARSELECT
            SNAME = ""
            NEXT_SNAME = ""
         END
         SFILE = CHK_FILE
         GOSUB 2510
         IF ERRMSG # "" THEN
            GOSUB 91000
            GOTO 3000
         END
         SNAME = ECD.VALDAT.ITEM<2>
         PCNAME = ECD.RET.VALUE
      END CASE
      RETURN
*
*---- VIEW SCREEN PROBLEMS
*
10000*
      ECD.NUM = SCV.REC(12)<ECD.SCRN.NO,LN.NO>
      ECD.SCRN.NO = 2
      ECD.ACTION = 4
      CALL SCRN.EDIT
*---- RETURNED FROM THE PMSG
      IF PC.PORT.TYPE = "termulator" AND ECD.SCRN.PC<ECD.SCRN.NO> THEN
         ECD.ACTION = 99
         CALL SCRN.EDIT
      END
      ECD.SCRN.NO = 1
      ECD.ACTION = 3
      CALL SCRN.EDIT
      GOSUB 2500
      IF ERRMSG # "" THEN
         GOSUB 91000
         OPTION = "END"
      END ELSE
         LN = 1
         OLD.START.LINE = 0
         GOSUB 80000
      END
      RETURN
*
*---- ENTER LINE NUMBER
*
30000*
      GOSUB 80000
      ECD.NUM = 7
      ECD.MAXV = LAST.LINE
      ECD.MINV = START.LINE
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
      ECD.ACTION=4
      CALL SCRN.EDIT
      LN.NO = ECD.RET.VALUE
      IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
      RETURN
*
*---- SCROLL
*
80000*
      START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
      LAST.LINE = START.LINE + PAGE.SIZE - 1
      IF LAST.LINE > LINES THEN LAST.LINE = LINES
      IF START.LINE = OLD.START.LINE THEN RETURN
      OLD.START.LINE = START.LINE
      CNT = 1
      FOR N = START.LINE TO LAST.LINE
         SCV.REC(11)<ECD.SCRN.NO,N> = N
         ECD.NUM = 11
         ECD.SUB.NUM = N
         ECD.ACTION = 5
         CALL SCRN.EDIT
         ECD.NUM = 12
         ECD.SUB.NUM = N
         ECD.ACTION = 5
         CALL SCRN.EDIT
         ECD.NUM = 13
         ECD.SUB.NUM = N
         ECD.ACTION = 5
         CALL SCRN.EDIT
         ECD.NUM = 14
         ECD.SUB.NUM = N
         ECD.ACTION = 5
         CALL SCRN.EDIT
         CNT = CNT + 1
      NEXT N
      FOR M = CNT TO PAGE.SIZE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
         P_X  = 0
         P_Y = SLN
         P_VALUE = ""
         P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT M
*
*---- Page numbers
      ECD.NUM = 18
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = INT(LN / PAGE.SIZE + .9) "R%2"
      ECD.ACTION = 5
      CALL SCRN.EDIT
      ECD.NUM = 19
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = INT(LINES / PAGE.SIZE + .9) "R%2"
      ECD.ACTION = 5
      CALL SCRN.EDIT
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):CL:
      RETURN
*
*--- CALLS FOR SYSCOM
*
91000*
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000*
      ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
*
99999*
      CLEARSELECT
   END
