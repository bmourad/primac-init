     SUBROUTINE GET.MENU.NAMES (MASTER, MENUS, MENU.NAMES)
*
*****************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM - GET.MENU.NAMES
*
* DATE    - 05/23/84
*
* BY      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DESCRIPTION
*
* This program will produce a list of menu names in hierarchical order
* and print a table of contents.
*
*ENDDOC
**************************************************************************
*
*---- SYSTEM EQUATES
*
      EQU AM TO CHAR(254)
      EQU VM TO CHAR(253)
*
*---- INITIALIZATION
*
      DIM MENU(50)
      MENU.NAMES = ""
      MENU.LEVEL = ""
      PAGE.NO = 0
      DIM PG(10)
      MAT PG = ""
      PG(1) = "i"
      PG(2) = "ii"
      PG(3) = "iii"
      PG(4) = "iv"
      PG(5) = "v"
      PG(6) = "vi"
      PG(7) = "vii"
      PG(8) = "viii"
      PG(9) = "ix"
      PG(10) = "x"
*
*---- MAIN PROCESSING
*
      MATREAD MENU FROM MENUS, MASTER ELSE
         STOP "CANNOT LOCATE MENU - ":MASTER
      END
      LEVEL = 1
      MENU.LEVEL = LEVEL
      MENU.NAMES = MASTER
      MENU.DESCR = "Master Menu"
      MENU.LST = MASTER
      MENU.PTR = 1
      GOSUB 1000
      PRINTER ON
      GOSUB 5000                       ;* PRINT HEADINGS
      MENU.CNT = COUNT(MENU.NAMES,AM) + (MENU.NAMES # "")
      FOR M = 1 TO MENU.CNT
         IF MENU.LEVEL<M> = 2 THEN
            PRINT
            LINE.CNT = LINE.CNT + 1
         END
         IF LINE.CNT > 50 THEN
            GOSUB 6000
            GOSUB 5000
         END
         PLINE = SPACE((MENU.LEVEL<M>-1)*3):MENU.DESCR<M>
         PLINE = PLINE "L#60"
         PRINT SPACE(10):PLINE:M "R#3"
         LINE.CNT = LINE.CNT + 1
      NEXT M
      GOSUB 6000
      PRINTER OFF
      RETURN
*
*---- PROCESS MENUS
*
1000*
      MENU.PTR<1> = MENU.PTR<1> + 1
      IF MENU.PTR<1> > 33 THEN
         LEVEL = LEVEL - 1
         MENU.LST = DELETE(MENU.LST,1,0,0)
         MENU.PTR = DELETE(MENU.PTR,1,0,0)
         MENU.NAME = MENU.LST<1>
         IF MENU.NAME = "M.PRS" THEN MENU.NAME = "M.EPS"
         IF MENU.NAME # "" THEN
            MATREAD MENU FROM MENUS, MENU.NAME ELSE
               STOP "CANNOT RE-READ MENU - ":MENU.NAME
            END
         END
         RETURN
      END
      SEL = MENU(MENU.PTR<1>)<1,1>
      IF SEL = "M.PRS" THEN SEL = "M.EP4"
      SEL.DESC = MENU(MENU.PTR<1>)<1,3>
      BEGIN CASE
         CASE SEL[1,2] = "M."
            MENU.NAME = SEL
            MATREAD MENU FROM MENUS, MENU.NAME ELSE
               GOTO 1000
            END
            LEVEL = LEVEL + 1
            MENU.LEVEL<-1> = LEVEL
            MENU.NAMES<-1> = MENU.NAME
            MENU.DESCR<-1> = SEL.DESC
            MENU.LST = INSERT(MENU.LST,1,0,0,MENU.NAME)
            MENU.PTR = INSERT(MENU.PTR,1,0,0,1)
            GOSUB 1000
         CASE FIELD(SEL,".",2) = "LOGTO"
            MENU.NAME = "M.":FIELD(SEL,".",1)
            IF MENU.NAME = "M.PRS" THEN MENU.NAME = "M.EP4"
            MATREAD MENU FROM MENUS, MENU.NAME ELSE
               GOTO 1000
            END
            LEVEL = LEVEL + 1
            MENU.LEVEL<-1> = LEVEL
            MENU.NAMES<-1> = MENU.NAME
            MENU.DESCR<-1> = SEL.DESC
            MENU.LST = INSERT(MENU.LST,1,0,0,MENU.NAME)
            MENU.PTR = INSERT(MENU.PTR,1,0,0,1)
            GOSUB 1000
      END CASE
      GOTO 1000
*
*---- PRINT PAGE HEADINGS
*
5000*
      PRINT CHAR(12):
      PRINT
      PRINT
      PRINT
      PAGE.NO = PAGE.NO + 1
      IF PAGE.NO = 1 THEN
         PRINT SPACE(30):"Table of Contents"
      END ELSE
         PRINT SPACE(30):"Table of Contents (cont'd)"
      END
      PRINT
      PRINT
      PRINT
      LINE.CNT = 7
      RETURN
*
*---- PRINT PAGE TRAILER
*
6000*
      IF PAGE.NO > 0 THEN
         FOR L = LINE.CNT TO 55
            PRINT
         NEXT L
         PLINE = PG(PAGE.NO)
         PRINT SPACE(38):PG(PAGE.NO)
      END
      RETURN
   END
