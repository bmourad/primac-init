      SUBROUTINE BOX1.ASCII (MAT GRAPH.REC,MAT VIDEO.REC)
*
*COPY>CPYLIB>TCC
*COPY>CPYLIB>CHAR
*
*---- Initialization
*
      IF BOX.CLEAR = 9 THEN
         BOX.CNT = BOX.CNT - 1
         IF BOX.CNT < 0 THEN BOX.CNT = 0
         GOTO 99999
      END
      BEGIN CASE
      CASE BOX.COLS > 80
         BOX.COLS = 80
      CASE BOX.COLS < 0
         BOX.COLS = 0
      END CASE
      BEGIN CASE
      CASE BOX.X = "C"
         BOX.X = INT((80 - BOX.COLS) / 2)
      CASE BOX.X > 79
         BOX.X = 79
      CASE BOX.X < 0
         BOX.X = 0
      END CASE
      FRM.X = BOX.X - 1
      BEGIN CASE
      CASE BOX.ROWS > 24
         BOX.ROWS = 24
         BOX.HROW = 0
      CASE BOX.ROWS < 0
         BOX.ROWS = 0
         BOX.HROW = 0
      END CASE
      BEGIN CASE
      CASE BOX.Y = "C"
         BOX.Y = INT((24 - BOX.ROWS) / 2 - 0.5)
      CASE BOX.Y > 23
         BOX.Y = 23
      CASE BOX.Y < 0
         BOX.Y = 0
      END CASE
      IF BOX.HROW THEN
         IF BOX.Y + BOX.ROWS + 1 < 23 THEN
            BOX.HROW = BOX.Y + BOX.ROWS + 1
         END ELSE
            BOX.HROW = 0
         END
      END
      FRM.Y = BOX.Y - 1
      IF FRM.X < 0 THEN
         PRT.X = 0
         ADD.X = FRM.X
      END ELSE
         PRT.X = FRM.X
         ADD.X = 0
      END
      BEGIN CASE
      CASE PRT.X = 79
         BOX.COLS = 0
      CASE PRT.X + BOX.COLS > 80
         BOX.COLS = BOX.COLS - PRT.X - ADD.X
      END CASE
      LST.X = PRT.X + BOX.COLS + 1 + ADD.X
      IF LST.X > 80 THEN
         BOX.COLS = BOX.COLS - (LST.X - 80) - ADD.X
      END
      IF FRM.Y < 0 THEN
         PRT.Y = 0
         ADD.Y = FRM.Y
      END ELSE
         PRT.Y = FRM.Y
         ADD.Y = 0
      END
      BEGIN CASE
      CASE PRT.Y = 23
         BOX.ROWS = 0
      CASE PRT.Y + BOX.ROWS > 24
         BOX.ROWS = BOX.ROWS - PRT.Y - ADD.Y
      END CASE
      LST.Y = PRT.Y + BOX.ROWS + 1 + ADD.Y
      IF LST.Y > 24 THEN
         BOX.ROWS = BOX.ROWS - (LST.Y - 24) - ADD.Y
      END
      FST.SIDE = PRT.Y + ADD.Y + 1
      LST.SIDE = BOX.ROWS + PRT.Y + ADD.Y
*
*---- Turn Graphics ON
*
      PRINT @(PRT.X,PRT.Y):BOX.ON:
*
*---- No ETX.OFF is used when NOT(BOX.CLEAR). @(-1) used by calling program.
*
*---- Print top left corner, top line and top right corner
*
      IF BOX.CLEAR AND PRT.X > 0 THEN
         ETX.X = PRT.X - 1
         PRINT @(ETX.X,PRT.Y):ETX.OFF:
      END
      PRINT @(PRT.X,PRT.Y):
      BEGIN CASE
      CASE ADD.X = 0 AND ADD.Y = 0
         PRINT TOP.LEFT:
         PRINT STR(HORZ.CHAR,BOX.COLS):
         IF LST.X < 80 THEN
            PRINT TOP.RIGHT:
         END
      CASE ADD.Y = 0
         PRINT STR(HORZ.CHAR,BOX.COLS):
         IF LST.X < 80 THEN
            PRINT TOP.RIGHT:
         END
      END CASE
*
*---- Print sides
*
      BEGIN CASE
      CASE BOX.CLEAR < 1 AND LST.SIDE < 23
         BEGIN CASE
         CASE ADD.X = 0 AND LST.X < 80
            FOR SIDE = FST.SIDE TO LST.SIDE
               PRINT @(PRT.X,SIDE):VERT.CHAR:
               PRINT @(LST.X,SIDE):VERT.CHAR:
            NEXT SIDE
         CASE ADD.X = 0
            FOR SIDE = FST.SIDE TO LST.SIDE
               PRINT @(PRT.X,SIDE):VERT.CHAR:
            NEXT SIDE
         CASE LST.X < 80
            FOR SIDE = FST.SIDE TO LST.SIDE
               PRINT @(LST.X,SIDE):VERT.CHAR:
            NEXT SIDE
         END CASE
      CASE BOX.CLEAR < 1
         LST.SIDE = LST.SIDE - 1
         BEGIN CASE
         CASE ADD.X = 0 AND LST.X < 80
            FOR SIDE = FST.SIDE TO LST.SIDE
               PRINT @(PRT.X,SIDE):VERT.CHAR:
               PRINT @(LST.X,SIDE):VERT.CHAR:
            NEXT SIDE
         CASE ADD.X = 0
            FOR SIDE = FST.SIDE TO LST.SIDE
               PRINT @(PRT.X,SIDE):VERT.CHAR:
            NEXT SIDE
         CASE LST.X < 80
            FOR SIDE = FST.SIDE TO LST.SIDE
               PRINT @(LST.X,SIDE):VERT.CHAR:
            NEXT SIDE
         END CASE
         LST.SIDE = LST.SIDE + 1
         IF PRT.X < 79 THEN
            IF ADD.X = 0 THEN
               PRINT @(PRT.X,LST.SIDE):VERT.CHAR:
            END
            IF LST.X < 79 THEN
               PRINT @(LST.X,LST.SIDE):VERT.CHAR:
            END
         END
      CASE PRT.X > 0 AND LST.SIDE < 23
         FOR SIDE = FST.SIDE TO LST.SIDE
            PRINT @(ETX.X,SIDE):ETX.OFF:
            PRINT @(PRT.X,SIDE):
            IF ADD.X = 0 THEN
               PRINT VERT.CHAR:
            END
            PRINT SPACE(BOX.COLS):
            IF LST.X < 80 THEN
               PRINT VERT.CHAR:
            END
         NEXT SIDE
      CASE PRT.X > 0
         LST.SIDE = LST.SIDE - 1
         FOR SIDE = FST.SIDE TO LST.SIDE
            PRINT @(ETX.X,SIDE):ETX.OFF:
            PRINT @(PRT.X,SIDE):
            IF ADD.X = 0 THEN
               PRINT VERT.CHAR:
            END
            PRINT SPACE(BOX.COLS):
            IF LST.X < 80 THEN
               PRINT VERT.CHAR:
            END
         NEXT SIDE
         LST.SIDE = LST.SIDE + 1
         IF PRT.X < 79 THEN
            PRINT @(ETX.X,LST.SIDE):ETX.OFF:
            PRINT @(PRT.X,LST.SIDE):
            IF ADD.X = 0 THEN
               PRINT VERT.CHAR:
            END
            IF LST.X < 79 THEN
               PRINT SPACE(BOX.COLS):
               PRINT VERT.CHAR:
            END ELSE
               PRINT SPACE(BOX.COLS-1):
            END
         END
      CASE LST.SIDE < 23
         FOR SIDE = FST.SIDE TO LST.SIDE
            PRINT @(PRT.X,SIDE):
            IF ADD.X = 0 THEN
               PRINT VERT.CHAR:
            END
            PRINT SPACE(BOX.COLS):
            IF LST.X < 80 THEN
               PRINT VERT.CHAR:
            END
         NEXT SIDE
      CASE 1
         LST.SIDE = LST.SIDE - 1
         FOR SIDE = FST.SIDE TO LST.SIDE
            PRINT @(PRT.X,SIDE):
            IF ADD.X = 0 THEN
               PRINT VERT.CHAR:
            END
            PRINT SPACE(BOX.COLS):
            IF LST.X < 80 THEN
               PRINT VERT.CHAR:
            END
         NEXT SIDE
         LST.SIDE = LST.SIDE + 1
         IF PRT.X < 79 THEN
            PRINT @(PRT.X,LST.SIDE):
            IF ADD.X = 0 THEN
               PRINT VERT.CHAR:
            END
            IF LST.X < 79 THEN
               PRINT SPACE(BOX.COLS):
               PRINT VERT.CHAR:
            END ELSE
               PRINT SPACE(BOX.COLS-1):
            END
         END
      END CASE
*
*---- Print bottom left corner, bottom line, and bottom right corner
*
      IF BOX.CLEAR AND PRT.X > 0 THEN
         PRINT @(ETX.X,LST.Y):ETX.OFF:
      END
      BEGIN CASE
      CASE LST.Y < 23
         PRINT @(PRT.X,LST.Y):
         IF ADD.X = 0 THEN
            PRINT BOT.LEFT:
         END
         PRINT STR(HORZ.CHAR,BOX.COLS):
         IF LST.X < 80 THEN
            PRINT BOT.RIGHT:
         END
      CASE LST.Y = 23 AND PRT.X < 79
         PRINT @(PRT.X,LST.Y):
         IF ADD.X = 0 THEN
            PRINT BOT.LEFT:
         END
         IF LST.X < 79 THEN
            PRINT STR(HORZ.CHAR,BOX.COLS):
            PRINT BOT.RIGHT:
         END ELSE
            PRINT STR(HORZ.CHAR,BOX.COLS-1):
         END
      END CASE
*
*---- Turn Graphics OFF
*
      PRINT BOX.OFF:
      BOX.CNT = BOX.CNT + 1
99999*
      BOX.CLEAR = 0
      RETURN
      END
