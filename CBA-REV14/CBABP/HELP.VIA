      SUBROUTINE HELP.VIA
*
*COPY>CPYLIB>COMMON3
*COPY>CPYLIB>TCC
*COPY>CPYLIB>KSEL
*COPY>CPYLIB>CHAR
*
      DIM SREC(3)
      EQU A.S TO SREC(1)
      EQU V.S TO SREC(2)
      EQU S.S TO SREC(3)
*
      S.BOX.X = BOX.X
      S.BOX.Y = BOX.Y
      S.BOX.COLS = BOX.COLS
      S.BOX.ROWS = BOX.ROWS
      S.BOX.SAVE = BOX.SAVE
      S.BOX.HROW = BOX.HROW
      S.BOX.CLEAR = BOX.CLEAR
      S.BOX.FRAME = BOX.FRAME
      S.BOX.WCOLOR = BOX.WCOLOR
*
*---- Check Help Message
*
      IF HBOX.MSG = "&OPENFILE&" THEN
         OPEN "", HBOX.FILE TO HBOX.FILE ELSE
            HBOX.ID = ""
         END
         HBOX.MSG = ""
      END
      BEGIN CASE
      CASE HBOX.ID # ""
         READ HBOX.MSG FROM HBOX.FILE, HBOX.ID ELSE
            HBOX.MSG = "No Help Message For This Selection"
         END
         HBOX.ID = ""
      CASE HBOX.MSG = ""
         HBOX.MSG = "No Help Message For This Selection"
      END CASE
*
*---- Verify if mouse is on
*
      MAT KSL.REC = ""
      VC = BOX.ON:VERT.CHAR:BOX.OFF
      KSL.HMSG = " Use - = , ESC "
      IF MOUSE.BUTTONS THEN
         PCCMD = CHAR(27):CHAR(8); ENDCMD = CHAR(0) ; ***** Unidata may not support CHAR(0) *****
         MOUSEON = PCCMD:"MOUSE /O-/L-/P-/R-/C-/D+/I64 ON ":ENDCMD
         MOUSEOFF = PCCMD:"MOUSE OFF ":ENDCMD
         KSL.HMSG = KSL.HMSG:VC:"Home":VC:"PgUp":VC:"PgDn":VC:"End":VC:"Esc":VC
      END
*
      BOX.X = "C"
      BOX.Y = "C"
      BOX.SAVE = 1
      BOX.FRAME = 3
      BOX.WCOLOR = BOX.HCOLOR
      BOX.COLS = 43; BOX.ROWS = 20
      BEGIN CASE
      CASE HBOX.SEP = "SM"
         HLINES = COUNT(HBOX.MSG,SVM) + 1
         HLN = 3; A.S = 1; V.S = 1
      CASE HBOX.SEP = "VM"
         HLINES = COUNT(HBOX.MSG,VM) + 1
         HLN = 2; A.S = 1; S.S = 0
      CASE 1
         HLINES = COUNT(HBOX.MSG,AM) + 1
         HLN = 1; V.S = 0; S.S = 0
      END CASE
      FOR SREC(HLN) = 1 TO HLINES
         L = LEN(HBOX.MSG<A.S,V.S,S.S>)
         IF L > BOX.COLS THEN
            BOX.COLS = L
         END
      NEXT SREC(HLN)
      IF BOX.COLS > 76 THEN
         BOX.COLS = 76
      END
      BOX.COLS = BOX.COLS + 2
      LFMT = "L#":BOX.COLS
      IF HLINES + 2 < BOX.ROWS THEN
         BOX.ROWS = HLINES + 2
      END
      PAGE.SIZE = BOX.ROWS - 2
      KSL.PAGES = INT(HLINES / PAGE.SIZE + .99)
      IF KSL.PAGES > 1 THEN
         BOX.HROW = 1
         CALL @GRAPH.SUB
         IF BOX.HROW THEN
            BOX.HROW = BOX.HROW - 1
         END
      END ELSE
         BOX.HROW = 0
         BOX.ROWS = BOX.ROWS + 1
         CALL @GRAPH.SUB
         BOX.ROWS = BOX.ROWS - 1
      END
      BEGIN.PAGE = BOX.Y + 1
      KSL.PGNO = 1
      GOSUB 1900
      PRINT @(1,BOX.ROWS):ETX.HHD.OFF:KSL.HMSG:ETX.HHD.OFF:
      PRINT @(1,BOX.ROWS):ETX.HHD:
*
      MORE = 1
      IF MOUSE.BUTTONS THEN PRINT MOUSEON:
      LOOP
         PRINT @(BOX.X,BOX.ROWS):
         ECHO OFF
         INPUT KSL.SEL,1:
         ECHO ON
110*
         BEGIN CASE
         CASE KSL.SEL = KSEL.ESC
            MORE = 0
         CASE KSL.SEL = KSEL.SOH
            IF KSL.PGNO > 1 THEN
               KSL.PGNO = 1
               GOSUB 1900
            END
         CASE KSL.SEL = KSEL.ETX
            IF KSL.PGNO < KSL.PAGES THEN
               KSL.PGNO = KSL.PAGES
               GOSUB 1900
            END
         CASE KSL.SEL = KSEL.PUP
            IF KSL.PGNO > 1 THEN
               KSL.PGNO = KSL.PGNO - 1
               GOSUB 1900
            END
         CASE KSL.SEL = KSEL.PDN
            IF KSL.PGNO < KSL.PAGES THEN
               KSL.PGNO = KSL.PGNO + 1
               GOSUB 1900
            END
         CASE MOUSE.BUTTONS AND KSL.SEL = "@"
            ECHO OFF
            INPUT MOUSE.SEL:
            ECHO ON
            M.ACTION = FIELD(MOUSE.SEL,' ',1)
* Bouble Click Action
            IF M.ACTION = 6 OR M.ACTION = 5 THEN
               BUTTON = FIELD(MOUSE.SEL,' ',2)
               BUTTON = BUTTON-INT(BUTTON/4)*4
               ROW = FIELD(MOUSE.SEL,' ',4)
               COL = FIELD(MOUSE.SEL,' ',3)
* Check for the left Button
               BEGIN CASE
               CASE BUTTON # 1
               CASE ROW # BOX.ROWS
               CASE COL > 17 AND COL < 22
                  KSL.SEL = KSEL.SOH
                  GOTO 110
               CASE COL > 22 AND COL < 27
                  KSL.SEL = KSEL.PUP
                  GOTO 110
               CASE COL > 27 AND COL < 32
                  KSL.SEL = KSEL.PDN
                  GOTO 110
               CASE COL > 32 AND COL < 37
                  KSL.SEL = KSEL.ETX
                  GOTO 110
               CASE COL > 37 AND COL < 41
                  KSL.SEL = KSEL.ESC
                  GOTO 110
               END CASE
            END
         END CASE
      WHILE MORE DO REPEAT
      IF MOUSE.BUTTONS THEN PRINT MOUSEOFF:
*
      BOX.CLEAR = 9
      CALL @GRAPH.SUB
      BOX.X = S.BOX.X
      BOX.Y = S.BOX.Y
      BOX.COLS = S.BOX.COLS
      BOX.ROWS = S.BOX.ROWS
      BOX.SAVE = S.BOX.SAVE
      BOX.HROW = S.BOX.HROW
      BOX.CLEAR = S.BOX.CLEAR
      BOX.FRAME = S.BOX.FRAME
      BOX.WCOLOR = S.BOX.WCOLOR
      GOTO 99999
1900*
      START.LINE = ((KSL.PGNO - 1) * PAGE.SIZE) + 1
      LAST.LINE = START.LINE + PAGE.SIZE - 1
      IF LAST.LINE > HLINES THEN LAST.LINE = HLINES
      SLN = BEGIN.PAGE - 1
      FOR SREC(HLN) = START.LINE TO LAST.LINE
         SLN = SLN + 1
         PRINT @(BOX.X,SLN):HBOX.MSG<A.S,V.S,S.S> LFMT:
      NEXT SREC(HLN)
      LST.V = LAST.LINE - START.LINE + 2
      FOR V = LST.V TO PAGE.SIZE
         SLN = SLN + 1
         PRINT @(BOX.X,SLN):CL:
      NEXT V
*
*---- Display lines & pages
*
      BEGIN CASE
      CASE BOX.HROW < 1
      CASE BOX.COLS < 10
      CASE BOX.COLS < 15
         PX = BOX.X + BOX.COLS - 12
         PRINT @(PX,BOX.HROW):ETX.HHD.OFF:KSL.PGNO "R%2":" of ":KSL.PAGES "R%2":ETX.HHD.OFF:
         PRINT @(PX,BOX.HROW):ETX.HHD:
      CASE 1
         PX = BOX.X + BOX.COLS - 17
         PRINT @(PX,BOX.HROW):ETX.HHD.OFF:"Page ":KSL.PGNO "R%2":" of ":KSL.PAGES "R%2":ETX.HHD.OFF:
         PRINT @(PX,BOX.HROW):ETX.HHD:
      END CASE
      RETURN
99999*
      RETURN
   END
