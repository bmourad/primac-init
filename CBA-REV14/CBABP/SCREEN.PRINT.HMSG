*COPY>CPYLIB>COMMON3
*********************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM - SCREEN.PRINT.HMSG
*
* BY      - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 12/18/85
*
* DESCRIPTION
* This program prints a hard copy of the screen and field help
* messages (optional).
*
*********************************************************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>NEW.BUILD.SCREEN
*COPY>CPYLIB>CHAR
*
*---- DEFINE VARIABLES
*
      DIM FORM(24)
      DIM XREF(24,3)
*
*---- OPEN ALL FILES
*
      OPEN "","VOC" TO MD ELSE
         PRINT "CANNOT OPEN VOC FILE"
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         PRINT "CANNOT OPEN CONTROL FILE"
         STOP
      END
*
*---- INITIALIZATION
*
      PROMPT ""
      COLNOS1 = "          1         2         3         4         5         6         7         "
      COLNOS2 = "01234567890123456789012345678901234567890123456789012345678901234567890123456789"
      FIRST.COL = 0
      LAST.COL = 79
      FIRST.ROW = 0
      LAST.ROW = 22
      WIDTH = LAST.COL - FIRST.COL + 1
      FLD.PER.PAGE = 4
      TYP = 0
      CALL EDIT.SUB
      FILL = "#"
      SPACE.WIDTH = SPACE(WIDTH)
      SPACE.LM = SPACE(20)
      STARS = STR("*",132)
*
*---- MAIN PROCESSING
*
100*
      MAT FORM = SPACE.WIDTH
      SCRN = CS
      SCRN = SCRN:@(00,00):"C.B.A."
      SCRN = SCRN:@(26,00):"S C R E E N   P R I N T E R"
      SCRN = SCRN:@(72,00):OCONV(DATE(),"D2/")
      SCRN = SCRN:@(00,01):STR("-",WIDTH)
      SCRN = SCRN:@(00,22):STR("-",WIDTH)
      PRINT SCRN:
110*
      X = 10; Y = 9
      PMSG = CL:"Enter Screen File Name "
      MINL = 1; MAXL = 25
      O.R = "O"; DEFAULT = "M.SCREENS"
      CALL EDIT.SUB
      IF VALUE = "END" THEN
         PRINT @(-1)
         GOTO 99999
      END
      READ ITEM FROM MD, VALUE ELSE
         GOTO 110
      END
      IF ITEM<1> # "F" THEN
         GOTO 110
      END
      OPEN "", VALUE TO M.SCREENS ELSE
         GOTO 110
      END
120*
      X = 10; Y = 11
      PMSG = CL:'Enter Screen Name or "ALL"'
      MINL = 1; MAXL = 25
      CALL EDIT.SUB
      SCREEN.NAME = VALUE
      IF SCREEN.NAME = "END" THEN
         PRINT @(-1)
         GOTO 99999
      END
125*
      IF SCREEN.NAME = "ALL" THEN
         X = 10 ; Y = 13
         PMSG = CL:'Enter Sub-system or "ALL"'
         MINL = 1 ; MAXL = 25
         CALL EDIT.SUB
         SYSTEM.NAME = VALUE
         IF SYSTEM.NAME = "END" THEN
            PRINT @(-1)
            GOTO 99999
         END
      END ELSE
         SYSTEM.NAME = ""
      END
*
      BEGIN CASE
      CASE SCREEN.NAME = "ALL" AND SYSTEM.NAME = "ALL"
         X = 10; Y = 13
         PMSG = CL:"Print Field Definition (Y or N) :"
         TYP = 8
         CALL EDIT.SUB
         DATA.DEF.FLAG = VALUE
         PRINT
         PRINT "Selecting all Screens"
         SCREEN.NAMES = ""
         SCREEN.CNT = 0
         SELECT M.SCREENS
200*
         READNEXT SCREEN.ID ELSE GOTO 300
         IF INDEX(SCREEN.ID,"*FRM",1) = 0 THEN GOTO 200
         SN = FIELD(SCREEN.ID,"*",1)
         LOCATE SN IN SCREEN.NAMES<1>,1 BY "AL" SETTING P ELSE NULL
         SCREEN.NAMES = INSERT(SCREEN.NAMES,1,P,0,SN)
         SCREEN.CNT = SCREEN.CNT + 1
         GOTO 200
300*
         PRINT SCREEN.CNT:" Screens Selected"
         PRINT "Begin Processing  ":
         FOR S = 1 TO SCREEN.CNT
            SCREEN.NAME = SCREEN.NAMES<1,S>
            MATREAD BLD.SCRN FROM M.SCREENS, SCREEN.NAME:"*FLD" ELSE
               MAT BLD.SCRN = ""
            END
            READ BLD.COMP.SCRN FROM M.SCREENS, SCREEN.NAME:"*FRM" ELSE GOTO 390
            PRINT "*":
            READ BLD.DEF.SCRN FROM M.SCREENS, SCREEN.NAME:"*DEF" THEN
               GOSUB 6100
            END ELSE
               GOSUB 6000
            END
            GOSUB 7000
            GOSUB 2000
            IF DATA.DEF.FLAG = "Y" THEN
               GOSUB 7500
               GOSUB 8000
            END
390*
         NEXT S
         PRINT @(-1)
         GOTO 99999
      CASE SCREEN.NAME = "ALL" AND SYSTEM.NAME # "ALL"
         X = 10; Y = 13
         PMSG = CL:"Print Field Definition (Y or N) :"
         TYP = 8
         CALL EDIT.SUB
         DATA.DEF.FLAG = VALUE
         SYSTEM.NAME = SYSTEM.NAME:"BP"
         OPEN "",SYSTEM.NAME TO SYSTEM.NAME ELSE
            PRINT @(0,23):"INVALID SYSTEM ID":;INPUT X:
            PRINT @(0,23):CL:; GOTO 125
         END
         SELECT SYSTEM.NAME
         SCREEN.CNT = 0 ; SCREEN.NAMES = ""
         GOSUB 10000
         PRINT SCREEN.CNT:" Screens Selected"
         PRINT "Begin Processing  ":
         FOR S = 1 TO SCREEN.CNT
            SCREEN.NAME = SCREEN.NAMES<1,S>
            MATREAD BLD.SCRN FROM M.SCREENS, SCREEN.NAME:"*FLD" ELSE
               MAT BLD.SCRN = ""
            END
            READ BLD.COMP.SCRN FROM M.SCREENS, SCREEN.NAME:"*FRM" ELSE GOTO 490
            PRINT "*":
            READ BLD.DEF.SCRN FROM M.SCREENS, SCREEN.NAME:"*DEF" THEN
               GOSUB 6100
            END ELSE
               GOSUB 6000
            END
            GOSUB 7000
            GOSUB 2000
            IF DATA.DEF.FLAG = "Y" THEN
               GOSUB 7500
               GOSUB 8000
            END
490*
         NEXT S
         PRINT @(-1)
         GOTO 99999
      CASE 1
         MATREAD BLD.SCRN FROM M.SCREENS, SCREEN.NAME:"*FLD" ELSE
            MAT BLD.SCRN = ""
         END
         READ BLD.COMP.SCRN FROM M.SCREENS, SCREEN.NAME:"*FRM" ELSE
            ERRMSG = "INVALID SCREEN NAME"
            PRINT @(0,23):CL:ERRMSG:
            PRINT @(0,23):CL:
            GOTO 120
         END
         X = 10; Y = 13
         PMSG = CL:"Print Field Definition (Y or N) :"
         TYP = 8
         CALL EDIT.SUB
         DATA.DEF.FLAG = VALUE
         READ BLD.DEF.SCRN FROM M.SCREENS, SCREEN.NAME:"*DEF" THEN
            GOSUB 6100
         END ELSE
            GOSUB 6000
         END
         GOSUB 7000
         GOSUB 2000
         IF DATA.DEF.FLAG = "Y" THEN
            GOSUB 7500
            GOSUB 8000
         END
      END CASE
      GOTO 100
*
*--- PRINT SCREEN IMAGE
*
2000*
      PRINTER ON
      FIRST.PAGE = 1
      GOSUB 9000
      PLINE = COLNOS1
      PRINT SPACE.LM:SPACE(5):PLINE
      PLINE = COLNOS2
      PRINT SPACE.LM:SPACE(5):PLINE
      PLINE = "+":STR("-",82):"+"
      PRINT SPACE.LM:SPACE(3):PLINE
      FOR ROW = 0 TO 23
         PRINT SPACE.LM:ROW "R#2":" | ":FORM(ROW+1):" | ":ROW "R#2"
      NEXT ROW
      PLINE = "+":STR("-",82):"+"
      PRINT SPACE.LM:SPACE(3):PLINE
      PLINE = COLNOS1
      PRINT SPACE.LM:SPACE(5):PLINE
      PLINE = COLNOS2
      PRINT SPACE.LM:SPACE(5):PLINE
      PRINTER OFF
      RETURN
*
*---- DECOMPRESSION ROUTINE
*
6000*
      MAT FORM = SPACE.WIDTH
      COL = 0
      ROW = 0
      SLEN = LEN(BLD.COMP.SCRN)
      P = 1
      LOOP UNTIL P > SLEN DO
         CHR = BLD.COMP.SCRN[P,1]
         BEGIN CASE
         CASE CHR = CS
            P = P + 1
         CASE CHR = CHAR(16)        ;* ADDRESS COLUMN
            ACOL = SEQ(BLD.COMP.SCRN[P+1,1])
            BEGIN CASE
            CASE SEQ(BLD.COMP.SCRN[P+2,1]) = 8
               COL = INT(ACOL/16) * 10 + MOD(ACOL,16) - 1
               P = P + 3
            CASE 1
               COL = INT(ACOL/16) * 10 + MOD(ACOL,16)
               P = P + 2
            END CASE
         CASE CHR = CHAR(11)        ;* ADDRESS ROW
            AROW = SEQ(BLD.COMP.SCRN[P+1,1])
            ROW = AROW - 64
            P = P + 2
         CASE COL = 0
            FORM(ROW+1) = CHR:FORM(ROW+1)[2,WIDTH-1]
            P = P + 1
            COL = COL + 1
            IF COL > LAST.COL THEN
               COL = FIRST.COL
               ROW = ROW + 1
            END
         CASE 1
            FORM(ROW+1) = FORM(ROW+1)[1,COL]:CHR:FORM(ROW+1)[COL+2,WIDTH-COL-1]
            P = P + 1
            COL = COL + 1
            IF COL > LAST.COL THEN
               COL = FIRST.COL
               ROW = ROW + 1
            END
         END CASE
      REPEAT
      RETURN
*
6100*
      MAT FORM = SPACE.WIDTH
      DPTR = DCOUNT(BLD.DEF.SCRN,AM)
      FOR DEF = 1 TO DPTR
         Y = BLD.DEF.SCRN<DEF,1> + 1
         X = BLD.DEF.SCRN<DEF,2>
         STMT = BLD.DEF.SCRN<DEF,3>
         L = LEN(STMT)
         FORM(Y) = FORM(Y)[1,X]:STMT:FORM(Y)[X+L+1,WIDTH-X-1]
      NEXT DEF
      RETURN
*
*---- DISPLAY DATA FIELDS
*
7000*
      MAT XREF = ""
      PMSG.PTR = 0
      FOR FLD = 1 TO BLD.FIELD.CNT
         FX = BLD.SCRN(FLD)<1,B.X>
         IF FX = "" THEN GOTO 7090
         FY = BLD.SCRN(FLD)<1,B.Y>
         IF FY > LAST.ROW THEN GOTO 7090
         ML = BLD.SCRN(FLD)<1,B.MULTI.LN>
         IF ML = "" THEN ML = 1
         LS = BLD.SCRN(FLD)<1,B.MULTI.SP>
         IF LS = "" THEN LS = 1
         IF BLD.SCRN(FLD)<1,B.MASK> = "" THEN
            DMASK = "#"
         END ELSE
            DMASK = BLD.SCRN(FLD)<1,B.MASK>
         END
         PTYP = BLD.SCRN(FLD)<1,B.TYP>
         MAXL = BLD.SCRN(FLD)<1,B.MAXL>
         BEGIN CASE
         CASE PTYP = 6
            PMASK = STR(DMASK,2):"/":STR(DMASK,2):"/":STR(DMASK,2)
            PMASK = PMASK[1,MAXL]
         CASE PTYP = 4
            PSCALER = BLD.SCRN(FLD)<1,B.SCALER>
            PMASK = STR(DMASK,MAXL-PSCALER-1):".":STR(DMASK,PSCALER)
         CASE 1
            PMASK = STR(DMASK,MAXL)
         END CASE
         IF BLD.SCRN(FLD)<1,B.PMSG> # "" THEN
            IF PMSG.PTR = 0 THEN
               PMSG.PTR = 1
               PMASK = BLD.SCRN(FLD)<1,B.PMSG>:" ":PMASK
               MAXL = LEN(PMASK)
            END ELSE
               PMASK = ""
               MAXL = 0
            END
         END
         FOR L = 1 TO ML
            BEGIN CASE
            CASE FX = 0
               FORM(FY+(L-1)*LS+1) = PMASK:FORM(FY+(L-1)*LS+1)[MAXL+1,WIDTH-MAXL]
            CASE FX + MAXL = WIDTH
               FORM(FY+(L-1)*LS+1) = FORM(FY+(L-1)*LS+1)[1,FX]:PMASK
            CASE 1
               FORM(FY+(L-1)*LS+1) = FORM(FY+(L-1)*LS+1)[1,FX]:PMASK:FORM(FY+(L-1)*LS+1)[FX+MAXL+1,WIDTH-FX-MAXL]
            END CASE
         NEXT L
         XREF(FY+1,1) = FY
         XREF(FY+1,2)<1,-1> = FX
         XREF(FY+1,3)<1,-1> = BLD.EXT.REF<1,FLD>
7090*
      NEXT FLD
      RETURN
*
*---- RETRIEVE FIELD SPECIFICS
*
7500*
      IF DATA.DEF.FLAG = "Y" THEN
         FOR FLD = 1 TO BLD.FIELD.CNT
            EXT.REF = BLD.EXT.REF<1,FLD>
            READ FIELD.NAME FROM M.SCREENS, SCREEN.NAME:"*NAME":EXT.REF ELSE
               FIELD.NAME = ""
            END
            IF FIELD.NAME # "" THEN BLD.FIELD.NAME<1,FLD> = FIELD.NAME
            READ HELP.MSG FROM M.SCREENS, SCREEN.NAME:"*HMSG":EXT.REF ELSE
               HELP.MSG = ""
            END
            IF HELP.MSG # "" THEN BLD.HMSG<1,FLD> = HELP.MSG
         NEXT FLD
      END
      RETURN
*
*---- PRINT FIELDS
*
8000*
      PRINTER ON
      FIRST.PAGE = 0
      PG = 1
      LINE.CNT = 99
      FOR FLD = 1 TO BLD.FIELD.CNT
         IF LINE.CNT > 55 THEN GOSUB 9000           ;* PRINT HEADINGS
         PLINE = ""
         PLINE = PLINE:BLD.SCRN(FLD)<1,B.Y> "R#2":SPACE(3)
         PLINE = PLINE:BLD.SCRN(FLD)<1,B.X> "R#2":SPACE(3)
         PLINE = PLINE:BLD.FIELD.NAME<1,FLD> "L#20":SPACE(3)
         IF BLD.SCRN(FLD)<1,B.PMSG> # "" THEN
            PLINE = PLINE:BLD.SCRN(FLD)<1,B.PMSG> "L#72":SPACE(3)
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
            PLINE = SPACE(33)
         END
         IF TRIM(PLINE) # "" OR BLD.HMSG<1,FLD,1> # "" THEN
            PLINE = PLINE:BLD.HMSG<1,FLD,1> "L#72"
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
         END
         IF BLD.HMSG<1,FLD,2> # "" THEN
            PLINE = SPACE(33)
            PLINE = PLINE:BLD.HMSG<1,FLD,2> "L#72"
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
         END
         IF BLD.HMSG<1,FLD,3> # "" THEN
            PLINE = SPACE(33)
            PLINE = PLINE:BLD.HMSG<1,FLD,3> "L#72"
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
         END
         PRINT
         LINE.CNT = LINE.CNT + 1
      NEXT FLD
      PRINTER OFF
      RETURN
*
*---- PRINT PAGE HEADINGS
*
9000*
      PRINT CHAR(12)
      HLINE = ""
      HLINE = HLINE:SPACE(51):"Computer Business Associates":SPACE(15)
      HLINE = HLINE:"Run Date : ":OCONV(DATE(), "D2/"):SPACE(10)
      IF FIRST.PAGE = 1 THEN
         HLINE = HLINE:"Page : ":FIRST.PAGE
      END ELSE
         HLINE = HLINE:"Page : ":PG + 1
         PG = PG + 1
      END
      PRINT HLINE
      HLINE = ""
*     HLINE = HLINE:SPACE(2):SCREEN.NAME "L#23":SPACE(32)
      HLINE = HLINE:SPACE(57)
      IF FIRST.PAGE = 1 THEN
         HLINE = HLINE:SPACE(2):"Screen Layout"
      END ELSE
         HLINE = HLINE:"Field Definition"
      END
      PRINT HLINE
      PRINT
      LINE.CNT = 4
      IF FIRST.PAGE = 0 THEN
         HLINE = ""
         HLINE = HLINE:"ROW":SPACE(2)
         HLINE = HLINE:"COL":SPACE(2)
         HLINE = HLINE:"     FIELD NAME" "L#20":SPACE(3)
         HLINE = HLINE:SPACE(31):"DEFINITION"
         PRINT HLINE
         HLINE = ""
         HLINE = HLINE:"---  ---  --------------------   "
         HLINE = HLINE:STR("-",72)
         PRINT HLINE
         LINE.CNT = LINE.CNT + 2
      END
      RETURN
*
*---- GET ALL SCREEN NAMES FOR SPECIFIED SUB-SYSTEM
*
10000*
      DATA = 0
      SF = "ECD.SCRN.NAME"
      SP = "CALL SCRN.EDIT"
      SO = "ECD.SCRN.NO"
      LOOP
         READNEXT PROG.ID ELSE DATA = 1
      WHILE DATA = 0 DO
         READ SYS.REC FROM SYSTEM.NAME , PROG.ID ELSE GOTO 11111
         LINE.CNT = COUNT(SYS.REC, AM) + (SYS.REC # "")
         FND = 0
         FOR LL = 1 TO LINE.CNT UNTIL FND
            P.LINE = SYS.REC<LL>
            FF = INDEX(P.LINE,SP,1)
            IF FF THEN
               FND = 1 ; GOTO 11111
            END
            F = INDEX(P.LINE,SF,1)
            IF F THEN
               SCRN.NO = COUNT(P.LINE, ";") + (P.LINE # "")
               FOR FST = 1 TO SCRN.NO
                  SS.NAME = ""
                  TR.LINE = FIELD(P.LINE,";",FST)
                  FN = INDEX(TR.LINE,SO,1)
                  IF FN THEN
                     FND = 1 ; GOTO 10111
                  END
                  TR.LINE = TRIM(TR.LINE) ; TR.LINE = FIELD(TR.LINE,"=",2)
                  TR.LINE = TRIM(TR.LINE) ; LINE.LEN = LEN(TR.LINE)
                  FOR TR = 1 TO LINE.LEN UNTIL SS.NAME # ""
                     IF TR.LINE[TR,1] = "'" OR TR.LINE[TR,1] = '"' THEN
                        ST = TR + 1  ; EN = LEN(TR.LINE) - 2
                        SS.NAME = TR.LINE[ST,EN]
                     END
                  NEXT TR
                  IF SS.NAME # "" THEN
                     S.NAME = TRIM(SS.NAME)
                     LOCATE S.NAME IN SCREEN.NAMES<1>,1 SETTING SSS ELSE SSS = 0
                     IF SSS = 0 THEN
                        SCREEN.CNT = SCREEN.CNT + 1
                        SCREEN.NAMES<1,SCREEN.CNT> = S.NAME
                     END
                  END
10111          NEXT FST
            END
         NEXT LL
11111 REPEAT
      RETURN
*
*---- ERROR PROCESS
*
95000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):CL:
      RETURN
*
*---- END OF PROGRAM
*
99999*
   END
