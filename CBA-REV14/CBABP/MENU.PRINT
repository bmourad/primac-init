************************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
*   PROGRAM    - MENU.PRINT
*
*   DATE       - 05/11/84
*
*   BY         - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
*   DESCRIPTION
*
*   This program will produce a hard copy of a specific menu or "ALL"
*   menus from the specified file. A complete table of contents listing is
*   also created. The menu documentation may optionally be printed along
*   with the associated menu.
*
************************************************************************
*
*COPY>PMC.CPYLIB>MENUS.CONTROL
*COPY>CPYLIB>CHAR
*
*---- VARIABLES
*
      DIM MENU(40)
      MAT MENU = ""
*      DIM PREC(24)
      DIM PREC(32)
      MAT PREC = ""
      DIM XREC(32)
      MAT XREC = ""
*      MAX.LINES = 55
      MAX.LINES = 44
*     MAX.LINES = 38    ;* FOR PRINTING ON HP LASER IN LANDSCAPE MODE
*
*---- OPEN ALL FILES
*
      OPEN "","CONTROL" TO CONTROL ELSE
         STOP "CANNOT OPEN CONTROL FILE"
      END
      OPEN "","EASY.DOC" TO DOC ELSE
         STOP "CANNOT OPEN EASY.DOC FILE"
      END
*
*---- INITIALIZATION
*
      PROMPT ""
      PROMPT ""
      DASHES = STR("-",80)
      ODATE = OCONV(DATE(),"D2/")
      OTIME = OCONV(TIME(),"MTS")
      PAGE.NO = 0
      LINE.CNT = 0
      MATREAD MENU.REC FROM CONTROL, "MENUS.CONTROL" ELSE
         MAT MENU.REC = ""
      END
      SNP = INT((80 - LEN(SYSTEM.NAME)) / 2)
*
*---- MAIN PROCESSING
*
100*
      PRINT @(-1):
      PRINT @(0,0):"C.B.A.":
      PRINT @(30,0):"PRINT SPECIFIED MENUS":
      PRINT @(72,0):OCONV(DATE(),"D2/"):
      PRINT @(0,1):STR("-",80):
      PRINT @(0,22):STR("-",80):
102*
      PRINT @(20,8):CL:"Enter File Name - ":
      INPUT FILENAME
      PRINT @(0,23):CL:
      IF FILENAME = "" OR FILENAME = "END" THEN GOTO 99999
      OPEN "",FILENAME TO MENUS ELSE
         PRINT @(0,23):"Invalid File Name":
         GOTO 102
      END
104*
      PRINT @(20,10):CL:'Enter Menu Name or "ALL" - ':
      INPUT MENU.NAME
      PRINT @(0,23):CL:
      IF MENU.NAME = "" OR MENU.NAME = "END" THEN GOTO 100
106*
      PRINT @(20,12):CL:'Print documentation (Y/N) - ':
      INPUT DOC.FLAG
      PRINT @(0,23):CL:
      IF DOC.FLAG = "END" THEN GOTO 100
      IF DOC.FLAG # "Y" AND DOC.FLAG # "N" THEN
         PRINT @(0,23):CL:"Invalid Response":
         GOTO 106
      END
      BEGIN CASE
         CASE MENU.NAME = "ALL"
            PRINT @(20,14):CL:"Enter Master Menu Name - ":
            INPUT MASTER
            IF MASTER = "" THEN MASTER = "M.MASTER"
            IF MASTER[1,2] # "M." THEN MASTER = "M.":MASTER
            MENU.NAMES = ""
            CALL GET.MENU.NAMES (MASTER, MENUS, MENU.NAMES)
            MENU.CNT = COUNT(MENU.NAMES,AM) + (MENU.NAMES # "")
            FOR MENU.PTR = 1 TO MENU.CNT
               MENU.ID = MENU.NAMES<MENU.PTR>
               IF MENU.ID[1,2] # "M." THEN GOTO 129
               IF MENU.ID = "M.PRS" THEN MENU.ID = "M.EP4"
               MATREAD MENU FROM MENUS, MENU.ID ELSE GOTO 129
               GOSUB 1000
129         NEXT MENU.PTR
         CASE 1
            MENU.ID = MENU.NAME
            IF MENU.ID = "PRS" OR MENU.ID = "M.PRS" THEN MENU.ID = "M.EP4"
            IF MENU.ID[1,2] # "M." THEN MENU.ID = "M.":MENU.ID
            MATREAD MENU FROM MENUS, MENU.ID ELSE
               PRINT @(0,23):CL:"Invalid Menu Name":
               GOTO 104
            END
            GOSUB 1000
            GOTO 100
      END CASE
      GOTO 99999
*
*---- PROCESS SELECTED MENU
*
1000*
      ITEM.CNT = 0
      FOR I = 33 TO 2 STEP -1 UNTIL ITEM.CNT > 0
         IF MENU(I) # "" THEN
            ITEM.CNT = I - 1
         END
      NEXT I
      MAT PREC = SPACE(80)
      MAT XREC = ""
      XPTR = 0
      PREC(0+1) = ACCOUNT.NAME "L#80"
      PREC(0+1) = PREC(0+1)[1,SNP]:SYSTEM.NAME:PREC(1)[SNP+LEN(SYSTEM.NAME)+1,999]
      PREC(0+1) = PREC(0+1)[1,62]:ODATE "L#10":OTIME
      PREC(1+1) = DASHES
      P = INT((80 - LEN(MENU(1)<1,3>)) / 2)
      PREC(3+1) = PREC(3+1)[1,P]:MENU(1)<1,3>
      SEL.CNT = 0
      BEGIN CASE
         CASE ITEM.CNT > 16
            I2 = INT((ITEM.CNT + 1) / 2)
            Y = 5 + INT((16 - I2) / 2)
            FOR I = 1 TO I2
               IF MENU(I+1)<1,1> = "C" THEN
                  PREC(Y+1) = MENU(I+1)<1,3> "L#40"
               END ELSE
                  SEL.CNT = SEL.CNT + 1
                  PREC(Y+1) = SEL.CNT "R#2" : " - " : MENU(I+1)<1,3> "L#35"
                  XPTR = XPTR + 1
                  XREC(XPTR)<1,1> = SEL.CNT
                  XREC(XPTR)<1,2> = MENU(I+1)<1,1>
               END
               Y = Y + 1
            NEXT I
            Y = 5 + INT((16 - I2) / 2)
            FOR I = I2 + 1 TO ITEM.CNT
               IF MENU(I+1)<1,1> = "C" THEN
                  PREC(Y+1) = PREC(Y+1)[1,40] : MENU(I+1)<1,3> "L#40"
               END ELSE
                  SEL.CNT = SEL.CNT + 1
                  PREC(Y+1) = PREC(Y+1)[1,40] : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3> "L#40"
                  XPTR = XPTR + 1
                  XREC(XPTR)<1,1> = SEL.CNT
                  XREC(XPTR)<1,2> = MENU(I+1)<1,1>
               END
               Y = Y + 1
            NEXT I
         CASE ITEM.CNT > 8
            Y = 5 + INT((16 - ITEM.CNT) / 2)
            FOR I = 1 TO ITEM.CNT
               IF MENU(I+1)<1,1> = "C" THEN
                  PREC(Y+1) = PREC(Y+1)[1,25] : MENU(I+1)<1,3>
               END ELSE
                  SEL.CNT = SEL.CNT + 1
                  PREC(Y+1) = PREC(Y+1)[1,25] : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3>
                  XPTR = XPTR + 1
                  XREC(XPTR)<1,1> = SEL.CNT
                  XREC(XPTR)<1,2> = MENU(I+1)<1,1>
               END
               Y = Y + 1
            NEXT I
         CASE 1
            Y = 5 + INT((16 - 2 * ITEM.CNT) / 2)
            FOR I = 1 TO ITEM.CNT
               IF MENU(I+1)<1,1> = "C" THEN
                  PREC(Y+1) = PREC(Y+1)[1,25] : MENU(I+1)<1,3>
               END ELSE
                  SEL.CNT = SEL.CNT + 1
                  PREC(Y+1) = PREC(Y+1)[1,25] : SEL.CNT "R#2" : " - " : MENU(I+1)<1,3>
                  XPTR = XPTR + 1
                  XREC(XPTR)<1,1> = SEL.CNT
                  XREC(XPTR)<1,2> = MENU(I+1)<1,1>
               END
               Y = Y + 2
            NEXT I
      END CASE
      PREC(22+1) = DASHES
      GOSUB 2000
      RETURN
*
*---- PRINT MENU
*
2000*
      PRINTER ON
      PAGE.NO = PAGE.NO + 1
      PAGE.SUB.NO = 0
      GOSUB 5000
*      PRINT MENU.ID "L#85" : "PROCEDURE NAMES"
      PRINT MENU.ID "L#81" : "PROCEDURE NAMES"
      PRINT
      LINE.CNT = LINE.CNT + 2
      IF XPTR > 24 THEN
         P2 = XPTR
      END ELSE
         P2 = 24
      END
      FOR P = 1 TO P2
*         PRINT PREC(P) "L#85" : XREC(P)<1,1> "R#2" : " " : XREC(P)<1,2>
         PRINT PREC(P) "L#81" : XREC(P)<1,1> "R#2" : " " : XREC(P)<1,2>
         LINE.CNT = LINE.CNT + 1
      NEXT P
      IF DOC.FLAG = "N" THEN GOTO 2090
      SEL.CNT = 0
      FOR I = 1 TO ITEM.CNT
         IF MENU(I+1)<1,1> # "C" THEN
            SEL.CNT = SEL.CNT + 1
            IF MENU(I+1)<1,2> = "" THEN
               DOC.REC = ""
            END ELSE
               READ DOC.REC FROM DOC, MENU(I+1)<1,2> ELSE DOC.REC = ""
            END
            IF DOC.REC = "" THEN
               DOC.REC = "No documentation available."
               DOC.LINE.CNT = 1
            END ELSE
               DOC.LINE.CNT = COUNT(DOC.REC, AM) + 1
            END
            IF LINE.CNT + DOC.LINE.CNT + 3 > MAX.LINES THEN
               GOSUB 6000
               GOSUB 5000
               PRINT
               LINE.CNT = LINE.CNT + 1
            END
            PRINT
            PRINT SEL.CNT "R#2":")  ":MENU(I+1)<1,3>:"   ":MENU(I+1)<1,2>
            PRINT
            LINE.CNT = LINE.CNT + 3
            FOR D = 1 TO DOC.LINE.CNT
               IF LINE.CNT > MAX.LINES THEN
                  GOSUB 6000
                  GOSUB 5000
                  PRINT
                  PRINT
                  LINE.CNT = LINE.CNT + 2
               END
               PRINT SPACE(6):DOC.REC<D>
               LINE.CNT = LINE.CNT + 1
            NEXT D
         END
      NEXT I
2090*
      GOSUB 6000
      PRINTER OFF
      RETURN
*
*---- PRINT HEADINGS
*
5000*
      PRINT CHAR(12):
      PRINT ODATE:SPACE(18):"Computer Business Associates"
      PRINT SPACE(26):"      Menu Definition"
      LINE.CNT = 2
      RETURN
*
*---- PRINT PAGE NUMBER
*
6000*
      X.LINES = MAX.LINES + 3 - LINE.CNT
MID.LINE = INT(X.LINES/2)
REM.LINE = X.LINES - MID.LINE
*     FOR L = LINE.CNT TO MAX.LINES+3
      FOR L = 1 TO MID.LINE
         PRINT
      NEXT L
      IF PAGE.SUB.NO = 0 THEN
         PRINT SPACE(36):"- ":PAGE.NO:" -"
      END ELSE
         PRINT SPACE(35):"- ":PAGE.NO:".":PAGE.SUB.NO:" -"
      END
      PAGE.SUB.NO = PAGE.SUB.NO + 1
      FOR L = 1 TO REM.LINE
         PRINT
      NEXT L
      RETURN
*
*---- END OF PROGRAM
*
99999*
      PRINTER CLOSE
      STOP
   END
