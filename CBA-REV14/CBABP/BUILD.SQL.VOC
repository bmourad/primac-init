* REVISION    - [08.1]
*  BUILD FILE NAME POINTERS IN VOC, USING ONLY '_', STRIP OUT '-' AND '.'
*  THIS WILL CREATE SQL COMPLIANT FILE POINTERS.
*  TASK 17220 - 06/29/93   B.P., C.B.A.
*
*COPY>CPYLIB>CHAR
     PROMPT ""
     PRINT CS
     PRINT @(0,3):"This program will create SQL compliant file name entries in the VOC"
     PRINT @(0,4):"for ALL existing 'F' type entries, Continue ? (Y/N) ":
     INPUT RESPONSE:
     IF RESPONSE # "Y" THEN STOP
     OPEN "","VOC" TO VOC ELSE STOP
     STMNT = "SSELECT VOC WITH F1 = 'F'"
     UDTEXECUTE STMNT CAPTURING MSG
     PRINT @(0,12):MSG:
     DIM ITEM(3)
     MAT ITEM = ''
     DONE = 0
     LOOP
        READNEXT ID ELSE DONE = 1
     UNTIL DONE DO
        PRINT @(0,22):"Processing - ":CL:ID:
10 *
        IF INDEX(ID,".",1) = 0 AND INDEX(ID,"-",1) = 0 THEN GOTO 1000
        NEW.ID = CHANGE(ID,'.','_')
        NEW.ID = CHANGE(NEW.ID,'-','_')
        MATREAD ITEM FROM VOC, NEW.ID ELSE
           MATREAD ITEM FROM VOC, ID ELSE
              PRINT "CANNOT READ FILE POINTER ":ID:" FROM VOC"
              GOTO 1000
           END
           PRINT @(0,23):"Changed to - ":CL:NEW.ID:
           MATWRITE ITEM ON VOC, NEW.ID
        END
1000 *
     REPEAT
     STOP
     END
