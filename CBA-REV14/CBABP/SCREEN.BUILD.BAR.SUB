      SUBROUTINE SCREEN.BUILD.BAR.SUB(LN,VALDAT.VALUE, VALDAT.TYPE, VALDAT.DESC, TOOLBAR_REC, DUP.NAME)
*COPY>CPYLIB>COM1
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*--- INIT
*
      TOOLBAR_REC = "ERROR"
      TOOLBAR_SEQ = ""
      TBAR_TYP = 1
      TBAR_VAL = 2
      TBAR_BIT = 3
      TBAR_HLP = 4
      DUP.NAME = ""
      READ TOOLBAR_TYPES FROM CONTROL, "TOOLBAR_TYPES" ELSE TOOLBAR_TYPES = ""
      READ CHECK_IDS FROM CONTROL, "STANDARD_TOOLBARS" ELSE CHECK_IDS = ""
*
*---- GET MAIN TOOLBAR
*
      MAINBAR_REC = ""
      OPEN.PMCBARS = 0
      OPEN "","PMCBARS" TO WORK_FILE THEN
         READ MAINBAR_REC FROM WORK_FILE, "MAIN" ELSE MAINBAR_REC = ""
         IF MAINBAR_REC<1>[1,7] # "TOOLBAR" THEN MAINBAR_REC = ""
         OPEN.PMCBARS = 1
      END
*
*---- CREATE TOOLBAR FROM VALDAT
*
      VLINES = DCOUNT(VALDAT.VALUE<1,LN>,SVM)
      LINES = 0
      FOR VLN = 1 TO VLINES
         BEGIN CASE
         CASE VALDAT.TYPE<1,LN,VLN> = ""
*        CASE NUM(VALDAT.TYPE<1,LN,VLN>)
         CASE 1
            LOCATE VALDAT.TYPE<1,LN,VLN> IN TOOLBAR_TYPES<1>,1 SETTING VFND THEN
               IF TOOLBAR_TYPES<4,VFND> > 0 THEN
*
*---- EXIST IN STANDARD BUTTONS
                  LINES = LINES + 1
                  LOCATE TOOLBAR_TYPES<4,VFND> IN TOOLBAR_SEQ,1 BY "AR" SETTING LNO ELSE NULL
                  INS "" BEFORE TOOLBAR_REC<2,LNO>
                  INS TOOLBAR_TYPES<4,VFND> BEFORE TOOLBAR_SEQ<LNO>
                  TOOLBAR_REC<2,LNO,TBAR_TYP> = 2
                  TOOLBAR_REC<2,LNO,TBAR_VAL> = VALDAT.VALUE<1,LN,VLN>
                  TOOLBAR_REC<2,LNO,TBAR_BIT> = "..\VSIPIC\BUTTONS\":TOOLBAR_TYPES<2,VFND>
                  IF VALDAT.DESC<1,LN,VLN> = "" THEN
                     TOOLBAR_REC<2,LNO,TBAR_HLP> = TOOLBAR_TYPES<3,VFND>
                  END ELSE
                     TOOLBAR_REC<2,LNO,TBAR_HLP> = VALDAT.DESC<1,LN,VLN>
                  END
               END
            END ELSE
               LINES = LINES + 1
               IF FIELD(VALDAT.TYPE<1,LN,VLN>,".",2) = "BMP" THEN
*
*---- VALID BITMAP BUTTONS
                  LOCATE "8888" IN TOOLBAR_SEQ,1 BY "AR" SETTING LNO ELSE NULL
                  INS "" BEFORE TOOLBAR_REC<2,LNO>
                  INS "8888" BEFORE TOOLBAR_SEQ<LNO>
                  TOOLBAR_REC<2,LNO,TBAR_TYP> = 2
                  TOOLBAR_REC<2,LNO,TBAR_VAL> = VALDAT.VALUE<1,LN,VLN>
                  TOOLBAR_REC<2,LNO,TBAR_BIT> = "..\VSIPIC\BUTTONS\":VALDAT.TYPE<1,LN,VLN>
                  TOOLBAR_REC<2,LNO,TBAR_HLP> = VALDAT.DESC<1,LN,VLN>
               END ELSE
*
*---- TEXT BUTTONS
                  LNO = LINES
                  TOOLBAR_REC<2,LNO> = ""
                  TOOLBAR_REC<2,LNO,TBAR_TYP> = 2
                  TOOLBAR_REC<2,LNO,TBAR_VAL> = VALDAT.VALUE<1,LN,VLN>
                  TOOLBAR_REC<2,LNO,TBAR_BIT> = VALDAT.TYPE<1,LN,VLN>
                  TOOLBAR_REC<2,LNO,TBAR_HLP> = VALDAT.DESC<1,LN,VLN>
                  TOOLBAR_SEQ<LNO> = 9999
               END
            END
         END CASE
      NEXT VLN
*
*---- APPEND TO MAIN TOOLBAR
*
      IF TOOLBAR_REC<2> = "" THEN GOTO 99999
      TOOLBAR_REC<1> = "TOOLBAR ":OCONV(TIMEDATE(),"D")
      TOOLBAR_REC<2> = MAINBAR_REC<2>:VM:TOOLBAR_REC<2>
      IF OPEN.PMCBARS < 1 THEN GOTO 99999
      VLINES = DCOUNT(CHECK_IDS,AM)
      FOR VLN = 1 TO VLINES WHILE DUP.NAME = ""
         ID = CHECK_IDS<VLN>
         BEGIN CASE
         CASE INDEX(ID,".",1)
            ID = ""
         CASE ID[1,4] = "MAIN"
         CASE ID[1,6] = "SCROLL"
         CASE ID[1,6] = "RPTSCN"
         CASE 1
            ID = ""
         END CASE
         IF ID # "" THEN
            READ DUMMY FROM WORK_FILE, ID ELSE DUMMY = ""
            IF DUMMY<2> = TOOLBAR_REC<2> THEN
               DUP.NAME = ID
            END
         END
      NEXT VLN
99999*
      RETURN
   END
