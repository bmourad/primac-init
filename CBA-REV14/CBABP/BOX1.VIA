      SUBROUTINE BOX1.VIA (MAT GRAPH.REC,MAT VIDEO.REC)
*
*COPY>CPYLIB>TCC
*COPY>CPYLIB>CHAR
*
*---- Initialization
*
      PCCMD = CHAR(27):CHAR(8); ENDCMD = CHAR(0) ; ***** Unidata may not support CHAR(0) *****
      IF BOX.CLEAR = 9 THEN
         BOX.CNT = BOX.CNT - 1
         BEGIN CASE
         CASE BOX.CNT < 0
            PRINT PCCMD:"WCLOSE /A ":ENDCMD:
            PRINT PCCMD:"WOPEN 0,0,80,24 ":ENDCMD:
            PRINT PCCMD:"COLOR ":BOX.COLOR:" ":ENDCMD:
            BOX.CNT = 0
         CASE BOX.SAVE
            PRINT PCCMD:"WCLOSE ":ENDCMD:
            BOX.SAVE = 0
         CASE 1
            PRINT @(78,23):
            PRINT PCCMD:"COLOR ":BOX.COLOR:" ":ENDCMD:
         END CASE
         GOTO 99999
      END
      BEGIN CASE
      CASE BOX.COLS > 80
         BOX.COLS = 80
      CASE BOX.COLS < 0
         BOX.COLS = 0
      END CASE
      BEGIN CASE
      CASE BOX.X = "C"
         BOX.X = INT((80 - BOX.COLS) / 2)
      CASE BOX.X > 79
         BOX.X = 79
      CASE BOX.X < 0
         BOX.X = 0
      END CASE
      FRM.X = BOX.X - 1
      BEGIN CASE
      CASE BOX.ROWS > 24
         BOX.ROWS = 24
         BOX.HROW = 0
      CASE BOX.ROWS < 0
         BOX.ROWS = 0
         BOX.HROW = 0
      END CASE
      BEGIN CASE
      CASE BOX.Y = "C"
         BOX.Y = INT((24 - BOX.ROWS) / 2 - 0.5)
      CASE BOX.Y > 23
         BOX.Y = 23
      CASE BOX.Y < 0
         BOX.Y = 0
      END CASE
      IF BOX.HROW THEN
         IF BOX.Y + BOX.ROWS + 1 < 23 THEN
            BOX.ROWS = BOX.ROWS + 1
         END ELSE
            BOX.HROW = 0
         END
      END
      FRM.Y = BOX.Y - 1
      IF FRM.X < 0 THEN
         PRT.X = 0
         ADD.X = FRM.X
      END ELSE
         PRT.X = FRM.X
         ADD.X = 0
      END
      BEGIN CASE
      CASE PRT.X = 79
         BOX.COLS = 0
      CASE PRT.X + BOX.COLS > 80
         BOX.COLS = BOX.COLS - PRT.X - ADD.X
      END CASE
      LST.X = PRT.X + BOX.COLS + 1 + ADD.X
      IF LST.X > 80 THEN
         BOX.COLS = BOX.COLS - (LST.X - 80) - ADD.X
      END
      IF FRM.Y < 0 THEN
         PRT.Y = 0
         ADD.Y = FRM.Y
      END ELSE
         PRT.Y = FRM.Y
         ADD.Y = 0
      END
      BEGIN CASE
      CASE PRT.Y = 23
         BOX.ROWS = 0
      CASE PRT.Y + BOX.ROWS > 24
         BOX.ROWS = BOX.ROWS - PRT.Y - ADD.Y
      END CASE
      LST.Y = PRT.Y + BOX.ROWS + 1 + ADD.Y
      IF LST.Y > 24 THEN
         BOX.ROWS = BOX.ROWS - (LST.Y - 24) - ADD.Y
      END
      FST.SIDE = PRT.Y + ADD.Y + 1
      LST.SIDE = BOX.ROWS + PRT.Y + ADD.Y
*
*---- Draw the Box
*
      PRINT @(BOX.X,BOX.Y):
      IF BOX.SAVE THEN
         PRINT PCCMD:"WOPEN /S ":(BOX.X+1):",":BOX.Y:",":(BOX.COLS-2):",":BOX.ROWS:" ":ENDCMD:
         PRINT PCCMD:"COLOR ":BOX.WCOLOR:" ":ENDCMD:
         IF BOX.FRAME THEN
            PRINT PCCMD:"WFRAME ":BOX.FRAME:" ":ENDCMD:
         END
*---- Always clear box for VIA
         PRINT @(-1):
         IF BOX.HROW THEN
            BOX.ROWS = BOX.ROWS - 1
            BOX.HROW = BOX.ROWS
         END
         BOX.X = 0; BOX.Y = 0
      END ELSE
         PRINT PCCMD:"WOPEN ":(BOX.X+1):",":BOX.Y:",":(BOX.COLS-2):",":BOX.ROWS:" ":ENDCMD:
         PRINT PCCMD:"COLOR ":BOX.WCOLOR:" ":ENDCMD:
         IF BOX.FRAME THEN
            PRINT PCCMD:"WFRAME ":BOX.FRAME:" ":ENDCMD:
         END
*---- Always clear box for VIA
         PRINT @(-1):
         PRINT PCCMD:"WCLOSE ":ENDCMD:
         IF BOX.CNT THEN
            PRINT @(78,23):
         END
         PRINT PCCMD:"COLOR ":BOX.WCOLOR:" ":ENDCMD:
         IF BOX.HROW THEN
            BOX.ROWS = BOX.ROWS - 1
            BOX.HROW = BOX.Y + BOX.ROWS
         END
      END
      BOX.CNT = BOX.CNT + 1
99999*
      BOX.CLEAR = 0
      RETURN
      END
