     SUBROUTINE MULTI.LINE.SCRN
*COPY>CPYLIB>COMMON2
*    SUBROUTINE MULTI.LINE.SUB(MAT MULTI.LINE.REC,MAT MLS.FILES.REC,MAT EDIT.COM.DRIVER,MAT BLD.SCRN,MAT FILE.VARS)
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - BP
* PROGRAM     - MULTI.LINE.SUB
* DATE        - 09/10/83
* DESCRIPTION -
*ENDDOC
*********************************************************************
*
*COPY>CPYLIB>MULTI.LINE.SCRN
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*
**** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
     MAT EDIT.COM = ''
     TYP = 0
     CALL EDIT.SUB
     PREV.START.LINE = 0
     BEGIN CASE
        CASE NOT(NUM(MLS.ACTION))
           GOTO 99999
        CASE MLS.HDR.VALUES = ''
           GOTO 99999
     END CASE
     IF MLS.ST.COL = '' THEN
        MLS.ST.COL = 0
     END
     VALUES.CNT = COUNT(MLS.HDR.VALUES,VM) + 1
     IF MLS.ACTION = 0 THEN
       IF ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>> # '' THEN
           NO.OF.LINES = COUNT(ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>>,SVM) + 1
           LN = 1
           GOSUB 10000
        END
        GOTO 99999
     END
     IF ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>> = '' THEN
        NO.OF.LINES = 0
        ACTION.CODE = 'A'
        LOOP
           LN = NO.OF.LINES + 1
           PREV.NO.OF.LINES = NO.OF.LINES
           GOSUB 1000
        WHILE NO.OF.LINES > PREV.NO.OF.LINES DO REPEAT
        LN = NO.OF.LINES
        GOSUB 10000
     END ELSE
        NO.OF.LINES = COUNT(ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>>,SVM) + 1
        LN = 1
        GOSUB 10000
     END
     MORE = 1
     LOOP
        ECD.RET.VALUES<ECD.SCRN.NO,MLS.ACTION> = ''
        ECD.NUM = MLS.ACTION
        ECD.ACTION = 4
        CALL SCREEN.EDIT
        ACTION.CODE = ECD.RET.VALUE
        BEGIN CASE
           CASE ACTION.CODE = '' OR ACTION.CODE = 'E' OR ACTION.CODE = 'END'
              MORE = 0
           CASE ACTION.CODE = 'F'
              MORE = 0
           CASE ACTION.CODE = 'A'
              LOOP
                 LN = NO.OF.LINES + 1
                 PREV.NO.OF.LINES = NO.OF.LINES
                 GOSUB 1000
              WHILE NO.OF.LINES > PREV.NO.OF.LINES DO REPEAT
              LN = NO.OF.LINES
              GOSUB 10000
           CASE ACTION.CODE = 'C'
              GOSUB 2000
              IF LN.NO THEN
                 LN = LN.NO
                 GOSUB 1000
              END
           CASE ACTION.CODE = 'D'
              GOSUB 2000
              IF LN.NO THEN
                 LN = LN.NO
                 FOR DLP = 1 TO VALUES.CNT
                    ECD.RET.VALUES = DELETE(ECD.RET.VALUES,ECD.SCRN.NO,MLS.HDR.VALUES<1,DLP>,LN)
                 NEXT DLP
                 NO.OF.LINES = COUNT(ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>>,SVM) + 1
                 IF LN > 1 AND LN > NO.OF.LINES THEN LN = LN - 1
                 IF LN < 1 THEN LN = 1
                 PREV.START.LINE = 0
              END
              GOSUB 10000
           CASE ACTION.CODE = 'S'
              LN = LN + MLS.PAGE.SIZE
              IF LN > NO.OF.LINES THEN LN = 1
              GOSUB 10000
        END CASE
     WHILE MORE DO REPEAT
     GOTO 99999
1000*
     GOSUB 10000
     SLN = MLS.BEGIN.PAGE + MLS.LINE.SPACE * MOD(LN - 1,MLS.PAGE.SIZE)
     IF MLS.PLN.FLG = '' THEN
      PRINT @(MLS.ST.COL,SLN) : CL : LN "R#3" :
     END
     SEQ = 1
     LOOP
        BEGIN CASE
           CASE MLS.ACTIONS<1,SEQ> = ''
              ECD.NUM = MLS.HDR.VALUES<1,SEQ>
              ECD.SUB.NUM = LN
              ECD.VALDATION = MLS.VALDATION<1,SEQ>
              ECD.PREFIX.ID = MLS.PREFIX.ID<1,SEQ>
              IF MLS.VALDAT.CODE<1,SEQ> # '' THEN
                 ECD.VALDAT.CODE = MLS.VALDAT.CODE<1,SEQ>
                 ECD.VALDAT.FILE = MLS.FILES.REC(SEQ)
              END
              ECD.Y = SLN
              ECD.ACTION = 4
              CALL SCREEN.EDIT
           CASE 1
              ECD.NUM = MLS.HDR.VALUES<1,SEQ>
              ECD.SUB.NUM = LN
              IF NUM(MLS.ACTIONS<1,SEQ>) AND MLS.ACTIONS<1,SEQ> > 1 THEN
                 ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,ECD.SUB.NUM> = ECD.VALDAT.ITEM<MLS.ACTIONS<1,SEQ>>
              END ELSE
                 ECD.RET.VALUES<ECD.SCRN.NO,ECD.NUM,ECD.SUB.NUM> = ECD.VALDAT.ITEM<1>
              END
              ECD.Y = SLN
              ECD.ACTION = 5
              CALL SCREEN.EDIT
        END CASE
        BEGIN CASE
           CASE (ECD.RET.VALUE = '' OR ECD.RET.VALUE = 'END') AND SEQ = 1
              SEQ = 0
              IF ACTION.CODE = 'A' THEN
              FOR K = 1 TO VALUES.CNT
                 ECD.RET.VALUES = DELETE(ECD.RET.VALUES,ECD.SCRN.NO,MLS.HDR.VALUES<1,K>,LN)
              NEXT K
              END
           CASE ECD.RET.VALUE = 'END'
              SEQ = 1
           CASE SEQ = VALUES.CNT
              SEQ = 0
           CASE 1
              SEQ = SEQ + 1
       END CASE
     WHILE SEQ DO REPEAT
     NO.OF.LINES = COUNT(ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>>,SVM) + (ECD.RET.VALUES<ECD.SCRN.NO,MLS.HDR.VALUES<1,1>> # '')
    RETURN
2000*
    GOSUB 10000
    X = 0
    Y = 23
    PMSG = 'ENTER LINE NUMBER :'
    TYP = 3
    MINV = START.LINE
    IF LAST.LINE < NO.OF.LINES THEN
       MAXV = LAST.LINE
    END ELSE
       MAXV = NO.OF.LINES
    END
    O.R = 'O'
    MAXL = 3
    CALL EDIT.SUB
    PRINT @(MLS.ST.COL,23) : CL :
    LN.NO = VALUE
    IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
    RETURN
10000*
     START.LINE = 1 + INT((LN-1)/MLS.PAGE.SIZE)*MLS.PAGE.SIZE
     IF START.LINE = PREV.START.LINE THEN GOTO 10999
     PREV.START.LINE = START.LINE
     FOR N = 1 TO MLS.PAGE.SIZE
        SLN = MLS.BEGIN.PAGE + MLS.LINE.SPACE * MOD(N - 1,MLS.PAGE.SIZE)
        PRINT @(MLS.ST.COL,SLN) : CL :
     NEXT N
     LAST.LINE = START.LINE + MLS.PAGE.SIZE - 1
     FOR N = START.LINE TO LAST.LINE UNTIL N > NO.OF.LINES
        SLN = MLS.BEGIN.PAGE + MLS.LINE.SPACE * MOD(N - 1,MLS.PAGE.SIZE)
        IF MLS.PLN.FLG = '' THEN
         PRINT @(MLS.ST.COL,SLN) : N "R#3" :
        END
        FOR M = 1 TO VALUES.CNT
           ECD.NUM = MLS.HDR.VALUES<1,M>
           ECD.Y = SLN
           ECD.SUB.NUM = N
           ECD.ACTION = 5
           CALL SCREEN.EDIT
        NEXT M
     NEXT N
10999*
     RETURN
91000 ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999*
     RETURN
     END
