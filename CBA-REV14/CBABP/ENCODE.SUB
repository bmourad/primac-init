      SUBROUTINE ENCODE.SUB (ACTION, KEYWORD, INDATA, OUTDATA)
*********************************************************************
*
* PROGRAM - ENCODE.SUB
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 07/07/90
*
* DESCRIPTION
*
* This subroutine is used to encode or decode string data.
*
*   ACTION  = (E)ncode or (D)ecode
*   KEYWORD = Keyword for encode or decode
*   INDATA  = String to encode or decode
*   OUTDATA = Encoded or decoded string
*
*********************************************************************
*
*---- INITIALIZATION
*
*----------- T E M P O R A R L E Y ------------*
OUTDATA = INDATA
RETURN
*----------------------------------------------*
      ENC.BASE = 237
      ENC.MOD  = 37
      ENC.INCR = 1.0137
      INCR.FLG = 1
      INCR.MIN = 9999
      INCR.MAX = 999999999
      INPDATA = INDATA
      ERRFLG = 1
*
*---- MAIN PROCESSING
*
      IF ACTION = "E" OR ACTION = "D" THEN
         LK = LEN(KEYWORD)
         KW.BASE = ENC.BASE
         FOR LP = 1 TO LK
            KW.BASE = KW.BASE + SEQ(KEYWORD[LP,1])
         NEXT LP
         OUTDATA = ""
         ERRFLG = 0
         LS = LEN(INPDATA)
         LP = 0
         LOOP
            LP = LP + 1
         WHILE LP <= LS DO
            IF INCR.FLG THEN
               KW.BASE = INT(KW.BASE * ENC.INCR)
               IF KW.BASE >= INCR.MAX THEN INCR.FLG = 0
            END ELSE
               KW.BASE = INT(KW.BASE * (2-ENC.INCR))
               IF KW.BASE <= INCR.MIN THEN INCR.FLG = 1
            END
            ENC.OFFSET = MOD(KW.BASE, ENC.MOD) + 1
            INPCHR = INPDATA[LP,1]
            INPVAL = SEQ(INPCHR)
            BEGIN CASE
            CASE ACTION = "E" AND (INPVAL < 32 OR INPVAL > 127)
               OUTCHR = CHAR(32):INPCHR
            CASE ACTION = "E"
               OUTCHR = CHAR(INPVAL + ENC.OFFSET)
            CASE ACTION = "D" AND INPVAL = 32
               LP = LP + 1
               OUTCHR = INPDATA[LP,1]
            CASE ACTION = "D"
               OUTCHR = CHAR(INPVAL - ENC.OFFSET)
            END CASE
            OUTDATA = OUTDATA : OUTCHR
         REPEAT
      END
      IF ERRFLG THEN OUTDATA = INPDATA
      RETURN
   END
