  SUBROUTINE ARS.CHECK.DET.INQ(CHR.ID,CHK.NO,GL.ACCT,GL.DESC)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COA
*********************************************************************
* REVISION    - [10A]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - GLSBP
* PROGRAM     - ARS.CHECK.DET.INQ
* BY          - CINDY STRAIT
* DATE        - 08/29/95
* DESCRIPTION -
*   This program is an inquiry for ars check activities
*T25978 adelgado 02/20/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COA.FILE.VARS
*COPY>GLS.CPYLIB>GLM
*COPY>GLS.CPYLIB>COA.BAL
*COPY>ARS.CPYLIB>CASH.HIST
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
  OPEN "CASH.HIST" TO CASH.HIST ELSE ERRMSG = "CASH.HIST FILE IS MISSING";GOSUB 91000
  OPEN "CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING";GOSUB 91000
*
  DONE = 0
  FOR X = 1 TO 10 UNTIL DONE
    MATREAD CHR.REC FROM CASH.HIST,CONO:CHR.ID:X THEN 
      DONE = 1
    END ELSE
      MAT CHR.REC = ''
    END
  NEXT X
*
  LN1=0
  LINES1=DCOUNT(CHR.INVOICE,VM)
  OLD.START.LINE = 0
  PAGE.SIZE1 = 10
  BEGIN.PAGE1 = 9
  LINE.SPACE1 = 1
  ERRMSG = ''
*
  ACTION = 2; CALL SCRN.EDIT
  SCV.REC(1)<ECD.SCRN.NO> = CHK.NO
  SCV.REC(4)<ECD.SCRN.NO> = FIELD(CHR.ID,"!",1)
  MATREAD CUST.REC FROM CUSTOMER,CONO:FIELD(CHR.ID,"!",1) ELSE MAT CUST.REC = ''
  SCV.REC(2)<ECD.SCRN.NO> = GL.ACCT
  SCV.REC(3)<ECD.SCRN.NO> = GL.DESC
  SCV.REC(5)<ECD.SCRN.NO> = CUST.NAME
  SCV.REC(6)<ECD.SCRN.NO> = FIELD(CHR.ID,"!",2)
  SCV.REC(10)<ECD.SCRN.NO>= CHR.MON
  SCV.REC(11)<ECD.SCRN.NO> = CHR.CASH
  ECD.ACTION = 3;CALL SCRN.EDIT
**
*
  GOSUB 1000
  GOTO 9000
*
1000 START.LINE = 1 + INT((LN1-1)/PAGE.SIZE1)*PAGE.SIZE1
  LAST.LINE = START.LINE + PAGE.SIZE1- 1
  IF LAST.LINE > LINES1 THEN LAST.LINE = LINES1
  IF START.LINE = OLD.START.LINE THEN GOTO 1999
  OLD.START.LINE = START.LINE
  CNT = 1
  FOR N = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(N-1,PAGE.SIZE1)
    P_X  = 1 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
    P_X  := AM:8 ; P_Y := AM:SLN ; P_VALUE := AM:CHR.INVOICE<1,N> "L#12"
    P_X  := AM:19 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(CHR.CASH.APPL<1,N>,"MD2") "R#13"
    P_X  := AM:36 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(CHR.GL.AMT<1,N>,"MD2") "R#13"
    P_X  := AM:52 ; P_Y := AM:SLN ; P_VALUE := AM:CHR.GL.NO<1,N> "R#13"
    P_X  := AM:69 ; P_Y := AM:SLN ; P_VALUE := AM:CHR.DIV<1,N> "L#2"
    P_X  := AM:72 ; P_Y := AM:SLN ; P_VALUE := AM:CHR.DEPT<1,N> "L#2"
    P_X  := AM:75 ; P_Y := AM:SLN ; P_VALUE := AM:CHR.CCTR<1,N> "L#3"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE1
    SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(M-1,PAGE.SIZE1)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
1999 RETURN
*
*
9000*
  ECD.NUM=50;ECD.ACTION=4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "E"
      GOTO 99999
    CASE ECD.RET.VALUE = "V"
      CALL VENDOR.MAINT
    CASE ECD.RET.VALUE = "I"
      GOSUB 30000
      LNO = ECD.RET.VALUE
      LINV = LEN(CHR.INVOICE<1,LNO>)
      IF CHR.INVOICE<1,LNO>[LINV-1,2] = "MI" THEN
        MIV.ID = CHR.INVOICE<1,LNO>
        ECD.SCRN.NO = 3
        CALL MANUAL.INV.INQ(MIV.ID)
      END ELSE
        INV.ID = CHR.INVOICE<1,LNO>
        ECD.SCRN.NO = 2
        CALL INVOICE.INQ(INV.ID)
      END
      ECD.SCRN.NO = 6; ECD.ACTION = 3; CALL SCRN.EDIT
      LN1 = 1; OLD.START.LINE = 0; GOSUB 1000
    CASE ECD.RET.VALUE = "SF" OR ECD.RET.VALUE = 'S' ; *T25978
      LN1 = LN1 + PAGE.SIZE1
      IF LN1 > LINES1 THEN
        IF CHR.INVOICE<1,LN1> = "" THEN LN1 = 1
      END
      GOSUB 1000
    CASE ECD.RET.VALUE = "SR"
      LN1 = LN1 - PAGE.SIZE1
      IF LN1 < LINES1 THEN LN1 = 1
      GOSUB 1000
    * T25978 v
    CASE ECD.RET.VALUE = 'ST'
      LN1 = 1
      GOSUB 1000
    CASE ECD.RET.VALUE = 'SB'
      LN1 = LINES1
      GOSUB 1000
    * T25978 ^
    CASE 1
      GOTO 9000
  END CASE
  GOTO 9000
*
30000*
  ECD.MINV = 1
  ECD.MAXV = LINES1
  ECD.NUM = 51
  ECD.ACTION = 4; CALL SCRN.EDIT
  RETURN
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23):ERRMSG:CL:
* INPUT XX:
* CRT @(0,23):CL:
* RETURN
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
