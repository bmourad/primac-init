**************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - GL.ACTIVITY.RPT
*
* SYSTEM        - GLSBP
*
* BY            - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 02/15/87
*
* DESCRIPTION   -
* This program produces a General Ledger Activity Report.
*
*T26493 cmykleb 04/02/2002 * Change pgm to get the rpt # from the proc.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>GLS.CPYLIB>GLA
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM PROC";GOSUB 91000;GOTO 99999
   CONO = ID<1>
   CO.NAME = ID<2>
   PERIOD = ID<3>
   E.DATE = ID<4>
   LEVEL = ID<5>
*
*---- DIMENSION RECORDS
*
*
*---- OPEN FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
   OPEN "","GLA" TO GLA ELSE ERRMSG = "GLA FILE MISSING";GOTO 93000
   OPEN "","COA" TO COA ELSE ERRMSG = "COA FILE MISSING";GOTO 93000
*
*---- READ COMPANY FILE
*
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = "CANNOT LOCATE COMPANY ":CONO; GOTO 93000
   END
*
*---- MAIN PROCESSING
*
   READNEXT GLA.ID ELSE GOTO 99999
   MATREAD GLA.REC FROM GLA, GLA.ID ELSE MAT GLA.REC = ""
   ERRMSG = ""
   SP = SPACE(1)
   D.LINE = SPACE(110) : STR("-",16)
   TD.LINE = SPACE(110) : STR("=",16)
   DIV.TOTAL = 0
   DEPT.TOTAL = 0
   CCTR.TOTAL = 0
   ACCT.TOTAL = 0
   STRUC = CO.ACCT.MASK:STR("#",15 - LEN(CO.ACCT.MASK))
   ACCT.LEN = LEN(CO.ACCT.PIC)
   DATA = 1
   PRINTER ON
*T26493 v
*  HEAD = "CURRENT ACTIVITY REPORT"
*  CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,"","",HD1,HD2)
   HEAD = ""
   CO.NAME = ""
   RPT.NUM = ID<2>
   CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,RPT.NUM,"",HD1,HD2)
*T26493 ^
   HEAD = HD1 : " 'PL'" : HD2
   HEAD = HEAD : SPACE(59):"FOR PERIOD ":PERIOD:" ENDING ":E.DATE:" 'LL'"
   H.LINE = "ACCOUNT NUMBER         DESCRIPTION":SPACE(8):"DV DP CTR":SPACE(10):"ENTRY DESCRIPTION":SPACE(11):"TR-DATE SRC  REFER#       AMOUNT"
   HEAD = HEAD : H.LINE : " 'L'"
   H.LINE = STR("-",15) : SP
   H.LINE = H.LINE : STR("-",25) : SP
   H.LINE = H.LINE : "-- -- --- "
   H.LINE = H.LINE : STR("-",35) : SP
   H.LINE = H.LINE : STR("-",8) : SP
   H.LINE = H.LINE : STR("-",3) : SP
   H.LINE = H.LINE : STR("-",8) : SP
   H.LINE = H.LINE : STR("-",16)
   HEAD = HEAD:H.LINE
   HEADING HEAD
   HEAD = ""
   BEGIN CASE
      CASE LEVEL = 0
         ACCT = GLA.ID[11,ACCT.LEN]
         DIV = ""
         DEPT = ""
         CCTR = ""
      CASE LEVEL = 1
         ACCT = GLA.ID[11,ACCT.LEN]
         DIV = GLA.ID[4,2]
         DEPT = ""
         CCTR = ""
      CASE LEVEL = 2
         ACCT = GLA.ID[11,ACCT.LEN]
         DIV = GLA.ID[4,2]
         DEPT = GLA.ID[6,2]
         CCTR = ""
      CASE LEVEL = 3
         ACCT = GLA.ID[11,ACCT.LEN]
         DIV = GLA.ID[4,2]
         DEPT = GLA.ID[6,2]
         CCTR = GLA.ID[8,3]
   END CASE
   PREV.ACCT = ACCT
   PREV.DIV = DIV
   PREV.DEPT = DEPT
   PREV.CCTR = CCTR
   MATREAD COA.REC FROM COA, CONO:ACCT ELSE
      MAT COA.REC = ""
      COA.DESC = "NOT ON FILE"
   END
   LOOP
      BEGIN CASE
         CASE ACCT <> PREV.ACCT
            BEGIN CASE
               CASE LEVEL > 2
                  IF CCTR.TOTAL <> DEPT.TOTAL THEN
                     PRINT D.LINE
                     T.LINE = SPACE(48):PREV.CCTR "L#3":SPACE(59)
                     PRINT T.LINE:OCONV(CCTR.TOTAL,"MD2,<") "R#16"
                     PREV.CCTR = ""
                  END
                  IF DEPT.TOTAL <> DIV.TOTAL THEN
                     IF PREV.CCTR # "" THEN
                        PRINT D.LINE
                     END
                     T.LINE = SPACE(45):PREV.DEPT "L#2":SP:PREV.CCTR "L#3":SPACE(59)
                     PRINT T.LINE:OCONV(DEPT.TOTAL,"MD2,<") "R#16"
                     PREV.DEPT = ""; PREV.CCTR = ""
                  END
                  IF DIV.TOTAL <> ACCT.TOTAL THEN
                     IF PREV.CCTR # "" THEN
                        PRINT D.LINE
                     END
                     T.LINE = SPACE(42):PREV.DIV "L#2":SP:PREV.DEPT "L#2":SP:PREV.CCTR "L#3"
                     PRINT T.LINE:SPACE(59):OCONV(DIV.TOTAL,"MD2,<") "R#16"
                     PREV.DIV = ""; PREV.DEPT = ""; PREV.CCTR = ""
                  END
                  PRINT TD.LINE
                  T.LINE = PREV.ACCT STRUC:SP:COA.DESC "L#25":SP:PREV.DIV "L#2":SP
                  T.LINE = T.LINE:PREV.DEPT "L#2":SP:PREV.CCTR "L#3":SPACE(59)
                  PRINT T.LINE:OCONV(ACCT.TOTAL,"MD2,<") "R#16"
               CASE LEVEL > 1
                  IF DEPT.TOTAL <> DIV.TOTAL THEN
                     PRINT D.LINE
                     T.LINE = SPACE(45):PREV.DEPT "L#2":SPACE(63)
                     PRINT T.LINE:OCONV(DEPT.TOTAL,"MD2,<") "R#16"
                     PREV.DEPT = ""
                  END
                  IF DIV.TOTAL <> ACCT.TOTAL THEN
                     IF PREV.DEPT # "" THEN
                        PRINT D.LINE
                     END
                     T.LINE = SPACE(42):PREV.DIV "L#2":SP:PREV.DEPT "L#2"
                     PRINT T.LINE:SPACE(63):OCONV(DIV.TOTAL,"MD2,<") "R#16"
                     PREV.DIV = ""; PREV.DEPT = ""
                  END
                  PRINT TD.LINE
                  T.LINE = PREV.ACCT STRUC:SP:COA.DESC "L#25":SP:PREV.DIV "L#2":SP
                  T.LINE = T.LINE:PREV.DEPT "L#2":SPACE(63)
                  PRINT T.LINE:OCONV(ACCT.TOTAL,"MD2,<") "R#16"
               CASE LEVEL > 0
                  IF DIV.TOTAL <> ACCT.TOTAL THEN
                     PRINT D.LINE
                     T.LINE = SPACE(42):PREV.DIV "L#2"
                     PRINT T.LINE:SPACE(66):OCONV(DIV.TOTAL,"MD2,<") "R#16"
                     PREV.DIV = ""
                  END
                  PRINT TD.LINE
                  T.LINE = PREV.ACCT STRUC:SP:COA.DESC "L#25":SP:PREV.DIV "L#2":SPACE(66)
                  PRINT T.LINE:OCONV(ACCT.TOTAL,"MD2,<") "R#16"
               CASE 1
                  PRINT TD.LINE
                  PRINT PREV.ACCT STRUC:SP:COA.DESC"L#25":SPACE(69):OCONV(ACCT.TOTAL,"MD2,<")"R#16"
            END CASE
            ACCT.TOTAL = 0; DIV.TOTAL = 0; DEPT.TOTAL = 0; CCTR.TOTAL = 0
            PREV.ACCT = ACCT; PREV.DIV = DIV; PREV.DEPT = DEPT
            PREV.CCTR = CCTR
            MATREAD COA.REC FROM COA, CONO:ACCT ELSE
               MAT COA.REC = ""
               COA.DESC = "NOT ON FILE"
            END
            PRINT
         CASE DIV <> PREV.DIV
            PRINT D.LINE
            BEGIN CASE
               CASE LEVEL > 2
                  IF CCTR.TOTAL <> DEPT.TOTAL THEN
                     T.LINE = SPACE(48):PREV.CCTR "L#3":SPACE(59)
                     PRINT T.LINE:OCONV(CCTR.TOTAL,"MD2,<") "R#16"
                     PREV.CCTR = ""
                  END
                  IF DEPT.TOTAL <> DIV.TOTAL THEN
                     T.LINE = SPACE(45):PREV.DEPT "L#2":SP:PREV.CCTR "L#3":SPACE(59)
                     PRINT T.LINE:OCONV(DEPT.TOTAL,"MD2,<") "R#16"
                     PREV.DEPT = ""; PREV.CCTR = ""
                  END
                  T.LINE = SPACE(42):PREV.DIV "L#2":SP:PREV.DEPT "L#2":SP:PREV.CCTR "L#3"
                  PRINT T.LINE:SPACE(59):OCONV(DIV.TOTAL,"MD2,<") "R#16"
               CASE LEVEL > 1
                  IF DEPT.TOTAL <> DIV.TOTAL THEN
                     T.LINE = SPACE(45):PREV.DEPT "L#2":SPACE(63)
                     PRINT T.LINE:OCONV(DEPT.TOTAL,"MD2,<") "R#16"
                     PREV.DEPT = ""
                  END
                  T.LINE = SPACE(42):PREV.DIV "L#2":SP:PREV.DEPT "L#2"
                  PRINT T.LINE:SPACE(63):OCONV(DIV.TOTAL,"MD2,<") "R#16"
               CASE 1
                  T.LINE = SPACE(42):PREV.DIV "L#2"
                  PRINT T.LINE:SPACE(66):OCONV(DIV.TOTAL,"MD2,<") "R#16"
            END CASE
            DIV.TOTAL = 0; DEPT.TOTAL = 0; CCTR.TOTAL = 0
            PREV.DIV = DIV; PREV.DEPT = DEPT; PREV.CCTR = CCTR
            PRINT
         CASE DEPT <> PREV.DEPT
            PRINT D.LINE
            BEGIN CASE
               CASE LEVEL > 2
                  IF CCTR.TOTAL <> DEPT.TOTAL THEN
                     T.LINE = SPACE(48):PREV.CCTR "L#3":SPACE(59)
                     PRINT T.LINE:OCONV(CCTR.TOTAL,"MD2,<") "R#16"
                     PREV.CCTR = ""
                  END
                  T.LINE = SPACE(45):PREV.DEPT "L#2":SP:PREV.CCTR "L#3":SPACE(59)
                  PRINT T.LINE:OCONV(DEPT.TOTAL,"MD2,<") "R#16"
               CASE 1
                  PRINT D.LINE
                  T.LINE = SPACE(45):PREV.DEPT "L#2":SPACE(63)
                  PRINT T.LINE:OCONV(DEPT.TOTAL,"MD2,<") "R#16"
            END CASE
            DEPT.TOTAL = 0; CCTR.TOTAL = 0
            PREV.DEPT = DEPT; PREV.CCTR = CCTR
            PRINT
         CASE CCTR <> PREV.CCTR
            PRINT D.LINE
            T.LINE = SPACE(48):PREV.CCTR "L#3":SPACE(59)
            PRINT T.LINE:OCONV(CCTR.TOTAL,"MD2,<") "R#16"
            CCTR.TOTAL = 0; PREV.CCTR = CCTR
            PRINT
      END CASE
   WHILE DATA DO
      ACT = GLA.ID[11,ACCT.LEN]
      DV = GLA.ID[4,2]
      DP = GLA.ID[6,2]
      CTR = GLA.ID[8,3]
      ACCT.TOTAL = ACCT.TOTAL + GLA.AMT
      CCTR.TOTAL = CCTR.TOTAL + GLA.AMT
      DEPT.TOTAL = DEPT.TOTAL + GLA.AMT
      DIV.TOTAL = DIV.TOTAL + GLA.AMT
      P.LINE = ACT STRUC:SP:COA.DESC "L#25":SP:DV "L#2":SP:DP "L#2":SP
      P.LINE = P.LINE:CTR "L#3":SP:GLA.DESC "L#35":SP
      P.LINE = P.LINE:OCONV(GLA.DATE,"D2/") "L#8":SP:GLA.SRC "L#3":SP
      PRINT P.LINE :GLA.REF "L#8":SP:OCONV(GLA.AMT,"MD2,<") "R#16"
      READNEXT GLA.ID ELSE
         GLA.ID = STR("X",25)
         DATA = 0
      END
      BEGIN CASE
         CASE LEVEL = 0
            ACCT = GLA.ID[11,ACCT.LEN]
            DIV = ""
            DEPT = ""
            CCTR = ""
         CASE LEVEL = 1
            ACCT = GLA.ID[11,ACCT.LEN]
            DIV = GLA.ID[4,2]
            DEPT = ""
            CCTR = ""
         CASE LEVEL = 2
            ACCT = GLA.ID[11,ACCT.LEN]
            DIV = GLA.ID[4,2]
            DEPT = GLA.ID[6,2]
            CCTR = ""
         CASE LEVEL = 3
            ACCT = GLA.ID[11,ACCT.LEN]
            DIV = GLA.ID[4,2]
            DEPT = GLA.ID[6,2]
            CCTR = GLA.ID[8,3]
      END CASE
      MATREAD GLA.REC FROM GLA,GLA.ID ELSE MAT GLA.REC = ""
   REPEAT
   PRINTER OFF
   GOTO 99999
*
*---- CALLS FOR SYSCOM
*
91000*
   PRINTER OFF
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   PRINTER ON
   RETURN
93000*
   PRINTER OFF
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
