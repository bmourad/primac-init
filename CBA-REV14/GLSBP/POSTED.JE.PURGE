***************************************************************************
*
* REVISION - [12.0]
*
* PROGRAM  - POSTED.JE.PURGE
*
* AUTHOR   - CHRIS MYKLEBUST, PRIMAC SYSTEMS
*
* DATE     - 03/31/2004
*
* DESCRIPTION
*
* This program is used to purge posted journal records that are older
* than the # of periods old in the CO.GLS.HIST<1,2> field.
*
* T27935 cmykleb 03/31/2004 * Initial programming.
**************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*
*--- PROCREAD
*
*
*--- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- OPEN ALL FIILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG="CANNOT OPEN COMPANY FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG="CANNOT OPEN CONTROL FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","POSTED.JE" TO POSTED.JE ELSE
      ERRMSG="CANNOT OPEN POSTED.JE FILE"
      GOSUB 91000
      STOP
   END
*
*---- INITIALIZATION
*
   TODAY = DATE()
   CONO=""
   MAT COMP.REC=""
   CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
   PURGE.PERIODS = CO.GLS.HIST<1,2>
   IF PURGE.PERIODS = "" THEN GOTO 99999
   READV CURRENT.PR FROM CONTROL, CONO:'FISCAL',1 ELSE
      ERRMSG = "CANNOT READ CURRENT FISCAL PERIOD"
      GOSUB 91000
      STOP
   END
   *** FIGURE WHAT THE PERIOD WAS FROM THE PURGE PERIOD # ***
   PURGE.YR = CURRENT.PR[1,4]
   PURGE.MO = CURRENT.PR[5,2]
   FOR I = 1 TO PURGE.PERIODS
      PURGE.MO -= 1
      IF PURGE.MO = 0 THEN
         PURGE.YR -= 1
         PURGE.MO = 12
      END
   NEXT I
   PURGE.PR = PURGE.YR:PURGE.MO'R%2'
*
*---- MAIN PROCESSING
*
   SELECT POSTED.JE
100*
   RELEASE
   READNEXT ID ELSE GOTO 99999
   IF ID[1,3] # CONO THEN GOTO 100
   PERIOD = FIELD(ID,'!',2)
   IF PERIOD LE PURGE.PR THEN DELETE POSTED.JE, ID
   GOTO 100
*
*---- ERROR ROUTINE
*
91000 *
   ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
*
99999*
   STOP
END
