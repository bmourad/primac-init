*COPY>CPYLIB>COM1
**************************************************************
*
* REVISION      - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - TRIAL.BALANCE
*
* SYSTEM        - GLSBP
*
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 09/22/85
*
* DESCRIPTION   -
* This program produces a Trial Balance Report.
*
*T21336 lanny 12/17/1996 * Adjust to pickup balance for last period of
*                        year for 52 week accting.
*T26493 cmykleb 04/02/2002 * Change pgm to get the rpt # from the proc.
*ENDDOC
**************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>GLS.CPYLIB>COA.BAL
*COPY>PMC.CPYLIB>COA
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
  PROCREAD ID ELSE ERRMSG = "MUST BE RUN FROM PROC";GOSUB 91000;GOTO 99999
  CONO = ID<1>
  CO.NAME = ID<2>
  PERIOD = ID<3>
  E.DATE = ID<4>
  LEVEL = ID<5>
  DIV.NO = ID<6>
  DEPT.NO = ID<7>
  CCTR.NO = ID<8>
  BEGIN CASE
    CASE LEVEL = 0
      OPEN '','CO.COA.BAL' TO CO.COA.BAL ELSE ERRMSG = 'CO.COA.BAL FILE MISSING';GOSUB 93000
      DIV = "";DEPT = "";CCTR = ""
    CASE LEVEL = 1
      OPEN '','DV.COA.BAL' TO DV.COA.BAL ELSE ERRMSG = 'DV.COA.BAL FILE MISSING';GOSUB 93000
      DIV = DIV.NO;DEPT = "";CCTR = ""
    CASE LEVEL = 2
      OPEN '','DP.COA.BAL' TO DP.COA.BAL ELSE ERRMSG = 'DP.COA.BAL FILE MISSING';GOSUB 93000
      DIV = DIV.NO;DEPT = DEPT.NO;CCTR = ""
    CASE LEVEL = 3
      OPEN '','CC.COA.BAL' TO CC.COA.BAL ELSE ERRMSG = 'CC.COA.BAL FILE MISSING';GOSUB 93000
      DIV = DIV.NO;DEPT = DEPT.NO;CCTR = CCTR.NO
    CASE 1
      ERRMSG = "INVALID LEVEL ENTRY";GOSUB 91000;GOTO 99999
  END CASE
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING';GOTO 93000
*
*---- DIMENSION RECORDS
*
  DIM SP(132)
  DIM DSH(40)
  DIM PL(5)
  DIM GTOT(5)
*
*---- READ COMPANY RECORD
*
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
*T21336 v
  READ PERIOD.REC FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
    PERIOD.REC = "" ; PERIOD.REC<1> = 12
  END
  NUM.PERIODS = PERIOD.REC<1>
*T21336 ^
*
*---- INITIALIZATION
*
  PG.NO = 0
  LINE.CNT = 0
  ERRMSG = ""
  UNKNOWN = STR("?",30)
  FIRST.FLAG = 1
  FOR S = 1 TO 132
    SP(S) = SPACE(S)
  NEXT S
  FOR D = 1 TO 40
    DSH(D) = STR('-',D)
  NEXT D
*T26493 v
*    H.LINE = "TRIAL BALANCE"
*    CALL GET.PROG.HEAD(CONO,CO.NAME,H.LINE,"","",HD1,HD2)
  CONO.NAME = ""
  H.LINE = ""
  RPT.NUM = ID<2>
  CALL GET.PROG.HEAD(CONO,CO.NAME,H.LINE,RPT.NUM,"",HD1,HD2)
*T26493 ^
  PL.LINE1 = SP(50):"P & L    "
  PL.LINE2 = SP(50):DSH(5):SP(4):DSH(14):SP(2):DSH(14):SP(3):DSH(14):SP(2):DSH(14)
  T.LINE = SP(45):"TOTALS:":SP(7)
  MAT PL = ""
  MAT GTOT = ""
  NODATA = 1
  OPN = CB.OPN + PERIOD[5,2] + 1
*T21336 v
  IF PERIOD[5,2] = NUM.PERIODS THEN OPN = 55
*T21336 ^
  STRUC = CO.ACCT.MASK:STR("#",15 - LEN(CO.ACCT.MASK))
  PRINTER ON
*
*---- MAIN PROCESSING
*
10*
  READNEXT CB.KEY ELSE
    IF NODATA THEN GOTO 99999
    GOTO 12
  END
  BEGIN CASE
    CASE LEVEL = 0
      MATREAD CB.REC FROM CO.COA.BAL,CB.KEY ELSE GOTO 10
      ACCT = CB.KEY[4,99]
    CASE LEVEL = 1
      MATREAD CB.REC FROM DV.COA.BAL,CB.KEY ELSE GOTO 10
      ACCT = CB.KEY[6,99]
    CASE LEVEL = 2
      MATREAD CB.REC FROM DP.COA.BAL,CB.KEY ELSE GOTO 10
      ACCT = CB.KEY[8,99]
    CASE LEVEL = 3
      MATREAD CB.REC FROM CC.COA.BAL,CB.KEY ELSE GOTO 10
      ACCT = CB.KEY[11,99]
  END CASE
  NODATA = 0
  IF FIRST.FLAG THEN GOSUB 20
  GOSUB 30
  GOTO 10
12*
  GOSUB 50
  PRINTER OFF
  GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
  IF FIRST.FLAG THEN FIRST.FLAG = 0
  PRINT CHAR(12)
  PRINT
  PG.NO = PG.NO + 1
  PRINT HD1:PG.NO
  PRINT HD2
  PRINT SPACE(59) : "FOR PERIOD " : PERIOD : " ENDING " : E.DATE
  PRINT
  H.LINE = SP(59):"<---- BALANCE SHEET A/C'S --->   <------- EXPENSE A/C'S ------>"
  PRINT H.LINE
  H.LINE = SP(3):"G/L NUMBER    DV  DP  CTR     ACCOUNT DESCRIPTION":SP(11):"DEBITS          CREDITS          DEBITS          CREDITS"
  PRINT H.LINE
  H.LINE = DSH(15):SP(2):DSH(2):SP(2):DSH(2):SP(2):DSH(3):SP(2):DSH(25):SP(4):DSH(14):SP(2):DSH(14):SP(3):DSH(14):SP(2):DSH(14)
  PRINT H.LINE
  PRINT
  LINE.CNT = 8
  RETURN
*
*---- PRINT DETAIL LINE
*
30*
  GOSUB 40
  IF TYPE = "" THEN GOTO 39
  P.LINE = ""
  P.LINE = ACCT STRUC:SP(2):DIV "L#2":SP(2):DEPT "L#2":SP(2):CCTR "L#3":SP(2):COA.DESC "L#25":SP(4)
  IF TYPE = "I" OR TYPE = "E" THEN
    P.LINE = P.LINE:SP(33):E.DEBIT "R#14":SP(2):E.CREDIT "R#14"
  END ELSE
    P.LINE = P.LINE:B.DEBIT "R#14":SP(2):B.CREDIT "R#14"
  END
  PRINT P.LINE
  LINE.CNT = LINE.CNT + 1
39*
  IF LINE.CNT >= 53 THEN GOSUB 20
  RETURN
*
*---- ENTER LAST TOTAL LINE
*
40*
  MATREAD COA.REC FROM COA,CONO:ACCT ELSE MAT COA.REC = ""
  BAL = CB.REC(OPN)
  TYPE = COA.TYPE
  IF TYPE = "" THEN GOTO 49
  IF TYPE = 'I' OR TYPE = 'E' THEN
    IF BAL > 0 THEN
      PL(1) = PL(1) + BAL
      GTOT(1) = GTOT(1) + BAL
      E.DEBIT = OCONV(BAL,"MD2,")
      E.CREDIT = ""
    END ELSE
      BAL = BAL * (-1)
      PL(2) = PL(2) + BAL
      GTOT(2) = GTOT(2) + BAL
      E.CREDIT = OCONV(BAL,"MD2,")
      E.DEBIT = ""
    END
  END ELSE
    IF BAL > 0 THEN
      PL(3) = PL(3) + BAL
      GTOT(3) = GTOT(3) + BAL
      B.DEBIT = OCONV(BAL,"MD2,")
      B.CREDIT = ""
    END ELSE
      BAL = BAL * (-1)
      PL(4) = PL(4) + BAL
      GTOT(4) = GTOT(4) + BAL
      B.CREDIT = OCONV(BAL,"MD2,")
      B.DEBIT = ""
    END
  END
49*
  RETURN
*
*---- PRINT P & L AND GRAND-TOTALS
*
50*
  IF LINE.CNT + 6 >= 56 THEN GOSUB 20
  GOSUB 60
  PRINT
  PRINT
  PL.LINE1 = PL.LINE1:PL.LINE1A
  PRINT PL.LINE1
  PRINT PL.LINE2
  PRINT
  T.LINE = T.LINE:T.LINE2
  PRINT T.LINE
  RETURN
*
*---- CALCULATE P & L  AND GRAND TOTALS
*
60*
  IF PL(3) >= PL(4) THEN
    PL.AMT = PL(3) - PL(4)
    IF PL.AMT = 0 THEN
      PL.LINE1A = SP(33)
      GOTO 61
    END
    PL.LINE1A = SP(16):OCONV(PL.AMT,"MD2,")"R#14":SP(3)
    GTOT(4) = GTOT(4) + PL.AMT
  END ELSE
    PL.AMT = PL(4) - PL(3)
    PL.LINE1A = OCONV(PL.AMT,"MD2,")"R#14":SP(19)
    GTOT(3) = GTOT(3) + PL.AMT
  END
61*
  IF PL(1) >= PL(2) THEN
    PL.AMT = PL(1) - PL(2)
    IF PL.AMT = 0 THEN
      PL.LINE1A = PL.LINE1A:SPACE(30)
      GOTO 62
    END
    PL.LINE1A = PL.LINE1A:SP(16):OCONV(PL.AMT,"MD2,")"R#14"
    GTOT(2) = GTOT(2) + PL.AMT
  END ELSE
    PL.AMT = PL(2) - PL(1)
    PL.LINE1A = PL.LINE1A:OCONV(PL.AMT,"MD2,")"R#14"
    GTOT(1) = GTOT(1) + PL.AMT
  END
62*
  T.LINE2 = OCONV(GTOT(3),"MD2,")"R#14":SP(2):OCONV(GTOT(4),"MD2,")"R#14":SP(3):OCONV(GTOT(1),"MD2,")"R#14":SP(2):OCONV(GTOT(2),"MD2,")"R#14"
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  PRINTER OFF
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  PRINTER ON
  RETURN
93000*
  PRINTER OFF
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
