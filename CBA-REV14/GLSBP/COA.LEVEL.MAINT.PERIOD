*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* SYSTEM       - PRIMAC
* SOURCE       - GLSBP
* PROGRAM      - COA.LEVEL.MAINT.PERIOD
* BY           - WALID YAMOUT ,C.B.A
* DATE         - 08/19/85
*
* TASK
*     18573 LLH 5/95 ACCOUNTING PERIODS 1-52 PROMPT FOR BEG,END PERIODS
*T21177 diane 11/06/1996 * REV11 UPG ADD CLOSE
*T25978 adelgado 02/07/2002 * Add the use of prompts (S,SR,SB,ST).
*T26090 wyamout 03/21/2002 * Add View/eMail
*T26090 wyamout 03/28/2002 * Add EMAIL_FORMAT
*********************************************************************
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FISCAL
*COPY>CPYLIB>FILE.VARS
* T26090 v
*COPY>PMC.CPYLIB>PMC_REPORTS
* T26090 ^
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
  ERRMSG = ""
  PERIOD = ""
  PROC.LINE = "END"
  END.DATE = ""
  LEVEL = ""
  DIV.NO = ""
  DEPT.NO = ""
  CCTR.NO = ""
  PROCREAD INBUFF ELSE ERRMSG = "MUST RUN FROM PROC"; GOSUB 91000; GOTO 999999
* T26090 v
* HEAD = INBUFF<1>
  REPORT_ID = INBUFF<1>
* T26090 ^
  PRD.FLG = INBUFF<2>
  BEGIN CASE
    CASE PRD.FLG = "OP"
    CASE PRD.FLG = "CN"
    CASE PRD.FLG = "OL"
    CASE PRD.FLG = "LY"
    CASE PRD.FLG = "FR"
    CASE PRD.FLG = "CP"
    CASE 1
      PRD.FLG = ""
  END CASE
*
*** OPEN FILES   
  OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING" ; GOSUB 91000; GOTO 999999   
  OPEN "","GLS.SCREENS" TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE MISSING" ; GOSUB 91000; GOTO 999999   
  OPEN "","PREFIX" TO PREFIX ELSE ERRMSG = "PREFIX FILE MISSING" ; GOSUB 91000; GOTO 999999   
  OPEN "","CONTROL" TO CONTROL ELSE  ERRMSG = "CONTROL FILE IS MISSING";GOSUB 91000; GOTO 999999
  OPEN "","DIVISION" TO DIVISION ELSE ERRMSG = "DIVISION FILE IS MISSING" ; GOSUB 91000; GOTO 999999
  OPEN "","DEPARTMENT" TO DEPARTMENT ELSE ERRMSG = "DEPARTMENT FILE IS MISSING" ; GOSUB 91000; GOTO 999999
  OPEN "","COST.CNTR" TO COST.CNTR ELSE ERRMSG = "COST CNTR FILE IS MISSING" ; GOSUB 91000; GOTO 999999
* T26090 v
  OPEN "","PMC_REPORTS" TO PMC_REPORTS ELSE ERRMSG = "PMC_REPORTS FILE IS MISSING" ; GOSUB 91000; GOTO 999999
* T26090 ^
*** GET COMPANY NAME   
  MAT COMP.REC = ""
  CONO = ""
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 999999
***** INT
  BEGIN.PAGE = 4
  PAGE.SIZE = 16
  LINE.SPACE = 1
  MATREAD FISCAL.REC FROM CONTROL , CONO:"FISCAL" ELSE
    ERRMSG = "CONTROL FISCAL IS NOT ON FILE"; GOSUB 91000; GOTO 999999
  END
  READ DATES.REC FROM CONTROL , CONO:"OPENDATES" ELSE
    ERRMSG = "CONTROL OPENDATES IS NOT ON FILE"; GOSUB 91000; GOTO 999999
  END
* T26090 v
  PORT_NO = 'TTY'
  CALL SYSVARS.SUB(PORT_NO)
  ACCT_NO = "ACCT"
  CALL SYSVARS.SUB(ACCT_NO)
  USER_ID = UPCASE(@LOGNAME)
  HF_NAME = PORT_NO :"_": USER_ID
* HF_NAME = CHANGE(PORT_NO,"*","_") :"_": REPORT_ID
  READ EMAIL_FORMAT FROM CONTROL, CONO:"EMAIL_FORMAT" ELSE EMAIL_FORMAT = ""
  IF EMAIL_FORMAT<1> # "PDF" AND EMAIL_FORMAT<1> # "RTF" AND EMAIL_FORMAT<1> # "BOTH" THEN EMAIL_FORMAT<1> = ""
  P.MSG = ""
  P.VALDAT = ""
  MATREAD PRPT.REC FROM PMC_REPORTS, REPORT_ID ELSE
    MAT PRPT.REC = ""
    PRPT.HEADING = INBUFF<1>
    ERRMSG = "PROC IS NOT CONVERTED"
    GOSUB 91000
  END
  HEAD = PRPT.HEADING
  P.MSG = "Enter Number to Change"
  P.VALDAT = ""
  IF PRPT.VIEW.FLAG = "Y" THEN
    P.MSG := ", (V)iew"
    P.VALDAT := ",V"
  END
  IF PRPT.EMAIL.FLAG = "Y" AND EMAIL_FORMAT # "" THEN
    P.MSG := ", e(M)ail"
    P.VALDAT := ",M"
  END
  P.MSG := ", (C)ontinue or (E)nd"
  P.VALDAT := ",C,E"
* T26090 ^
  READV ALL.DIV FROM CONTROL , CONO:"DIVISIONS" , 1 ELSE ALL.DIV = "01"
  DIV.CNT = COUNT(ALL.DIV,VM) + (ALL.DIV # "")
  YEAR = FR.CURR.PER[1,4]
  CURR = FR.CURR.PER[5,2]
  READ PERIOD.REC FROM CONTROL, CONO : "ACCT.PERIODS" ELSE
    PERIOD.REC = ""
    PERIOD.REC<1>  = 12
  END
  NUM.PERIODS = PERIOD.REC<1>
*
*--- SCREEN
*
  MAT EDIT.COM.DRIVER = ""
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "COA.LEVEL.MAINT.PERIOD"
  ECD.SCRN.FLAG<1> = 1
  ECD.ACTION = 1; CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  POS1 = INT((40 - LEN(HEAD))/2)
  SCV.REC(1) = SPACE(POS1):HEAD
* T26090 v
  SCV.REC(23) = REPORT_ID
* T26090 ^
  ECD.ACTION = 3; CALL SCRN.EDIT
  IF GUIFORM THEN
* T26090 v
*   P_TITLE = SCV.REC(1)
    P_TITLE = SCV.REC(23) :"-": SCV.REC(1)
* T26090 ^
    CALL VSI_PTITLE(P_TITLE,ERROR)
  END
100*
  FOR OPTION = 1 TO 5 UNTIL ECD.RET.VALUE = "END"
    ON OPTION GOSUB 1000,2000,3000,7000,8000
  NEXT OPTION
  IF ECD.RET.VALUE = "END" THEN GOTO 99990
500*
* T26090 v
* ECD.NUM = 11; SCV.REC(ECD.NUM) = ""
  IF P.MSG # "" THEN
    ECD.NUM = 22
    ECD.PMSG = P.MSG
    ECD.VALDAT = P.VALDAT
    ECD.TOOLBAR.EFF = 1
  END ELSE
    ECD.NUM = 11
  END
  SCV.REC(ECD.NUM) = ""
* T26090 ^
  ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "E" OR OPTION = "END"
* T26090 v
      READU HF_REC FROM CONTROL, HF_NAME THEN
        DELETE CONTROL, HF_NAME
      END ELSE
        RELEASE CONTROL, HF_NAME
      END
* T26090 ^
      GOTO 99990
    CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 5
      ON OPTION GOSUB 1000,2000,3000,4000
    CASE NUM(OPTION) AND OPTION = 5
      IF DIV.NO = "" OR DIV.NO = "00" THEN GOSUB 4000 ELSE GOSUB 5000
    CASE NUM(OPTION) AND OPTION = 6
      IF DIV.NO = "" OR DIV.NO = "00" THEN
        GOSUB 4000
      END ELSE
        IF DEPT.NO = "" OR DEPT.NO = "00" THEN GOSUB 5000 ELSE GOSUB 6000
      END
    CASE NUM(OPTION) AND OPTION = 7
      GOSUB 7000
    CASE NUM(OPTION) AND OPTION = 8
      GOSUB 8000
* T26090 v
*   CASE OPTION = "C"
    CASE OPTION = "C" OR OPTION = "V" OR OPTION = "M"
* T26090 ^
      BEGIN CASE
        CASE BEG.PERIOD = ""
          ERRMSG = "Must have a Beginning Period"
          GOSUB 91000
        CASE END.PERIOD = ""
          ERRMSG = "Must have an Ending Period"
          GOSUB 91000
        CASE 1
          NUM.PERIODS.REPORT = END.PERIOD - BEG.PERIOD + 1
          IF NUM.PERIODS.REPORT > 13 THEN
            ERRMSG = "Cannot Report more than 13 periods at one time."
            GOSUB 91000
          END ELSE
            PROC.LINE<1> = CONO
* T26090 v
*           PROC.LINE<2> = CO.NAME
            BEGIN CASE
              CASE PRPT.RPT.SCRN.FLAG = "H"
                PTR = INDEX(PRPT.HEADING,"(",1)
                IF PTR = 0 THEN
                  PRPT.HEADING = TRIM(PRPT.HEADING)
                  REPORT.SUFFIX = ""
                END ELSE
                  REPORT.SUFFIX = TRIM(PRPT.HEADING[PTR,99])
                  PRPT.HEADING = TRIM(PRPT.HEADING[1,PTR-1])
                END
                IHL = LEN(PRPT.HEADING)
                SAVE.PRPT.HEADING = ""
                FOR P = 1 TO IHL
                  SAVE.PRPT.HEADING = SAVE.PRPT.HEADING:" ":PRPT.HEADING[P,1]
                NEXT P
                IF LEN(SAVE.PRPT.HEADING) < 81 THEN
                  PRPT.HEADING = SAVE.PRPT.HEADING[2,80]
                END
                HLINE1 = "RUN D - T: ":OCONV(DATE(), "D2/"):" - ":OCONV(TIME(), "MTS")
                HLINE1 = HLINE1 "L#33"
                HLINE2 = SPACE(INT((85-LEN(CO.NAME))/2)):CO.NAME
                HLINE2 = HLINE2 "L#85"
                HLINE3 = SPACE(3):"PAGE 'PL'"
                HLINE = HLINE1:HLINE2:HLINE3
                HLINE4 = "RPT #-BY : ":REPORT_ID:"-":USER_ID
                HLINE4 = HLINE4 "L#33"
                HLINE5 = SPACE(INT((85-LEN(PRPT.HEADING))/2)):PRPT.HEADING
                HLINE5 = HLINE5:"   ":REPORT.SUFFIX
                HLINEA = HLINE4:HLINE5
                PROC.LINE<2> = HLINE:HLINEA
              CASE PRPT.RPT.SCRN.FLAG = "R"
                PROC.LINE<2> = REPORT_ID
              CASE 1
                PROC.LINE<2> = CO.NAME
            END CASE
* T26090 ^
            PROC.LINE<3> = PERIOD
            PROC.LINE<4> = OCONV(END.DATE, "D2/")
            PROC.LINE<5> = LEVEL
            PROC.LINE<6> = DIV.NO
            PROC.LINE<7> = DEPT.NO
            PROC.LINE<8> = CCTR.NO
            PROC.LINE<9> = BEG.PERIOD
            PROC.LINE<10> = END.PERIOD
* T26090 v
            BEGIN CASE
              CASE OPTION = "C"
                READU HF_REC FROM CONTROL, HF_NAME THEN
                  DELETE CONTROL, HF_NAME
                END ELSE
                  RELEASE CONTROL, HF_NAME
                END
              CASE 1
                READU HF_REC FROM CONTROL, HF_NAME ELSE HF_REC = ""
                HF_REC<1> = OPTION
                WRITE HF_REC ON CONTROL, HF_NAME
                CMD = 'SETPTR ,,,,,3,BRIEF,BANNER ':HF_NAME:',NOMESSAGE,NHEAD'
                EXECUTE CMD CAPTURING JUNK
            END CASE
* T26090 ^
            GOTO 99990
          END
      END CASE
  END CASE
  GOTO 500
*********************
*--- SUBROUTINES ---*
*********************
*
*--- PERIOD
*
1000*
  BEGIN CASE
    CASE PRD.FLG = "OP"
      ECD.RET.VALUE = "01"
    CASE PRD.FLG = "CN"
      ECD.RET.VALUE = CURR
    CASE 1
      ECD.NUM = 2; SCV.REC(ECD.NUM) = ""
      ECD.ACTION = 5; CALL SCRN.EDIT
      ECD.NUM = 2; ECD.MAXL = 2
      ECD.MINV = 1
      IF PERIOD # "" THEN SCV.REC(ECD.NUM) = PERIOD[5,2]
      BEGIN CASE
        CASE PRD.FLG = "LY"
          ECD.MAXV = 12
        CASE PRD.FLG = "FR"
          ECD.DEFAULT = CURR
          ECD.MAXV = 12
        CASE PRD.FLG = "CP"
          ECD.DEFAULT = CURR
          ECD.MAXV = CURR
        CASE 1
          ECD.MAXV = CURR
      END CASE
      ECD.ACTION = 4; CALL SCRN.EDIT
  END CASE
  IF ECD.RET.VALUE = "END" THEN GOTO 1999
  IF PRD.FLG = "LY" THEN
    PERIOD = (YEAR - 1) : ECD.RET.VALUE
  END ELSE
    PERIOD = YEAR : ECD.RET.VALUE
  END
  ECD.NUM = 2; SCV.REC(ECD.NUM) = PERIOD
  ECD.ACTION = 5; CALL SCRN.EDIT
1999*
  RETURN
*
*--- DATE
*
2000*
  IF PRD.FLG = "OP" OR PRD.FLG = "OL" OR PRD.FLG = "LY" THEN
    IF PERIOD[5,2] = 12 THEN
      END.DATE = OCONV(DATES.REC<1> - 1, "D/")
      END.DATE = END.DATE[1,6] : END.DATE[7,4] + 1
      END.DATE = ICONV(END.DATE, "D/")
    END ELSE
      END.DATE = DATES.REC<PERIOD[5,2] + 1> - 1
    END
    ECD.NUM = 5; SCV.REC(ECD.NUM) = END.DATE
    ECD.ACTION = 5; CALL SCRN.EDIT
  END ELSE
    IF END.DATE = "" THEN
      IF PERIOD[5,2] = 12 THEN
        ECD.DEFAULT = OCONV(DATES.REC<1> - 1, "D/")
        ECD.DEFAULT = ECD.DEFAULT[1,6] : ECD.DEFAULT[7,4] + 1
        ECD.DEFAULT = ICONV(ECD.DEFAULT, "D/")
      END ELSE
        ECD.DEFAULT = DATES.REC<PERIOD[5,2] + 1> - 1
      END
    END
    ECD.NUM = 5; ECD.ACTION = 4; CALL SCRN.EDIT
    IF ECD.RET.VALUE # "END" THEN END.DATE = ECD.RET.VALUE
  END
  RETURN
*
*--- LEVEL
*
3000*
  ECD.NUM = 8; SCV.REC(ECD.NUM) = ""; ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 6999
    CASE ECD.RET.VALUE > 3 AND PRD.FLG # "FR"
      ERRMSG = "** OUT OF RANGE FOR THIS FUNCTION **"
      GOSUB 91000; GOTO 3000
  END CASE
  LEVEL = ECD.RET.VALUE
**** ENTER DIVISION
4000*
  IF LEVEL < 1 OR LEVEL > 4 OR PRD.FLG = "CN" THEN
    DIV.NO = "00"
    DEPT.NO = "00"
    CCTR.NO = "000"
    ECD.NUM = 3; SCV.REC(ECD.NUM) = DIV.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 4; SCV.REC(ECD.NUM) = "GENERAL DIVISION"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 6; SCV.REC(ECD.NUM) = DEPT.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 7; SCV.REC(ECD.NUM) = "GENERAL DEPARTMENT"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 9; SCV.REC(ECD.NUM) = CCTR.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 10; SCV.REC(ECD.NUM) = "GENERAL COST CENTER"
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 6999
  END
  ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 6999
  IF ECD.RET.VALUE = "" THEN
    IF DIV.CNT < 1 THEN GOTO 4000
    S.NO = ""
    S.DESC = ""
    FOR I = 1 TO DIV.CNT
      FND = 1
      MATREAD DIV.REC FROM DIVISION, CONO:ALL.DIV<1,I> ELSE FND = 0
      IF FND THEN
        S.NO<-1> = ALL.DIV<1,I>
        S.DESC<-1> = DIV.DESC
      END
    NEXT I
    LINES = COUNT(S.NO,AM) + (S.NO # "")
    IF LINES < 1 THEN GOTO 4000
    ECD.NUM = 13; SCV.REC(ECD.NUM) = "Div"
    ECD.ACTION = 5; CALL SCRN.EDIT
    LN = 1
    OLD.START = 0
    GOSUB 85000
    GOSUB 50000
    IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "N" THEN GOTO 4000
    ECD.RET.VALUE = S.NO<ECD.RET.VALUE>
    ECD.NUM = 3; SCV.REC(3) = ECD.RET.VALUE
    ECD.ACTION = 5; CALL SCRN.EDIT
  END
  IF ECD.RET.VALUE = "00" THEN
    DIV.NO = "00"
    DEPT.NO = "00"
    CCTR.NO = "000"
    ECD.NUM = 3; SCV.REC(ECD.NUM) = DIV.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 4; SCV.REC(ECD.NUM) = "GENERAL DIVISION"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 6; SCV.REC(ECD.NUM) = DEPT.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 7; SCV.REC(ECD.NUM) = "GENERAL DEPARTMENT"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 9; SCV.REC(ECD.NUM) = CCTR.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 10; SCV.REC(ECD.NUM) = "GENERAL COST CENTER"
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 6999
  END
  MATREAD DIV.REC FROM DIVISION, CONO:ECD.RET.VALUE ELSE
    ERRMSG = "DIVISION ":ECD.RET.VALUE:" IS NOT ON FILE"; GOSUB 91000; GOTO 4000
  END
  DIV.NO = ECD.RET.VALUE
  ECD.NUM = 4; SCV.REC(ECD.NUM) = DIV.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
**** ENTER DEPARTMENT
5000*
  IF LEVEL < 2 OR PRD.FLG = "CN" THEN
    DEPT.NO = "00"
    CCTR.NO = "000"
    ECD.NUM = 6; SCV.REC(ECD.NUM) = DEPT.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 7; SCV.REC(ECD.NUM) = "GENERAL DEPARTMENT"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 9; SCV.REC(ECD.NUM) = CCTR.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 10; SCV.REC(ECD.NUM) = "GENERAL COST CENTER"
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 6999
  END
  ECD.NUM = 6; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 6999
  IF ECD.RET.VALUE = "" THEN
    LINES = COUNT(DIV.DEPT,VM) + (DIV.DEPT # "")
    IF LINES < 1 THEN GOTO 5000
    S.NO = ""
    S.DESC = ""
    FOR I = 1 TO LINES
      IF LEN(DIV.DEPT<1,I>) = 2 THEN
        FND = 1
        MATREAD DEPT.REC FROM DEPARTMENT, CONO : DIV.DEPT<1,I> ELSE FND = 0
        IF FND THEN
          S.NO<-1> = DIV.DEPT<1,I>
          S.DESC<-1> = DEPT.DESC
        END
      END
    NEXT I
    LINES = COUNT(S.NO,AM) + (S.NO # "")
    IF LINES < 1 THEN GOTO 5000
    ECD.NUM = 13; SCV.REC(ECD.NUM) = "Dept"
    ECD.ACTION = 5; CALL SCRN.EDIT
    LN = 1
    OLD.START = 0
    GOSUB 85000
    GOSUB 50000
    IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "N" THEN GOTO 5000
    ECD.RET.VALUE = S.NO<ECD.RET.VALUE>
    ECD.NUM = 6; SCV.REC(ECD.NUM) = ECD.RET.VALUE
    ECD.ACTION = 5; CALL SCRN.EDIT
  END
  IF ECD.RET.VALUE = "00" THEN
    DEPT.NO = "00"
    CCTR.NO = "000"
    ECD.NUM = 6; SCV.REC(ECD.NUM) = DEPT.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 7; SCV.REC(ECD.NUM) = "GENERAL DEPARTMENT"
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 9; SCV.REC(ECD.NUM) = CCTR.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 10; SCV.REC(ECD.NUM) = "GENERAL COST CENTER"
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 6999
  END
  LOCATE ECD.RET.VALUE IN DIV.DEPT<1>,1 SETTING FND ELSE
    ERRMSG = "DEPARTMENT ":ECD.RET.VALUE:" IS NOT FOR THIS DIVISION"; GOSUB 91000; GOTO 5000
  END
  MATREAD DEPT.REC FROM DEPARTMENT, CONO:ECD.RET.VALUE ELSE
    ERRMSG = "DEPARTMENT ":ECD.RET.VALUE:" IS NOT ON FILE"; GOSUB 91000; GOTO 5000
  END
  DEPT.NO = ECD.RET.VALUE
  ECD.NUM = 7; SCV.REC(ECD.NUM) = DEPT.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
**** ENTER COST CENTER
6000*
  IF LEVEL < 3 OR PRD.FLG = "CN" THEN
    CCTR.NO = "000"
    ECD.NUM = 9; SCV.REC(ECD.NUM) = CCTR.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 10; SCV.REC(ECD.NUM) = "GENERAL COST CENTER"
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 6999
  END
  ECD.NUM = 9; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 6999
  IF ECD.RET.VALUE = "" THEN
    LINES = COUNT(DEPT.CCTRS,VM) + (DEPT.CCTRS # "")
    IF LINES < 1 THEN GOTO 6000
    S.NO = ""
    S.DESC = ""
    FOR I = 1 TO LINES
      FND = 1
      MATREAD CCTR.REC FROM COST.CNTR, CONO : DEPT.CCTRS<1,I> ELSE FND = 0
      IF FND THEN
        S.NO<-1> = DEPT.CCTRS<1,I>
        S.DESC<-1> = CCTR.DESC
      END
    NEXT I
    LINES = COUNT(S.NO,AM) + (S.NO # "")
    IF LINES < 1 THEN GOTO 6000
    ECD.NUM = 13; SCV.REC(ECD.NUM) = "Cctr"
    ECD.ACTION = 5; CALL SCRN.EDIT
    LN = 1
    OLD.START = 0
    GOSUB 85000
    GOSUB 50000
    IF ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "N" THEN GOTO 6000
    ECD.RET.VALUE = S.NO<ECD.RET.VALUE>
    ECD.NUM = 9; SCV.REC(ECD.NUM) = ECD.RET.VALUE
    ECD.ACTION = 5; CALL SCRN.EDIT
  END
  IF ECD.RET.VALUE = "000" THEN
    CCTR.NO = "000"
    ECD.NUM = 9; SCV.REC(ECD.NUM) = CCTR.NO
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 10; SCV.REC(ECD.NUM) = "GENERAL COST CENTER"
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 6999
  END
  LOCATE ECD.RET.VALUE IN DEPT.CCTRS<1>,1 SETTING FND ELSE
    ERRMSG = "COST CENTER ":ECD.RET.VALUE:" IS NOT FOR THIS DEPARTMENT"; GOSUB 91000; GOTO 6000
  END
  MATREAD CCTR.REC FROM COST.CNTR, CONO:ECD.RET.VALUE ELSE
    ERRMSG = "COST CENTER ":ECD.RET.VALUE:" IS NOT ON FILE"; GOSUB 91000; GOTO 6000
  END
  CCTR.NO = ECD.RET.VALUE
  ECD.NUM = 10; SCV.REC(ECD.NUM) = CCTR.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
6999*
  RETURN
*
*--- GET BEGINNING PERIOD
*
7000*
  ECD.NUM = 20; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    BEG.PERIOD = ECD.RET.VALUE
  END
  RETURN
*
*--- GET ENDING PERIOD
*
8000*
  ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    END.PERIOD = ECD.RET.VALUE
  END
  RETURN
*
*--- GET XREF SELECTION
*
50000*
  ECD.NUM = 12
  SCV.REC(ECD.NUM) = ""
  ECD.ACTION = 4
  CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END" OR ECD.RET.VALUE = "N"
      GOTO 59999
    CASE NUM(ECD.RET.VALUE) AND ECD.RET.VALUE > 0
      IF ECD.RET.VALUE < ST.LINE OR ECD.RET.VALUE > LST.LINE THEN
        ERRMSG = "* * * OUT OF RANGE * * *"
        GOSUB 91000
      END ELSE
        GOTO 59999
      END
    CASE ECD.RET.VALUE = "S"
      LN = 1 + INT((LN - 1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
      IF LN > LINES THEN LN = 1
      GOSUB 85000
    * T25978 v
    CASE ECD.RET.VALUE = 'SR'
      LN = 1 + INT((LN - 1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
      IF LN < 1 THEN LN = 1
      GOSUB 85000
    CASE ECD.RET.VALUE = 'ST'
      LN = 1
      GOSUB 85000
    CASE ECD.RET.VALUE = 'SB'
      LN = LINES
      GOSUB 85000
    * T25978 ^
  END CASE
  GOTO 50000
59999*
  ECD.NUM = 13; SCV.REC(ECD.NUM) = ""
  ECD.ACTION = 5; CALL SCRN.EDIT
  FOR SLN = BEGIN.PAGE TO (BEGIN.PAGE + PAGE.SIZE - 1)
*     PRINT @(45,SLN) : CL :
    P_X = 45 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT SLN
  RETURN
*
*
*--- SCROLL XREF
*
85000*
  ST.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF ST.LINE = OLD.START THEN RETURN
  OLD.START = ST.LINE
  LST.LINE = ST.LINE + PAGE.SIZE - 1
  IF LST.LINE > LINES THEN LST.LINE = LINES
  CNT = 1
  FOR N = ST.LINE TO LST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
*     PRINT @(45,SLN) : CL : N "R#3" :
*     PRINT @(49,SLN) : S.NO<N> "L#3" :
*     PRINT @(54,SLN) : S.DESC<N> "L#26" :
    P_X = 45 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = "CL"
    P_X := AM:49 ; P_Y := AM:SLN ; P_VALUE := AM:S.NO<N> "L#3"
    P_X := AM:54 ; P_Y := AM:SLN ; P_VALUE := AM:S.DESC<N> "L#26"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
*     PRINT @(45,SLN) : CL :
    P_X = 45 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
  RETURN   
**** ERROR ROUTINE
91000*
  ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
99990 *
  ECD.ACTION = 99 ; CALL SCRN.EDIT
999999*
  PROCWRITE PROC.LINE
END
