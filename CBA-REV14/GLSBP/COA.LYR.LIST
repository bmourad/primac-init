********************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
*  PRIMAC - LAST YEAR BALANCE LISTING FOR G/L
*
*  DATE: 09/08/85
*
*  DESC -  Prints the open balance listing from the COA file.
*
*T26090 wyamout 03/22/2002 * Add PMC_REPORTS heading
********************************************************
*
*--- INSERT CPYLIB
*
*COPY>GLS.CPYLIB>COA.BAL
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
ERRMSG = ""
PROCREAD INPBUF ELSE ERRMSG = "MUST RUN FROM PROC"; GOTO 91000
CONO=EXTRACT(INPBUF,1,0,0)
* T26090 v
REPORT_ID = INPBUF<2>
* T26090 ^
PERIOD = INPBUF<3>
PER = PERIOD[5,2]
IF NOT(NUM(PER)) OR PER < 1 OR PER > 12 THEN
  ERRMSG = "ERROR IN PERIOD"; GOTO 91000
END
E.DATE = INPBUF<4>
LEVEL = INPBUF<5>
BEGIN CASE
  CASE LEVEL = 3
    FILE = "CC.COA.BAL"
  CASE LEVEL = 2
    FILE = "DP.COA.BAL"
  CASE LEVEL = 1
    FILE = "DV.COA.BAL"
  CASE LEVEL = 0
    FILE = "CO.COA.BAL"
  CASE 1
    ERRMSG = "ERROR IN LEVEL BREAK DOWN"; GOTO 91000
END CASE
OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"; GOTO 91000
MATREAD COMP.REC FROM COMPANY, CONO ELSE ERRMSG = "COMPANY NOT ON FILE"; GOTO 91000
LN.ACCT = LEN(CO.ACCT.MASK)
IF LN.ACCT > 14 THEN ERRMSG = "REPORT CANNOT HANDLE ACCOUNT LENGTH"; GOTO 91000
LN.ACCT = 14 - LN.ACCT
OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOTO 91000
READ REC FROM CONTROL, "COA.BAL.SUM" ELSE ERRMSG = "CONTROL COA.BAL.SUM IS NOT ON FILE "; GOTO 91000
CNT = COUNT(REC<1>,VM) + (REC<1> # "")
OPEN "","COA" TO COA ELSE ERRMSG = "COA FILE IS MISSING"; GOTO 91000
OPEN "",FILE TO OPEN.FILE ELSE ERRMSG = FILE:" FILE IS MISSING"; GOTO 91000
*
*--- HEADING
*
   SP2 = SPACE(2)
* T26090 v
*  HEAD = "ACCOUNT LAST YEAR BALANCE LISTING"
*  CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,"","",HEAD1,HEAD2)
   HEAD = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,REPORT_ID,"",HEAD1,HEAD2)
* T26090 ^
   HEAD3 = "ACCOUNT NUMBER" :SP2: "       DESCRIPTION       " :SP2
   HEAD4 = "--------------" :SP2: "-------------------------" :SP2
   TOT.POS = 43
   IF LEVEL > 0 THEN
     HEAD3 = HEAD3 : "DIV" :SP2
     HEAD4 = HEAD4 : "---" :SP2
     TOT.POS = TOT.POS + 5
   END
   IF LEVEL > 1 THEN
     HEAD3 = HEAD3 : "DEPT" :SP2
     HEAD4 = HEAD4 : "----" :SP2
     TOT.POS = TOT.POS + 6
   END
   IF LEVEL > 2 THEN
     HEAD3 = HEAD3 : "CCTR" :SP2
     HEAD4 = HEAD4 : "----" :SP2
     TOT.POS = TOT.POS + 6
   END
   HEAD3 = HEAD3 : " OPEN BALANCE  " :SP2: "    DEBITS     " :SP2: "    CREDITS    " :SP2: " CLOSE BALANCE"
   HEAD5 = "---------------" :SP2: "---------------" :SP2: "---------------" :SP2: "---------------"
      PRINTER ON
      HEAD = SPACE(59):"FOR PERIOD ":PERIOD:" ENDING ":E.DATE:" 'L'"
      HEADING HEAD1:" 'PL'":HEAD2:HEAD:HEAD3:"'L'":HEAD4:HEAD5:"'L'"
*
*--- INITIALIZE
*
   IF LN.ACCT < 1 THEN
     LN.ACCT = ""
   END ELSE
     LN.ACCT = SPACE(LN.ACCT)
   END
   TOT.OPEN = 0
   TOT.DEBIT = 0
   TOT.CREDIT = 0
   TOT.CLOSE = 0
100*
   READNEXT ID ELSE GOTO 200
   IF CONO # ID[1,3] THEN GOTO 100
   MATREAD CB.REC FROM OPEN.FILE, ID ELSE GOTO 100
   ACCT.NO = ""
   CCTR = ""
   DEPT = ""
   DIV = ""
   BEGIN CASE
     CASE LEVEL = 3
       ACCT.NO = ID[11,99]
       CCTR = ID[8,3]
       DEPT = ID[6,2]
       DIV = ID[4,2]
     CASE LEVEL = 2
       ACCT.NO = ID[8,99]
       DEPT = ID[6,2]
       DIV = ID[4,2]
     CASE LEVEL = 1
       ACCT.NO = ID[6,99]
       DIV = ID[4,2]
     CASE LEVEL = 0
       ACCT.NO = ID[4,99]
   END CASE
   MATREAD COA.REC FROM COA, CONO : ACCT.NO ELSE
     MAT COA.REC = ""
     COA.DESC = "NOT ON FILE"
   END
   OPEN.BAL = CB.REC(CB.LYR+PER)
   TOT.OPEN = TOT.OPEN + OPEN.BAL
   DEBIT = ""
   CREDIT = ""
   CLOSE.BAL = CB.REC(CB.LYR+PER+1)
   AMT = CLOSE.BAL - OPEN.BAL
   BEGIN CASE
     CASE AMT > 0
       DEBIT = AMT
     CASE AMT < 0
       CREDIT = AMT
   END CASE
   TOT.DEBIT = TOT.DEBIT + DEBIT
   TOT.CREDIT = TOT.CREDIT + CREDIT
   TOT.CLOSE = TOT.CLOSE + CLOSE.BAL
   V.LINE = ACCT.NO CO.ACCT.MASK : LN.ACCT :SP2: COA.DESC "L#25" :SP2
   IF LEVEL > 0 THEN V.LINE = V.LINE : DIV "L#3" :SP2
   IF LEVEL > 1 THEN V.LINE = V.LINE : DEPT "L#4" :SP2
   IF LEVEL > 2 THEN V.LINE = V.LINE : CCTR "L#4" :SP2
   V.LINE = V.LINE : OCONV(OPEN.BAL, "MD2Z,<") "R#15" :SP2
   V.LINE = V.LINE : OCONV(DEBIT, "MD2Z,") "R#15" :SP2
   V.LINE = V.LINE : OCONV(CREDIT, "MD2Z,<") "R#15" :SP2
   V.LINE = V.LINE : OCONV(CLOSE.BAL, "MD2Z,<") "R#15"
   PRINT V.LINE
   GOTO 100
200*
   PRINT
   PRINT SPACE(TOT.POS) : HEAD5
   V.LINE = SPACE(TOT.POS) : OCONV(TOT.OPEN, "MD2Z,<") "R#15" :SP2
   V.LINE = V.LINE : OCONV(TOT.DEBIT, "MD2Z,") "R#15" :SP2
   V.LINE = V.LINE : OCONV(TOT.CREDIT, "MD2Z,<") "R#15" :SP2
   V.LINE = V.LINE : OCONV(TOT.CLOSE, "MD2Z,<") "R#15"
   PRINT V.LINE
   PRINTER OFF
   STOP
91000*
   PRINT @(0,23) : ERRMSG : CL :
   INPUT REPLY,1 :
   PRINT @(0,23) : CL :
   END
