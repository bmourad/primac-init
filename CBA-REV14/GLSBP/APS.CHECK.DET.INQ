   SUBROUTINE APS.CHECK.DET.INQ(MCD.ID,CHK.NO,GL.ACCT,GL.DESC)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COA
*********************************************************************
* REVISION    - [10A]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - GLSBP
* PROGRAM     - APS.CHECK.DET.INQ
* BY          - CINDY STRAIT
* DATE        - 08/29/95
* DESCRIPTION -
*   This program is an inquiry for aps check activities
*T21360 lanny 12/23/1996 * Mod for MCD purge periods.
*T23183 gat 08/14/1998 * FIX DRILL DOWN PROBLEM
*T25978 adelgado 02/20/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COA.FILE.VARS
*COPY>PMC.CPYLIB>VEND
*COPY>GLS.CPYLIB>GLM
*COPY>GLS.CPYLIB>COA.BAL
*COPY>APS.CPYLIB>MCD
*COPY>GLS.CPYLIB>GLX
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
   OPEN "MCD" TO MCD ELSE ERRMSG = "MCD FILE IS MISSING";GOSUB 91000
   OPEN "VEND" TO VEND ELSE ERRMSG = "VEND FILE IS MISSING";GOSUB 91000
*
*T21360 v
   MAT MCD.REC = ''
*     MATREAD MCD.REC FROM MCD,CONO:MCD.ID ELSE MAT MCD.REC = ''
   MATREAD MCD.REC FROM MCD,CONO:MCD.ID ELSE
      MATREAD MCD.REC FROM MCD,CONO:'C':MCD.ID[2,99] ELSE
         MATREAD MCD.REC FROM MCD,CONO:'H':MCD.ID[2,99] ELSE NULL
      END
   END
*T21360 ^
*
   LN1=0
   LINES1=DCOUNT(MCD.VOUCH,VM)
   OLD.START.LINE = 0
   PAGE.SIZE1 = 10
   BEGIN.PAGE1 = 10
   LINE.SPACE1 = 1
   ERRMSG = ''
*
   SCV.REC(1)<ECD.SCRN.NO> = CHK.NO
   SCV.REC(4)<ECD.SCRN.NO> = MCD.VEND
   SCV.REC(2)<ECD.SCRN.NO> = GL.ACCT
   SCV.REC(3)<ECD.SCRN.NO> = GL.DESC
   MATREAD VEND.REC FROM VEND,CONO:MCD.VEND ELSE MAT VEND.REC = ''
   SCV.REC(5)<ECD.SCRN.NO> = VEND.DESC
   SCV.REC(6)<ECD.SCRN.NO> = MCD.CHK.DATE
   SCV.REC(7)<ECD.SCRN.NO> = MCD.GRS.AMT
   SCV.REC(8)<ECD.SCRN.NO> = MCD.DSC.AMT
   SCV.REC(9)<ECD.SCRN.NO> = MCD.NET.AMT
   SCV.REC(10)<ECD.SCRN.NO> = MCD.MON
   ECD.ACTION = 3;CALL SCRN.EDIT
**
*
   GOSUB 1000
   GOTO 9000
*
1000 START.LINE = 1 + INT((LN1-1)/PAGE.SIZE1)*PAGE.SIZE1
   LAST.LINE = START.LINE + PAGE.SIZE1- 1
   IF LAST.LINE > LINES1 THEN LAST.LINE = LINES1
   IF START.LINE = OLD.START.LINE THEN GOTO 1999
   OLD.START.LINE = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(N-1,PAGE.SIZE1)
      P_X  = 1 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
      P_X  := AM:8 ; P_Y := AM:SLN ; P_VALUE := AM:MCD.VOUCH<1,N> "L#12"
      P_X  := AM:19 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MCD.VOUCH.GRS<1,N>,"MD2") "R#13"
      P_X  := AM:36 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MCD.VOUCH.DSC<1,N>,"MD2") "R#13"
      P_X  := AM:52 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MCD.VOUCH.NET<1,N>,"MD2") "R#13"
      P_X  := AM:69 ; P_Y := AM:SLN ; P_VALUE := AM:MCD.DIV<1,N> "L#2"
      P_X  := AM:72 ; P_Y := AM:SLN ; P_VALUE := AM:MCD.DEPT<1,N> "L#2"
      P_X  := AM:75 ; P_Y := AM:SLN ; P_VALUE := AM:MCD.CCTR<1,N> "L#3"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE1
      SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(M-1,PAGE.SIZE1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
1999 RETURN
*
*
9000*
   ECD.NUM=50;ECD.ACTION=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "E"
         GOTO 99999
      CASE ECD.RET.VALUE = "V"
* T23183
         ECD.ACTION=99; CALL SCRN.EDIT
* T23183
         ECD.SCRN.NO = 8
         CALL VENDOR.MAINT.INQ(MCD.VEND)
         ECD.SCRN.NO = 5
         ECD.ACTION=3;CALL SCRN.EDIT
         LN1=1; OLD.START.LINE = 0; GOSUB 1000
      CASE ECD.RET.VALUE = "O"
         GOSUB 30000
         LNO = ECD.RET.VALUE
         VCH.AMT = MCD.VOUCH.NET<1,LNO>
         SQV.ID = MCD.VOUCH<1,LNO>
* T23183
         ECD.ACTION=99; CALL SCRN.EDIT
* T23183
         ECD.SCRN.NO = 4
         CALL VOUCHER.DET.INQ(SQV.ID,VCH.AMT)
         GOTO 99999
      CASE ECD.RET.VALUE = "S"     ;* T25978
         LN1 = LN1 + PAGE.SIZE1
         IF LN1 > LINES1 THEN
            IF MCD.VOUCH<1,LN1> = "" THEN LN1 = 1
         END
         GOSUB 1000
      CASE ECD.RET.VALUE = "SR"     ;* T25978
         LN1 = LN1 - PAGE.SIZE1
         IF LN1 < 1 THEN LN1 = 1
         GOSUB 1000
    * T25978 v
      CASE ECD.RET.VALUE = 'SB'
         LN1 = LINES1
         GOSUB 1000
      CASE ECD.RET.VALUE = 'ST'
         LN1 = 1
         GOSUB 1000
    * T25978 ^
      CASE 1
         GOTO 9000
   END CASE
   GOTO 9000
*
30000*
   ECD.MINV = 1
   ECD.MAXV = LINES1
   ECD.NUM = 51
   ECD.ACTION = 4; CALL SCRN.EDIT
   RETURN
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23):ERRMSG:CL:
* INPUT XX:
* CRT @(0,23):CL:
* RETURN
99999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
