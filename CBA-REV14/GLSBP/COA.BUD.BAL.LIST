********************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
*  PRIMAC - BUDGET BALANCE LISTING FOR G/L
*
*  DATE: 09/27/85
*
*  DESC -  Prints the budget listing from the COA file.
*T21583 lanny 02/10/1997 * YTD BUDGETS MISSING.
*T21941 lanny 05/15/1997 * Last period requested calc wrong.
*T26090 wyamout 03/22/2002 * Add heading from PMC_REPORTS
*
********************************************************
*
*--- INSERT CPYLIB
*
*COPY>GLS.CPYLIB>COA.BAL
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
   ERRMSG = ""
   PROCREAD INPBUF ELSE ERRMSG = "MUST RUN FROM PROC"; GOTO 91000
   CONO=EXTRACT(INPBUF,1,0,0)
* T26090 v
REPORT_ID = INPBUF<2>
* T26090 ^
   LEVEL = INPBUF<5>
   BEG.PERIOD = INPBUF<9>
   END.PERIOD = INPBUF<10>
   BEGIN CASE
   CASE LEVEL = 3
     FILE = "CC.COA.BAL"
   CASE LEVEL = 2
     FILE = "DP.COA.BAL"
   CASE LEVEL = 1
     FILE = "DV.COA.BAL"
   CASE LEVEL = 0
     FILE = "CO.COA.BAL"
   CASE 1
     ERRMSG = "ERROR IN LEVEL BREAK DOWN"; GOTO 91000
   END CASE
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING"; GOTO 91000
   MATREAD COMP.REC FROM COMPANY, CONO ELSE ERRMSG = "COMPANY NOT ON FILE"; GOTO 91000
   LN.ACCT = LEN(CO.ACCT.MASK)
   IF LN.ACCT > 14 THEN ERRMSG = "REPORT CANNOT HANDLE ACCOUNT LENGTH"; GOTO 91000
   LN.ACCT = 14 - LN.ACCT
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOTO 91000
   READ REC FROM CONTROL, "COA.BAL.SUM" ELSE ERRMSG = "CONTROL COA.BAL.SUM IS NOT ON FILE "; GOTO 91000
   READ NUM.PERIODS FROM CONTROL, CONO:"ACCT.PERIODS" ELSE
     NUM.PERIODS = 12
   END
   CNT = COUNT(REC<1>,VM) + (REC<1> # "")
   OPEN "","COA" TO COA ELSE ERRMSG = "COA FILE IS MISSING"; GOTO 91000
   OPEN "",FILE TO OPEN.FILE ELSE ERRMSG = FILE:" FILE IS MISSING"; GOTO 91000
*
*--- HEADING
*
   SP2 = SPACE(2)
* T26090 v
*  HEAD = "ACCOUNT BUDGET PERIOD LISTING"
*  CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,"","",HEAD1,HEAD2)
   HEAD = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,HEAD,REPORT_ID,"",HEAD1,HEAD2)
* T26090 ^
   BEGIN CASE
   CASE LEVEL = 3
     RUN.HEAD = "COST CENTER LEVEL"
   CASE LEVEL = 2
     RUN.HEAD = "DEPARTMENT LEVEL "
   CASE LEVEL = 1
     RUN.HEAD = " DIVISION LEVEL  "
   CASE LEVEL = 0
     RUN.HEAD = "  COMPANY LEVEL  "
   END CASE
   HEAD3 = SPACE(67) : RUN.HEAD : "'LL'"
   HEAD4 = ' Period ':BEG.PERIOD[5,2]"R%2"
   HEAD5 = ' ---------'
   FOR PP = BEG.PERIOD+1 TO END.PERIOD
    HEAD4 = HEAD4:" Period ":PP[5,2]"R%2"
    HEAD5 = HEAD5:' ---------'
   NEXT PP
    HEAD4 = HEAD4:"'L'"
    HEAD5 = HEAD5:"'L'"
   PRINTER ON
   HEADING HEAD1:"'PL'":HEAD2:HEAD3:HEAD4:HEAD5
*
*--- INITIALIZE
*
   IF LN.ACCT < 1 THEN
     LN.ACCT = ""
   END ELSE
     LN.ACCT = SPACE(LN.ACCT)
   END
   TOT.BUDGET = 0
100*
   READNEXT ID ELSE GOTO 200
   IF CONO # ID[1,3] THEN GOTO 100
   MATREAD CB.REC FROM OPEN.FILE, ID ELSE GOTO 100
   ACCT.NO = ""
   CCTR = ""
   DEPT = ""
   DIV = ""
   BEGIN CASE
   CASE LEVEL = 3
     ACCT.NO = ID[11,99]
     CCTR = ID[8,3]
     DEPT = ID[6,2]
     DIV = ID[4,2]
   CASE LEVEL = 2
     ACCT.NO = ID[8,99]
     DEPT = ID[6,2]
     DIV = ID[4,2]
   CASE LEVEL = 1
     ACCT.NO = ID[6,99]
     DIV = ID[4,2]
   CASE LEVEL = 0
     ACCT.NO = ID[4,99]
   END CASE
   MATREAD COA.REC FROM COA, CONO : ACCT.NO ELSE
     MAT COA.REC = ""
     COA.DESC = "NOT ON FILE"
   END
   TOT.BUDGET = 0
   BEGIN CASE
   CASE LEVEL = 3
     LINE.HEAD = "CCTR : " : CCTR
   CASE LEVEL = 2
     LINE.HEAD = "DEPT : " : DEPT
   CASE LEVEL = 1
     LINE.HEAD = "DIV  : " : DIV
   CASE LEVEL = 0
     LINE.HEAD = "          "
   CASE 1
     LINE.HEAD = "          "
   END CASE
*T21583 v
   YR.AMT = CB.REC(161) / 100
*T21583 ^
   V.LINE = ACCT.NO CO.ACCT.MASK : LN.ACCT : SPACE(2) : COA.DESC "L#25" : SPACE(4) : LINE.HEAD : SPACE(46) : "Year End Balance : " : OCONV(YR.AMT, "MD0") "R#10"
   PRINT V.LINE
   V.LINE = ""
   FOR I = (CB.BUD + BEG.PERIOD[5,2]) TO (CB.BUD + END.PERIOD[5,2])
*T21583 v
*T21941 IF I # (CB.BUD + END.PERIOD[5,2]) THEN
     IF I # (CB.BUD + END.PERIOD[5,2]) OR END.PERIOD[5,2] # NUM.PERIODS THEN
        LN.AMT = (CB.REC(I+1) - CB.REC(I)) / 100
     END ELSE
        LN.AMT = (CB.REC(161) - CB.REC(I)) / 100
     END
*T21583 ^
     V.LINE = V.LINE : " " : OCONV(LN.AMT, "MD0") "R#9"
   NEXT I
   PRINT V.LINE
   V.LINE = ""
   PRINT V.LINE
   GOTO 100
200*
   PRINT
   PRINTER OFF
   STOP
91000*
   PRINT @(0,23) : ERRMSG : CL :
   INPUT REPLY,1 :
   PRINT @(0,23) : CL :
 END
