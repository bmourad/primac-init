*COPY>CPYLIB>COM1
***************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* PROGRAM    - JE.ACCT.LIST
* SYSTEM     - GLSBP
* BY         - STRAKA, VERCOM SOFTWARE
* DATE       - 04/30/91
* DESCRIPTION   -
* This program prints journal entries by account number.
*T26090 wyamout 03/29/2002 * Add PMC_REPORTS
***************************************************************
*
*---- INSERT FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>GLS.CPYLIB>JE
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- READ PROC VALUES
*
   PROCREAD ID ELSE
   ERRMSG = 'CANNOT PERFORM PROCREAD'
   GOTO 93000
   END
   CONO = ID<1>
*  CONO.NAME = ""
   PERIOD = ID<3>
* T26090 v
*  FILE = ID<4>
*  LEVEL = ID<5>
   FILE = ID<10>
   LEVEL = ID<8>
   REPORT.NUMBER = ID<2>
* T26090 ^
*
*---- OPEN FILES
*
   IF FILE # "JE" AND FILE # "JE.REVERSE" AND FILE #  "JE.RECUR" THEN
      ERRMSG = "INVALID FILE NAME FROM PROC"; GOTO 93000
   END
   OPEN '',FILE TO OPEN.FILE ELSE ERRMSG = FILE:" FILE MISSING"; GOTO 93000
   OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING';GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
   OPEN '','SECURITY' TO SECURITY ELSE ERRMSG = 'SECURITY FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
   MATREAD COMP.REC FROM COMPANY, CONO ELSE
      ERRMSG = 'CANNOT LOCATE COMPANY' :   CONO
      GOTO 93000
   END
   STRUC = CO.ACCT.MASK[1,12]
* T26090 v
*  REPORT.NAME = "JOURNAL ENTRY LISTING BY ACCOUNT"
*  BEGIN CASE
*  CASE FILE = "JE.RECUR"
*     REPORT.NAME = "RECURRING ":REPORT.NAME
*  CASE FILE = "JE.REVERSE"
*     REPORT.NAME = "REVERSE ":REPORT.NAME
*  END CASE
   REPORT.NAME = ""
*  REPORT.NUMBER = "XXXX"
* T26090 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2=""
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   LINE.COUNT = 99
   PAGE.NO = 0
   H.LINE1 = SPACE(61):"BY ACCOUNT FOR PERIOD ":PERIOD
   H.LINE2 = "USER REFERENCE REF DATE  REFERENCE DESC  / POST STATUS    ACCOUNT   DV DP CTR    ACCOUNT DESCRIPTION    CT   DB AMOUNT    CR AMOUNT"
   U.LINE = "---- --------- -------- ------------------------------ ------------ -- -- --- ------------------------- -- ------------ ------------"
   T.LINE = SPACE(107):"------------ ------------"
   G.LINE=SPACE(107):"============ ============"
   TOT.DB = 0; TOT.CR=0
*
*---- TOTAL TABLE FOR ACCOUNT
*
   DIM TOT.ACCT(2)
   EQU ACCT.DB   TO TOT.ACCT(1)
   EQU ACCT.CR   TO TOT.ACCT(2)
   MAT TOT.ACCT = ""
*
*---- TOTAL TABLE FOR DIV
*
   DIM TOT.DIV(2)
   EQU DIV.DB   TO TOT.DIV(1)
   EQU DIV.CR   TO TOT.DIV(2)
   MAT TOT.DIV = ""
*
*---- TOTAL TABLE FOR DEPT
*
   DIM TOT.DEPT(2)
   EQU DEPT.DB    TO TOT.DEPT(1)
   EQU DEPT.CR    TO TOT.DEPT(2)
   MAT TOT.DEPT = ""
*
*---- TOTAL TABLE FOR CCTR
*
   DIM TOT.CCTR(2)
   EQU CCTR.DB   TO TOT.CCTR(1)
   EQU CCTR.CR   TO TOT.CCTR(2)
   MAT TOT.CCTR = ""
*
*---- READ AND PROCESS FIRST RECORD
*
   PRINTER ON
100 *
   READNEXT KEY, PTR ELSE GOTO 99999
   PTR = PTR<1,1,1>
   MATREAD JE.REC FROM OPEN.FILE, KEY ELSE GOTO 100
   MATREAD COA.REC FROM COA, CONO:JE.ACCT<1,PTR> ELSE
      COA.DESC = STR("?",25)
   END
   PREV.ACCT = JE.ACCT<1,PTR>
   PREV.DIV = JE.DIV<1,PTR>
   PREV.DEPT = JE.DEPT<1,PTR>
   PREV.CCTR = JE.CCTR<1,PTR>
   GOSUB 1000
*
*---- READ AND PROCESS ALL RECORDS
*
   DATA = 0
   LOOP
      READNEXT KEY, PTR ELSE DATA = 1
      PTR = PTR<1,1,1>
   WHILE DATA = 0 DO
      MATREAD JE.REC FROM OPEN.FILE, KEY ELSE GOTO 200
      MATREAD COA.REC FROM COA, CONO:JE.ACCT<1,PTR> ELSE
         COA.DESC = STR("?",25)
      END
      GOSUB 500
      GOSUB 1000
200 *
   REPEAT
   GOSUB 2000
   GOSUB 3000
   GOSUB 4000
   GOSUB 5000
   IF LINE.COUNT > 50 THEN GOSUB 9000
   P.LINE = "TOTAL":SPACE(102)
   P.LINE = P.LINE : OCONV(TOT.DB,"MD2Z") "R#12 "
   P.LINE = P.LINE : OCONV(TOT.CR,"MD2Z") "R#12"
   PRINT G.LINE
   PRINT P.LINE
   GOTO 99999
*
*---- FIND BREAK AND PRINT TOTALS
*
500 *
   BEGIN CASE
   CASE PREV.ACCT # JE.ACCT<1,PTR>
      GOSUB 2000
      GOSUB 3000
      GOSUB 4000
      GOSUB 5000
      PRINT
      LINE.COUNT = LINE.COUNT+1
   CASE PREV.DIV # JE.DIV<1,PTR>
      GOSUB 2000
      GOSUB 3000
      GOSUB 4000
      PRINT
      LINE.COUNT = LINE.COUNT+1
   CASE PREV.DEPT # JE.DEPT<1,PTR>
      GOSUB 2000
      GOSUB 3000
      PRINT
      LINE.COUNT = LINE.COUNT+1
   CASE PREV.CCTR # JE.CCTR<1,PTR>
      GOSUB 2000
      PRINT
      LINE.COUNT = LINE.COUNT+1
   END CASE
   PREV.CCTR = JE.CCTR<1,PTR>
   PREV.DEPT = JE.DEPT<1,PTR>
   PREV.DIV = JE.DIV<1,PTR>
   PREV.ACCT = JE.ACCT<1,PTR>
   RETURN
*
*---- ADD TOTALS AND PRINT DETAIL
*
1000 *
   CCTR.DB = CCTR.DB+JE.DB.AMT<1,PTR>
   CCTR.CR = CCTR.CR+JE.CR.AMT<1,PTR>
   DEPT.DB = DEPT.DB+JE.DB.AMT<1,PTR>
   DEPT.CR = DEPT.CR+JE.CR.AMT<1,PTR>
   DIV.DB = DIV.DB+JE.DB.AMT<1,PTR>
   DIV.CR = DIV.CR+JE.CR.AMT<1,PTR>
   ACCT.DB = ACCT.DB+JE.DB.AMT<1,PTR>
   ACCT.CR = ACCT.CR+JE.CR.AMT<1,PTR>
   TOT.DB = TOT.DB+JE.DB.AMT<1,PTR>
   TOT.CR = TOT.CR+JE.CR.AMT<1,PTR>
   IF LINE.COUNT > 50 THEN GOSUB 9000
   P.LINE = JE.OPER "L#4 "
   P.LINE = P.LINE : KEY[4,99] "L#9 "
   P.LINE = P.LINE : OCONV(JE.DATE,"D2/") "L#8 "
   IF JE.STATUS = "" THEN
      IF FILE = "JE" THEN
         P.LINE = P.LINE : JE.CMT<1,PTR> "L#30 "
      END ELSE
         P.LINE = P.LINE : JE.DESC "L#30 "
      END
   END ELSE
      P.LINE = P.LINE : JE.STATUS "L#30 "
   END
   P.LINE = P.LINE : JE.ACCT<1,PTR> STRUC : " "
   P.LINE = P.LINE : JE.DIV<1,PTR> "R#2 "
   P.LINE = P.LINE : JE.DEPT<1,PTR> "R#2 "
   P.LINE = P.LINE : JE.CCTR<1,PTR> "R#3 "
   P.LINE = P.LINE : COA.DESC "R#25 "
   P.LINE = P.LINE : JE.PER.CNT "R#2 "
   P.LINE = P.LINE : OCONV(JE.DB.AMT<1,PTR>,"MD2Z") "R#12 "
   P.LINE = P.LINE : OCONV(JE.CR.AMT<1,PTR>,"MD2Z") "R#12"
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT+1
   RETURN
*
*---- PRINT TOTAL FOR CCTR
*
2000 *
   IF LEVEL > 2 THEN
      IF LINE.COUNT > 50 THEN GOSUB 9000
      P.LINE = SPACE(59) : "TOTAL FOR CCTR " :  PREV.CCTR "L#33"
      P.LINE = P.LINE : OCONV(CCTR.DB,"MD2Z") "R#12 "
      P.LINE = P.LINE : OCONV(CCTR.CR,"MD2Z") "R#12"
      PRINT T.LINE
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT+2
   END
   MAT TOT.CCTR = 0
   RETURN
*
*---- PRINT TOTAL FOR DEPT
*
3000 *
   IF LEVEL > 1 THEN
      IF LINE.COUNT > 50 THEN GOSUB 9000
      P.LINE = SPACE(56) : "TOTAL FOR DEPT " :  PREV.DEPT "L#36"
      P.LINE = P.LINE : OCONV(DEPT.DB,"MD2Z") "R#12 "
      P.LINE = P.LINE : OCONV(DEPT.CR,"MD2Z") "R#12"
      PRINT T.LINE
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT+2
   END
   MAT TOT.DEPT = 0
   RETURN
*
*---- PRINT TOTAL FOR DIV
*
4000 *
   IF LEVEL > 0 THEN
      IF LINE.COUNT > 50 THEN GOSUB 9000
      P.LINE = SPACE(49) : "TOTAL FOR DIVISION " :  PREV.DIV "L#39"
      P.LINE = P.LINE : OCONV(DIV.DB,"MD2Z") "R#12 "
      P.LINE = P.LINE : OCONV(DIV.CR,"MD2Z") "R#12"
      PRINT T.LINE
      PRINT P.LINE
      LINE.COUNT = LINE.COUNT+2
   END
   MAT TOT.DIV = 0
   RETURN
*
*---- PRINT TOTAL FOR ACCT
*
5000 *
   IF LINE.COUNT > 50 THEN GOSUB 9000
   P.LINE = SPACE(37) : "TOTAL FOR ACCOUNT " :  PREV.ACCT STRUC : SPACE(40)
   P.LINE = P.LINE : OCONV(ACCT.DB,"MD2Z") "R#12 "
   P.LINE = P.LINE : OCONV(ACCT.CR,"MD2Z") "R#12"
   PRINT T.LINE
   PRINT P.LINE
   LINE.COUNT = LINE.COUNT+2
   MAT TOT.ACCT = 0
   RETURN
*
*---- PRINT THE HEADINGS
*
9000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO+1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT H.LINE1
   PRINT
   PRINT H.LINE2
   PRINT U.LINE
   LINE.COUNT = 6
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- END OF JOB
*
99999*
   PRINTER OFF
END
