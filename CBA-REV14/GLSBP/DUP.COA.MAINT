*COPY>CPYLIB>COM1
**************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - GLSBP
* PROGRAM      - DUP.COA.MAINT
* BY           - WALID YAMOUT ,C.B.A
* DATE         - 08/19/85
* DESCRIPTION  -
* LAST COMPILE - 414
*ENDDOC
*************************************************************************
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COA.FILE.VARS
*COPY>GLS.CPYLIB>CATG.AND.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
ERRMSG = ""
*
*--- OPEN FILES   
*
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING' ; GOSUB 91000; GOTO 999999   
OPEN '','GLS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'M.SCREENS FILE MISSING' ; GOSUB 91000; GOTO 999999   
OPEN '','PREFIX' TO PREFIX ELSE ERRMSG = 'PREFIX FILE MISSING' ; GOSUB 91000; GOTO 999999   
OPEN '','CONTROL' TO CONTROL ELSE  ERRMSG = 'CONTROL FILE IS MISSING';GOSUB 91000; GOTO 999999
OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE MISSING' ; GOSUB 91000; GOTO 999999   
OPEN 'COA.XREF' TO COA.XREF ELSE ERRMSG = 'COA.XREF FILE MISSING' ; GOSUB 91000; GOTO 999999   
OPEN '','CATG.AND.SUB' TO CATG.AND.SUB ELSE  ERRMSG = 'CATG.AND.SUB FILE IS MISSING';GOSUB 91000; GOTO 999999
*
*--- GET COMPANY NAME   
*
    MAT COMP.REC = ''
    CONO = ""
    CALL GET.CONO(CONO,MAT COMP.REC)
    IF CONO = 'END' THEN GOTO 999999
*
*--- SCREEN
*
   MAT EDIT.COM.DRIVER = ""
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = "DUP.COA.MAINT"
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   ECD.ACTION = 2; CALL SCRN.EDIT
   MAT SCV.REC = ""
100*
   FOR OPTION = 1 TO 2 UNTIL ECD.RET.VALUE = "END"
     ON OPTION GOSUB 1000,2000
   NEXT OPTION
   IF ECD.RET.VALUE = "END" THEN GOTO 999999
500*
  ECD.NUM = 11; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
  ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "E" OR OPTION = "END"
      GOTO 999999
    CASE OPTION = "C"
      GOSUB 10000
      GOTO 999999
  END CASE
  GOTO 500
*********************
*--- SUBROUTINES ---*
*********************
*
*--- FROM COMPANY
*
1000*
   ECD.NUM = 1; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 1999
   MATREAD COMP.REC FROM COMPANY, ECD.RET.VALUE ELSE
     ERRMSG = "COMPANY IS NOT ON FILE"; GOSUB 91000; GOTO 1000
   END
   F.CONO = ECD.RET.VALUE
   F.LEN = LEN(CO.ACCT.PIC)
   F.PIC = CO.ACCT.PIC
   F.MASK = CO.ACCT.MASK
   F.STRUC = CO.ACCT.STRUC
   F.MATCH = CO.ACCT.MATCH
   ECD.NUM = 2; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.NAME
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 3; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.ACCT.PIC
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 4; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.ACCT.MATCH
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 5; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.ACCT.STRUC
   ECD.ACTION = 5; CALL SCRN.EDIT
1999*
RETURN
*
*--- TO COMPANY
*
2000*
   ECD.NUM = 6; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = "END" THEN GOTO 2999
   IF ECD.RET.VALUE = F.CONO THEN
     ERRMSG = "FROM COMPANY AND TO COMPANY ARE THE SAME"; GOSUB 91000; GOTO 2000
   END
   MATREAD COMP.REC FROM COMPANY, ECD.RET.VALUE ELSE
     ERRMSG = "COMPANY IS NOT ON FILE"; GOSUB 91000; GOTO 2000
   END
   ECD.NUM = 7; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.NAME
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 8; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.ACCT.PIC
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 9; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.ACCT.MATCH
   ECD.ACTION = 5; CALL SCRN.EDIT
   ECD.NUM = 10; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = CO.ACCT.STRUC
   ECD.ACTION = 5; CALL SCRN.EDIT
   IF F.LEN # LEN(CO.ACCT.PIC) THEN
     ERRMSG = "MISMATCH IN ACCOUNT LENGTH"; GOSUB 91000; GOTO 2000
   END
   IF F.MATCH # CO.ACCT.MATCH THEN
     ERRMSG = "MISMATCH IN ACCOUNT MATCH"; GOSUB 91000; GOTO 2000
   END
   IF F.STRUC # CO.ACCT.STRUC THEN
     ERRMSG = "MISMATCH IN ACCOUNT STRUCTURE"; GOSUB 91000; GOTO 2000
   END
   IF F.MASK # CO.ACCT.MASK THEN
     ERRMSG = "MISMATCH IN ACCOUNT MASK"; GOSUB 91000; GOTO 2000
   END
   IF F.PIC # CO.ACCT.PIC THEN
     ERRMSG = "MISMATCH IN ACCOUNT PICTURE"; GOSUB 91000
   END
   T.CONO = ECD.RET.VALUE
2999*
RETURN
********************
*--- COPY FILES ---*
********************
*
*--- SELECT AND COPY COA FILE
*
10000*
   SELECT COA
10100*
   READNEXT ID ELSE GOTO 15000
   IF ID[1,3] # F.CONO THEN GOTO 10100
   MATREADU COA.REC FROM COA, ID ELSE GOTO 10200
   ACCT.NO = ID[4,F.LEN]
   READU REC FROM COA, T.CONO : ACCT.NO ELSE REC = ""
   MATWRITE COA.REC ON COA, T.CONO : ACCT.NO
   NEW.NAME = COA.L.DESC
   CALL GEN.XREF.MAINT(CONO,ACCT.NO,'',NEW.NAME,COA.XREF,PREFIX)
10200*
   RELEASE COA, ID
   GOTO 10100
*
*--- SELECT AND COPY CATG.AND.SUB FILE
*
15000*
   SELECT CATG.AND.SUB
15100*
   READNEXT ID ELSE GOTO 19999
   IF ID[1,3] # F.CONO THEN GOTO 15100
   MATREADU CAS.REC FROM CATG.AND.SUB, ID ELSE GOTO 15200
   KEY = ID[4,99]
   READU REC FROM CATG.AND.SUB, T.CONO : KEY ELSE REC = ""
   MATWRITE CAS.REC ON CATG.AND.SUB, T.CONO : KEY
15200*
   RELEASE CATG.AND.SUB, ID
   GOTO 15100
19999*
RETURN
*
*--- ERROR ROUTINE
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
*    PRINT @(0,23): ERRMSG : CL :
*    INPUT ERRMSG,1 :
*    PRINT @(0,23): CL :
* RETURN
999999*
*    PRINT @(-1)
ECD.ACTION = 99 ; CALL SCRN.EDIT
END
