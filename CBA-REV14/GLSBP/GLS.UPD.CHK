*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* DESCRIPTION - R  = GLM.DETAIL.LIST
*               U  = COA.BAL.UPDATE
*               C  = CLEAR PRIOR ENTRY
*               F  = FILE PRIOR ENTRY
*               DR = DAILY DETAIL LIST
*               DU = DAILY BALANCE  UPDATE
*ENDOC
*T19837 rick 11/16/1995 * LOCKING PROBLEM ALLOWED MORE THAN ONE USER TO
*                         PERFORM UPDATES
*********************************************************************
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
   PROMPT ""
   PROCREAD INBUFF ELSE
      ERRMSG = "MUST RUN FROM A PROC"
      GOSUB 91000; GOTO 99999
   END
   CONO = INBUFF<1>
   TYPE = INBUFF<3>
   BEGIN CASE
      CASE TYPE = "R"
      CASE TYPE = "U"
      CASE TYPE = "C"
      CASE TYPE = "F"
      CASE TYPE = "DR"
      CASE TYPE = "DU"
      CASE 1
         ERRMSG = "INVALID PARAMETER PASSED FROM PROC"
         GOSUB 91000
         GOTO 88888
   END CASE
   OPEN '','CONTROL' TO CONTROL ELSE
      ERRMSG = 'CANNOT LOCATE CONTROL FILE'
      GOSUB 91000
      GOTO 88888
   END
   OPEN '','SECURITY' TO SECURITY ELSE
      ERRMSG = 'CANNOT LOCATE SECURITY FILE'
      GOSUB 91000
      GOTO 88888
   END
   PORT = 'TTY'
   CALL SYSVARS.SUB(PORT)
   READV USER.ID FROM SECURITY, 'R.':PORT,2 ELSE USER.ID = ''
   PORT = USER.ID
   ACCT = @WHO
   READU ITEM FROM CONTROL, CONO:"GLS.UPD.CHK" ELSE ITEM = ""
   ERRMSG = ""
   BEGIN CASE
      CASE ITEM<2> # "" AND ITEM<2> # PORT
         BEGIN CASE
            CASE ITEM<3> = "R"
               ERRMSG = 'NEED TO WAIT FOR USER (':ITEM<2>:') TO FINISH ACCOUNT DETAIL REPORT'
            CASE ITEM<3> = "U"
               ERRMSG = 'NEED TO WAIT FOR USER (':ITEM<2>:') TO FINISH COA BALANCE UPDATE'
            CASE ITEM<3> = "DR"
               ERRMSG = 'NEED TO WAIT FOR USER (':ITEM<2>:') TO FINISH ACCOUNT CURRENT DETAIL REPORT'
            CASE 1
               ERRMSG = 'NEED TO WAIT FOR USER (':ITEM<2>:') TO FINISH COA CURRENT BALANCE UPDATE'
         END CASE
         GOSUB 91000
         RELEASE CONTROL, CONO:"GLS.UPD.CHK"
         GOTO 88888
      CASE TYPE = "C"
         ITEM<2> = ""; ITEM<3> = ""
         WRITE ITEM ON CONTROL, CONO:"GLS.UPD.CHK"
      CASE TYPE = "F"
         IF ITEM<3> = "U" AND ITEM<1> = "R" THEN ITEM<3> = "RU"
         ITEM<1> = ITEM<3>
         ITEM<2> = ""; ITEM<3> = ""
         WRITE ITEM ON CONTROL, CONO:"GLS.UPD.CHK"
      CASE TYPE = "DU" AND ITEM<1> # "DR"
         ERRMSG = "YOU MUST RUN DAILY SUMMARY POSTINGS BEFORE THIS MENU OPTION"
         GOSUB 91000
         RELEASE CONTROL, CONO:"GLS.UPD.CHK"
         GOTO 88888
      CASE 1
         ITEM<3> = TYPE
         ITEM<2> = PORT
         WRITE ITEM ON CONTROL, CONO:"GLS.UPD.CHK"
   END CASE
   GOTO 99999
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
88888 INBUFF<3> = "END"
   PROCWRITE INBUFF
99999 END
