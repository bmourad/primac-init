   SUBROUTINE INVOICE.INQ(INV.ID)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COA
*********************************************************************
* REVISION    - [10A]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - GLSBP
* PROGRAM     - INVOICE.INQ
* BY          - CINDY STRAIT
* DATE        - 08/29/95
* DESCRIPTION -
*   This program is an inquiry for invoice activities
*T25978 adelgado 02/07/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COA.FILE.VARS
*COPY>PMC.CPYLIB>SALESMAN
*COPY>GLS.CPYLIB>GLM
*COPY>GLS.CPYLIB>COA.BAL
*COPY>JCS.CPYLIB>INVOICE
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
   OPEN "INVOICE" TO INVOICE ELSE ERRMSG = "INVOICE FILE IS MISSING";GOSUB 91000
   OPEN "JOB" TO JOB ELSE ERRMSG = "JOB FILE IS MISSING";GOSUB 91000
   OPEN "SALESMAN" TO SALESMAN ELSE ERRMSG = "SALESMAN FILE IS MISSING";GOSUB 91000
*
   MATREAD IVC.REC FROM INVOICE,CONO:INV.ID ELSE MAT IVC.REC = ''
   MATREAD JOB.REC FROM JOB,CONO:IVC.JOB.NO ELSE MAT JOB.REC = ''
*
   LN1=0
   LINES1=DCOUNT(IVC.CHG.CODE,VM)
   OLD.START.LINE = 0
   PAGE.SIZE1 = 6
   BEGIN.PAGE1 = 14
   LINE.SPACE1 = 1
   ERRMSG = ''
*
   SCV.REC(1)<ECD.SCRN.NO> = INV.ID
   SCV.REC(2)<ECD.SCRN.NO> = IVC.CUST.NO
   SCV.REC(3)<ECD.SCRN.NO> = IVC.CUST.NAME
   SCV.REC(4)<ECD.SCRN.NO> = IVC.ADDR1
   SCV.REC(5)<ECD.SCRN.NO> = IVC.ADDR2
   SCV.REC(6)<ECD.SCRN.NO> = IVC.ADDR3
   SCV.REC(7)<ECD.SCRN.NO> = IVC.ADDR4
   SCV.REC(8)<ECD.SCRN.NO> = IVC.ATTENTION
   SCV.REC(9)<ECD.SCRN.NO> = JOB.SLSMN
   MATREAD SALESMAN.REC FROM SALESMAN,CONO:JOB.SLSMN ELSE MAT SALESMAN.REC = ""
   SCV.REC(10)<ECD.SCRN.NO> = SALS.NAME
   SCV.REC(11)<ECD.SCRN.NO> = IVC.PO.NO
   SCV.REC(12)<ECD.SCRN.NO> = IVC.JOB.NO
   SCV.REC(13)<ECD.SCRN.NO> = IVC.DATE
   SCV.REC(14)<ECD.SCRN.NO> = JOB.DIV
   SCV.REC(15)<ECD.SCRN.NO> = JOB.CATG
   SCV.REC(16)<ECD.SCRN.NO> = JOB.SALES.CODE
   SCV.REC(17)<ECD.SCRN.NO> = SUM(IVC.AMOUNT)
   ECD.ACTION = 3;CALL SCRN.EDIT
**
*
   GOSUB 1000
   GOTO 9000
*
1000 START.LINE = 1 + INT((LN1-1)/PAGE.SIZE1)*PAGE.SIZE1
   LAST.LINE = START.LINE + PAGE.SIZE1- 1
   IF LAST.LINE > LINES1 THEN LAST.LINE = LINES1
   IF START.LINE = OLD.START.LINE THEN GOTO 1999
   OLD.START.LINE = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(N-1,PAGE.SIZE1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#2" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      IF IVC.CHG.CODE<1,N> = "TAX" OR IVC.CHG.CODE<1,N> = "MSC" THEN
         P_X  = 3 ; P_Y = SLN ; P_VALUE = IVC.TAX.JURS<1,N> "L#4" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END ELSE
         P_X  = 3 ; P_Y = SLN ; P_VALUE = IVC.CHG.CODE<1,N> "L#4" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      P_X  = 8 ; P_Y = SLN ; P_VALUE = IVC.QUANTITY<1,N> "R#7" ; P_OPT = ""
      P_X  := AM:15 ; P_Y := AM:SLN ; P_VALUE := AM:IVC.UM<1,N> "L#3"
      P_X  := AM:19 ; P_Y := AM:SLN ; P_VALUE := AM:IVC.DESC<1,N> "L#40"
      P_X  := AM:61 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(IVC.UNIT.PRICE<1,N>,"MD3") "R#8"
      P_X  := AM:70 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(IVC.AMOUNT<1,N>,"MD2") "R#9"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE1
      SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(M-1,PAGE.SIZE1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
1999 RETURN
*
*
8000*
   ECD.MINV = 1
   ECD.MAXV = LINES1
   ECD.NUM = 51
   ECD.ACTION = 4; CALL SCRN.EDIT
   RETURN
9000*
   ECD.NUM=50;ECD.ACTION=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "E"
         GOTO 99999
      CASE ECD.RET.VALUE = "S"
         LN1 = LN1 + PAGE.SIZE1
         IF LN1 > LINES1 THEN
            IF IVC.CHG.CODE<1,LN1> = "" THEN LN1 = 1
         END
         GOSUB 1000
    * T25978 v
      CASE ECD.RET.VALUE = 'SR'
         LN1 -= PAGE.SIZE1
         IF LN1 < 1 THEN LN1 = 1
         GOSUB 1000
      CASE ECD.RET.VALUE = 'ST'
         LN1 = 1
         GOSUB 1000
      CASE ECD.RET.VALUE = 'SB'
         LN1 = LINES1
         GOSUB 1000
    * T25978 ^
      CASE ECD.RET.VALUE = "R"
         GOSUB 8000
         IF ECD.RET.VALUE # "END" THEN
            LNO = ECD.RET.VALUE
            IF IVC.ALLOC.AMT<1,LNO> = "" THEN
               ERRMSG = "No distribution acitivity"
               GOSUB 91000
            END ELSE
               ECD.SCRN.NO = 7
               CALL DETAIL.REV.DIST(MAT IVC.REC,LNO,"SJ")
               ECD.SCRN.NO=2
               ECD.ACTION=3;CALL SCRN.EDIT
               LN1=1; OLD.START.LINE = 0; GOSUB 1000
            END
         END
      CASE 1
         GOTO 9000
   END CASE
   GOTO 9000
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23):ERRMSG:CL:
* INPUT XX:
* CRT @(0,23):CL:
* RETURN
99999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
