*COPY>CPYLIB>COM1
*COPY>GLS.CPYLIB>COM.GLS.REPORT
*********************************************************************
* REVISION     - [08.1]
* SOURCE       - GLSBP
* PROGRAM      - REPORT.MAINT
* BY           - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE         - 11/05/84
* DESCRIPTION  -
*
*T25978 adelgado 02/07/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
**************************
* DIMENSIONS AND EQUATES *
**************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>GLS.CPYLIB>GLS.REPORT
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
**************
* OPEN FILES *
**************
  MAT FILE.VARS = ''
  OPEN '', 'GLS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'GLS.SCREENS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'GLS.REPORT' TO GLS.REPORT ELSE
    ERRMSG = 'GLS.REPORT FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'DIVISION' TO DIVISION ELSE
    ERRMSG = 'DIVISION FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'DEPARTMENT' TO DEPARTMENT ELSE
    ERRMSG = 'DEPARTMENT FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'COST.CNTR' TO COST.CNTR ELSE
    ERRMSG = 'COST.CNTR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'COA' TO COA ELSE
    ERRMSG = 'COA FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'ACCT.TYPE' TO ACCT.TYPE ELSE
    ERRMSG = 'ACCT.TYPE FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'ACCT.CATG' TO ACCT.CATG ELSE
    ERRMSG = 'ACCT.CATG FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'ACCT.SUB.CATG' TO ACCT.SUB.CATG ELSE
    ERRMSG = 'ACCT.SUB.CATG FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'CATG.AND.SUB' TO CATG.AND.SUB ELSE
    ERRMSG = 'CATG.AND.SUB FILE IS MISSING'
    GOTO 93000
  END
  MAT COMP.REC = ''
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
  IN.ACCT.LEN = LEN(CO.ACCT.PIC)
******************
* INITIALIZATION *
******************
*    MAT EDIT.COM = ''
*    TYP = 0
*    CALL EDIT.SUB
*    FILL = "#"
  MAT EDIT.COM.DRIVER = ''
  DASHES = STR('-',80)
  UNKNOWN = STR("?",30)
  PAGE.SIZE = 4
  HBEGIN.PAGE = 13
  TBEGIN.PAGE = 6
  LINE.SPACE = 1
  REPORT.LEN = 132
*
*******************
* MAIN PROCESSING *
*******************
  ECD.SCRN.CNT = 3
  ECD.SCRN.NAME<1> = 'REPORT.MAINT'
  ECD.SCRN.NAME<2> = 'REPORT.COLUMN.SUB'
  ECD.SCRN.NAME<3> = 'REPORT.LINE.SUB'
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAIN.LOOP = 1
  LOOP
    GLRP.ID = ''
    MAT GLRP.REC = ''
    MAT SCV.REC = ""
    ECD.ACTION=6;CALL SCRN.EDIT
    ECD.NUM = 1
*        ECD.VALDAT.CODE = '4'
*        ECD.VALDAT.FILE = GLS.REPORT
*        ECD.PREFIX.ID = CONO
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE = "END" THEN
      MAIN.LOOP = 0
    END ELSE
      * T26126 v
      MATREADU GLRP.REC FROM GLS.REPORT, CONO:ECD.RET.VALUE LOCKED
        ERRMSG = 'Record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000 
      END THEN
      * T26126 ^
*        BEGIN CASE
*           CASE ECD.RET.VALUE = 'END'
*              MAIN.LOOP = 0
*           CASE ECD.RET.VALUE # '' AND  ECD.VALDAT.ITEM # ''
*              FOR I = 1 TO GLRP.REC.SIZE
*                 GLRP.REC(I) = ECD.VALDAT.ITEM<I>
*              NEXT I
        GLRP.ID = ECD.RET.VALUE
        SCV.REC(2)<1> = GLRP.DESC
        SCV.REC(3)<1> = GLRP.GRP.LEN
        ECD.ACTION = 3; CALL SCRN.EDIT
        TLINES = COUNT(GLRP.TITLE,VM) + (GLRP.TITLE # '')
        TLN = 1; OLD.START.TLINE = 0; GOSUB 11000
        HLINES = COUNT(GLRP.HD.COL,VM) + (GLRP.HD.COL # '')
        HLN = 1; OLD.START.HLINE = 0; GOSUB 12000
        GOSUB 100
*           CASE ECD.RET.VALUE # ''
      END ELSE
        GLRP.ID = ECD.RET.VALUE
        TLINES = 0; OLD.START.TLINE = 0
        HLINES = 0; OLD.START.HLINE = 0
        FOR ANS = 1 TO 4 WHILE ECD.RET.VALUE # 'END'
          ON ANS GOSUB 1000,2000,3000,4000
        NEXT ANS
        IF ECD.RET.VALUE # 'END' THEN GOSUB 100
*        END CASE
      END
    END
  WHILE MAIN.LOOP DO REPEAT
  GOTO 99999
100 CHANGE = 1
  LOOP
    ECD.NUM = 4
    ECD.ACTION = 4; CALL SCRN.EDIT
    SCV.REC(ECD.NUM)<1> = ''
    ANS = ECD.RET.VALUE
    BEGIN CASE
      CASE ANS = 'E' OR ANS = 'END'
        RELEASE GLS.REPORT, CONO : GLRP.ID
        CHANGE = 0
      CASE NUM(ANS)
        ON ANS GOSUB 1000,2000,3000,4000
      CASE ANS = 'C'
        ECD.SCRN.NO = 2
        SCV.REC(1)<2> = GLRP.ID
        SCV.REC(2)<2> = GLRP.DESC
        CALL REPORT.COLUMN.SUB(CONO)
        ECD.SCRN.NO = 1
        ECD.ACTION = 3; CALL SCRN.EDIT
        OLD.START.TLINE = 0; GOSUB 11000
        OLD.START.HLINE = 0; GOSUB 12000
      CASE ANS = 'L'
        ECD.SCRN.NO = 3
        SCV.REC(1)<3> = GLRP.ID
        SCV.REC(2)<3> = GLRP.DESC
        CALL REPORT.LINE.SUB(CONO,IN.ACCT.LEN,CO.ACCT.MASK)
        ECD.SCRN.NO = 1
        ECD.ACTION = 3; CALL SCRN.EDIT
        OLD.START.TLINE = 0; GOSUB 11000
        OLD.START.HLINE = 0; GOSUB 12000
      CASE ANS = 'F'
        MATWRITE GLRP.REC ON GLS.REPORT, CONO : GLRP.ID
        CHANGE = 0
      CASE ANS = 'M'
        ECD.NUM = 7
        ECD.VALDAT.CODE = '3'
        ECD.VALDAT.FILE = GLS.REPORT
        ECD.PREFIX.ID = CONO
        ECD.ACTION=4;CALL SCRN.EDIT
        IF ECD.RET.VALUE # 'END' THEN
          GLRP.ID = ECD.RET.VALUE
          ECD.NUM = 1; SCV.REC(ECD.NUM)<1> = GLRP.ID
          ECD.ACTION = 5; CALL SCRN.EDIT
        END
      CASE ANS = 'D'
        ECD.NUM = 8
        ECD.ACTION = 4;CALL SCRN.EDIT
        SCV.REC(ECD.NUM)<1> = ''
        CHOICE = ECD.RET.VALUE
        IF CHOICE = "Y" THEN
          DELETE GLS.REPORT, CONO : GLRP.ID
          CHANGE = 0
        END
    END CASE
  WHILE CHANGE DO REPEAT
  RETURN
1000 ECD.NUM = 2; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN GLRP.DESC = ECD.RET.VALUE
  RETURN
2000 IF GLRP.TITLE = '' THEN
    REQUEST = 'A'
    TLINES = 0
    LOOP
      TLN = TLINES + 1
      OLD.TLINES = TLINES
      GOSUB 2100
    WHILE TLINES > OLD.TLINES DO REPEAT
    TLN = TLINES
  END
  MORE = 1
  LOOP
    ECD.NUM = 5
    ECD.ACTION=4;CALL SCRN.EDIT
    SCV.REC(ECD.NUM)<1> = ''
    REQUEST = ECD.RET.VALUE
    BEGIN CASE
      CASE REQUEST = '' OR REQUEST = 'END'
        MORE = 0
      CASE REQUEST = 'A'
        LOOP
          TLN = TLINES + 1
          OLD.TLINES = TLINES
          GOSUB 2100
        WHILE TLINES > OLD.TLINES DO REPEAT
        TLN = TLINES
        GOSUB 11000
      CASE REQUEST = 'C'
        GOSUB 9100
        IF LN.NO THEN
          TLN = LN.NO
          GOSUB 2100
        END
      CASE REQUEST = 'D'
        GOSUB 9100
        IF LN.NO THEN
          TLN = LN.NO
          GLRP.TITLE = DELETE(GLRP.TITLE,1,TLN,0)
          TLINES = COUNT(GLRP.TITLE,VM) + (GLRP.TITLE # '')
          IF TLN > 1 AND TLN > TLINES THEN TLN = TLN - 1
          IF TLN < 1 THEN TLN = 1
          OLD.START.TLINE = 0
        END
        GOSUB 11000
      CASE REQUEST = 'S'
        TLN = 1 + INT((TLN - 1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
        IF TLN > TLINES THEN TLN = 1
        GOSUB 11000
      * T25978 v
      CASE REQUEST = 'SR'
        TLN = 1 + INT((TLN - 1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
        IF TLN < 1 THEN TLN = 1
        GOSUB 11000
      CASE REQUEST = 'ST'
        TLN = 1
        GOSUB 11000
      CASE REQUEST = 'SB'
        TLN = TLINES
        GOSUB 11000
      * T25978 ^
    END CASE
  WHILE MORE DO REPEAT
  P_X  = 0 ; P_Y = 21 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RETURN
2100 GOSUB 11000
  TSLN = TBEGIN.PAGE + LINE.SPACE * MOD(TLN-1,PAGE.SIZE)
  P_X  = 3 ; P_Y = TSLN ; P_VALUE = TLN "R#2" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
2200 X = 6; Y = TSLN
  TYP = 1; MAXL = 60
  IF GLRP.TITLE<1,TLN> = '' THEN
    O.R = 'R'
  END ELSE
    O.R = 'O'; DEFAULT = GLRP.TITLE<1,TLN>
  END
  CALL EDIT.SUB
  IF VALUE = 'END' THEN
    IF REQUEST = 'A' THEN
      GLRP.TITLE = DELETE(GLRP.TITLE,1,TLN,0)
    END
    P_X  = 0 ; P_Y = TSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 2999
  END
  GLRP.TITLE<1,TLN> = VALUE
  TLINES = COUNT(GLRP.TITLE,VM) + (GLRP.TITLE # '')
2999 RETURN
3000 IF GLRP.HD.COL = '' THEN
    REQUEST = 'A'
    HLINES = 0
    LOOP
      HLN = HLINES + 1
      OLD.HLINES = HLINES
      GOSUB 3100
    WHILE HLINES > OLD.HLINES DO REPEAT
    HLN = HLINES
  END
  MORE = 1
  LOOP
    ECD.NUM = 5
    ECD.ACTION=4;CALL SCRN.EDIT
    SCV.REC(ECD.NUM)<1> = ''
    REQUEST = ECD.RET.VALUE
    BEGIN CASE
      CASE REQUEST = '' OR REQUEST = 'END'
        MORE = 0
      CASE REQUEST = 'A'
        LOOP
          HLN = HLINES + 1
          OLD.HLINES = HLINES
          GOSUB 3100
        WHILE HLINES > OLD.HLINES DO REPEAT
        HLN = HLINES
        GOSUB 12000
      CASE REQUEST = 'C'
        GOSUB 9200
        IF LN.NO THEN
          HLN = LN.NO
          GOSUB 3100
        END
      CASE REQUEST = 'D'
        GOSUB 9200
        IF LN.NO THEN
          HLN = LN.NO
          GLRP.HD.COL = DELETE(GLRP.HD.COL,1,HLN,0)
          GLRP.HD.DESC = DELETE(GLRP.HD.DESC,1,HLN,0)
          HLINES = COUNT(GLRP.HD.COL,VM) + (GLRP.HD.COL # '')
          IF HLN > 1 AND HLN > HLINES THEN HLN = HLN - 1
          IF HLN < 1 THEN HLN = 1
          OLD.START.HLINE = 0
        END
        GOSUB 12000
      CASE REQUEST = 'S'
        HLN = 1 + INT((HLN - 1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
        IF HLN > HLINES THEN HLN = 1
        GOSUB 12000
      * T25978 v
      CASE REQUEST = 'SR'
        HLN = 1 + INT((HLN - 1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
        IF HLN < 1 THEN HLN = 1
        GOSUB 12000
      CASE REQUEST = 'ST'
        HLN = 1
        GOSUB 12000
      CASE REQUEST = 'SB'
        HLN = HLINES
        GOSUB 12000
      * T25978 ^
    END CASE
  WHILE MORE DO REPEAT
  P_X  = 0 ; P_Y = 21 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RETURN
3100 GOSUB 12000
  HSLN = HBEGIN.PAGE + LINE.SPACE * MOD(HLN-1,PAGE.SIZE)
  P_X  = 3 ; P_Y = HSLN ; P_VALUE = HLN "R#2" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
3200 X = 6; Y = HSLN
  TYP = 3; MAXL = 3
  IF HLN = 1 THEN
    MINV = 1
  END ELSE
    MINV = GLRP.HD.COL<1,HLN-1> + LEN(GLRP.HD.DESC<1,HLN-1>) + 1
  END
  IF GLRP.HD.COL<1,HLN+1>+0 = 0 THEN
    MAXV = REPORT.LEN
  END ELSE
    MAXV = GLRP.HD.COL<1,HLN+1> - 2
  END
  IF GLRP.HD.COL<1,HLN> = '' THEN
    O.R = 'R'
  END ELSE
    O.R = 'O'; DEFAULT = GLRP.HD.COL<1,HLN>
  END
  CALL EDIT.SUB
  IF VALUE = 'END' THEN
    IF REQUEST = 'A' THEN
      GLRP.HD.COL = DELETE(GLRP.HD.COL,1,HLN,0)
      GLRP.HD.DESC = DELETE(GLRP.HD.DESC,1,HLN,0)
    END
    P_X  = 0 ; P_Y = HSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 3999
  END
  GLRP.HD.COL<1,HLN> = VALUE
3300 X = 10; Y = HSLN
  TYP = 1; MAXL = 60
  IF GLRP.HD.DESC<1,HLN> = '' THEN
    O.R = 'R'
  END ELSE
    O.R = 'O'; DEFAULT = GLRP.HD.DESC<1,HLN>
  END
  CALL EDIT.SUB
  IF VALUE = 'END' THEN GOTO 3100
  GLRP.HD.DESC<1,HLN> = VALUE
  HLINES = COUNT(GLRP.HD.COL,VM) + (GLRP.HD.COL # '')
3999 RETURN
4000 ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN GLRP.GRP.LEN = ECD.RET.VALUE
  RETURN
9100 GOSUB 11000
  ECD.NUM = 6
  ECD.MINV = OLD.START.TLINE
  IF LAST.TLINE < TLINES THEN
    ECD.MAXV = LAST.TLINE
  END ELSE
    ECD.MAXV = TLINES
  END
  ECD.ACTION = 4; CALL SCRN.EDIT
  LN.NO = ECD.RET.VALUE
  IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
  RETURN
9200 GOSUB 12000
  ECD.NUM = 6
  ECD.MINV = OLD.START.HLINE
  IF LAST.HLINE < HLINES THEN
    ECD.MAXV = LAST.HLINE
  END ELSE
    ECD.MAXV = HLINES
  END
  ECD.ACTION = 4; CALL SCRN.EDIT
  LN.NO = ECD.RET.VALUE
  IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
  RETURN
11000 START.TLINE = 1 + INT((TLN-1)/PAGE.SIZE)*PAGE.SIZE
  IF START.TLINE = OLD.START.TLINE THEN GOTO 11999
  OLD.START.TLINE = START.TLINE
  LAST.TLINE = START.TLINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = START.TLINE TO LAST.TLINE UNTIL N > TLINES
    TSLN = TBEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 3 ; P_Y = TSLN ; P_VALUE = N "R#2" ; P_OPT = ""
    P_X  := AM:6 ; P_Y := AM:TSLN ; P_VALUE := AM:GLRP.TITLE<1,N> "L#60"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    TSLN = TBEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = TSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
11999 RETURN
12000 START.HLINE = 1 + INT((HLN-1)/PAGE.SIZE)*PAGE.SIZE
  IF START.HLINE = OLD.START.HLINE THEN GOTO 12999
  OLD.START.HLINE = START.HLINE
  LAST.HLINE = START.HLINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = START.HLINE TO LAST.HLINE UNTIL N > HLINES
    HSLN = HBEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 3 ; P_Y = HSLN ; P_VALUE = N "R#2" ; P_OPT = ""
    P_X  := AM:6 ; P_Y := AM:HSLN ; P_VALUE := AM:GLRP.HD.COL<1,N> "R#3"
    P_X  := AM:10 ; P_Y := AM:HSLN ; P_VALUE := AM:GLRP.HD.DESC<1,N> "L#60"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    HSLN = HBEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = HSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
12999 RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23) : CL : ERRMSG : ; INPUT XX :
*       PRINT @(0,23) : CL :
*       RETURN
92000 ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
*       RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
* 93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
