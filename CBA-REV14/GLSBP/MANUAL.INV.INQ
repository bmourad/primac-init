   SUBROUTINE MANUAL.INV.INQ(MIV.ID)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COA
*********************************************************************
* REVISION    - [10A]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - GLSBP
* PROGRAM     - MANUAL.INV.INQ
* BY          - CINDY STRAIT
* DATE        - 08/29/95
* DESCRIPTION -
*   This program is an inquiry for manual invoice activities
*T25978 adelgado 02/07/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQUATES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>COA.FILE.VARS
*COPY>PMC.CPYLIB>SALESMAN
*COPY>GLS.CPYLIB>GLM
*COPY>GLS.CPYLIB>COA.BAL
*COPY>ARS.CPYLIB>MANUAL.INVOICE
*COPY>GLS.CPYLIB>GLX
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
   OPEN "MANUAL.INVOICE" TO MANUAL.INVOICE ELSE ERRMSG = "MANUAL.INVOICE FILE IS MISSING";GOSUB 91000
   OPEN "SALESMAN" TO SALESMAN ELSE ERRMSG = "SALESMAN FILE IS MISSING";GOSUB 91000
*
   MATREAD MIV.REC FROM MANUAL.INVOICE,CONO:MIV.ID ELSE MAT MIV.REC = ''
*
   LN1=0
   LINES1=DCOUNT(MIV.CHG.CODE,VM)
   OLD.START.LINE = 0
   PAGE.SIZE1 = 6
   BEGIN.PAGE1 = 14
   LINE.SPACE1 = 1
   ERRMSG = ''
*
   SCV.REC(1)<ECD.SCRN.NO> = MIV.ID
   SCV.REC(2)<ECD.SCRN.NO> = MIV.CUST.NO
   SCV.REC(3)<ECD.SCRN.NO> = MIV.CUST.NAME
   SCV.REC(4)<ECD.SCRN.NO> = MIV.ADDR1
   SCV.REC(5)<ECD.SCRN.NO> = MIV.ADDR2
   SCV.REC(6)<ECD.SCRN.NO> = MIV.ADDR3
   SCV.REC(7)<ECD.SCRN.NO> = MIV.ADDR4
   SCV.REC(8)<ECD.SCRN.NO> = MIV.ATTENTION
   SCV.REC(9)<ECD.SCRN.NO> = MIV.SLSMAN
   MATREAD SALESMAN.REC FROM SALESMAN,CONO:MIV.SLSMAN ELSE MAT SALESMAN.REC = ""
   SCV.REC(10)<ECD.SCRN.NO> = SALS.NAME
   SCV.REC(11)<ECD.SCRN.NO> = MIV.PO
   SCV.REC(12)<ECD.SCRN.NO> = MIV.JOB.NO
   SCV.REC(13)<ECD.SCRN.NO> = MIV.DATE
   SCV.REC(14)<ECD.SCRN.NO> = MIV.DIV
   SCV.REC(15)<ECD.SCRN.NO> = MIV.DEPT
   SCV.REC(16)<ECD.SCRN.NO> = MIV.SALES.CODE
   SCV.REC(17)<ECD.SCRN.NO> = MIV.INV.AMT
   ECD.ACTION = 3;CALL SCRN.EDIT
**
*
   GOSUB 1000
   GOTO 9000
*
1000 START.LINE = 1 + INT((LN1-1)/PAGE.SIZE1)*PAGE.SIZE1
   LAST.LINE = START.LINE + PAGE.SIZE1- 1
   IF LAST.LINE > LINES1 THEN LAST.LINE = LINES1
   IF START.LINE = OLD.START.LINE THEN GOTO 1999
   OLD.START.LINE = START.LINE
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(N-1,PAGE.SIZE1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      IF MIV.CHG.CODE<1,N> = "TAX" OR MIV.CHG.CODE<1,N> = "MSC" THEN
         IF MIV.TAX.JURS<1,N> # "" THEN
            P_X  = 4 ; P_Y = SLN ; P_VALUE = MIV.TAX.JURS<1,N> "L#4" ; P_OPT = ""
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         END ELSE
            P_X  = 4 ; P_Y = SLN ; P_VALUE = MIV.SALES.CODE "L#4" ; P_OPT = ""
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         END
      END ELSE
         P_X  = 4 ; P_Y = SLN ; P_VALUE = MIV.CHG.CODE<1,N> "L#4" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      P_X  = 9 ; P_Y = SLN ; P_VALUE = MIV.QUANTITY<1,N> "R#7" ; P_OPT = ""
      P_X  := AM:17 ; P_Y := AM:SLN ; P_VALUE := AM:MIV.UM<1,N> "L#3"
      P_X  := AM:21 ; P_Y := AM:SLN ; P_VALUE := AM:MIV.DESC<1,N> "L#40"
      P_X  := AM:62 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MIV.UNIT.PRICE<1,N>,"MD3") "R#8"
      P_X  := AM:71 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MIV.AMOUNT<1,N>,"MD2") "R#9"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE1
      SLN = BEGIN.PAGE1 + LINE.SPACE1 * MOD(M-1,PAGE.SIZE1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
1999 RETURN
*
*
8000*
   ECD.MINV = 1
   ECD.MAXV = LINES1
   ECD.NUM = 51
   ECD.ACTION = 4; CALL SCRN.EDIT
   RETURN
9000*
   ECD.NUM=50;ECD.ACTION=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "E"
         GOTO 99999
      CASE ECD.RET.VALUE = "S"
         LN1 = LN1 + PAGE.SIZE1
         IF LN1 > LINES1 THEN
            IF MIV.CHG.CODE<1,LN1> = "" THEN LN1 = 1
         END
         GOSUB 1000
    * T25978 v
      CASE ECD.RET.VALUE = 'SR'
         LN1 -= PAGE.SIZE1
         IF LN1 < 1 THEN LN1 = 1
         GOSUB 1000
      CASE ECD.RET.VALUE = 'ST'
         LN1 = 1
         GOSUB 1000
      CASE ECD.RET.VALUE = 'SB'
         LN1 = LINES1
         GOSUB 1000
    * T25978 ^
      CASE ECD.RET.VALUE = "R"
         GOSUB 8000
         IF ECD.RET.VALUE # "END" THEN
            LNO = ECD.RET.VALUE
            IF MIV.ALLOC.AMT<1,LNO> = "" THEN
               ERRMSG = "No distribution acitivity"
               GOSUB 91000
            END ELSE
               ECD.SCRN.NO = 7
               CALL DETAIL.REV.DIST(MAT MIV.REC,LNO,"MJ")
               ECD.SCRN.NO=3;ECD.ACTION=3;CALL SCRN.EDIT
               LN1 = 1; OLD.START.LINE = 0; GOSUB 1000
            END
         END
      CASE 1
         GOTO 9000
   END CASE
   GOTO 9000
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000 PRINT @(0,23):ERRMSG:CL:
* INPUT XX:
* CRT @(0,23):CL:
* RETURN
99999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
