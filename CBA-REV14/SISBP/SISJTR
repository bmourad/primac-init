   SUBROUTINE SISJTR (JOBNO, SHIFT, TRANS, HRS, JOB.TRANS, MAT SISTRN_REC)
*********************************************************************
*
* PROGRAM  - SISJTR
*
* DESCRIPTION
*
* This subroutine is used to update JOB.TRANS file with labor data
* reported from the shop floor.
*
* T 23902 - SARA CRISMON APRIL 1999
*           ADDED CODE TO PASS TRANSACTION TO NEW POWERBUILDER PSS
*
*T28778 lross 01/27/2006 * JTRN.IMP OVERSTATED BY * 100.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>SIS.CPYLIB>SIS_TRANS
*COPY>JCS.CPYLIB>JOB.TRANS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   MATREADU JTRN.REC FROM JOB.TRANS, SISTRN_CONO:JOBNO ELSE
      MAT JTRN.REC = ""
   END
*
*---- MAIN PROCESSING
*
100*
   ODATE = FIELD(SISTRN_TIMESTMP," ",1)
   OTIME = FIELD(SISTRN_TIMESTMP," ",2)
   IDATE = ICONV(ODATE,"D")
   ITIME = ICONV(OTIME,"MT")
   TIMESTMP = IDATE*100000 + ITIME
*
   LOCATE TIMESTMP+1 IN JTRN.TIME.STAMP<1>,1 BY "AR" SETTING TSP ELSE NULL
   JTRN.TIME.STAMP = INSERT(JTRN.TIME.STAMP,1,TSP,0,TIMESTMP)
   JTRN.TRANS = INSERT(JTRN.TRANS,1,TSP,0,TRANS)
   JTRN.DEPT = INSERT(JTRN.DEPT,1,TSP,0,SISTRN_DEPT)
   JTRN.CCTR = INSERT(JTRN.CCTR,1,TSP,0,SISTRN_CCTR)
   JTRN.OPER = INSERT(JTRN.OPER,1,TSP,0,SISTRN_OPER)
   JTRN.EMP = INSERT(JTRN.EMP,1,TSP,0,SISTRN_EMP)
   JTRN.HRS = INSERT(JTRN.HRS,1,TSP,0,HRS)
*T28778JTRN.IMP = INSERT(JTRN.IMP,1,TSP,0,SISTRN_UNITS*100)
   JTRN.IMP = INSERT(JTRN.IMP,1,TSP,0,SISTRN_UNITS)
   JTRN.SHIFT = INSERT(JTRN.SHIFT,1,TSP,0,SHIFT)
   JTRN.CMP.FLG = INSERT(JTRN.CMP.FLG,1,TSP,0,SISTRN_COMPLETE)
   JTRN.OPER.TYPE = INSERT(JTRN.OPER.TYPE,1,TSP,0,"")
   JTRN.POST.FLAG = INSERT(JTRN.POST.FLAG,1,TSP,0,"")
   MATWRITE JTRN.REC ON JOB.TRANS, SISTRN_CONO:JOBNO
* T 23902
*
*--- The following call to write PowerBuilder flat file
   CALL HNP.PSSTRANS.SIS (JOBNO,SHIFT,TRANS,HRS,JOB.TRANS, MAT SISTRN_REC)
* T 23902
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
