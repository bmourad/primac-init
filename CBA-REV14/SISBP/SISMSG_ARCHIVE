*COPY>CPYLIB>COM1
*********************************************************************
*
* PROGRAM  - SISMSG_ARCHIVE
*
*********************************************************************
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>SIS.CPYLIB>SIS_TRANS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- PRE-INITIALIZATION
*
   PROMPT ""
   CONO = ""
*
*---- GET COMPANY
*
   OPEN "","CONTROL" TO CONTROL ELSE
      CRT @(0,23):@(-4):"CANNOT OPEN CONTROL FILE":
      INPUT REPLY,1:
      GOTO 99999
   END
   OPEN "","COMPANY" TO COMPANY ELSE
      CRT @(0,23):@(-4):"CANNOT OPEN COMPANY FILE":
      INPUT REPLY,1:
      GOTO 99999
   END
   CALL GET.CONO (CONO, MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
*
*---- OPEN ALL FILES
*
   OPEN "","SIS_CONTROL" TO SIS_CONTROL ELSE
      CRT @(0,23):@(-4):"CANNOT OPEN SIS_CONTROL FILE":
      INPUT REPLY,1:
      GOTO 99999
   END
   OPEN "","SIS_TRANS" TO SIS_TRANS ELSE
      CRT @(0,23):@(-4):"CANNOT OPEN SIS_TRANS FILE":
      INPUT REPLY,1:
      GOTO 99999
   END
   OPEN "","SIS_HIST" TO SIS_HIST ELSE
      CRT @(0,23):@(-4):"CANNOT OPEN SIS_HIST FILE":
      INPUT REPLY,1:
      GOTO 99999
   END
*
*---- INITIALIZATION
*
   DASHES = STR("-",80)
   ACTIVE = 0
   CDATE = DATE()
   CTIME = TIME()
   IF CTIME < 10 THEN CDATE = DATE()
   DATE_ARCHIVE = CDATE - CO.TIME.MATL.DAYS
   ADATE = OCONV(DATE_ARCHIVE, "D4/")
   PDATETIME = OCONV(CDATE, "D/") "L#10 " : OCONV(CTIME, "MTS:")
*
   SLINE = @(-1)
   SLINE = SLINE:@(0,0):"PRIMAC"
   SLINE = SLINE:@(20,0):"Shop Floor Information System Archive Process"
   SLINE = SLINE:@(72,0):OCONV(CDATE,"D2/")
   SLINE = SLINE:@(0,1):DASHES
   SLINE = SLINE:@(0,20):DASHES
   SLINE = SLINE:@(0,22):DASHES
   CRT SLINE:
*
   READU TREC FROM SIS_CONTROL, CONO : "MSG_ARCHIVE" THEN
      TREC<1> = OCONV(TREC<1>, "D4/")
      CRT @(5,10):"LAST PROCESSED UNTIL   : " : TREC<1> "L#20":
      CRT @(5,12):"ARCHIVE MESSAGES UNTIL : " : ADATE "L#20":
   END LOCKED
      CRT @(5,10):"Shop Floor Information Archive Process is ACTIVE! ":
      ACTIVE = 1
   END ELSE
      TREC = "NONE"
      CRT @(5,10):"LAST PROCESSED UNTIL   : " : TREC "L#20":
      CRT @(5,12):"ARCHIVE MESSAGES UNTIL : " : ADATE "L#20":
   END
*
   LOOP
      IF ACTIVE THEN
         CRT @(0,21):@(-4):"Enter (E)xit: ":
      END ELSE
         CRT @(0,21):@(-4):"Enter (I)nitiate or (E)xit: ":
      END
      INPUT ACTION,3:_
      BEGIN CASE
         CASE ACTION = "E" OR ACTION = "END" OR ACTION = CHAR(27) OR ACTION = "^"
            ACTION = "END"
         CASE ACTION = "I" AND ACTIVE = 0
         CASE 1
            ACTION = ""
      END CASE
   WHILE ACTION = "" DO
   REPEAT
   CRT @(0,21):@(-4):
*
*---- MAIN PROCESSING
*
   BEGIN CASE
      CASE ACTION = "END"
         GOTO 99999
      CASE ACTION = "I";* Initate
         GOSUB 10000
   END CASE
   RELEASE SIS_CONTROL, CONO : "MSG_ARCHIVE"
   CRT @(0,21):@(-4):"Press <RETURN> to continue":
   INPUT REPLY,1:
   GOTO 99999
*---------------------*
*---- SUBROUTINES ----*
*---------------------*
*
*---- ARCHIVING PROCESS
*
10000*
   TREC = DATE_ARCHIVE
   TREC<2> = PDATETIME
   WRITEU TREC ON SIS_CONTROL, CONO : "MSG_ARCHIVE"
   SIS_STMT = 'SELECT SIS_TRANS WITH CONO = "' : CONO : '"'
   SIS_STMT := ' AND WITH PROCESSED # ""'
   SIS_STMT := ' AND WITH PROCESSED_DATE <= "' : ADATE : '"'
   UDTEXECUTE SIS_STMT CAPTURING SCREENDATA
   DONE = 0
   REC_COUNT = 0
   LOOP
      READNEXT ID ELSE DONE = 1
   UNTIL DONE DO
      REC_COUNT += 1
      IF REM(REC_COUNT,100) = 0 THEN
         CRT @(5,15):CL:"PROCESSING COUNT    : " : REC_COUNT "L#20":
      END
      MATREADU SISTRN_REC FROM SIS_TRANS, ID THEN
         READU DUMMY FROM SIS_HIST, ID ELSE DUMMY = ""
         SISTRN_PURGE = PDATETIME
         MATWRITE SISTRN_REC ON SIS_HIST, ID
         DELETE SIS_TRANS, ID
      END LOCKED
      END ELSE
         RELEASE SIS_TRANS, ID
      END
   REPEAT
   CRT @(5,15):CL:"PROCESSING COUNT    : " : REC_COUNT "L#20":
   RETURN
*
*---- END OF PROGRAM
*
99999*
END
