   SUBROUTINE REPORT.VIEW.SUB
*COPY>CPYLIB>COM1
*************************************************************
* REVISION      - [12.0]
* Copyright 1997 by Vercom Software, Inc.
* SOURCE        - CBABP
* PROGRAM       - REPORT.VIEW.SUB
* DESCRIPTION   - This program displays a report in PDF.
*
* PROGRAMS REQUIRED FOR MAIL FEATURE:
*  -GHOSTSCRIPT
*************************************************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>SECURITY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>TCC
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>PORT.CONTROL
*
*---- Initialization
*
   DOWNLOAD_PATH = "C:\PRIMAC_REPORTS\"
   DOWNLOAD_DIR = "C:\PRIMAC_REPORTS"
*
*---- Setup system error messages
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   SYSVAR = 'TTY'
   CALL SYSVARS.SUB(SYSVAR)
*
*---- Open files
*
   OPEN '','_HOLD_' TO HOLD.FILE ELSE ERRMSG = 'CANNOT LOCATE THE _HOLD_ FILE!' ; GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CANNOT LOCATE THE CONTROL FILE!' ; GOTO 93000
   OPEN '','SECURITY' TO SECURITY ELSE ERRMSG = 'CANNOT LOCATE THE SECURITY FILE!' ; GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'CANNOT LOCATE THE COMPANY FILE!' ; GOTO 93000
*
*---- Read in hold file record
*
   HF.NAME = SYSVAR:"_":OCONV(@LOGNAME,'MCU')
   READ CR FROM CONTROL,HF.NAME ELSE NULL
   DEFAULT.PTR = CR<2>
   DEFAULT.OPTION = CR<3>
   READ HOLD.REC FROM HOLD.FILE, HF.NAME ELSE HOLD.REC = ''
*
*---- Main processing
*
   CONO = ''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF HOLD.REC = '' THEN
      STMT = 'SETPTR ,,,,,1,BRIEF,NOMESSAGE'
      UDTEXECUTE STMT CAPTURING TEXT
      ERRMSG = 'There is no data to display!  <RETURN> to Exit.'
      GOSUB 91000
      GOTO 99999
   END
   ERRMSG = ""
   IN_PARAM = CONO
   IN_PARAM<2> = "PMCProcessStatus"
   OUT_PARAM = ""
   CALL PMC_GETREPORTID(ERRMSG,IN_PARAM,OUT_PARAM,HOLD.FILE,CONTROL)
   IF ERRMSG # "" THEN
      GOSUB 91000
      GOTO 99999
   END
   PPSR_ID = OUT_PARAM
   WRITE HOLD.REC ON HOLD.FILE, PPSR_ID
   MATREAD USER.REC FROM SECURITY, "R.":SYSVAR THEN
      SUBJECT = QUOTE(USER.V.DSP)
   END ELSE
      SUBJECT = "REPORT"
   END
   READ REC FROM CONTROL, CONO:"EMAIL_FORMAT" ELSE REC = ''
   BEGIN CASE
      CASE REC<1> = "RTF"
         GOSUB RTF_SUB
      CASE REC<1> = "PDF"
         GOSUB PDF_SUB
      CASE REC<1> = "SLK"
         GOSUB SLK_SUB
      CASE 1
         GOSUB PDF_SUB
   END CASE
   STATUS = 0
   SBVERSION = ""
   CALL TU.GET.VERSION(SBVERSION, STATUS)
   IF SBVERSION<1,1> = "" THEN
      GOSUB WINTEGRATE_SUB
   END ELSE
      GOSUB SBCLIENT_SUB
   END
   DELETE HOLD.FILE, HF.NAME
   DELETE HOLD.FILE, PPSR_ID
   DELETE HOLD.FILE, VIEW_ID
   GOTO 99999
*
*
*
RTF_SUB: 
   PCFILE = DOWNLOAD_PATH:PPSR_ID:".RTF"
   VIEW_ID = PPSR_ID:".rtf"
   APP_NAME = "WINWORD.EXE"
   FORMATSTR = ''
   FORMATSTR<1,1> = "WIDE REPORT"
   FORMATSTR<1,2> = "FONT SIZE"
   FORMATSTR<2,2> = 9
   CALL CONVERT.TO.RTF(CO.NAME,"_HOLD_",PPSR_ID,SUBJECT,FORMATSTR)
   RETURN
*
PDF_SUB: 
   PCFILE = DOWNLOAD_PATH:PPSR_ID:".PDF"
   APP_NAME = "ACRORD32.EXE"
   VIEW_ID = PPSR_ID:".pdf"
   CALL CONVERT.TO.PDF("_HOLD_",PPSR_ID,'')
   RETURN
*
SLK_SUB: 
   PCFILE = DOWNLOAD_PATH:PPSR_ID:".SLK"
   APP_NAME = "EXCEL.EXE"
   VIEW_ID = PPSR_ID:".slk"
   CALL CONVERT.TO.SLK("_HOLD_",PPSR_ID,'')
   RETURN
*
SBCLIENT_SUB: 
   SLEEP 1
   OSREAD HOSTFILE FROM "./_HOLD_/":VIEW_ID THEN
      OPTSX    = "OHB"
      DESCRIPT = "Hold File Transfer"
      STATUS  = ""
      TU_VERNO = ""
      TU_FUNC = "TU.DOWNLOAD":TU_VERNO
      CALL @TU_FUNC(HOSTFILE,PCFILE,OPTSX,DESCRIPT,STATUS)
   END ELSE
      STATUS = VIEW_ID : " FILE MISSING"
   END
   IF STATUS = 0 THEN
      APP_OPTIONS = "5"
      APP_STATUS = ""
      APP_NAME := " " : PCFILE
      TU_FUNC = "TU.LAUNCH.APP":TU_VERNO
      CALL @TU_FUNC(APP_NAME, APP_OPTIONS, APP_STATUS)
      SLEEP 2
      ERRMSG = "HIT <RETURN> TO DELETE THE PC FILE"
      GOSUB 91000
      TU_FUNC = "TU.DELETE.FILE":TU_VERNO
      CALL @TU_FUNC(PCFILE, APP_STATUS)
   END ELSE
      ERRMSG = "FILE TRANSFER STATUS = " : STATUS
      GOSUB 91000
   END
   RETURN
*
WINTEGRATE_SUB: 
   SLEEP 1
   FILE = "_HOLD_"
   ITEMS = VIEW_ID
   FIELDS = "*"
   OPTS = ""
   OPTS = "Raw"
   OPTS<3> = "capture"
   OPTS<4> = "yes"
   OPTS<6> = "All"
   OPTS<7> = "\255,\r\n,\254,\r\n"
   STATUS = ""
   CALL WIN.IMPORT(PCFILE,FILE,ITEMS,FIELDS,OPTS,STATUS)
   IF STATUS = "OK" THEN
      APP_OPTIONS = PCFILE
      CALL WIN.PCRUN(APP_NAME, APP_OPTIONS)
      SLEEP 2
      ERRMSG = "HIT <RETURN> TO DELETE THE PC FILE"
      GOSUB 91000
      CALL WIN.PCDELETE(PCFILE)
   END ELSE
      ERRMSG = "FILE TRANSFER STATUS = " : STATUS
      GOSUB 91000
   END
   RETURN
*
*---- Calls for syscom
*
91000* 
   ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000* 
   ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
*
99999 *
*
   DELETE HOLD.FILE, HF.NAME
   DELETE CONTROL, HF.NAME
   STMT = 'SETPTR ,,,,,1,BRIEF,DEST ':DEFAULT.PTR:',NOMESSAGE'
   UDTEXECUTE STMT CAPTURING TEXT
   RETURN
END
