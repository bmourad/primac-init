   SUBROUTINE EST.CASE.SUB (CONO, ACTION, RECALC)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.CASE.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/03/86
*
* DESCRIPTION
*
* This subroutine is used to calculate the dimensions of the endleaf, board
* and cloth required to construct the book cover.
*
*    ACTION - "ENLEAF"   Calculate endleaf only
*             "BOARD"    Calculate board only
*             "CLOTH"    Calculate cloth only
*             "ALL"      Calculate all of the above
*
*    RECALC   "0"        Do not calculate cut size
*             "1"        Calculate cut size
*
*T26358 cmykleb 01/07/2002 * Change the board "Building In Allowance" from
*                            a hard coded amount to an entered amount.
*T26449 cmykleb 03/05/2002 * Change the side cloth "Building In Allowance"
*                            from a hard coded amount to an entered amount.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.BIND.SPOIL
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TOT.BULK = 0
   C2 = COUNT(EST.BIND.FOLD.BULK,VM) + (EST.BIND.FOLD.BULK # "")
   FOR C = 1 TO C2
      TOT.BULK = TOT.BULK + EST.BIND.FOLD.BULK<1,C>
   NEXT C
   TOT.BULK = TOT.BULK / 10000
*
   PAGE.WIDTH = EST.BIND.TP.WIDTH / 10000
   PAGE.LENGTH = EST.BIND.TP.LENGTH / 10000
   SHOW.SIZE = EST.CASE.SHOW.SIZE / 10000
*
*---- MAIN PROCESSING
*
   IF ACTION = "ENDLEAF" OR ACTION = "ALL" THEN
      EST.CASE.ENDLF.CUT.OUT = ""
      IF RECALC THEN
         EST.CASE.ENDLF.CUT.SIZE = ""
      END
      IF EST.CASE.ENDLF<1,1> # "" THEN
         IF RECALC THEN
            EST.CASE.ENDLF.CUT.SIZE<1,1> = INT(((PAGE.WIDTH + 0.125) * 2) * 10000 + 0.5)
            EST.CASE.ENDLF.CUT.SIZE<1,2> = EST.BIND.US.LENGTH
         END
         BEGIN CASE
            CASE EST.CASE.ENDLF.CUT.SIZE<1,1>+0 = 0
            CASE EST.CASE.ENDLF.CUT.SIZE<1,2>+0 = 0
            CASE EST.CASE.ENDLF.SRC = "R"
               EST.CASE.ENDLF.CUT.OUT = INT(EST.CASE.ENDLF.SIZE<1,1> / EST.CASE.ENDLF.CUT.SIZE<1,1> * 100 + 0.5)
            CASE EST.CASE.ENDLF.SRC = "S"
               OUT1 = INT(EST.CASE.ENDLF.SIZE<1,1> / EST.CASE.ENDLF.CUT.SIZE<1,1>)
               OUT2 = INT(EST.CASE.ENDLF.SIZE<1,2> / EST.CASE.ENDLF.CUT.SIZE<1,2>)
               EST.CASE.ENDLF.CUT.OUT = OUT1 * OUT2 * 100
         END CASE
      END
   END
*
   IF ACTION = "BOARD" OR ACTION = "ALL" THEN
      EST.CASE.SIDE.BD.CUT.OUT = ""
      EST.CASE.SPINE.BD.CUT.OUT = ""
      IF RECALC THEN
         EST.CASE.SIDE.BD.CUT.SIZE = ""
         EST.CASE.SPINE.BD.CUT.SIZE = ""
      END
      BIA = OCONV(EST.CASE.BOARD.PRICE<1,4>,'MD4'); * T26358
      IF EST.CASE.BOARD<1,1> # "" THEN
         IF RECALC THEN
            BEGIN CASE
               CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "R"
                  BEGIN CASE
                     CASE EST.BIND.EDGE.TYPE = "S"
*T26358 v
*                       EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - 0.1875) * 10000 + 0.5)
                        EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - BIA) * 10000 + 0.5)
*T26358 ^
                        EST.CASE.SIDE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.2500) * 10000 + 0.5)
                        BOARD.WIDTH = EST.CASE.SIDE.BD.CUT.SIZE<1,1> / 10000
                     CASE EST.BIND.EDGE.TYPE = "RF" OR EST.BIND.EDGE.TYPE = "RFF"
*T26358 v
*                       EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - 0.1250) * 10000 + 0.5)
                        EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - BIA) * 10000 + 0.5)
*T26358 ^
                        EST.CASE.SIDE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.2500) * 10000 + 0.5)
                        BOARD.WIDTH = EST.CASE.SIDE.BD.CUT.SIZE<1,1> / 10000
                  END CASE
               CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "F"
*T26358 v
*                 EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - 0.0625) * 10000 + 0.5)
                  EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - BIA) * 10000 + 0.5)
*T26358 ^
                  EST.CASE.SIDE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.2500) * 10000 + 0.5)
                  BOARD.WIDTH = EST.CASE.SIDE.BD.CUT.SIZE<1,1> / 10000
                  EST.CASE.SPINE.BD.CUT.SIZE<1,1> = INT((TOT.BULK + 0.1900) * 10000 + 0.5)
                  EST.CASE.SPINE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.1875) * 10000 + 0.5)
            END CASE
         END
         BEGIN CASE
            CASE EST.CASE.SIDE.BD.CUT.SIZE<1,1>+0 = 0
            CASE EST.CASE.SIDE.BD.CUT.SIZE<1,2>+0 = 0
            CASE EST.CASE.BOARD.SPEC<1,2> = "L"
               OUT1 = INT(EST.CASE.BOARD.SIZE<1,1> / EST.CASE.SIDE.BD.CUT.SIZE<1,1>)
               OUT2 = INT(EST.CASE.BOARD.SIZE<1,2> / EST.CASE.SIDE.BD.CUT.SIZE<1,2>)
               EST.CASE.SIDE.BD.CUT.OUT = OUT1 * OUT2 * 100
               IF EST.CASE.SPINE.BD.CUT.SIZE # "" THEN
                  OUT1 = INT(EST.CASE.BOARD.SIZE<1,1> / EST.CASE.SPINE.BD.CUT.SIZE<1,1>)
                  OUT2 = INT(EST.CASE.BOARD.SIZE<1,2> / EST.CASE.SPINE.BD.CUT.SIZE<1,2>)
                  EST.CASE.SPINE.BD.CUT.OUT = OUT1 * OUT2 * 100
               END
            CASE EST.CASE.BOARD.SPEC<1,2> = "S"
               OUT1 = INT(EST.CASE.BOARD.SIZE<1,1> / EST.CASE.SIDE.BD.CUT.SIZE<1,2>)
               OUT2 = INT(EST.CASE.BOARD.SIZE<1,2> / EST.CASE.SIDE.BD.CUT.SIZE<1,1>)
               EST.CASE.SIDE.BD.CUT.OUT = OUT1 * OUT2 * 100
               IF EST.CASE.SPINE.BD.CUT.SIZE # "" THEN
                  OUT1 = INT(EST.CASE.BOARD.SIZE<1,1> / EST.CASE.SPINE.BD.CUT.SIZE<1,2>)
                  OUT2 = INT(EST.CASE.BOARD.SIZE<1,2> / EST.CASE.SPINE.BD.CUT.SIZE<1,1>)
                  EST.CASE.SPINE.BD.CUT.OUT = OUT1 * OUT2 * 100
               END
         END CASE
      END
   END
*
   IF ACTION = "CLOTH" OR ACTION = "ALL" THEN
      EST.CASE.SIDE.CL.CUT.OUT = ""
      EST.CASE.SPINE.CL.CUT.OUT = ""
      IF RECALC THEN
         EST.CASE.SIDE.CL.CUT.SIZE = ""
         EST.CASE.SPINE.CL.CUT.SIZE = ""
      END
      BIA = OCONV(EST.CASE.SIDE.CL.PRICE<1,4>,'MD4') ; * T26449
      IF EST.CASE.SIDE.CL<1,1> # "" THEN
         IF RECALC THEN
            BEGIN CASE
               CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "R"
                  BEGIN CASE
                     CASE EST.BIND.EDGE.TYPE = "S"
*T26449 v
*                       BOARD.WIDTH = INT((PAGE.WIDTH - 0.1875) * 10000 + 0.5) / 10000
                        BOARD.WIDTH = INT((PAGE.WIDTH - BIA) * 10000 + 0.5) / 10000
*T26449 ^
                     CASE EST.BIND.EDGE.TYPE = "RF" OR EST.BIND.EDGE.TYPE = "RFF"
*T26449 v
*                       BOARD.WIDTH = INT((PAGE.WIDTH - 0.1250) * 10000 + 0.5) / 10000
                        BOARD.WIDTH = INT((PAGE.WIDTH - BIA) * 10000 + 0.5) / 10000
*T26449 ^
                  END CASE
                  BEGIN CASE
                     CASE EST.CASE.PIECES = 1
                        EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((TOT.BULK * 1.5 + BOARD.WIDTH * 2 + 2.0000) * 10000 + 0.5)
                        EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                     CASE EST.CASE.PIECES = 3
*T26449 v
*                       EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((BOARD.WIDTH - SHOW.SIZE + 0.6250) * 10000 + 0.5)
                        EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((BOARD.WIDTH - SHOW.SIZE + BIA) * 10000 + 0.5)
*T26449 ^
                        EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                        EST.CASE.SPINE.CL.CUT.SIZE<1,1> = INT((TOT.BULK * 1.5 + SHOW.SIZE * 2 + 1.2500) * 10000 + 0.5)
                        EST.CASE.SPINE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                  END CASE
               CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "F"
*T26449 v
*                 BOARD.WIDTH = INT((PAGE.WIDTH - 0.0625) * 10000 + 0.5) / 10000
                  BOARD.WIDTH = INT((PAGE.WIDTH - BIA) * 10000 + 0.5) / 10000
*T26449 ^
                  BEGIN CASE
                     CASE EST.CASE.PIECES = 1
                        EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((TOT.BULK + BOARD.WIDTH * 2 + 2.1900) * 10000 + 0.5)
                        EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                        EST.CASE.SPINE.CL.CUT.SIZE<1,1> = ""
                        EST.CASE.SPINE.CL.CUT.SIZE<1,2> = ""
                     CASE EST.CASE.PIECES = 3
                        EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((BOARD.WIDTH - SHOW.SIZE + 0.6250) * 10000 + 0.5)
                        EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                        EST.CASE.SPINE.CL.CUT.SIZE<1,1> = INT((TOT.BULK + SHOW.SIZE * 2 + 1.4400) * 10000 + 0.5)
                        EST.CASE.SPINE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                  END CASE
            END CASE
         END
         BEGIN CASE
            CASE EST.CASE.SIDE.CL.CUT.SIZE<1,1>+0 = 0
            CASE EST.CASE.SIDE.CL.CUT.SIZE<1,2>+0 = 0
            CASE EST.CASE.SIDE.CL.SRC = "R"
               IF EST.CASE.SIDE.CL.CUT.DIR = "W" THEN
                  EST.CASE.SIDE.CL.CUT.OUT = INT(EST.CASE.SIDE.CL.SIZE<1,1> / EST.CASE.SIDE.CL.CUT.SIZE<1,1> * 100 + 0.5)
               END ELSE
                  EST.CASE.SIDE.CL.CUT.OUT = INT(EST.CASE.SIDE.CL.SIZE<1,1> / EST.CASE.SIDE.CL.CUT.SIZE<1,2> * 100 + 0.5)
               END
            CASE EST.CASE.SIDE.CL.SRC = "S"
               IF EST.CASE.SIDE.CL.CUT.DIR = "W" THEN
                  OUT1 = INT(EST.CASE.SIDE.CL.SIZE<1,1> / EST.CASE.SIDE.CL.CUT.SIZE<1,1>)
                  OUT2 = INT(EST.CASE.SIDE.CL.SIZE<1,2> / EST.CASE.SIDE.CL.CUT.SIZE<1,2>)
                  EST.CASE.SIDE.CL.CUT.OUT = OUT1 * OUT2 * 100
               END ELSE
                  OUT1 = INT(EST.CASE.SIDE.CL.SIZE<1,1> / EST.CASE.SIDE.CL.CUT.SIZE<1,2>)
                  OUT2 = INT(EST.CASE.SIDE.CL.SIZE<1,2> / EST.CASE.SIDE.CL.CUT.SIZE<1,1>)
                  EST.CASE.SIDE.CL.CUT.OUT = OUT1 * OUT2 * 100
               END
         END CASE
         BEGIN CASE
            CASE EST.CASE.SPINE.CL.CUT.SIZE<1,1>+0 = 0
            CASE EST.CASE.SPINE.CL.CUT.SIZE<1,2>+0 = 0
            CASE EST.CASE.SPINE.CL.SRC = "R"
               IF EST.CASE.SPINE.CL.CUT.DIR = "W" THEN
                  EST.CASE.SPINE.CL.CUT.OUT = INT(EST.CASE.SPINE.CL.SIZE<1,1> / EST.CASE.SPINE.CL.CUT.SIZE<1,1> * 100 + 0.5)
               END ELSE
                  EST.CASE.SPINE.CL.CUT.OUT = INT(EST.CASE.SPINE.CL.SIZE<1,1> / EST.CASE.SPINE.CL.CUT.SIZE<1,2> * 100 + 0.5)
               END
            CASE EST.CASE.SPINE.CL.SRC = "S"
               IF EST.CASE.SPINE.CL.CUT.DIR = "W" THEN
                  OUT1 = INT(EST.CASE.SPINE.CL.SIZE<1,1> / EST.CASE.SPINE.CL.CUT.SIZE<1,1>)
                  OUT2 = INT(EST.CASE.SPINE.CL.SIZE<1,2> / EST.CASE.SPINE.CL.CUT.SIZE<1,2>)
                  EST.CASE.SPINE.CL.CUT.OUT = OUT1 * OUT2 * 100
               END ELSE
                  OUT1 = INT(EST.CASE.SPINE.CL.SIZE<1,1> / EST.CASE.SPINE.CL.CUT.SIZE<1,2>)
                  OUT2 = INT(EST.CASE.SPINE.CL.SIZE<1,2> / EST.CASE.SPINE.CL.CUT.SIZE<1,1>)
                  EST.CASE.SPINE.CL.CUT.OUT = OUT1 * OUT2 * 100
               END
         END CASE
      END
   END
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
