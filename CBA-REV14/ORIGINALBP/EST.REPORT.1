   SUBROUTINE EST.REPORT.1 (CONO, CO.NAME, EST.KEY, PAGE.NO,PRT.QTY,HD1,HD2)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.REPORT.1
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/18/86
*
* DESCRIPTION
*
* This program will print the estimate report.
*
* MOD TASK 18262 DMT 7/24/94 INCREASE ESTIMATE QUANTITIES TO 99
*T26138 cm 10/04/2001 Expand qty from 8 to 9 digits.
*T26090 cmykleb 04/04/2002 * Change rpt to use rpt heading from call pgm.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATOR
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>JES.CPYLIB>EST.COST.TYPE.SUM
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>SALESMAN
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
10*
   PAGE.NO = 0
   CDATE = DATE()
   CTIME = TIME()
   IF CTIME < 10 THEN CDATE = DATE()
   CTIME = OCONV(CTIME,"MTS")
   CHR = CTIME[1,2]+0
   CMN = CTIME[4,2]
   BEGIN CASE
      CASE CHR < 12
         PTIME = CHR:":":CMN:" AM"
      CASE CHR = 12
         PTIME = CHR:":":CMN:" PM"
      CASE 1
         PTIME = (CHR-12):":":CMN:" PM"
   END CASE
*T26090 v
*  HD1A = OCONV(CDATE,"D2/"):"   ":PTIME
*  HD1B = SPACE((40-LEN(CO.NAME))/2):CO.NAME
*  HD1 = HD1A"L#46":HD1B"L#40":SPACE(34):"PAGE: "
*  HD2 = SPACE(51):"E S T I M A T E   R E P O R T"
*T26090 ^
20*
*
*---- MAIN PROCESSING
*
100*
   BEGIN CASE
      CASE EST.TYPE = "R"
         ETYPE = "REGULAR"
      CASE EST.TYPE = "C"
         ETYPE = "CHANGE ORDER"
      CASE EST.TYPE = "S"
         ETYPE = "SPOILAGE"
      CASE EST.TYPE = "N"
         ETYPE = "NON-CHARGEABLE"
      CASE 1
         ETYPE = ""
   END CASE
   MATREAD DIV.REC FROM DIVISION, CONO:FIELD(EST.DIV,"-",1) ELSE
      MAT DIV.REC = ""
   END
   IF EST.SALESMAN = "" THEN
      MAT SALESMAN.REC = ""
   END ELSE
      MATREAD SALESMAN.REC FROM SALESMAN, CONO:EST.SALESMAN ELSE
         MAT SALESMAN.REC = ""
      END
   END
   IF EST.CATG = "" THEN
      MAT JCATG.REC = ""
   END ELSE
      MATREAD JCATG.REC FROM JOB.CATEGORY, CONO:EST.CATG ELSE
         MAT JCATG.REC = ""
      END
   END
   IF EST.ESTIMATOR = "" THEN
      MAT ESTIMATOR.REC = ""
   END ELSE
      MATREAD ESTIMATOR.REC FROM ESTIMATOR, CONO:EST.ESTIMATOR ELSE
         MAT ESTIMATOR.REC = ""
      END
   END
*
*---- PRINT DEMOGRAPHICS
*
2000*
   PRINTER ON
   GOSUB 10000
   PLINE = "CUSTOMER : ":EST.CUST.NAME"L#30"
   PLINE = PLINE:SPACE(5):"DIVISION : "
   PLINE = PLINE:DIV.DESC"L#30"
   PLINE = PLINE:SPACE(5):"ESTIMATE TYPE : ":ETYPE
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PLINE = SPACE(11):EST.CUST.ADDR1"L#30"
   PLINE = PLINE:SPACE(5):"CATEGORY : "
   PLINE = PLINE:JCATG.DESC"L#30"
   PLINE = PLINE:SPACE(5):"DATE ENTERED  : ":OCONV(EST.DATE.ENTERED,"D2/")
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PLINE = SPACE(11):EST.CUST.ADDR2"L#30"
   PLINE = PLINE:SPACE(5):"SALES CD : "
   PLINE = PLINE:EST.SALES.CODE"L#30"
   PLINE = PLINE:SPACE(5):"DATE REQUIRED : ":OCONV(EST.DATE.REQUIRED,"D2/")
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PLINE = SPACE(11):EST.CUST.ADDR3"L#30"
   PLINE = PLINE:SPACE(5):"SALESREP : "
   PLINE = PLINE:SALS.NAME"L#30"
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PLINE = SPACE(11):EST.CUST.ADDR4"L#30"
   PLINE = PLINE:SPACE(5):"ESTIMATOR: "
   PLINE = PLINE:ESTMTR.NAME"L#30"
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PLINE = "ATTENTION: ":EST.CUST.ATTN"L#30"
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PRINT
   LINE.CNT = LINE.CNT + 1
   PLINE = " QUANTITIES                         JOB DESCRIPTION / REMARKS                                COMPONENTS       "
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   PLINE = "------------     ----------------------------------------------------------------     ------------------------"
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   ECNT = DCOUNT(PRT.QTY,SVM)
   QPTR = ''
   FOR Q = 1 TO ECNT
      LOCATE PRT.QTY<1,1,Q> IN EST.QTY<1>,1 SETTING Q1 THEN
         QPTR<1,Q> = Q1
      END
   NEXT Q
   DC = COUNT(EST.COMMENTS,VM) + (EST.COMMENTS # "")
   CMNT.TYPE = ""
   CMNTS = ""
   DCNT = 0
   FOR DP = 1 TO DC
      IF EST.COMMENT.TYPE<1,DP>="" OR EST.COMMENT.TYPE<1,DP>="I" THEN
         DCNT = DCNT + 1
         CMNT.TYPE<1,DCNT> = EST.COMMENT.TYPE<1,DP>
         CMNTS<1,DCNT> = EST.COMMENTS<1,DP>
      END
   NEXT DP
   CCNT = COUNT(EST.PROD.DESC,VM) + (EST.PROD.DESC # "")
   LC = ECNT
   IF DCNT > LC THEN LC = DCNT
   IF CCNT > LC THEN LC = CCNT
   FOR LP = 1 TO LC
      IF LINE.CNT > 55 THEN GOSUB 10000
      IF ECNT < LP THEN
         QVAL = ''
      END ELSE
         QVAL = OCONV(EST.QTY<1,QPTR<1,LP>>,"MD0")
      END
*T26138 v
*     PLINE = QVAL"R#11":SPACE(1)
      PLINE = OCONV(QVAL,'MD0,')"R#11":SPACE(1)
*T26138 ^
      PLINE = PLINE:SPACE(5):CMNT.TYPE<1,LP>"L#2"
      PLINE = PLINE:SPACE(2):CMNTS<1,LP>"L#65"
      IF LP <= CCNT THEN
         PLINE = PLINE:SPACE(5):LP"R#2":SPACE(2):EST.PROD.DESC<1,LP>"L#30"
      END
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
   NEXT LP
*
*---- PRINT STOCK AND PRODUCTION INFORMATION
*
3000*
   MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:"I" ELSE MAT ESTM.REC = ""
   CCNT = COUNT(EST.PROD.DESC,VM) + (EST.PROD.DESC # "")
   FOR COMP = 1 TO CCNT
      PREC = ""; PCNT = 0
      SREC = ""; SCNT = 0
      GOSUB 5000
      IF EST.PROD.OS.TYPE<1,COMP> # "" THEN
         WEB.CNT = EST.PROD.WEB.CNT<1,COMP>+0
         IF WEB.CNT = 0 THEN WEB.CNT = 1
         FOR WPTR = 1 TO WEB.CNT
            X1 = " ":EST.PROD.US.WIDTH<1,COMP,WPTR>/10000
            X2 = "x":EST.PROD.US.LENGTH<1,COMP,WPTR>/10000
            X3 = " ":EST.PROD.DESC<1,COMP>
            IF WPTR = 1 THEN
               PCNT = PCNT + 1
               PREC<PCNT> = "*COMPONENT ":COMP:":":X3
            END ELSE
               PCNT = PCNT + 2
               PREC<PCNT> = "  *WEB ":WPTR
            END
            PREC<PCNT> = PREC<PCNT>:"  FLAT SIZE:":X1:X2
            X1 = " ":EST.PROD.TS.WIDTH<1,COMP,WPTR>/10000
            X2 = "x":EST.PROD.TS.LENGTH<1,COMP,WPTR>/10000
            PREC<PCNT> = PREC<PCNT>:"  ":"FINAL SIZE:":X1:X2
            X1 = EST.PROD.OS.WIDTH<1,COMP,WPTR>/10000
            X2 = EST.PROD.OS.LENGTH<1,COMP,WPTR>/10000
            IF X2 = 0 THEN
               X1 = " ":X1:'"'
               X2 = ""
            END ELSE
               X1 = " ":X1
               X2 = "x":X2
            END
            X3 = " ":EST.PROD.BASIS.WT<1,COMP,WPTR>:" LB"
            X4 = " ":EST.PROD.OS.DESC<1,COMP,WPTR>
            IF EST.PROD.COLOR<1,COMP,WPTR> = "" THEN
               X5 = ""
            END ELSE
               X5 = "; ":EST.PROD.COLOR<1,COMP,WPTR>
            END
            IF EST.PROD.FINISH<1,COMP,WPTR> = "" THEN
               X6 = ""
            END ELSE
               X6 = "; ":EST.PROD.FINISH<1,COMP,WPTR>
            END
            PCNT = PCNT + 2
            PREC<PCNT> = "    STOCK:":X1:X2:X3:X4:X5:X6
            IF EST.PROD.INV.ID<1,COMP,WPTR> # "" THEN
               X1 = " ":EST.PROD.INV.ID<1,COMP,WPTR>
               PCNT = PCNT + 1
               PREC<PCNT> = "     PROD:":X1
            END
            IF EST.PROD.PSS.OSS<1,COMP,WPTR>+0 > 1 THEN
               X1 = " ":EST.PROD.PSS.OSS<1,COMP,WPTR>:"-OUT"
               X2 = EST.PROD.PS.WIDTH<1,COMP,WPTR>/10000
               X3 = EST.PROD.PS.LENGTH<1,COMP,WPTR>/10000
               IF X3 = 0 THEN
                  X2 = " to ":X2:'"'
                  X3 = ""
               END ELSE
                  X2 = " to ":X2
                  X3 = "x":X3
               END
               PCNT = PCNT + 2
               PREC<PCNT> = "      CUT:":X1:X2:X3
            END
            IF EST.PROD.NO.UP<1,COMP,WPTR> = 1 THEN
               X3 = " ":EST.PROD.FORMS<1,COMP,1>:" ":EST.PROD.PGS.PER.FORM<1,COMP,WPTR>:"PP"
            END ELSE
               X3 = " ":EST.PROD.PG.CNT<1,COMP,WPTR>:"PP ":EST.PROD.NO.UP<1,COMP,WPTR>+0:"-UP"
            END
            XX = EST.PROD.MODE<1,COMP,WPTR>
            BEGIN CASE
               CASE XX = "SS"
                  X4 = " S/S"
               CASE XX = "WT"
                  X4 = " W/T"
               CASE XX = "WR"
                  X4 = " W/R"
               CASE XX = "SW"
                  X4 = " S/W"
               CASE XX = "P"
                  X4 = " P/F"
            END CASE
            PRESS = FIELD(EST.PROD.PRESS.ID<1,COMP>,"-",1)
            MATREAD CCTR.REC FROM COST.CNTR, CONO:PRESS ELSE
               MAT CCTR.REC = ""
               CCTR.DESC = "???????????????"
            END
            X5 = " on ":TRIM(CCTR.DESC)
            X6 = "; ":EST.PROD.COLORS.1<1,COMP,WPTR>+0:"/":EST.PROD.COLORS.2<1,COMP,WPTR>+0
            X7 = "; ":EST.PROD.PLATES<1,COMP,WPTR>+0:" PLATES"
            PCNT = PCNT + 2
            PREC<PCNT> = "      RUN:":X3:X4:X5:X6:X7
            ICNT = COUNT(EST.PROD.INK.ID<1,COMP,WPTR>,"!") + (EST.PROD.INK.ID<1,COMP,WPTR> # "")
            FOR IPTR = 1 TO ICNT
               INK.ID = FIELD(EST.PROD.INK.ID<1,COMP,WPTR>,"!",IPTR)
               LOCATE INK.ID IN ESTM.PROD<1>,1 SETTING IP ELSE IP = 0
               IF IP > 0 THEN
                  IDESC = ESTM.DESC<1,IP>
                  SIDES = FIELD(EST.PROD.INK.SIDES<1,COMP,WPTR>,"!",IPTR)
                  COVER = FIELD(EST.PROD.INK.COVER<1,COMP,WPTR>,"!",IPTR)
                  IF IPTR = 1 THEN
                     PCNT = PCNT + 2
                     PREC<PCNT> = "      INK: ":IDESC"L#30":"  ":SIDES:"S; ":COVER:"%"
                  END ELSE
                     PCNT = PCNT + 1
                     PREC<PCNT> = "           ":IDESC"L#30":"  ":SIDES:"S; ":COVER:"%"
                  END
               END
            NEXT IPTR
         NEXT WPTR
      END
      IF PCNT > SCNT THEN LCNT = PCNT ELSE LCNT = SCNT
      IF LINE.CNT + LCNT+3 > 55 THEN GOSUB 10000
      PRINT
      PRINT STR("=",130)
      PRINT
      LINE.CNT = LINE.CNT + 3
      FOR LP = 1 TO LCNT
         PRINT PREC<LP>"L#80":SPACE(5):SREC<LP>"L#45"
         LINE.CNT = LINE.CNT + 1
      NEXT LP
   NEXT COMP
   DCNT = COUNT(EST.DESC,VM) + (EST.DESC # "")
   IF DCNT <= 10 THEN
      LC = DCNT
   END ELSE
      LC = INT(DCNT/2+0.99)
   END
   IF (LINE.CNT + LC + 4) > 55 THEN GOSUB 10000
   PRINT
   PRINT STR("*",55):"  JOB INSTRUCTIONS  ":STR("*",55)
   LINE.CNT = LINE.CNT + 2
   PRINT
   LINE.CNT = LINE.CNT + 1
   FOR LP = 1 TO LC
      PLINE=EST.DESC.HEADING<1,LP>"L#12":SPACE(2):EST.DESC<1,LP>"L#50"
      PLINE=PLINE:" | ":EST.DESC.HEADING<1,LP+LC>"L#12":SPACE(2):EST.DESC<1,LP+LC>"L#50"
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
   NEXT LP
   GOTO 99999
*
*---- GET COMPONENT INSTRUCTIONS
*
5000*
   OLD.DESC = ""
   OP = 0
   CC = COUNT(EST.COMMENTS,VM) + (EST.COMMENTS # "")
   FOR LP = 1 TO CC
      IF EST.COMMENT.TYPE<1,LP> = COMP THEN
         OP = OP + 1
         OLD.DESC<1,OP> = EST.COMMENTS<1,LP>
      END
   NEXT LP
   CALL DESC.REFORMAT (OLD.DESC,45,NEW.DESC)
   SCNT = SCNT + 1
   SREC<SCNT> = "Special Instructions"
   SCNT = SCNT + 1
   SREC<SCNT> = STR("-",45)
   CC = COUNT(NEW.DESC,VM) + (NEW.DESC # "")
   FOR LP = 1 TO CC
      SCNT = SCNT + 1
      SREC<SCNT> = NEW.DESC<1,LP>
   NEXT LP
   RETURN
*
*---- PRINT PAGE HEADINGS
*
10000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1 : PAGE.NO
   PRINT HD2
   PRINT
   PRINT "ESTIMATE : ":EST.KEY
   PRINT
   LINE.CNT = 6
   RETURN
*
*---- END OF PROGRAM
*
99999*
   PRINTER OFF
   RETURN
END
