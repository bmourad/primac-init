      SUBROUTINE EST.FORMULA.F611 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.F611
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/18/87
*
* DESCRIPTION
*
* This formula is used to determine the run efficiency factor for the
* cutter run operation. The factor is basis upon the number of cuts
* to be made and the size of the sheets to be cut.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.CUT.EFF
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEMP.FCTR = 1000
      TYP = 0
      IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
*
      WIDTH = EST.BIND.CUT.WIDTH<1,MPTR> / 10000
      XXLENGTH = EST.BIND.CUT.LENGTH<1,MPTR> / 10000
      CUTS.PER.LIFT = EST.BIND.CUT.CUTS<1,MPTR>
*
      MATREAD CEFF.REC FROM ESTIMATE.CUT.EFF, CONO:"CUT.EFF" ELSE RETURN
*
*---- MAIN PROCESSING
*
      LOCATE CUTS.PER.LIFT IN CEFF.QTY<1>,1 BY "AR" SETTING Y2 ELSE NULL
      BEGIN CASE
      CASE Y2 = 1
         Y1 = 1
      CASE Y2 > COUNT(CEFF.QTY,VM)+1
         Y2 = Y2 - 1
         Y1 = Y2
      CASE 1
         Y1 = Y2 - 1
      END CASE
*
      SQIN = INT(WIDTH * XXLENGTH + 0.5)
      LOCATE SQIN IN CEFF.SQIN<1>,1 BY "AR" SETTING X2 ELSE NULL
      BEGIN CASE
      CASE X2 = 1
         X1 = 1
      CASE X2 > COUNT(CEFF.SQIN,VM)+1
         X2 = X2 - 1
         X1 = X2
      CASE 1
         X1 = X2 - 1
      END CASE
*
      BEGIN CASE
      CASE CEFF.EXTR = "Y"
         TF22 = CEFF.FCTR<1,Y2,X2>
         TF21 = CEFF.FCTR<1,Y2,X1>
         TF12 = CEFF.FCTR<1,Y1,X2>
         TF11 = CEFF.FCTR<1,Y1,X1>
         IF CEFF.QTY<1,Y2> = CEFF.QTY<1,Y1> THEN
            TFY2 = TF22
            TFY1 = TF21
         END ELSE
            TFY2 = INT(TF22-(CEFF.QTY<1,Y2>-CUTS.PER.LIFT)/(CEFF.QTY<1,Y2>-CEFF.QTY<1,Y1>)*(TF22-TF12)+0.5)
            TFY1 = INT(TF21-(CEFF.QTY<1,Y2>-CUTS.PER.LIFT)/(CEFF.QTY<1,Y2>-CEFF.QTY<1,Y1>)*(TF21-TF11)+0.5)
         END
         IF CEFF.SQIN<1,X2> = CEFF.SQIN<1,X1> THEN
            TF = TFY2
         END ELSE
            TF = INT(TFY2-(CEFF.SQIN<1,X2>-SQIN)/(CEFF.SQIN<1,X2>-CEFF.SQIN<1,X1>)*(TFY2-TFY1)+0.5)
         END
      CASE 1
         TF = CEFF.FCTR<1,Y2,X2>
      END CASE
      TEMP.FCTR = TF * 10
      IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
