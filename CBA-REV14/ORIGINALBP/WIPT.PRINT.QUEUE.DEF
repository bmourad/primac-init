*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - WIPT.PRINT.QUEUE.DEF
* BY          - BOB SHERER
* DATE        - 01/06/98
* DESCRIPTION -
* This program maintains the WIPT.PRINT.QUEUE file.
*T22322 ct6 01/06/1998 * ADD INSTALL WIP INPROCESS SYSTEM WITH CHANGES -
*                        PROVIDE A FILE FOR PRINT QUEUE DIRECTION.
*T25651 lanny 02/27/2001 * Correct method of extracting Queues.
*T26132 gat 09/06/2001 * CHANGE SCREEN TO MANDATE THREE POSITION FIELD
*                        FOR LOCATE IN HNP400
*T26126 adelgado 02/28/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>WTR.CPYLIB>WIPT.PRINT.QUEUE
*
*---- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE=1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','WTR.SCREENS' TO M.SCREENS ELSE ERRMSG="WTR.SCREENS FILE MISSING";GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE MISSING";GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
  OPEN '','WIPT.PRINT.QUEUE' TO WIPT.PRINT.QUEUE ELSE ERRMSG="WIPT.PRINT.QUEUE FILE MISSING";GOTO 93000
*
*---- INITIALIZATION
*
  MAT SCV.REC = ""
*MAT EDIT.COM = ''
*TYP = 0; CALL EDIT.SUB; FILL = "#"
  MAT EDIT.COM.DRIVER = ''
  ERRMSG = ''
  LINE.SPACE = 1
  BEGIN.PAGE = 6
  PAGE.SIZE = 10
  LINES = 0
  LN = 1
  OLD.START = 0
  GOSUB 5000  ;* SETUP THE VALID.QUEUES LIST
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'WIPT.PRINT.QUEUE.DEF'
  ECD.ACTION = 1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2;CALL SCRN.EDIT
10*
  RELEASE          ;* T26126
  MAT SCV.REC = ""
  ECD.ACTION = 6; CALL SCRN.EDIT
  LINES = 0; LN = 1; OLD.START = 0
*
  SCV.REC(15)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=15; ECD.ACTION=5; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
  MAT WIPPQ.REC = ""
15*
  ECD.NUM = 1;ECD.ACTION = 4;CALL SCRN.EDIT
  WIPPQ.KEY = ECD.RET.VALUE
  IF WIPPQ.KEY = 'END' THEN
    GOTO 99999
  END
  NEW.WIPPQ = 0
  * T26126 v
  MATREADU WIPPQ.REC FROM WIPT.PRINT.QUEUE, CONO:WIPPQ.KEY LOCKED
    ERRMSG = 'Record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 15 
  END ELSE
  * T26126  ^
    NEW.WIPPQ = 1;MAT WIPPQ.REC = ''
  END
  IF NEW.WIPPQ = 1 THEN
    GOSUB 1000
    IF ECD.RET.VALUE = "END" THEN GOTO 10
  END ELSE
    SCV.REC(2)<ECD.SCRN.NO,1> = WIPPQ.PRINT.QUEUE.DEFAULT
    ECD.ACTION = 3;CALL SCRN.EDIT
    LINES = DCOUNT(WIPPQ.STATION.ID,VM)
    GOSUB 80000
  END
*
*--- ENTER OPTION
*
500*
  MORE = 1
  LOOP
  WHILE MORE DO
    IF NEW.WIPPQ THEN
      NEW.WIPPQ = 0
      OPTION = "A"
    END ELSE
      ECD.NUM = 21; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
    END
    BEGIN CASE
      CASE OPTION = "E" OR OPTION = "END"
        MORE = 0
      CASE OPTION = "F"
        MATWRITE WIPPQ.REC ON WIPT.PRINT.QUEUE, CONO:WIPPQ.KEY
        MORE = 0
      CASE OPTION = "A" AND LINES < 999
        MODE = "A"
        DONE = 0
        FOR LN = LINES+1 TO 99 UNTIL DONE
          GOSUB 80000
          GOSUB 10000
          IF VALUE = "END" THEN
            DONE = 1
            WIPPQ.STATION.ID = DELETE(WIPPQ.STATION.ID,1,LN,0)
            WIPPQ.PRINT.QUEUE = DELETE(WIPPQ.PRINT.QUEUE,1,LN,0)
          END ELSE
            LINES = LINES + 1
          END
        NEXT LN
        LN = LINES
        OLD.START = 0
        GOSUB 80000
      CASE OPTION = "C" AND LINES > 0
        MODE = "C"
        GOSUB 30000
        BEGIN CASE
          CASE LNO = "0"
          CASE LNO = "A"
            VALUE = ""
            FOR LN = 1 TO LINES UNTIL VALUE = "END"
              GOSUB 80000
              GOSUB 10000
            NEXT LN
          CASE NUM(LNO)
            LN = LNO
            GOSUB 10000
        END CASE
        IF VALUE = "END" THEN
          OLD.START = 0
          GOSUB 80000
        END
      CASE OPTION = "D" AND LINES > 0
        MODE = "D"
        GOSUB 30000
        IF LNO # 0 THEN
          LN = LNO
          WIPPQ.STATION.ID = DELETE(WIPPQ.STATION.ID,1,LN,0)
          WIPPQ.PRINT.QUEUE = DELETE(WIPPQ.PRINT.QUEUE,1,LN,0)
          LINES = LINES - 1
          IF LN > LINES THEN LN = LN - 1
          OLD.START = 0
          GOSUB 80000
        END
      CASE OPTION = "I" AND LINES > 0 AND LINES < 99
        MODE = "I"
        GOSUB 30000
        IF LNO > 0 THEN
          LN = LNO
          WIPPQ.STATION.ID = INSERT(WIPPQ.STATION.ID,1,LN,0,"")
          WIPPQ.PRINT.QUEUE = INSERT(WIPPQ.PRINT.QUEUE,1,LN,0,"")
          LINES = LINES + 1
          OLD.START = 0
          GOSUB 80000
          GOSUB 10000
          IF VALUE = "END" THEN
            WIPPQ.STATION.ID = DELETE(WIPPQ.STATION.ID,1,LN,0)
            WIPPQ.PRINT.QUEUE = DELETE(WIPPQ.PRINT.QUEUE,1,LN,0)
            LINES = LINES - 1
          END
          OLD.START = 0
          GOSUB 80000
        END
      CASE OPTION[1,1] = "S"
        PGT2 = OPTION[2,1]
        BEGIN CASE
          CASE PGT2 = "" OR PGT2 = "F"
            LN = LN + PAGE.SIZE
            IF LN > LINES THEN LN = 1
          CASE PGT2 = "R"
            LN = LN - PAGE.SIZE
            IF LN < 1 THEN LN = 1
          CASE PGT2 = "T"
            LN = 1
          CASE PGT2 = "B"
            LN = LINES
            IF LN < 1 THEN LN = 1
        END CASE
        GOSUB 80000
      CASE OPTION = 1
        GOSUB 1000
    END CASE
  REPEAT
  GOTO 10
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- ENTER DEFAULT PRINT QUEUE
*
1000*
  ECD.NUM = 2;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
*  GOSUB 5000
    LOCATE ECD.RET.VALUE IN VALID.QUEUES<1>,1 SETTING POS ELSE
      ERRMSG = ECD.RET.VALUE:" is not a valid Spooler Queue"
      GOSUB 91000
      GOTO 1000
    END
    WIPPQ.PRINT.QUEUE.DEFAULT = ECD.RET.VALUE
  END
  RETURN
*
*--- VALIDATE INPUT SPOOLER
*
5000 *
  VALID.QUEUES = ''
  QCNT=1
  PQCNT=1
  PQUEUE = ""
  CHKQUE = ""
  TQUEUE = ""
  EXECUTE 'GET.QUEUES' CAPTURING MSG
*T25651 v
  MCNT = DCOUNT(MSG,AM)
*     FOR X = 3 TO X+1 UNTIL MSG<X>[15,1]=''
  FOR X = 3 TO MCNT
    TQUEUE<QCNT> = MSG<X>[1,23]
    QCNT=QCNT+1
*T25651  IF MSG<X>[1,1] # " " THEN
    IF MSG<X>[1,1] # " " AND MSG<X>[1,7] # 'fsdata:' THEN
      PQUEUE<PQCNT> = MSG<X>[1,23]
      CHKQUE<PQCNT> = FIELD(PQUEUE<PQCNT>," ",1)
      VALID.QUEUES = INSERT(VALID.QUEUES,1,1,0,CHKQUE<PQCNT>)
      PQCNT = PQCNT + 1
    END
  NEXT X
  RETURN
*
*--- LINE ENTRY
*
10000*
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 8 ; P_Y = SLN ; P_VALUE = LN "R%3" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10100*
  P_X  = 14 ; P_Y = SLN ; P_VALUE = SPACE(3) ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  TYP=1; X=14; Y=SLN; MAXL=3
  IF MODE = "C" THEN
    O.R = "O"
    DEFAULT = WIPPQ.STATION.ID<1,LN>
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10990
  VALUE = VALUE "R%3"
  FOR TEST.LN = 1 TO LINES
    IF TEST.LN # LN AND VALUE = WIPPQ.STATION.ID<1,TEST.LN> THEN
      ERRMSG = "** STATION ID ALREADY DEFINED ON LINE ":TEST.LN:" **"
      GOSUB 91000
      GOTO 10100
    END
  NEXT TEST.LN
  TEMP.STATION.ID = VALUE
  P_X  = 14 ; P_Y = SLN ; P_VALUE = TEMP.STATION.ID "L#8" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
10200* Get the spooler queue for the station on this line
  TYP=1; X=25; Y=SLN; MAXL=25
  HMSG = "PRINT QUEUE FOR THE SPECIFIED STATION"
  IF WIPPQ.PRINT.QUEUE<1,LN> # "" THEN
    DEFAULT = WIPPQ.PRINT.QUEUE<1,LN>
    O.R = "O"
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10990
*GOSUB 5000
  LOCATE VALUE IN VALID.QUEUES<1>,1 SETTING POS ELSE
    ERRMSG = VALUE:" is not a valid Spooler Queue"
    GOSUB 91000
    GOTO 10200
  END
  TEMP.PRINT.QUEUE = VALUE
  P_X  = 25 ; P_Y = SLN ; P_VALUE = TEMP.PRINT.QUEUE"L#25" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10900*
  WIPPQ.STATION.ID<1,LN> = TEMP.STATION.ID
  WIPPQ.PRINT.QUEUE<1,LN> = TEMP.PRINT.QUEUE
  RETURN
10990*
  RETURN
*
*--- GET LINE NUMBER
*
30000*
  GOSUB 80000
  IF MODE = "C" THEN
    ECD.NUM = 22
    ECD.ACTION = 4; CALL SCRN.EDIT
  END ELSE
    ECD.NUM = 23
    ECD.ACTION = 4; CALL SCRN.EDIT
  END
  BEGIN CASE
    CASE ECD.RET.VALUE = ""
      LNO = 0
    CASE ECD.RET.VALUE = "END"
      LNO = 0
    CASE ECD.RET.VALUE = "A"
      LNO = "A"
    CASE NOT(NUM(ECD.RET.VALUE))
      ERRMSG = "** INVALID RESPONSE **"; GOSUB 91000; GOTO 30000
    CASE ECD.RET.VALUE >= ST.LINE AND ECD.RET.VALUE <= LST.LINE
      LNO = ECD.RET.VALUE
    CASE 1
      ERRMSG = "** OUT OF RANGE **"; GOSUB 91000; GOTO 30000
  END CASE
  RETURN
*
*--- SCROLL
*
80000*
  ST.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LST.LINE = ST.LINE + PAGE.SIZE -1
  IF LST.LINE > LINES THEN LST.LINE = LINES
  IF ST.LINE = OLD.START THEN RETURN
  OLD.START = ST.LINE
  CNT = 1
  FOR N = ST.LINE TO LST.LINE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 8 ; P_Y = SSLN ; P_VALUE = N "R%3" ; P_OPT = ""
    P_X  := AM:14 ; P_Y := AM:SSLN ; P_VALUE := AM:WIPPQ.STATION.ID<1,N>"L#8"
    P_X  := AM:25 ; P_Y := AM:SSLN ; P_VALUE := AM:WIPPQ.PRINT.QUEUE<1,N>"L#25"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
  RETURN
*
*--- ERROR ROUTINE
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
* PRINT @(-1)
END
