   SUBROUTINE EST.COMP.DEL (CONO, ACTION, EST.KEY, COMP)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.COMP.DEL
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/04/86
*
* DESCRIPTION
*
* This program will delete detail data for specified component and
* modify all subsequent component data by decrementing the component
* pointer part of the key on the ESTIMATE.DEPT file.
*
*T25383 cm 08/11/2000 * Delete Outside Purchases Information when a
*                       component is deleted.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
*
*---- MAIN PROCESSING
*
*---- DELETE ASSOCIATED MULTI-VALUES
*
   AC = COUNT(EST.A9A,",") + (EST.A9A # "")
   FOR AP = 1 TO AC
      AA = FIELD(EST.A9A,",",AP)
      AA1 = FIELD(AA,"-",1)
      AA2 = FIELD(AA,"-",2)
      IF AA2="" THEN AA2=AA1
      FOR AAP = AA1 TO AA2
         EST.REC(AAP) = DELETE(EST.REC(AAP),1,COMP,0)
      NEXT AAP
   NEXT AP
   IF ACTION = "P" THEN RETURN
   DPC = COUNT(EST.DEPT,VM) + (EST.DEPT # "")
   FOR DPP = 1 TO DPC
      EST.COMP.FLAG = DELETE(EST.COMP.FLAG,1,DPP,COMP)
   NEXT DPP
*
*---- DELETE SPECIFIED COMPONENT DATA
*
   CCNT = COUNT(EST.PROD.DESC,VM) + (EST.PROD.DESC # "")
   DC = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
   FOR DP = DC TO 1 STEP -1
      ESTD.ID = EST.DEPT.COMP<1,DP>
      DPTR = FIELD(ESTD.ID,"!",1)
      CPTR = FIELD(ESTD.ID,"!",2)
      IF CPTR = COMP THEN
         DELETE ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID
         EST.DEPT.COMP = DELETE(EST.DEPT.COMP,1,DP,0)
         EST.DEPT.COMP.HRS = DELETE(EST.DEPT.COMP.HRS,1,DP,0)
         EST.DEPT.COMP.DCOST = DELETE(EST.DEPT.COMP.DCOST,1,DP,0)
         EST.DEPT.COMP.COST = DELETE(EST.DEPT.COMP.COST,1,DP,0)
         EST.DEPT.COMP.SALE = DELETE(EST.DEPT.COMP.SALE,1,DP,0)
         EST.DEPT.COMP.TSALE = DELETE(EST.DEPT.COMP.TSALE,1,DP,0)
         EST.DEPT.COMP.VSALE = DELETE(EST.DEPT.COMP.VSALE,1,DP,0)
         EST.DEPT.COMP.VCOST = DELETE(EST.DEPT.COMP.VCOST,1,DP,0)
      END
   NEXT DP
*T25383 v
   DC = DCOUNT(EST.DEPT.OSP,VM)
   FOR DP = DC TO 1 STEP - 1
      CPTR = FIELD(EST.DEPT.OSP<1,DP>,'!',2)
      IF CPTR = COMP THEN
         EST.DEPT.OSP = DELETE(EST.DEPT.OSP,1,DP,0)
         EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DP,0)
         EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DP,0)
         EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DP,0)
         EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DP,0)
      END
   NEXT DP
*T25383 ^
*
*---- CHANGE SUBSEQUENT COMPONENT KEYS
*
   DC = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
   FOR CP = COMP+1 TO CCNT+1
      FOR DP = DC TO 1 STEP -1
         ESTD.ID = EST.DEPT.COMP<1,DP>
         CPTR = FIELD(ESTD.ID,"!",2)
         IF CPTR = CP THEN
            READU REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE
               RELEASE ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID
               EST.DEPT.COMP = DELETE(EST.DEPT.COMP,1,DP,0)
               EST.DEPT.COMP.HRS = DELETE(EST.DEPT.COMP.HRS,1,DP,0)
               EST.DEPT.COMP.DCOST = DELETE(EST.DEPT.COMP.DCOST,1,DP,0)
               EST.DEPT.COMP.COST = DELETE(EST.DEPT.COMP.COST,1,DP,0)
               EST.DEPT.COMP.SALE = DELETE(EST.DEPT.COMP.SALE,1,DP,0)
               EST.DEPT.COMP.TSALE = DELETE(EST.DEPT.COMP.TSALE,1,DP,0)
               EST.DEPT.COMP.VSALE = DELETE(EST.DEPT.COMP.VSALE,1,DP,0)
               EST.DEPT.COMP.VCOST = DELETE(EST.DEPT.COMP.VCOST,1,DP,0)
               GOTO 290
            END
            DEPT = FIELD(ESTD.ID,"!",1)
            CPTR = CPTR - 1
            EQTY = FIELD(ESTD.ID,"!",3)
            NEW.ESTD.ID = DEPT:"!":CPTR:"!":EQTY
            WRITE REC ON ESTIMATE.DEPT, CONO:EST.KEY:"!":NEW.ESTD.ID
            DELETE ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID
            EST.DEPT.COMP<1,DP> = NEW.ESTD.ID
         END
290   NEXT DP
   NEXT CP
   RETURN
END
