      SUBROUTINE POST.SP.RCLS.SUB(CONO)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JOB.RCLS
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JCSBP
* PROGRAM     - POST.SP.RCLS.SUB
* BY          - ZIAD A. YAMOUT , C.B.A
* DESCRIPTION -
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUETE
*
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>DAILY.RCLS
*COPY>JCS.CPYLIB>JOB.RCLS
*COPY>JCS.CPYLIB>JOB.SHIP
*COPY>JCS.CPYLIB>COST.CNTR.WIP
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
***** MAIN PROCESSING
*
      READU NEW.SEQ FROM CONTROL, CONO:"DAILY.SHIP" ELSE NEW.SEQ = 10001
      SEQ.CNT = COUNT(DRC.SEQ,VM) + (DRC.SEQ # "")
1000  FOR MLP = SEQ.CNT TO 1 STEP -1
         DEPT = DRC.DEPT<1,MLP>; CCTR = DRC.CCTR<1,MLP>
         SEQ = DRC.SEQ<1,MLP>
         SP.F.ID = CONO:DRC.F.JOB:"!":DEPT:"!":CCTR:"!"
         SP.T.ID = CONO:DRC.T.JOB:"!":DEPT:"!":CCTR:"!"
         MATREAD JSP.REC FROM JOB.SHIP, SP.F.ID : SEQ ELSE
            DRC.STATUS<1,MLP> = "CANNOT LOCATE JOB.SHIP - ":SP.F.ID:SEQ
            GOTO 1999
         END
         BEGIN CASE
         CASE JSP.OPER # DRC.OPER<1,MLP>
            DRC.STATUS<1,MLP> = "OPERATION MISMATCH"
            GOTO 1999
         CASE JSP.VIA # DRC.EMP<1,MLP>
            DRC.STATUS<1,MLP> = "SHIP VIA MISMATCH"
            GOTO 1999
         CASE (JSP.COST-JSP.RC.COST) = DRC.COST<1,MLP>
            REV.ALL = 1
         CASE (JSP.COST-JSP.RC.COST) > DRC.COST<1,MLP>
            REV.ALL = 0
         CASE 1
            DRC.STATUS<1,MLP> = "COST MISMATCH"
            GOTO 1999
         END CASE
         MAT JOB.REC = MAT FROM.JOB.REC
         PTR = 1
         LOOP
            LOCATE DEPT IN JOB.SP.DEPT<1>,PTR SETTING DFND ELSE
               DFND = 0; PTR = 0
            END
            BEGIN CASE
            CASE PTR = 0
            CASE CCTR = JOB.SP.CCTR<1,DFND>
               PTR = 0
            CASE 1
               PTR = DFND + 1
            END CASE
         WHILE PTR DO REPEAT
         IF DFND = 0 THEN
            DRC.STATUS<1,MLP> = "CANNOT LOCATE MAIN DEPT/CCTR/SEQ"
            GOTO 1999
         END
         WCNT = COUNT(JSP.WIP,VM) + (JSP.WIP # "")
         WPTR = 0; TOT.WIP = 0; INV.WIP = ""
         FOR I = 1 TO 2
            IF JSP.WIP<1,1,I> + 0 <> 0 THEN
               INV.WIP<1,1,I> = 0 - JSP.WIP<1,1,I>
               TOT.WIP = TOT.WIP + JSP.WIP<1,1,I>
               WPTR = I
            END
         NEXT I
         FOR W = 2 TO WCNT
            FOR I = 1 TO WPTR
               INV.WIP<1,1,I> = INV.WIP<1,1,I> - JSP.WIP<1,W,I>
            NEXT I
         NEXT W
         TOT.RC.DCOST = 0; TOT.RC.COST = 0; TOT.RC.SALE = 0
         TOT.RC.WIP = 0; REV.WIP = ""
         IF JSP.RC.COST + 0 <> 0 THEN
            FOR S = SEQ + 1 TO JOB.SP.SEQ<1,DFND>
               MATREAD JSP.REC FROM JOB.SHIP, SP.F.ID : S ELSE
                  DRC.STATUS<1,MLP> = "CANNOT LOCATE JOB.SHIP - ":SP.F.ID:S
                  GOTO 1999
               END
               IF JSP.RC.TRAN = DRC.TRAN<1,MLP> THEN
                  FOR I = 1 TO WPTR
                     REV.WIP<1,1,I> = REV.WIP<1,1,I> - JSP.WIP<1,1,I>
                     TOT.RC.WIP = TOT.RC.WIP - JSP.WIP<1,1,I>
                  NEXT I
                  TOT.RC.DCOST = TOT.RC.DCOST - JSP.DCOST
                  TOT.RC.COST = TOT.RC.COST - JSP.COST
                  TOT.RC.SALE = TOT.RC.SALE - JSP.SALE
               END
            NEXT S
            MATREAD JSP.REC FROM JOB.SHIP, SP.F.ID : SEQ ELSE
               DRC.STATUS<1,MLP> = "CANNOT LOCATE JOB.SHIP - ":SP.F.ID:SEQ
               GOTO 1999
            END
            IF TOT.RC.COST <> JSP.RC.COST THEN
               DRC.STATUS<1,MLP> = "REVERSE COST MISMATCH ":OCONV(TOT.RC.COST,"MD2")
               GOTO 1999
            END
         END
         NEW.WIP = ""
         IF REV.ALL THEN
            NEW.QTY = 0 - JSP.QTY
            NEW.DCOST = TOT.RC.DCOST - JSP.DCOST
            NEW.SALE = TOT.RC.SALE - JSP.SALE
            FOR I = 1 TO WPTR
               NEW.WIP<1,1,I> = REV.WIP<1,1,I> - JSP.WIP<1,1,I>
            NEXT I
         END ELSE
            NEW.QTY = 0
            PCT = DRC.COST<1,MLP> / (JSP.COST/1000)
            IF JSP.COST = JSP.DCOST THEN
               NEW.DCOST = 0 - DRC.COST<1,MLP>
            END ELSE
               NEW.DCOST = 0 - INT((JSP.DCOST/1000) * PCT + .5)
               IF TOT.RC.DCOST - NEW.DCOST > JSP.DCOST THEN
                  NEW.DCOST = TOT.RC.DCOST - JSP.DCOST
               END
            END
            IF JSP.SALE = JSP.COST THEN
               NEW.SALE = 0 - DRC.COST<1,MLP>
            END ELSE
               NEW.SALE = 0 - INT(((JSP.SALE-JSP.COST)/(JSP.COST/1000)) * (DRC.COST<1,MLP>/1000) + DRC.COST<1,MLP> + .5)
               IF TOT.RC.SALE - NEW.SALE > JSP.SALE THEN
                  NEW.SALE = TOT.RC.SALE - JSP.SALE
               END
            END
            BEGIN CASE
            CASE TOT.WIP = JSP.COST
               CHK.WIP = DRC.COST<1,MLP>
            CASE TOT.WIP = JSP.DCOST
               CHK.WIP = 0 - NEW.DCOST
            CASE 1
               CHK.WIP = INT((TOT.WIP/1000) * PCT + .5)
               IF TOT.RC.WIP + CHK.WIP > TOT.WIP THEN
                  CHK.WIP = TOT.WIP - TOT.RC.WIP
               END
            END CASE
            TOT.WIP = 0
            FOR I = 1 TO WPTR
               NEW.WIP<1,1,I> = 0 - INT((JSP.WIP<1,1,I>/1000) * PCT + .5)
               IF REV.WIP<1,1,I> - NEW.WIP<1,1,I> > JSP.WIP<1,1,I> THEN
                  NEW.WIP<1,1,I> = REV.WIP<1,1,I> - JSP.WIP<1,1,I>
               END
               TOT.WIP = TOT.WIP - NEW.WIP<1,1,I>
            NEXT I
            DIFF = CHK.WIP - TOT.WIP
            BEGIN CASE
            CASE DIFF = 0
            CASE DIFF > 0
               FOR I = WPTR TO 1 STEP -1 WHILE DIFF > 0
                  IF NEW.WIP<1,1,I> < 0 - DIFF THEN
                     IF REV.WIP<1,1,I>-NEW.WIP<1,1,I>+DIFF <= JSP.WIP<1,1,I> THEN
                        NEW.WIP<1,1,I> = NEW.WIP<1,1,I> - DIFF
                        TOT.WIP = TOT.WIP + DIFF
                        DIFF = 0
                     END
                  END
               NEXT I
            CASE 1
               FOR I = WPTR TO 1 STEP -1 WHILE DIFF < 0
                  IF NEW.WIP<1,1,I> < DIFF THEN
                     NEW.WIP<1,1,I> = NEW.WIP<1,1,I> - DIFF
                     TOT.WIP = TOT.WIP + DIFF
                     DIFF = 0
                  END
               NEXT I
            END CASE
            IF TOT.WIP <> CHK.WIP THEN
               DRC.STATUS<1,MLP> = "WIP MISMATCH ":OCONV(TOT.WIP,"MD2")
               GOTO 1999
            END
         END
         ADD.MON = 0; WIP.CNTR = 0
         FOR I = 1 TO WPTR
            INV.WIP<1,1,I> = INV.WIP<1,1,I> + 0
            BEGIN CASE
            CASE INV.WIP<1,1,I> < NEW.WIP<1,1,I>
               INV.WIP<1,1,I> = NEW.WIP<1,1,I>
               ADD.MON = 1; WIP.CNTR = 3
            CASE INV.WIP<1,1,I> <> 0
               ADD.MON = 1; WIP.CNTR = 2
            CASE NEW.WIP<1,1,I> <> 0
               WIP.CNTR = 2
            END CASE
         NEXT I
         IF ADD.MON THEN
            IF JSP.MON<1,WCNT> > JRC.MON THEN
               NEW.MON = JSP.MON<1,WCNT>
            END ELSE
               NEW.MON = JRC.MON
            END
            WCNT = WCNT + 1
            JSP.MON<1,WCNT> = NEW.MON
            JSP.WIP<1,WCNT> = INV.WIP
            CCW.ID = CONO : JSP.DIV : DEPT : "!" : CCTR : NEW.MON
            MATREADU CCW.REC FROM COST.CNTR.WIP, CCW.ID ELSE MAT CCW.REC = ""
            FOR I = 1 TO WPTR
               CCW.SP.O<1,I> = CCW.SP.O<1,I> - INV.WIP<1,1,I>
            NEXT I
         END ELSE
            IF JSP.MON<1,1> > JRC.MON THEN
               NEW.MON = JSP.MON<1,1>
            END ELSE
               NEW.MON = JRC.MON
            END
            CCW.ID = CONO : JSP.DIV : DEPT : "!" : CCTR : NEW.MON
            MATREADU CCW.REC FROM COST.CNTR.WIP, CCW.ID ELSE MAT CCW.REC = ""
         END
         IF WIP.CNTR THEN
            FOR I = 1 TO WPTR
               NEW.WIP<1,2,I> = 0 - NEW.WIP<1,1,I>
            NEXT I
            NEW.MON<1,2> = NEW.MON<1,1>
         END
         JSP.RC.COST = JSP.RC.COST + DRC.COST<1,MLP>
         MATWRITE JSP.REC ON JOB.SHIP, SP.F.ID : SEQ
         JSP.RC.COST = 0
         JSP.QTY = NEW.QTY
         JSP.SALE = NEW.SALE
         JSP.COST = 0 - DRC.COST<1,MLP>
         JSP.DCOST = NEW.DCOST
         JSP.DATE = DRC.DATE
         JSP.MON = NEW.MON
         JSP.WIP = NEW.WIP
         JSP.RC.TRAN = DRC.TRAN<1,MLP>
         JSP.RC.JOB = DRC.T.JOB
         JSP.RC.DESC = DRC.REASON<1,MLP>
         GOSUB 2000
         JSP.SEQ = NEW.SEQ
         JSP.GLA.DATE = ""
         MATWRITE JSP.REC ON JOB.SHIP, SP.F.ID : JOB.SP.SEQ<1,DFND>
         PPTR = COUNT(JRC.TRAN,VM) + (JRC.TRAN # "") + 1
         JRC.TRAN<1,PPTR> = DRC.TRAN<1,MLP>
         JRC.F.TRAN<1,PPTR> = NEW.SEQ
         JRC.SEQ<1,PPTR> = SEQ
         JRC.F.SEQ<1,PPTR> = JOB.SP.SEQ<1,DFND>
         NEW.SEQ = NEW.SEQ + 1
         MAT FROM.JOB.REC = MAT JOB.REC
         JSP.QTY = 0 - NEW.QTY
         JSP.SALE = 0 - NEW.SALE
         JSP.COST = DRC.COST<1,MLP>
         JSP.DCOST = 0 - NEW.DCOST
         JSP.DATE = DRC.DATE
         JSP.MON = JRC.MON
         JSP.WIP = NEW.WIP<1,2>
         IF WIP.CNTR THEN
            FOR I = 1 TO WPTR
               CCW.SP.I<1,I> = CCW.SP.I<1,I> + JSP.WIP<1,1,I>
               IF INV.WIP<1,1,I> <> NEW.WIP<1,1,I> THEN
                  JSP.WIP<1,2,I> = NEW.WIP<1,1,I> - INV.WIP<1,1,I>
                  CCW.SP.O<1,I> = CCW.SP.O<1,I> - JSP.WIP<1,2,I>
               END
               INV.WIP<1,1,I> = 0 - INV.WIP<1,1,I>
            NEXT I
            WIP.CNTR = 2
            IF JSP.WIP<1,2> # "" THEN JSP.MON<1,2> = NEW.MON<1,1>
         END
         JSP.RC.TRAN = DRC.TRAN<1,MLP>
         JSP.RC.JOB = DRC.F.JOB
         JSP.RC.DESC = DRC.REASON<1,MLP>
         MAT JOB.REC = MAT TO.JOB.REC
         PTR = 1
         LOOP
            LOCATE DEPT IN JOB.SP.DEPT<1>,PTR SETTING DFND ELSE
               JOB.SP.DEPT<1,DFND> = DEPT
               JOB.SP.CCTR<1,DFND> = CCTR
               JOB.SP.OPER<1,DFND> = JSP.OPER
               JOB.SP.VIA<1,DFND> = JSP.VIA
               JOB.SP.DATE<1,DFND,1> = JSP.DATE
            END
            IF CCTR = JOB.SP.CCTR<1,DFND> THEN
               PTR = 0
            END ELSE
               PTR = DFND + 1
            END
         WHILE PTR DO REPEAT
         IF JOB.STATUS = "" THEN
            JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"1")
            JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
         END
         IF JOB.STATUS<1,1> # 1 THEN
            JOB.STATUS = INSERT(JOB.STATUS,1,1,0,"5")
            JOB.STAT.DATE = INSERT(JOB.STAT.DATE,1,1,0,DATE())
         END
         GOSUB 2000
         JSP.SEQ = NEW.SEQ
         JSP.GLA.DATE = ""
         MATWRITE JSP.REC ON JOB.SHIP, SP.T.ID : JOB.SP.SEQ<1,DFND>
         MAT TO.JOB.REC = MAT JOB.REC
         MATWRITE CCW.REC ON COST.CNTR.WIP, CCW.ID
         JRC.T.TRAN<1,PPTR> = NEW.SEQ
         JRC.T.SEQ<1,PPTR> = JOB.SP.SEQ<1,DFND>
         JRC.DEPT<1,PPTR> = DRC.DEPT<1,MLP>
         JRC.CCTR<1,PPTR> = DRC.CCTR<1,MLP>
         JRC.OPER<1,PPTR> = DRC.OPER<1,MLP>
         JRC.EMP<1,PPTR> = DRC.EMP<1,MLP>
         JRC.REASON<1,PPTR> = DRC.REASON<1,MLP>
         JRC.COST<1,PPTR> = DRC.COST<1,MLP>
         DRC.DEPT = DELETE(DRC.DEPT,1,MLP,0)
         DRC.CCTR = DELETE(DRC.CCTR,1,MLP,0)
         DRC.OPER = DELETE(DRC.OPER,1,MLP,0)
         DRC.EMP = DELETE(DRC.EMP,1,MLP,0)
         DRC.TRAN = DELETE(DRC.TRAN,1,MLP,0)
         DRC.REASON = DELETE(DRC.REASON,1,MLP,0)
         DRC.COST = DELETE(DRC.COST,1,MLP,0)
         DRC.STATUS = DELETE(DRC.STATUS,1,MLP,0)
         DRC.SEQ = DELETE(DRC.SEQ,1,MLP,0)
         NEW.SEQ = NEW.SEQ + 1
1999  NEXT MLP
      WRITE NEW.SEQ ON CONTROL, CONO:"DAILY.SHIP"
      GOTO 99999
2000  IF WIP.CNTR THEN
         JOB.SP.WIP<1,1> = JOB.SP.WIP<1,1> + WIP.CNTR
         JOB.WIP<1,1> = JOB.WIP<1,1> + WIP.CNTR
         FOR I = 1 TO WPTR
            JOB.SP.WIP<1,2,I> = JOB.SP.WIP<1,2,I> + INV.WIP<1,1,I>
            JOB.SP.WIP<1,3,I> = JOB.SP.WIP<1,3,I> - INV.WIP<1,1,I> + JSP.WIP<1,1,I>
            JOB.WIP<1,2,I> = JOB.WIP<1,2,I> + INV.WIP<1,1,I>
            JOB.WIP<1,3,I> = JOB.WIP<1,3,I> - INV.WIP<1,1,I> + JSP.WIP<1,1,I>
         NEXT I
      END
      IF JSP.MON<1,1> < JOB.SP.WIP<1,4> OR JOB.SP.WIP<1,4> = "" THEN
         JOB.SP.WIP<1,4> = JSP.MON<1,1>
         IF JSP.MON<1,1> < JOB.WIP<1,4> OR JOB.WIP<1,4> = "" THEN
            JOB.WIP<1,4> = JSP.MON<1,1>
         END
      END
      IF JSP.MON<1,1> > JOB.SP.WIP<1,5> THEN
         JOB.SP.WIP<1,5> = JSP.MON<1,1>
         IF JSP.MON<1,1> > JOB.WIP<1,5> THEN
            JOB.WIP<1,5> = JSP.MON<1,1>
         END
      END
      JOB.SP.QTY<1,DFND> = JOB.SP.QTY<1,DFND> + JSP.QTY
*CSF 25042 v
      IF REV.ALL THEN
        JOB.QTY<1,2> = JOB.QTY<1,2> + JSP.QTY
        JOB.QTY<1,3> = JOB.QTY<1,3> + JSP.QTY
        IF JOB.QTY<1,2> < 0 THEN JOB.QTY<1,2> = 0
        IF JOB.QTY<1,3> < 0 THEN JOB.QTY<1,3> = 0
      END
*CSF 25042 ^
      JOB.SP.DCOST<1,DFND> = JOB.SP.DCOST<1,DFND> + JSP.DCOST
      JOB.SP.COST<1,DFND> = JOB.SP.COST<1,DFND> + JSP.COST
      JOB.SP.SALE<1,DFND> = JOB.SP.SALE<1,DFND> + JSP.SALE
      BEGIN CASE
      CASE JSP.TYPE = "N"
         PTR = 2
      CASE JSP.TYPE = "C"
         PTR = 3
      CASE JSP.TYPE = "S"
         PTR = 4
      CASE 1
         PTR = 1
      END CASE
      JOB.SP.TCOST<1,DFND,PTR> = JOB.SP.TCOST<1,DFND,PTR> + JSP.COST
      JOB.TOT.DCOST = JOB.TOT.DCOST + JSP.DCOST
      JOB.TOT.COST = JOB.TOT.COST + JSP.COST
      JOB.TOT.SALE = JOB.TOT.SALE + JSP.SALE
      JOB.SP.SEQ<1,DFND> = JOB.SP.SEQ<1,DFND> + 1
2999  RETURN
99999 RETURN
   END
