   SUBROUTINE EST.FORMULA.Q050 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q050
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate the number of sheets, pounds or yards
* of endleaf material required for casebound book.
*
*T26306 cmykleb * Problem with recalc after a qty is deleted.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.BIND.SPOIL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.QTY = ""
   TYP = 0
*T26306 v
*  LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE RETURN
*T26306 ^
   LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MP ELSE RETURN
*
*---- MAIN PROCESSING
*
   BEGIN CASE
      CASE NUM(EST.CASE.SPOIL<1,1>)
         SPOIL = INT(EQTY * (EST.CASE.SPOIL<1,1> / 100) + 0.99)
      CASE 1
         MATREAD BST.REC FROM ESTIMATE.BIND.SPOIL, CONO:EST.CASE.SPOIL<1,1> ELSE
            MAT BST.REC = ""
         END
         LOCATE EQTY IN BST.QTY<1>,1 BY "AR" SETTING BP ELSE NULL
         BEGIN CASE
            CASE BP = 1
               BSPCT = BST.PCT<1,BP>
            CASE BP > COUNT(BST.QTY,VM) + 1
               BSPCT = BST.PCT<1,BP-1>
            CASE BST.EXTR = "Y"
               BSPCT = INT(BST.PCT<1,BP>-(BST.QTY<1,BP>-EQTY)/(BST.QTY<1,BP>-BST.QTY<1,BP-1>)*(BST.PCT<1,BP>-BST.PCT<1,BP-1>)+0.5)
            CASE 1
               BSPCT = BST.PCT<1,BP>
         END CASE
         SPOIL = INT(EQTY * (BSPCT / 10000) + 0.99)
   END CASE
   PROD.QTY = EQTY + SPOIL
   NO.OUT = INT(EST.CASE.ENDLF.CUT.OUT / 100)
   IF NO.OUT = 0 THEN
      TEMP.QTY = 0
      SHEETS = ""
   END ELSE
      SHEETS = INT(PROD.QTY * 2 / NO.OUT + 0.99)
      PRICE.UNIT = EST.CASE.ENDLF.PRICE<1,2>
      BEGIN CASE
         CASE PRICE.UNIT[1,2] = "LB"
            IF EST.CASE.ENDLF.SRC = "R" THEN
               XXLENGTH = EST.BIND.US.LENGTH
            END ELSE
               XXLENGTH = EST.CASE.ENDLF.SIZE<1,2>
            END
            M.WEIGHT = EST.CASE.ENDLF.SPEC<1,1> * (EST.CASE.ENDLF.SIZE<1,1>/10000 * XXLENGTH/10000) / 950 * 2
            TEMP.QTY = INT(M.WEIGHT * (SHEETS / 1000) + 0.5)
            EST.CASE.ENDLF.QTY<1,1,QPTR> = TEMP.QTY
            EST.CASE.ENDLF.QTY<1,2> = "LBS"
         CASE PRICE.UNIT[1,2] = "YD"
            XXLENGTH = EST.BIND.US.LENGTH
            TEMP.QTY = INT(SHEETS * (XXLENGTH/10000) / 36 + 0.99)
            EST.CASE.ENDLF.QTY<1,1,QPTR> = TEMP.QTY
            EST.CASE.ENDLF.QTY<1,2> = "YDS"
         CASE PRICE.UNIT = "SHT" OR PRICE.UNIT = "EA"
            TEMP.QTY = SHEETS
            EST.CASE.ENDLF.QTY<1,1,QPTR> = TEMP.QTY
            EST.CASE.ENDLF.QTY<1,2> = "SHT"
      END CASE
   END
   TEMP.FCTR = 1000
   TEMP.STD = EST.CASE.ENDLF.PRICE<1,1,QPTR> / 100
   TEMP.STD.TYPE = "$/":EST.CASE.ENDLF.PRICE<1,2>
   TEMP.UM = EST.CASE.ENDLF.PRICE<1,3>
   TEMP.IND.1 = ESTM.FOH.PCT<1,MP>
   TEMP.MARKUP = ESTM.MARKUP<1,MP>
   TEMP.DESC = EST.CASE.ENDLF<1,2>
   TEMP.VSALE = "Y"
   RETURN
END
