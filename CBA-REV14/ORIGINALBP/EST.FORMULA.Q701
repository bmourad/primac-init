      SUBROUTINE EST.FORMULA.Q701 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q701
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/26/86
*
* DESCRIPTION
*
* This formula is used to calculate run time requirements for specified
* folder and job specification.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEMP.QTY = ""
      TYP = 0
*
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
10*
      IF QPTR = 1 AND ACTION # "R" THEN
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=13;MAXL=3;MINV=1;MAXV=999;JUSTIFY="L"
         O.R = "O"
         BEGIN CASE
         CASE TEMP.FVAR1 # ""
            DEFAULT = TEMP.FVAR1
         CASE 1
            DEFAULT = 1
         END CASE
         PMSG = "Number up to fold (Default = ":DEFAULT:") ?"
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         NUMBER.UP = VALUE
         TEMP.FVAR1 = VALUE
      END ELSE
         NUMBER.UP = TEMP.FVAR1
      END
20*
      IF QPTR = 1 AND ACTION # "R" THEN
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=13;MAXL=3;MINV=1;MAXV=999;JUSTIFY="L"
         O.R = "O"
         BEGIN CASE
         CASE TEMP.FVAR2 # ""
            DEFAULT = TEMP.FVAR2
         CASE 1
            DEFAULT = 1
         END CASE
         PMSG = "Number of forms (Default = ":DEFAULT:") ?"
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 20
         FORMS = VALUE
         TEMP.FVAR2 = VALUE
      END ELSE
         FORMS = TEMP.FVAR2
      END
*
*---- MAIN PROCESSING
*
      PROD.QTY = EQTY + FIELD(EST.PROD.BS.REQ<1,COMP,1>,"!",QPTR)
      IF NUMBER.UP + 0 = 0 OR FORMS + 0 = 0 THEN
         TEMP.QTY = 0
      END ELSE
        TEMP.QTY = INT(PROD.QTY / NUMBER.UP * FORMS + 0.99)
      END
      TEMP.VSALE = "Y"
      RETURN
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
