      SUBROUTINE EST.FORMULA.Q065 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q065
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate the number of sheets or yards of spine
* cloth required for casebound book.
*
*T26306 cmykleb * Problem with recalc after a qty is deleted.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.BIND.SPOIL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEMP.QTY = ""
      TYP = 0
*
*T26306 v
*     LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE RETURN
*T26306 ^
      IF TEMP.TYPE[1,1] = "M" THEN
         LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MP ELSE RETURN
      END
*
      CLOTH.WIDTH = EST.CASE.SPINE.CL.CUT.SIZE<1,1> / 10000
      CLOTH.LENGTH = EST.CASE.SPINE.CL.CUT.SIZE<1,2> / 10000
      NO.OUT = INT(EST.CASE.SPINE.CL.CUT.OUT / 100)
*
*---- MAIN PROCESSING
*
      BEGIN CASE
      CASE NUM(EST.CASE.SPOIL<1,3>)
         SPOIL = INT(EQTY * (EST.CASE.SPOIL<1,3> / 100) + 0.99)
      CASE 1
         MATREAD BST.REC FROM ESTIMATE.BIND.SPOIL, CONO:EST.CASE.SPOIL<1,3> ELSE
            MAT BST.REC = ""
         END
         LOCATE EQTY IN BST.QTY<1>,1 BY "AR" SETTING BP ELSE NULL
         BEGIN CASE
         CASE BP = 1
            BSPCT = BST.PCT<1,BP>
         CASE BP > COUNT(BST.QTY,VM) + 1
            BSPCT = BST.PCT<1,BP-1>
         CASE BST.EXTR = "Y"
            BSPCT = INT(BST.PCT<1,BP>-(BST.QTY<1,BP>-EQTY)/(BST.QTY<1,BP>-BST.QTY<1,BP-1>)*(BST.PCT<1,BP>-BST.PCT<1,BP-1>)+0.5)
         CASE 1
            BSPCT = BST.PCT<1,BP>
         END CASE
         SPOIL = INT(EQTY * (BSPCT / 10000) + 0.99)
      END CASE
      PROD.QTY = EQTY + SPOIL
      BEGIN CASE
      CASE EST.BIND.COVER.STYLE # "C"
         TEMP.QTY = 0
      CASE NO.OUT = 0
         TEMP.QTY = 0
      CASE EST.CASE.PIECES # 3
         TEMP.QTY = 0
      CASE EST.CASE.SPINE.CL.SRC = "S"
         TEMP.QTY = INT(PROD.QTY / NO.OUT + 0.99)
      CASE EST.CASE.PIECES = 3
         TEMP.QTY = INT(PROD.QTY * CLOTH.LENGTH / 36 / NO.OUT + 0.99)
      CASE 1
         TEMP.QTY = 0
      END CASE
      IF EST.CASE.SPINE.CL.SRC = "S" THEN
         EST.CASE.SPINE.CL.QTY<1,1,QPTR> = TEMP.QTY
         EST.CASE.SPINE.CL.QTY<1,2> = "SHT"
      END ELSE
         EST.CASE.SPINE.CL.QTY<1,1,QPTR> = TEMP.QTY
         EST.CASE.SPINE.CL.QTY<1,2> = "YDS"
      END
      IF TEMP.TYPE[1,1] = "M" THEN
         TEMP.FCTR = 1000
         TEMP.STD = EST.CASE.SPINE.CL.PRICE<1,1,QPTR> / 100
         TEMP.STD.TYPE = "$/":EST.CASE.SPINE.CL.PRICE<1,2>
         TEMP.UM = EST.CASE.SPINE.CL.PRICE<1,3>
         TEMP.IND.1 = ESTM.FOH.PCT<1,MP>
         TEMP.MARKUP = ESTM.MARKUP<1,MP>
         TEMP.DESC = EST.CASE.SPINE.CL<1,2>
      END
      TEMP.VSALE = "Y"
      RETURN
   END
