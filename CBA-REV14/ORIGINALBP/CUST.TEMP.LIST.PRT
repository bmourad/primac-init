*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - DRSBP
* PROGRAM     - CUST.TEMP.LIST.PRT
* AUTHOR      - L. ROSS, C.B.A.
* DATE        - 10/01/91
* DESCRIPTION
* This program prints the customer template list records for a specific 
* customer/template.
*T25901 cmykleb 03/14/2002 * Change heading to use GET.PROG.HEAD pgm.
*********************************************************************
*
***** INSERT FILES EQUATES
*
*COPY>JCS.CPYLIB>SHIPTO.MASTER
*COPY>JCS.CPYLIB>CUST.TEMPLATE.LIST
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
   OPEN '','SHIPTO.MASTER' TO SHIPTO.MASTER ELSE
      ERRMSG = 'SHIPTO.MASTER FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','CUST.TEMPLATE.LIST' TO CUST.TEMPLATE.LIST ELSE
      ERRMSG = 'CUST.TEMPLATE.LIST FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','CUSTOMER' TO CUSTOMER ELSE
      ERRMSG = 'CUSTOMER FILE IS MISSING'
      GOTO 93000
   END
*
**** READ PROC VALUES
*
   PROCREAD ID ELSE
      ERRMSG = 'CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
   CONO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = ID<2>
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   CUST.NO = ID<3>
   TEMP.NO = ID<4>
*
**** INITIALIZATION
*
   LINE.COUNT = 99
   PAGE.NO = 0
   HD3 = 'FOR CUSTOMER: ':CUST.NO 'L#6':SPACE(6)
   HD4 = '    TEMPLATE: ':TEMP.NO 'L#10':'  '
   HH.LINE1 = 'RUN#  SHIPTO#  STORE  SHIPTO NAME':SPACE(20):'DESTINATION NAME'
   HH.LINE2 = '----  -------  -----  ':STR('-',30):' ':STR('-',30)
*
***** READ AND PROCESS FIRST RECORD
*
100 *
   READNEXT KEY ELSE GOTO 99999
   MATREAD CUST.REC FROM CUSTOMER, CONO:CUST.NO ELSE
      CUST.NAME = '??????????'
   END
   HD3 = HD3:CUST.NAME 'L#30'
   MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, CONO:CUST.NO:"!":TEMP.NO:"!001" ELSE
      ERRMSG = 'The CUST.TEMPLATE.LIST record is missing'
      GOSUB 91000
      GOTO 99999
   END
   HD4 = HD4:CTL.DESC 'L#30'
   IF CTL.STATUS<1,1> > 0 THEN HD4 = HD4:' - (STATUS IS FROZEN)'
   SHIPTO = CTL.SHIPS<1,1>
   IF SHIPTO = '' THEN
      ERRMSG = 'The first record of CUST.TEMPLATE.LIST has no shipto# - run resequencing'
      GOSUB 91000
      GOTO 99999
   END
   SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
   PRINTER ON
   RUN.NO = 1
   GOSUB 9000
   RSEQ = '001'
   KEY = CONO:CUST.NO:"!":TEMP.NO:"!"
*
***** READ AND PROCESS ALL RECORDS
*
   DATA = 1
   LOOP
      IF SHIP.CNT < 1 THEN
         RSEQ =RSEQ + 1
         IF LEN(RSEQ) < 3 THEN RSEQ = STR('0',3-LEN(RSEQ)):RSEQ
         MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, KEY:RSEQ THEN
            SHIP.CNT = DCOUNT(CTL.SHIPS,VM)
         END ELSE DATA = 0
         IF SHIP.CNT < 1 THEN DATA = 0
      END
   WHILE DATA = 1 DO
      FOR I = 1 TO SHIP.CNT
         SHIPTO = CTL.SHIPS<1,I>
         MATREAD SHM.REC FROM SHIPTO.MASTER, CONO:SHIPTO ELSE
            MAT SHM.REC = ''
            SHM.SP.NAME = '??????????????????????'
            SHM.DT.NAME = '??????????????????????'
         END
         PLINE = ''
         GOSUB 1000
         RUN.NO = RUN.NO + 1
      NEXT I
      SHIP.CNT = 0
   REPEAT
   GOTO 99999
*
**** DETAIL ROUTINE
*
1000 *
*
   PLINE = RUN.NO 'R#4':'  ':SHIPTO 'R#7':'  ':SHM.SP.STORE 'R#5':'  ':SHM.SP.NAME 'L#30':' ':SHM.DT.NAME 'L#30'
   IF LINE.COUNT > 52 THEN GOSUB 9000
   PRINT PLINE
   LINE.COUNT = LINE.COUNT + 1
   PLINE = ''
   RETURN
*
***** PRINT HEADINGS
*
9000 *
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT HD3
   PRINT HD4
   PRINT
   PRINT HH.LINE1
   PRINT HH.LINE2
   PRINT
   LINE.COUNT = 0
   RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
   PRINTER OFF
END
