   SUBROUTINE EST.INSERT.CASE (CONO, EQTY, DEPT, COMP, REF.NO)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.INSERT.CASE
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/04/86
*
* DESCRIPTION
*
* This subroutine updates the ESTIMATE.DEPT data with casemaking materials.
*
* T26306 cmykleb 11/29/2001 * When estimate quantites are added, use the
*                             first quantity price for the new quantities
*                             in endleaf, board and cloth.
*T27716 cmykleb 10/09/2003 * Add Pre-pres inclusive functionality.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
   TEST.FLAG = 0                    ;****  T E S T   F L A G  ****
   GRP.ID = TEMP.GRP.ID
   GRP.QTY = TEMP.GRP.QTY
   GRP.FCTR = TEMP.GRP.FCTR
   GRP.TYPE = TEMP.TYPE
   GRP.CCTR = TEMP.CCTR
   GRP.QCALC = TEMP.GRP.QCALC
   GRP.QPARAM = TEMP.GRP.QPARAM
   GRP.QOR = TEMP.GRP.QOR
   GRP.FCALC = TEMP.GRP.FCALC
   GRP.FPARAM = TEMP.GRP.FPARAM
   GRP.FOR = TEMP.GRP.FOR
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE RETURN
*T26306 v
   ECNT = DCOUNT(EST.QTY<1>,VM)
*T26306 ^
   MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:"C" ELSE RETURN
*
*---- MAIN PROCESSING
*
*
*---- PROCESS ENDLEAF DATA
*
100*
   IF EST.CASE.ENDLF<1,1> # "" THEN
*T26306 v
      FOR E = 2 TO ECNT
         IF EST.CASE.ENDLF.PRICE<1,1,E> = '' THEN
            EST.CASE.ENDLF.PRICE<1,1,E> = EST.CASE.ENDLF.PRICE<1,1,1>
         END
      NEXT E
*T26306 ^
      MAT ESTD.TEMP = ""
      TEMP.GRP.ID = GRP.ID
      TEMP.GRP.QTY = GRP.QTY
      TEMP.GRP.FCTR = GRP.FCTR
      TEMP.TYPE = GRP.TYPE
      TEMP.CCTR = GRP.CCTR
      TEMP.GRP.QCALC = GRP.QCALC
      TEMP.GRP.QPARAM = GRP.QPARAM
      TEMP.GRP.QOR = GRP.QOR
      TEMP.GRP.FCALC = GRP.FCALC
      TEMP.GRP.FPARAM = GRP.FPARAM
      TEMP.GRP.FOR = GRP.FOR
      TEMP.OPV = EST.CASE.ENDLF<1,1>
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING CP ELSE GOTO 120
      TEMP.STD = EST.CASE.ENDLF.PRICE<1,1,QPTR> / 100
      TEMP.STD.TYPE = EST.CASE.ENDLF.PRICE<1,2>
      TEMP.UM = EST.CASE.ENDLF.PRICE<1,3>
      TEMP.DESC = EST.CASE.ENDLF<1,2>
      GOSUB 500
      GOSUB 1000
   END
*
*---- PROCESS BOARD DATA
*
120*
   IF EST.CASE.BOARD<1,1> # "" THEN
*T26306 v
      FOR E = 2 TO ECNT
         IF EST.CASE.BOARD.PRICE<1,1,E> = '' THEN
            EST.CASE.BOARD.PRICE<1,1,E> = EST.CASE.BOARD.PRICE<1,1,1>
         END
      NEXT E
*T26306 ^
      MAT ESTD.TEMP = ""
      TEMP.GRP.ID = GRP.ID
      TEMP.GRP.QTY = GRP.QTY
      TEMP.GRP.FCTR = GRP.FCTR
      TEMP.TYPE = GRP.TYPE
      TEMP.CCTR = GRP.CCTR
      TEMP.OPV = EST.CASE.BOARD<1,1>
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING CP ELSE GOTO 140
      TEMP.STD = EST.CASE.BOARD.PRICE<1,1,QPTR> / 100
      TEMP.STD.TYPE = EST.CASE.BOARD.PRICE<1,2>
      TEMP.UM = EST.CASE.BOARD.PRICE<1,3>
      TEMP.DESC = EST.CASE.BOARD<1,2>
      GOSUB 500
      GOSUB 1000
   END
*
*---- PROCESS SIDE CLOTH DATA
*
140*
   IF EST.CASE.SIDE.CL<1,1> # "" THEN
*T26306 v
      FOR E = 2 TO ECNT
         IF EST.CASE.SIDE.CL.PRICE<1,1,E> = '' THEN
            EST.CASE.SIDE.CL.PRICE<1,1,E> = EST.CASE.SIDE.CL.PRICE<1,1,1>
         END
      NEXT E
*T26306 ^
      MAT ESTD.TEMP = ""
      TEMP.GRP.ID = GRP.ID
      TEMP.GRP.QTY = GRP.QTY
      TEMP.GRP.FCTR = GRP.FCTR
      TEMP.TYPE = GRP.TYPE
      TEMP.CCTR = GRP.CCTR
      TEMP.OPV = EST.CASE.SIDE.CL<1,1>
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING CP ELSE GOTO 160
      TEMP.STD = EST.CASE.SIDE.CL.PRICE<1,1,QPTR> / 100
      TEMP.STD.TYPE = EST.CASE.SIDE.CL.PRICE<1,2>
      TEMP.UM = EST.CASE.SIDE.CL.PRICE<1,3>
      TEMP.DESC = EST.CASE.SIDE.CL<1,2>
      GOSUB 500
      GOSUB 1000
   END
*
*---- PROCESS SPINE CLOTH DATA
*
160*
   IF EST.CASE.SPINE.CL<1,1> # "" THEN
*T26306 v
      FOR E = 1 TO ECNT
         IF EST.CASE.SPINE.CL.PRICE<1,1,E> = '' THEN
            EST.CASE.SPINE.CL.PRICE<1,1,E> = EST.CASE.SPINE.CL.PRICE<1,1,1>
         END
      NEXT E
*T26306 ^
      MAT ESTD.TEMP = ""
      TEMP.GRP.ID = GRP.ID
      TEMP.GRP.QTY = GRP.QTY
      TEMP.GRP.FCTR = GRP.FCTR
      TEMP.TYPE = GRP.TYPE
      TEMP.CCTR = GRP.CCTR
      TEMP.OPV = EST.CASE.SPINE.CL<1,1>
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING CP ELSE GOTO 180
      TEMP.STD = EST.CASE.SPINE.CL.PRICE<1,1,QPTR> / 100
      TEMP.STD.TYPE = EST.CASE.SPINE.CL.PRICE<1,2>
      TEMP.UM = EST.CASE.SPINE.CL.PRICE<1,3>
      TEMP.DESC = EST.CASE.SPINE.CL<1,2>
      GOSUB 500
      GOSUB 1000
   END
*
*---- ALL DONE
*
180*
   GOTO 99999
*
*---- STORE DATA FROM MATERIAL FILE
*
500*
   IF TEMP.GRP.QCALC = "" THEN
      TEMP.GRP.QCALC = ESTM.QCALC<1,CP>
      TEMP.GRP.QPARAM = ESTM.QPARAM<1,CP>
      TEMP.GRP.QOR = ESTM.QOR<1,CP>
   END
   IF TEMP.GRP.FCALC = "" THEN
      TEMP.GRP.FCALC = ESTM.FCALC<1,CP>
      TEMP.GRP.FPARAM = ESTM.FPARAM<1,CP>
      TEMP.GRP.FOR = ESTM.FOR<1,CP>
   END
   BEGIN CASE
      CASE TEMP.GRP.QCALC = ""
         TEMP.QTY = ""
      CASE TEMP.GRP.QCALC = "C"
         TEMP.QTY = TEMP.GRP.QPARAM
      CASE TEMP.GRP.QCALC = "F"
         TEMP.FPTR = REF.NO + 1
         SUB.NAME = "EST.FORMULA.Q":TEMP.GRP.QPARAM
         IF TEST.FLAG THEN MSG=SUB.NAME;GOSUB 90000
         CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
*        IF TEMP.QTY = 0 THEN GOTO 390
         IF TEMP.QTY = "" THEN
            TEMP.GRP.QOR = ""
         END
   END CASE
   BEGIN CASE
      CASE TEMP.GRP.FCALC = ""
         TEMP.FCTR = 1000
      CASE TEMP.GRP.FCALC = "C"
         TEMP.FCTR = INT(TEMP.GRP.FPARAM*1000+0.5)
      CASE TEMP.GRP.FCALC = "M"
         TEMP.FCTR = INT(GRP.FCTR*TEMP.GRP.FPARAM+0.5)
      CASE TEMP.GRP.FCALC = "F"
         TEMP.FPTR = REF.NO + 1
         SUB.NAME = "EST.FORMULA.F":TEMP.GRP.FPARAM
         IF TEST.FLAG THEN MSG=SUB.NAME;GOSUB 90000
         CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
*        IF TEMP.FCTR = 0 THEN GOTO 390
         IF TEMP.FCTR = "" THEN TEMP.GRP.FOR = ""
      CASE TEMP.GRP.FCALC = "T"
         TBL.ID = TEMP.GRP.FPARAM
         CALL EST.TABLE.FCTR (CONO, TBL.ID, TEMP.GRP.FCTR)
         IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
   END CASE
   TEMP.IND.1 = ESTM.FOH.PCT<1,CP>
   TEMP.MARKUP = ESTM.MARKUP<1,CP>
   RETURN
*
*---- STORE LINE ITEM DATA
*
1000*
*T27716 v
*  CALL EST.CALC.COST (CONO)
   CALL EST.CALC.COST (CONO,DEPT)
*T27716 ^
   REF.NO = REF.NO + 1
   FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
      ESTD.REC(A) = INSERT(ESTD.REC(A),1,REF.NO,0,ESTD.TEMP(A))
   NEXT A
   ESTD.QTY<1,REF.NO> = TEMP.QTY * 100
   ESTD.STD<1,REF.NO> = TEMP.STD * 100
   ESTD.GRP.QTY<1,REF.NO> = TEMP.GRP.QTY * 100
   RETURN
*
*---- DISPLAY ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:MSG:
*       INPUT REPLY:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
