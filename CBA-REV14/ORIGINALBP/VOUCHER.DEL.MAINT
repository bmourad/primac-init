*COPY>CPYLIB>COM1
******************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - APSBP
* PROGRAM     - VOUCHER.DEL.MAINT
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 06/07/84
* DESCRIPTION -
* 06/28/91 DLG TASK 15960 - ALLOW 'REVERS','DELETE' CHECK# VOUCHERS
*                           TO BE DELETED.
* 10/14/94 CLW Task 17919 - Accrual accouting
* T19149 RKB 05/01/95 add support for ap div,dept,cctr fields
* T19406 Bob Sherer 10/11/95 Display owning division
*T23278 markt 11/24/1998 * Add check for divisional security.
*                          Make fiscal data multi-value by division.
*T25632 lanny 02/13/2001 * Voucher Numbers are Sub-valued in VPDS.VOU.NO.
*T25755 cm 04/19/2001 * Modify pgm to work with new seq # in then
*                       OUTSIDE.PO file.
*T26249 lanny 10/31/2001 * DV/DP/CC left off Accrued Liab Hist.
*                          See Task 24522
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*T23880 lross 10/16/2002 * Include offset acct in ALH record for OS 
*                          PO's.   
*T27646 thompson 08/19/2003 * MODIFY FOR MULTI PLATFORM ISSUE
*T28779 lross 02/07/2006 * Retain identity of multiple same Prod/Whse
*                          lines.
*ENDDOC
*******************************************************************
**** INSERT FILE EQUATES
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>APS.CPYLIB>OAP
*COPY>APS.CPYLIB>CKP
*COPY>APS.CPYLIB>SQV
*COPY>APS.CPYLIB>HDCH
*COPY>APS.CPYLIB>VOUCHERS
*COPY>APS.CPYLIB>APCHECK
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COA
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>JCS.CPYLIB>JOB.OSP
*COPY>JCS.CPYLIB>JOB
*COPY>FAS.CPYLIB>EQUIP.FILE
*COPY>FAS.CPYLIB>ASSETS
*COPY>FAS.CPYLIB>REPAIRS
*COPY>APS.CPYLIB>VEND.STATS
*COPY>APS.CPYLIB>VEND.PO.STATS
*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>APS.CPYLIB>VEND.VOUCH.STATS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
* 17919
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>CATEGORY.MISC
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>PMC.CPYLIB>PO
*COPY>POS.CPYLIB>MISC.PO
*COPY>POS.CPYLIB>ACCRUED.LIAB.HIST
*COPY>ICS.CPYLIB>WAREHOUSE            ; * T26249
DIM SVEND.REC(50)
EQU SVEND.DESC TO SVEND.REC(1)
EQU SVEND.ADDR1 TO SVEND.REC(2)
EQU SVEND.CT.ST TO SVEND.REC(4)
EQU SVEND.ZIP TO SVEND.REC(5)
EQU SVEND.TERMS TO SVEND.REC(6)
EQU SVEND.OP.BAL TO SVEND.REC(13)
DIM L.VOU(2)
EQU VOU.NO TO L.VOU(1)
EQU VOU.DATE TO L.VOU(2)
DIM OPAX.REC(10)
EQU VOU.NUM TO OPAX.REC(1)
**** SETUP FOR SYSTEM ERRMSGS
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
**** OPEN FILES
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING' ; GOTO 93000
OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING' ; GOTO 93000
OPEN '','INV.XREF' TO INV.XREF ELSE ERRMSG = 'INV.XREF FILE MISSING' ; GOTO 93000
OPEN '','OAP' TO OAP ELSE ERRMSG = "OAP FILE IS MISSING" ; GOTO 93000
OPEN '','CKP' TO CKP ELSE ERRMSG = "CKP FILE IS MISSING" ; GOTO 93000
OPEN '','SQV' TO SQV ELSE ERRMSG = "SQV FILE IS MISSING" ; GOTO 93000
OPEN '','VEND.STATS' TO VEND.STATS ELSE ERRMSG = "VEND.STATS FILE IS MISSING" ; GOTO 93000
OPEN '','VEND.PO.STATS' TO VEND.PO.STATS ELSE ERRMSG = "VEND.PO.STATS FILE IS MISSING" ; GOTO 93000
OPEN '','VEND.PROD.STATS' TO VEND.PROD.STATS ELSE ERRMSG = "VEND.PROD.STATS FILE IS MISSING" ; GOTO 93000
OPEN '','VEND.VOUCH.STATS' TO VEND.VOUCH.STATS ELSE ERRMSG = "VEND.VOUCH.STATS FILE IS MISSING" ; GOTO 93000
OPEN '','HDCH' TO HDCH ELSE ERRMSG = "HDCH FILE IS MISSING" ; GOTO 93000
OPEN '','VOUCHERS' TO VOUCHERS ELSE ERRMSG = "VOUCHERS FILE IS MISSING" ; GOTO 93000
OPEN '','VOUCHERS.TAG' TO VOUCHERS.TAG ELSE ERRMSG = "VOUCHERS.TAG FILE IS MISSING" ; GOTO 93000
OPEN '','VEND' TO VEND ELSE ERRMSG = 'VEND FILE MISSING' ; GOTO 93000
OPEN '','APCHECK' TO APCHECK ELSE ERRMSG = 'APCHECK FILE MISSING' ; GOTO 93000
OPEN '','APS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'APS.SCREENS FILE MISSING' ; GOTO 93000
OPEN '','PREFIX' TO PREFIX ELSE ERRMSG = 'PREFIX FILE MISSING' ; GOTO 93000
OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG = 'INVENTORY FILE MISSING' ; GOTO 93000
OPEN 'CATEGORY' TO CATEGORY ELSE ERRMSG = 'CATEGORY FILE MISSING' ; GOTO 93000
OPEN 'CATEGORY.MISC' TO CATEGORY.MISC ELSE ERRMSG = 'CATEGORY.MISC FILE MISSING' ; GOTO 93000
OPEN 'CATEGORY.OSP' TO CATEGORY.OSP ELSE ERRMSG = 'CATEGORY.OSP FILE MISSING' ; GOTO 93000
*T26249 v
OPEN 'WAREHOUSE' TO WAREHOUSE ELSE ERRMSG = 'WAREHOUSE FILE MISSING' ; GOTO 93000
*T26249 ^
***** GET COMPANY NAME
MAT COMP.REC = ''
CONO = ''
CALL GET.CONO(CONO,MAT COMP.REC)
IF CONO = 'END' THEN GOTO 99999
IN.ACCT.LEN = LEN(CO.ACCT.PIC)
IF CO.GLS = "Y" THEN
  OPEN '','COA' TO COA ELSE  ERRMSG = 'COA FILE IS MISSING';GOTO 93000
END
IF CO.JCS = 'Y' THEN
  OPEN '','JOB' TO JOB ELSE ERRMSG = "JOB FILE IS MISSING" ; GOTO 93000
  OPEN '','JOB.OSP' TO JOB.OSP ELSE ERRMSG = "JOB.OSP FILE IS MISSING" ; GOTO 93000
END
IF CO.FAS = "Y" THEN
  OPEN '','ASSETS' TO ASSETS ELSE ERRMSG = "ASSETS FILE IS MISSING" ; GOTO 93000
  OPEN '','EQUIP.FILE' TO EQUIP.FILE ELSE ERRMSG = "EQUIP.FILE FILE IS MISSING" ; GOTO 93000
  OPEN '','REPAIRS' TO REPAIRS ELSE ERRMSG = "REPAIRS FILE IS MISSING" ; GOTO 93000
END
MATREAD GLTABLE.REC FROM CONTROL ,CONO:"GLTABLE" ELSE
  ERRMSG = "GLTABLE IS MISSING FROM CONTROL FILE" ; GOTO 93000
END
IF CO.POS = "Y" THEN
  OPEN '','OUTSIDE.PO' TO OUTSIDE.PO ELSE ERRMSG = 'OUTSIDE.PO FILE IS MISSING';GOTO 93000
  OPEN '','OPOAPS.XREF' TO OPOAPS.XREF ELSE ERRMSG = 'OPOAPS.XREF FILE IS MISSING';GOTO 93000
  OPEN 'PO' TO PO ELSE ERRMSG = 'PO FILE MISSING' ; GOTO 93000
  OPEN 'MISC.PO' TO MISC.PO ELSE ERRMSG = 'MISC.PO FILE MISSING' ; GOTO 93000
  OPEN 'ACCRUED.LIAB.HIST' TO ACCRUED.LIAB.HIST ELSE ERRMSG = 'ACCRUED.LIAB.HIST FILE MISSING' ; GOTO 93000
END
***** T23278 v
READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
  ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
  GOTO 93000
END
READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
  ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
  GOTO 93000
END
***** T23278 ^
*T26249 v
GEN.DIV = '00' ; GEN.DEPT = '00' ; GEN.CCTR = '000'
*T26249 ^
***** MAIN PROCESSING
  LOGNAME = 'LOGNAME'; CALL SYSVARS.SUB(LOGNAME) ; *T27646
MAT EDIT.COM.DRIVER = '' ; MAT GEN.XREF.REC = ''
* TYP = 0 ;CALL EDIT.SUB
FILL = "#"
ECD.SCRN.CNT = 1
ECD.SCRN.NAME<1> = 'VOUCHER.DEL.MAINT'
ECD.ACTION=1;CALL SCRN.EDIT
**** PRINT SCREEN
1 *
MAT OAP.REC = '' ;MAT HDCK.REC = '' ;MAT SQV.REC = '' ; MAT APCK.REC = '' ; MAT OPO.REC = '' ; MAT JOB.REC = ''
MAT JOS.REC = ''
ACCRUE.OFFACCT = '' ;*T23880 
ECD.SCRN.NO = 1
MAT SCV.REC = ""
ECD.ACTION=6;CALL SCRN.EDIT
*
**** GET VOUCHERS FISCAL PERIOD
*
READ FIS.REC FROM CONTROL , CONO:"APVFISCAL" ELSE
  ERRMSG = "Vouchers fiscal record is missing from control" ; GOTO 93000
END
*  VOUCH.MON = FIS.REC<1>;* T23278
***** ENTER VOUCHER NUMBER
5 *
PAY.FLG = 0 ; SQV.FLG = 0 ; JOB.NUMBER = ''
RELEASE
ECD.NUM = 5
ECD.ACTION=4;CALL SCRN.EDIT
V.CODE = ECD.RET.VALUE
BEGIN CASE
  CASE ECD.RET.VALUE = 'END'
    GOTO 99999
  CASE ECD.RET.VALUE # ''
      * T26126 v
    MATREADU OAP.REC FROM OAP , CONO:V.CODE LOCKED
      ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 5 
    END ELSE
      MATREADU OAP.REC FROM HDCH , CONO:V.CODE LOCKED
        ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
      * T26126 ^
        ERRMSG = "VOUCHER NUMBER (":V.CODE:") NOT ON FILE" ; GOSUB 91000 ; GOTO 5
      END
      PAY.FLG = 1
    END
*T23278 v
    DIV.CODE = OAP.DIV.OWNER; USER.ID = UPCASE(LOGNAME); ERRMSG = '' ; *T27646
    CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
    IF ERRMSG # '' THEN
      GOSUB 91000; GOTO 5
    END
    IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
      IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
        ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
        GOSUB 91000; GOTO 5
      END
      LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING POS ELSE
        ERRMSG = "Division ":DIV.CODE:" not found on Control File DIVISIONS Record"
        GOTO 93000
      END
    END ELSE
      POS = 1
    END
    VOUCH.MON = FIS.REC<1,POS>
*T23278 ^
    SCH.FLG = 0
    MATREAD CKP.REC FROM CKP , CONO:V.CODE ELSE SCH.FLG = 1
    IF SCH.FLG = 0 THEN
      ERRMSG = "THIS VOUCHER IS SELECTED TO BE PAID CAN'T DELETE" ; GOSUB 91000 ; GOTO 5
    END
    IF OAP.DUE.DATE = "ADVANCE" THEN
        * T26126 v
      MATREADU OAP.REC FROM HDCH , CONO:V.CODE LOCKED
        ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
        * T26126 ^
        ERRMSG = "VOUCHER NUMBER (":V.CODE:") NOT ON FILE";GOSUB 91000;GOTO 5
      END
    END
    IF PAY.FLG = 0 THEN
        * T26126 v
      MATREADU SQV.REC FROM SQV , CONO:V.CODE LOCKED
        ERRMSG = 'VOUCHER record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
        * T26126 ^
        SQV.FLG = 1
      END
    END
    GOSUB 1000
    ECD.ACTION=3;CALL SCRN.EDIT
END CASE
GOSUB 60000
GOTO 1
***** GET ALL VALUES FROM FILE
1000 *
MATREAD VEND.REC FROM VEND , CONO:OAP.VEND<1,1> ELSE
  MAT VEND.REC = "??????????"
END
IF COUNT(OAP.VEND,VM) + (OAP.VEND # "") # 1 THEN
  VEND.DESC = OAP.VEND<1,2>
  VEND.ADDR1 = OAP.VEND<1,3>
  VEND.CT.ST = OAP.VEND<1,4>
  VEND.ZIP = OAP.VEND<1,5>
END
*** TASK 19406 ***
SCV.REC(55)<ECD.SCRN.NO,1> = SQV.DIV.OWNER
*** **** ***** ***
SCV.REC(6)<ECD.SCRN.NO,1> = V.CODE[1,2]
SCV.REC(10)<ECD.SCRN.NO,1> = OAP.VEND<1,1>
SCV.REC(11)<ECD.SCRN.NO,1> = VEND.DESC
SCV.REC(13)<ECD.SCRN.NO,1> = VEND.ADDR1
SCV.REC(16)<ECD.SCRN.NO,1> = VEND.CT.ST[1,19]
SCV.REC(53)<ECD.SCRN.NO,1> = VEND.ZIP
IF OAP.VEND<1,1>[6,1] = "-" THEN GOSUB 1200
SCV.REC(17)<ECD.SCRN.NO,1> = OAP.TERMS<1,2>
SCV.REC(47)<ECD.SCRN.NO,1> = OAP.TERMS<1,1>[1,4]
SCV.REC(48)<ECD.SCRN.NO,1> = OAP.TERMS<1,1>[5,2]
SCV.REC(12)<ECD.SCRN.NO,1> = OAP.PO
SCV.REC(14)<ECD.SCRN.NO,1> = OAP.INV
SCV.REC(15)<ECD.SCRN.NO,1> = OAP.INV.DATE
SCV.REC(18)<ECD.SCRN.NO,1> = OCONV(OAP.DUE.DATE, "D2/")
SCV.REC(23)<ECD.SCRN.NO,1> = OAP.GRS.AMT
SCV.REC(46)<ECD.SCRN.NO,1> = OAP.MER.AMT
SCV.REC(24)<ECD.SCRN.NO,1> = OAP.DSC.AMT
SCV.REC(25)<ECD.SCRN.NO,1> = OAP.GRS.AMT - OAP.DSC.AMT
SCV.REC(50)<ECD.SCRN.NO,1> = OAP.DISB.ACCT CO.ACCT.MASK
SCV.REC(51)<ECD.SCRN.NO,1> = OAP.CHK.NO
SCV.REC(52)<ECD.SCRN.NO,1> = OAP.PAID.DATE
REM.AMT = (OAP.GRS.AMT - OAP.DSC.AMT) - (OAP.PAID.AMT + OAP.DSC.PAID)
IF REM.AMT < 0 AND OAP.GRS.AMT > 0 THEN REM.AMT = 0
IF REM.AMT > 0 AND OAP.GRS.AMT < 0 THEN REM.AMT = 0
SCV.REC(54)<ECD.SCRN.NO,1> = REM.AMT
RETURN
**** PRINT MAIN VEND IF SUB VEND WAS INPUT
1200 *
MATREAD SVEND.REC FROM VEND , CONO:OAP.VEND<1,1>[1,5] ELSE
  MAT SVEND.REC = "??????????????"
END
SCV.REC(41)<ECD.SCRN.NO> = OAP.VEND<1,1>[1,5]
SCV.REC(42)<ECD.SCRN.NO> = SVEND.DESC
SCV.REC(43)<ECD.SCRN.NO> = SVEND.ADDR1
SCV.REC(44)<ECD.SCRN.NO> = SVEND.CT.ST[1,19]:" ":SVEND.ZIP
RETURN
***** ENTER OPTIONS
60000 *
MORE = 1
LOOP
  ECD.NUM = 40
  IF OAP.CHK.NO='REVERS' OR OAP.CHK.NO='DELETE' THEN
    CHK.NO = ''
  END ELSE
    CHK.NO = OAP.CHK.NO
  END
*CSF 24463 v
* IF OAP.DUE.DATE[1,4] # "PAID" AND OAP.DUE.DATE # "ADVANCE" AND CHK.NO = "" THEN
  IF OAP.DUE.DATE # "ADVANCE" AND CHK.NO = "" THEN
*CSF 24463 ^
    ECD.VALDAT = "D,E"
    ECD.PMSG = "Enter (D)elete or (E)xit "
  END ELSE
    ECD.NUM = 56
    ECD.VALDAT = "E"
    ECD.PMSG = "VOUCHER HAS BEEN PAID, USE CHECK REVERSAL TO DELETE OR MODIFY. ENTER (E)XIT"
    IF OAP.CHK.NO = 'DELETE' THEN
      ECD.PMSG="VOUCHER HAS BEEN DELETED THROUGH CHECK REVERSAL. ENTER (E)XIT"
    END
  END
  ECD.ACTION=4;CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = '' OR OPTION = 'END' OR OPTION = 'E'
      MORE = 1
    CASE OPTION = 'D'
      PMSG = "YOU ARE ABOUT TO DELETE THIS VOUCHER, OK. (Y)ES OR (N)O"
      X=0 ; Y=23 ; TYP = 18;MINL=1;MAXL=1;O.R ='R'
      CALL EDIT.SUB
      P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      IF VALUE = 'Y' THEN
        IF CO.POS = "Y" AND CO.VOU.PO.FLG = "Y" THEN
          MATREAD OPAX.REC FROM OPOAPS.XREF ,CONO:OAP.JOB ELSE MAT OPAX.REC = ''
          LOCATE V.CODE IN VOU.NUM<1> SETTING XX ELSE XX = 0
          IF XX > 0 THEN
            VOU.NUM = DELETE(VOU.NUM,1,XX,0)
            MATWRITE OPAX.REC ON OPOAPS.XREF , CONO:OAP.JOB
          END
          MATREAD OPO.REC FROM OUTSIDE.PO , CONO:OAP.PO ELSE
            MAT OPO.REC = ""
          END
          IF OPO.VOUCHER.NO # "" THEN
            OPO.VOUCHER.NO = ""
            OPO.VOUCHER.DATE = ""
            MATWRITE OPO.REC ON OUTSIDE.PO , CONO:OAP.PO
          END
        END
        DELETE INV.XREF , CONO:STR('0',10-LEN(OAP.INV)):OAP.INV:OAP.VEND<1,1>
* CSF 22741 3/31/95 (LMR)
        IF OAP.CHK.NO # 'DELETE' THEN
          FOR X = 1 TO OAP.CTR
            MATREAD VCH.REC FROM VOUCHERS , CONO:V.CODE:X ELSE
              MAT VCH.REC = ""
              VCH.VEND = OAP.VEND
              VCH.INV = OAP.INV
              VCH.INV.DATE = OAP.INV.DATE
              VCH.DUE.DATE = OAP.DUE.DATE
              VCH.GRS.AMT = OAP.GRS.AMT
              VCH.MER.AMT = OAP.MER.AMT
              VCH.DSC.AMT = OAP.DSC.AMT
              VCH.PO = OAP.PO
              VCH.CTR = OAP.CTR
              VCH.MON = VOUCH.MON
              VCH.ACCT = OAP.ACCT<1,X>
              VCH.DIST.AMT = OAP.DIST.AMT<1,X>
              VCH.DIV = OAP.DIV<1,X>
              VCH.DEPT = OAP.DEPT<1,X>
              VCH.CCTR = OAP.CCTR<1,X>
              VCH.DESC = ""
              VCH.JOB = OAP.JOB<1,X>
              VCH.ASSET = OAP.ASSET.ID<1,X>
            END
            VCH.GRS.AMT = VCH.GRS.AMT * (-1)
            VCH.DSC.AMT = VCH.DSC.AMT * (-1)
            VCH.MER.AMT = VCH.MER.AMT * (-1)
            VCH.DIST.AMT = VCH.DIST.AMT * (-1)
            VCH.DESC = "DELETED VOUCHER"
            VCH.GLA.DATE = ""
            MATWRITE VCH.REC ON VOUCHERS , CONO:V.CODE:X:"D"
            TAG = ''
            WRITE TAG ON VOUCHERS.TAG , CONO:V.CODE:X:"D"
60001     NEXT X
*
*   TASK 17919
          AP.CNT = COUNT(OAP.AP.ACCT,VM) + (OAP.AP.ACCT # "")
          FOR X2 = 1 TO AP.CNT
            SUPP = X2 + OAP.CTR
            MATREAD VCH.REC FROM VOUCHERS , CONO:V.CODE:SUPP ELSE
              MAT VCH.REC = ""
              VCH.VEND = OAP.VEND
              VCH.INV = OAP.INV
              VCH.INV.DATE = OAP.INV.DATE
              VCH.DUE.DATE = OAP.DUE.DATE
              VCH.GRS.AMT = OAP.GRS.AMT
              VCH.MER.AMT = OAP.MER.AMT
              VCH.DSC.AMT = OAP.DSC.AMT
              VCH.PO = OAP.PO
              VCH.CTR = AP.CNT
              VCH.MON = VOUCH.MON
              VCH.ACCT = OAP.AP.ACCT<1,X2>
              VCH.DIST.AMT = OAP.AP.AMT<1,X2>
*T19149 v
*            VCH.DIV = ""
*            VCH.DEPT = ""
*            VCH.CCTR = ""
              VCH.DIV = OAP.AP.DIV<1,X2>
              VCH.DEPT = OAP.AP.DEPT<1,X2>
              VCH.CCTR = OAP.AP.CCTR<1,X2>
*T19149 ^            
              VCH.DESC = ""
              VCH.JOB = ""
              VCH.ASSET = ""
            END
            VCH.GRS.AMT = VCH.GRS.AMT * (-1)
            VCH.DSC.AMT = VCH.DSC.AMT * (-1)
            VCH.MER.AMT = VCH.MER.AMT * (-1)
            VCH.DIST.AMT = VCH.DIST.AMT * (-1)
            VCH.DESC = "DELETED VOUCHER"
            VCH.GLA.DATE = ""
            MATWRITE VCH.REC ON VOUCHERS , CONO:V.CODE:SUPP:"D"
            TAG = ''
            WRITE TAG ON VOUCHERS.TAG , CONO:V.CODE:SUPP:"D"
          NEXT X2
        END
*
        IF COUNT(OAP.VEND,VM) + (OAP.VEND # "") = 1 THEN
          IF LEN(OAP.VEND<1,1>) = 8 THEN
*             SVEND.OP.BAL = SVEND.OP.BAL - (OAP.GRS.AMT-OAP.DSC.AMT)
            SVEND.OP.BAL = SVEND.OP.BAL - OAP.GRS.AMT
            MATWRITE SVEND.REC ON VEND , CONO:OAP.VEND[1,5]
          END ELSE
*             VEND.OP.BAL = VEND.OP.BAL - (OAP.GRS.AMT-OAP.DSC.AMT)
            VEND.OP.BAL = VEND.OP.BAL - OAP.GRS.AMT
            MATWRITE VEND.REC ON VEND , CONO:OAP.VEND
          END
        END ELSE
          FOR CV = 1 TO 6
            VEND.REC(CV) = ""
          NEXT CV
*            VEND.OP.BAL = VEND.OP.BAL - (OAP.GRS.AMT-OAP.DSC.AMT)
          VEND.OP.BAL = VEND.OP.BAL - OAP.GRS.AMT
          MATWRITE VEND.REC ON VEND , CONO:OAP.VEND<1,1>
        END
        IF CO.FAS = "Y" THEN
          FOR XX = 1 TO OAP.CTR
            IF OAP.ASSET.ID<1,XX> # "" THEN
              READV REPAIRNO FROM CONTROL , CONO:"REPAIR-NO",1 ELSE REPAIRNO = 0
              REPAIRNO = REPAIRNO + 1
              WRITEV REPAIRNO ON CONTROL , CONO:"REPAIR-NO",1
              RPR.CHARGE = OAP.DIST.AMT<1,XX> * (-1)
              RPR.DATE = DATE()
              RPR.COMMENT = "VOUCHER - ":V.CODE:" (DELETED)"
              RPR.JOB = OAP.JOB<1,XX>
              RPR.JOB.AMT = OAP.DIST.AMT<1,XX> * (-1)
              RPR.CATEGORY = OAP.JOB<1,XX>[7,3]
              RPR.SOURCE = "AP"
              MATWRITE RPR.REC ON REPAIRS , CONO:OAP.ASSET.ID<1,XX>:STR('0',3-LEN(REPAIRNO)):REPAIRNO
              MATREAD EQP.REC FROM EQUIP.FILE , CONO:OAP.ASSET.ID<1,XX> ELSE GOTO 60002
              EQP.REPAIR = EQP.REPAIR - OAP.DIST.AMT<1,XX>
              EQP.YTD.REPAIR = EQP.YTD.REPAIR - OAP.DIST.AMT<1,XX>
              EQP.CUM.REPAIR = EQP.CUM.REPAIR - OAP.DIST.AMT<1,XX>
              MATWRITE EQP.REC ON EQUIP.FILE, CONO:OAP.ASSET.ID<1,XX>
60002       END
          NEXT XX
        END
        DELETE OAP , CONO:V.CODE
* CSF 22741
        IF SQV.FLG = 0 THEN
*       DELETE SQV , CONO:V.CODE
          SQV.CHK.NO = 'DELETE'
          MATWRITE SQV.REC ON SQV, CONO:V.CODE
        END
      END
      VVOS.KEY = CONO:OAP.VEND<1,1>:"!":V.CODE
      MATREADU VVOS.REC FROM VEND.VOUCH.STATS, VVOS.KEY ELSE MAT VVOS.REC = ""
      IF VVOS.PO.NO # "" THEN
        PO.CNT = COUNT(VVOS.PO.NO, VM) + (VVOS.PO.NO # "")
        FOR UA = PO.CNT TO 1 STEP -1
* Task 17919
* Update Accrued Liability History
*T26249 v
          ACC.WORK.DIV = GEN.DIV
          ACC.WORK.DEPT = GEN.DEPT
          ACC.WORK.CCTR = GEN.CCTR
*T26249 ^
          BEGIN CASE
            CASE VVOS.PO.TYPE<1,UA> = "R"
              MATREAD PO.REC FROM PO, CONO:VVOS.PO.NO<1,UA> ELSE MAT PO.REC=""
              MATREAD INV.REC FROM INVENTORY, CONO:VVOS.PROD<1,UA> ELSE MAT INV.REC=""
              MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE MAT CATG.REC=""
*T26249 v
              MATREAD WHSE.REC FROM WAREHOUSE, CONO:VVOS.WHSE<1,UA> ELSE MAT WHSE.REC = ""
              ACC.WORK.DIV = WHS.DIV
              ACC.WORK.DEPT = GEN.DEPT
              ACC.WORK.CCTR = GEN.CCTR
*T26249 ^
              ACCRUE.FLAG = PO.ACCRUE
              ACCRUE.REF = "R*":VVOS.PO.NO<1,UA>:"*":VVOS.PROD<1,UA>
              ACCRUE.SRC = "IV"
              ACCRUE.ACCT = CATG.ACCRU.LIAB
            CASE VVOS.PO.TYPE<1,UA> = "M"
              MATREAD MPO.REC FROM MISC.PO, CONO:VVOS.PO.NO<1,UA> ELSE MAT MPO.REC=""
              LOCATE VVOS.SEQ.NO<1,UA> IN MPO.SEQ.NO<1>,1 SETTING POFND ELSE POFND = 0
              MATREAD CAMISC.REC FROM CATEGORY.MISC, CONO:MPO.MISC.CAT<1,POFND> ELSE MAT CAMISC.REC=""
*T26249 v
              ACC.WORK.DIV = MPO.MISC.DIV<1,POFND>
              ACC.WORK.DEPT = MPO.MISC.DEPT<1,POFND>
              ACC.WORK.CCTR = MPO.MISC.CCTR<1,POFND>
*T26249 ^
              ACCRUE.FLAG = MPO.ACCRUE
              ACCRUE.REF = "M*":VVOS.PO.NO<1,UA>:"*":VVOS.PROD<1,UA>
              ACCRUE.SRC = "MV"
              ACCRUE.ACCT = CAMISC.CRED.EXP
            CASE 1
              MATREAD OPO.REC FROM OUTSIDE.PO, CONO:VVOS.PO.NO<1,UA> ELSE MAT OPO.REC=""
              POPTR = 1
              LOOP
                LOCATE VVOS.PROD<1,UA> IN OPO.JOB.NO<1>,POPTR SETTING POFND THEN
*T25755 v
*                             IF VVOS.WHSE<1,UA> = OPO.PROD.LINE<1,POFND> THEN POPTR = 0
                  IF VVOS.PO.SEQ<1,UA> = OPO.PROD.SEQ<1,POFND> THEN POPTR = 0
*T25755 ^
                END ELSE
                  POPTR = 0; POFND = 0
                END
              WHILE POPTR DO
                POPTR = POFND + 1
              REPEAT
              MATREAD CAOS.REC FROM CATEGORY.OSP, CONO:OPO.PROD.LINE<1,POFND> ELSE MAT CAOS.REC=""
*T26249 v
              ACC.WORK.DIV = OPO.DVDPCC<1,1,POFND>
              ACC.WORK.DEPT = OPO.DVDPCC<1,2,POFND>
              ACC.WORK.CCTR = OPO.DVDPCC<1,3,POFND>
*T26249 ^
              ACCRUE.FLAG = OPO.ACCRUE
              ACCRUE.REF = "O*":VVOS.PO.NO<1,UA>:"*":VVOS.PROD<1,UA>
              ACCRUE.SRC = "OV"
              ACCRUE.ACCT = CAOS.ACCRU.LIAB
              ACCRUE.OFFACCT = CAOS.WIP ;*T23880
          END CASE
          IF ACCRUE.FLAG = "Y" THEN
            READU ALH.CT FROM CONTROL,CONO:"ALHCOUNTER" ELSE ALH.CT=0
            ALH.CT = ALH.CT + 1
            IF ALH.CT > 999999 THEN ALH.CT = 1
            WRITE ALH.CT ON CONTROL,CONO:"ALHCOUNTER"
            MAT ALH.REC = ""
            ALH.ID = CONO:STR("0",6-LEN(ALH.CT)):ALH.CT
            ALH.DATE = DATE()
            ALH.REF = ACCRUE.REF
            ALH.SRC = ACCRUE.SRC
            ALH.MON = VOUCH.MON
            ALH.AMT = 0 - VVOS.PROD.COST<1,UA>
            ALH.ACCT = ACCRUE.ACCT
*T26249 v
            ALH.DV.DP.CC = ACC.WORK.DIV"R%2":ACC.WORK.DEPT"R%2":ACC.WORK.CCTR"R%3"
*T26249 ^
            ALH.OFF.ACCT = ACCRUE.OFFACCT ;*T23880   
            MATWRITE ALH.REC ON ACCRUED.LIAB.HIST,ALH.ID
          END
          VSTAT.KEY = CONO:OAP.VEND<1,1>:"!":VVOS.PO.TYPE<1,UA>
          MATREADU VSTAT.REC FROM VEND.STATS, VSTAT.KEY ELSE MAT VSTAT.REC = ""
          LOCATE VVOS.PO.NO<1,UA> IN VSTAT.PO<1>,1 SETTING VSFND ELSE VSFND = 0
          IF VSFND # 0 THEN
            VSTAT.PO.VOUCH<1,VSFND> = VSTAT.PO.VOUCH<1,VSFND> - VVOS.PROD.COST<1,UA>
            LOCATE V.CODE IN VSTAT.VOUCH.NOS<1,VSFND>,1 SETTING VSVFND ELSE VSVFND = 0
            IF VSVFND # 0 THEN
              VSTAT.VOUCH.NOS = DELETE(VSTAT.VOUCH.NOS,1,VSFND,VSVFND)
            END
            VPS.KEY = VSTAT.KEY:"!":VVOS.PO.NO<1,UA>
            MATREADU VPS.REC FROM VEND.PO.STATS,VPS.KEY ELSE MAT VPS.REC = ""
            IF VVOS.PO.TYPE<1,UA> = "M" THEN
              LOCATE VVOS.SEQ.NO<1,UA> IN VPS.SEQ.NO<1>,1 SETTING VPFND ELSE VPFND = 0
            END ELSE
              PTR = 1
              LOOP
                LOCATE VVOS.PROD<1,UA> IN VPS.PROD<1>,PTR SETTING VPFND ELSE VPFND = 0
                BEGIN CASE
                  CASE VPFND = 0
                    PTR = 0
*T25755 v
*T28779           CASE VVOS.U.M<1,UA> = VPS.U.M<1,VPFND> AND VVOS.WHSE<1,UA>:"@":VVOS.PO.SEQ<1,UA> = VPS.WHSE<1,VPFND> AND VVOS.PO.TYPE<1,UA> = "O"
                  CASE VVOS.U.M<1,UA> = VPS.U.M<1,VPFND> AND VVOS.WHSE<1,UA>:"@":VVOS.PO.SEQ<1,UA> = VPS.WHSE<1,VPFND> AND (VVOS.PO.TYPE<1,UA> = "O" OR VVOS.PO.TYPE<1,UA> = 'R')
                    PTR = 0
*T25755 ^
                  CASE VVOS.U.M<1,UA> = VPS.U.M<1,VPFND> AND VVOS.WHSE<1,UA> = VPS.WHSE<1,VPFND>
                    PTR = 0
                  CASE 1
                    PTR = VPFND + 1
                END CASE
              WHILE PTR DO
              REPEAT
            END
            IF VPFND # 0 THEN
              VPS.VOU.QTY<1,VPFND> = VPS.VOU.QTY<1,VPFND> - VVOS.VOU.QTY<1,UA>
              VPS.VOU.AMT<1,VPFND> = VPS.VOU.AMT<1,VPFND> - VVOS.PROD.COST<1,UA>
              LOCATE V.CODE IN VPS.VOU.NO<1,VPFND>,1 SETTING VPVFND ELSE VPVFND = 0
              IF VPVFND # 0 THEN
                VPS.VOU.NO = DELETE(VPS.VOU.NO,1,VPFND,VPVFND)
              END
              IF VVOS.PO.TYPE<1,UA> = "M" THEN
                VPDS.KEY = VPS.KEY:"!":VVOS.SEQ.NO<1,UA>:"!":VVOS.WHSE<1,UA>
              END ELSE
*T25755 v
*                          VPDS.KEY = VPS.KEY:"!":VVOS.PROD<1,UA>:"!":VVOS.WHSE<1,UA>
*T28779         IF VVOS.PO.TYPE<1,UA> = 'O' THEN
                IF VVOS.PO.TYPE<1,UA> = 'O' OR VVOS.PO.TYPE<1,UA> = 'R' THEN
                  VPDS.KEY = VPS.KEY:'!':VVOS.PROD<1,UA>:'!':VVOS.WHSE<1,UA>:'@':VVOS.PO.SEQ<1,UA>
                END ELSE
                  VPDS.KEY = VPS.KEY:"!":VVOS.PROD<1,UA>:"!":VVOS.WHSE<1,UA>
                END
*T25755 ^
              END
              MATREADU VPDS.REC FROM VEND.PROD.STATS, VPDS.KEY ELSE MAT VPDS.REC = ""
*T25632 v
              FOR VVV = DCOUNT(VPDS.VOU.NO<1>,VM) TO 1 STEP -1
*                          LOCATE V.CODE IN VPDS.VOU.NO<1>,1 SETTING VPSFND ELSE VPSFND = 0
                LOCATE V.CODE IN VPDS.VOU.NO<1,VVV>,1 SETTING VPSFND ELSE VPSFND = 0
                IF VPSFND # 0 THEN
                  VPDS.VOU.NO = DELETE(VPDS.VOU.NO,1,VVV,VPSFND)
                  VPDS.VOU.DATE = DELETE(VPDS.VOU.DATE,1,VVV,VPSFND)
                  VPDS.INV.NO = DELETE(VPDS.INV.NO,1,VVV,VPSFND)
                  VPDS.INV.DATE = DELETE(VPDS.INV.DATE,1,VVV,VPSFND)
                  VPDS.QTY = DELETE(VPDS.QTY,1,VVV,VPSFND)
                  VPDS.UN.COST = DELETE(VPDS.UN.COST,1,VVV,VPSFND)
                  VPDS.PERIOD = DELETE(VPDS.PERIOD,1,VVV,VPSFND)
                END
              NEXT VVV
*T25632 ^
              IF VSTAT.PO.ORDERD<1,VSFND> + 0 = 0 AND VSTAT.PO.RECV<1,VSFND> + 0 = 0 AND VSTAT.PO.VOUCH<1,VSFND> + 0 = 0 AND VSTAT.VOUCH.NOS<1,VSFND> = "" THEN
                VSTAT.PO = DELETE(VSTAT.PO,1,VSFND,0)
                VSTAT.PO.ORDERD = DELETE(VSTAT.PO.ORDERD,1,VSFND,0)
                VSTAT.PO.RECV = DELETE(VSTAT.PO.RECV,1,VSFND,0)
                VSTAT.PO.VOUCH = DELETE(VSTAT.PO.VOUCH,1,VSFND,0)
                VSTAT.VOUCH.NOS = DELETE(VSTAT.VOUCH.NOS,1,VSFND,0)
              END
              IF VSTAT.PO = "" THEN
                DELETE VEND.STATS , VSTAT.KEY
              END ELSE
                MATWRITE VSTAT.REC ON VEND.STATS, VSTAT.KEY
              END
              IF VPS.ORD.QTY<1,VPFND>+0 = 0 AND VPS.REC.QTY<1,VPFND>+0 = 0 AND VPS.VOU.QTY<1,VPFND> + 0 = 0 AND VPS.VOU.NO<1,VPFND> = "" THEN
                VPS.PROD = DELETE(VPS.PROD,1,VPFND,0)
                VPS.PROD.DESC = DELETE(VPS.PROD.DESC,1,VPFND,0)
                VPS.U.M = DELETE(VPS.U.M,1,VPFND,0)
                VPS.WHSE = DELETE(VPS.WHSE,1,VPFND,0)
                VPS.ORD.QTY = DELETE(VPS.ORD.QTY,1,VPFND,0)
                VPS.ORD.AMT = DELETE(VPS.ORD.AMT,1,VPFND,0)
                VPS.REC.QTY = DELETE(VPS.REC.QTY,1,VPFND,0)
                VPS.REC.AMT = DELETE(VPS.REC.AMT,1,VPFND,0)
                VPS.VOU.QTY = DELETE(VPS.REC.QTY,1,VPFND,0)
                VPS.VOU.AMT = DELETE(VPS.VOU.AMT,1,VPFND,0)
                VPS.SEQ.NO = DELETE(VPS.SEQ.NO,1,VPFND,0)
                VPS.VOU.NO = DELETE(VPS.VOU.NO,1,VPFND,0)
              END
              IF VPS.PROD = "" AND VPS.SEQ.NO = "" AND VPS.VOU.NO = "" THEN
                DELETE VEND.PO.STATS, VPS.KEY
              END ELSE
                MATWRITE VPS.REC ON VEND.PO.STATS, VPS.KEY
              END
              IF VPDS.ORD.DATE = "" AND VPDS.REC.DATE = "" AND VPDS.VOU.DATE = "" THEN
                DELETE VEND.PROD.STATS, VPDS.KEY
              END ELSE
                MATWRITE VPDS.REC ON VEND.PROD.STATS, VPDS.KEY
              END
              VVOS.PO.NO = DELETE(VVOS.PO.NO,1,UA,0)
              VVOS.PO.TYPE = DELETE(VVOS.PO.TYPE,1,UA,0)
              VVOS.PROD = DELETE(VVOS.PROD,1,UA,0)
              VVOS.PROD.DESC = DELETE(VVOS.PROD.DESC,1,UA,0)
              VVOS.U.M = DELETE(VVOS.U.M,1,UA,0)
              VVOS.WHSE = DELETE(VVOS.WHSE,1,UA,0)
              VVOS.VOU.QTY = DELETE(VVOS.VOU.QTY,1,UA,0)
              VVOS.VOU.UN.COST = DELETE(VVOS.VOU.UN.COST,1,UA,0)
              VVOS.PROD.COST = DELETE(VVOS.PROD.COST,1,UA,0)
              VVOS.SEQ.NO = DELETE(VVOS.SEQ.NO,1,UA,0)
              VVOS.PO.SEQ = DELETE(VVOS.PO.SEQ,1,UA,0) ; * T25755
            END
          END
        NEXT UA
      END
      IF VVOS.PO.NO = "" THEN
        DELETE VEND.VOUCH.STATS, VVOS.KEY
      END ELSE
        MATWRITE VVOS.REC ON VEND.VOUCH.STATS, VVOS.KEY
      END
      MORE = 1
  END CASE
WHILE MORE = 0 DO
REPEAT
RETURN
**** CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
