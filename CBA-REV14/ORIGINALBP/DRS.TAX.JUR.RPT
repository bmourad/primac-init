*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - DRSBP
* PROGRAM     - DRS.TAX.JUR.RPT
* AUTHOR      - L. ROSS, C.B.A.
* DATE        - 10/02/91
* DESCRIPTION
* This program prints the tax jurisdiction summary report for a specific
* customer/template.
*T23831 gat 04/06/1999 * FIX RPT BREAK PROBLEM
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*
***** INSERT FILES EQUATES
*
*COPY>JCS.CPYLIB>SHIPTO.MASTER
*COPY>JCS.CPYLIB>CUST.TEMPLATE.LIST
*COPY>JCS.CPYLIB>TEMPLATE.DETAIL
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>TAX
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** SETUP ERRMSG ROUTINE
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
      OPEN '','SHIPTO.MASTER' TO SHIPTO.MASTER ELSE
         ERRMSG = 'SHIPTO.MASTER FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CUST.TEMPLATE.LIST' TO CUST.TEMPLATE.LIST ELSE
         ERRMSG = 'CUST.TEMPLATE.LIST FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','TEMPLATE.DETAIL' TO TEMPLATE.DETAIL ELSE
         ERRMSG = 'TEMPLATE.DETAIL FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CUSTOMER' TO CUSTOMER ELSE
         ERRMSG = 'CUSTOMER FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','TAX' TO TAX ELSE
         ERRMSG = 'TAX FILE IS MISSING'
         GOTO 93000
      END
*
**** READ PROC VALUES
*
      PROCREAD ID ELSE
         ERRMSG = 'CANNOT PERFORM PROCREAD'
         GOTO 93000
      END
      CONO = ID<1>
*T26493 v
*     CONO.NAME = ID<2>
*     REPORT.NAME = 'DRS TAX JURISDICTION SUMMARY REPORT'
*     REPORT.NUMBER = "XXXX"
      CONO.NAME = ""
      REPORT.NAME = ""
      REPORT.NUMBER = ID<2>
*T26943 ^
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
      CUST.NO = ID<3>
      TEMP.NO = ID<4>
      FRST = 1
*
**** INITIALIZATION
*
      LINE.COUNT = 99
      PAGE.NO = 0
      DIST.QTY = 0
      TJUR.QTY = 0
      TOTL.QTY = 0
      HD3 = 'FOR CUSTOMER: ':CUST.NO 'L#6':SPACE(6)
      HD4 = '    TEMPLATE: ':TEMP.NO 'R#10':'  '
      HH.LINE1 = 'TAX  JUR':SPACE(19):'DIST   TEMPLATE'
      HH.LINE2 = 'JUR  DESCRIPTION':SPACE(11):'CODE   QUANTITY'
      HH.LINE3 = '---  ':STR('-',20):'  ----  ----------'
*
***** READ AND PROCESS FIRST RECORD
*
100 *
      READNEXT KEY,SEQ ELSE GOTO 99999
      MATREAD CUST.REC FROM CUSTOMER, CONO:CUST.NO ELSE
         CUST.NAME = '??????????'
      END
      MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, CONO:CUST.NO:"!":TEMP.NO:"!001" ELSE
         ERRMSG = 'The CUST.TEMPLATE.LIST record 1 is missing'
         GOSUB 91000
         GOTO 99999
      END
      SHIPTO = CTL.SHIPS<1,1>
      IF SHIPTO = '' THEN
         ERRMSG = 'The first record of CUST.TEMPLATE.LIST has no shipto# - run resequencing'
         GOSUB 91000
         GOTO 99999
      END
      IF FRST THEN
         HD3 = HD3:CUST.NAME 'L#30'
         HD4 = HD4:CTL.DESC 'L#30'
         FRST = 0
      END
      MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, KEY ELSE
         ERRMSG = 'The CUST.TEMPLATE.LIST record ':KEY:' is missing'
         GOSUB 91000
         GOTO 99999
      END
      SHIPTO = CTL.SHIPS<1,SEQ>
      MATREAD SHM.REC FROM SHIPTO.MASTER, CONO:SHIPTO ELSE
         ERRMSG = 'SHIPTO.MASTER record for ':SHIPTO:' is missing'
         GOSUB 91000
         GOTO 99999
      END
      MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
         ERRMSG = 'TEMPLATE.DETAIL record for shipto ':SHIPTO:' template ':TEMP.NO:' is missing'
         GOSUB 91000
         GOTO 99999
      END
      IF TDT.STORE.QTY > 0 AND TDT.QTY <= 0 THEN SHM.DT.TAXJUR = SHM.SP.TAXJUR
      IF SHM.DT.TAXJUR = '' THEN GOTO 100
      PRV.JUR = SHM.DT.TAXJUR
      MATREAD TAX.REC FROM TAX, CONO:PRV.JUR ELSE MAT TAX.REC = ''
      PRV.DIST = TDT.DIST.CODE
      PRINTER ON
      GOSUB 9000
      GOSUB 1000
*
***** READ AND PROCESS ALL RECORDS
*
      DATA = 1
      LOOP
         READNEXT KEY,SEQ ELSE DATA =0
         IF DATA THEN
            MATREAD CTL.REC FROM CUST.TEMPLATE.LIST, KEY THEN
               SHIPTO = CTL.SHIPS<1,SEQ>
               MATREAD SHM.REC FROM SHIPTO.MASTER, CONO:SHIPTO ELSE
                  ERRMSG = 'SHIPTO.MASTER record for ':SHIPTO:' is missing'
                  GOSUB 91000
                  GOTO 99999
               END
               MATREAD TDT.REC FROM TEMPLATE.DETAIL, CONO:SHIPTO:"!":TEMP.NO ELSE
                  ERRMSG = 'TEMPLATE.DETAIL record for shipto ':SHIPTO:' template ':TEMP.NO:' is missing'
                  GOSUB 91000
                  GOTO 99999
               END
            END ELSE DATA = 0
         END
         IF DATA = 0 THEN TDT.DIST.CODE = ''; SHM.DT.TAXJUR = '' SHM.SP.TAXJUR = ''
         IF SHM.DT.TAXJUR # '' THEN
            IF SHM.DT.TAXJUR # PRV.JUR THEN GOSUB 2000; GOSUB 3000 ;* T23831
            IF TDT.DIST.CODE # PRV.DIST THEN GOSUB 2000
            IF TDT.STORE.QTY > 0 AND TDT.QTY <= 0 THEN SHM.DT.TAXJUR = SHM.SP.TAXJUR
*            IF SHM.DT.TAXJUR # PRV.JUR THEN GOSUB 3000
         END
      WHILE DATA = 1 DO
          IF SHM.DT.TAXJUR # '' THEN GOSUB 1000
      REPEAT
      GOSUB 4000
      GOTO 99999
*
**** DETAIL ROUTINE
*
1000 *
*
      IF TDT.STORE.QTY > 0 AND TDT.QTY <= 0 THEN
         DIST.QTY = DIST.QTY + TDT.STORE.QTY
      END ELSE DIST.QTY = DIST.QTY + TDT.QTY
      RETURN
*
**** DIST BREAK
*
2000 *
      PLINE = PRV.JUR 'L#3':'  ':TAX.JUR 'L#20':'  ':PRV.DIST 'L#4':'  ':OCONV(DIST.QTY, 'MD0,') 'R#10'
      IF LINE.COUNT > 52 THEN GOSUB 9000
      PRINT PLINE
      LINE.COUNT = LINE.COUNT + 1
      TJUR.QTY = TJUR.QTY + DIST.QTY
      DIST.QTY = 0
      PRV.DIST = TDT.DIST.CODE
      RETURN
*
**** JUR BREAK
*
3000 *
      IF LINE.COUNT > 50 THEN GOSUB 9000
      PRINT
      PLINE = SPACE(5):'-TOTAL -' 'L#20':SPACE(8):OCONV(TJUR.QTY, 'MD0,') 'R#10'
      PRINT PLINE
      PRINT
      LINE.COUNT = LINE.COUNT + 3
      TOTL.QTY = TOTL.QTY + TJUR.QTY
      TJUR.QTY = 0
      PRV.JUR = SHM.DT.TAXJUR
      MATREAD TAX.REC FROM TAX, CONO:PRV.JUR ELSE MAT TAX.REC = ''
      RETURN
*
**** GRAND TOTAL
*
4000 *
*      GOSUB 3000
      IF LINE.COUNT > 50 THEN GOSUB 9000
      PRINT
      PLINE = SPACE(5):'- GRAND TOTAL -' 'L#20':SPACE(8):OCONV(TOTL.QTY, 'MD0,') 'R#10'
      PRINT PLINE
      RETURN
*
***** PRINT HEADINGS
*
9000 *
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PRINT HD3
      PRINT HD4
      PRINT
      PRINT HH.LINE1
      PRINT HH.LINE2
      PRINT HH.LINE3
      PRINT
      LINE.COUNT = 0
      RETURN
*
***** CALL SYSCOM(MAT SYSCOM.REC)
*
91000 *
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 *
      ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF JOB
*
99999 *
      PRINTER OFF
      END
