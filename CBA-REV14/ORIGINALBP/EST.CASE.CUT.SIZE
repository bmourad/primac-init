   SUBROUTINE EST.CASE.CUT.SIZE (CONO, ACTION)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.CASE.CUT.SIZE
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/07/88
*
* DESCRIPTION
*
* This subroutine is used to calculate the dimensions of the endleaf, board
* and cloth required to construct the book cover.
*
*    ACTION - "ENLEAF"   Calculate endleaf only
*             "BOARD"    Calculate board only
*             "CLOTH"    Calculate cloth only
*             "ALL"      Calculate all of the above
*
*T26358 cmykleb 01/07/2002 * Change the board "Buiding In Allowance" from
*                            a hard coded amount to an entered amount.
*T26449 cmykleb 03/05/2002 * Change the side cloth "Buiding In Allowance"
*                            from a hard coded amount to an entered amount.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.BIND.SPOIL
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TOT.BULK = 0
   C2 = COUNT(EST.BIND.FOLD.BULK,VM) + (EST.BIND.FOLD.BULK # "")
   FOR C = 1 TO C2
      TOT.BULK = TOT.BULK + EST.BIND.FOLD.BULK<1,C>
   NEXT C
   TOT.BULK = TOT.BULK / 10000
*
   PAGE.WIDTH = EST.BIND.TP.WIDTH / 10000
   PAGE.LENGTH = EST.BIND.TP.LENGTH / 10000
   SHOW.SIZE = EST.CASE.SHOW.SIZE / 10000
*
*---- MAIN PROCESSING
*
   IF ACTION = "ENDLEAF" OR ACTION = "ALL" THEN
      EST.CASE.ENDLF.CUT.SIZE = ""
      EST.CASE.ENDLF.CUT.SIZE<1,1> = INT(((PAGE.WIDTH + 0.125) * 2) * 10000 + 0.5)
      EST.CASE.ENDLF.CUT.SIZE<1,2> = EST.BIND.US.LENGTH
   END
*
   IF ACTION = "BOARD" OR ACTION = "ALL" THEN
      EST.CASE.SIDE.BD.CUT.SIZE = ""
      EST.CASE.SPINE.BD.CUT.SIZE = ""
      BIA = OCONV(EST.CASE.BOARD.PRICE<1,4>,'MD4') ; * T26358
      BEGIN CASE
         CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "R"
            BEGIN CASE
               CASE EST.BIND.EDGE.TYPE = "S"
*T26358 v
*                 EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - 0.1875) * 10000 + 0.5)
                  EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - BIA) * 10000 + 0.5)
*T26358 ^
                  EST.CASE.SIDE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.2500) * 10000 + 0.5)
                  BOARD.WIDTH = EST.CASE.SIDE.BD.CUT.SIZE<1,1> / 10000
               CASE EST.BIND.EDGE.TYPE = "RF" OR EST.BIND.EDGE.TYPE = "RFF"
                  EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - 0.1250) * 10000 + 0.5)
                  EST.CASE.SIDE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.2500) * 10000 + 0.5)
                  BOARD.WIDTH = EST.CASE.SIDE.BD.CUT.SIZE<1,1> / 10000
            END CASE
         CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "F"
*T26358 v
*           EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - 0.0625) * 10000 + 0.5)
            EST.CASE.SIDE.BD.CUT.SIZE<1,1> = INT((PAGE.WIDTH - BIA) * 10000 + 0.5)
*T26358 ^
            EST.CASE.SIDE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.2500) * 10000 + 0.5)
            BOARD.WIDTH = EST.CASE.SIDE.BD.CUT.SIZE<1,1> / 10000
            EST.CASE.SPINE.BD.CUT.SIZE<1,1> = INT((TOT.BULK + 0.1900) * 10000 + 0.5)
            EST.CASE.SPINE.BD.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 0.1875) * 10000 + 0.5)
      END CASE
   END
*
   IF ACTION = "CLOTH" OR ACTION = "ALL" THEN
      EST.CASE.SIDE.CL.CUT.SIZE = ""
      EST.CASE.SPINE.CL.CUT.SIZE = ""
      BIA = OCONV(EST.CASE.SIDE.CL.PRICE<1,4>,'MD4')
      BEGIN CASE
         CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "R"
*T26449 v
*           BEGIN CASE
*              CASE EST.BIND.EDGE.TYPE = "S"
*                 BOARD.WIDTH = INT((PAGE.WIDTH - 0.1875) * 10000 + 0.5) / 10000
*              CASE EST.BIND.EDGE.TYPE = "RF" OR EST.BIND.EDGE.TYPE = "RFF"
*                 BOARD.WIDTH = INT((PAGE.WIDTH - 0.1250) * 10000 + 0.5) / 10000
*           END CASE
            BOARD.WIDTH = INT((PAGE.WIDTH - BIA) * 10000 + 0.5) / 10000
*T26449 ^
            BEGIN CASE
               CASE EST.CASE.PIECES = 1
                  EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((TOT.BULK * 1.5 + BOARD.WIDTH * 2 + 2.0000) * 10000 + 0.5)
                  EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
               CASE EST.CASE.PIECES = 3
                  EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((BOARD.WIDTH - SHOW.SIZE + 0.6250) * 10000 + 0.5)
                  EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                  EST.CASE.SPINE.CL.CUT.SIZE<1,1> = INT((TOT.BULK * 1.5 + SHOW.SIZE * 2 + 1.2500) * 10000 + 0.5)
                  EST.CASE.SPINE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
            END CASE
         CASE EST.BIND.COVER.STYLE = "C" AND EST.CASE.BACK.STYLE = "F"
*T26449 v
*           BOARD.WIDTH = INT((PAGE.WIDTH - 0.0625) * 10000 + 0.5) / 10000
            BOARD.WIDTH = INT((PAGE.WIDTH - BIA) * 10000 + 0.5) / 10000
*T26449 ^
            BEGIN CASE
               CASE EST.CASE.PIECES = 1
                  EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((TOT.BULK + BOARD.WIDTH * 2 + 2.1900) * 10000 + 0.5)
                  EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                  EST.CASE.SPINE.CL.CUT.SIZE<1,1> = ""
                  EST.CASE.SPINE.CL.CUT.SIZE<1,2> = ""
               CASE EST.CASE.PIECES = 3
                  EST.CASE.SIDE.CL.CUT.SIZE<1,1> = INT((BOARD.WIDTH - SHOW.SIZE + 0.6250) * 10000 + 0.5)
                  EST.CASE.SIDE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
                  EST.CASE.SPINE.CL.CUT.SIZE<1,1> = INT((TOT.BULK + SHOW.SIZE * 2 + 1.4400) * 10000 + 0.5)
                  EST.CASE.SPINE.CL.CUT.SIZE<1,2> = INT((PAGE.LENGTH + 1.5000) * 10000 + 0.5)
            END CASE
      END CASE
   END
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
