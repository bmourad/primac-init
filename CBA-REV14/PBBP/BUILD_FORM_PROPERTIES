   SUBROUTINE BUILD_FORM_PROPERTIES(RTN_CODE,FORM_NAME)
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [11.B]
* Revision Date - 09/11/98
* System        - PRIMAC
* Library       - JISBP/BUILD_FORM_PROPERTIES
* Author        - Ziad Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>JIS.CPYLIB>FORM_PROPERTIES
*COPY>JIS.CPYLIB>SYS_SCN_DEF
*COPY>JIS.CPYLIB>SYS_TKT_DEF
*COPY>JIS.CPYLIB>SYS_FIELDS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
   DIM FP_SAMPLE(FP.REC.SIZE)
*
   SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
*
   OPEN "","FORM_PROPERTIES" TO FORM_PROPERTIES ELSE
      ERRMSG = "Cannot locate the FORM_PROPERTIES file"
      GOTO 93000
   END
   OPEN "","SYS_TKT_DEF" TO SYS_TKT_DEF ELSE
      ERRMSG = "Cannot locate the SYS_TKT_DEF file"
      GOTO 93000
   END
   OPEN "","SYS_SCN_DEF" TO SYS_SCN_DEF ELSE
      ERRMSG = "Cannot locate the SYS_SCN_DEF file"
      GOTO 93000
   END
   OPEN "","SYS_FIELDS" TO SYS_FIELDS ELSE
      ERRMSG = "Cannot locate the SYS_FIELDS file"
      GOTO 93000
   END
   OPEN "","SYS_FLD_HMSG" TO SYS_FLD_HMSG ELSE
      ERRMSG = "Cannot locate the SYS_FLD_HMSG file"
      GOTO 93000
   END
*
*---- Get Passed Parameters
*
   RTN_CODE = 0
   MATREAD SSD.REC FROM SYS_SCN_DEF, FORM_NAME THEN
      OPEN "", SSD_SRC_FILE TO FORM_HDR_DEF THEN
         READ SCN_HDR_DEF FROM FORM_HDR_DEF, FORM_NAME:"_DEF" ELSE
            RTN_CODE = -2
         END
      END ELSE
         RTN_CODE = -1
      END
   END ELSE
      RTN_CODE = -2
   END
   IF RTN_CODE < 0 THEN GOTO 99999
*
*---- Define / purge previous data
*
   SELECT FORM_PROPERTIES
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      IF FIELD(ID,"_",1) = FORM_NAME THEN
         READU ITEM FROM FORM_PROPERTIES, ID THEN
            DELETE FORM_PROPERTIES, ID
         END ELSE
            RELEASE FORM_PROPERTIES, ID
         END
      END
   REPEAT
*
*---- Process collected data
*
   UDATE = DATE()
   UTIME = TIME()
   SEQNO = 10000
   STNO = 0
*
*---- Process header information (X,Y,Text)
*
   MAT FP.REC = ""
   FP_FORM = FORM_NAME
   FP_CLASS = "St"
   FP_HEIGHT = 65
   FP_ALIGN = "Left"
   FP_FONTCLASS = "Fixedsys"
   FP_FONTSTYLE = "Regular"
   FP_FONTSIZE = 8
   FP_DATE = UDATE
   FP_TIME = UTIME
   MAT FP_SAMPLE = MAT FP.REC
*
   HCNT = DCOUNT(SCN_HDR_DEF,AM)
   FOR HNO = 1 TO HCNT
      MAT FP.REC = MAT FP_SAMPLE
      FP_ROW = SCN_HDR_DEF<HNO,1> * 1
      FP_COL = SCN_HDR_DEF<HNO,2> * 1
      FP_TEXT = SCN_HDR_DEF<HNO,3>
      FP_TEXT = CHANGE(FP_TEXT,":"," ")
      FP_WIDTH = LEN(FP_TEXT) * 1
      BEGIN CASE
         CASE TRIM(FP_TEXT) = ""
            GOTO 503
*           CASE FP_ROW > 1 AND FP_ROW < 20
         CASE FP_ROW < 20
            BEGIN CASE
               CASE TRIM(CHANGE(FP_TEXT,"-","")) = ""
                  FP_CLASS = "Ln"
                  FP_BACKCOLOR = "DarkGrey"
                  FP_BORDER = "3D Raised"
                  FP_TEXT = ""
               CASE TRIM(CHANGE(FP_TEXT,"=","")) = ""
                  FP_CLASS = "Ln"
                  FP_BACKCOLOR = "DarkGrey"
                  FP_BORDER = "3D Raised"
                  FP_TEXT = ""
               CASE INDEX(FP_TEXT,"---",1)
                  FP_CLASS = "Ln"
                  FP_TEXT = CHANGE(FP_TEXT,"-"," ")
                  FP_BACKCOLOR = "DarkGrey"
                  FP_BORDER = "3D Raised"
                  IF NOT(NUM(TRIM(FP_TEXT)[1,1])) THEN
                     FP_ALIGN = "Center"
                  END
               CASE INDEX(FP_TEXT,"==",1)
                  FP_CLASS = "Ln"
                  FP_TEXT = CHANGE(FP_TEXT,"="," ")
                  FP_BACKCOLOR = "DarkGrey"
                  FP_BORDER = "3D Raised"
                  IF NOT(NUM(TRIM(FP_TEXT)[1,1])) THEN
                     FP_ALIGN = "Center"
                  END
               CASE 1
                  FP_TEXT = TRIM(FP_TEXT)
            END CASE
         CASE TRIM(CHANGE(FP_TEXT,"-","")) = ""
            GOTO 503
         CASE TRIM(CHANGE(FP_TEXT,"=","")) = ""
            GOTO 503
      END CASE
      STNO = STNO + 1
      FP_NAME = FP_CLASS : STNO
      SEQNO = SEQNO + 1
      MATWRITE FP.REC ON FORM_PROPERTIES, FORM_NAME:"_":SEQNO
503*
   NEXT HNO
*
*---- Process Record ID
*
*
   MAT FP.REC = ""
   FP_FORM = FORM_NAME
   FP_FONTCLASS = "MS Sans Serif"
   FP_FONTSTYLE = "Regular"
   FP_FONTSIZE = 8
   FP_DATE = UDATE
   FP_TIME = UTIME
   FP_HELP = ""
   FP_VALUES = ""
   MAT FP_SAMPLE = MAT FP.REC
   FP_NAME = "MainID"
   FP_CLASS = "Sle"
   FP_COL = SSD_ID_X * 1
   FP_ROW = SSD_ID_Y * 1
   FP_HEIGHT = 69
   FP_WIDTH = FIELD(SSD_ID_FMT,"#",2) * 1
   FP_ALIGN = FIELD(SSD_ID_FMT,"#",1)
   BEGIN CASE
      CASE FP_ALIGN = "L"
         FP_ALIGN = "Left"
      CASE FP_ALIGN = "R"
         FP_ALIGN = "Right"
      CASE 1
         FP_ALIGN = "Justify"
   END CASE
   FP_DATATYPE = "String"
   FP_BACKCOLOR = "ButtonFace"
   SEQNO = SEQNO + 1
   MATWRITE FP.REC ON FORM_PROPERTIES, FORM_NAME:"_":SEQNO
*
*---- Process Data fields
*
   VCNT = DCOUNT(SSD_FIELDS,VM)
   FOR VNO = 1 TO VCNT
      MAT FP.REC = MAT FP_SAMPLE
      MATREAD DFD.REC FROM SYS_FIELDS, SSD_FIELDS<1,VNO> ELSE MAT DFD.REC = ""
      READ HMSG FROM SYS_FLD_HMSG, SSD_FIELDS<1,VNO> ELSE HMSG = ""
      LCNT = DCOUNT(HMSG,AM)
      FOR L = 1 TO LCNT
         FP_HELP := HMSG<L>:'/n'
      NEXT L
      FP_NAME = SSD_FIELDS<1,VNO>
      FP_COL = SSD_FLD_X<1,VNO> * 1
      FP_ROW = SSD_FLD_Y<1,VNO> * 1
      FP_WIDTH = FIELD(SSD_FLD_FMT<1,VNO>,"#",2)
      FP_HEIGHT = SSD_FLD_LINES<1,VNO>
      LCNT = DCOUNT(DFD_VALDAT,VM)
      FOR L = 1 TO LCNT
         FP_VALUES := DFD_VALDAT<1,L>:'/t':DFD_VALDAT<1,L>:'/'
      NEXT L
      IF DFD_O_R = "R" THEN FP_REQUIRED = "yes" ELSE FP_REQUIRED = "no"
      BEGIN CASE
         CASE DFD_TYPE = "C"
            DEC = SSD_FLD_CNV<1,VNO>[3,99]
            IF NUM(DEC) THEN
               FP_DATATYPE = "Dec":DEC
            END ELSE
               FP_DATATYPE = "Decimal"
            END
            GOSUB 1500
         CASE DFD_TYPE = "N"
            IF FP_WIDTH < 4 THEN
               FP_DATATYPE = "Integer"
            END ELSE
               FP_DATATYPE = "Long"
            END
            GOSUB 1500
         CASE DFD_TYPE = "A"
            FP_DATATYPE = "String"
            GOSUB 1500
         CASE DFD_TYPE = "D"
            FP_DATATYPE = "Date"
            GOSUB 1500
         CASE DFD_TYPE = "L"
            FP_CLASS = "Cbx"
            FP_DATATYPE = "Boolean"
            FP_BACKCOLOR = "ButtonFace"
            FP_HEIGHT = 69
         CASE DFD_TYPE = "P"
            FP_DATATYPE = "String"
            GOSUB 1500
         CASE 1
            FP_DATATYPE = "String"
            GOSUB 1500
      END CASE
      FP_WIDTH = FP_WIDTH * 1
      FP_ALIGN = FIELD(SSD_FLD_FMT<1,VNO>,"#",1)
      BEGIN CASE
         CASE FP_ALIGN = "L"
            FP_ALIGN = "Left"
         CASE FP_ALIGN = "R"
            FP_ALIGN = "Right"
         CASE 1
            FP_ALIGN = "Justify"
      END CASE
      FP_RECALC = SSD_FLD_RECALC<1,VNO>
      FP_TAB_ORDER = SSD_FLD_REF<1,VNO>
      FP_LINES = SSD_FLD_LINES<1,VNO>
      IF FP_TAB_ORDER = 999 THEN FP_TAB_ORDER = 0
      IF SSD_FLD_HIDE<1,VNO>+0 > 0 THEN FP_VISIBLE = 0 ELSE FP_VISIBLE = 1
      SEQNO = SEQNO + 1
      MATWRITE FP.REC ON FORM_PROPERTIES, FORM_NAME:"_":SEQNO
509*
   NEXT VNO
   GOTO 99999
*
*---- Process FP Class, BackColor, & Height
1500*
   BEGIN CASE
      CASE FP_HEIGHT > 1
         FP_CLASS = "Mle"
         FP_BACKCOLOR = "ButtonFace"
      CASE FP_VALUES # ""
         FP_CLASS = "Ddlb"
         FP_EDIT_DDLB = DFD_ORVALDAT + 0
         FP_BACKCOLOR = "ButtonFace"
      CASE 1
         FP_CLASS = "Sle"
         FP_BACKCOLOR = "ButtonFace"
   END CASE
   FP_HEIGHT = 69 * FP_HEIGHT
   RETURN
*
*---- Error routines
91000*
*  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RTN_CODE = -2
   RETURN
93000*
*  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
   RTN_CODE = -1
99999*
END
