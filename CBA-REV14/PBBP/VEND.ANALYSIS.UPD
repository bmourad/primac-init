*COPY>APS.CPYLIB>VEND.PROD.STATS
*COPY>PB.CPYLIB>VEND.ANALYSIS
*COPY>CPYLIB>CHAR
OPEN '','VEND' TO VEND ELSE PRINT 'VEND MISSING'; STOP
OPEN '','VEND.PROD.STATS' TO VEND.PROD.STATS ELSE PRINT 'VEND.PROD.STATS MISSING'; STOP
OPEN '','VEND.ANALYSIS' TO VEND.ANALYSIS ELSE PRINT 'VEND.ANALYSIS MISSING'; STOP
OPEN '','CONTROL' TO CONTROL ELSE PRINT 'CONTROL MISSING'; STOP
VDATA = 1
SELECT VEND
LOOP
  READNEXT CONO_VEND ELSE VDATA = 0
WHILE VDATA DO
  DELETE VEND.ANALYSIS, CONO_VEND:'!R'
  DELETE VEND.ANALYSIS, CONO_VEND:'!O'
  DELETE VEND.ANALYSIS, CONO_VEND:'!M'
  READ REC FROM CONTROL, CONO_VEND[1,3]:'VEND.TOLERANCES' THEN
    DATE.TOL.DAYS = REC<1>
    QTY.TOL.PCT = REC<2>
    COST.TOL.PCT = REC<3>
  END ELSE
    DATE.TOL.DAYS = 5
    QTY.TOL.PCT = 5
    COST.TOL.PCT = 5
  END
  SENTENCE = 'SELECT VEND.PROD.STATS WITH CONO_VEND = "':CONO_VEND:'"'
  PERFORM SENTENCE RTNLIST IDLIST CAPTURING JUNK
  DATA = 1
  LOOP
    READNEXT ID FROM IDLIST ELSE DATA = 0
  WHILE DATA DO
    TYPE = FIELD(ID,'!',2)
    PO.ID = ID[INDEX(ID,'!',2)+1,99]
    MATREAD VPDS.REC FROM VEND.PROD.STATS, ID THEN
      IF VPDS.ORD.DATE # '' AND VPDS.REC.DATE # '' THEN
        MATREAD VA.REC FROM VEND.ANALYSIS, CONO_VEND:'!':TYPE ELSE
          MAT VA.REC=''
          VA.DATE=0; VA.DATE<1,2>=0; VA.DATE<1,3>=0
          VA.QTY=0; VA.QTY<1,2>=0; VA.QTY<1,3>=0
          VA.COST=0; VA.COST<1,2>=0; VA.COST<1,3>=0
        END
        BEGIN CASE
        CASE VPDS.REC.DATE<1,1> < (VPDS.ORD.DATE<1,1> - DATE.TOL.DAYS)
          VA.DATE<1,3> += 1
          LOCATE PO.ID IN VA.DATE.ID<1,3>,1 BY 'AR' SETTING PTR ELSE
            VA.DATE.ID = INSERT(VA.DATE.ID,1,3,PTR,PO.ID)
          END
        CASE VPDS.REC.DATE<1,1> > (VPDS.ORD.DATE<1,1> + DATE.TOL.DAYS)
          VA.DATE<1,1> += 1
          LOCATE PO.ID IN VA.DATE.ID<1,1>,1 BY 'AR' SETTING PTR ELSE
            VA.DATE.ID = INSERT(VA.DATE.ID,1,1,PTR,PO.ID)
          END
        CASE 1
          VA.DATE<1,2> += 1
          LOCATE PO.ID IN VA.DATE.ID<1,2>,1 BY 'AR' SETTING PTR ELSE
            VA.DATE.ID = INSERT(VA.DATE.ID,1,2,PTR,PO.ID)
          END
        END CASE
*
        ORD.QTY = SUM(VPDS.ORD.QTY)
        REC.QTY = SUM(VPDS.REC.QTY)
        TOL.QTY = ORD.QTY * (QTY.TOL.PCT / 100)
        BEGIN CASE
        CASE REC.QTY < (ORD.QTY - TOL.QTY)
          VA.QTY<1,1> += 1
          LOCATE PO.ID IN VA.QTY.ID<1,1>,1 BY 'AR' SETTING PTR ELSE
            VA.QTY.ID = INSERT(VA.QTY.ID,1,1,PTR,PO.ID)
          END
        CASE REC.QTY > (ORD.QTY + TOL.QTY)
          VA.QTY<1,3> += 1
          LOCATE PO.ID IN VA.QTY.ID<1,3>,1 BY 'AR' SETTING PTR ELSE
            VA.QTY.ID = INSERT(VA.QTY.ID,1,3,PTR,PO.ID)
          END
        CASE 1
          VA.QTY<1,2> += 1
          LOCATE PO.ID IN VA.QTY.ID<1,2>,1 BY 'AR' SETTING PTR ELSE
            VA.QTY.ID = INSERT(VA.QTY.ID,1,2,PTR,PO.ID)
          END
        END CASE
*
        ORD.COST = INT(SUM(VPDS.ORD.UN.COST) / DCOUNT(VPDS.ORD.DATE,VM) + .5)
        REC.COST = INT(SUM(VPDS.REC.UN.COST) / DCOUNT(VPDS.REC.DATE,VM) + .5)
        TOL.COST = ORD.COST * (COST.TOL.PCT / 100)
        BEGIN CASE
        CASE REC.COST < (ORD.COST - TOL.COST)
          VA.COST<1,3> += 1
          LOCATE PO.ID IN VA.COST.ID<1,3>,1 BY 'AR' SETTING PTR ELSE
            VA.COST.ID = INSERT(VA.COST.ID,1,3,PTR,PO.ID)
          END
        CASE REC.COST > (ORD.COST + TOL.COST)
          VA.COST<1,1> += 1
          LOCATE PO.ID IN VA.COST.ID<1,1>,1 BY 'AR' SETTING PTR ELSE
            VA.COST.ID = INSERT(VA.COST.ID,1,1,PTR,PO.ID)
          END
        CASE 1
          VA.COST<1,2> += 1
          LOCATE PO.ID IN VA.COST.ID<1,2>,1 BY 'AR' SETTING PTR ELSE
            VA.COST.ID = INSERT(VA.COST.ID,1,2,PTR,PO.ID)
          END
        END CASE
        MATWRITE VA.REC ON VEND.ANALYSIS, CONO_VEND:'!':TYPE
      END
    END
  REPEAT
REPEAT
PRINT "*** VEND.ANALYSIS.UPD FINISHED ***"
END
