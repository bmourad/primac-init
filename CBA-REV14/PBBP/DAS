SUBROUTINE DAS(CONO,DEPT,CCTR,OPER,SDATE,EDATE,ACT,EST)
************************************************************
* COPYRIGHT   - Vercom Software, Inc.
* PROGRAM     - BUILD.PRODCCTR
************************************************************
*
*-- EQUATE FILES
*
*COPY>JCS.CPYLIB>JOB.TIME
*COPY>JES.CPYLIB>EST.JOB.ACT
*COPY>CPYLIB>CHAR
*
*-- OPEN FILES
*
OPEN "JOB.TIME" TO JOB.TIME ELSE STOP
OPEN "EST.JOB.ACT" TO EST.JOB.ACT ELSE STOP
*
*-- INITIALIZE
*
JOBS=""
OPERS=""
ACT=0
EST=0
*
*-- MAIN PROCESSING
*
SENTENCE='SELECT JOB.TIME WITH JLB_DATE GE "':SDATE:'" AND WITH JLB_DATE LE "':EDATE:'"'
PRINT SENTENCE
UDTEXECUTE SENTENCE
DATA=1
LOOP
  READNEXT ID ELSE DATA=0
WHILE DATA DO
  IF CONO=ID[1,3] THEN
    JOB.ID=FIELD(ID,'!',1)
    MATREAD ESTJA.REC FROM EST.JOB.ACT, JOB.ID THEN
      MATREAD JLB.REC FROM JOB.TIME, ID THEN
        DPNO=FIELD(ID,'!',2)
        CCNO=FIELD(ID,'!',3)
        BEGIN CASE
        CASE DEPT # '00' AND DEPT # DPNO
        CASE CCTR # '00' AND CCTR # CCNO
        CASE OPER # '00' AND OPER # JLB.OPER
        CASE 1
          OPTR=0
          LOCATE JOB.ID IN JOBS,1 BY AR SETTING JPTR THEN
            LOCATE JLB.OPER IN OPERS<JPTR>,1 SETTING OPTR ELSE OPTR=0
          END
          IF NOT(OPTR) THEN
            JOBS<JPTR>=JOB.ID
            OPERS<JPTR,-1>=JLB.OPER
            LOCATE JLB.OPER IN ESTJA.LB.OPER<1>,1 SETTING FND THEN
              ACT=ACT + ESTJA.LB.ACT.HRS<1,FND>
              EST=EST + ESTJA.LB.HRS<1,FND>
            END
          END
        END CASE
      END
    END
  END
REPEAT
RETURN
END
