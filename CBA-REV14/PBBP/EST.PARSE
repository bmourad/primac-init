SUBROUTINE EST.PARSE(RTN.VAL,EST.KEY)
************************************************************
* COPYRIGHT   - Vercom Software, Inc.
* PROGRAM     - EST.PARSE
* DESCRIPTION - Create tables for Paper and Ink
************************************************************
*
*-- EQUATE FILES
*
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>PB.CPYLIB>EST.DP.CC.SUM
*COPY>CPYLIB>CHAR
*
*-- OPEN FILES
*
RTN.VAL = 0
OPEN "ESTIMATE" TO ESTIMATE ELSE RETURN
OPEN "ESTIMATE.RES" TO ESTIMATE.RES ELSE RETURN
OPEN "ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE RETURN
OPEN "EST.DP.CC.SUM" TO EST.DP.CC.SUM ELSE RETURN
OPEN "EST.PAPER" TO EST.PAPER ELSE RETURN
OPEN "EST.INK" TO EST.INK ELSE RETURN
OPEN "COST.CNTR" TO COST.CNTR ELSE RETURN
*
*-- MAIN PROCESSING
*
CONO = EST.KEY[1,3]
MATREAD EST.REC FROM ESTIMATE, EST.KEY THEN
  MATREAD EST.RL.REC FROM ESTIMATE.RES, EST.KEY ELSE MAT EST.RL.REC = ""
*
  IDS = ''
  DCNT = DCOUNT(EST.DEPT.COMP,VM)
  FOR DP = 1 TO DCNT
    ESTD.ID = EST.DEPT.COMP<1,DP>
    MATREAD ESTD.REC FROM ESTIMATE.DEPT, EST.KEY:"!":ESTD.ID THEN
      DEPT = ESTD.DEPT
      COMP = FIELD(ESTD.ID,"!",2)
      QTY = FIELD(ESTD.ID,"!",3)
      ID = EST.KEY:"!":COMP:"!":QTY
      ID2 = EST.KEY:"!ALL!":QTY
      LOCATE ID IN IDS,1 SETTING FND ELSE
        DELETE EST.DP.CC.SUM, ID
        IDS<-1> = ID
      END
      MATREADU DSUM.REC FROM EST.DP.CC.SUM, ID ELSE MAT DSUM.REC = ""
      GOSUB 1000
      MATWRITE DSUM.REC ON EST.DP.CC.SUM, ID
      LOCATE ID2 IN IDS,1 SETTING FND ELSE
        DELETE EST.DP.CC.SUM, ID2
        IDS<-1> = ID2
      END
      MATREADU DSUM.REC FROM EST.DP.CC.SUM, ID2 ELSE MAT DSUM.REC = ""
      GOSUB 1000
      MATWRITE DSUM.REC ON EST.DP.CC.SUM, ID2
    END
  NEXT DP
*
  CCNT = DCOUNT(EST.PROD.OS.PROD,VM)
  FOR C = 1 TO CCNT
    WCNT = DCOUNT(EST.PROD.OS.PROD<1,C>,SM)
    FOR W = 1 TO WCNT
      IF EST.PDEF.TYPE = "RL" THEN
        QCNT = DCOUNT(EST.RL.PAP.MSI<1,C,W>,'!')
        FOR Q = 1 TO QCNT
          ID = EST.KEY:"!":EST.QTY<1,Q>
          QTY = FIELD(EST.RL.PAP.MSI<1,C,W>,'!',Q)
          CST = FIELD(EST.RL.PAP.PRICE<1,C,W>,'!',Q)
          READU REC FROM EST.PAPER, ID ELSE REC = ""
          IF C = 1 AND W = 1 THEN REC = ""
          REC<1,C,W> = QTY
          REC<2,C,W> = CST
          WRITE REC ON EST.PAPER, ID
        NEXT Q
      END ELSE
        QCNT = DCOUNT(EST.PROD.PQTY<1,C,W>,'!')
        FOR Q = 1 TO QCNT
          ID = EST.KEY:"!":EST.QTY<1,Q>
          QTY = FIELD(EST.PROD.PQTY<1,C,W>,'!',Q)
          CST = FIELD(EST.PROD.PCST<1,C,W>,'!',Q)
          READU REC FROM EST.PAPER, ID ELSE REC = ""
          IF C = 1 AND W = 1 THEN REC = ""
          REC<1,C,W> = QTY
          REC<2,C,W> = CST
          WRITE REC ON EST.PAPER, ID
        NEXT Q
      END
      ICNT = DCOUNT(EST.PROD.INK.ID<1,C,W>,'!')
      FOR I = 1 TO ICNT
        ID = EST.KEY:"!":I
        INK = FIELD(EST.PROD.INK.ID<1,C,W>,'!',I)
        SIDES = FIELD(EST.PROD.INK.SIDES<1,C,W>,'!',I)
        COVER = FIELD(EST.PROD.INK.COVER<1,C,W>,'!',I)
        READU REC FROM EST.INK, ID ELSE REC = ""
        IF C = 1 AND W = 1 THEN REC = ""
        REC<1,C,W> = INK
        REC<2,C,W> = SIDES
        REC<3,C,W> = COVER
        WRITE REC ON EST.INK, ID
      NEXT I
    NEXT W
  NEXT C
END
GOTO 99999
*
*--- BUILD DEPARTMENT SUMMARY
*
1000 *
LOCATE DEPT IN DSUM.DEPT<1>,1 BY "AL" SETTING P1 ELSE
  IF P1 <= DCNT THEN
    FOR N = 1 TO 40
      DSUM.REC(N) = INSERT(DSUM.REC(N),1,P1,0,"")
    NEXT N
  END
  DSUM.DEPT<1,P1> = DEPT
  LOCATE DEPT IN EST.DEPT<1>,1 SETTING PP ELSE NULL
  DSUM.DEPT.DESC<1,P1> = EST.DEPT.DESC<1,PP>
END
TCNT = DCOUNT(ESTD.TYPE,VM)
FOR TP = 1 TO TCNT
  TYPE = ESTD.TYPE<1,TP>
  IF TYPE = "G" THEN GOTO 1099
  CCTR = ESTD.CCTR<1,TP>
  LOCATE CCTR IN DSUM.CCTR<1,P1>,1 BY "AL" SETTING P2 ELSE
    IF P2 <= DCOUNT(DSUM.CCTR<1,P1>,SM) THEN
      FOR N = 21 TO 40
        DSUM.REC(N) = INSERT(DSUM.REC(N),1,P1,P2,"")
      NEXT N
    END
    DSUM.CCTR<1,P1,P2> = CCTR
    READV DESC FROM COST.CNTR, CONO:CCTR,1 ELSE DESC = "??????????"
    DSUM.CCTR.DESC<1,P1,P2> = DESC
  END
  TYPE = ESTD.TYPE<1,TP>[1,1]
  TSUB = ESTD.TYPE<1,TP>[2,1]
  HRS = ESTD.HRS<1,TP>
  DCOST = ESTD.DCOST<1,TP>
  COST = ESTD.COST<1,TP>
  SALE = ESTD.TSALE<1,TP>
  BEGIN CASE
  CASE TYPE = "L"
    DSUM.DEPT.LB.HRS<1,P1> = DSUM.DEPT.LB.HRS<1,P1> + HRS
    DSUM.DEPT.LB.DCOST<1,P1> = DSUM.DEPT.LB.DCOST<1,P1> + DCOST
    DSUM.DEPT.LB.COST<1,P1> = DSUM.DEPT.LB.COST<1,P1> + COST
    DSUM.DEPT.LB.SALE<1,P1> = DSUM.DEPT.LB.SALE<1,P1> + SALE
    DSUM.CCTR.LB.HRS<1,P1,P2> = DSUM.CCTR.LB.HRS<1,P1,P2> + HRS
    DSUM.CCTR.LB.DCOST<1,P1,P2> = DSUM.CCTR.LB.DCOST<1,P1,P2> + DCOST
    DSUM.CCTR.LB.COST<1,P1,P2> = DSUM.CCTR.LB.COST<1,P1,P2> + COST
    DSUM.CCTR.LB.SALE<1,P1,P2> = DSUM.CCTR.LB.SALE<1,P1,P2> + SALE
  CASE TYPE = "M" OR TYPE = "I"
    DSUM.DEPT.MT.DCOST<1,P1> = DSUM.DEPT.MT.DCOST<1,P1> + DCOST
    DSUM.DEPT.MT.COST<1,P1> = DSUM.DEPT.MT.COST<1,P1> + COST
    DSUM.DEPT.MT.SALE<1,P1> = DSUM.DEPT.MT.SALE<1,P1> + SALE
    DSUM.CCTR.MT.DCOST<1,P1,P2> = DSUM.CCTR.MT.DCOST<1,P1,P2> + DCOST
    DSUM.CCTR.MT.COST<1,P1,P2> = DSUM.CCTR.MT.COST<1,P1,P2> + COST
    DSUM.CCTR.MT.SALE<1,P1,P2> = DSUM.CCTR.MT.SALE<1,P1,P2> + SALE
    BEGIN CASE
    CASE TSUB = "S" OR TSUB = "R" OR TSUB = "L"
      DET = 1
    CASE TSUB = "I"
      DET = 2
    CASE TSUB = "F"
      DET = 3
    CASE TSUB = "P"
      DET = 4
    CASE TSUB = "C"
      DET = 5
    CASE 1
      DET = 6
    END CASE
    DSUM.MT.DCOST.DET<1,DET> = DSUM.MT.DCOST.DET<1,DET> + DCOST
    DSUM.MT.COST.DET<1,DET> = DSUM.MT.COST.DET<1,DET> + COST
    DSUM.MT.SALE.DET<1,DET> = DSUM.MT.SALE.DET<1,DET> + SALE
    FOR F = DET TO 6
      DSUM.MT.DCOST.DET<1,F> = DSUM.MT.DCOST.DET<1,F> + 0
      DSUM.MT.COST.DET<1,F> = DSUM.MT.COST.DET<1,F> + 0
      DSUM.MT.SALE.DET<1,F> = DSUM.MT.SALE.DET<1,F> + 0
    NEXT F
  CASE TYPE = "P"
    DSUM.DEPT.OS.DCOST<1,P1> = DSUM.DEPT.OS.DCOST<1,P1> + DCOST
    DSUM.DEPT.OS.COST<1,P1> = DSUM.DEPT.OS.COST<1,P1> + COST
    DSUM.DEPT.OS.SALE<1,P1> = DSUM.DEPT.OS.SALE<1,P1> + SALE
    DSUM.CCTR.OS.DCOST<1,P1,P2> = DSUM.CCTR.OS.DCOST<1,P1,P2> + DCOST
    DSUM.CCTR.OS.COST<1,P1,P2> = DSUM.CCTR.OS.COST<1,P1,P2> + COST
    DSUM.CCTR.OS.SALE<1,P1,P2> = DSUM.CCTR.OS.SALE<1,P1,P2> + SALE
  CASE TYPE = "S"
    DSUM.DEPT.SP.DCOST<1,P1> = DSUM.DEPT.SP.DCOST<1,P1> + DCOST
    DSUM.DEPT.SP.COST<1,P1> = DSUM.DEPT.SP.COST<1,P1> + COST
    DSUM.DEPT.SP.SALE<1,P1> = DSUM.DEPT.SP.SALE<1,P1> + SALE
    DSUM.CCTR.SP.DCOST<1,P1,P2> = DSUM.CCTR.SP.DCOST<1,P1,P2> + DCOST
    DSUM.CCTR.SP.COST<1,P1,P2> = DSUM.CCTR.SP.COST<1,P1,P2> + COST
    DSUM.CCTR.SP.SALE<1,P1,P2> = DSUM.CCTR.SP.SALE<1,P1,P2> + SALE
  CASE TYPE = "O"
    DSUM.DEPT.MS.DCOST<1,P1> = DSUM.DEPT.MS.DCOST<1,P1> + DCOST
    DSUM.DEPT.MS.COST<1,P1> = DSUM.DEPT.MS.COST<1,P1> + COST
    DSUM.DEPT.MS.SALE<1,P1> = DSUM.DEPT.MS.SALE<1,P1> + SALE
    DSUM.CCTR.MS.DCOST<1,P1,P2> = DSUM.CCTR.MS.DCOST<1,P1,P2> + DCOST
    DSUM.CCTR.MS.COST<1,P1,P2> = DSUM.CCTR.MS.COST<1,P1,P2> + COST
    DSUM.CCTR.MS.SALE<1,P1,P2> = DSUM.CCTR.MS.SALE<1,P1,P2> + SALE
  END CASE
1099 *
NEXT TP
RETURN
*
*--- END
*
99999 *
RTN.VAL = 1
RETURN
END
