   SUBROUTINE SAVE_DOCUSET_DATA(RTN_CODE,TKT_ID,DOCUSET_ID)
*COPY>CPYLIB>COM1
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [11.B]
* Revision Date - 09/11/98
* System        - PRIMAC
* Library       - JISBP/SAVE_DOCUSET_DATA
* Author        - Ziad Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>JIS.CPYLIB>DOCUSET_SFIS_DATA
*COPY>JIS.CPYLIB>SYS_SCN_DEF
*COPY>JIS.CPYLIB>SYS_TKT_DEF
*COPY>JIS.CPYLIB>JKT_NOTIFY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*
   OPEN "","DOCUSET_SFIS_DATA" TO DOCUSET_SFIS_DATA ELSE
      RTN_CODE = "Cannot locate the DOCUSET_SFIS_DATA file"
      GOTO 99999
   END
   OPEN "","SYS_TKT_DEF" TO SYS_TKT_DEF ELSE
      RTN_CODE = "Cannot locate the SYS_TKT_DEF file"
      GOTO 99999
   END
   OPEN "","SYS_SCN_DEF" TO SYS_SCN_DEF ELSE
      RTN_CODE = "Cannot locate the SYS_SCN_DEF file"
      GOTO 99999
   END
*  OPEN "","DOCUSET_CONTROL" TO DOCUSET_CONTROL ELSE
*     RTN_CODE = "Cannot locate the DOCUSET_CONTROL file"
*     GOTO 99999
*  END
   OPEN "","JKT_NOTIFY" TO JKT_NOTIFY ELSE
      RTN_CODE = "Cannot locate the JKT_NOTIFY file"
      GOTO 99999
   END
   OPEN "","CPYLIB" TO CPYLIB ELSE
      RTN_CODE = "Cannot locate the CPYLIB file"
      GOTO 99999
   END
*
*--- Initialize
*
   PREV_FORM = ""
   FLDNOS = ""
   CONO = TKT_ID[1,3]
   TICKETNO = TKT_ID[4,99]
*
*---- Get Passed Parameters
*
   READ FVAR_REC FROM CPYLIB, "FILE.VARS" ELSE
      RTN_CODE = "Cannot locate CPYLIB, FILE.VARS"
      GOTO 99999
   END
   PFX = "^IGNORE^"
   CALL CPYLIB_SUB(FVAR_REC,PFX,ARRAY_NAME,FVAR_SIZE,RTN_CODE)
   IF RTN_CODE # "" THEN GOTO 99999
   MATREAD STKD.REC FROM SYS_TKT_DEF, DOCUSET_ID ELSE
      RTN_CODE = "Cannot locate screen names for Job Jacket ":DOCUSET_ID
      GOTO 99999
   END
   IF STKD_REV_CTL = "" THEN
      STKD_REV_CTL = 1
   END
   IF STKD_GEN_N_ID = "" THEN
      STKD_GEN_N_ID = 1
   END
   STKD_BATCHMODE = STKD_BATCHMODE + 0
   STKD_TRANSMODE = STKD_TRANSMODE + 0
   JCNT = DCOUNT(STKD_SCN,VM)
   SCN_NAMES = STKD_SCN<1,1>
   FOR JPTR = 2 TO JCNT
      SCN_NAMES = SCN_NAMES:",":STKD_SCN<1,JPTR>
   NEXT JPTR
*
   READ FLD_NAMES FROM SYS_TKT_DEF, DOCUSET_ID:"*FLD" ELSE
      RTN_CODE = "Cannot locate Docuset # ":DOCUSET_ID
      GOTO 99999
   END
*  TRN_FILENAME = DOCUSET_ID:"_TRN"
   TRN_FILENAME = "JOB_JKT_IMG"
   OPEN "", TRN_FILENAME TO TRN_FILE ELSE
      RTN_CODE = "Cannot locate the ":TRN_FILENAME:" file"
      GOTO 99999
   END
   RTN_CODE = 0
*  READU ITEM FROM TRN_FILE, TKT_ID:"!":DOCUSET_ID ELSE ITEM = ""
   READU ITEM FROM TRN_FILE, TKT_ID ELSE ITEM = ""
*
   SENTENCE = 'SSELECT DOCUSET_SFIS_DATA BY DSD_FORM WITH TKT_ID = "':TKT_ID:'" AND WITH DSD_DOCUSET = "':DOCUSET_ID:'"'
   EXECUTE SENTENCE CAPTURING JUNK
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD DSD.REC FROM DOCUSET_SFIS_DATA, ID THEN
         IF DSD_FORM # PREV_FORM THEN
            MATREAD SSD.REC FROM SYS_SCN_DEF, DSD_FORM ELSE MAT SSD.REC = ""
            IF PREV_FORM = "" THEN
               OPEN "", SSD_M_TABLE TO JOB_TKT ELSE
                  RTN_CODE = "Cannot locate the ":SSD_M_TABLE:" file"
                  GOTO 99999
               END
               READU TKT FROM JOB_TKT, TKT_ID ELSE TKT = ""
               OTKT = TKT
            END
         END
         PREV_FORM = DSD_FORM
         LOCATE DSD_NAME IN SSD_FIELDS<1>,1 SETTING FPTR THEN
            IF SSD_FLD_TABLE<1,FPTR> = "" THEN
               LOCATE DSD_NAME IN FLD_NAMES, 1 SETTING FLDNO THEN
                  LOCATE FLDNO IN FLDNOS, 1 SETTING FND ELSE
                     ITEM<FLDNO> = ""
                     FLDNOS<FND> = FLDNO
                  END
                  VALUE = ""
                  BEGIN CASE
                  CASE DSD_STRING # ""
                     VALUE = DSD_STRING
                  CASE DSD_INTEGER # ""
                     VALUE = DSD_INTEGER
                  CASE DSD_LONG # ""
                     VALUE = DSD_LONG
                  CASE DSD_DECIMAL # ""
                     VALUE = DSD_DECIMAL
                  CASE DSD_DATE # ""
                     VALUE = DSD_DATE
                  CASE DSD_TIME # ""
                     VALUE = DSD_TIME
                  CASE DSD_BOOLEAN # ""
                     IF DSD_BOOLEAN = 1 THEN VALUE = "Y" ELSE VALUE = "N"
                  END CASE
                  ITEM<FLDNO,DSD_MVNO,DSD_SMVNO> = VALUE
                  TKT<SSD_FLD_ATTNO<1,FPTR>> = ITEM<FLDNO>
               END
            END
         END
      END
   REPEAT
*
*---- Updates
*
   IF ITEM # "" THEN
*     WRITE ITEM ON TRN_FILE, TKT_ID:"!":DOCUSET_ID
      WRITE ITEM ON TRN_FILE, TKT_ID
   END ELSE
*     RELEASE TRN_FILE, TKT_ID:"!":DOCUSET_ID
      RELEASE TRN_FILE, TKT_ID
   END
   IF TKT # "" AND TKT # OTKT THEN
      DIM O_REC(650)
      MATPARSE O_REC FROM OTKT, CHAR(254)
      FNAME = SSD_M_TABLE  ; FID = TICKETNO
      MATREADU SSJN.REC FROM JKT_NOTIFY, CONO:FNAME:"!":FID ELSE MAT SSJN.REC = ""
      JKTBASE = FIELD(TICKETNO,"-",1)
      JCNT = DCOUNT(SSJN_TICKET,VM)
      TPTR = JCNT + 1
      FOR JPTR = 1 TO JCNT UNTIL TPTR <= JCNT
         TKTBASE = FIELD(SSJN_TICKET<1,JPTR>,"-",1)
         BEGIN CASE
         CASE TKTBASE = JKTBASE
            TPTR = JPTR
         CASE TKTBASE > JKTBASE
            TPTR = JPTR
            SSJN_TICKET = INSERT(SSJN_TICKET,1,TPTR,0,"")
            SSJN_TKTDEF = INSERT(SSJN_TKTDEF,1,TPTR,0,"")
            SSJN_SCREEN = INSERT(SSJN_SCREEN,1,TPTR,0,"")
         END CASE
      NEXT JPTR
      SSJN_TICKET<1,TPTR> = TICKETNO
      SSJN_TKTDEF<1,TPTR> = DOCUSET_ID
      SSJN_SCREEN<1,TPTR> = ""
      SCNT = DCOUNT(SCN_NAMES,",")
      FOR SPTR = 1 TO SCNT
         SNAME = TRIM(FIELD(SCN_NAMES,",",SPTR))
         LOCATE SNAME IN SSJN_SCREEN<1,TPTR>,1 SETTING P ELSE
            SSJN_SCREEN<1,TPTR,P> = SNAME
         END
      NEXT SPTR
      IF SSJN_SCREEN<1,TPTR> = "" THEN
         SSJN_TICKET = DELETE(SSJN_TICKET,1,TPTR,0)
         SSJN_TKTDEF = DELETE(SSJN_TKTDEF,1,TPTR,0)
         SSJN_SCREEN = DELETE(SSJN_SCREEN,1,TPTR,0)
      END
      IF SSJN_TICKET = "" THEN
         RELEASE JKT_NOTIFY, CONO:FNAME:"!":FID
      END ELSE
         MATWRITE SSJN.REC ON JKT_NOTIFY, CONO:FNAME:"!":FID
      END
      LOCATE SSD_M_TABLE IN FVAR_REC,1 SETTING MTBL_PTR ELSE
         LIMIT = 1; MTBL_PTR = 0
         FOR I = FVAR_SIZE TO LIMIT STEP -1
            IF FVAR_REC<I> = "" THEN
               FVAR_REC<I> = CONVERT(".","_",SSD_M_TABLE)
               LIMIT = FVAR_SIZE; MTBL_PTR = I
            END
         NEXT I
      END
*     IF MTBL_PTR # 0 THEN
*        CALL COURIER.NOTIFY.SUB("0",CONO,SSD_M_TABLE,TICKETNO,DOCUSET_ID,"650",MAT O_REC,FILE.VARS(MTBL_PTR),"1")
*     END
      WRITE TKT ON JOB_TKT, TKT_ID
   END ELSE
      RELEASE JOB_TKT, TKT_ID
   END
*
*---- END OF PROGRAM
*
99999*
END
