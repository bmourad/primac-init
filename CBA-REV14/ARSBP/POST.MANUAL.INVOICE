*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - ARSBP
* PROGRAM     - POST.MANUAL.INVOICE
* BY          - WALID YAMOUT, CBA
* DATE        - 05/16/85
* DESCRIPTION -
*T21177 diane 12/04/1996 * Close screen manually
*T23278 markt 12/03/1998 * Make fiscal data multi-value by division.
*T25764 renee 07/25/2001 * Add .DUE.DATE to the .. record.
*T27942 lross 02/09/2004 * Incorrect PERIOD used for TAX.BAL file.
*ENDDOC
*********************************************************************
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>TAX
*COPY>PMC.CPYLIB>TAX.BAL
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>INVOICE.CODE
*COPY>ARS.CPYLIB>MANUAL.INVOICE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ARS.CPYLIB>OPEN.RECV
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- SETUP SYSTEM ERROR MESSAGES
*
    SYS.TYPE = 1
    CALL SYSCOM(MAT SYSCOM.REC)
*
*--- OPEN THE FILES
*
OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG="PMC.SCREENS FILE IS MISSING"; GOTO 93000
OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE IS MISSING"; GOTO 93000
OPEN '','INVOICE' TO INVOICE ELSE ERRMSG="INVOICE FILE IS MISSING"; GOTO 93000
OPEN '','MANUAL.INVOICE' TO MANUAL.INVOICE ELSE ERRMSG="MANUAL.INVOICE FILE IS MISSING"; GOTO 93000
OPEN '','MANUAL.INVOICE.TAG' TO MANUAL.INVOICE.TAG ELSE ERRMSG="MANUAL.INVOICE.TAG FILE IS MISSING"; GOTO 93000
OPEN '','INVOICE.CODE' TO INVOICE.CODE ELSE ERRMSG="INVOICE.CODE FILE IS MISSING"; GOTO 93000
OPEN '','DAILY.MANUAL.INVOICE' TO DAILY.MANUAL.INVOICE ELSE ERRMSG="DAILY.MANUAL.INVOICE FILE IS MISSING"; GOTO 93000
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE IS MISSING"; GOTO 93000
OPEN '','OPEN.RECV' TO OPEN.RECV ELSE ERRMSG="OPEN.RECV FILE IS MISSING"; GOTO 93000
OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG="CUSTOMER FILE IS MISSING"; GOTO 93000
OPEN '','TAX' TO TAX ELSE ERRMSG="TAX FILE IS MISSING"; GOTO 93000
OPEN '','TAX.BAL' TO TAX.BAL ELSE ERRMSG="TAX.BAL FILE IS MISSING"; GOTO 93000
OPEN '','SHIP.VIA' TO SHIP.VIA ELSE ERRMSG="SHIP.VIA FILE IS MISSING"; GOTO 93000
*
*---INITIALIZATION
*
   MAT EDIT.COM.DRIVER = ""
   POSTING.MON = ""
*
*--- SETUP VALUES FROM M.SCREENS
*
   ECD.ACTION = 1
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME = 'GET.FISCAL.MONTH'
   CALL SCRN.EDIT
   ECD.SCRN.NO = 1
*
*--- PROCREAD 
*
***** T23278 v
   PROCREAD INBUFF ELSE
       ERRMSG = "MUST BE RUN FROM A PROC"
       GOTO 99990
   END
   DIV.CODE = INBUFF<4>
*
***** T23278 ^
*
*--- PRINT SCREEN
*
   MAT SCV.REC = ""
   ECD.ACTION = 2
   CALL SCRN.EDIT
*
*--- MAIN PROCESSING
*
   READNEXT MIV.ID ELSE GOTO 99990
   CONO = MIV.ID[1,3]
   MATREAD COMP.REC FROM COMPANY, CONO ELSE GOTO 99990
*
*--- INPUT POSTING MONTH
*
   HEADING = "MANUAL INVOICE POSTING PROCESS"
*   CALL GET.FISCAL.MONTH(CONO,POSTING.MON,HEADING,"ARSFISCAL",CURR.MON);* T23278
   CALL GET.DIV.FISCAL.MONTH(CONO,POSTING.MON,HEADING,"ARSFISCAL",CURR.MON,DIV.CODE);* T23278
   IF POSTING.MON = "END" THEN GOTO 99990
   IF CO.SAS = "Y" THEN
     OPEN "","COST.CNTR" TO COST.CNTR ELSE ERRMSG = "COST.CNTR FILE IS MISSING"; ECD.ACTION = 99 ; CALL SCRN.EDIT ; GOTO 93000
   END
   GOSUB 1000
100*
   READNEXT MIV.ID ELSE GOTO 99990
   GOSUB 1000
   GOTO 100
*
*--- UPDATE ROUTINE
*
1000*
   BLK = 0
   TOTAL = 0
   TAX.AMT = 0
   SHIP.AMT = 0
   AGC.AMT = 0
   DSC.AMT = 0
   MSC.AMT = 0
   IVC.NO = ""
   MAT OR.REC = ""
   IVC.NO = MIV.ID[4,LEN(MIV.ID)-3]
   MATREADU MIV.REC FROM DAILY.MANUAL.INVOICE, MIV.ID ELSE GOTO 1999
   MATREADU CUST.REC FROM CUSTOMER, CONO:MIV.CUST.NO ELSE
     MIV.STATUS = "CUSTOMER ":MIV.CUST.NO:" IS MISSING" ; BLK = 1 ; GOTO 1900
   END
   LOCATE IVC.NO IN CUST.INVOICE<1>,1 SETTING CFND ELSE CFND = 0
   IF CFND THEN MIV.STATUS = "RECORD ALREADY POSTED TO CUSTOMER"; BLK = 1; GOTO 1900
   REC = ""
   READU REC FROM INVOICE , MIV.ID ELSE GOTO 1111
   MIV.STATUS = "RECORD ":IVC.NO:" IS ALREADY ON INVOICE FILE" ; BLK = 1 ; GOTO 1900
1111*
   REC = ""
   READU REC FROM OPEN.RECV , MIV.ID ELSE GOTO 1222
   MIV.STATUS = "RECORD ":IVC.NO:" IS ALREADY ON OPEN.RECV FILE" ; BLK = 1 ; GOTO 1900
1222*
   CNT = COUNT(MIV.CHG.CODE,VM) + (MIV.CHG.CODE # "")
   FOR I = 1 TO CNT UNTIL BLK
      BEGIN CASE
      CASE MIV.CHG.CAT<1,I> = "CMT"
         GOTO 1888
      CASE MIV.CHG.CODE<1,I> = "TOT"
         GOTO 1888
      CASE MIV.CHG.CODE<1,I> = "SUB"
         GOTO 1888
      CASE MIV.CHG.CODE<1,I> = "SUBT"
         GOTO 1888
      END CASE
     BEGIN CASE
     CASE MIV.CHG.CAT<1,I> = "TAX"
       TAX.AMT = TAX.AMT + MIV.AMOUNT<1,I>
       MATREADU TAX.REC FROM TAX, CONO:MIV.TAX.JURS<1,I> ELSE
          MAT TAX.REC = ""
       END
       LOCATE POSTING.MON IN TAX.PERIOD<1>,1 BY "DR" SETTING FND.PER ELSE
          INS POSTING.MON BEFORE TAX.PERIOD<1,FND.PER>
          INS "0" BEFORE TAX.TAXABLE<1,FND.PER>
          INS "0" BEFORE TAX.CHARGED<1,FND.PER>
       END
       TAX.CHARGED<1,FND.PER> = TAX.CHARGED<1,FND.PER> + MIV.AMOUNT<1,I>
       TAX.TAXABLE<1,FND.PER> = TAX.TAXABLE<1,FND.PER> + MIV.TAXABLE<1,I>
       MATWRITE TAX.REC ON TAX, CONO : MIV.TAX.JURS<1,I>
       MATREADU TAX.BAL.REC FROM TAX.BAL, CONO:MIV.DIV:MIV.DEPT:MIV.CCTR:MIV.TAX.JURS<1,I> ELSE
          MAT TAX.BAL.REC = ""
       END
*T27942 v
*      LOCATE POSTING.MON IN TBR.PERIOD<1>,1 BY "DR" SETTING FND.PER ELSE
*         INS POSTING.MON BEFORE TBR.PERIOD<1,FND.PER>
       ODI.DATE = OCONV(MIV.DATE,"D4/")
       TAX.MONTH = ODI.DATE[1,2]
       TAX.YEAR  = ODI.DATE[7,4]
       TAX.POST.MONTH = TAX.YEAR:TAX.MONTH"R%2"
       LOCATE TAX.POST.MONTH IN TBR.PERIOD<1>,1 BY "DR" SETTING FND.PER ELSE
          INS TAX.POST.MONTH BEFORE TBR.PERIOD<1,FND.PER>
*T27942 ^
          INS "0" BEFORE TBR.TAXABLE<1,FND.PER>
          INS "0" BEFORE TBR.CHARGED<1,FND.PER>
       END
       TBR.CHARGED<1,FND.PER> = TBR.CHARGED<1,FND.PER> + MIV.AMOUNT<1,I>
       TBR.TAXABLE<1,FND.PER> = TBR.TAXABLE<1,FND.PER> + MIV.TAXABLE<1,I>
       MATWRITE TAX.BAL.REC ON TAX.BAL, CONO:MIV.DIV:MIV.DEPT:MIV.CCTR:MIV.TAX.JURS<1,I>
     CASE MIV.AMOUNT<1,I> + 0 = 0
        GOTO 1888
     CASE MIV.CHG.CAT<1,I> = "SHP"
       SHIP.AMT = SHIP.AMT + MIV.AMOUNT<1,I>
       MATREADU SHIP.VIA.REC FROM SHIP.VIA, CONO:MIV.TAX.JURS<1,I> ELSE
         MAT SHIP.VIA.REC = ""
       END
        LOCATE POSTING.MON IN SHPV.PERIOD<1>,1 BY "DR" SETTING FND.PER ELSE
           INS POSTING.MON BEFORE SHPV.PERIOD<1,FND.PER>
           INS "0" BEFORE SHPV.SHPMT<1,FND.PER>
           INS "0" BEFORE SHPV.COST<1,FND.PER>
           INS "0" BEFORE SHPV.SALE<1,FND.PER>
        END
        SHPV.SALE<1,FND.PER> = SHPV.SALE<1,FND.PER> + MIV.AMOUNT<1,I>
        MATWRITE SHIP.VIA.REC ON SHIP.VIA, CONO : MIV.TAX.JURS<1,I>
     CASE MIV.CHG.CAT<1,I> = "AGC"
        AGC.AMT = AGC.AMT + MIV.AMOUNT<1,I>
     CASE MIV.CHG.CAT<1,I> = "MSC"
        MSC.AMT = MSC.AMT + MIV.AMOUNT<1,I>
     CASE MIV.CHG.CAT<1,I> = "DSC"
        DSC.AMT = DSC.AMT + MIV.AMOUNT<1,I>
     END CASE
     TOTAL = TOTAL + MIV.AMOUNT<1,I>
1888*
   NEXT I
1900*
   IF BLK THEN
      MATWRITE MIV.REC ON DAILY.MANUAL.INVOICE, MIV.ID
      GOTO 1999
   END
   MIV.PROC.MON = POSTING.MON
   MIV.PROC.DATE = DATE()
   MIV.STATUS = ""
   CUST.INVOICE = INSERT(CUST.INVOICE,1,-1,0,IVC.NO)
   CUST.AR.BAL = CUST.AR.BAL + TOTAL
   POST.AMT = TOTAL - TAX.AMT - SHIP.AMT
   LOCATE POSTING.MON IN CUST.SALES.PER<1>,1 BY "DR" SETTING FND.PER ELSE
      INS POSTING.MON BEFORE CUST.SALES.PER<1,FND.PER>
      INS "0" BEFORE CUST.SALES<1,FND.PER>
   END
   CUST.SALES<1,FND.PER> = CUST.SALES<1,FND.PER> + POST.AMT
   CUST.LAST.SALE = MIV.DATE
   CUST.INVOICE.CNT<1,1> = CUST.INVOICE.CNT<1,1> + 1
   CUST.TOT.AMT = CUST.TOT.AMT + TOTAL
   BAL.DATE = OCONV(CUST.HIGH.BAL.DATE<1,1>, "D2-")
   NEW.DATE = OCONV(MIV.PROC.DATE, "D2-")
   BAL.MON = FIELD(BAL.DATE,"-",1)
   NEW.MON = FIELD(NEW.DATE,"-",1)
   BAL.YR = BAL.DATE[7,2]
   NEW.YR = NEW.DATE[7,2]
   IF NEW.MON # BAL.MON OR NEW.YR # BAL.YR THEN
     CUST.HIGH.BAL = INSERT(CUST.HIGH.BAL,1,1,0,CUST.AR.BAL)
     CUST.HIGH.BAL.DATE = INSERT(CUST.HIGH.BAL.DATE,1,1,0,MIV.PROC.DATE)
   END ELSE
     IF CUST.HIGH.BAL<1,1> < CUST.AR.BAL THEN
        CUST.HIGH.BAL<1,1> = CUST.AR.BAL
        CUST.HIGH.BAL.DATE<1,1> = MIV.PROC.DATE
     END
   END
   BAL.CNT = COUNT(CUST.HIGH.BAL.DATE,VM) + (CUST.HIGH.BAL.DATE # "")
   FOR DROP = BAL.CNT TO 2 STEP - 1
     IF (CUST.HIGH.BAL.DATE<1,DROP> + 400) < MIV.PROC.DATE THEN
        CUST.HIGH.BAL.DATE = DELETE(CUST.HIGH.BAL.DATE,1,DROP,0)
        CUST.HIGH.BAL = DELETE(CUST.HIGH.BAL,1,DROP,0)
     END
   NEXT DROP
   OR.CUST = MIV.CUST.NO
*  OR.JOB = MIV.JOB.NO
   IF MIV.PO # "" THEN
      OR.PO = MIV.PO
   END ELSE
      OR.PO = MIV.JOB.NO
   END
   OR.INV.DATE = MIV.DATE
   OR.DUE.DATE = MIV.DUE.DATE     ; * T25764 
   OR.AS.DATE = MIV.PROC.DATE
   OR.TR.DATE = MIV.DATE
   OR.DIV = MIV.DIV
   OR.DEPT = MIV.DEPT<1,1>
   OR.CCTR = MIV.CCTR<1,1>
   OR.TYPE = "I"
   OR.INV.AMT = TOTAL - TAX.AMT - SHIP.AMT - AGC.AMT - DSC.AMT - MSC.AMT
   OR.SLSMAN = MIV.SLSMAN
   OR.BAL = TOTAL
   OR.MON = POSTING.MON
   IF TAX.AMT <> 0 THEN
     PTR = COUNT(OR.TYPE,VM) + (OR.TYPE # "") + 1
     OR.TR.DATE<1,PTR> = MIV.DATE
     OR.TYPE<1,PTR> = "T"
     OR.INV.AMT<1,PTR> = TAX.AMT
     OR.CHECK<1,PTR> = ""
     OR.CHK.DATE<1,PTR> = ""
     OR.CHK.AMT<1,PTR> = ""
   END
   IF SHIP.AMT <> 0 THEN
     PTR = COUNT(OR.TYPE,VM) + (OR.TYPE # "") + 1
     OR.TR.DATE<1,PTR> = MIV.DATE
     OR.TYPE<1,PTR> = "S"
     OR.INV.AMT<1,PTR> = SHIP.AMT
     OR.CHECK<1,PTR> = ""
     OR.CHK.DATE<1,PTR> = ""
     OR.CHK.AMT<1,PTR> = ""
   END
   IF AGC.AMT <> 0 THEN
     PTR = COUNT(OR.TYPE,VM) + (OR.TYPE # "") + 1
     OR.TR.DATE<1,PTR> = MIV.DATE
     OR.TYPE<1,PTR> = "G"
     OR.INV.AMT<1,PTR> = AGC.AMT
     OR.CHECK<1,PTR> = ""
     OR.CHK.DATE<1,PTR> = ""
     OR.CHK.AMT<1,PTR> = ""
   END
   IF DSC.AMT <> 0 THEN
     PTR = COUNT(OR.TYPE,VM) + (OR.TYPE # "") + 1
     OR.TR.DATE<1,PTR> = MIV.DATE
     OR.TYPE<1,PTR> = "U"
     OR.INV.AMT<1,PTR> = DSC.AMT
     OR.CHECK<1,PTR> = ""
     OR.CHK.DATE<1,PTR> = ""
     OR.CHK.AMT<1,PTR> = ""
   END
   IF MSC.AMT <> 0 THEN
     PTR = COUNT(OR.TYPE,VM) + (OR.TYPE # "") + 1
     OR.TR.DATE<1,PTR> = MIV.DATE
     OR.TYPE<1,PTR> = "M"
     OR.INV.AMT<1,PTR> = MSC.AMT
     OR.CHECK<1,PTR> = ""
     OR.CHK.DATE<1,PTR> = ""
     OR.CHK.AMT<1,PTR> = ""
   END
   MATWRITE MIV.REC ON MANUAL.INVOICE, MIV.ID
   TAG = ''
   WRITE TAG ON MANUAL.INVOICE.TAG, MIV.ID
   MATWRITE CUST.REC ON CUSTOMER, CONO:MIV.CUST.NO
   MATWRITE OR.REC ON OPEN.RECV, MIV.ID
   DELETE DAILY.MANUAL.INVOICE, MIV.ID
1999*
   RELEASE
RETURN
*
*--- CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC) ; RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
GOTO 99990
*
99990 *
ECD.ACTION = 99 ; CALL SCRN.EDIT
*
999999*
   RELEASE
   END
