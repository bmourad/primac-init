***************************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM    - PRIMAC
* SOURCE    - BP
* PROGRAM   - MISC.CASH.REC.MAINT
* BY        - JIHAD YAMOUT ,C.B.A
* DATE      - 09/04/84
* DESCTIPTION  - THIS PROGRAM FOR INPUTING MISC CASH RECEIPTS INTO THE
*                SYSTEM .
*
* LAST COMPILE - 1009
*
* TASK 19406 Bob Sherer 10/16/95 - added code to divisionalize by putting
* the owning division number in the daily.cash and cash.hist records.
*T21876 diane 05/05/1997 * Use Gen.xref
*T23272 gat 09/17/1998 * FIX XREF PROBLEM
*T23278 markt 10/13/1998 * Add division security check
*T23778 cm 03/17/1999 * NOTIFY IF REC DATE IS +/- 30 DAYS - ALLOW OVERRIDE
*T25978 adelgado 02/06/2002 * Add the use of prompts (S,SR,SB,ST)
*T27624 lross 08/12/2003 * Fix dept lookup.
*ENDDOC
***************************************************************************
*COPY>CPYLIB>COM1
*COPY>ARS.CPYLIB>DAILY.CASH
*COPY>ARS.CPYLIB>CASH.HIST
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COMPANY
**COPY>CPYLIB>GEN.XREF  ;* T21876 *T27624
*COPY>CPYLIB>GEN.XREF.SUB ;*T27624
*COPY>CPYLIB>XREF.DATA ;*T27624
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
***** SETUP ERRMSG ROUTINE
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
**** OPEN FILES
  OPEN '','DAILY.CASH' TO DAILY.CASH ELSE ERRMSG = "DAILY.CASH FILE IS MISSING" ; GOTO 93000
  OPEN '','DAILY.CASH.TAG' TO DAILY.CASH.TAG ELSE ERRMSG = "DAILY.CASH.TAG FILE IS MISSING" ; GOTO 93000
  OPEN '','CASH.HIST' TO CASH.HIST ELSE ERRMSG = "CASH.HIST FILE IS MISSING" ; GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE IS MISSING" ; GOTO 93000
  OPEN '','PREFIX' TO PREFIX ELSE ERRMSG = "PREFIX FILE IS MISSING" ; GOTO 93000  ;* T21876
  OPEN '','ARS.SCREENS' TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE IS MISSING" ; GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY FILE IS MISSING" ; GOTO 93000
  OPEN '','DIVISION' TO DIVISION ELSE ERRMSG = "DIVISION FILE IS MISSING" ; GOTO 93000
  OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG = "DEPARTMENT FILE IS MISSING" ; GOTO 93000
  OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG = "COST.CNTR FILE IS MISSING" ; GOTO 93000
  OPEN '','XREF.DATA' TO XREF.DATA ELSE ERRMSG = "XREF.DATA FILE IS MISSING" ; GOTO 93000 ;*T27624
**** GET CONO
  CONO = '' ; MAT COMP.REC = '' ; FND = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
  IF CO.GLS = "Y" THEN
    OPEN '','COA' TO COA ELSE ERRMSG = 'COA FILE IS MISSING' ; GOTO 93000
  END
**** GET PERIOD
***** T23278 v
  READ DIVISION.REC FROM CONTROL, CONO:"DIVISIONS" ELSE
    ERRMSG = "DIVISIONS CONTROL RECORD IS MISSING"
    GOTO 93000
  END
  READ SECURITY.REC FROM CONTROL, CONO:"DIV.SECURITY" ELSE
    ERRMSG = "DIV.SECURITY CONTROL RECORD IS MISSING"
    GOTO 93000
  END
*
***** T23278 ^
  READ ARCMON FROM CONTROL, CONO:"ARCFISCAL" ELSE
    ERRMSG = "ARCFISCAL RECORD IS MISSING FROM CONTROL"; GOTO 93000
  END
*   PERIOD = ARCMON<1>;* T23278
  ACCT.LEN = LEN(CO.ACCT.PIC)
**** INT
  BEGIN.PAGE = 9 ; PAGE.SIZE = 11 ; LINE.SPACE = 1 
  LINE.SP = 1; PAGE.S = 11
  DS.X = 19
  DV.X = 51
  DP.X = 55
  CC.X = 59
  PP.NO = 12
  UNKNOWN = "NOT ON FILE"
  SP14 = SPACE(14)
  GEN.DIV = "00"
  GEN.DEPT = "00"
  GEN.CCTR = "000"
  MAT GEN.XREF.REC = ""  ;* T21876
  GXR.CO = CONO          ;* T21876
* TYP = 0 ; CALL EDIT.SUB
  FILL = '#'
  SI = (-1)
**** MAIN PROCESSING
  MAT EDIT.COM = ""; *23272
  MAT EDIT.COM.DRIVER = ''
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "MISC.CASH.REC.MAINT"
  ECD.ACTION=1;CALL SCRN.EDIT
**** PRINT SCREEN
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION=2; CALL SCRN.EDIT
  MATREAD GLTABLE.REC FROM CONTROL, CONO : "GLTABLE" ELSE MAT GLTABLE.REC = ""
**** CONTROL SUB
80*
  MAT CHR.REC = '' ; MAT DC.REC = ''
  MAT COA.REC = ''
  LINES = 0 ; OLD.START = 0
  APPLY.AMT = 0 ; CH.AMT = 0
  DESC = ""
  MAT SCV.REC = ""; ECD.ACTION = 6; CALL SCRN.EDIT
  FOR I = 1 TO 7
    ON I GOSUB 100,200,300,400,500,600
    IF ECD.RET.VALUE = "END" AND I = 1 THEN GOTO 99999 
    IF ECD.RET.VALUE = "END" AND I > 1 THEN GOTO 80
  NEXT I
  GOSUB 9000 ; GOTO 80
**** ENTER GL ACCOUNT NO AND PRINT DESCRIPTION
100*
  IF CO.GLS = "Y" THEN
    ECD.NUM = 1; ECD.MAXL = ACCT.LEN; ECD.DEFAULT = GLTB.CASH
    ECD.ACTION = 4 ; CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      MATREAD COA.REC FROM COA, CONO : ECD.RET.VALUE ELSE
        ERRMSG = "GL ACCOUNT ":ECD.RET.VALUE:" IS NOT ON FILE"; GOSUB 91000; GOTO 100
      END
      AR.ACCT = ECD.RET.VALUE
      ECD.NUM = 1; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = AR.ACCT CO.ACCT.MASK
      ECD.ACTION = 5; CALL SCRN.EDIT
      ECD.NUM = 2; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = COA.DESC
      ECD.ACTION = 5; CALL SCRN.EDIT
    END
  END ELSE
    AR.ACCT = GLTB.CASH
    MAT COA.REC = ""
    COA.DESC = "CASH ACCOUNT"
    ECD.NUM = 1; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = AR.ACCT CO.ACCT.MASK
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 2; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = COA.DESC
    ECD.ACTION = 5; CALL SCRN.EDIT
  END
  RETURN
**** ENTER OWNING DIVISION NUMBER  *** TASK 19405
* NO OWNING DIVISION PER SARA 3/20/96
*150*
*    ECD.NUM = 13; ECD.VALDAT.FILE = DIVISION; ECD.VALDAT.CODE = '2'
*    ECD.PREFIX.ID = CONO; ECD.ACTION = 4; CALL SCRN.EDIT
*    IF ECD.RET.VALUE # 'END' THEN
*       IF ECD.VALDAT.ITEM = "" THEN GOTO 150
*    END
*    DIV.OWNER = ECD.RET.VALUE
*    RETURN
**** ENTER MISC DESC
200*
  ECD.NUM = 3 ; ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    MISC.CODE = "MISC" ; MISC.DESC = ECD.RET.VALUE
  END
  RETURN
**** ENTER REC DATE
300*
  ECD.NUM = 5 ; ECD.ACTION = 4 ; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    REC.DATE = ECD.RET.VALUE
*** T23778 v ***
    BEGIN CASE
      CASE REC.DATE GT DATE()+31
        CHECK.DATE=1
      CASE REC.DATE LT DATE()-31
        CHECK.DATE=1
      CASE 1
        CHECK.DATE=''
    END CASE
301 *
    IF CHECK.DATE THEN
      X=0;Y=23;TYP=18;MINL=1;MAXL=1
      PMSG='THE DATE ':OCONV(REC.DATE,'D4/'):' IS OUT OF RANGE, ACCEPT (Y/N) '
      CALL EDIT.SUB
      P_X=0;P_Y=23;P_VALUE='';P_OPT='CL'
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      IF VALUE='Y' OR VALUE='N' THEN NULL ELSE GO 301
      IF VALUE='N' THEN REC.DATE='';GO 300
      IF VALUE='Y' THEN NULL
    END
*** T23778 ^ ***
  END 
  RETURN
**** ENTER CHECK.NUM
400*
  ECD.NUM = 6 ;ECD.ACTION = 4 ; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    CH.NUM = ECD.RET.VALUE
    IF CH.NUM = '' THEN CH.NUM = 000000
  END
  RETURN
**** ENTER CASH AMOUNT TO BE APPLY
500*
  ECD.NUM = 8 ; ECD.ACTION = 4 ; CALL SCRN.EDIT
  IF ECD.RET.VALUE # "END" THEN
    CH.AMT = ECD.RET.VALUE
    GOSUB 800
  END
  ECD.NUM =10;SCV.REC(ECD.NUM)<1> = APPLY.AMT;ECD.ACTION=5;CALL SCRN.EDIT
  RETURN
**** VALDAT CASH REC RECORD
600*
  READU REF.NUM FROM CONTROL , CONO:"CRREF" ELSE REF.NUM = 800000
  IF REF.NUM > 999999 THEN REF.NUM = 800000
  WRITE REF.NUM+1 ON CONTROL , CONO:"CRREF"
  DC.KEY = MISC.CODE:"!":REC.DATE:"!":REF.NUM
  MATREAD DC.REC FROM DAILY.CASH , CONO:DC.KEY ELSE
    FND = '0';MAT DC.REC = ''
  END
  IF DC.CASH = '' THEN
    IF CH.AMT * 1 # 0 THEN
      LOOP 
        OPTION = "A"
        LN = LINES + 1
        OLD.LINES = LINES
        GOSUB 10000
      WHILE LINES > OLD.LINES AND APPLY.AMT # 0 DO REPEAT
      LN = LINES
    END ELSE
      LN = 1
    END
  END ELSE
    GOTO 600
  END
  GOSUB 80000
  GOSUB 9000
  GOTO 80
  RETURN
**** PRINT OPEN BAL
800*
  TOT.CNT = COUNT(DC.AMT,VM) + (DC.AMT # '')
  APPLY.AMT = CH.AMT
  FOR SS = 1 TO TOT.CNT
    APPLY.AMT = APPLY.AMT + DC.AMT<1,SS>
  NEXT SS
  ECD.NUM = 10 ; ECD.ACTION = 5 ; SCV.REC(ECD.NUM)<1> = APPLY.AMT ; CALL SCRN.EDIT
  RETURN
**** ENTER OPTION
9000*
  MORE = 1
  LOOP
    ECD.NUM = 11 ;ECD.ACTION = 4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'E' OR OPTION = 'END'
        MORE = 0
      CASE OPTION = 'A'
        IF APPLY.AMT # 0 THEN
          LOOP
            LN = LINES + 1
            OLD.LINES = LINES
            GOSUB 10000
          WHILE LINES > OLD.LINES AND APPLY.AMT # 0 DO REPEAT
          LN = LINES
          GOSUB 80000
        END ELSE
          ERRMSG = "CAN'T APPLY CASH WHEN CHECK BALANCE IS EQUAL TO ZERO"
          GOSUB 91000 
        END
      CASE OPTION = 'C'
        GOSUB 60000
        IF LNO THEN
          LN = LNO
          SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          GOSUB 10100
          GOSUB 800
        END
        GOSUB 80000
      CASE OPTION = 'D'
        GOSUB 60000
        IF LNO THEN
          LN = LNO
          DC.CASH.APPL = DELETE(DC.CASH.APPL,1,LN,0)
          DC.AMT = DELETE(DC.AMT,1,LN,0)
          DC.ACCT = DELETE(DC.ACCT,1,LN,0)
          DC.DIV = DELETE(DC.DIV,1,LN,0)
          DC.DEPT = DELETE(DC.DEPT,1,LN,0)
          DC.CCTR = DELETE(DC.CCTR,1,LN,0)
          DESC = DELETE(DESC,LN,0,0)
          LINES = COUNT(DC.AMT, VM) + (DC.AMT # "")
          IF LN > 1 AND LN > LINES THEN LN = LN - 1
          IF LN < 1 THEN LN = 1
          OLD.START = 0
          GOSUB 800
        END
        GOSUB 80000
      CASE OPTION = 'CC'
        GOSUB 500
      CASE OPTION = 'S'
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 80000
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 80000
      CASE OPTION = 'ST'
        LN = 1
        GOSUB 80000
      CASE OPTION = 'SB'
        LN = LINES
        GOSUB 80000
      * T25978 ^
      CASE OPTION = 'F'
        IF APPLY.AMT = 0 AND CH.AMT # 0 THEN
          DC.CASH = CH.AMT
          DC.INVOICE = MISC.CODE
          DC.DESC = MISC.DESC
          DC.BANK.ACCT = AR.ACCT
          DC.CHECK = CH.NUM
*TASK 19405
*          DC.DIV.OWNER = DIV.OWNER
*
          MATWRITE DC.REC ON DAILY.CASH , CONO:DC.KEY
          TAG = ''
          WRITE TAG ON DAILY.CASH.TAG , CONO:DC.KEY
          CHR.CUST = MISC.CODE
          CHR.CASH = DC.CASH
          CHR.INVOICE = DC.INVOICE
          CHR.CASH.APPL = DC.CASH.APPL
          CHR.GL.AMT = DC.AMT
          CHR.GL.NO = DC.ACCT
          CHR.MON = PERIOD
          CHR.MON = PERIOD
          CHR.DIV = DC.DIV
          CHR.DEPT = DC.DEPT
          CHR.CCTR = DC.CCTR
          CHR.MISC.DESC = DC.DESC
          CHR.BANK.NO  = DC.BANK.ACCT
          CH.NUM = STR("0",6-LEN(CH.NUM)):CH.NUM
*          CHR.DIV.OWNER = DC.DIV.OWNER
          MATWRITE CHR.REC ON CASH.HIST , CONO:MISC.CODE:"!":REC.DATE:"!":CH.NUM:"!":REF.NUM
          MORE = 0
        END ELSE
          ERRMSG = "CAN'T FILE WHEN BALANCE IS NOT EQUAL TO ZERO";GOSUB 91000
        END
    END CASE
  WHILE MORE = 1 DO REPEAT
  RETURN
*
*--- LINE ENTRY
*
10000*
  GOSUB 80000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LINES = LINES + 1
**** ENTER ACCOUNT
10100*
  P_X  = 4 ; P_Y = SLN ; P_VALUE = SP14 ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  X = 4; Y = SLN; TYP = 3; O.R = "R"
  MAXL = ACCT.LEN; FILL.CHR = 0; JUSTIFY = "L"
  IF DC.ACCT<1,LN> # "" THEN
    DEFAULT = DC.ACCT<1,LN>
    O.R = "O"
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN
    IF OPTION = "A" THEN
      DC.CASH.APPL = DELETE(DC.CASH.APPL,1,LN,0)
      DC.AMT = DELETE(DC.AMT,1,LN,0)
      DC.ACCT = DELETE(DC.ACCT,1,LN,0)
      DC.DIV = DELETE(DC.DIV,1,LN,0)
      DC.DEPT = DELETE(DC.DEPT,1,LN,0)
      DC.CCTR = DELETE(DC.CCTR,1,LN,0)
      DESC = DELETE(DESC,LN,0,0)
    END
    OLD.START = 0
    GOTO 19999
  END
  IF CO.GLS = "Y" THEN
    MATREAD COA.REC FROM COA , CONO : VALUE ELSE
      ERRMSG = "ACCOUNT ":VALUE:" IS NOT ON FILE"; GOSUB 91000;GOTO 10100
    END
  END ELSE
    MAT COA.REC = ""
    COA.DESC = "MISCELLANEOUS ACCOUNT"
    COA.LEVEL = 3
  END
  DC.ACCT<1,LN> = VALUE
  DESC<LN> = COA.DESC
  P_X  = 4 ; P_Y = SLN ; P_VALUE = VALUE CO.ACCT.MASK ; P_OPT = ""
  P_X  := AM:DS.X ; P_Y := AM:SLN ; P_VALUE := AM:COA.DESC "L#30"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  DV.NO = DC.DIV<1,LN>
  DP.NO = DC.DEPT<1,LN>
  CC.NO = DC.CCTR<1,LN>
*COPY>PMCBP>DV.DP.CC.MAINT
*
10500*
***** T23278 v
  DIV.CODE = DV.NO
  IF SECURITY.REC<1> = "Y" AND SECURITY.REC<2> = "Y" THEN
    IF DIV.CODE = "00" OR DIV.CODE = "ALL" THEN
      ERRMSG = " 'ALL' OR '00' INVALID WHEN DIVISION-LEVEL POSTING & CLOSING IS SELECTED"
      GOSUB 91000; GOTO 99999
    END
    LOCATE DIV.CODE IN DIVISION.REC<1>,1 SETTING POS ELSE
      ERRMSG = "Division ":DIV.CODE:" not found in DIVISIONS Control Record"
      GOTO 93000
    END
  END ELSE
    POS = 1
  END

  PERIOD = ARCMON<1,POS>;* T23278
***** T23278 ^
  DC.DIV<1,LN> = DV.NO
  DC.DEPT<1,LN> = DP.NO
  DC.CCTR<1,LN> = CC.NO
*
*--- GET MISC AMOUNT
*
10700*
  X = 64; Y = SLN; MAXL = 10; TYP = 4; SCALER=2; O.R = "O"
  IF DC.AMT<1,LN> = "" THEN
    DEFAULT = OCONV(APPLY.AMT , "MD2")
  END ELSE
    DEFAULT = OCONV(DC.AMT<1,LN> * SI , "MD2")
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  DC.AMT<1,LN> = VALUE * SI
  DC.CASH.APPL<1,LN> = VALUE
19999*
  GOSUB 800
  LINES = COUNT(DC.ACCT,VM) + (DC.ACCT # "")
  RETURN
*****ENTER LINE NUMBER
60000**
  GOSUB 80000
  ECD.NUM = 17
  ECD.MINV = OLD.START
  IF LAST.LINE < LINES THEN
    ECD.MAXV = LAST.LINE
  END ELSE
    ECD.MAXV = LINES
  END
  ECD.ACTION = 4; CALL SCRN.EDIT
  LNO = ECD.RET.VALUE
  IF LNO = '' OR LNO = "END" THEN LNO = 0
  RETURN
80000**
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF START.LINE = OLD.START THEN GOTO 10001
  OLD.START = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
    S.LN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = S.LN ; P_VALUE = "" ; P_OPT = "CL"
    P_X  := AM:0 ; P_Y := AM:S.LN ; P_VALUE := AM:N "R#3"
    P_X  := AM:4 ; P_Y := AM:S.LN ; P_VALUE := AM:DC.ACCT<1,N> CO.ACCT.MASK
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    IF DESC<N> = "" THEN
      IF CO.GLS = "Y" THEN
        MATREAD COA.REC FROM COA, CONO : DC.ACCT<1,N> ELSE COA.DESC = UNKNOWN
      END ELSE
        COA.DESC = "MISCELLANEOUS ACCOUNT"
      END
      DESC<N> = COA.DESC
    END
    P_X  = DS.X ; P_Y = S.LN ; P_VALUE = DESC<N> "L#30" ; P_OPT = ""
    P_X  := AM:DV.X ; P_Y := AM:S.LN ; P_VALUE := AM:DC.DIV<1,N> "L#2"
    P_X  := AM:DP.X ; P_Y := AM:S.LN ; P_VALUE := AM:DC.DEPT<1,N> "L#2"
    P_X  := AM:CC.X ; P_Y := AM:S.LN ; P_VALUE := AM:DC.CCTR<1,N> "L#3"
    P_X  := AM:64 ; P_Y := AM:S.LN ; P_VALUE := AM:OCONV(DC.AMT<1,N> * SI , "MD2") "R#10"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    S.LN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = S.LN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
10001**
  RETURN
*
*--- SCROLL XREF
*
*T21876 *COPY>PMCBP>DV.DP.CC.SCROLL
*****CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
