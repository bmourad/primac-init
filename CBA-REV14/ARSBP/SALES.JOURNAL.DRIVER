********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM     - SALES.JOURNAL.DRIVER
* SYSTEM      - ARSBP
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 10/11/85
* DESCRIPTION -
* This program creates a temporary file for Invoices/Manual Invoices
* to run for SALES.JOURNAL.RPT program.
*T25310 gat 07/07/2000 * FIX DIVISION ON ORDER.INVOICE
*ENDDOC
********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>ARS.CPYLIB>MANUAL.INVOICE
*COPY>ARS.CPYLIB>SALES.JOURNAL.TEMP
*COPY>JCS.CPYLIB>JOB
*COPY>OPS.CPYLIB>ORDER
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
SYS.TYPE=1
CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
PROCREAD XX ELSE
  ERRMSG="MUST BE RUN FROM A PROC"; GOTO 93000
END
CONO = XX<1>
PERIOD = XX<3>
FILE.FLAG = XX<6>
BEGIN CASE
CASE FILE.FLAG = "J"
  OPEN '','INVOICE' TO INVOICE ELSE
    ERRMSG = 'CANNOT LOCATE INVOICE FILE'; GOTO 93000
  END
  OPEN '','JOB' TO JOB ELSE
    ERRMSG = 'CANNOT LOCATE JOB FILE'; GOTO 93000
  END
CASE FILE.FLAG = "O"
  OPEN '','ORDER.INVOICE' TO INVOICE ELSE
    ERRMSG = 'CANNOT LOCATE ORDER.INVOICE FILE'; GOTO 93000
  END
  OPEN "","ORDER" TO ORDER ELSE
    ERRMSG = 'CANNOT LOCATE ORDER FILE'; GOTO 93000
  END
CASE FILE.FLAG = "M"
  OPEN '','MANUAL.INVOICE' TO INVOICE ELSE
    ERRMSG = 'CANNOT LOCATE MANUAL.INVOICE FILE'; GOTO 93000
  END
CASE 1
  ERRMSG = "MUST BE A (J)OB, (O)RDER, OR (M)ANUAL INVOICE"; GOTO 93000
END CASE
*
*---- OPEN FILES
*
OPEN '','SALES.JOURNAL.TEMP' TO SALES.JOURNAL.TEMP ELSE ERRMSG='SALES.JOURNAL.TEMP FILE MISSING';GOTO 93000
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
*
*---- MAIN PROCESSING
*
READ ITEM FROM CONTROL,CONO:"WEB.CONTROL.NUMBER" ELSE ITEM=""
WEB.NO = ITEM<1>
OFF.NO = ITEM<2>
SHT.NO = ITEM<3>
WEB.CNT = COUNT(WEB.NO,VM) + (WEB.NO # '')
WEB.LEN = ''
FOR K = 1 TO WEB.CNT
  WEB.LEN<K> = LEN(WEB.NO<1,K>)
NEXT K
SHT.CNT = COUNT(SHT.NO,VM) + (SHT.NO # '')
SHT.LEN = ''
FOR K = 1 TO SHT.CNT
  SHT.LEN<K> = LEN(SHT.NO<1,K>)
NEXT K
DATA = 1
MAT SJT.REC = ''
LOOP
  READNEXT MIV.ID ELSE DATA = 0
WHILE DATA DO
  MATREAD MIV.REC FROM INVOICE, MIV.ID ELSE
    ERRMSG = 'CANNOT LOCATE INVOICE - ' : MIV.ID[3,99]
    GOSUB 91000; GOTO 999
  END
  SFX =  MIV.ID[LEN(MIV.ID)-1,2]
  ALL.JOB = ""
  ALL.SCD = ""
  CHG.JOBS = ""
  BEGIN CASE
  CASE FILE.FLAG = "J"
    IF MIV.ID[LEN(MIV.ID)-1,2] = "PB" THEN
      IF MIV.DEPT<1,1> # PERIOD THEN GOTO 999
    END
    MATREAD JOB.REC FROM JOB, CONO : MIV.JOB.NO ELSE
      ERRMSG = 'CANNOT LOCATE JOB - ' : MIV.JOB.NO
      GOSUB 91000; GOTO 999
    END
    SJT.DIV = JOB.DIV
    SJT.JOB = MIV.JOB.NO
    SJT.SLSM = JOB.SLSMN
*          SALES.CODE = JOB.SALES.CODE
    ALL.JOB = MIV.JOB.NO
    ALL.SCD = JOB.SALES.CODE
    CHG.JOBS = MIV.SLSMAN
  CASE FILE.FLAG = "O"
    MATREAD ORD.REC FROM ORDER, CONO:MIV.JOB.NO ELSE
      ERRMSG = "Cannot loacte order # ":MIV.JOB.NO
      GOSUB 91000; GOTO 999
    END
*T25310
*    SJT.DIV = "00"
     IF ORD.DIV = "" THEN 
        SJT.DIV = "00" 
     END ELSE
        SJT.DIV = ORD.DIV
     END
*T25310
    SJT.JOB = MIV.JOB.NO
    SJT.SLSM = ORD.SLSMN
    ALL.JOB = MIV.JOB.NO
    ALL.SCD = "XXX"
    CHG.JOBS = MIV.SLSMAN
  CASE 1
    SJT.DIV = STR("0",2-LEN(MIV.DIV)) : MIV.DIV
    SJT.JOB = MIV.ID[4,99]
    SJT.SLSM = MIV.SLSMAN
*          SALES.CODE = 'XXX'
    IF MIV.SALES.CODE<1,1> = "" THEN
      ALL.SCD = "XXX"
    END ELSE
      ALL.SCD = MIV.SALES.CODE<1,1>
    END
  END CASE
  SJT.CUST = MIV.CUST.NO
  LOCATE SJT.SLSM IN OFF.NO<1>,1 SETTING FND ELSE FND = 0
  IF FND THEN SJT.SSLSM = 'ZZZ' ELSE SJT.SSLSM = '000'
  CODE.CNT = COUNT(MIV.CHG.CODE,VM) + (MIV.CHG.CODE # "")
  SJT.WEB = 0; SJT.SHEET = 0; SJT.OTHER = 0
  SJT.AGENCY = 0; SJT.POSTAGE = 0; SJT.TAX = 0
  FOR I = 1 TO CODE.CNT
    CATG = MIV.CHG.CAT<1,I>
    IF FILE.FLAG = "O" AND SFX = "CM" THEN
      AMT = 0 - MIV.AMOUNT<1,I>
    END ELSE
      AMT = MIV.AMOUNT<1,I>
    END
    BEGIN CASE
    CASE CATG = "OTH" OR CATG = "DSC" OR CATG = "MSC"
      BEGIN CASE
      CASE FILE.FLAG = "M"
        IF CATG = "DSC" OR CATG = "MSC" THEN
          SALES.CODE = MIV.TAX.JURS<1,I>
        END ELSE
*                      SALES.CODE = "XXX"
          SALES.CODE = ALL.SCD<1>
        END
      CASE FILE.FLAG = "J"
        IF CATG = "DSC" OR CATG = "MSC" THEN
          SALES.CODE = MIV.TAX.JURS<1,I>
        END ELSE
          CHARG.JOB = CHG.JOBS<1,I>
          IF CHARG.JOB = "" THEN CHARG.JOB = MIV.JOB.NO
          LOCATE CHARG.JOB IN ALL.JOB,1 SETTING JFND ELSE
            MATREAD JOB.REC FROM JOB, CONO:CHARG.JOB ELSE
              MAT JOB.REC = ""
              JOB.SALES.CODE = ALL.SCD<1>
            END
            ALL.JOB<JFND> = CHARG.JOB
            ALL.SCD<JFND> = JOB.SALES.CODE
          END
          SALES.CODE = ALL.SCD<JFND>
        END
      CASE 1
        IF CATG = "DSC" OR CATG = "MSC" OR CATG = "OTH" THEN
          SALES.CODE = MIV.TAX.JURS<1,I>
        END ELSE
*                      SALES.CODE = "XXX"
          SALES.CODE = ALL.SCD<1>
        END
      END CASE
      SC = 0
      FOR K = 1 TO WEB.CNT WHILE SC = 0
        IF SALES.CODE[1,WEB.LEN<K>] = WEB.NO<1,K> THEN SC = K
      NEXT K
      IF SC THEN
        SJT.WEB = SJT.WEB + AMT
      END ELSE
        FOR K = 1 TO SHT.CNT WHILE SC = 0
          IF SALES.CODE[1,SHT.LEN<K>] = SHT.NO<1,K> THEN SC = K
        NEXT K
        IF SC THEN
          SJT.SHEET = SJT.SHEET + AMT
        END ELSE
          SJT.OTHER = SJT.OTHER + AMT
        END
      END
    CASE CATG = "SHP"
      SJT.POSTAGE = SJT.POSTAGE + AMT
    CASE CATG = "TAX"
      SJT.TAX = SJT.TAX + AMT
    CASE CATG = "AGC"
      SJT.AGENCY = SJT.AGENCY + AMT
    END CASE
  NEXT I
  SJT.NET = SJT.SHEET + SJT.WEB + SJT.OTHER
  SJT.GROSS = SJT.NET + SJT.AGENCY + SJT.POSTAGE
  SJT.GRAND = SJT.GROSS + SJT.TAX
  MATWRITE SJT.REC ON SALES.JOURNAL.TEMP, MIV.ID : '*' : FILE.FLAG
999*
REPEAT
GOTO 99999
*
*---- CALLS FOR SYSCOM
*
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
99999*
END
