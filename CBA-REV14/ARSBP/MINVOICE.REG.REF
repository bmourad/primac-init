***************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM       - MINVOICE.REG.REF
* SYSTEM        - ARSBP
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE          - 07/01/85
* DESCRIPTION   -
* This program produces an Invoice Register sorted by reference
* number for MANUAL.INVOICE.
*T26493 cmykleb 04/30/2002 * Change rpt to use GET.PROG.HEAD and get the
*                            rpt # from the proc.
*T27364 thompson 04/03/2003 * FIX PAGE COUNT PROBLEM
***************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>ARS.CPYLIB>MANUAL.INVOICE
*COPY>PMC.CPYLIB>CUSTOMER
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- PROCREAD
*
   PROCREAD XX ELSE
      ERRMSG="MUST BE RUN FROM A PROC"
      GOSUB 91000
      GOTO 99999
   END
   CONO = XX<1>
   CO.NAME = XX<2>
   PERIOD=XX<3>
   PERIOD = PERIOD[5,2]
   FROM.DATE = XX<4>
   TO.DATE = XX<5>
   INV.ZER = XX<6>
   IF ICONV(FROM.DATE,"D2/") = ICONV(TO.DATE,"D2/") THEN
      DATE.TITLE = SPACE(60):"FOR ":FROM.DATE
   END ELSE
      DATE.TITLE = SPACE(54):"FROM ":FROM.DATE:" TO ":TO.DATE
   END
*
*T26493 v
   CO.NAME = ""
   RPT.NAME = ""
   RPT.NO = XX<2>
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,RPT.NAME,RPT.NO,"",HD1,HD2)
*T26493 ^
*---- DIMENTION VARIABLES
*
   DIM TOTAL.REC(5)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','MANUAL.INVOICE' TO MANUAL.INVOICE ELSE ERRMSG='MANUAL.INVOICE FILE MISSING';GOTO 93000
   OPEN '','CUSTOMER' TO CUSTOMER ELSE ERRMSG='CUSTOMER FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
   PG.NO=0
   LINE.CNT=0
   ERRMSG=""
   UNKNOWN=STR("?",30)
   DASH=STR("-",30)
   EQL=STR("=",15)
   SP=" ";SP2="  "
   MAT TOTAL.REC=""
   NODATA=0
   PRINTER ON
*
*---- MAIN PROCESSING
*
   GOSUB 20
10 *
   MAT MIV.REC = ""
   SALES=0;AGENCY=0;FREIGHT=0;TAX=0;TOTAL=0
   READNEXT MIV.KEY ELSE 
      IF NODATA=0 THEN GOTO 99999
      GOTO 15
   END
   NODATA=1
   MATREAD MIV.REC FROM MANUAL.INVOICE, MIV.KEY ELSE GOTO 10
   MATREAD CUST.REC FROM CUSTOMER, CONO:MIV.CUST.NO ELSE
      MAT CUST.REC = ""
   END
   IF CUST.NAME # "" AND MIV.CUST.NAME # CUST.NAME THEN
      MIV.CUST.NAME = CUST.NAME
   END
   SLS.NO=MIV.SLSMAN
   NUMBER=MIV.PO
   MINVOICE.NO=MIV.KEY[4,8]
   MINVOICE.DATE=OCONV(MIV.DATE,"D2/")
   CODE.CNT=COUNT(MIV.CHG.CODE,VM) + (MIV.CHG.CODE # "")
   IF CODE.CNT=0 OR CODE.CNT="" THEN GOTO 10
   FOR I=1 TO CODE.CNT
      IF MIV.CHG.CODE<1,I> = "SUB" OR MIV.CHG.CODE<1,I> = "TOT" OR MIV.CHG.CODE<1,I> = "SUBT" OR MIV.CHG.CAT<1,I> = "CMT" THEN GOTO 14
      BEGIN CASE
         CASE MIV.CHG.CAT<1,I> = "SHP"
            FREIGHT = FREIGHT + MIV.AMOUNT<1,I>
         CASE MIV.CHG.CAT<1,I> = "TAX"
            TAX = TAX + MIV.AMOUNT<1,I>
         CASE MIV.CHG.CAT<1,I> = "AGC"
            AGENCY = AGENCY + MIV.AMOUNT<1,I>
            SALES = SALES + MIV.AMOUNT<1,I>
         CASE MIV.CHG.CAT<1,I> = "OTH"
            SALES = SALES + MIV.AMOUNT<1,I>
         CASE MIV.CHG.CAT<1,I> = "MSC"
            SALES = SALES + MIV.AMOUNT<1,I>
         CASE MIV.CHG.CAT<1,I> = "DSC"
            SALES = SALES + MIV.AMOUNT<1,I>
      END CASE
14 *
   NEXT I
   TOTAL=SALES + FREIGHT + TAX
   IF SALES=0 THEN SALES=""
   IF AGENCY=0 THEN AGENCY=""
   IF FREIGHT=0 THEN FREIGHT=""
   IF TAX=0 THEN TAX=""
   IF TOTAL=0 THEN TOTAL=""
   IF INV.ZER = "N" THEN
      IF SALES="" AND AGENCY="" AND FREIGHT="" AND TAX="" AND TOTAL="" THEN GOTO 10
   END
   GOSUB 30
   GOTO 10
15 *
   GOSUB 50
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20 *
   PRINT CHAR(12)
   LINE.CNT=0
   PG.NO=PG.NO + 1
*T26493 v
*  H.LINE="";HLINE1="";HLINE2="";HLINE3=""
*  HLINE1="RUN DATE : ":OCONV(DATE(),"D2/")"R#8":SPACE(22)
*  N=SPACE((50 - LEN(CO.NAME)) / 2)
*  HLINE2=N:CO.NAME:N
*  HLINE3=SPACE(20):"PAGE ":PG.NO
*  H.LINE=HLINE1:HLINE2:HLINE3
*  PRINT H.LINE
*  H.LINE=""
*  H.LINE=H.LINE:SPACE(48):"MANUAL INVOICE REGISTER BY REFERENCE":SPACE(13):"PERIOD ":PERIOD
*  PRINT H.LINE
*  H.LINE=""
*  H.LINE=DATE.TITLE
*  PRINT H.LINE
*T27364   HD1 = HD1:PG.NO
   PRINT HD1:PG.NO "R#3" ; *T27364
   PRINT HD2
   PRINT "PERIOD ":PERIOD"L#2":DATE.TITLE
*T26493 ^
   PRINT
   H.LINE=""
   H.LINE=" REF #    REF DATE  CUST #           CUSTOMER              SLS   JOB/PO       SALES      AGENCY     FREIGHT      TAX        TOTAL"
   PRINT H.LINE
   H.LINE=""
   H.LINE=DASH[1,8]:SP2:DASH[1,8]:SP2:DASH[1,6]:SP2:DASH:' --- ':DASH[1,10]:SP:DASH[1,12]:SP:DASH[1,10]:SP:DASH[1,10]:SP:DASH[1,10]:SP:DASH[1,12]
   PRINT H.LINE
   PRINT
   LINE.CNT=LINE.CNT + 7
   RETURN
*
*---- PRINT THE VALUES
*
30 *
   GOSUB 40
   IF LINE.CNT >= 52 THEN GOSUB 20
   P.LINE=""
   P.LINE=MINVOICE.NO "L#8":SP2:MINVOICE.DATE "L#8":SP2:MIV.CUST.NO "L#6":SP2:MIV.CUST.NAME "L#30":SP:SLS.NO "L#3":SP:NUMBER "L#10":SP:SALES "R#12":SP:AGENCY "R#10":SP:FREIGHT "R#10":SP:TAX "R#10":SP:TOTAL "R#12"
   PRINT P.LINE
   LINE.CNT=LINE.CNT + 1
   RETURN
*
*---- CALCULATE FIELD VALUES
*
40 *
   TOTAL.REC(1)=TOTAL.REC(1) + SALES
   TOTAL.REC(2)=TOTAL.REC(2) + AGENCY
   TOTAL.REC(3)=TOTAL.REC(3) + FREIGHT
   TOTAL.REC(4)=TOTAL.REC(4) + TAX
   TOTAL.REC(5)=TOTAL.REC(5) + TOTAL
   SALES=OCONV(SALES,"MD2,")
   AGENCY=OCONV(AGENCY,"MD2,")
   FREIGHT=OCONV(FREIGHT,"MD2,")
   TAX=OCONV(TAX,"MD2,")
   TOTAL=OCONV(TOTAL,"MD2,")
   RETURN
*
*---- PRINT GRAND TOTALS
*
50 *
   GOSUB 60
   G.LINE=""
   G.LINE=SPACE(74):EQL[1,12]:SP:EQL[1,10]:SP:EQL[1,10]:SP:EQL[1,10]:SP:EQL[1,12]
   PRINT G.LINE
   G.LINE=""
   G.LINE="*** GRAND TOTALS ***":SPACE(54):TOT.SALES "R#12":SP:TOT.AGENCY "R#10":SP:TOT.FREIGHT "R#10":SP:TOT.TAX "R#10":SP:TOT.TOTAL "R#12"
   PRINT G.LINE
*      MAT CURTOTAL.REC = ""
   RETURN
*
*---- VALUES FOR GRAND TOTALS
*
60 *
   TOT.SALES=OCONV(TOTAL.REC(1),"MD2,")
   TOT.AGENCY=OCONV(TOTAL.REC(2),"MD2,")
   TOT.FREIGHT=OCONV(TOTAL.REC(3),"MD2,")
   TOT.TAX=OCONV(TOTAL.REC(4),"MD2,")
   TOT.TOTAL=OCONV(TOTAL.REC(5),"MD2,")
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   PRINTER OFF
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 *
END
