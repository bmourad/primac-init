****************************************************************
* REVISION    - [08.1]
* COPYRIGHT   - 1982 by C.B.A.   (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ARSBP
* PROGRAM     - CASH.REC.JOURNAL
* AUTHOR      - CHRIS MYKLEBUST, PRIMAC
* DATE        - 11/01/2000
* DESCRIPTION - This program will print a report of daily cash records.
*               The report used to run with an access statement but
*               a problem arises when there are multiple cash amounts
*               applied to the same invoice #.
*T25505 cm 11/01/2000 * Initial programming.
****************************************************************
*
**** FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ARS.CPYLIB>DAILY.CASH
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** OPEN FILES
*
   OPEN "","DAILY.CASH" TO DAILY.CASH ELSE ERRMSG = "DAILY.CASH FILE IS MISSING"; GOSUB 91000; GOTO 99999
   OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING"; GOSUB 91000; GOTO 99999
*
**** SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** PERFORM PROCREAD
*
   PROCREAD BUFFER ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOSUB 91000; GOTO 99999
   CONO = BUFFER<1>
   CO.NAME = ""
   PERIOD = BUFFER<98>
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
   REPORT.DATE = DATE()
   HD1 = "";HD2 = "";PG.NO = 0
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** INTIALIZATION
*
   LINE.CNT = 99
   SP = SPACE(1)
   HD3 = SPACE(63):" FOR PERIOD : ":PERIOD
   HD4 = SPACE(7):"CUST-NAME"
   HD4 = HD4:SPACE(8):"CHECK#"
   HD4 = HD4:SPACE(5):"CASH"
   HD4 = HD4:SPACE(5):"INVOICE"
   HD4 = HD4:SPACE(4):"CASH-APPL"
   HD4 = HD4:SPACE(5):"G/L-AMT"
   HD4 = HD4:SPACE(3):"G/L #"
   HD4 = HD4:SPACE(4):"DV DP CTR"
   HD4 = HD4:SPACE(4):"ADJ-A/R"
   HD4 = HD4:SPACE(8):"COMMENTS-MISC"
   HD5 = "-----------------------":SPACE(1)
   HD5 = HD5:"------":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"--------":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"--------":SPACE(1)
   HD5 = HD5:"-- -- ---":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"---------------------"
*
**** MAIN PROCESSING
*
   DATA = 1
   FIRST = 1
   RECORD.CNT = ''
   PREV.CUST = ''
   PREV.CUST.NAME = ''
   PREV.DATE = ''
   PREV.ACCT = ''
   CUST.TOTALS = '' ; DATE.TOTALS = '' ; ACCT.TOTALS = '' ; CO.TOTALS = ''
   PRINTER ON
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      RECORD.CNT = RECORD.CNT + 1
      MATREAD DC.REC FROM DAILY.CASH, ID ELSE GOTO 100
      CUST.NO = FIELD(ID,'!',1)[4,99]
      DC.DATE = FIELD(ID,'!',2)
      MATREAD CUST.REC FROM CUSTOMER, CONO:CUST.NO ELSE MAT CUST.REC = ''
      IF FIRST THEN
         PREV.CUST = CUST.NO
         PREV.CUST.NAME = CUST.NAME
         PREV.DATE = DC.DATE
         PREV.ACCT = DC.BANK.ACCT
         FIRST = 0
      END
      BEGIN CASE
         CASE DC.BANK.ACCT # PREV.ACCT
            GOSUB 300
            GOSUB 400
            GOSUB 500
         CASE DC.DATE # PREV.DATE
            GOSUB 300
            GOSUB 400
         CASE CUST.NO # PREV.CUST
            GOSUB 300
      END CASE
*     IF CUST.NO # PREV.CUST THEN GOSUB 300
*     IF DC.DATE # PREV.DATE THEN GOSUB 400
*     IF DC.BANK.ACCT # PREV.ACCT THEN GOSUB 500
      GOSUB 200
      PREV.CUST = CUST.NO
      PREV.CUST.NAME = CUST.NAME
      PREV.DATE = DC.DATE
      PREV.ACCT = DC.BANK.ACCT
100 *
   REPEAT
   GOSUB 300
   GOSUB 400
   GOSUB 500
   GOSUB 600
   GOTO 99999
*
*--- PROCESS DAILY.CASH RECORD
*
200 *
   ICNT = DCOUNT(DC.CASH.APPL,VM)
   FOR I = 1 TO ICNT
      H.LINE = ''
      IF LINE.CNT > 55 THEN GOSUB 1000
      IF I = 1 THEN
         H.LINE = H.LINE:CUST.NAME'L#23':SPACE(1)
         H.LINE = H.LINE:DC.CHECK'L#6':SPACE(1)
         H.LINE = H.LINE:OCONV(DC.CASH,'MD2')'R#12':SPACE(1)
      END ELSE
         H.LINE = H.LINE:SPACE(44)
      END
      SCNT = DCOUNT(DC.CASH.APPL<1,I>,SM)
      FOR S = 1 TO SCNT
         IF S = 1 THEN
            IF DC.INVOICE<1,1> = 'MISC' THEN
               PRINT.INVOICE = DC.INVOICE<1,1>
            END ELSE
               PRINT.INVOICE = DC.INVOICE<1,I>
            END
            H.LINE = H.LINE:PRINT.INVOICE'L#8':SPACE(1)
         END ELSE
            H.LINE = SPACE(53)
         END
         H.LINE = H.LINE:OCONV(DC.CASH.APPL<1,I,S>,'MD2')'R#12':SPACE(1)
         H.LINE = H.LINE:OCONV(DC.AMT<1,I,S>,'MD2')'R#12':SPACE(1)
         IF S = 1 THEN
            H.LINE = H.LINE:DC.ACCT<1,I>'L#8':SPACE(1)
         END ELSE
            H.LINE = H.LINE:SPACE(9)
         END
         H.LINE = H.LINE:DC.DIV<1,I,S>'L#2':SPACE(1)
         H.LINE = H.LINE:DC.DEPT<1,I,S>'L#2':SPACE(1)
         H.LINE = H.LINE:DC.CCTR<1,I,S>'L#3':SPACE(1)
         IF S = 1 THEN
            AR.ADJ = 0 - (SUM(DC.CASH.APPL<1,I>) + SUM(DC.AMT<1,I>))
            H.LINE = H.LINE:OCONV(AR.ADJ,'MD2')'R#12':SPACE(1)
            CUST.TOTALS<4> = CUST.TOTALS<4> + AR.ADJ
         END
         IF I = 1 AND S = 1 THEN
            H.LINE = H.LINE:DC.DESC'L#21'
         END
         PRINT H.LINE
         CUST.TOTALS<2> = CUST.TOTALS<2> + DC.CASH.APPL<1,I,S>
         CUST.TOTALS<3> = CUST.TOTALS<3> + DC.AMT<1,I,S>
         LINE.CNT = LINE.CNT + 1
      NEXT S
      CUST.TOTALS<1> = CUST.TOTALS<1> + DC.CASH<1,I>
   NEXT I
   RETURN
*
*--- PRINT CUSTOMER TOTALS
*
300 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(31):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'TOTAL FOR CUSTOMER ':PREV.CUST'L#12'
   H.LINE = H.LINE:OCONV(CUST.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(CUST.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(CUST.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(CUST.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT
   LINE.CNT = LINE.CNT + 3
   FOR I = 1 TO 4
      DATE.TOTALS<I> = DATE.TOTALS<I> + CUST.TOTALS<I>
      CUST.TOTALS<I> = ''
   NEXT I
   RETURN
*
*--- PRINT RECEIPT DATE TOTALS
*
400 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(31):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'CASH RECEIPTS FOR ':OCONV(PREV.DATE,'D2/')'L#8'
   H.LINE = H.LINE:SPACE(1):'-->':SPACE(1)
   H.LINE = H.LINE:OCONV(DATE.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(DATE.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(DATE.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(DATE.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT
   LINE.CNT = LINE.CNT + 3
   FOR I = 1 TO 4
      ACCT.TOTALS<I> = ACCT.TOTALS<I> + DATE.TOTALS<I>
      DATE.TOTALS<I> = ''
   NEXT I
   RETURN
*
*--- PRINT BANK ACCOUNT TOTALS
*
500 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(31):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'TOTAL FOR ACCOUNT ':PREV.ACCT'L#13'
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT
   LINE.CNT = LINE.CNT + 3
   FOR I = 1 TO 4
      CO.TOTALS<I> = CO.TOTALS<I> + ACCT.TOTALS<I>
      ACCT.TOTALS<I> = ''
   NEXT I
   IF DATA THEN GOSUB 1000
   PREV.CUST = ''
   PREV.DATE = ''
   RETURN
*
*--- PRINT COMPANY TOTALS
*
600 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(31):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'TOTALS FOR COMPANY ':CONO'L#12'
   H.LINE = H.LINE:OCONV(CO.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(CO.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(CO.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(CO.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT RECORD.CNT:' records listed'
   RETURN
*
*--- PRINT HEADING
*
1000 *
   PG.NO = PG.NO + 1
   PRINT CHAR(12)
   PRINT HD1:PG.NO "R#4"
   PRINT HD2
   PRINT HD3
   PRINT
   PRINT HD4
   PRINT HD5
   LINE.CNT = 6
   RETURN
*
**** CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   PRINTER OFF
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF PROGRAM
99999 *
   PRINTER OFF
   PRINT @(-1):
END
