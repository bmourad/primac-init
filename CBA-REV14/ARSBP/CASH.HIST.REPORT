****************************************************************
* REVISION    - [08.1]
* COPYRIGHT   - 1982 by C.B.A.   (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ARSBP
* PROGRAM     - CASH.HIST.REPORT
* AUTHOR      - CHRIS MYKLEBUST, PRIMAC
* DATE        - 11/01/2000
* DESCRIPTION - This program will print a report of cash history records.
*               The report used to run with an access statement but
*               a problem arises when there are multiple cash amounts
*               applied to the same invoice #.
*T25505 cm 11/01/2000 * Initial programming.
*C42103 cmykleb 07/02/2003 * GL # should be sub-multivalued also.
****************************************************************
*
**** FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ARS.CPYLIB>CASH.HIST
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** OPEN FILES
*
   OPEN "","CASH.HIST" TO CASH.HIST ELSE ERRMSG = "CASH.HIST FILE IS MISSING"; GOSUB 91000; GOTO 99999
   OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CUSTOMER FILE IS MISSING"; GOSUB 91000; GOTO 99999
*
**** SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** PERFORM PROCREAD
*
   PROCREAD BUFFER ELSE ERRMSG = "CONTROL FILE IS MISSING"; GOSUB 91000; GOTO 99999
   CONO = BUFFER<1>
   CO.NAME = ""
   BEGIN.DATE = BUFFER<3>
   END.DATE = BUFFER<4>
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
   REPORT.DATE = DATE()
   HD1 = "";HD2 = "";PG.NO = 0
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** INTIALIZATION
*
   LINE.CNT = 99
   SP = SPACE(1)
   HD3 = SPACE(57):"FOR PERIOD ":BEGIN.DATE:" THROUGH ":END.DATE
   HD4 = SPACE(5):"CUST-NAME"
   HD4 = HD4:SPACE(7):"CHECK#"
   HD4 = HD4:SPACE(5):"CASH"
   HD4 = HD4:SPACE(5):"INVOICE"
   HD4 = HD4:SPACE(4):"CASH-APPL"
   HD4 = HD4:SPACE(5):"G/L-AMT"
   HD4 = HD4:SPACE(3):"G/L #"
   HD4 = HD4:SPACE(4):"DV DP CTR"
   HD4 = HD4:SPACE(4):"ADJ-A/R"
   HD4 = HD4:SPACE(5):"COMMENTS-MISC"
   HD4 = HD4:SPACE(4):"DATE"
   HD5 = "--------------------":SPACE(1)
   HD5 = HD5:"------":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"--------":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"--------":SPACE(1)
   HD5 = HD5:"-- -- ---":SPACE(1)
   HD5 = HD5:"------------":SPACE(1)
   HD5 = HD5:"------------------":SPACE(1)
   HD5 = HD5:"-----"
*
**** MAIN PROCESSING
*
   DATA = 1
   FIRST = 1
   RECORD.CNT = ''
   PREV.CUST = ''
   PREV.CUST.NAME = ''
   PREV.DATE = ''
   PREV.ACCT = ''
   CUST.TOTALS = '' ;  ACCT.TOTALS = '' ; CO.TOTALS = ''
   PRINTER ON
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      RECORD.CNT = RECORD.CNT + 1
      MATREAD CHR.REC FROM CASH.HIST, ID ELSE GOTO 100
      CUST.NO = FIELD(ID,'!',1)[4,99]
      CHR.DATE = FIELD(ID,'!',2)
      CHR.CHECK = FIELD(ID,'!',3)
      MATREAD CUST.REC FROM CUSTOMER, CONO:CUST.NO ELSE MAT CUST.REC = ''
      IF FIRST THEN
         PREV.CUST = CUST.NO
         PREV.CUST.NAME = CUST.NAME
         PREV.ACCT = CHR.BANK.NO
         FIRST = 0
      END
      BEGIN CASE
         CASE CHR.BANK.NO # PREV.ACCT
            GOSUB 300
            GOSUB 400
         CASE CUST.NO # PREV.CUST
            GOSUB 300
      END CASE
*     IF CUST.NO # PREV.CUST THEN GOSUB 300
*     IF CHR.BANK.NO # PREV.ACCT THEN GOSUB 400
      GOSUB 200
      PREV.CUST = CUST.NO
      PREV.CUST.NAME = CUST.NAME
      PREV.DATE = CHR.DATE
      PREV.ACCT = CHR.BANK.NO
100 *
   REPEAT
   GOSUB 300
   GOSUB 400
   GOSUB 500
   GOTO 99999
   GOTO 99999
*
*--- PROCESS CASH.HIST RECORD
*
200 *
   ICNT = DCOUNT(CHR.CASH.APPL,VM)
   FOR I = 1 TO ICNT
      H.LINE = ''
      IF LINE.CNT > 55 THEN GOSUB 1000
      IF I = 1 THEN
         H.LINE = H.LINE:CUST.NAME'L#20':SPACE(1)
         H.LINE = H.LINE:CHR.CHECK'L#6':SPACE(1)
         H.LINE = H.LINE:OCONV(CHR.CASH,'MD2')'R#12':SPACE(1)
      END ELSE
         H.LINE = H.LINE:SPACE(41)
      END
      SCNT = DCOUNT(CHR.CASH.APPL<1,I>,SM)
      FOR S = 1 TO SCNT
         IF S = 1 THEN
            IF CHR.INVOICE<1,1> = 'MISC' THEN
               PRINT.INVOICE = CHR.INVOICE<1,1>
            END ELSE
               PRINT.INVOICE = CHR.INVOICE<1,I>
            END
            H.LINE = H.LINE:PRINT.INVOICE'L#8':SPACE(1)
         END ELSE
            H.LINE = SPACE(50)
         END
         H.LINE = H.LINE:OCONV(CHR.CASH.APPL<1,I,S>,'MD2')'R#12':SPACE(1)
         H.LINE = H.LINE:OCONV(CHR.GL.AMT<1,I,S>,'MD2')'R#12':SPACE(1)
*C42103 v
*        IF S = 1 THEN
*           H.LINE = H.LINE:CHR.GL.NO<1,I>'L#8':SPACE(1)
*        END ELSE
*           H.LINE = H.LINE:SPACE(9)
*        END
         H.LINE = H.LINE:CHR.GL.NO<1,I,S>'L#8':SPACE(1)
*C42103 ^
         H.LINE = H.LINE:CHR.DIV<1,I,S>'L#2':SPACE(1)
         H.LINE = H.LINE:CHR.DEPT<1,I,S>'L#2':SPACE(1)
         H.LINE = H.LINE:CHR.CCTR<1,I,S>'L#3':SPACE(1)
         IF S = 1 THEN
            AR.ADJ = 0 - (SUM(CHR.CASH.APPL<1,I>) + SUM(CHR.GL.AMT<1,I>))
            H.LINE = H.LINE:OCONV(AR.ADJ,'MD2')'R#12':SPACE(1)
            CUST.TOTALS<4> = CUST.TOTALS<4> + AR.ADJ
         END
         IF I = 1 AND S = 1 THEN
            H.LINE = H.LINE:CHR.MISC.DESC'L#18':SPACE(1)
            H.LINE = H.LINE:OCONV(CHR.DATE,'D2/')'L#5'
         END
         PRINT H.LINE
         CUST.TOTALS<2> = CUST.TOTALS<2> + CHR.CASH.APPL<1,I,S>
         CUST.TOTALS<3> = CUST.TOTALS<3> + CHR.GL.AMT<1,I,S>
         LINE.CNT = LINE.CNT + 1
      NEXT S
      CUST.TOTALS<1> = CUST.TOTALS<1> + CHR.CASH<1,I>
   NEXT I
   RETURN
*
*--- PRINT CUSTOMER TOTALS
*
300 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(28):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'TOTAL FOR CUSTOMER ':PREV.CUST'L#9'
   H.LINE = H.LINE:OCONV(CUST.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(CUST.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(CUST.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(CUST.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT
   LINE.CNT = LINE.CNT + 3
   FOR I = 1 TO 4
      ACCT.TOTALS<I> = ACCT.TOTALS<I> + CUST.TOTALS<I>
      CUST.TOTALS<I> = ''
   NEXT I
   RETURN
*
*--- PRINT BANK ACCOUNT TOTALS
*
400 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(28):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'TOTAL FOR ACCOUNT ':PREV.ACCT'L#10'
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(ACCT.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT
   LINE.CNT = LINE.CNT + 3
   FOR I = 1 TO 4
      CO.TOTALS<I> = CO.TOTALS<I> + ACCT.TOTALS<I>
      ACCT.TOTALS<I> = ''
   NEXT I
   PREV.CUST = ''
   RETURN
*
*--- PRINT COMPANY TOTALS
*
500 *
   H.LINE = ''
   H.LINE = H.LINE:SPACE(28):'------------'
   H.LINE = H.LINE:SPACE(10):'------------'
   H.LINE = H.LINE:SPACE(1):'------------'
   H.LINE = H.LINE:SPACE(20):'------------'
   PRINT H.LINE
   H.LINE = ''
   H.LINE = H.LINE:'TOTALS FOR COMPANY ':CONO'L#9'
   H.LINE = H.LINE:OCONV(CO.TOTALS<1>,'MD2')'R#12':SPACE(10)
   H.LINE = H.LINE:OCONV(CO.TOTALS<2>,'MD2')'R#12':SPACE(1)
   H.LINE = H.LINE:OCONV(CO.TOTALS<3>,'MD2')'R#12':SPACE(20)
   H.LINE = H.LINE:OCONV(CO.TOTALS<4>,'MD2')'R#12'
   PRINT H.LINE
   PRINT RECORD.CNT:' records listed'
   RETURN
*
*--- PRINT HEADING
*
1000 *
   PG.NO = PG.NO + 1
   PRINT CHAR(12)
   PRINT HD1:PG.NO "R#4"
   PRINT HD2
   PRINT HD3
   PRINT
   PRINT HD4
   PRINT HD5
   LINE.CNT = 6
   RETURN
*
**** CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   PRINTER OFF
   ERR.TYPE=3
   CALL SYSCOM(MAT SYSCOM.REC)
*
**** END OF PROGRAM
99999 *
   PRINTER OFF
   PRINT @(-1):
END
