      SUBROUTINE RCV.PROD.SEL(CONO, PRODNOS, INVENTORY, SELECTION)
*********************************************************************
*
* PROGRAM - RCV.PROD.SEL
*
* AUTHOR  - Nick Amendola, NASTech, Inc.
*
* DATE    - 10/01/97
*
* DESCRIPTION
*
* This program is used to select a P/O paper product number.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY CPYLIB>CHAR
*
*---- INITIALIZATION
*
      SELECTION = ""
      PCNT = DCOUNT(PRODNOS,VM)
      SPTR = 1
      CW1 = 2                   ;* COLUMN 1 MINIMUM WIDTH
      CW2 = 8                   ;* COLUMN 2 MINIMUM WIDTH
      CW3 = 20                  ;* COLUMN 3 MINIMUM WIDTH
      CW4 = 12                  ;* COLUMN 4 MINIMUM WIDTH
      CHEAD1 = "L#"[1,CW1]
      CHEAD2 = "Product"[1,CW2]
      CHEAD3 = "Description"[1,CW3]
      CHEAD4 = "Weight/Width"[1,CW4]
      CDATA1 = ""               ;* COLUMN 1 DATA
      CDATA2 = ""               ;* COLUMN 2 DATA
      CDATA3 = ""               ;* COLUMN 3 DATA
      CDATA4 = ""               ;* COLUMN 4 DATA
      FOR PPTR = 1 TO PCNT
         PRODNO = PRODNOS<1,PPTR>
         MATREAD INV.REC FROM INVENTORY, CONO:PRODNO THEN
            IDESC1 = INV.DESC
            BASWGT = INT(INV.BAS.WT/100)
            BEGIN CASE
            CASE MOD(INV.PAP.WIDTH,10000) = 0
               IWIDTH = OCONV(INT(INV.PAP.WIDTH/10000),"MD0")
            CASE MOD(INV.PAP.WIDTH,1000) = 0
               IWIDTH = OCONV(INT(INV.PAP.WIDTH/1000),"MD1")
            CASE MOD(INV.PAP.WIDTH,100) = 0
               IWIDTH = OCONV(INT(INV.PAP.WIDTH/100),"MD2")
            CASE MOD(INV.PAP.WIDTH,10) = 0
               IWIDTH = OCONV(INT(INV.PAP.WIDTH/10),"MD3")
            CASE 1
               IWIDTH = OCONV(INV.PAP.WIDTH,"MD4")
            END CASE
            IDESC2 = BASWGT:"# ":IWIDTH:'"'
         END ELSE
            MAT INV.REC = ""
            IDESC1 = "???????????????"
            IDESC2 = ""
         END
         CDATA1<PPTR> = PPTR
         CDATA2<PPTR> = PRODNO
         CDATA3<PPTR> = IDESC1
         CDATA4<PPTR> = IDESC2
         IF LEN(PPTR)   > CW1 THEN CW1 = LEN(PPTR)
         IF LEN(PRODNO) > CW2 THEN CW2 = LEN(PRODNO)
         IF LEN(IDESC1) > CW3 THEN CW3 = LEN(IDESC1)
         IF LEN(IDESC2) > CW4 THEN CW4 = LEN(IDESC2)
      NEXT PPTR
      FM1 = "R#":CW1
      FM2 = "L#":CW2
      FM3 = "L#":CW3
      FM4 = "L#":CW4
      CHEAD1 = (SPACE(INT((CW1-LEN(CHEAD1))/2)):CHEAD1) FM1
      CHEAD2 = (SPACE(INT((CW2-LEN(CHEAD2))/2)):CHEAD2) FM2
      CHEAD3 = (SPACE(INT((CW3-LEN(CHEAD3))/2)):CHEAD3) FM3
      CHEAD4 = (SPACE(INT((CW4-LEN(CHEAD4))/2)):CHEAD4) FM4
      ULINE1 = STR("-",CW1)
      ULINE2 = STR("-",CW2)
      ULINE3 = STR("-",CW3)
      ULINE4 = STR("-",CW4)
      SPW = 2
      SPX = "  "
      SWIDTH = 2+1+CW1+SPW+CW2+SPW+CW3+SPW+CW4+1+2
      IF SWIDTH > 80 THEN
         SPW = 1
         SPX = " "
         SWIDTH = 2+1+CW1+SPW+CW2+SPW+CW3+SPW+CW4+1+2
      END
      IWIDTH = SWIDTH - 4
*
*---- MAIN PROCESSING
*
100 *
      COL = INT((80-SWIDTH)/2)
      ROW = 3
      ROW += 1; PRINT @(COL,ROW):"  ":STR(" ",IWIDTH):"  ":
      ROW += 1; PRINT @(COL,ROW):" +":STR("-",IWIDTH):"+ ":
      ROW += 1; PRINT @(COL,ROW):" | ":CHEAD1:SPX:CHEAD2:SPX:CHEAD3:SPX:CHEAD4:" | ":
      ROW += 1; PRINT @(COL,ROW):" | ":ULINE1:SPX:ULINE2:SPX:ULINE3:SPX:ULINE4:" | ":
      FOR PP = SPTR TO (SPTR+9)
         PRODNO = PRODNOS<1,PP>
         PDESC1 = PDESC1
         ROW += 1
         IF PP <= PCNT THEN
            PRINT @(COL,ROW):" | ":CDATA1<PP> FM1:SPX:CDATA2<PP> FM2:SPX:CDATA3<PP> FM3:SPX:CDATA4<PP> FM4:" | ":
         END ELSE
            PRINT @(COL,ROW):" |":STR(" ",IWIDTH):"| ":
         END
      NEXT PP
      ROW += 1; PRINT @(COL,ROW):" |":STR(" ",IWIDTH):"| ":
      ROW += 1; PRINT @(COL,ROW):" +":STR("-",IWIDTH):"+ ":
      ROW += 1; PRINT @(COL,ROW):"  ":STR(" ",IWIDTH):"  ":
      ROW -= 2; PRINT @(COL,ROW):" |":" Selection: " :
      MINV = SPTR
      MAXV = SPTR+9
      IF MAXV > PCNT THEN MAXV = PCNT
      DONE = 0
      LOOP
         INPUT SEL,3:_
         SEL = UPCASE(SEL)
         BEGIN CASE
         CASE SEL="" OR SEL="END" OR SEL=CHAR(27) OR SEL="^" OR SEL="X"
            DONE = 1
         CASE NUM(SEL)
            IF SEL >= MINV AND SEL <= MAXV THEN
               SELECTION = SEL
               DONE = 1
            END ELSE
               PRINT BEL:
            END
         CASE SEL = "S" OR SEL = "SF" OR SEL = "+"
            SPTR = SPTR + 10
            IF SPTR > PCNT THEN SPTR = 1
            GOTO 100
         CASE SEL = "SR" OR SEL = "-"
            SPTR = SPTR - 10
            IF SPTR < 1 THEN SPTR = 1
            GOTO 100
         CASE SEL = "ST"
            SPTR = 1
            GOTO 100
         CASE SEL = "SB"
            SPTR = PCNT
            GOTO 100
         CASE 1
            PRINT BEL:
         END CASE
      UNTIL DONE DO
      REPEAT
      RETURN
   END
