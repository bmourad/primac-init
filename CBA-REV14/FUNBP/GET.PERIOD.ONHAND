FUNCTION GET.PERIOD.ONHAND(IWH.ID,CUR.PERIOD,PERIOD)
********************************************************************* 
* REVISION- [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM- PRIMAC
* SOURCE- FUNBP 
* PROGRAM - GET.PERIOD.ONHAND
* BY- EDVARD PITKA
* DATE- 06/17/2002
* DESCRIPTION - THIS FUNCTION RETURN IWH.ON.HAND QTY FOR GIVEN PERIOD
*ENDDOC 
********************************************************************* 
*
*COPY>ICS.CPYLIB>INV_AUDIT_BAL 
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>WAREHOUSE 
* 
DEFFUN DIVISION.POSITION (CONO,CONTROL.FILE,DIV.CODE) 
DEFFUN CURRENT.PERIOD(CONO,CONTROL.FILE,DIV.POS,FISCAL.FLAG)
DEFFUN CALC.PTD(CONO,PROD,WHSE,PERIOD) ; * C40082 
*
IF @LOGNAME ='epitka' THEN DEBUG
ONHAND=''
CONO=IWH.ID[1,3]
PROD=OCONV(IWH.ID,"G!1")[4,99]
WHSE=OCONV(IWH.ID,"G1!1")
GEN.DIV='00'
*
IF FILEINFO(INV_AUDIT_HIST,0)=0 THEN
  OPEN '','INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE 
    ONAHND<1,2>='CANNOT OPEN INV_AUDIT_HIST FILE'      
    ONHAND<1,1>='-2' ; GOTO 99999
  END                                             
END
IF FILEINFO(INV_AUDIT_BAL,0)=0 THEN
  OPEN '','INV_AUDIT_BAL' TO INV_AUDIT_BAL ELSE   
    ONAHND<1,2>='CANNOT OPEN INV_AUDIT_BAL FILE'       
    ONHAND<1,1>='-2' ; GOTO 99999
  END                                             
END
IF FILEINFO(WAREHOUSE,0)=0 THEN
  OPEN '','WAREHOUSE' TO WAREHOUSE ELSE           
    ONAHND<1,2>='CANNOT OPEN WAREHOUSE FILE'           
    ONHAND<1,1>='-2' ; GOTO 99999
  END                                             
END
IF CUR.PERIOD ='' THEN
  GOSUB GET.CURRENT.PERIOD
  GOSUB GET.PERIOD.ONHAND
END ELSE
  GOSUB GET.PERIOD.ONHAND
END
*
GOTO 99999
*
**************************************************************************
****** S U B R O U T I N E S *********************************************
**************************************************************************
*
* 
********************* 
GET.CURRENT.PERIOD: 
********************* 
* 
WHSE.ID=CONO:WHSE 
MATREAD WHSE.REC FROM WAREHOUSE,WHSE.ID THEN
  IF WHS.DIV='' THEN WHS.DIV=GEN.DIV
  DIV.POS=DIVISION.POSITION(CONO,CONTROL,WHS.DIV) 
  IF DIV.POS<1,1>='' THEN
    DIV.POS=DIV.POS<1,2>
  END ELSE
    ONHAND=DIV.POS ; GOTO 99999
  END
  CUR.PERIOD=CURRENT.PERIOD(CONO,CONTROL,DIV.POS,"IC")
  IF CUR.PERIOD<1,1>='' THEN
    CUR.PERIOD=CUR.PERIOD<1,2>
  END ELSE
    ONHAND=CUR.PERIOD ; GOTO 99999
  END
END ELSE
  ONHAND<1,1>='-1'
  ONHAND<1,2>='Cannot locate warehouse record ':WHSE.ID
  GOTO 99999
END
RETURN
*
***********************
GET.PERIOD.ONHAND: 
***********************
*
IF PERIOD # '' THEN
  BEGIN CASE
    CASE PERIOD<CUR.PERIOD
      ITB.ID=CONO:PROD:"!":WHSE:"!":PERIOD
      MATREAD ITB.REC FROM INV_AUDIT_BAL,ITB.ID THEN
        ONHAND=ITB.BEG+ITB.RECV+ITB.SALE+ITB.SHRNK+ITB.TRAN.IN+ITB.TRAN.OUT
      END ELSE
        ONHAND=0
      END
    CASE CUR.PERIOD=PERIOD
      PTD=CALC.PTD(CONO,PROD,WHSE,PERIOD)
      MATPARSE ITB.REC FROM PTD,@AM
      ONHAND=ITB.BEG+ITB.RECV+ITB.SALE+ITB.SHRNK+ITB.TRAN.IN+ITB.TRAN.OUT
    CASE PERIOD>CUR.PERIOD
      ITB.ID=CONO:PROD:"!":WHSE:"!":CUR.PERIOD 
      MATREAD ITB.REC FROM INV_AUDIT_BAL,ITB.ID THEN 
        ONHAND=ITB.BEG
        SYSLIST='' 
        CMD='SELECT INV_AUDIT_HIST WITH PROD_WHSE_IDX="':CONO:PROD:"!":WHSE 
        CMD:='" AND WITH INAH_PERIOD>="':CUR.PERIOD:'" AND WITH INAH_PERIOD<="':PERIOD:'"'
        PERFORM CMD RTNLIST SYSLIST CAPTURING MSG
        DONE = 0 
        LOOP 
          READNEXT INAH.ID FROM SYSLIST ELSE DONE=1
        UNTIL DONE DO
          MATREAD INAH.REC FROM INV_AUDIT_HIST, INAH.ID THEN 
            ONHAND+=INAH.QTY
          END ELSE
            ONHAND<1,1>='-1'                                         
            ONHAND<1,2>='Cannot locate INV_AUDIT_HIST record ':INAH.ID 
            GOTO 99999
          END                                                        
        REPEAT 
      END ELSE                                                   
        ONHAND=0
      END                                                        
  END CASE
END
RETURN 
99999 
RETURN ONHAND
