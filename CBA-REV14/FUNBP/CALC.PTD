FUNCTION CALC.PTD(CONO,PROD.NUM,CURR.WHSE,CUR.PERIOD)
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - FUNBP
* PROGRAM     - CALC.PTD
* BY          - ABDULLAH JIBALY
* DATE        - 04/01/2001
* DESCRIPTION - THIS FUNCTION RETURNS THE PERIOD TO DATE AMOUNTS
*               OF THE ITEMS DESCRIBED BY THE INV_AUDIT_BAL CPYLIB
*               (EXCEPT FOR BEGIN BALANCES) FROM THE INV_AUDIT_HIST FILE
*               GIVEN THE CONO, PRODUCT NUMBER, WAREHOUSE, AND PERIOD.
*               BEGIN BALANCES ARE TAKEN FROM INV_AUDIT_BAL FOR CUR.PERIOD
*ENDDOC
*********************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
PTD = ''
*
*----- OPEN FILES
*
OPEN '', 'INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE 
  RETURN 'INV_AUDIT_HIST FILE IS MISSING'
END                                      
OPEN '', 'INV_AUDIT_BAL' TO INV_AUDIT_BAL ELSE 
  RETURN 'INV_AUDIT_BAL FILE IS MISSING'
END                                      
*
*----- READ BEGIN BALANCES FOR CURRENT PERIOD
*
FOR I = 1 TO 12
  PTD<I> = 0
NEXT I
*
ITB.ID = CONO:PROD.NUM:"!":CURR.WHSE:"!":CUR.PERIOD
MATREAD ITB.REC FROM INV_AUDIT_BAL, ITB.ID ELSE
  MAT ITB.REC = ""
END
PTD<1> = ITB.BEG + 0
PTD<2> = ITB.BEG.AMT + 0
*
*----- SELECT AUDIT HIST FOR PERIOD TO DATE 
*
ITEM.LIST = ""
SELECTKEY = CONO:PROD.NUM:"!":CURR.WHSE:"!":CUR.PERIOD
IDX = "PROD_WHSE_PERIOD_IDX"
SELECTINDEX IDX, SELECTKEY FROM INV_AUDIT_HIST TO ITEM.LIST
*
DONE   = 0
LOOP
  READNEXT INAH.ID FROM ITEM.LIST ELSE DONE=1
UNTIL DONE DO
  MATREAD INAH.REC FROM INV_AUDIT_HIST, INAH.ID ELSE
    MAT INAH.REC = ""
  END
*
  BEGIN CASE
*T27801 v
*   CASE INAH.TYPE = 'R'
    CASE INAH.TYPE = 'R' OR (INAH.TYPE = 'A' AND (INAH.SRC='IQ' OR INAH.SRC='IC'))
      PTD<3> = PTD<3> + INAH.QTY
      PTD<4> = PTD<4> + INAH.EXT.COST
    CASE INAH.TYPE = 'S'
      PTD<5> = PTD<5> + INAH.QTY
      PTD<6> = PTD<6> + INAH.EXT.COST
    CASE INAH.TYPE = 'A'
      PTD<7> = PTD<7> + INAH.QTY
      PTD<8> = PTD<8> + INAH.EXT.COST
    CASE INAH.TYPE = 'I'
      PTD<9> = PTD<9> + INAH.QTY
      PTD<10> = PTD<10> + INAH.EXT.COST
    CASE INAH.TYPE = 'O'
      PTD<11> = PTD<11> + INAH.QTY
      PTD<12> = PTD<12> + INAH.EXT.COST
  END CASE
REPEAT
RETURN PTD
