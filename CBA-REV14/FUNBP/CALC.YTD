      FUNCTION CALC.YTD(CONO,PROD.NUM,CURR.WHSE,CUR.PERIOD)
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - FUNBP
* PROGRAM     - CALC.YTD
* BY          - ABDULLAH JIBALY
* DATE        - 04/01/2001
* DESCRIPTION - THIS FUNCTION RETURNS THE YEAR TO DATE AMOUNTS
*               OF THE ITEMS DESCRIBED BY THE INV_AUDIT_BAL CPYLIB
*               FROM THE INV_AUDIT_BAL FILE GIVEN THE CONO, PRODUCT NUMBER,
*               WAREHOUSE, AND CURRENT PERIOD.
*ENDDOC
*********************************************************************
*
*----- INSERT FILE EQUATES
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
*
*----- DEFINE FUNCTION
*
*- RETURNS: ARRAY OF 12 ATTRIBUTES DEFINED IN ICS.CPYLIB>INV_AUDIT_BAL
      DEFFUN CALC.PTD(CONO,PROD.NUM,CURR.WHSE,CUR.PERIOD)
      YTD=''
*
*----- OPEN FILES
*
      OPEN '', 'INV_AUDIT_BAL' TO INV_AUDIT_BAL ELSE 
         RETURN 'INV_AUDIT_BAL FILE IS MISSING'
      END                                      
*
*----- GET PERIOD TO DATE FIGURES
*
      YTD = CALC.PTD(CONO,PROD.NUM,CURR.WHSE,CUR.PERIOD)
      IF NUM(YTD<1,1>) THEN
         NULL
      END ELSE
         GOTO 99999
      END
      IF DCOUNT(YTD,@AM) # 12 THEN
         RETURN 'ERROR CALLING CALC.PTD FUNCTION: ':YTD
      END
*
*----- READ EVERY PERIOD UNTIL CURRENT ONE
*
      YEAR = CUR.PERIOD[1,4]
      PER  = CUR.PERIOD[5,2]
      ITB.ID = CONO:PROD.NUM:"!":CURR.WHSE:"!":YEAR
      PER-=1
      FOR I = 1 TO PER
         MATREAD ITB.REC FROM INV_AUDIT_BAL, ITB.ID:I'R%2' ELSE
            MAT ITB.REC = ''
         END
         IF I = 1 THEN
            YTD<1> = ITB.BEG + 0
            YTD<2> = ITB.BEG.AMT + 0
         END
         YTD<3> = YTD<3> + ITB.RECV
         YTD<4> = YTD<4> + ITB.RECV.AMT
         YTD<5> = YTD<5> + ITB.SALE
         YTD<6> = YTD<6> + ITB.SALE.AMT
         YTD<7> = YTD<7> + ITB.SHRNK
         YTD<8> = YTD<8> + ITB.SHRNK.AMT
         YTD<9> = YTD<9> + ITB.TRAN.IN
         YTD<10> = YTD<10> + ITB.TRAN.IN.AMT
         YTD<11> = YTD<11> + ITB.TRAN.OUT
         YTD<12> = YTD<12> + ITB.TRAN.OUT.AMT
      NEXT I
*
99999 
      RETURN YTD
