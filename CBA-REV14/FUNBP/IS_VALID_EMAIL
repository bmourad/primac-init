FUNCTION IS_VALID_EMAIL(ADDRESS,ERRMSG)
*******************************************************************************
* REVISION    - [12.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - FUNBP
* PROGRAM     - IS_VALID_EMAIL
* BY          - ABDULLAH JIBALY; CBA
* DATE        - 11/20/2002
* DESCRIPTION - THIS FUNCTION CHECKS THE VALIDITY OF AN EMAIL ADDRESS
*               IF THE ADDRESS IS VALID IT RETURNS 1 (TRUE)
*               OTHERWISE, IT RETURNS 0 (FALSE) AND ERRMSG IS SET
*
*ENDDOC
*******************************************************************************
*
* Initialize variables
*
  BAD.CHARS = "`!#$%^&*()+={}[]|\:;'/<>?, " : '"'
  MATCH.STR = "1X0X'@'1X0X'.'2A" : @VM : "1X0X'@'1X0X'.'3A"
*
* Common mistakes
*
  BAD.MATCH.STR = "0X'@'0X'@'0X"
  BAD.MATCH.STR<1,-1> = "'.'0X"
  BAD.MATCH.STR<1,-1> = "0X'..'0X"
  BAD.MATCH.STR<1,-1> = "0X'.@'0X"
  BAD.MATCH.STR<1,-1> = "0X'@.'0X"
*
* Test for invalid characters
*
  VALID = (CONVERT(BAD.CHARS,'',ADDRESS) = ADDRESS)
  IF NOT(VALID) THEN
    BAD.CHARS.MASK = STR(BAD.CHARS[1,1],LEN(BAD.CHARS))
    BAD.ADDRESS.MASK = CONVERT(BAD.CHARS,BAD.CHARS.MASK,ADDRESS)
    BAD.ADDRESS.IDX = INDEX(BAD.ADDRESS.MASK,BAD.CHARS.MASK[1,1],1)
    ERRMSG = QUOTE(ADDRESS[BAD.ADDRESS.IDX,1])
    ERRMSG:= ' is not a valid character in an email address.'
    RETURN VALID
  END
*
* Test for valid format
*
  *VALID = (INDEX(ADDRESS,'@',2) = 0)
  VALID = (ADDRESS MATCHES MATCH.STR)
  VALID = VALID AND NOT((ADDRESS MATCHES BAD.MATCH.STR))
  IF NOT(VALID) THEN
    ERRMSG = 'Invalid email address format.'
    RETURN VALID
  END
*
* Return result
*
  RETURN VALID
