   FUNCTION GET_XML_CPYLIB(CPYLIB.PATH)
********************************************************************
* REVISION - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - FUNBP
* PROGRAM  - GET_XML_CPYLIB
* BY       - Abdullah Jibaly, COMPUTER BUSINESS ASSOCIATES
* DATE     - 08/06/02
*
* DESCRIPTION
*
* This function returns a parsed cpylib for xml record conversion
*
*********************************************************************
*
  ;*
  ;* Initialization
  ;*
  LINE.FEED = CHAR(10)
  ;*
  ;* Open files
  ;*
  OPEN "XML_CPYLIB" TO XML_CPYLIB ELSE
    RETURN "ERROR_MISSING_XML_CPYLIB_FILE"
  END
  OPEN "VOC" TO VOC ELSE
    RETURN "ERROR_MISSING_VOC_FILE"
  END
  ;*
  ;* Get the cpylib file location
  ;*
  CPYLIB.DIR  = FIELD(CPYLIB.PATH,"/",1)
  CPYLIB.FILE = FIELD(CPYLIB.PATH,"/",2)
  READ REC FROM VOC, CPYLIB.DIR ELSE
    RETURN "ERROR_MISSING_VOC_ENTRY"
  END
  CPYLIB.DIR = REC<2>
  READ REC FROM VOC, "XML_CPYLIB" ELSE
    RETURN "ERROR_MISSING_VOC_ENTRY"
  END
  XML.CPYLIB.DIR = REC<2>
  ;*
  ;* Convert the cpylib path to the XML version and get file statistics
  ;*
  XML.CPYLIB.FILE  = CHANGE(CPYLIB.PATH,"/","~")
  XML.CPYLIB.FILE  = CHANGE(XML.CPYLIB.FILE,".","_")
  XML.CPYLIB.STATS = DIR(XML.CPYLIB.DIR:"/":XML.CPYLIB.FILE)
  CPYLIB.STATS     = DIR(CPYLIB.DIR:"/":CPYLIB.FILE)
  ;*
  ;* Check if the XML cpylib file is current:
  ;* Attrib 2 is the date, and 3 is the time of modification
  ;*
  XML.UPDATE = 0
  IF XML.CPYLIB.STATS<2> < CPYLIB.STATS<2> THEN XML.UPDATE = 1
  IF XML.CPYLIB.STATS<2> = CPYLIB.STATS<2> THEN
    IF XML.CPYLIB.STATS<3> < CPYLIB.STATS<3> THEN XML.UPDATE = 1
  END
  ;*
  ;* If the cpylib is current then read it and return it
  ;*
  IF NOT(XML.UPDATE) THEN
    READ XML.CPYLIB.REC FROM XML_CPYLIB, XML.CPYLIB.FILE THEN
      RETURN XML.CPYLIB.REC
    END
  END
  ;*
  ;* Otherwise, read the original cpylib and parse it
  ;*
  OSREAD CPYLIB.REC FROM CPYLIB.DIR:"/":CPYLIB.FILE ELSE
    RETURN "ERROR_MISSING_CPYLIB_ITEM"
  END
  SWAP LINE.FEED WITH @AM IN CPYLIB.REC
  CPYLIB.CNT = 1
  XML.CPYLIB.REC = ''
  PREFIX.IDX     = ''
  FOR CPYLIB.IDX = 1 TO DCOUNT(CPYLIB.REC,@AM)
    CPYLIB.LINE = TRIM(CPYLIB.REC<CPYLIB.IDX>)
    IF CPYLIB.LINE[1,3] # "EQU" THEN CONTINUE
    CPYLIB.LINE.NAME = FIELD(CPYLIB.LINE," ",2)
    CPYLIB.LINE.NUM  = FIELD(CPYLIB.LINE," ",4)
    CPYLIB.LINE.NUM  = FIELD(CPYLIB.LINE.NUM,"(",2)
    CPYLIB.LINE.NUM  = FIELD(CPYLIB.LINE.NUM,")",1)
    IF CPYLIB.LINE.NUM # CPYLIB.CNT THEN CONTINUE
    XML.FIELD.NAME = CHANGE(CPYLIB.LINE.NAME,".","-")
    XML.FIELD.NAME = CHANGE(XML.FIELD.NAME,"_","-")
    IF PREFIX.IDX = '' THEN
      PREFIX.IDX = INDEX(XML.FIELD.NAME,"-",1)+1
    END
    XML.FIELD.NAME = XML.FIELD.NAME[PREFIX.IDX,LEN(XML.FIELD.NAME)]
    XML.FIELD.NAME = DOWNCASE(XML.FIELD.NAME)
    XML.CPYLIB.REC<-1> = XML.FIELD.NAME
    CPYLIB.CNT = CPYLIB.CNT + 1
  NEXT CPYLIB.IDX
  ;*
  ;* Write the newly generated cpylib to the XML_CPYLIB file, and return it
  ;*
  WRITE XML.CPYLIB.REC ON XML_CPYLIB, XML.CPYLIB.FILE
  RETURN XML.CPYLIB.REC
*
