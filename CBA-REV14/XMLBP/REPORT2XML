   FUNCTION REPORT2XML(REPORT.REC,BACKGROUND.IMG)
********************************************************************
* REVISION - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - FUNBP
* PROGRAM  - REPORT2XML
* BY       - Abdullah Jibaly, COMPUTER BUSINESS ASSOCIATES
* DATE     - 06/26/02
*
* DESCRIPTION
*
* This function returns a report converted to xml
* 
* ARGUMENTS
*
* - REPORT.REC     : Contains the report
* - BACKGROUND.IMG : Path to the background image, or empty for none
*
*********************************************************************
*
   ;*
   ;* Initialization
   ;*
   DEFFUN XML_FORMAT_CDATA(STR)
   PAGE.FEED = CHAR(12)
   XML.REC  = ''
   ;*
   ;* Create XML declaration and specify background if it exists
   ;*
   XML.REC<-1> = '<?xml version="1.0"?>'
   IF BACKGROUND.IMG = '' THEN
      XML.REC<-1> = '<Report>'
   END ELSE
      XML.REC<-1> = '<Report backround-image=':QUOTE(BACKGROUND.IMG):'>'
   END
   ;*
   ;* Convert each line to XML
   ;* The first line cannot be a page-feed, so it's treated seperately
   ;*
   IF REPORT.REC<1> # PAGE.FEED THEN
      XML.REC<-1> = '<Line>':XML_FORMAT_CDATA(REPORT.REC<1>):'</Line>'
   END
   REPORT.LINES = DCOUNT(REPORT.REC,@AM)
   FOR REPORT.IDX = 2 TO REPORT.LINES
      IF REPORT.REC<REPORT.IDX> = PAGE.FEED THEN
         XML.REC<-1> = '<Page/>'
         CONTINUE
      END
      XML.REC<-1> = '<Line>':XML_FORMAT_CDATA(REPORT.REC<REPORT.IDX>):'</Line>'
   NEXT REPORT.IDX
   ;*
   ;* Return the converted report
   ;*
   XML.REC<-1> = '</Report>'
   RETURN XML.REC
