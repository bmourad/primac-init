OPEN "CO.ARCHIVE" TO CO.ARCHIVE ELSE PRINT "Unable to open CO.ARCHIVE" ; STOP
*
PRINT "Enter File Name where program lives. (e.g. AETNABP...) :":
INPUT FILENAME
OPEN FILENAME TO FROMFILE ELSE PRINT "Unable to open ":FILENAME ; STOP
*
VERB = "SSELECT CO.ARCHIVE WITH @ID LIKE ":FILENAME:"... BY ITEMNAME BY SORT.DATE"
*
UDTEXECUTE VERB CAPTURING JUNK
DONE = 0
ITEMLIST=''
LOOP
  READNEXT ID ELSE DONE = 1
UNTIL DONE DO
  ITEMLIST<1,-1> = ID
REPEAT
NUMITEMS = DCOUNT(ITEMLIST,CHAR(253))
*
*
LINES.PER.PAGE=15
CURR.PTR=1
TOP.PTR=CURR.PTR
EXIT.LOOP = 0
LINENUM=1
PRINT @(0,0):@(-3):"LN.. Item Name.......................... Date...... Time......"
LOOP
UNTIL EXIT.LOOP DO
  IF LINENUM = 1 THEN
    PRINT @(0,1):@(-3):
  END
  LN = CURR.PTR"R#4 ":FIELD(ITEMLIST<1,CURR.PTR>,"*",2)"L#35 "
  LN = LN:FIELD(ITEMLIST<1,CURR.PTR>,"*",3)"R#10 "
  LN = LN:FIELD(ITEMLIST<1,CURR.PTR>,"*",4)"R#10"
  PRINT @(0,LINENUM):LN
  LINENUM = LINENUM + 1
  CURR.PTR = CURR.PTR+1
  DO.PROMPT=0 ; DO.SCROLL=0
  IF CURR.PTR > NUMITEMS THEN
    DO.PROMPT=1
    DO.SCROLL=0
  END ELSE
    IF LINENUM > LINES.PER.PAGE THEN
      DO.PROMPT=1
      DO.SCROLL=1
    END
  END
  IF DO.PROMPT THEN
    VALID.RESP=0
    LOOP
    UNTIL VALID.RESP DO
      IF DO.SCROLL THEN
        PR = 'Enter Selection, "S" to Scroll, or "E" to Exit :'
      END ELSE
        PR = 'Enter Selection, or "E" to Exit :'
      END
      PRINT @(0,21):PR:
      INPUT RESP
      PRINT @(0,23):@(-3):
      BEGIN CASE
        CASE RESP = '' 
          PRINT @(0,23):"'' is not a valid response":
        CASE RESP = 'E' OR RESP = CHAR(28)
          VALID.RESP=1
          EXIT.LOOP=1
        CASE NUM(RESP)
          IF RESP >= TOP.PTR  AND RESP <= TOP.PTR + LINES.PER.PAGE - 1 THEN
            RECOVER.NUM=RESP
            GOSUB DO.RECOVER
            VALID.RESP=1
            EXIT.LOOP=1
          END ELSE
            PRINT @(0,23):RESP[1,10]:" is not a valid selection":
          END
        CASE RESP = 'S'
          CURR.PTR = TOP.PTR + LINES.PER.PAGE
          LINENUM=1
          TOP.PTR = CURR.PTR
          IF CURR.PTR > NUM.ITEMS THEN
            CURR.PTR=1
            TOP.PTR=1
          END
        CASE 1
          PRINT @(0,23):RESP[1,10]:" is not a valid response":
      END CASE
    REPEAT
  END
REPEAT
*
STOP
*
DO.RECOVER: ;* THIS IS WHERE THE PROGRAM IS EXTRACTED AND PUT BACK INTO THE
*               ORIGINAL FILE.
REC.ID = ITEMLIST<1,RECOVER.NUM>
READ REC FROM CO.ARCHIVE,REC.ID THEN
  NEW.ID = FIELD(REC.ID,"*",2)
  PRINT @(0,21):@(-3):"Last chance to quit.  Do you still want to overlay the version in"
  PRINT "the ":FILENAME:" file 'C'-Copy :":
  INPUT RESP
  IF RESP[1,1]='C' THEN
    WRITE REC ON FROMFILE,NEW.ID
    PRINT FILENAME:" File updated with copy of ":NEW.ID:" from archive version"
    PRINT REC.ID
  END ELSE
    PRINT "No update"
  END
END
RETURN

