INCLUDE INCS GLOBAL.EQUATES
PRINT "Enter First File  :":
INPUT FN1
PRINT "Enter Second File :":
INPUT FN2
*-----
GET.FLAG:
*-----
PRINT "List (M)atches or (D)ifferences?:":
INPUT DIFF.FLAG
IF DIFF.FLAG # 'D' AND DIFF.FLAG # 'M' AND DIFF.FLAG # "DD" THEN
  PRINT BELL:"Invalid Response"
  GOTO GET.FLAG
END
IF DIFF.FLAG = 'DD' THEN
  DIFF.FLAG = 'D'
  DETAIL.FLAG = TRUE
END ELSE
  DETAIL.FLAG = FALSE
END
IF DIFF.FLAG = 'D' THEN DIFF.FLAG = TRUE ELSE DIFF.FLAG = FALSE
OPEN FN1 TO F1 ELSE
  PRINT BELL:"Unable to Open ":FN1
  STOP
END
OPEN FN2 TO F2 ELSE
  PRINT BELL:"Unable to Open ":FN2
  STOP
END
SELECT F1
DONE = FALSE
REC.COUNT = 0
REC.COUNT = 0
HIT.LIST = ''
HIT.COUNT = 0
DONE = FALSE
LOOP
  READNEXT ID ELSE DONE = TRUE
UNTIL DONE DO
  IF REM(REC.COUNT,10) = 0 THEN PRINT @(0,20):@(-3):REC.COUNT"R#7":" Records, ":HIT.COUNT"R#7":" Hits"
  REC.COUNT = REC.COUNT + 1
  READ REC FROM F1, ID ELSE REC = ''
  GOSUB CLEAN.REC
  REC1 = REC
  READ REC FROM F2, ID ELSE REC = ''
  GOSUB CLEAN.REC
  REC2 = REC
  IF REC1 # REC2 THEN
    DIFF = TRUE
    IF DIFF = DIFF.FLAG THEN GOSUB FIND.DIFF.ATTRS
  END ELSE
    DIFF = FALSE
  END
  IF DIFF = DIFF.FLAG THEN
    HIT.LIST<-1> = ID
    HIT.COUNT = HIT.COUNT + 1
  END
REPEAT
PRINT "Compare Complete -- "
PRINT REC.COUNT"R#7":" Records,"
PRINT HIT.COUNT"R#7":" Mismatches"
WRITE HIT.LIST ON F1, "@@@ID@@@"
EXECUTE "QSELECT ":FN1:" @@@ID@@@"
EXECUTE "SAVE-LIST COMP@":FN1:"@":FN2
DELETE F1, "@@@ID@@@"
STOP
*----
FIND.DIFF.ATTRS:
*----
NUM.ATTRS = DCOUNT(REC1, AM)
TT = DCOUNT(REC2, AM)
IF TT > NUM.ATTRS THEN NUM.ATTRS = TT
FOR I = 1 TO NUM.ATTRS
  IF (TRIM(REC1<I>) # TRIM(REC2<I>)) AND DETAIL.FLAG  THEN
    ID<1,-1> = 'A':I
*    PRINT I"R%5":' ':TRIM(REC1<I>)
*    PRINT I"R%5":' ':TRIM(REC2<I>)
  END
NEXT I
RETURN
*-----
CLEAN.REC:
*-----
NUM.ATTRS = DCOUNT(REC, AM)
T.REC = ''
FOR I = 1 TO NUM.ATTRS
  T.LINE = REC<I>
  LOOP
    TT = INDEX(T.LINE, ' ',1)
  UNTIL TT = 0 DO
    T.LINE = T.LINE[1,TT-1]:T.LINE[TT+1,9999]
  REPEAT
  T.REC<-1> = T.LINE
NEXT I
REC = T.REC
RETURN
