INCLUDE INCS GLOBAL.EQUATES
*
*----
SRC.FILE.PR:
*----
PRINT "Enter Source File :":
INPUT SRC.FILE.NAME
OPEN SRC.FILE.NAME TO SRC.FILE ELSE PRINT BELL:"Unable to Open ":SRC.FILE.NAME; GOTO SRC.FILE.PR
*----
ATTR.PR:
*----
PRINT "Enter Attribute   :":
INPUT TT
ATTR = ''
NUM.ATTR = 1
OCONVS = ''
FOR I = 1 TO DCOUNT(TT, ';')
  TT1 = FIELD(TT,';',I)
  TT2 = FIELD(TT1,'\',2)
  TT1 = FIELD(TT1,'\',1)
  IF TT1 MATCHES "0N" THEN
    ATTR<NUM.ATTR> = TT1
    OCONVS<NUM.ATTR> = TT2
    NUM.ATTR = NUM.ATTR + 1
  END ELSE
    PRINT BELL:"Invalid Attr Number"
    GOTO ATTR.PR
  END
NEXT I
NUM.ATTR = DCOUNT(ATTR,AM)
*----
TAG.FILE.PR:
*----
PRINT "Enter Tag File    :":
INPUT TAG.FILE.NAME
OPEN TAG.FILE.NAME TO TAG.FILE ELSE PRINT BELL:"Unable to Open ":TAG.FILE.NAME; GOTO TAG.FILE.PR
*----
CLR.FLAG.PR:
*----
PRINT "Clear Tag File ?  :":
INPUT CLR.FLAG
IF CLR.FLAG # 'Y' AND CLR.FLAG # 'N' THEN PRINT BELL:"Must be a 'Y' or 'N'";GOTO CLR.FLAG.PR
*
IF CLR.FLAG = 'Y' THEN CLEARFILE TAG.FILE
SELECT SRC.FILE
DONE = FALSE
REC.COUNT = 0
LOOP
  READNEXT ID ELSE DONE = TRUE
UNTIL DONE DO
  IF REM(REC.COUNT, 100) = 0 THEN PRINT REC.COUNT
  REC.COUNT = REC.COUNT + 1
  READ REC FROM SRC.FILE, ID THEN
    TAG.ID = ''
    FOR I = 1 TO NUM.ATTR
      IF ATTR<I> = 0 THEN
        T.ATTR = ID
      END ELSE
        T.ATTR = REC<ATTR<I>>
      END
      IF OCONVS<I> # '' THEN T.ATTR = OCONV(T.ATTR, OCONVS<I>)
      TAG.ID = TAG.ID:'*':T.ATTR
    NEXT I
    IF TAG.ID[1,1] = '*' THEN TAG.ID = TAG.ID[2,999]
    NUM.TAG = DCOUNT(TAG.ID, VM)
    FOR I = 1 TO NUM.TAG
      READ TAG.REC FROM TAG.FILE, TAG.ID<1,I> ELSE TAG.REC = ''
      LOCATE(ID, TAG.REC, 1;T.POS;"AL") ELSE
        TAG.REC = INSERT(TAG.REC, 1, T.POS;ID)
      END
      WRITE TAG.REC ON TAG.FILE, TAG.ID<1,I>
    NEXT I
  END
REPEAT
