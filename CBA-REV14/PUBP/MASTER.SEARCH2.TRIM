OPEN "DEVDEVBP" TO DEVBP ELSE
  PRINT "UNABLE TO OPEN DEVBP IN DEV ACCT"
  STOP
END
OPEN "MD" TO MD ELSE PRINT "NO MD " ; STOP
OPEN "REV10DEVBP" TO REV10DEVBP ELSE
  PRINT "UNABLE TO OPEN REV10DEVBP IN REV ACCT"
  STOP
END
INCLUDE INCS GLOBAL.EQUATES
PLIST="APS"
PLIST<-1>="ARS"
PLIST<-1>="CBA"
PLIST<-1>="CNV"
PLIST<-1>="EP4"
PLIST<-1>="EPS"
PLIST<-1>="FAS"
PLIST<-1>="GLS"
PLIST<-1>="ICS"
PLIST<-1>="JCS"
PLIST<-1>="JES"
PLIST<-1>="NDC"
PLIST<-1>="OPS"
PLIST<-1>="PMC"
PLIST<-1>="POS"
PLIST<-1>="PR4"
PLIST<-1>="PRS"
PLIST<-1>="PSS"
PLIST<-1>="QPL"
PLIST<-1>="SAS"
PLIST<-1>="UTL"
*PLIST<-1>="REPORT.PROC.FILE"
*PLIST<-1>="PRIMAC.MENUS"
NUM.FILES = DCOUNT(PLIST,AM)
*
*
FOR I = 1 TO NUM.FILES
  CURR.MOD = PLIST<I>
  PRINTER ON
  PRINT
  PRINT
  PRINT "PROCESSING ":CURR.MOD
  PRINTER OFF
  PRINT "PROCESSING ":CURR.MOD
*
*   PROCESS BP , PROC, SCREENS, VOC
  IF CURR.MOD = 'REPORT.PROC.FILE' OR CURR.MOD = 'PRIMAC.MENUS' THEN
* HANDLE SPECIAL CASES
*    SPECIAL CASES CODE
*
  END ELSE
    IF CURR.MOD # 'CBA' THEN
      ORIGVOC = "REV10-":CURR.MOD:"/VOC"
      NEWVOC = "REV10A-":CURR.MOD:"/VOC"
      FNAME1 = NEWVOC
      FNAME2 = ORIGVOC
*      GOSUB 1000;* PROCESS FNAME1 AND FNAME2
      P1 = "CBA-REV10/"
      P2 = "CBA-REV10A/"
      ORIGBP = P1:CURR.MOD:"BP"
      NEWBP = P2:CURR.MOD:"BP"
      FNAME1=NEWBP;FNAME2=ORIGBP
      ONAME=CURR.MOD:"BP"
      GOSUB 1000
      ORIGPROCS = P1:CURR.MOD:"PROCS"
      NEWPROCS  = P2:CURR.MOD:"PROCS"
      FNAME1=NEWPROCS;FNAME2=ORIGPROCS
      ONAME=CURR.MOD:"PROCS"
      GOSUB 1000
      ORIG.SCREENS = P1:CURR.MOD:".SCREENS"
      NEW.SCREENS  = P2:CURR.MOD:".SCREENS"
      FNAME1=NEW.SCREENS;FNAME2 = ORIG.SCREENS
      ONAME=CURR.MOD:".SCREENS"
      GOSUB 1000
      ORIG.CPYLIB = P1:CURR.MOD:".CPYLIB"
      NEW.CPYLIB = P2:CURR.MOD:".CPYLIB"
      FNAME1=NEW.CPYLIB ; FNAME2=ORIG.CPYLIB
      ONAME=CURR.MOD:".CPYLIB"
      GOSUB 1000
    END ELSE
*      IN THE CBA MOD COMPARE MENUS, REPORT.PROC.FILE AND VOC
      FNAME1="CBA-REV10A/PRIMAC.MENUS"
      FNAME2="CBA-REV10/PRIMAC.MENUS"
      ONAME="PRIMAC.MENUS"
      GOSUB 1000
      FNAME1="REV10A/REPORT.PROC.FILE"
      FNAME2="REV10/REPORT.PROC.FILE"
      ONAME="REPORT.PROC.FILE"
      GOSUB 1000
    END
  END
NEXT I
*
*
STOP
*
1000 ***
CTR = 0
MCTR=0
*
*    FNAME1 AND FNAME2 ARE IMPORTED AND TO BE PROCESSED
*
CMD1 = 'SETFILE /usr/ud/primac/rev10/basea/':FNAME1:" TEMPQNAME1 OVERWRITING"
CMD2 = 'SETFILE /usr/ud/primac/rev10/base/':FNAME2:" TEMPQNAME2 OVERWRITING"
UDTEXECUTE CMD1 CAPTURING ERRMSG1
ERR.PRINTED=0
IF ERRMSG1[1,7] = "No such" THEN
  ERR.PRINTED=1
   PRINTER ON
   PRINT SPACE(3):FNAME1:"  Unable to Open"
   PRINTER OFF
END
READ V1 FROM MD,"TEMPQNAME1" THEN
  IF V1<3> = "" THEN V1<3> = "D_VOC"
  WRITE V1 ON MD,"TEMPQNAME1" 
END
UDTEXECUTE CMD2 CAPTURING ERRMSG2
IF ERRMSG2[1,7] = "No such" THEN
   PRINTER ON
   ERR.PRINTED=1
   PRINT SPACE(3):FNAME2:" Unable to Open"
   PRINTER OFF
END
IF ERR.PRINTED THEN RETURN
READ V1 FROM MD,"TEMPQNAME2" THEN
  IF V1<3> = "" THEN V1<3> = "D_VOC"
  WRITE V1 ON MD,"TEMPQNAME2"
END
PRINT CMD1
PRINT CMD2
SELECTVERB="SSELECT TEMPQNAME1"
DONE=0
PRINTER ON
*  OPEN THE FILES TO FILEVAR1 AND FILEVAR2
OPEN "TEMPQNAME1" TO FILEVAR1 ELSE
  PRINT "UNABLE TO OPEN ":FNAME1
  RETURN
END
OPEN "TEMPQNAME2" TO FILEVAR2 ELSE
  PRINT "UNABLE TO OPEN ":FNAME2
  RETURN
END
PRINTER OFF
SCREEN.ARRAY=''
PRINT.ONAME=TRUE
LEN.ONAME=LEN(ONAME)
IF ONAME[LEN.ONAME-7,8]='.SCREENS' THEN
  ISA.SCREEN=TRUE
END ELSE
  ISA.SCREEN=FALSE
END
PRINT SELECTVERB
UDTEXECUTE SELECTVERB
PRINTER ON
LOOP
  READNEXT ID ELSE DONE=1
UNTIL DONE DO
  CTR=CTR+1
  IF ID[1,1] # "_" THEN
    READ REC1 FROM FILEVAR1,ID THEN
      FOUND.SCREEN=TRUE
      IF ISA.SCREEN THEN
        SCRNAME=ONAME:"|":FIELD(ID,"*",1)
        LOCATE SCRNAME IN SCREEN.ARRAY<1>,1 SETTING POS ELSE
          SCREEN.ARRAY = INSERT(SCREEN.ARRAY,1,POS;SCRNAME)
          FOUND.SCREEN=FALSE
        END
      END
      READ REC2 FROM FILEVAR2,ID THEN
        IF REC1 # REC2 THEN
*            THIS ITEM HAS CHANGED"
          IF PRINT.ONAME THEN
            PRINT SPACE(3):ONAME
            PRINT.ONAME=FALSE
          END
          IF ISA.SCREEN THEN
            IF NOT(FOUND.SCREEN) THEN
              PRINT SPACE(6):SCRNAME
            END
          END ELSE
            PRINT SPACE(6):ID
          END
          MCTR=MCTR+1
        END ELSE
*          THEN ITEMS ARE THE SAME
        END
      END ELSE
*          THE ITEM ONLY EXIST IN FILEVAR1
        IF PRINT.ONAME THEN
          PRINT SPACE(3):ONAME
          PRINT.ONAME=FALSE
        END
          IF ISA.SCREEN THEN
            IF NOT(FOUND.SCREEN) THEN
              PRINT SPACE(6):SCRNAME     
            END
          END ELSE
            PRINT SPACE(6):ID:STR("-",40-LEN(ID)):" MISSING"
          END
      END
    END ELSE
*     THIS SHOULD NEVER OCCUR
      PRINT SPACE(4):"**************":"ITEM IN SELECT LIST NOT IN FILE??? ":FNAME1:" ":ID
    END
  END
REPEAT
*PRINT SPACE(2):"FOR FILE ":FNAME1:" PROCESSED ":CTR:" RECORDS AND ":MCTR:" MISMATCHED"
PRINT SPACE(3):ONAME:"  *** PROCESSED ":CTR:" MISSMATCH ":MCTR
PRINTER OFF
RETURN

