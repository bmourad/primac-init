* This macro is used to define the specified screen name to the system.
*
* PARAM 1 = SCREEN NUMBER
*       2 = SCREEN NAME
*       3 = SCREEN FILENAME
*
      S$SCR=\1\
      S$NAME<S$SCR>="\2\"
      S$FILE(S$SCR)="\3\"
      S$FVAR(S$SCR)=\3\
      IF TCC.REC(6) = "STD" THEN
         READ S$FORMAT(S$SCR) FROM S$FVAR(S$SCR), "\2\*FRM" ELSE
            PRINT @(0,23):CL:"INVALID SCREEN - \2\":
            INPUT REPLY,1:
            PRINT CS:
            STOP
         END
         READ S$CLEAR(S$SCR) FROM S$FVAR(S$SCR),"\2\*CLR" ELSE S$CLEAR(S$SCR)=""
      END ELSE
         READ S$DREC FROM S$FVAR(S$SCR), "\2\*DEF" THEN
            S$RCNT = DCOUNT(S$DREC,AM)
            S$FORMAT(S$SCR) = CS
            IF TCC.REC(1) = "STD" OR GRAPH.REC(17) = "-" THEN
               FOR S$R = 1 TO S$RCNT
                  S$Y = S$DREC<S$R,1>; S$X = S$DREC<S$R,2>; S$ML = S$DREC<S$R,3>
                  S$FORMAT(S$SCR) = S$FORMAT(S$SCR):@(S$X,S$Y):S$ML
               NEXT S$R
            END ELSE
               FOR S$R = 1 TO S$RCNT
                  S$Y = S$DREC<S$R,1>; S$X = S$DREC<S$R,2>; S$ML = S$DREC<S$R,3>
                  S$POS = INDEX(S$ML,"--",1)
                  IF S$POS THEN
                     S$YL = LEN(S$ML)
                     IF S$ML = STR("-",S$YL) THEN
                        S$FORMAT(S$SCR) = S$FORMAT(S$SCR):@(S$X,S$Y):GRAPH.REC(15):STR(GRAPH.REC(17),S$YL):GRAPH.REC(16)
                     END ELSE
                        IF S$POS > 1 THEN
                           S$FORMAT(S$SCR) = S$FORMAT(S$SCR):@(S$X,S$Y):S$ML[1,S$POS-1]
                        END ELSE
                           S$FORMAT(S$SCR) = S$FORMAT(S$SCR):@(S$X,S$Y)
                        END
                        S$BLEN = 2; S$POS = S$POS + 2
                        FOR S$P = S$POS TO S$YL
                           S$CHR = S$ML[S$P,1]
                           BEGIN CASE
                           CASE S$CHR = "-"
                              S$BLEN = S$BLEN + 1
                           CASE S$BLEN
                              S$FORMAT(S$SCR) = S$FORMAT(S$SCR):GRAPH.REC(15):STR(GRAPH.REC(17),S$BLEN):GRAPH.REC(16):S$CHR
                              S$BLEN = 0
                           CASE 1
                              S$FORMAT(S$SCR) = S$FORMAT(S$SCR):S$CHR
                           END CASE
                        NEXT S$P
                        IF S$BLEN THEN
                           S$FORMAT(S$SCR) = S$FORMAT(S$SCR):GRAPH.REC(15):STR(GRAPH.REC(17),S$BLEN):GRAPH.REC(16)
                        END
                     END
                  END ELSE
                     S$FORMAT(S$SCR) = S$FORMAT(S$SCR):@(S$X,S$Y):S$ML
                  END
               NEXT S$R
            END
         END ELSE
            READ S$FORMAT(S$SCR) FROM S$FVAR(S$SCR), "\2\*FRM" ELSE
               PRINT @(0,23):CL:"INVALID SCREEN - \2\":
               INPUT REPLY,1:
               PRINT CS:
               STOP
            END
         END
         READ S$DREC FROM S$FVAR(S$SCR), "\2\*DCL" THEN
            S$RCNT = DCOUNT(S$DREC,AM)
            S$CLEAR(S$SCR) = ""
            FOR S$R = 1 TO S$RCNT
               S$ML = S$DREC<S$R,3>
               IF NUM(S$ML) THEN
                  S$CLEAR(S$SCR) = S$CLEAR(S$SCR):@(S$DREC<S$R,2>,S$DREC<S$R,1>):SPACE(S$ML)
               END ELSE
                  S$CLEAR(S$SCR) = S$CLEAR(S$SCR):@(S$DREC<S$R,2>,S$DREC<S$R,1>):CL
               END
            NEXT S$R
         END ELSE
            READ S$CLEAR(S$SCR) FROM S$FVAR(S$SCR),"\2\*CLR" ELSE S$CLEAR(S$SCR)=""
         END
         S$DREC = ""
      END
