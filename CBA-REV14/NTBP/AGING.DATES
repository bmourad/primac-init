*COPY>CPYLIB>COM1
********************************************************************
*
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* SOURCE         - ARSBP
*
* PROGRAM        - AGING.DATES
*
* BY             - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE           - 05/28/85
*
* DESCRIPTION    -
* This program will maintain and update CONTROL (CONO:"AGING") file.
*
*T21177 diane 12/04/1996 * Close screen manually
*T25460 Faheem            * Ignoring The Available Extra Prompts From ePrimac

*ENDDOC
********************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- DIMENTION RECORD
*
   DIM DATE.REC(4)
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
   OPEN '','ARS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'M.SCREENS FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
*     MAT EDIT.COM = ""
   MAT DATE.REC = ""
   MAT EDIT.COM.DRIVER = ""
   ERRMSG = ""
*
*---- GET COMPANY NUMBER
*
   CONO = ""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
   MAT EDIT.COM.DRIVER = ""
*
*---- PRINT SCREEN
*
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'AGING.DATES'
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ""
   ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
   SCV.REC(5)<ECD.SCRN.NO,1>=DATE()
   ECD.NUM=5
   ECD.ACTION=5;CALL SCRN.EDIT
   NEW.AGING = "N"
   MATREAD DATE.REC FROM CONTROL,CONO:"AGING" ELSE
      NEW.AGING = "Y"
   END
   IF NEW.AGING = "Y" THEN
      FOR AA = 1 TO 4
         ON AA GOSUB 10,20,30,40
         IF ECD.RET.VALUE = "END" THEN
            IF AA = 1 THEN GOTO 99990
            RELEASE CONTROL,CONO:"AGING"
            GOSUB 300;GOTO 5
         END
      NEXT AA
   END ELSE
   *T25460 v
   * READ PROC VALUES WITH PROCREAD IF COUNT > 10 THEN WRITE TO CONTROL FILE AND GOTO 99990 ELSE EXECUTE FOLLOWING STEPS
	PROCREAD ARSDATES THEN
		IF ARSDATES<9> = 1 THEN
			 READ DUMMYDATES FROM CONTROL,"GOTDATES" THEN
		   IF DUMMYDATES<1> # "" THEN 
		       GOTO 99990	
		     * GOTO 99999
		   END
		END
	END
*T25460 ^

      GOSUB 100
   END
   GOSUB 200
   GOTO 99990
*
*---- ENTER OLDEST DATE
*
10*
   ECD.NUM = 1
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      DATE.REC(1) = ECD.RET.VALUE
   END 
   RETURN
*
*---- ENTER SECOND DATE
*
20*
   ECD.NUM = 2;SCV.REC(2)<ECD.SCRN.NO,1> = DATE.REC(1) + 30
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      IF ECD.RET.VALUE < DATE.REC(1) THEN
         ERRMSG = "DATE SHOULD BE AFTER PREVIOUS DATE"
         GOSUB 91000;GOTO 20
      END
      DATE.REC(2) = ECD.RET.VALUE
   END 
   RETURN
*
*---- ENTER THIRD DATE
*
30*
   ECD.NUM = 3;SCV.REC(3)<ECD.SCRN.NO,1> = DATE.REC(1) + 60
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      IF ECD.RET.VALUE < DATE.REC(2) THEN
         ERRMSG = "DATE SHOULD BE AFTER PREVIOUS DATE"
         GOSUB 91000;GOTO 30
      END
      DATE.REC(3) = ECD.RET.VALUE
   END 
   RETURN
*
*---- ENTER FOURTH DATE
*
40*
   ECD.NUM = 4;SCV.REC(4)<ECD.SCRN.NO,1> = DATE.REC(1) + 90
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      IF ECD.RET.VALUE < DATE.REC(3) THEN
         ERRMSG = "DATE SHOULD BE AFTER PREVIOUS DATE"
         GOSUB 91000;GOTO 40
      END
      DATE.REC(4) = ECD.RET.VALUE
   END 
   RETURN
*
*---- GET VALUES FROM FILE
*
100*
   SCV.REC(1)<ECD.SCRN.NO,1> = DATE.REC(1)
   SCV.REC(2)<ECD.SCRN.NO,1> = DATE.REC(2)
   SCV.REC(3)<ECD.SCRN.NO,1> = DATE.REC(3)
   SCV.REC(4)<ECD.SCRN.NO,1> = DATE.REC(4)
   ECD.ACTION = 3;CALL SCRN.EDIT
   RETURN
*
*---- ENTER OPTIONS
*
200*
   MORE = 0
   LOOP
      ECD.NUM = 6
      ECD.ACTION = 4; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = 'END' OR OPTION = 'E'
            MORE = 1
         CASE NUM(OPTION)
            ON OPTION GOSUB 10,20,30,40
         CASE OPTION = 'A'
            FOR AA = 1 TO 4
               ON AA GOSUB 10,20,30,40
               IF ECD.RET.VALUE = "END" THEN
                  GOSUB 100;GOTO 299
               END
            NEXT AA
         CASE OPTION = 'F'
            GOSUB 400
            MORE = 1
      END CASE
299*
   WHILE MORE = 0 DO REPEAT
   RETURN
*
*---- CLEAR SCREEN AND RE-INITIALIZE
*
300*
   ECD.ACTION = 6; CALL SCRN.EDIT
   MAT SCV.REC = ""
   RETURN
*
*---- UPDATE TERMS FILE
*
400*
   MATWRITE DATE.REC ON CONTROL,CONO:"AGING"
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
   GOTO 99999
*
99990 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
*
99999*
*      PRINT @(-1)
   IF OPTION = "E" OR OPTION = "END" THEN
      LINE = ""
      LINE<1> = "END"
      PROCWRITE LINE
   END
END
