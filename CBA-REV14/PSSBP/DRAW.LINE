   SUBROUTINE DRAW.LINE(MAT WCOM)
*********************************************************************
*
* PROGRAM - DRAW.LINE
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 06/01/91
*
* DESCRIPTION
*
* This subroutine is used to draw a line within the specified window.
*
* GENERAL RULES:
*
* If the width  is set to 0, the line drawn will be vertical.
* If the height is set to 0, the line drawn will be horizontal.
*
* If neither the width or height are 0, the line drawn will be the
* diagonal of the specified rectangle as defined below.
*
* SPECIFIC RULES:
*
* If the width is positive and the height is 0, the column and row
* represent the left point of the horizontal line.
*
* If the width is negative and the height is 0, the column and row
* represent the right point of the horizontal line.
*
* If the width is 0 and the height is positive, the column and row
* represent the top point of the vertical line.
*
* If the width is 0 and the height is negative, the column and row
* represent the bottom point of the vertical line.
*
* If the width and height are positive, the column and row represent
* the top left corner of the rectangle.
*
* If the width is negative, the column and row represent the top
* right corner of the rectangle.
*
* If the height is negative, the column and row represent the bottom
* left corner of the rectangle.
*
* If the width and height are negative, the column and row represent
* the bottom right corner of the rectangle.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.WND          Window ID              Required
* 
*   WCOM.STYLE        Line style             Optional
*                                            Default = "S"
*                                            Options: S = solid
*                                                     D = dashed
*
*   WCOM.TYPE         Line type              Optional
*                                            Options: V  = scroll
*                                                          vertical
*                                                     H  = scroll
*                                                          horizontal
*                                                     VH = scroll
*                                                          vertical &
*                                                          horizontal
*
*   WCOM.COL          Starting column        Required
*                                            Range:  0 - 499.9999
*
*   WCOM.ROW          Starting row           Required
*                                            Range:  0 - 499.9999
*
*   WCOM.WIDTH        Width                  Optional
*                                            Default = 0
*                                            Range:  -500.0000 to
*                                                    +500.0000
*
*   WCOM.HEIGHT       Height                 Optional
*                                            Default = 0
*                                            Range:  -500.0000 to
*                                                    +500.0000
*
*   WCOM.FCOLOR       Line color             Optional
*                                            Default = BLACK
*
*   WCOM.LWIDTH       Line width             Optional
*                                            Default = 1 pixel
*
*   WCOM.LEVEL        Data level             Optional
*                                            Default = 0
*
*********************************************************************
*
   INCLUDE WINCOM FROM PSSBP
   INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
   EMSG = ""
*
   IF WCOM.WND    = "" THEN EMSG = EMSG : " " : "WCOM.WND"
   IF WCOM.COL    = "" THEN EMSG = EMSG : " " : "WCOM.COL"
   IF WCOM.ROW    = "" THEN EMSG = EMSG : " " : "WCOM.ROW"
*
   BEGIN CASE
      CASE WCOM.FCOLOR = ""
         WCOM.FCOLOR = 0
      CASE NUM(WCOM.FCOLOR)
         IF WCOM.FCOLOR < 0 OR WCOM.FCOLOR > 15 THEN
            EMSG = EMSG : " " : "WCOM.FCOLOR"
         END
      CASE 1
         LOCATE WCOM.FCOLOR IN WCOM.COLORS,1 SETTING C ELSE
            EMSG = EMSG : " " : "WCOM.FCOLOR"
         END
         WCOM.FCOLOR = C-1
   END CASE
*
   WCOM.STATUS = EMSG
*
   IF WCOM.STATUS = "" THEN
      PMSG = BCMD        : "DL"
      PMSG = PMSG : CSEP : WCOM.WND
      PMSG = PMSG : CSEP : WCOM.STYLE
      PMSG = PMSG : CSEP : WCOM.TYPE
      PMSG = PMSG : CSEP : WCOM.COL
      PMSG = PMSG : CSEP : WCOM.ROW
      PMSG = PMSG : CSEP : WCOM.WIDTH
      PMSG = PMSG : CSEP : WCOM.HEIGHT
      PMSG = PMSG : CSEP : WCOM.FCOLOR
      PMSG = PMSG : CSEP : WCOM.LWIDTH
      PMSG = PMSG : CSEP : WCOM.LEVEL
      PMSG = PMSG : ECMD
      PRINT PMSG :
* P_X  = 0 ; P_Y = 23 ; P_VALUE = PMSG ; P_OPT = ""
* CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   END ELSE
      WCOM.STATUS = "(DRAW.LINE) INVALID":WCOM.STATUS
   END
*
*---- RESET PARAMETERS USED
*
   WCOM.CAPTION = ""
   WCOM.STYLE   = ""
   WCOM.TYPE    = ""
   WCOM.COL     = ""
   WCOM.ROW     = ""
   WCOM.WIDTH   = ""
   WCOM.HEIGHT  = ""
   WCOM.FCOLOR  = ""
   WCOM.LWIDTH  = ""
   WCOM.LEVEL   = ""
   RETURN
END
