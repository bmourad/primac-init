      SUBROUTINE WINDOW.INIT(MAT WCOM)
*********************************************************************
*
* PROGRAM  - WINDOW.INIT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/01/91
*
* DESCRIPTION
*
* This subroutine is used initialize screen and menu options.
* This subroutine should be called at the beginning of the main
* program, prior to any other windowing activity.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.CAPTION      Window heading         Optional
*
*   WCOM.FCOLOR       Foreground color       Optional 
*                                            Default = "BLACK"
*
*   WCOM.BCOLOR       Background color       Optional 
*                                            Default = "WHITE"
*
*   WCOM.MSELECT      Multiple select        Optional
*                                            Default = "N"
*
*********************************************************************
*
      INCLUDE WINCOM FROM PSSBP
      INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
      EMSG = ""
*
      BEGIN CASE
         CASE WCOM.FCOLOR = ""
            WCOM.FCOLOR = 0
         CASE NUM(WCOM.FCOLOR)
            IF WCOM.FCOLOR < 0 OR WCOM.FCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.FCOLOR"
            END
         CASE 1
            LOCATE WCOM.FCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.FCOLOR"
            END
            WCOM.FCOLOR = C-1
      END CASE
*
      BEGIN CASE
         CASE WCOM.BCOLOR = ""
            WCOM.BCOLOR = 15
         CASE NUM(WCOM.BCOLOR)
            IF WCOM.BCOLOR < 0 OR WCOM.BCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.BCOLOR"
            END
         CASE 1
            LOCATE WCOM.BCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.BCOLOR"
            END
            WCOM.BCOLOR = C-1
      END CASE
*
      BEGIN CASE
         CASE WCOM.MSELECT = ""
            WCOM.MSELECT = "N"
         CASE WCOM.MSELECT = "Y"
         CASE WCOM.MSELECT = "N"
         CASE 1
            EMSG = EMSG : " " : "WCOM.MSELECT"
      END CASE
*
      WCOM.STATUS = EMSG
*
      IF WCOM.STATUS = "" THEN
         PMSG = BCMD        : "WI"
         PMSG = PMSG : CSEP : WCOM.CAPTION
         PMSG = PMSG : CSEP : WCOM.FCOLOR
         PMSG = PMSG : CSEP : WCOM.BCOLOR
         PMSG = PMSG : CSEP : WCOM.MSELECT
         PMSG = PMSG : ECMD
         PRINT PMSG :
      END ELSE
         WCOM.STATUS = "(WINDOW.INIT) INVALID":WCOM.STATUS
      END
*
*---- RESET PARAMETERS USED
*
      WCOM.CAPTION = ""
      WCOM.FCOLOR = ""
      WCOM.BCOLOR = ""
      WCOM.MSELECT = ""
      WCOM.CURR.WND = "MAIN"
      RETURN
   END
