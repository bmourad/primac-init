SUBROUTINE PAPER.AND.INK.DISPLAY(JOB.ID, DEPT, CCTR, COMP, CONO)
***************************************************************************
* REVISION    - [08.0]
*T21741 larryt 05/28/1997 * New screen for displaying paper and ink
*                           information
*T21741 renee 06/25/1997 * CHECK IN
*T22005 renee 07/22/1997 * Adding CPI mods (21740-21746) to REV10B
*                          standard package.
*ENDDOC
***************************************************************************
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.DIVISION
*COPY>CPYLIB>SCREEN.COM
*COPY>JCS.CPYLIB>JOB
*COPY>JES.CPYLIB>ESTIMATE
*COPY>CPYLIB>CHAR
*
********
* main *
********
GOSUB OPEN.FILES
GOSUB INIT.VARS
GOSUB DISPLAY.SUB
GOSUB USER.INPUT
*
RETURN
*
***********
OPEN.FILES:
***********
* T22005 v (Change from CPI.SCREENS to DEV.SCREENS, but make PSS.SCREENS)
OPEN "","PSS.SCREENS" TO M.SCREENS ELSE
  ERRMSG='PSS.SCREENS FILE IS MISSING'
  GOSUB DISPLAY.ERROR
  GOTO 99999
END
* T22005 ^
*
OPEN "","JOB" TO JOB ELSE
  ERRMSG = "JOB FILE IS MISSING"
  GOSUB DISPLAY.ERROR
  GOTO 99999
END
*
OPEN "","ESTIMATE" TO ESTIMATE ELSE
  ERRMSG = 'ESTIMATE FILE IS MISSING'
  GOSUB DISPLAY.ERROR
  GOTO 99999
END
*
RETURN
*
**********
INIT.VARS:
**********
*
SCREEN DEFINE;PAPER.AND.INK.DISPLAY
SCREENNO = 1
LINES.PER.SCREEN = 12
NEXT.OK = 0
*
IF COMP = "" THEN COMP = 1
*
MATREAD JOB.REC FROM JOB, CONO:JOB.ID ELSE
  MAT JOB.REC =""
END
*
MATREAD EST.REC FROM ESTIMATE, CONO:JOB.EST ELSE
  MAT EST.REC = ""
END
*
PAPER.ID = EST.PROD.INV.ID<1,COMP>
IF PAPER.ID = "" THEN
  PAPER.ID = EST.PROD.OS.PROD<1,COMP>
END
*
QTY.LIST = ""
T.QTY = EST.PROD.PQTY<1,COMP>
NUM.QTY = DCOUNT(T.QTY, "!")
FOR QTY.NO = 1 TO NUM.QTY
  ONE.QTY = FIELD(T.QTY, "!", QTY.NO)
  QTY.LIST<1,-1> = ONE.QTY
NEXT QTY.NO
*
PRICE.LIST = ""
T.PRICE = EST.PROD.PCST<1,COMP>
NUM.PRICE = DCOUNT(T.PRICE, "!")
FOR PRICE.NO = 1 TO NUM.PRICE
  ONE.PRICE = FIELD(T.PRICE, "!", PRICE.NO)
  PRICE.LIST<1,-1> = ONE.PRICE
NEXT PRICE.NO
*
INK.LIST = ""
T.INK = EST.PROD.INK.ID<1,COMP>
NUM.INK = DCOUNT(T.INK, "!")
FOR INK.NO = 1 TO NUM.INK
  ONE.INK = FIELD(T.INK, "!", INK.NO)
  INK.LIST<1,-1> = ONE.INK
NEXT INK.NO
*
SIDES.LIST = ""
T.SIDES = EST.PROD.INK.SIDES<1,COMP>
NUM.SIDES = DCOUNT(T.SIDES, "!")
FOR SIDE.NO = 1 TO NUM.SIDES
  ONE.SIDE = FIELD(T.SIDES, "!", SIDE.NO)
  SIDES.LIST<1,-1> = ONE.SIDE
NEXT SIDE.NO
*
COVER.LIST = ""
T.COVER = EST.PROD.INK.COVER<1,COMP>
NUM.COVER = DCOUNT(T.COVER, "!")
FOR COVER.NO = 1 TO NUM.COVER
  ONE.COVER = FIELD(T.COVER, "!", COVER.NO)
  COVER.LIST<1,-1> = ONE.COVER
NEXT COVER.NO
*
RETURN
*
************
DISPLAY.SUB:
************
S$DATA(2)<S$SCR> = "1"
S$DATA(3)<S$SCR> = DEPT
S$DATA(4)<S$SCR> = CCTR
S$DATA(5)<S$SCR> = PAPER.ID
S$DATA(20)<S$SCR> = JOB.ID
S$DATA(21)<S$SCR> = OCONV(JOB.TRACK.DATE<1,4>, "D2/")
S$DATA(22)<S$SCR> = OCONV(DATE(), "D2/")
*
SCREEN FORMAT
*
SCREEN DISPLAY;;ALL
*
GOSUB DISPLAY.PAPER
*
GOSUB DISPLAY.INK
*
RETURN
*
**************
DISPLAY.PAPER:
**************
FOR XX = 1 TO LINES.PER.SCREEN
  S$DATA(6)<S$SCR,XX> = ""
  S$DATA(7)<S$SCR,XX> = ""
NEXT XX
*
START.POS = ((SCREENNO - 1) * LINES.PER.SCREEN) + 1
END.POS = (START.POS + LINES.PER.SCREEN) - 1 
*
FOR XX = START.POS TO END.POS
  S$DATA(6)<S$SCR,XX> = QTY.LIST<1,XX>"R,#10"
  TEMP.VAL = PRICE.LIST<1,XX>
  IF TEMP.VAL # "" THEN TEMP.VAL = (TEMP.VAL/100)"R2#7"
  S$DATA(7)<S$SCR,XX> = TEMP.VAL
  S$VAL = XX
  SCREEN DISPLAY;;6
  S$VAL = XX
  SCREEN DISPLAY;;7
NEXT XX
*
NEXT.OK = 0
IF END.POS < DCOUNT(QTY.LIST, @VM) THEN
  NEXT.OK = 1
END
*
RETURN
*  
************
DISPLAY.INK:
************
FOR XX = 1 TO LINES.PER.SCREEN
  S$DATA(8)<S$SCR,XX> = ""
  S$DATA(9)<S$SCR,XX> = ""
  S$DATA(10)<S$SCR,XX> = ""
NEXT XX
*
START.POS = ((SCREENNO - 1) * LINES.PER.SCREEN) + 1
END.POS = (START.POS + LINES.PER.SCREEN) - 1 
*
FOR XX = START.POS TO END.POS
  S$DATA(8)<S$SCR,XX> = INK.LIST<1,XX>
  S$DATA(9)<S$SCR,XX> = SIDES.LIST<1,XX>
  S$DATA(10)<S$SCR,XX> = COVER.LIST<1,XX>
  S$VAL = XX
  SCREEN DISPLAY;;8
  S$VAL = XX
  SCREEN DISPLAY;;9
  S$VAL = XX
  SCREEN DISPLAY;;10
NEXT XX
*
RETURN
*  
***********
USER.INPUT:
***********
MORE = 1
*
LOOP
  * Get user input...
  SCREEN FIELD;;1
  SCREEN INPUT;;1
  ACTION = S$VALUE
  *
  BEGIN CASE
    CASE ACTION = 'E' OR ACTION = "END" OR ACTION = ""
      MORE = 0
    CASE ACTION = 'S'
      IF NEXT.OK = 1 THEN
        SCREENNO = SCREENNO + 1
        GOSUB DISPLAY.PAPER
      END ELSE
        IF SCREENNO > 1 THEN
          SCREENNO = 1
          GOSUB DISPLAY.PAPER
        END
      END
    CASE 1
      ERRMSG = '"':ACTION[1,5]:'" is Not a Valid Response'
      GOSUB DISPLAY.ERROR
  END CASE
WHILE MORE DO 
  *
REPEAT
SCREEN CLOSE
*
RETURN
*
********************************************************
*
**************
DISPLAY.ERROR:
**************
PRINT @(0,23):ERRMSG:
INPUT REPLY:
PRINT @(0,23):SPACE(79):
*
RETURN
*
99999*
END
