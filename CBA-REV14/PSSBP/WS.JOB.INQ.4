      SUBROUTINE WS.JOB.INQ.4(CONO, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.JOB.INQ.4
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/20/93
*
* DESCRIPTION
*
* This subroutine is used to display the job specifications for
* the specified job.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- OPEN ALL FILES
*
      OPEN "","ESTIMATE" TO ESTIMATE ELSE
         WCOM.STATUS = "Cannot open ESTIMATE file"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","ESTIMATE.MATL" TO ESTIMATE.MATL ELSE
         WCOM.STATUS = "Cannot open ESTIMATE.MATL file"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","JOB.CATEGORY" TO JOB.CATEGORY ELSE
         WCOM.STATUS = "Cannot open JOB.CATEGORY file"
         GOSUB 90000
         GOTO 99999
      END
      OPEN "","SALESMAN" TO SALESMAN ELSE
         WCOM.STATUS = "Cannot open SALESMAN file"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      JOBNO = WIN.CURR.JOB
      MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job Schedule # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      MATREAD PJBS.REC FROM PEND.JOB.SCHED, CONO:JOBNO ELSE MAT PJBS.REC = ""
      MATREAD JOB.REC FROM JOB, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      IF JOB.EST = "" THEN
         MAT EST.REC = ""
      END ELSE
         MATREAD EST.REC FROM ESTIMATE, CONO:JOB.EST ELSE
            MAT EST.REC = ""
         END
      END
      IF JOB.SLSMN = "" THEN
         MAT SALESMAN.REC = ""
      END ELSE
         MATREAD SALESMAN.REC FROM SALESMAN, CONO:JOB.SLSMN ELSE
            MAT SALESMAN.REC = ""
         END
      END
      IF JOB.CATG = "" THEN
         MAT JCATG.REC = ""
      END ELSE
         MATREAD JCATG.REC FROM JOB.CATEGORY, CONO:JOB.CATG ELSE
            MAT JCATG.REC = ""
         END
      END
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
         MAT CUST.REC = ""
         CUST.NAME = JBS.CUST.NAME
      END
      MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:"I" ELSE MAT ESTM.REC = ""
*
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      WIN.JSEL<WPTR> = WIN.CURR.JOB
      LOCATE WCOM.WND IN WIN.SEL.ID,1 SETTING SPTR ELSE WIN.SEL.ID<SPTR>=WCOM.WND
      WIN.SEL.JOB<SPTR>  = WIN.CURR.JOB
      READ TP.DIV FROM PSS.LOCK, @TTY ELSE TP.DIV = ""
*
      SP = SPACE(1)
*
*---- OPEN WINDOW
*
      WW = 75
      HEAD.CNT = 6
      COL2 = 0
      ROW2 = HEAD.CNT
      WCOM.CAPTION = "Job Specifications"
      WCOM.COL = 1
      WCOM.ROW = "C"
      WCOM.WIDTH = WW
      WCOM.HEIGHT = 16
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "IWHITE"
      CALL OPEN.WINDOW(MAT WCOM)
      WCOM.COL = COL2
      WCOM.ROW = ROW2
      CALL SET.HSCROLL.AREA(MAT WCOM)
      WCOM.ROW = ROW2+1
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY DEMOGRAPHIC DATA
*
      WCOM.COL=1; WCOM.ROW=0; WCOM.DATA = "Job #   : ":JOBNO
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=1; WCOM.DATA = "Estimate: ":JOB.EST
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=2; WCOM.DATA = "Quantity: ":OCONV(JOB.QTY<1,1>,"MD0,")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=3; WCOM.DATA = "Customer: ":CUST.NAME
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=1; WCOM.ROW=5; WCOM.DATA = JOB.DESC<1,1>
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=0; WCOM.DATA = "Category: ":JCATG.DESC
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=1; WCOM.DATA = "Salesman: ":SALS.NAME
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=35; WCOM.ROW=2; WCOM.DATA = "Due Date: ":OCONV(JOB.TRACK.DATE<1,4>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY COMPONENT DETAIL
*
      CCNT = DCOUNT(EST.PROD.DESC,VM)
      FOR COMP = 1 TO CCNT
         WCOM.TYPE = "H"
         WCOM.COL = (COMP-1)*WW
         WCOM.ROW = ROW2
         WCOM.WIDTH = WW
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.DATA = "Component ":COMP:" of ":CCNT
         WCOM.ALIGN = "CH"
         CALL LOAD.DATA(MAT WCOM)
         GOSUB 1000
         IF COMP = 1 THEN GOSUB 3000
         FOR PPTR = 1 TO PCNT
            IF PREC<PPTR>[1,1] = "*" THEN
               WCOM.TYPE = "HV"
               WCOM.COL = (COMP-1)*WW
               WCOM.ROW = ROW2+PPTR
               WCOM.WIDTH = WW
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
               WCOM.DATA = PREC<PPTR>[2,99]
               WCOM.ALIGN = "CH"
               CALL LOAD.DATA(MAT WCOM)
            END ELSE
               WCOM.TYPE = "HV"
               WCOM.COL = (COMP-1)*WW
               WCOM.ROW = ROW2+PPTR
               WCOM.DATA = PREC<PPTR>
               CALL LOAD.DATA(MAT WCOM)
            END
         NEXT PPTR
         IF COMP > 1 THEN
            WCOM.TYPE = "H"
            WCOM.COL = (COMP-1)*WW
            WCOM.ROW = ROW2+1
            WCOM.WIDTH = 0
            WCOM.HEIGHT = 16-HEAD.CNT-1
            CALL DRAW.LINE(MAT WCOM)
         END
      NEXT COMP
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- FORMAT DATA
*
1000*
      PREC = ""; PCNT = 0
      IF EST.PROD.OS.TYPE<1,COMP> # "" THEN
         WEB.CNT = EST.PROD.WEB.CNT<1,COMP>+0
         IF WEB.CNT = 0 THEN WEB.CNT = 1
         FOR WPTR = 1 TO WEB.CNT
            X1 = EST.PROD.US.WIDTH<1,COMP,WPTR>/10000
            X2 = "x":EST.PROD.US.LENGTH<1,COMP,WPTR>/10000
            X3 = EST.PROD.DESC<1,COMP>
            IF WPTR = 1 THEN
               PCNT = PCNT + 1
               IF X3 = COMP THEN
                  PREC<PCNT> = ""
               END ELSE
                  PREC<PCNT> = " ":X3:" - "
               END
            END ELSE
               PCNT = PCNT + 1
               PREC<PCNT> = ("Web ":WPTR:": ")"R#12"
            END
            PREC<PCNT> = PREC<PCNT>:"Flat Size: ":X1:X2
            X1 = " ":EST.PROD.TS.WIDTH<1,COMP,WPTR>/10000
            X2 = "x":EST.PROD.TS.LENGTH<1,COMP,WPTR>/10000
            PREC<PCNT> = PREC<PCNT>:"; ":"Final Size: ":X1:X2
*
            X1 = EST.PROD.OS.WIDTH<1,COMP,WPTR>/10000
            X2 = EST.PROD.OS.LENGTH<1,COMP,WPTR>/10000
            IF X2 = 0 THEN
               X1 = X1:'"'
               X2 = ""
            END ELSE
               X1 = X1
               X2 = "x":X2
            END
            X3 = " ":EST.PROD.BASIS.WT<1,COMP,WPTR>:" lb"
            X4 = " ":EST.PROD.OS.DESC<1,COMP,WPTR>
            IF EST.PROD.COLOR<1,COMP,WPTR> = "" THEN
               X5 = ""
            END ELSE
               X5 = "; ":EST.PROD.COLOR<1,COMP,WPTR>
            END
            IF EST.PROD.FINISH<1,COMP,WPTR> = "" THEN
               X6 = ""
            END ELSE
               X6 = "; ":EST.PROD.FINISH<1,COMP,WPTR>
            END
            PCNT = PCNT + 1
            PREC<PCNT> = ("Stock: ")"R#12":X1:X2:X3:X4:X5:X6
*
            IF EST.PROD.INV.ID<1,COMP,WPTR> # "" THEN
               X1 = " ":EST.PROD.INV.ID<1,COMP,WPTR>
               PCNT = PCNT + 1
               PREC<PCNT> = ("Product: ")"R#12":X1
            END
*
            IF EST.PROD.PSS.OSS<1,COMP,WPTR>+0 > 1 THEN
               X1 = EST.PROD.PSS.OSS<1,COMP,WPTR>:"-OUT"
               X2 = EST.PROD.PS.WIDTH<1,COMP,WPTR>/10000
               X3 = EST.PROD.PS.LENGTH<1,COMP,WPTR>/10000
               IF X3 = 0 THEN
                  X2 = " to ":X2:'"'
                  X3 = ""
               END ELSE
                  X2 = " to ":X2
                  X3 = "x":X3
               END
               PCNT = PCNT + 1
               PREC<PCNT> = ("Cut: ")"R#12":X1:X2:X3
            END
*
            IF EST.PROD.NO.UP<1,COMP,WPTR> = 1 THEN
               X3 = EST.PROD.FORMS<1,COMP,1>:" ":EST.PROD.PGS.PER.FORM<1,COMP,WPTR>:"pp"
            END ELSE
               X3 = EST.PROD.PG.CNT<1,COMP,WPTR>:"PP ":EST.PROD.NO.UP<1,COMP,WPTR>+0:"-UP"
            END
            XX = EST.PROD.MODE<1,COMP,WPTR>
            BEGIN CASE
            CASE XX = "SS"
               X4 = " S/S"
            CASE XX = "WT"
               X4 = " W/T"
            CASE XX = "WR"
               X4 = " W/R"
            CASE XX = "SW"
               X4 = " S/W"
            CASE XX = "P"
               X4 = " P/F"
            END CASE
            PRESS = FIELD(EST.PROD.PRESS.ID<1,COMP>,"-",1)
            MATREAD CCTR.REC FROM COST.CNTR, CONO:PRESS ELSE
               MAT CCTR.REC = ""
               CCTR.DESC = "???????????????"
            END
            X5 = " on ":TRIM(CCTR.DESC)
            X6 = "; ":EST.PROD.COLORS.1<1,COMP,WPTR>+0:"/":EST.PROD.COLORS.2<1,COMP,WPTR>+0
            X7 = "; ":EST.PROD.PLATES<1,COMP,WPTR>+0:" Plates"
            PCNT = PCNT + 1
            PREC<PCNT> = ("Run: ")"R#12":X3:X4:X5:X6:X7
*
            ICNT = DCOUNT(EST.PROD.INK.ID<1,COMP,WPTR>,"!")
            FOR IPTR = 1 TO ICNT
               INK.ID = FIELD(EST.PROD.INK.ID<1,COMP,WPTR>,"!",IPTR)
               LOCATE INK.ID IN ESTM.PROD<1>,1 SETTING IP THEN
                  IDESC = ESTM.DESC<1,IP>
               END ELSE
                  IDESC = INK.ID
               END
               SIDES = FIELD(EST.PROD.INK.SIDES<1,COMP,WPTR>,"!",IPTR)
               COVER = FIELD(EST.PROD.INK.COVER<1,COMP,WPTR>,"!",IPTR)
               IF SIDES = 1 THEN
                  SDMSG = " side  at "
               END ELSE
                  SDMSG = " sides at "
               END
               IF IPTR = 1 THEN
                  PCNT = PCNT + 1
                  PREC<PCNT> = ("Ink: ")"R#12":IDESC"L#30":"  ":SIDES:SDMSG:COVER:"% coverage"
               END ELSE
                  PCNT = PCNT + 1
                  IF SIDES = 1 THEN
                     PREC<PCNT> = """R#12":IDESC"L#30":"  ":SIDES:SDMSG:COVER:"% coverage"
                  END ELSE
                     PREC<PCNT> = """R#12":IDESC"L#30":"  ":SIDES:SDMSG:COVER:"% coverage"
                  END
               END
            NEXT IPTR
         NEXT WPTR
      END
      GOSUB 2000
      RETURN
*
*---- GET COMPONENT INSTRUCTIONS
*
2000*
      DESC = ""
      OP = 0
      CC = DCOUNT(EST.COMMENTS,VM)
      FOR LP = 1 TO CC
         IF EST.COMMENT.TYPE<1,LP> = COMP THEN
            OP = OP + 1
            DESC<1,OP> = EST.COMMENTS<1,LP>
         END
      NEXT LP
      CC = DCOUNT(DESC,VM)
      IF CC > 0 THEN
         PCNT = PCNT + 1
         PREC<PCNT> = "*Component Instructions"
         FOR LP = 1 TO CC
            PCNT = PCNT + 1
            PREC<PCNT> = """L#4":DESC<1,LP>
         NEXT LP
      END
      RETURN
*
*---- BUILD JOB INSTRUCTIONS
*
3000*
      DCNT = DCOUNT(EST.DESC,VM)
      IF DCNT > 0 THEN
         PCNT = PCNT + 1
         PREC<PCNT> = "*Job Instructions"
         FOR DPTR = 1 TO DCNT
            PCNT = PCNT + 1
            PREC<PCNT>="""L#4":EST.DESC.HEADING<1,DPTR>"L#12":SPACE(2):EST.DESC<1,DPTR>
         NEXT DPTR
      END
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA=WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- TRACE ROUTINE
*
95000*
      READU TRACE.REC FROM CONTROL,"WINCOM.TRACE" ELSE TRACE.REC=""
      TRPTR = DCOUNT(TRACE.REC,AM)+1
      TRACE.REC<TRPTR> = TREC
      FOR XPTR = 101 TO TRPTR
         TRACE.REC = DELETE(TRACE.REC,1,0,0)
      NEXT XPTR
      WRITE TRACE.REC ON CONTROL,"WINCOM.TRACE"
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
