      SUBROUTINE TRANSACTION.RECOVER (CONO,DIV,PSS.JOURNAL,ERRMSG)
**************************************************************************
*
* PROGRAM - TRANSACTION.RECOVER
*
* AUTHOR  - NICK AMENDOLA, NASTech, Inc.
*
* DATE    - 05/14/96
*
* DESCRIPTION
*
* This program is used to back out all changes to the database for the
* most recent transaction in progress.
*
*    PARAM        DESCRIPTION
*    -----------  --------------------------------------------------------
*    CONO         Company ID
*    DIV          Division ID
*    PSS.JOURNAL  File variable of Journal file
*    ERRMSG       Status
*
**************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSS.CPYLIB>PSS.JOURNAL
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
      CDATE = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN CDATE = DATE()
      FNAMES = ""
      DIM FVAR(99)
      MAT FVAR = ""
*
*---- MAIN PROCESSING
*
      MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!":DIV ELSE
         RELEASE PSS.JOURNAL, CONO:"CONTROL!":DIV
         ERRMSG = "NO TRANSACTIONS IN PROGRESS"
         GOTO 99999
      END
      TCNT = DCOUNT(JRNLC.TRAN,VM)
      TPTR = TCNT
      BPTR = JRNLC.TPTR<1,TPTR>
      EPTR = BPTR + JRNLC.TCNT<1,TPTR> - 1
      FOR JPTR = EPTR TO BPTR STEP -1
         MATREADU JRNLI.REC FROM PSS.JOURNAL, CONO:"INDEX!":DIV:"!":JPTR ELSE
            DELETE PSS.JOURNAL, CONO:"XDATA!":DIV:"!":JPTR
            RELEASE PSS.JOURNAL, CONO:"INDEX!":DIV:"!":JPTR
            GOTO 199
         END
         LOCATE JRNLI.FNAME IN FNAMES<1>,1 SETTING P ELSE
            FNAMES<1,P> = JRNLI.FNAME
            OPEN JRNLI.FNAME TO FVAR(P) ELSE
               ERRMSG = "CANNOT OPEN FILE - ":JRNLI.FNAME
               GOTO 99999
            END
         END
         IF JRNLI.STATUS = "NEW" THEN
            DELETE PSS.JOURNAL, CONO:"XDATA!":DIV:"!":JPTR
            DELETE FVAR(P), JRNLI.INAME
         END ELSE
            READU BEFORE.IMAGE FROM PSS.JOURNAL, CONO:"XDATA!":DIV:"!":JPTR ELSE
               ERRMSG = "CANNOT LOCATE BEFORE IMAGE - ":JRNLI.FNAME:",":JRNLI.INAME
               GOTO 99999
            END
            WRITE BEFORE.IMAGE ON FVAR(P), JRNLI.INAME
         END
         DELETE PSS.JOURNAL, CONO:"INDEX!":DIV:"!":JPTR
         DELETE PSS.JOURNAL, CONO:"XDATA!":DIV:"!":JPTR
         JRNLC.JCNT = JPTR - BPTR
         JRNLC.TCNT<1,TPTR> = JPTR - BPTR
         MATWRITEU JRNLC.REC ON PSS.JOURNAL, CONO:"CONTROL!":DIV
199   NEXT JPTR
      IF TCNT = 1 THEN
         JRNLC.DATE = CDATE
         JRNLC.TIME = CTIME
         JRNLC.TPTR = 1
         JRNLC.TCNT = 0
         JRNLC.JPTR = 1
         JRNLC.JCNT = 0
      END ELSE
         DEL JRNLC.TRAN<1,TPTR>
         DEL JRNLC.DATE<1,TPTR>
         DEL JRNLC.TIME<1,TPTR>
         DEL JRNLC.DESC<1,TPTR>
         DEL JRNLC.TPTR<1,TPTR>
         DEL JRNLC.TCNT<1,TPTR>
      END
      MATWRITE JRNLC.REC ON PSS.JOURNAL, CONO:"CONTROL!":DIV
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
