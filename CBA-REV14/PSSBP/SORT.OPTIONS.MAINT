SUBROUTINE SORT.OPTIONS.MAINT(SORT.OPTIONS)
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.DIVISION
**************************************************************************
* REVISION    - [08.0]
*T21741 larryt 05/28/1997 * NEW SCREEN FOR PROMPTING SORT OPTIONS
*T21741 renee 06/25/1997 * CHECK IN
*T22102 renee 07/25/1997 * New program created for CPI. I am making
*                          those same CPI changes in the Rev10b base
*                          system package. This screen is called from
*                          GEN.JOB.SCHED.DRIVER. 
*T22991 renee 10/14/1997 * Fixed some bugs in this program: 1) Blew up when
*                          you deleted all sort options. 2) Displayed in-
*                          correctly when you deleted 1 or more sort
*                          options, filed, then come back to sort screen
*                          again. 3) Displayed esc character when ESCAPED
*                          out of sort prompt.
*T22102 renee 03/20/1998 * Making GUI for rev11
**************************************************************************
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSS.CPYLIB>PSS.HIERARCHY
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
DIM SORTS(100)
DIM TYPES(100)
*
********
* main *
********
GOSUB OPEN.FILES
GOSUB INIT.VARS
GOSUB DISPLAY.SUB
*
IF NUM.LINES = 0 THEN
  GOSUB AUTOWALK
END
*
GOSUB USER.INPUT
*
GOTO 99999
*
***********
OPEN.FILES: 
***********
OPEN '','PSS.SCREENS' TO M.SCREENS ELSE 
  ERRMSG='PSS.SCREENS FILE IS MISSING'
  GOSUB DISPLAY.ERROR
  GOTO 99999
END
*
RETURN
*
**********
INIT.VARS: 
**********
SCREEN DEFINE;SORT.OPTIONS.MAINT
*SCREEN FORMAT
*
* Load in sort info that may have been defined from a previous visit to this
*   screen
MAT SORTS = ""
MAT TYPES = ""
NUM.LINES = 0
FOR XX = 1 TO DCOUNT(SORT.OPTIONS, @AM)
  NUM.LINES = NUM.LINES + 1
  SORTS(XX) = SORT.OPTIONS<XX,1>
  TYPES(XX) = SORT.OPTIONS<XX,2>
NEXT XX
*
RETURN
*
************
DISPLAY.SUB: 
************
SCREEN CLEAR
IF NUM.LINES <= 0 THEN RETURN    ; * T22291
*
FOR XX = 1 TO NUM.LINES
  S$DATA(5)<S$SCR,XX> = XX
  S$DATA(6)<S$SCR,XX> = SORTS(XX)
  S$DATA(7)<S$SCR,XX> = TYPES(XX)
NEXT XX
*
SCREEN DISPLAY;;ALL
SCREEN MULTI;;C;5;6;7
*
RETURN
*
*********
AUTOWALK: 
*********
EXIT.NOW = 0
*
LOOP
  NUM.LINES = NUM.LINES + 1
  INPUT.LINE = NUM.LINES
  GOSUB PROMPT.SORT
UNTIL EXIT.NOW OR INPUT.LINE >= 4 DO
REPEAT
*
IF EXIT.NOW THEN NUM.LINES = NUM.LINES - 1
*
RETURN
*
***********
USER.INPUT: 
***********
MORE = 1
*
LOOP
  * Get user input...
  SCREEN FIELD;;2
  SCREEN INPUT;;2
  ACTION = S$VALUE
  *
  BEGIN CASE
    CASE ACTION = 'E' OR ACTION = "END"
      MORE = 0
    CASE ACTION = 'F'
      GOSUB SAVE.CHANGES
      MORE = 0
    CASE ACTION = 'A'
      IF NUM.LINES >= 4 THEN
        ERRMSG = "You May Only Define Four Sort Options"
        GOSUB DISPLAY.ERROR
      END ELSE
        EXIT.NOW = 0
        LOOP
          INPUT.LINE = NUM.LINES + 1
          GOSUB PROMPT.SORT
          IF NOT(EXIT.NOW) THEN NUM.LINES = NUM.LINES + 1
        UNTIL EXIT.NOW OR NUM.LINES >= 4 DO
        REPEAT
      END
    CASE ACTION = 'C'
      GOSUB PROMPT.LINE.NUMBER
      IF LNO >= 1 AND LNO <= NUM.LINES THEN
        INPUT.LINE = LNO
        GOSUB PROMPT.SORT
      END ELSE
        ERRMSG = "Invalid Line Number"
        GOSUB DISPLAY.ERROR
      END
    CASE ACTION = 'D' AND NUM.LINES >= 1
      GOSUB PROMPT.LINE.NUMBER
      IF LNO >= 1 AND LNO <= NUM.LINES THEN
        FOR XX = LNO TO NUM.LINES - 1
          SORTS(XX) = SORTS(XX+1)
          TYPES(XX) = TYPES(XX+1)
        NEXT XX
        SORTS(NUM.LINES) = ""
        TYPES(NUM.LINES) = ""
        NUM.LINES = NUM.LINES - 1
        GOSUB DISPLAY.SUB
      END ELSE
        ERRMSG = "Invalid Line Number"
        GOSUB DISPLAY.ERROR
      END
    CASE ACTION = "D"
      ERRMSG = "No Lines to Delete"
      GOSUB DISPLAY.ERROR
    CASE 1
      ERRMSG = '"':ACTION[1,5]:'" is Not a Valid Response'
      GOSUB DISPLAY.ERROR
  END CASE
WHILE MORE DO 
  *
REPEAT
*
RETURN
*
************
PROMPT.SORT: 
************
S$DATA(5)<S$SCR,INPUT.LINE> = INPUT.LINE
S$VAL = INPUT.LINE
SCREEN DISPLAY;;5
*
S$VAL = INPUT.LINE
SCREEN FIELD;;6
SCREEN INPUT;;6
RESP = S$VALUE
*
IF RESP = "END" THEN
  S$VAL = INPUT.LINE
  IF S$DATA(6)<S$SCR,INPUT.LINE> NE "" THEN ; * T22991
    S$VALUE = S$DATA(6)<S$SCR,INPUT.LINE>   ; * T22991
    SCREEN DISPLAY;;6
  END ELSE                                  ; * T22991
    S$DATA(5)<S$SCR,INPUT.LINE> = ""        ; * T22991
    S$DATA(6)<S$SCR,INPUT.LINE> = ""        ; * T22991
    S$DATA(7)<S$SCR,INPUT.LINE> = ""        ; * T22991
    SCREEN DISPLAY;;5
    SCREEN DISPLAY;;6
    SCREEN DISPLAY;;7
  END                                       ; * T22991
  EXIT.NOW = 1
  GOTO 1999
END ELSE
  SSEQ = RESP
END
*
S$VAL = INPUT.LINE
SCREEN FIELD;;7
SCREEN INPUT;;7
RESP = S$VALUE
*
IF RESP = "END" THEN
  OLD.FSTART.LINE = 0
  S$VAL = INPUT.LINE
  IF S$DATA(7)<S$SCR,INPUT.LINE> NE "" THEN ; * T22991
    S$VALUE = S$DATA(7)<S$SCR,INPUT.LINE>   ; * T22991
    SCREEN DISPLAY;;7
  END ELSE                                  ; * T22991
    S$DATA(5)<S$SCR,INPUT.LINE> = ""        ; * T22991
    S$DATA(6)<S$SCR,INPUT.LINE> = ""        ; * T22991
    S$DATA(7)<S$SCR,INPUT.LINE> = ""        ; * T22991
    SCREEN DISPLAY;;5
    SCREEN DISPLAY;;6
    SCREEN DISPLAY;;7
  END                                       ; * T22991
  GOTO PROMPT.SORT
END ELSE
  STYPE = RESP
  SORTS(NUM.LINES) = SSEQ
  TYPES(NUM.LINES) = STYPE
END
*
1999 *
RETURN
*
*******************
PROMPT.LINE.NUMBER: 
*******************
SCREEN FIELD;;3
SCREEN INPUT;;3
RESP = S$VALUE
IF RESP = 'END' THEN
  LNO = 0
END ELSE
  LNO = RESP
END
*
RETURN
*
*************
SAVE.CHANGES: 
*************
DONE = 0
SORT.OPTIONS = ""    ; * T22291
FOR XX = 1 TO NUM.LINES
  SORT.OPTIONS<XX,1> = SORTS(XX)
  SORT.OPTIONS<XX,2> = TYPES(XX)
NEXT XX
*
RETURN
*
********************************************************
*
**************
DISPLAY.ERROR: 
**************
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*
99999*
SCREEN CLOSE
RETURN
END
