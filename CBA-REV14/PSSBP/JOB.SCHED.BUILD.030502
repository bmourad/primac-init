   SUBROUTINE JOB.SCHED.BUILD.UPD (CONO, ACTION, EST.NUM, JOB.NUM, COMP.NUM, CUST.NAME, LOADING.FLG,DUE.DATE)
*********************************************************************
* REVISION - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - JOB.SCHED.BUILD.UPD
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE     - 05/04/86
* DESCRIPTION
* This program uses the ESTIMATE record and the booked component to
* create the JOB.SCHED record.
* MODIFIED  - October 1992, Graham Jarvis, Pacrim Software Pty Ltd
*           - Program now creates PEND.JOB.SCHED and dummy schedules
*             the Job based on the due date and B/F flag.
* MODIFIED - 02/21/96, NA, TASK 19935, AUTO EXTRACT FROM JOB BOOKING
*T22010 renee 07/24/1997 * Adding open statement for PSS.LOCK. end
*T22102 renee 12/08/1997 * STORE COMP NUM IN JOB.SCHED RECORD
*T25695 walid 03/16/2001 * CHANGE JOB.SCHED.MODE TO BE READ FROM CONTROL
*T25704 edvard 04/03/2001 * CALL TO GET.JBS.USER.FIELDS
*T26342 cmykleb 12/05/2001 * When a sub cost center exists, the job is not
*                            extracting to scheduling.
*T26328 cmykleb 12/11/2001 * When jobs are extracted, the component # is
*                            not always coming over to scheduling.
*T26020 cmykleb 02/06/2002 * Allow consolidation of hours for multiple
*                            components.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.DIVISION
*COPY>JES.CPYLIB>COM.ESTIMATE
*COPY>JCS.CPYLIB>COM.JOB
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>CCTR.AVAIL
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSS.CPYLIB>PSS.HIERARCHY
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
   OPEN '','OPERATION' TO OPERATION ELSE STOP
   OPEN '','PSS.LOCK' TO PSS.LOCK ELSE STOP
*
   READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
   BLK = 0
   ERRMSG = ""
   READ TP.DIV FROM PSS.LOCK, @TTY ELSE TP.DIV = ""
*
*---- MAIN PROCESSING
*
   MAT JBS.REC = ""
   MAT PJBS.REC = ""
   MAT CCTR.REC = ""
   MAT OPER.REC = ""
   JBS.DUE.DATE = DUE.DATE
   JBS.CUST.NAME = CUST.NAME
   JBS.DIV = JOB.DIV
   JBS.CUST.ID = JOB.CUST
   JBS.JOB.COMMENT = JOB.COMMENTS<1,1>
*---- TASK 19935
*T25695 v
*     JBS.SCHED.MODE = "S"
*     JBS.SCHED.DATE = TODAY+1
   IF CO.PSS.BACKWARD.FLAG = "Y" THEN
      JBS.SCHED.MODE = "D"
      JBS.SCHED.DATE = DUE.DATE
   END ELSE
      JBS.SCHED.MODE = "S"
      JBS.SCHED.DATE = TODAY+1
   END
*T25695 ^
*---------------
   CALL GET.JBS.USER.FIELDS (CONO,JOB.NUM,MAT JOB.REC, MAT JBS.REC, XXX,YYY,ZZZ); * 25704
   DCNT = DCOUNT(EST.DEPT,VM)
   IF COMP.NUM = "ALL" THEN
      FOR D = 1 TO DCNT UNTIL BLK
         FOR COMP.NUM = 1 TO EST.COMPONENT.CNT UNTIL BLK
            GOSUB 1000
         NEXT COMP.NUM
      NEXT D
   END ELSE
      COMP.AVAIL = COMP.NUM
      COMP.CNT = DCOUNT(COMP.AVAIL<1,1>,SM)
      FOR D = 1 TO DCNT UNTIL BLK
         FOR C = 1 TO COMP.CNT UNTIL BLK
            COMP.NUM = COMP.AVAIL<1,1,C>
            GOSUB 1000
         NEXT C
      NEXT D
   END
   IF JBS.DEPT = "" OR BLK THEN
      RELEASE JOB.SCHED, CONO : JOB.NUM
      GOTO 999999
   END
   GOSUB 20000
   JCNT = DCOUNT(JBS.DEPT,VM)
   IF LOADING.FLG = "Y" THEN
      JBS.HOLD.CODE<1,1> = "L"
   END ELSE
      JBS.HOLD.CODE<1,1> = "SS"
   END
   MATREAD CCTR.REC FROM COST.CNTR, CONO:JBS.CCTR<1,1> ELSE
      MAT CCTR.REC = ""
   END
   MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:JBS.DEPT<1,1>:"*":JBS.CCTR<1,1> ELSE
      MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:JBS.DEPT<1,1> ELSE
         MAT CCTR.AVAIL.REC = ""
      END
   END
   IF CCTR.SCHED.FLAG = "O" THEN
      JBS.OVER.LOAD<1,1> = "Y"
   END ELSE
      JBS.CONT.SCH<1,1> = CCA.CONT.SCHD
      JBS.SAME.EQUIP<1,1> = CCA.SAME.EQP
   END
   FOR T = 2 TO JCNT
      BEGIN CASE
         CASE LOADING.FLG = "Y"
            JBS.HOLD.CODE<1,T> = "L"
*        CASE JBS.DEPT<1,T-1>[1,2] # JBS.DEPT<1,T>[1,2]
         CASE JBS.DEPT<1,T-1> # JBS.DEPT<1,T>
            MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : JBS.DEPT<1,T-1>[1,2] ELSE
               MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : "HIERARCHY" ELSE
                  MATREAD PSHR.REC FROM PSS.CONTROL, "HIERARCHY" ELSE MAT PSHR.REC = ""
               END
            END
            IF PSHR.TO = "" THEN
               POS = 0
            END ELSE
               LOCATE JBS.DEPT<1,T> IN PSHR.TO<1>,1 SETTING POS ELSE
                  LOCATE JBS.DEPT<1,T>[1,2] IN PSHR.TO<1>,1 SETTING POS ELSE POS = 0
               END
            END
            IF POS THEN
               JBS.HOLD.CODE<1,T> = PSHR.CODE<1,POS>
               JBS.HOLD.DAYS<1,T> = PSHR.DAYS<1,POS>
            END ELSE
               JBS.HOLD.CODE<1,T> = PSHR.DEF.CODE
               JBS.HOLD.DAYS<1,T> = PSHR.DEF.DAYS
            END
         CASE JBS.CCTR<1,T-1> # JBS.CCTR<1,T>
            MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : JBS.DEPT<1,T>[1,2] :"*": JBS.CCTR<1,T-1> THEN
               LOCATE JBS.CCTR<1,T> IN PSHR.TO<1>,1 SETTING POS ELSE POS = 0
               IF POS THEN
                  JBS.HOLD.CODE<1,T> = PSHR.CODE<1,POS>
               END ELSE
                  JBS.HOLD.CODE<1,T> = PSHR.CCTR.CODE
               END
            END ELSE
               MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : JBS.DEPT<1,T>[1,2] ELSE
                  MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : "HIERARCHY" ELSE
                     MATREAD PSHR.REC FROM PSS.CONTROL, "HIERARCHY" ELSE MAT PSHR.REC = ""
                  END
               END
               JBS.HOLD.CODE<1,T> = PSHR.CCTR.CODE
            END
         CASE 1
            JBS.HOLD.CODE<1,T> = "SS"
      END CASE
      BEGIN CASE
         CASE JBS.DEPT<1,T> # JBS.DEPT<1,T-1>
            MATREAD CCTR.REC FROM COST.CNTR, CONO:JBS.CCTR<1,T> ELSE
               MAT CCTR.REC = ""
            END
            MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:JBS.DEPT<1,T>:"*":JBS.CCTR<1,T> ELSE
               MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:JBS.DEPT<1,T> ELSE
                  MAT CCTR.AVAIL.REC = ""
               END
            END
         CASE JBS.CCTR<1,T> # JBS.CCTR<1,T-1>
            MATREAD CCTR.REC FROM COST.CNTR, CONO:JBS.CCTR<1,T> ELSE
               MAT CCTR.REC = ""
            END
            MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:JBS.DEPT<1,T>:"*":JBS.CCTR<1,T> ELSE
               MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:JBS.DEPT<1,T> ELSE
                  MAT CCTR.AVAIL.REC = ""
               END
            END
         CASE 1
      END CASE
      IF CCTR.SCHED.FLAG = "O" THEN
         JBS.OVER.LOAD<1,T> = "Y"
      END ELSE
         JBS.CONT.SCH<1,T> = CCA.CONT.SCHD
         JBS.SAME.EQUIP<1,T> = CCA.SAME.EQP
      END
   NEXT T
***   MATWRITE JBS.REC ON JOB.SCHED, CONO:JOB.NUM
   TPFID = CONO:JOB.NUM
   MATBUILD TPREC FROM JBS.REC
   CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"JOB.SCHED",TPFID,TPREC,JOB.SCHED,PSS.JOURNAL,ERRMSG)
**********************************************************************
*
* Write Job pending record
*
   MAT PJBS.REC = MAT JBS.REC
***   MATWRITE PJBS.REC ON PEND.JOB.SCHED,CONO:JOB.NUM
   TPFID = CONO:JOB.NUM
   MATBUILD TPREC FROM PJBS.REC
   CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"PEND.JOB.SCHED",TPFID,TPREC,PEND.JOB.SCHED,PSS.JOURNAL,ERRMSG)
**********************************************************************
*
   GOTO 999999
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*---- LOAD JBS.REC FROM ESTD.REC
*
1000 *
   KEY = D :"!": COMP.NUM :"!": EST.BOOK.QTY
   LOCATE KEY IN EST.DEPT.COMP<1>,1 SETTING LN ELSE LN = 0
   IF LN = 0 THEN GOTO 1999
   MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.NUM:"!":KEY ELSE MAT ESTD.REC = ""
   TYPE.CNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
   ADD = 1
   FOR T = 1 TO TYPE.CNT UNTIL BLK
      EHOURS = ESTD.HRS<1,T>
      CCTR = ESTD.CCTR<1,T> ; * T26020
      OPER = ESTD.OPV<1,T> ; * T26020
      EQTY = INT(ESTD.QTY<1,T> / 100) * 100 ; * T26020
      IF ESTD.TYPE<1,T> = "L" AND EHOURS+0 > 0 THEN
         MATREAD CCTR.REC FROM COST.CNTR, CONO:ESTD.CCTR<1,T> ELSE
            MAT CCTR.REC = ""
            CCTR.SCHED.FLAG = "N"
         END
         MATREAD OPER.REC FROM OPERATION, CONO:ESTD.OPV<1,T> ELSE
            MAT OPER.REC = ""
            OPER.SCHED.FLG = "N"
         END
         JCNT = COUNT(JBS.DEPT,VM) + (JBS.DEPT # "")
*T26020 v
         OSUB = 0
         COMBINE.COMP = EST.PROD.COMP.COMBINE<1,COMP.NUM>
         IF COMBINE.COMP # "" THEN
            FOR J = 1 TO JCNT UNTIL OSUB
               CHECK.DEPT = JBS.DEPT<1,J>
               CHECK.CCTR = JBS.CCTR<1,J>
               IF CCTR.DEPT = CHECK.DEPT AND CCTR = CHECK.CCTR THEN
                  LOCATE OPER IN JBS.OPER<1,J> SETTING OSUB ELSE OSUB = 0
               END
            NEXT J
         END
*T26020 ^
         BEGIN CASE
            CASE CCTR.SCHED.FLAG = "N" OR OPER.SCHED.FLG = "N"
               EHOURS = 0
*T26020 v
            CASE OSUB GT 0
               JBS.SCH.HRS<1,COMBINE.COMP> += EHOURS
               JBS.OPER.SCH.HRS<1,COMBINE.COMP,OSUB> += EHOURS
               IF ESTD.OPV.TYPE<1,T> = "V" THEN
                  JBS.SCH.IMP<1,COMBINE.COMP> += EQTY
               END
*T26020 ^
            CASE JCNT = 0 OR ADD = 1
               GOSUB 10100
               ADD = 0
            CASE JBS.CCTR<1,JCNT> # ESTD.CCTR<1,T>
               GOSUB 10100
            CASE CO.PSS.OPER.LVL # "Y"
               JBS.OPER<1,JCNT> = "ALL"
               JBS.OPER.SCH.HRS<1,JCNT> = JBS.OPER.SCH.HRS<1,JCNT> + EHOURS
               JBS.SCH.HRS<1,JCNT> = JBS.SCH.HRS<1,JCNT> + EHOURS
               IF ESTD.OPV.TYPE<1,T> = "V" THEN
                  JBS.SCH.IMP<1,JCNT> = JBS.SCH.IMP<1,JCNT> + INT(ESTD.QTY<1,T>/100)*100
               END
            CASE 1
               OCNT = COUNT(JBS.OPER<1,JCNT>,SVM) + (JBS.OPER<1,JCNT> # "")
               BEGIN CASE
                  CASE OCNT = 0
                     GOSUB 10200
                  CASE JBS.OPER<1,JCNT,OCNT> # ESTD.OPV<1,T>
                     GOSUB 10200
                  CASE 1
                     JBS.OPER.SCH.HRS<1,JCNT,OCNT> = JBS.OPER.SCH.HRS<1,JCNT,OCNT> + EHOURS
                     JBS.SCH.HRS<1,JCNT> = JBS.SCH.HRS<1,JCNT> + EHOURS
                     IF ESTD.OPV.TYPE<1,T> = "V" THEN
                        JBS.SCH.IMP<1,JCNT> = JBS.SCH.IMP<1,JCNT> + INT(ESTD.QTY<1,T>/100)*100
                     END
               END CASE
         END CASE
      END
      JBS.REQ.HRS = JBS.REQ.HRS + EHOURS
   NEXT T
1999 *
   RETURN
*
*---- ADD A MULTI VALUE
*
10100 *
   MATREAD CCTR.REC FROM COST.CNTR, CONO :ESTD.CCTR<1,T> ELSE
      MAT CCTR.REC = ""
      CCTR.MASTER = ESTD.CCTR<1,T> : "!@#$%&*"
   END
   IF CCTR.MASTER # "" AND CCTR.MASTER # ESTD.CCTR<1,T> THEN
*T26342    BLK = 1
      ERRMSG = "Cost Center ":ESTD.CCTR<1,T>:" is not a master"
      GOSUB 90000
      GOTO 10199
   END
   JCNT = JCNT + 1
   AC = DCOUNT(JBS.A1,",")
   FOR AP = 1 TO AC
      AA = FIELD(JBS.A1,",",AP)
      AA1 = FIELD(AA,"-",1)
      AA2 = FIELD(AA,"-",2)
      IF AA2 = "" THEN AA2 = AA1
      FOR AAP = AA1 TO AA2
         JBS.REC(AAP) = INSERT(JBS.REC(AAP),1,JCNT,0,"")
      NEXT AAP
   NEXT AP
**    JBS.DEPT<1,JCNT> = ESTD.DEPT   ;** NA 11-20-90 **
   JBS.DEPT<1,JCNT> = CCTR.DEPT
   JBS.CCTR<1,JCNT> = ESTD.CCTR<1,T>
   JBS.SCH.HRS<1,JCNT> = EHOURS
   IF ESTD.OPV.TYPE<1,T> = "V" THEN
      JBS.SCH.IMP<1,JCNT> = INT(ESTD.QTY<1,T>/100)*100
   END
   IF CO.PSS.OPER.LVL = "Y" THEN
*T22102 v
*T26328   JBS.COMP.NO<1,JCNT> = COMP.NUM
*T22102 ^
      JBS.OPER<1,JCNT> = ESTD.OPV<1,T>
   END ELSE
      JBS.OPER<1,JCNT> = "ALL"
   END
   JBS.COMP.NO<1,JCNT> = COMP.NUM; * T26328
   JBS.OPER.SCH.HRS<1,JCNT> = EHOURS
10199 *
   RETURN
*
*---- ADD A SUB MULTI VALUE
*
10200 *
   OCNT = OCNT + 1
   JBS.OPER<1,JCNT,OCNT> = ESTD.OPV<1,T>
      * T22102 v
   JBS.COMP.NO<1,JCNT,OCNT> = COMP.NUM
      * T22102 ^
   JBS.OPER.SCH.HRS<1,JCNT,OCNT> = EHOURS
   JBS.SCH.HRS<1,JCNT> = JBS.SCH.HRS<1,JCNT> + EHOURS
   IF ESTD.OPV.TYPE<1,T> = "V" THEN
      JBS.SCH.IMP<1,JCNT> = JBS.SCH.IMP<1,JCNT> + INT(ESTD.QTY<1,T>/100)*100
   END
   RETURN
*
*---- ROUND HOURS UP BASED ON DEPARTMENT SPEC
*
20000 *
   ROUND.HRS = ""
   JCNT = DCOUNT(JBS.DEPT,VM)
   FOR JPTR = 1 TO JCNT
      MDEPT = FIELD(JBS.DEPT<1,JPTR>,"-",1)
      RVAL = ROUND.HRS<MDEPT>
      IF RVAL = "" THEN
         MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:MDEPT THEN
            RVAL = CCA.ROUND.HRS+0
         END
         ROUND.HRS<MDEPT> = RVAL+0
      END
      IF RVAL > 0 THEN
         JBS.SCH.HRS<1,JPTR> = INT((JBS.SCH.HRS<1,JPTR>-1+RVAL)/RVAL)*RVAL
      END
   NEXT JPTR
   RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23) : CL : ERRMSG :
*       INPUT REPLY,1 :
*       PRINT @(0,23) : CL :
*       RETURN
999999 *
   RETURN
END
