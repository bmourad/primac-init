      SUBROUTINE JOB.SCHED.DET.INQ (CONO,JOB.NUM,TODAY,LOADING.FLG,PROC.TYPE,DEPT,P.DEPT.DESC,STMV,EDMV)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - JOB.SCHED.DET.INQ
* AUTHOR   - CURTIS WILLIAMS,COMPUTER BUSINESS ASSOCIATES
* DATE     - 07/28/87
* DESCRIPTION
* This subroutine is called from JOB.SCHED.DET.MAINT program in
* inquiry mode to view the status of all cost centers for a specific
* department.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      SCREEN DEFINE;JOB.SCHED.DET.INQ
      SCREEN FORMAT
      SCREEN COUNT;;11
      CC.LINE.COUNT = S$LCNT
      CC.LINE.SPACE = S$LSPC
      SCREEN COUNT;;21
      OO.LINE.COUNT = S$LCNT
      OO.LINE.SPACE = S$LSPC
*
*---- MAIN PROCESSING
*
100*
      S$DATA(3)<S$SCR> = TODAY
      S$DATA(36)<S$SCR> = JOB.NUM
      S$DATA(1)<S$SCR> = DEPT
      S$DATA(2)<S$SCR> = P.DEPT.DESC
      GOSUB 750
      CC.CNT =  1 + (EDMV - STMV)
*
*---- LOAD SCREEN DATA (TOP)
*
      N = 0
      FOR CC = STMV TO EDMV
         N = N + 1
         MATREAD CCTR.REC FROM COST.CNTR,CONO:JBS.CCTR<1,CC> ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "UNKOWN"
         END
         S$DATA(13)<S$SCR,N> = JBS.CCTR<1,CC>
         S$DATA(14)<S$SCR,N> = CCTR.DESC
         S$DATA(16)<S$SCR,N> = JBS.SCH.HRS<1,CC>
         BEGIN CASE
         CASE JBS.CCTR.STATUS<1,CC> = "C"
            S$DATA(15)<S$SCR,N> = JBS.ACT.DATE<1,CC>
            S$DATA(17)<S$SCR,N> = JBS.CMP.DATE<1,CC>
            S$DATA(18)<S$SCR,N> = JBS.ACT.HRS<1,CC>
            S$DATA(19)<S$SCR,N> = "CMP"
         CASE JBS.CCTR.STATUS<1,CC> = "I"
            S$DATA(15)<S$SCR,N> = JBS.ACT.DATE<1,CC>
            IF JBS.EXP.DATE<1,CC> >= JBS.ACT.DATE<1,CC> THEN
               S$DATA(17)<S$SCR,N> = JBS.EXP.DATE<1,CC>
            END ELSE
               S$DATA(17)<S$SCR,N> = ""
            END
            S$DATA(18)<S$SCR,N> = JBS.ACT.HRS<1,CC>
            S$DATA(19)<S$SCR,N> = "INP"
         CASE JBS.SCH.DATE<1,CC> = "" AND JBS.EXP.DATE<1,CC> = ""
            S$DATA(15)<S$SCR,N> = PJBS.SCH.DATE<1,CC>
            S$DATA(17)<S$SCR,N> = PJBS.EXP.DATE<1,CC>
            S$DATA(18)<S$SCR,N> = ""
            S$DATA(19)<S$SCR,N> = "PND"
         CASE JBS.USD.HRS<1,CC> >= JBS.SCH.HRS<1,CC>
            S$DATA(15)<S$SCR,N> = JBS.SCH.DATE<1,CC>
            S$DATA(17)<S$SCR,N> = JBS.EXP.DATE<1,CC>
            S$DATA(18)<S$SCR,N> = JBS.USD.HRS<1,CC>
            S$DATA(19)<S$SCR,N> = "SCH"
         CASE 1
            S$DATA(15)<S$SCR,N> = JBS.SCH.DATE<1,CC>
            S$DATA(17)<S$SCR,N> = ""
            S$DATA(18)<S$SCR,N> = JBS.USD.HRS<1,CC>
            S$DATA(19)<S$SCR,N> = "SCH"
         END CASE
         LOCATE "N" IN JBS.POST.FLAG<1,CC>,1 SETTING NPF ELSE NPF=0
         IF NPF = 0 THEN
            S$DATA(20)<S$SCR,N> = ""
         END ELSE
            S$DATA(20)<S$SCR,N> = "*"
         END
      NEXT CC
      CURR.REF.NO = ""
      REF.NO = 1
      GOSUB 50000
      IF CC.CNT = 1 THEN
         OPTION = "R"
         GOTO 550
      END
*
*---- GET OPERATOR LEVEL REQUEST
*
500*
      SCREEN FIELD;;34
      SCREEN INPUT;;34
      OPTION = S$VALUE
550*
      BEGIN CASE
      CASE OPTION = "" OR OPTION = "END"
         GOTO 999999
      CASE OPTION = "R"
         IF CC.CNT = 1 THEN
            S$VALUE = 1
         END ELSE
            GOSUB 30000
         END
         IF S$VALUE # "" OR S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 81000
            CURR.OREF.NO = ""
            OREF.NO = 1
            GOSUB 51000
            S$VAL = REF.NO
            S$DATA(12)<S$SCR,REF.NO> = "*"
            SCREEN DISPLAY;;12
            GOSUB 1000
         END
      CASE OPTION = "S" OR OPTION = "SF"
         REF.NO = CURR.REF.NO + CC.LINE.COUNT
         IF REF.NO > CC.CNT THEN REF.NO = 1
         GOSUB 50000
      CASE OPTION = "ST"
         REF.NO = 1
         GOSUB 50000
      CASE OPTION = "SB"
         REF.NO = CC.CNT
         GOSUB 50000
      CASE OPTION = "SR"
         REF.NO = CURR.REF.NO - CC.LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPTION = "CD"
         S$SCR = S$SCR + 1
         CALL JOB.SCHED.DET.DATE (CONO,JOB.NUM,TODAY,LOADING.FLG,PROC.TYPE)
SCREEN CLOSE
         S$SCR = S$SCR - 1
         GOSUB 700
         CURR.REF.NO = ""
         GOSUB 50000
      END CASE
      GOTO 500
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- FORMAT SCREEN AND PRINT DATA
*
700*
     SCREEN FORMAT
750*
     SCREEN DISPLAY;;ALL
     RETURN
*
*---- GET OPERATION LEVEL REQUEST
*
1000*
      SCREEN FIELD;;35
      SCREEN INPUT;;35
      OPT2 = S$VALUE
      BEGIN CASE
      CASE OPT2 = "" OR OPT2 = "END"
         GOTO 1999
      CASE OPT2 = "S" OR OPT2 = "SF"
         OREF.NO = CURR.OREF.NO + OO.LINE.COUNT
         IF OREF.NO > OO.CNT THEN OREF.NO = 1
      CASE OPT2 = "ST"
         OREF.NO = 1
      CASE OPT2 = "SB"
         OREF.NO = OO.CNT
      CASE OPT2 = "SR"
         OREF.NO = CURR.OREF.NO - OO.LINE.COUNT
         IF OREF.NO < 1 THEN OREF.NO = 1
      END CASE
      GOSUB 51000
      GOTO 1000
1999*
      S$VAL = REF.NO
      S$DATA(12)<S$SCR,REF.NO> = ""
      SCREEN DISPLAY;;12
      S$DATA(4)<S$SCR> = ""
      SCREEN DISPLAY;;4
      S$DATA(21)<S$SCR> = ""
      S$DATA(22)<S$SCR> = ""
      S$DATA(23)<S$SCR> = ""
      S$DATA(25)<S$SCR> = ""
      S$DATA(26)<S$SCR> = ""
      S$DATA(27)<S$SCR> = ""
      S$DATA(28)<S$SCR> = ""
      CURR.OREF.NO = ""
      OREF.NO = 1
      OO.CNT = 0
      GOSUB 51000
      RETURN
*
*---- GET CCTR LINE REFERENCE
*
30000*
      SCREEN FIELD;;33
      S$MINV = CURR.REF.NO
      S$MAXV = S$MINV + CC.LINE.COUNT - 1
      IF S$MAXV > CC.CNT THEN S$MAXV = CC.CNT
      SCREEN INPUT;;33
      RETURN
*
*---- DISPLAY MULTI-LINE DATA (TOP)
*
50000*
      START.REF.NO = 1 + INT((REF.NO-1)/CC.LINE.COUNT)*CC.LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = CC.CNT
      SCREEN MULTI;;C;11;12;13;14;15;16;18;17;19;20
      RETURN
*
*---- DISPLAY MULTI-LINE DATA (OPER)
*
51000*
      START.REF.NO = 1 + INT((OREF.NO-1)/OO.LINE.COUNT)*OO.LINE.COUNT
      IF START.REF.NO = CURR.OREF.NO THEN RETURN
      CURR.OREF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = OO.CNT
      SCREEN MULTI;;C;21;22;23;25;26;27;28;29
      RETURN
*
*---- LOAD SCREEN DATA (OPER)
*
81000*
      S$DATA(21)<S$SCR> = ""
      S$DATA(22)<S$SCR> = ""
      S$DATA(23)<S$SCR> = ""
      S$DATA(25)<S$SCR> = ""
      S$DATA(26)<S$SCR> = ""
      S$DATA(27)<S$SCR> = ""
      S$DATA(28)<S$SCR> = ""
      S$DATA(29)<S$SCR> = ""
      CC = REF.NO + STMV - 1
      BEGIN CASE
      CASE JBS.SCH.IMP<1,CC> = "" AND JBS.ACT.IMP<1,CC> = ""
         S$DATA(4)<S$SCR> = ""
      CASE JBS.CCTR.STATUS<1,CC> = "C"
         S$DATA(4)<S$SCR> = "Req. "
         IF MOD(JBS.SCH.IMP<1,CC>,100) THEN
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : OCONV(JBS.SCH.IMP<1,CC>, "MD2")
         END ELSE
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : INT(JBS.SCH.IMP<1,CC>/100)
         END
         S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : " , Fin. "
         IF MOD(JBS.ACT.IMP<1,CC>,100) THEN
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : OCONV(JBS.ACT.IMP<1,CC>, "MD2")
         END ELSE
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : INT(JBS.ACT.IMP<1,CC>/100)
         END
      CASE JBS.CCTR.STATUS<1,CC> = "I"
         S$DATA(4)<S$SCR> = "Req. "
         IF MOD(JBS.SCH.IMP<1,CC>,100) THEN
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : OCONV(JBS.SCH.IMP<1,CC>, "MD2")
         END ELSE
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : INT(JBS.SCH.IMP<1,CC>/100)
         END
         S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : " , Fin. "
         IF MOD(JBS.ACT.IMP<1,CC>,100) THEN
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : OCONV(JBS.ACT.IMP<1,CC>, "MD2")
         END ELSE
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : INT(JBS.ACT.IMP<1,CC>/100)
         END
         IF JBS.SCH.IMP<1,CC> + 0 # 0 THEN
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : " , % "
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : INT((JBS.ACT.IMP<1,CC>/JBS.SCH.IMP<1,CC>)*100)
         END
      CASE 1
         S$DATA(4)<S$SCR> = "Req. "
         IF MOD(JBS.SCH.IMP<1,CC>,100) THEN
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : OCONV(JBS.SCH.IMP<1,CC>, "MD2")
         END ELSE
            S$DATA(4)<S$SCR> = S$DATA(4)<S$SCR> : INT(JBS.SCH.IMP<1,CC>/100)
         END
      END CASE
      SCREEN DISPLAY;;4
      OO.CNT = DCOUNT(JBS.OPER<1,CC>,SM)
      FOR N = 1 TO OO.CNT
         S$DATA(22)<S$SCR,N> = JBS.OPER<1,CC,N>
         IF JBS.OPER<1,CC,N> = "ALL" THEN
            S$DATA(23)<S$SCR,N> = "ALL"
         END ELSE
            MATREAD OPER.REC FROM OPERATION,CONO:JBS.OPER<1,CC,N> ELSE
               MAT OPER.REC = ""
               OPER.DESC = "UNKNOWN"
            END
            S$DATA(23)<S$SCR,N> = OPER.DESC
         END
         S$DATA(25)<S$SCR,N> = JBS.OPER.SCH.HRS<1,CC,N>
         S$DATA(27)<S$SCR,N> = JBS.OPER.ACT.HRS<1,CC,N>
         S$DATA(26)<S$SCR,N> = JBS.OPER.ACT.DATE<1,CC,N>
         BEGIN CASE
         CASE JBS.OPER.STATUS<1,CC,N> = "C"
            S$DATA(28)<S$SCR,N> = "CMP"
         CASE JBS.OPER.STATUS<1,CC,N> = "I"
            S$DATA(28)<S$SCR,N> = "INP"
         CASE JBS.OPER.STATUS<1,CC,N> = ""
            S$DATA(28)<S$SCR,N> = ""
         END CASE
         IF JBS.POST.FLAG<1,CC,N> = "N" THEN
            S$DATA(29)<S$SCR,N> = "*"
         END
      NEXT N
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*---- END OF PROGRAM
*
999999*
      SCREEN CLEAR;;D
      RETURN
   END
