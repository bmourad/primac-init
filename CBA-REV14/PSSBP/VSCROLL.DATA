      SUBROUTINE VSCROLL.DATA(MAT WCOM)
*********************************************************************
*
* PROGRAM  - VSCROLL.DATA
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 08/01/91
*
* DESCRIPTION
*
* This subroutine is used to scroll in the direction defined by
* WCOM.TYPE. When using the "LU" or "LD" options, the number of
* rows scrolled is 1 or that which was specified by a prior
* call to SET.VSCROLL.COUNT subroutine.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.WND          Window ID              Required
* 
*   WCOM.TYPE         Scroll type            Required
*                                            Options:
*                                             "T"  = top
*                                             "B"  = bottom
*                                             "LU" = line up
*                                             "LD" = line down
*                                             "PU" = page up
*                                             "PD" = page down
*
*********************************************************************
*
      INCLUDE WINCOM FROM PSSBP
      INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
      EMSG = ""
*
      IF WCOM.WND      = "" THEN EMSG = EMSG : " " : "WCOM.WND"
      IF WCOM.TYPE     = "" THEN EMSG = EMSG : " " : "WCOM.TYPE"
*
      WCOM.STATUS = EMSG
*
      IF WCOM.STATUS = "" THEN
         PMSG = BCMD        : "HSR"
         PMSG = PMSG : CSEP : WCOM.WND
         PMSG = PMSG : CSEP : WCOM.TYPE
         PMSG = PMSG : ECMD
         PRINT PMSG :
      END ELSE
         WCOM.STATUS = "(VSCROLL.DATA) INVALID":WCOM.STATUS
      END
*
*---- RESET PARAMETERS USED
*
      WCOM.TYPE   = ""
      RETURN
   END
