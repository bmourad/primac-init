      SUBROUTINE JOB.CRTL.DATE.EXC.UPD (CONO,JOB.NO)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - JOB.CRTL.DATE.EXC.UPD
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE     - 01/12/88
* DESCRIPTION
* This program update JOB.CRTL.DATE.EXC file for critical date
* exception report.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSS.CPYLIB>JOB.CRTL.DATE.EXC
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      READ TP.DIV FROM PSS.LOCK, @TTY ELSE TP.DIV = ""
*
*---- MAIN PROCESSING
*
100*
      MATREADU JCDER.REC FROM JOB.CRTL.DATE.EXC, CONO : JOB.NO ELSE
         MAT JCDER.REC = ""
         GOTO 110
      END
      MAT JCDER.REC = ""
110*
      TR.CNT = DCOUNT(JBS.CRTCL.REF,VM)
      LN = 0
      FOR TR = 1 TO TR.CNT
         STATUS = ""
         REF = JBS.CRTCL.REF<1,TR>
         BEGIN CASE
         CASE JBS.ACTUAL.OUT<1,TR> # ""
            BEGIN CASE
            CASE JBS.PROMISE.OUT<1,TR> = ""
               STATUS = "Delivered But Not Promised"
            CASE JBS.SCH.DATE<1,REF> = ""
               STATUS = "Delivered But Not Scheduled"
            CASE JBS.USD.HRS<1,REF> < JBS.SCH.HRS<1,REF>
               STATUS = "Delivered But Partially Scheduled"
            CASE 1
            END CASE
         CASE JBS.PROMISE.OUT<1,TR> # ""
            BEGIN CASE
            CASE JBS.SCH.DATE<1,REF> = ""
               STATUS = "Promised But Not Scheduled"
            CASE JBS.USD.HRS<1,REF> < JBS.SCH.HRS<1,REF>
               STATUS = "Promised But Partially Scheduled"
            CASE JBS.EXP.DATE<1,REF> > JBS.PROMISE.OUT<1,TR>
               STATUS = "Cannot Meet Promised Date"
            CASE 1
            END CASE
         CASE 1
            BEGIN CASE
            CASE JBS.SCH.DATE<1,REF> = ""
            CASE JBS.EXP.DATE<1,REF> = ""
            CASE JBS.USD.HRS<1,REF> < JBS.SCH.HRS<1,REF>
               STATUS = "Need To Complete The Schedule"
            CASE 1
               STATUS = "Scheduled But Not Promised"
            END CASE
         END CASE
         IF STATUS # "" THEN
            GOSUB 1000
            JCDER.STATUS<1,LN> = STATUS
         END
      NEXT TR
      IF JCDER.REF = "" THEN
***      DELETE JOB.CRTL.DATE.EXC, CONO : JOB.NO
         TPFID = CONO:JOB.NO
         CALL TRANSACTION.DELETE(CONO,TP.DIV,"JOB.CRTL.DATE.EXC",TPFID,JOB.CRTL.DATE.EXC,PSS.JOURNAL,ERRMSG)
**********************************************************************
      END ELSE
***      MATWRITE JCDER.REC ON JOB.CRTL.DATE.EXC, CONO : JOB.NO
         TPFID = CONO:JOB.NO
         MATBUILD TPREC FROM JCDER.REC
         CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"JOB.CRTL.DATE.EXC",TPFID,TPREC,JOB.CRTL.DATE.EXC,PSS.JOURNAL,ERRMSG)
**********************************************************************
      END
      MAT JCDER.REC = ""
      GOTO 999999
*-------------------*
*--- SUBROUTINES ---*
*-------------------*
*
*--- LOAD JCDER.REC
*
1000*
      LN = LN + 1
      JCDER.DEPT<1,LN> = JBS.DEPT<1,REF>
      JCDER.CCTR<1,LN> = JBS.CCTR<1,REF>
      JCDER.REF<1,LN> = REF
      JCDER.DESC<1,LN> = JBS.CRTCL.DESC<1,TR>
      JCDER.PROMISE<1,LN> = JBS.PROMISE.OUT<1,TR>
      JCDER.EXPECTED<1,LN> = JBS.EXP.DATE<1,REF>
      RETURN
*
*---- END OF PROGRAM
*
999999*
      RETURN
   END
