      SUBROUTINE WS.GEN.CODE.XREF (CONO,XCOL,XROW,XTYPE,XFILE,XFLD,XCODE,MAT WCOM)
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - WS.GEN.CODE.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/10/91
*
* DESCRIPTION
*
* This program is used to display the requested codes for selection.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      DHEAD   = XTYPE:" Selection"
      DESCLEN = LEN(DHEAD)+4
*
*---- MAIN PROCESSING
*
100 *
      XREF.CODE = ""
      ICNT = 0
      SELECT XFILE
120 *
      READNEXT ID ELSE GOTO 140
      IF CONO = "" THEN
         CODE = ID
      END ELSE
         IF ID[1,3] # CONO THEN GOTO 120
         CODE = ID[4,99]
      END
      READV DESC FROM XFILE, ID,XFLD ELSE DESC = ""
      IF TRIM(DESC) = "" THEN GOTO 120
      IF LEN(DESC) > DESCLEN THEN DESCLEN = LEN(DESC)
      ICNT = ICNT + 1
      IF ICNT > 200 THEN GOTO 120
      LOCATE CODE IN XREF.CODE<1>,1 BY "AL" SETTING P ELSE NULL
      XREF.CODE = INSERT(XREF.CODE,1,P,0,CODE)
      GOTO 120
140 *
      IF XREF.CODE = "" THEN
         WCOM.STATUS = "No selections available"
         GOSUB 90000
         GOTO 99999
      END
      LINE.CNT = DCOUNT(XREF.CODE,VM)
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
      XCODE = ""
      DONE = 0
      LOOP
      UNTIL DONE DO
         CALL GET.INPUT(TYPE,WND,DATA,0,MAT WCOM)
         BEGIN CASE
         CASE DATA = "OK"
            DONE = 1
         CASE DATA = "CANCEL"
            DONE = 1
         CASE WND = 101 AND DATA >= 1 AND DATA <= LINE.CNT
            REF = DATA
            IF REF >= 1 THEN XCODE = XREF.CODE<1,REF>
         CASE 1
            WCOM.CAPTION = "WS.GEN.CODE.XREF"
            WCOM.TYPE = "Q"
            WCOM.STATUS = "Received message: ":TYPE:"-":WND:"-":DATA
            GOSUB 90000
         END CASE
      REPEAT
      GOTO 99999
*
*---- DISPLAY DIALOG BOX
*
50000 *
      WCOM.CAPTION = XTYPE:" Selection"
      WCOM.STYLE   = "X"
      WCOM.COL     = XCOL
      WCOM.ROW     = XROW
      WCOM.WIDTH   = DESCLEN+3+8
      WCOM.HEIGHT  = 12
      CALL DEFINE.DIALOG.BOX(MAT WCOM)
*
      WCOM.CLASS   = "BUTTON"
      WCOM.DATA    = "OK"
      WCOM.IDENT   = 1
      WCOM.COL     = DESCLEN+2
      WCOM.ROW     = 3
      WCOM.WIDTH   = 8
      WCOM.HEIGHT  = 1.5
      WCOM.FCOLOR  = "BLACK"
      CALL DEFINE.DIALOG.CONTROL(MAT WCOM)
*
      WCOM.CLASS   = "BUTTON"
      WCOM.DATA    = "CANCEL"
      WCOM.IDENT   = 2
      WCOM.COL     = DESCLEN+2
      WCOM.ROW     = 7
      WCOM.WIDTH   = 8
      WCOM.HEIGHT  = 1.5
      WCOM.FCOLOR  = "BLACK"
      CALL DEFINE.DIALOG.CONTROL(MAT WCOM)
*
      WCOM.CLASS   = "LISTBOX"
      WCOM.IDENT   = 101
      WCOM.COL     = 1
      WCOM.ROW     = 1
      WCOM.WIDTH   = DESCLEN
      WCOM.HEIGHT  = 10+1
      WCOM.FCOLOR  = "BLACK"
      CALL DEFINE.DIALOG.CONTROL(MAT WCOM)
*
      FOR REF = 1 TO LINE.CNT
         XCODE = XREF.CODE<1,REF>
         READV DESC FROM XFILE, CONO:XCODE,XFLD ELSE DESC = STR("?",DESCLEN)
         WCOM.IDENT  = 101
         WCOM.DATA   = DESC
         WCOM.SELECT = REF
         CALL LOAD.CONTROL.DATA(MAT WCOM)
      NEXT REF
      CALL PROCESS.DIALOG(MAT WCOM)
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
