   SUBROUTINE GEN.JOB.SCHED.FWD.DATE(CONO,ACTION,JOB.NUM,XDATE,SFT,TODAY,BLK,ERRMSG,MAT HOLD.REC,REQ.DATE,TR,ST.PTR)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - GEN.JOB.SCHED.FWD.DATE
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE     - 01/11/89
* DESCRIPTION
* This program is the algorithm to adjust the starting date depending
* on the hold/schedule code or hierarchy. (FORWARD SCHEDULING).
* TASK 20406 JR
*ENDDOC
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED.MATRIX
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSS.CPYLIB>PSS.HIERARCHY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SET UP SYSCOM
*
      SYS.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*--- LINE ITEM DIM
   DIM HOLD.REC(15)
   EQU DEPT         TO HOLD.REC(1)
   EQU CCTR         TO HOLD.REC(2)
   EQU H.CODE       TO HOLD.REC(3)
   EQU H.SHIFT      TO HOLD.REC(4)
   EQU H.EQUIP      TO HOLD.REC(5)
   EQU H.LOAD       TO HOLD.REC(6)
   EQU H.DATE       TO HOLD.REC(7)
   EQU H.PO         TO HOLD.REC(8)
   EQU H.DC         TO HOLD.REC(9)
   EQU H.DAYS       TO HOLD.REC(10)
   EQU H.CONT.SCH   TO HOLD.REC(11)
   EQU H.SAME.EQUIP TO HOLD.REC(12)
   EQU CCTR.HRS     TO HOLD.REC(13)
*
*--- INITIALIZATION
   IF ACTION = "W" THEN WIN.MODE = 1 ELSE WIN.MODE = 0
   ACTION = ""
   MAT JBSM.REC = ""
*
*---- MAIN PROCESSING
*
*--- ALGORITHIM TO ADJUST START DATE
   BEGIN CASE
      CASE H.CODE = "L" AND H.DATE # "" AND NUM(H.DATE) AND H.LOAD = "Y"
         IF H.DATE >= REQ.DATE THEN XDATE = H.DATE
         IF H.SHIFT # "" THEN SFT = H.SHIFT ELSE SFT = 1
      CASE H.CODE = "H" AND H.DATE # "" AND NUM(H.DATE)
         GOSUB 5000
         BEGIN CASE
            CASE H.DATE > XDATE
               XDATE = H.DATE
               IF H.SHIFT # "" THEN SFT = H.SHIFT ELSE SFT = 1
            CASE H.DATE = XDATE AND H.SHIFT > SFT
               SFT = H.SHIFT
         END CASE
      CASE H.CODE = "D" AND NUM(H.DAYS)
         XDATE = XDATE + H.DAYS + 1
         IF H.SHIFT # "" THEN SFT = H.SHIFT ELSE SFT = 1
      CASE H.CODE = "SS" AND H.SHIFT = "" AND H.EQUIP = ""
      CASE H.CODE = "SN" AND H.SHIFT = "" AND H.EQUIP = ""
         IF SFT > 2 THEN
            XDATE = XDATE + 1
            SFT = 1
         END ELSE
            SFT = SFT + 1
         END
      CASE H.CODE = "SC" AND NUM(H.DC) AND H.DC > 0 AND H.DC < TR
         IF JBS.SCH.DATE<1,H.DC> < REQ.DATE THEN
            XDATE = REQ.DATE
            SFT = 1
         END ELSE
            XDATE = JBS.SCH.DATE<1,H.DC>
            IF JBS.SCH.SHIFT<1,H.DC> < 1 OR JBS.SCH.SHIFT<1,H.DC> > 3 THEN
               SFT = 3
            END ELSE
               SFT = JBS.SCH.SHIFT<1,H.DC>
            END
         END
      CASE H.CODE = "SCN" AND NUM(H.DC) AND H.DC > 0 AND H.DC < TR
         IF JBS.SCH.DATE<1,H.DC> < REQ.DATE THEN
            XDATE = REQ.DATE
            SFT = 1
         END ELSE
            XDATE = JBS.SCH.DATE<1,H.DC>
            IF JBS.SCH.SHIFT<1,H.DC> < 1 OR JBS.SCH.SHIFT<1,H.DC> > 3 THEN
               SFT = 3
            END ELSE
               SFT = JBS.SCH.SHIFT<1,H.DC>
            END
            IF SFT > 2 THEN
               XDATE = XDATE + 1
               SFT = 1
            END ELSE
               SFT = SFT + 1
            END
         END
      CASE H.CODE = "SA" AND NUM(H.DC) AND H.DC > 0 AND H.DC < TR
         IF JBS.EXP.DATE<1,H.DC> < REQ.DATE THEN
            XDATE = REQ.DATE
            SFT = 1
         END ELSE
            XDATE = JBS.EXP.DATE<1,H.DC>
            IF JBS.EXP.SHIFT<1,H.DC> < 1 OR JBS.EXP.SHIFT<1,H.DC> > 3 THEN
               SFT = 3
            END ELSE
               SFT = JBS.EXP.SHIFT<1,H.DC>
            END
         END
      CASE H.CODE = "SAN" AND NUM(H.DC) AND H.DC > 0 AND H.DC < TR
         IF JBS.EXP.DATE<1,H.DC> < REQ.DATE THEN
            XDATE = REQ.DATE
            SFT = 1
         END ELSE
            XDATE = JBS.EXP.DATE<1,H.DC>
            IF JBS.EXP.SHIFT<1,H.DC> < 1 OR JBS.EXP.SHIFT<1,H.DC> > 3 THEN
               SFT = 3
            END ELSE
               SFT = JBS.EXP.SHIFT<1,H.DC>
            END
            IF SFT > 2 THEN
               XDATE = XDATE + 1
               SFT = 1
            END ELSE
               SFT = SFT + 1
            END
         END
      CASE H.CODE = "SL" AND H.SHIFT = "" AND H.EQUIP = "" AND TR > 1
         IF XDATE < REQ.DATE THEN
            TEMP.DATE = REQ.DATE
            TEMP.SFT = 1
         END ELSE
            TEMP.DATE = XDATE
            TEMP.SFT = SFT
         END
         FOR CK.TR = TR - 1 TO 1 STEP - 1
            BEGIN CASE
               CASE JBS.EXP.DATE<1,CK.TR> > TEMP.DATE
                  TEMP.DATE = JBS.EXP.DATE<1,CK.TR>
                  TEMP.SFT = JBS.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT < 1 OR TEMP.SFT > 3 THEN TEMP.SFT = 3
               CASE JBS.EXP.SHIFT<1,CK.TR> > TEMP.SFT
                  TEMP.SFT = JBS.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT > 3 THEN TEMP.SFT = 3
            END CASE
         NEXT CK.TR
         XDATE = TEMP.DATE
         SFT = TEMP.SFT
      CASE H.CODE = "SLN" AND H.SHIFT = "" AND H.EQUIP = "" AND TR > 1
         IF XDATE < REQ.DATE THEN
            TEMP.DATE = REQ.DATE - 1
            TEMP.SFT = 3
         END ELSE
            TEMP.DATE = XDATE
            TEMP.SFT = SFT
         END
         FOR CK.TR = TR - 1 TO 1 STEP - 1
            BEGIN CASE
               CASE JBS.EXP.DATE<1,CK.TR> > TEMP.DATE
                  TEMP.DATE = JBS.EXP.DATE<1,CK.TR>
                  TEMP.SFT = JBS.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT < 1 OR TEMP.SFT > 3 THEN TEMP.SFT = 3
               CASE JBS.EXP.SHIFT<1,CK.TR> > TEMP.SFT
                  TEMP.SFT = JBS.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT > 3 THEN TEMP.SFT = 3
            END CASE
         NEXT CK.TR
         XDATE = TEMP.DATE
         IF TEMP.SFT > 2 THEN
            XDATE = XDATE + 1
            SFT = 1
         END ELSE
            SFT = TEMP.SFT + 1
         END
      CASE H.CODE = "SLD" AND NUM(H.DAYS) AND TR > 1
         IF XDATE < REQ.DATE THEN
            TEMP.DATE = REQ.DATE
         END ELSE
            TEMP.DATE = XDATE
         END
         FOR CK.TR = TR - 1 TO 1 STEP - 1
            IF JBS.EXP.DATE<1,CK.TR> > TEMP.DATE THEN TEMP.DATE = JBS.EXP.DATE<1,CK.TR>
         NEXT CK.TR
         XDATE = TEMP.DATE + H.DAYS + 1
         IF H.SHIFT # "" THEN SFT = H.SHIFT ELSE SFT = 1
      CASE H.CODE = "PO" AND H.DATE # "" AND NUM(H.DATE)
         GOSUB 5000
         BEGIN CASE
            CASE H.DATE > XDATE
               XDATE = H.DATE
               IF H.SHIFT # "" THEN SFT = H.SHIFT ELSE SFT = 1
            CASE H.DATE = XDATE AND H.SHIFT > SFT
               SFT = H.SHIFT
         END CASE
      CASE H.CODE = "OPO" AND H.DATE # "" AND NUM(H.DATE)
         GOSUB 5000
         BEGIN CASE
            CASE H.DATE > XDATE
               XDATE = H.DATE
               IF H.SHIFT # "" THEN SFT = H.SHIFT ELSE SFT = 1
            CASE H.DATE = XDATE AND H.SHIFT > SFT
               SFT = H.SHIFT
         END CASE
      CASE H.CODE = "J" AND H.PO # "" AND H.SHIFT = "" AND H.EQUIP = ""
         GOSUB 5000
         GOSUB 6000
         IF BLK THEN GOTO 999999
         BEGIN CASE
            CASE TEMP.DATE > XDATE
               XDATE = TEMP.DATE
               SFT = TEMP.SFT
            CASE TEMP.DATE = XDATE AND TEMP.SFT > SFT
               SFT = TEMP.SFT
         END CASE
      CASE H.CODE = "JN" AND H.PO # "" AND H.SHIFT = "" AND H.EQUIP = ""
         GOSUB 5000
         GOSUB 6000
         IF BLK THEN GOTO 999999
         BEGIN CASE
            CASE TEMP.DATE > XDATE
               XDATE = TEMP.DATE
               SFT = TEMP.SFT
               IF SFT > 2 THEN
                  XDATE = XDATE + 1
                  SFT = 1
               END ELSE
                  SFT = SFT + 1
               END
            CASE TEMP.DATE = XDATE AND TEMP.SFT > SFT
               SFT = TEMP.SFT
               IF SFT > 2 THEN
                  XDATE = XDATE + 1
                  SFT = 1
               END ELSE
                  SFT = SFT + 1
               END
         END CASE
      CASE H.CODE = "JD" AND H.PO # "" AND NUM(H.DAYS)
         GOSUB 5000
         GOSUB 6000
         IF BLK THEN GOTO 999999
         TEMP.DATE = TEMP.DATE + H.DAYS + 1
         IF H.SHIFT # "" THEN TEMP.SFT = H.SHIFT ELSE TEMP.SFT = 1
         BEGIN CASE
            CASE TEMP.DATE > XDATE
               XDATE = TEMP.DATE
               SFT = TEMP.SFT
            CASE TEMP.DATE = XDATE AND TEMP.SFT > SFT
               SFT = TEMP.SFT
         END CASE
      CASE 1
         BLK = 1
         ERRMSG = "INVALID HOLDING/SCHEDULING PARAMETERS"
   END CASE
   IF BLK THEN GOTO 999999
   IF XDATE < REQ.DATE THEN
      XDATE = REQ.DATE
      SFT = 1
   END
   GOTO 999999
*---------------------*
*--- SUBROUTINES   ---*
*---------------------*
*
*--- CHECK HIERARCHY
*
5000 *
   BEGIN CASE
      CASE TR = ST.PTR
         GOTO 5099
      CASE TR < 2
         GOTO 5099
      CASE DEPT[1,2] # JBS.DEPT<1,TR-1>[1,2]
         MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO:JBS.DEPT<1,TR-1>[1,2] ELSE
            MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO:"HIERARCHY" ELSE
               MATREAD PSHR.REC FROM PSS.CONTROL, "HIERARCHY" ELSE MAT PSHR.REC = ""
            END
         END
         LOCATE DEPT IN PSHR.TO<1>,1 SETTING FND ELSE
            LOCATE DEPT[1,2] IN PSHR.TO<1>,1 SETTING FND ELSE FND = 0
         END
         IF FND THEN
            XCODE = PSHR.CODE<1,FND>
            XDAYS = PSHR.DAYS<1,FND>
         END ELSE
            XCODE = PSHR.DEF.CODE
            XDAYS = PSHR.DEF.DAYS
         END
* T20406
*     CASE CCTR # JBS.CCTR<1,TR-1>
CASE 1
* T20406
         XDAYS = 0
         MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO:DEPT[1,2]:"*":JBS.CCTR<1,TR-1> THEN
            LOCATE CCTR IN PSHR.TO<1>,1 SETTING FND ELSE FND = 0
            IF FND THEN
               XCODE = PSHR.CODE<1,FND>
            END ELSE
               XCODE = PSHR.CCTR.CODE
            END
         END ELSE
            MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : DEPT[1,2] ELSE
               MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO : "HIERARCHY" ELSE
                  MATREAD PSHR.REC FROM PSS.CONTROL, "HIERARCHY" ELSE MAT PSHR.REC = ""
               END
            END
            XCODE = PSHR.CCTR.CODE
         END
   END CASE
   BEGIN CASE
      CASE XCODE = "SS"
      CASE XCODE = "SN" AND SFT > 2
         XDATE = XDATE + 1
         SFT = 1
      CASE XCODE = "SN"
         SFT = SFT + 1
      CASE 1
         XDATE = XDATE + XDAYS + 1
         SFT = 1
   END CASE
5099 *
   RETURN
*
*--- GET LAST DATE AND LAST SHIFT FROM ANOTHER JOB
*
6000 *
   TEMP.DATE = ""
   TEMP.SFT = ""
   MATREAD JBSM.REC FROM JOB.SCHED, CONO : H.PO ELSE
      ERRMSG = "JOB.SCHED (":H.PO:") IS MISSING"
      BLK = 1
      GOTO 6099
   END
   CK.CNT = DCOUNT(JBSM.DEPT,VM)
   IF CK.CNT < 1 THEN
      ERRMSG = "NEED TO SCHEDULE JOB (":H.PO:") BEFORE THIS JOB"
      BLK = 1
   END
   FOR CK.TR = 1 TO CK.CNT UNTIL BLK
      BEGIN CASE
         CASE JBSM.DEPT.STATUS<1,CK.TR> = "C"
            IF JBSM.CMP.DATE<1,CK.TR> > TEMP.DATE THEN
               TEMP.DATE = JBSM.CMP.DATE<1,CK.TR>
               TEMP.SFT = 3
            END
         CASE JBSM.DEPT.STATUS<1,CK.TR> = "I"
            BEGIN CASE
               CASE JBSM.EXP.DATE<1,CK.TR> > TEMP.DATE
                  TEMP.DATE = JBSM.EXP.DATE<1,CK.TR>
                  TEMP.SFT = JBSM.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT < 1 OR TEMP.SFT > 3 THEN TEMP.SFT = 3
               CASE JBSM.EXP.DATE<1,CK.TR> < TEMP.DATE
               CASE JBSM.EXP.SHIFT<1,CK.TR> > TEMP.SFT
                  TEMP.SFT = JBSM.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT > 3 THEN TEMP.SFT = 3
            END CASE
         CASE JBSM.USD.HRS<1,CK.TR> >= JBSM.SCH.HRS<1,CK.TR>
            BEGIN CASE
               CASE JBSM.EXP.DATE<1,CK.TR> > TEMP.DATE
                  TEMP.DATE = JBSM.EXP.DATE<1,CK.TR>
                  TEMP.SFT = JBSM.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT < 1 OR TEMP.SFT > 3 THEN TEMP.SFT = 3
               CASE JBSM.EXP.DATE<1,CK.TR> < TEMP.DATE
               CASE JBSM.EXP.SHIFT<1,CK.TR> > TEMP.SFT
                  TEMP.SFT = JBSM.EXP.SHIFT<1,CK.TR>
                  IF TEMP.SFT > 3 THEN TEMP.SFT = 3
            END CASE
         CASE 1
            ERRMSG = "NEED TO SCHEDULE JOB (":H.PO:") BEFORE THIS JOB"
            BLK = 1
      END CASE
   NEXT CK.TR
6099 *
   MAT JBSM.REC = ""
   RETURN
*
*--- ERROR ROUTINE
*
90000 *
   IF WIN.MODE THEN
      P_X  = 3 ; P_Y = 23 ; P_VALUE = ERRMSG:" INPUT" ; P_OPT = "CL"
      CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      P_X  = 3 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      RETURN
   END ELSE
      ERR.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
   END
999999 *
   RETURN
END
