      SUBROUTINE WS.JOB.INQ.3(CONO, JSDATE, JEDATE, JXREF, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.JOB.INQ.3
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/01/91
*
* DESCRIPTION
*
* This subroutine is used to display all jobs scheduled for the cost
* center and date range specified.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      CCTR = WIN.CURR.CCTR
      LOCATE CCTR IN WIN.CCTR<1>,1 SETTING CPTR ELSE NULL
      JXREF = ""
      JXTYP = ""
      FOR CDATE = JSDATE TO JEDATE
         DT = CDATE-JSDATE+1
         MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         JCNT = DCOUNT(CCS.JOB,VM)
         FOR JP = 1 TO JCNT
            JOBNO = CCS.JOB<1,JP>
            IF JOBNO # "" THEN
               LOCATE JOBNO IN JXREF,1 SETTING JPTR ELSE
                  JXREF<JPTR> = JOBNO
                  JXTYP<JPTR> = ""
               END
            END
         NEXT JP
         MATREAD CCTR.SCHED.REC FROM PEND.CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         JCNT = DCOUNT(CCS.JOB,VM)
         FOR JP = 1 TO JCNT
            JOBNO = CCS.JOB<1,JP>
            IF JOBNO # "" THEN
               LOCATE JOBNO IN JXREF,1 SETTING JPTR ELSE
                  JXREF<JPTR> = JOBNO
                  JXTYP<JPTR> = "P"
               END
            END
         NEXT JP
      NEXT CDATE
      JCNT = DCOUNT(JXREF,AM)
      IF JCNT = 0 THEN
         WCOM.STATUS = "Sorry, no jobs scheduled for period"
         GOSUB 90000
         GOTO 99999
      END
*
*---- OPEN WINDOW
*
      HEAD.CNT=1
      WCOM.CAPTION=WIN.CDESC<1,CPTR>:" (":CCTR:")"
      WCOM.COL=0
      WCOM.ROW="C"
      WCOM.WIDTH=77
      BEGIN CASE
      CASE (HEAD.CNT+JCNT) > 12
         WCOM.HEIGHT = HEAD.CNT+12
      CASE 1
         WCOM.HEIGHT = HEAD.CNT+JCNT
      END CASE
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.COL=10
      CALL SET.HSCROLL.AREA(MAT WCOM)
      WCOM.ROW=HEAD.CNT
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY JOB SCHEDULE DATA
*
      WCOM.DATA = "  Job #"
      WCOM.COL=0
      WCOM.ROW=0
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.FCOLOR="BLACK"
*     WCOM.BCOLOR="WHITE"
      WCOM.BCOLOR="GREY"
      WCOM.RCOLOR="BLACK"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.TYPE="H"
      WCOM.DATA = "  Customer"
      WCOM.COL=10
      WCOM.ROW=0
      WCOM.WIDTH=32:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.FCOLOR="BLACK"
*     WCOM.BCOLOR="WHITE"
      WCOM.BCOLOR="GREY"
      WCOM.RCOLOR="BLACK"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.TYPE="H"
      WCOM.DATA = "  Job Description"
      WCOM.COL=42
      WCOM.ROW=0
      WCOM.WIDTH=67:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.FCOLOR="BLACK"
*     WCOM.BCOLOR="WHITE"
      WCOM.BCOLOR="GREY"
      WCOM.RCOLOR="BLACK"
      CALL LOAD.DATA(MAT WCOM)
*
      FOR JPTR = 1 TO JCNT
         JOBNO = JXREF<JPTR>
         JTYPE = JXTYP<JPTR>
         IF JTYPE = "" THEN
            MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
               MAT JBS.REC = ""
            END
         END ELSE
            MATREAD JBS.REC FROM PEND.JOB.SCHED, CONO:JOBNO ELSE
               MAT JBS.REC = ""
            END
         END
         MATREAD JOB.REC FROM JOB, CONO:JOBNO ELSE
            MAT JOB.REC = ""
         END
         IF JOB.DESC<1,1> = "" THEN
            JOB.DESC = "GENERIC JOB"
         END
         IF JOB.CUST = "" THEN
            MAT CUST.REC = ""
            CUST.NAME = JBS.CUST.NAME
         END ELSE
            MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
               MAT CUST.REC = ""
               CUST.NAME = JBS.CUST.NAME
            END
         END
         WCOM.TYPE = "V"
         WCOM.DATA = " ":JOBNO
         WCOM.COL=0
         WCOM.ROW=JPTR
         WCOM.WIDTH=10:XADJ
         WCOM.HEIGHT=1:YADJ
         IF JTYPE = "P" THEN
            WCOM.FCOLOR="BLUE"
         END ELSE
            WCOM.FCOLOR="BLACK"
         END
         WCOM.BCOLOR="WHITE"
         WCOM.RCOLOR="BLACK"
         WCOM.SELECT=JPTR*1000
         CALL LOAD.DATA(MAT WCOM)
         WCOM.TYPE="HV"
         WCOM.DATA=" ":JBS.CUST.NAME[1,30]
         WCOM.COL=10
         WCOM.ROW=JPTR
         WCOM.WIDTH=32:XADJ
         WCOM.HEIGHT=1:YADJ
         WCOM.FCOLOR="BLACK"
         WCOM.BCOLOR="WHITE"
         WCOM.RCOLOR="BLACK"
         CALL LOAD.DATA(MAT WCOM)
         WCOM.TYPE="HV"
         WCOM.DATA=" ":JOB.DESC<1,1>[1,65]
         WCOM.COL=42
         WCOM.ROW=JPTR
         WCOM.WIDTH=67:XADJ
         WCOM.HEIGHT=1:YADJ
         WCOM.FCOLOR="BLACK"
         WCOM.BCOLOR="WHITE"
         WCOM.RCOLOR="BLACK"
         CALL LOAD.DATA(MAT WCOM)
      NEXT JPTR
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
