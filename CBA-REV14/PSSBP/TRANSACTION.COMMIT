      SUBROUTINE TRANSACTION.COMMIT (CONO,DIV,PSS.JOURNAL,ERRMSG)
**************************************************************************
*
* PROGRAM - TRANSACTION.COMMIT
*
* AUTHOR  - NICK AMENDOLA, NASTech, Inc.
*
* DATE    - 05/14/96
*
* DESCRIPTION
*
* This program is used to commit all changes to the database for the
* oldest transaction in progress.
*
*    PARAM        DESCRIPTION
*    -----------  --------------------------------------------------------
*    CONO         Company ID
*    DIV          Division ID
*    PSS.JOURNAL  File variable of Journal file
*    ERRMSG       Status
*
**************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSS.CPYLIB>PSS.JOURNAL
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
      CDATE = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN CDATE = DATE()
*
*---- MAIN PROCESSING
*
      MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!":DIV ELSE
         RELEASE PSS.JOURNAL, CONO:"CONTROL!":DIV
         ERRMSG = "NO TRANSACTIONS IN PROGRESS"
         GOTO 99999
      END
      TCNT = DCOUNT(JRNLC.TRAN,VM)
      TPTR = 1
      TDESC = JRNLC.DESC<1,TPTR>
      BPTR = JRNLC.TPTR<1,TPTR>
      EPTR = BPTR + JRNLC.TCNT<1,TPTR> - 1
      FOR JPTR = BPTR TO EPTR
         DELETE PSS.JOURNAL, CONO:"INDEX!":DIV:"!":JPTR
         DELETE PSS.JOURNAL, CONO:"XDATA!":DIV:"!":JPTR
      NEXT JPTR
      JRNLC.JPTR = EPTR + 1
      JRNLC.JCNT -= (EPTR-BPTR+1)
      IF TCNT = 1 THEN
         JRNLC.DATE = CDATE
         JRNLC.TIME = CTIME
         JRNLC.TPTR = 1
         JRNLC.TCNT = 0
         JRNLC.JPTR = 1
         JRNLC.JCNT = 0
      END ELSE
         DEL JRNLC.TRAN<1,TPTR>
         DEL JRNLC.DATE<1,TPTR>
         DEL JRNLC.TIME<1,TPTR>
         DEL JRNLC.DESC<1,TPTR>
         DEL JRNLC.TPTR<1,TPTR>
         DEL JRNLC.TCNT<1,TPTR>
      END
      MATWRITE JRNLC.REC ON PSS.JOURNAL, CONO:"CONTROL!":DIV
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
