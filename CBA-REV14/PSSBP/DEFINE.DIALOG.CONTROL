   SUBROUTINE DEFINE.DIALOG.CONTROL(MAT WCOM)
*********************************************************************
*
* PROGRAM  - DEFINE.DIALOG.CONTROL
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/28/91
*
* DESCRIPTION
*
* This subroutine is used to define a dialog box control.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WND.CLASS         Control class          Required
*                                            Options: GROUPBOX
*                                                     BUTTON
*                                                     EDIT
*                                                     LISTBOX
*                                                     COMBOBOX
*                                                     HSCROLLBAR
*                                                     VSCROLLBAR
* 
*   WND.DATA          Control text           Optional
*
*   WND.IDENT         Control ID             Required
*
*   WND.COL           Starting column        Required
*                                            Range: 0 - 999
*
*   WND.ROW           Starting row           Required
*                                            Range: 0 - 999
*
*   WND.WIDTH         Width                  Required
*                                            Range: 0.01 - 999.99
*
*   WND.HEIGHT        Height                 Required
*                                            Range: 0.01 - 999.99
*
*   WND.FCOLOR        Foreground color       Optional
*                                            Default = BLACK
*
*   WND.MINV          Minimum scroll value   Optional
*                                            Default = 0
*
*   WND.MAXV          Maximum scroll value   Optional
*                                            Default = 9999
*
*   WND.CURV          Current scroll value   Optional
*                                            Default = 0
*
*   WND.INCR          Scroll increment       Optional
*                                            Default = 1
*
*   WND.AID           Associated control     Optional
*                                            Default = 1
*
*********************************************************************
*
   INCLUDE WINCOM FROM PSSBP
   INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
   EMSG = ""
*
   IF WCOM.CLASS  = "" THEN EMSG = EMSG : " " : "WCOM.CLASS"
   IF WCOM.IDENT  = "" THEN EMSG = EMSG : " " : "WCOM.IDENT"
   IF WCOM.COL    = "" THEN EMSG = EMSG : " " : "WCOM.COL"
   IF WCOM.ROW    = "" THEN EMSG = EMSG : " " : "WCOM.ROW"
   IF WCOM.WIDTH  = "" THEN EMSG = EMSG : " " : "WCOM.WIDTH"
   IF WCOM.HEIGHT = "" THEN EMSG = EMSG : " " : "WCOM.HEIGHT"
*
   BEGIN CASE
      CASE WCOM.FCOLOR = ""
         WCOM.FCOLOR = 0
      CASE NUM(WCOM.FCOLOR)
         IF WCOM.FCOLOR < 0 OR WCOM.FCOLOR > 15 THEN
            EMSG = EMSG : " " : "WCOM.FCOLOR"
         END
      CASE 1
         LOCATE WCOM.FCOLOR IN WCOM.COLORS,1 SETTING C ELSE
            EMSG = EMSG : " " : "WCOM.FCOLOR"
         END
         WCOM.FCOLOR = C-1
   END CASE
*
   WCOM.STATUS = EMSG
*
   IF WCOM.STATUS = "" THEN
      PMSG = BCMD        : "DDC"
      PMSG = PMSG : CSEP : WCOM.CLASS
      PMSG = PMSG : CSEP : WCOM.DATA
      PMSG = PMSG : CSEP : WCOM.IDENT
      PMSG = PMSG : CSEP : WCOM.COL
      PMSG = PMSG : CSEP : WCOM.ROW
      PMSG = PMSG : CSEP : WCOM.WIDTH
      PMSG = PMSG : CSEP : WCOM.HEIGHT
      PMSG = PMSG : CSEP : WCOM.FCOLOR
      PMSG = PMSG : CSEP : WCOM.MINV
      PMSG = PMSG : CSEP : WCOM.MAXV
      PMSG = PMSG : CSEP : WCOM.CURV
      PMSG = PMSG : CSEP : WCOM.INCR
      PMSG = PMSG : CSEP : WCOM.AID
      PMSG = PMSG : ECMD
      PRINT PMSG :
* P_X  = 0 ; P_Y = 23 ; P_VALUE = PMSG ; P_OPT = ""
* CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   END ELSE
      WCOM.STATUS = "(DEFINE.DIALOG.CONTROL) INVALID":WCOM.STATUS
   END
*
*---- RESET PARAMETERS USED
*
   WCOM.CLASS   = ""
   WCOM.DATA    = ""
   WCOM.IDENT   = ""
   WCOM.COL     = ""
   WCOM.ROW     = ""
   WCOM.WIDTH   = ""
   WCOM.HEIGHT  = ""
   WCOM.FCOLOR  = ""
   WCOM.MINV    = ""
   WCOM.MAXV    = ""
   WCOM.CURV    = ""
   WCOM.INCR    = ""
   WCOM.AID     = ""
   RETURN
END
