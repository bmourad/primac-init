      SUBROUTINE LOAD.DATA(MAT WCOM)
*********************************************************************
*
* PROGRAM - LOAD.DATA
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/01/91
*
* DESCRIPTION
*
* This subroutine is used to load data in the currently active
* virtual window.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.WND          Window ID              Required
* 
*   WCOM.DATA         Data to be displayed   Optional
*
*   WCOM.TYPE         Data type              Optional
*                                            Default = no scroll
*                                            Options: H  = scroll
*                                                          horizontal
*                                                     V  = scroll
*                                                          vertical
*                                                     VH = scroll
*                                                          vertical &
*                                                          horizontal
*                                                     HV = scroll
*                                                          vertical &
*                                                          horizontal
*
*   WCOM.COL          Starting column        Required
*                                            Range:  0 - 499.9999
*
*   WCOM.ROW          Starting row           Required)
*                                            Range:  0 - 499.9999
*
*   WCOM.WIDTH        Width                  Optional
*                                            Default = data width
*                                            Range:  1 - 500.0000
*
*   WCOM.HEIGHT       Height                 Optional
*                                            Default = 1
*                                            Range:  1 - 500.0000
*
*   WCOM.FCOLOR       Foreground color       Optional
*                                            Default = window color
*
*   WCOM.BCOLOR       Background color       Optional
*                                            Default = window color
*
*   WCOM.RCOLOR       Border color           Optional
*                                            Default = window color
*
*   WCOM.SELECT       Return value           Optional
*                                            Range:  1 - 3999999999
*
*   WCOM.LEVEL        Data level             Optional
*                                            Default = 0
*
*   WCOM.FONT         Data font ID           Optional
*                                            Default = 0
*                                             0 = system
*
*   WCOM.ALIGN        Data alignment         Optional
*                                            Default = LH
*                                             LH = left  horizontal
*                                             RH = right     "
*                                             CH = center    "
*                                             LV = left  vertical
*                                             RV = right    "
*                                             CV = center   "
*
*********************************************************************
*
      INCLUDE WINCOM FROM PSSBP
      INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
      EMSG = ""
*
      IF WCOM.CURR.WND = "" THEN
         IF WCOM.WND    = "" THEN EMSG = EMSG : " " : "WCOM.WND"
      END ELSE
         IF WCOM.WND = WCOM.CURR.WND THEN WCOM.WND = ""
      END
      IF WCOM.COL    = "" THEN EMSG = EMSG : " " : "WCOM.COL"
      IF WCOM.ROW    = "" THEN EMSG = EMSG : " " : "WCOM.ROW"
*
      BEGIN CASE
         CASE WCOM.FCOLOR = ""
         CASE NUM(WCOM.FCOLOR)
            IF WCOM.FCOLOR < 0 OR WCOM.FCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.FCOLOR"
            END
         CASE 1
            LOCATE WCOM.FCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.FCOLOR"
            END
            WCOM.FCOLOR = C-1
      END CASE
*
      BEGIN CASE
         CASE WCOM.BCOLOR = ""
         CASE NUM(WCOM.BCOLOR)
            IF WCOM.BCOLOR < 0 OR WCOM.BCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.BCOLOR"
            END
         CASE 1
            LOCATE WCOM.BCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.BCOLOR"
            END
            WCOM.BCOLOR = C-1
      END CASE
*
      BEGIN CASE
         CASE WCOM.RCOLOR = ""
         CASE NUM(WCOM.RCOLOR)
            IF WCOM.RCOLOR < 0 OR WCOM.RCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.RCOLOR"
            END
         CASE 1
            LOCATE WCOM.RCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.RCOLOR"
            END
            WCOM.RCOLOR = C-1
      END CASE
*
      WCOM.STATUS = EMSG
*
      IF WCOM.STATUS = "" THEN
         PMSG = BCMD        : "LD"
         PMSG = PMSG : CSEP : WCOM.WND
         PMSG = PMSG : CSEP : WCOM.DATA
         PMSG = PMSG : CSEP : WCOM.TYPE
         PMSG = PMSG : CSEP : WCOM.COL
         PMSG = PMSG : CSEP : WCOM.ROW
         PMSG = PMSG : CSEP : WCOM.WIDTH
         PMSG = PMSG : CSEP : WCOM.HEIGHT
         PMSG = PMSG : CSEP : WCOM.FCOLOR
         PMSG = PMSG : CSEP : WCOM.BCOLOR
         PMSG = PMSG : CSEP : WCOM.RCOLOR
         PMSG = PMSG : CSEP : WCOM.SELECT
         PMSG = PMSG : CSEP : WCOM.SELTYPE
         PMSG = PMSG : CSEP : WCOM.LEVEL
         PMSG = PMSG : CSEP : WCOM.FONT
         PMSG = PMSG : CSEP : WCOM.ALIGN
         NL = 0
         FOR N = LEN(PMSG) TO 1 STEP-1 WHILE NL = 0
            IF PMSG[N,1] # CSEP THEN NL = N+1
         NEXT N
         PMSG = PMSG[1,NL]
         PMSG = PMSG : ECMD
         PRINT PMSG :
      END ELSE
         WCOM.STATUS = "(LOAD.DATA) INVALID":WCOM.STATUS
      END
*
*---- RESET PARAMETERS USED
*
      IF WCOM.WND = "" THEN
         WCOM.WND = WCOM.CURR.WND
      END ELSE
         WCOM.CURR.WND = WCOM.WND
      END
      WCOM.DATA    = ""
      WCOM.TYPE    = ""
      WCOM.COL     = ""
      WCOM.ROW     = ""
      WCOM.WIDTH   = ""
      WCOM.HEIGHT  = ""
      WCOM.FCOLOR  = ""
      WCOM.BCOLOR  = ""
      WCOM.RCOLOR  = ""
      WCOM.SELECT  = ""
      WCOM.SELTYPE = ""
      WCOM.LEVEL   = ""
      WCOM.FONT    = ""
      WCOM.ALIGN   = ""
      RETURN
   END
