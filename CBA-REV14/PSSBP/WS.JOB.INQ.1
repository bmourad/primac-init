      SUBROUTINE WS.JOB.INQ.1(CONO, JSDATE, JEDATE, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.JOB.INQ.1
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/01/91
* MODIFIED - 02/07/96, NA, TASK 19937, JOB DESC BASED ON COMPANY FLAG.
*
* DESCRIPTION
*
* This subroutine displays all jobs scheduled in the cost center and
* date range specified.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>PSSBP>WINCOM2
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      CCTR = WIN.CURR.CCTR
      LOCATE CCTR IN WIN.CCTR<1>,1 SETTING CPTR ELSE NULL
      WIN.CSEL<WPTR> = CCTR
      WIN.JOB<WPTR> = ""
      WIN.TEMP<WPTR> = JSDATE
      FOR N = 1 TO WIN.JCNT
         WIN.JSHOURS(N)<WPTR> = ""
         WIN.JAHOURS(N)<WPTR> = ""
         WIN.JPHOURS(N)<WPTR> = ""
      NEXT N
      FOR CDATE = JSDATE TO JEDATE
         DT = CDATE-JSDATE+1
         MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         JCNT = DCOUNT(CCS.JOB,VM)
         FOR JP = 1 TO JCNT
            JOBNO = CCS.JOB<1,JP>
            IF JOBNO # "" THEN
               LOCATE JOBNO IN WIN.JOB<WPTR>,1 SETTING JPTR ELSE
                  WIN.JOB<WPTR,JPTR> = JOBNO
                  WIN.JTYPE<WPTR,JPTR> = ""
               END
               SFT = CCS.JOB.SHIFT<1,JP>
               WIN.JSHOURS(DT)<WPTR,JPTR,SFT> = WIN.JSHOURS(DT)<WPTR,JPTR,SFT> + CCS.JOB.HRS<1,JP>
            END
         NEXT JP
         FOR SFT = 1 TO 3
            WIN.JAHOURS(DT)<WPTR,1,SFT> = CCS.AVL.SHR<1,SFT>*CCS.MULT<1,SFT>
         NEXT SFT
*---- GET PENDING JOB HOURS
         MATREAD CCTR.SCHED.REC FROM PEND.CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         JCNT = DCOUNT(CCS.JOB,VM)
         FOR JP = 1 TO JCNT
            JOBNO = CCS.JOB<1,JP>
            IF JOBNO # "" THEN
               LOCATE JOBNO IN WIN.JOB<WPTR>,1 SETTING JPTR ELSE
                  WIN.JOB<WPTR,JPTR> = JOBNO
                  WIN.JTYPE<WPTR,JPTR> = "P"
               END
               SFT = CCS.JOB.SHIFT<1,JP>
               WIN.JPHOURS(DT)<WPTR,JPTR,SFT> = WIN.JPHOURS(DT)<WPTR,JPTR,SFT> + CCS.JOB.HRS<1,JP>
            END
         NEXT JP
      NEXT CDATE
      JCNT = DCOUNT(WIN.JOB<WPTR>,VM)
      IF JCNT = 0 THEN
         WCOM.STATUS = "Sorry, no jobs scheduled for period"
         GOSUB 90000
         GOTO 99999
      END
*
*---- OPEN WINDOW
*
      HEAD.CNT=1
      COL2=38
      WCOM.CAPTION=WIN.CDESC<1,CPTR>:" (":CCTR:")"
      WCOM.COL="C"
      WCOM.ROW="C"
      WCOM.WIDTH=62
      BEGIN CASE
      CASE (HEAD.CNT+JCNT) > 18
         WHEIGHT = HEAD.CNT+14
      CASE 1
         WHEIGHT = HEAD.CNT+JCNT
      END CASE
      WCOM.HEIGHT = WHEIGHT
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.COL = COL2
      CALL SET.HSCROLL.AREA(MAT WCOM)
      WCOM.ROW = HEAD.CNT
      CALL SET.VSCROLL.AREA(MAT WCOM)
      WCOM.WIDTH = 8 : CSEP : 24
      CALL SET.HSCROLL.COUNT(MAT WCOM)
*
*---- DISPLAY JOB SCHEDULE DATA
*
      WCOM.DATA = "  Job #"
      WCOM.COL = 0
      WCOM.ROW = 0
      WCOM.WIDTH = 10:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
*     WCOM.BCOLOR = "CYAN"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* JOB NUMBER HEADING
      WCOM.DATA = "  Customer"
      WCOM.COL = 10
      WCOM.ROW = 0
      WCOM.WIDTH = 27:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
*     WCOM.BCOLOR = "CYAN"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* CUSTOMER HEADING
      FOR CDATE = JSDATE TO JEDATE
         DT=CDATE-JSDATE+1
         WCOM.DATA = OCONV(CDATE,"D2/")
         WCOM.DATA = "<------ ":WCOM.DATA:" ------>"
         WCOM.TYPE = "H"
         WCOM.COL = COL2+(DT-1)*24
         WCOM.ROW = 0
         WCOM.WIDTH = 24:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
*        WCOM.BCOLOR = "CYAN"
         WCOM.BCOLOR = "GREY"
         WCOM.RCOLOR = "BLACK"
         CALL LOAD.DATA(MAT WCOM)          ;* DAY HEADING
         WCOM.TYPE = "H"
         WCOM.STYLE = "S"
         WCOM.COL = COL2+(DT-1)*24
         WCOM.ROW = 1
         WCOM.WIDTH = 0
         WCOM.HEIGHT = WHEIGHT
         WCOM.FCOLOR = "GREY"
         CALL DRAW.LINE(MAT WCOM)
         WCOM.TYPE = "H"
         WCOM.STYLE = "D"
         WCOM.COL = COL2+(DT-1)*24+8
         WCOM.ROW = 1
         WCOM.WIDTH = 0
         WCOM.HEIGHT = WHEIGHT
         WCOM.FCOLOR = "GREY"
         CALL DRAW.LINE(MAT WCOM)
         WCOM.TYPE = "H"
         WCOM.STYLE = "D"
         WCOM.COL = COL2+(DT-1)*24+16
         WCOM.ROW = 1
         WCOM.WIDTH = 0
         WCOM.HEIGHT = WHEIGHT
         WCOM.FCOLOR = "GREY"
         CALL DRAW.LINE(MAT WCOM)
      NEXT CDATE
*
      FOR JPTR = 1 TO JCNT
         JOBNO = WIN.JOB<WPTR,JPTR>
         JTYPE = WIN.JTYPE<WPTR,JPTR>
         IF JTYPE = "" THEN
            MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
               MAT JBS.REC = ""
            END
         END ELSE
            MATREAD JBS.REC FROM PEND.JOB.SCHED, CONO:JOBNO ELSE
               MAT JBS.REC = ""
            END
         END
         WCOM.DATA = " ":JOBNO
         WCOM.TYPE = "V"
         WCOM.COL = 0
         WCOM.ROW = JPTR
         WCOM.WIDTH = 10:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
*        WCOM.BCOLOR = "CYAN"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = JPTR*1000
         WCOM.SELTYPE = 1
         CALL LOAD.DATA(MAT WCOM)          ;* JOB NUMBER
*---- TASK # 19937 ----
         JDESC = JBS.CUST.NAME
         MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ""
         IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
            MATREAD JOB.REC FROM JOB, CONO:JOBNO THEN
               IF TRIM(JOB.DESC<1,1>) # "" THEN
                  IF CO.PSS.JDESC.FLAG = "J" THEN
                     JDESC = JOB.DESC<1,1>
                  END ELSE
                     JDESC = JDESC[1,10]:":":JOB.DESC<1,1>
                  END
               END
            END
         END
*        WCOM.DATA = " ":JBS.CUST.NAME[1,25]
         WCOM.DATA = " ":JDESC[1,25]
*----------------------
         WCOM.TYPE = "V"
         WCOM.COL = 10
         WCOM.ROW = JPTR
         WCOM.WIDTH = 27:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
*        WCOM.BCOLOR = "CYAN"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = JPTR*1000
         CALL LOAD.DATA(MAT WCOM)          ;* CUSTOMER NAME
         IF JTYPE = "P" THEN
            WCOM.DATA = ""
            WCOM.TYPE = "V"
            WCOM.COL = 37+0.2
            WCOM.ROW = JPTR+0.2
            WCOM.WIDTH = 0.6:XADJ
            WCOM.HEIGHT = 0.6:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = "WHITE"
            WCOM.RCOLOR = "BLACK"
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)       ;* PENDING JOB FLAG
         END
      NEXT JPTR
*
      FOR CDATE = JSDATE TO JEDATE
         DT=CDATE-JSDATE+1
         FOR SFT = 1 TO 3
            STIME = 0
            SCOL=COL2+(DT-1)*24+(SFT-1)*8
            AHRS = WIN.JAHOURS(DT)<WPTR,1,SFT>+0
            FOR JPTR = 1 TO JCNT
               JHRS = WIN.JSHOURS(DT)<WPTR,JPTR,SFT>+WIN.JPHOURS(DT)<WPTR,JPTR,SFT>
               BEGIN CASE
               CASE AHRS = 0 AND JHRS = 0
                  WH1 = 0
                  WH2 = 0
               CASE (STIME+JHRS) <= AHRS
                  WH1 = JHRS
                  WH2 = 0
               CASE STIME >= AHRS
                  WH1 = 0
                  WH2 = JHRS
               CASE 1
                  WH1 = AHRS - STIME
                  WH2 = JHRS - WH1
               END CASE
               WIDTH1 = INT(WH1/10)/10
               WIDTH2 = INT(WH2/10)/10
               IF WIDTH1 > 0 THEN
                  WCOM.TYPE = "HV"
                  WCOM.COL = SCOL + INT(STIME/10)/10
                  WCOM.ROW = JPTR
                  WCOM.WIDTH = WIDTH1:XADJ
                  WCOM.HEIGHT = 1:YADJ
                  WCOM.FCOLOR = "BLACK"
                  WCOM.BCOLOR = "YELLOW"
                  WCOM.RCOLOR = "BLACK"
                  WCOM.LEVEL  = 10
                  WCOM.SELECT = JPTR*1000+DT
                  CALL LOAD.DATA(MAT WCOM) ;* PLOT NORMAL TIME
                  STIME = STIME + WH1
               END
               IF WH2 > 0 THEN
                  WCOM.TYPE = "HV"
                  WCOM.COL = SCOL + INT(STIME/10)/10
                  WCOM.ROW = JPTR
                  WCOM.WIDTH = WIDTH2:XADJ
                  WCOM.HEIGHT = 1:YADJ
                  WCOM.FCOLOR = "BLACK"
                  WCOM.BCOLOR = "RED"
                  WCOM.RCOLOR = "BLACK"
                  WCOM.LEVEL  = 10
                  WCOM.SELECT = JPTR*1000+DT
                  CALL LOAD.DATA(MAT WCOM) ;* PLOT OVERLOAD TIME
                  STIME = STIME + WH2
               END
            NEXT JPTR
         NEXT SFT
      NEXT CDATE
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
