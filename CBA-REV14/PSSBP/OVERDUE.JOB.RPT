*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - OVERDUE.JOB.RPT
* AUTHOR   - KERRY WILSON, C.B.A.
* DATE     - 08/31/88
* MODIFIED - 02/07/96, NA, TASK 19937, JOB DESC BASED ON COMPANY FLAG.
* DESCRIPTION
* This program will load the OVERDUE.JOB file.
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>OVERDUE.JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZE SYSCOM
*
      MAT SYSCOM.REC = ''
      SYS.TYPE=1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*--- PROC READ
*
      PROCREAD BUFFER ELSE
         ERRMSG = "MUST RUN FROM PROC"
         GOTO 90000
      END
      CONO = BUFFER<1>
      CO.NAME = BUFFER<2>
*
*---- OPEN FILES
*
      OPEN "CONTROL" TO CONTROL ELSE
         ERRMSG = 'CANNOT OPEN CONTROL FILE'
         GOTO 90000
      END
      OPEN "COMPANY" TO COMPANY ELSE
         ERRMSG = 'CANNOT OPEN COMPANY FILE'
         GOTO 90000
      END
      OPEN "JOB" TO JOB ELSE
         ERRMSG = 'CANNOT OPEN JOB FILE'
         GOTO 90000
      END
      OPEN "JOB.SCHED" TO JOB.SCHED ELSE
         ERRMSG = 'CANNOT OPEN JOB.SCHED FILE'
         GOTO 90000
      END
      OPEN "PEND.JOB.SCHED" TO PEND.JOB.SCHED ELSE
         ERRMSG = 'CANNOT OPEN PEND.JOB.SCHED FILE'
         GOTO 90000
      END
      OPEN "OVERDUE.JOB" TO OVERDUE.JOB ELSE
         ERRMSG = 'CANNOT OPEN OVERDUE.JOB FILE'
         GOTO 90000
      END
*
*---- INITIALIZATION
*
      READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
      CURRENT.DATE = TODAY-1
*---- TASK # 19937 ----
      MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ""
*----------------------
*
*---- MAIN PROCESSING
*
100*
      DATA = 1
      LOOP 
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         PREV.DEPT = ''
         DAYS.LATE = 0
         X = 0
         Y = 0
         MAT ODJR.REC = ''
         TEMP.REC = ''
         MATREAD JBS.REC FROM JOB.SCHED,ID ELSE GOTO 199
         ODJR.DUE.DATE = JBS.DUE.DATE
*---- TASK # 19937 ----
         JDESC = JBS.CUST.NAME
         IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
            MATREAD JOB.REC FROM JOB, ID THEN
               IF TRIM(JOB.DESC<1,1>) # "" THEN
                  IF CO.PSS.JDESC.FLAG = "J" THEN
                     JDESC = JOB.DESC<1,1>
                  END ELSE
                     JDESC = JDESC[1,10]:":":JOB.DESC<1,1>
                  END
               END
            END
         END
*        ODJR.CUST.NAME = JBS.CUST.NAME
         ODJR.CUST.NAME = JDESC
*----------------------
         DEPT.CNT = DCOUNT(JBS.DEPT,VM)
         FOR I = 1 TO DEPT.CNT
            IF JBS.CCTR.STATUS<1,I> = 'C' THEN GOTO 110
            IF JBS.SCH.DATE<1,I> = '' THEN GOTO 110
            IF JBS.SCH.DATE<1,I> > CURRENT.DATE THEN GOTO 110
            LATE.DAYS = CURRENT.DATE - JBS.EXP.DATE<1,I>
            IF LATE.DAYS > DAYS.LATE THEN
               DAYS.LATE = LATE.DAYS
            END
            LOCATE LATE.DAYS IN TEMP.REC<4> BY "DR" SETTING POS ELSE NULL
            INS LATE.DAYS BEFORE TEMP.REC<4,POS>
            INS JBS.DEPT<1,I> BEFORE TEMP.REC<3,POS>
            INS JBS.CCTR<1,I> BEFORE TEMP.REC<5,POS>
            INS JBS.SCH.DATE<1,I> BEFORE TEMP.REC<6,POS>
            INS JBS.EXP.DATE<1,I> BEFORE TEMP.REC<7,POS>
110*
         NEXT I
         DEPT.CNT = DCOUNT(TEMP.REC<3>,VM)
         FOR J = 1 TO DEPT.CNT
            IF PREV.DEPT # TEMP.REC<3,J> THEN
               Y = 0
               X = X + 1
               ODJR.DEPT<1,X> = TEMP.REC<3,J>
               PREV.DEPT = TEMP.REC<3,J>
            END
            Y = Y + 1
            ODJR.DAY.LATE<1,X,Y> = TEMP.REC<4,J>
            ODJR.CCTR<1,X,Y> = TEMP.REC<5,J>
            ODJR.SCH.DATE<1,X,Y> = TEMP.REC<6,J>
            ODJR.EXP.DATE<1,X,Y> = TEMP.REC<7,J>
         NEXT J
         IF DAYS.LATE > 0 THEN
*** NO JOURNALIZATION NECESSARY FOR THIS FILE
            MATWRITE ODJR.REC ON OVERDUE.JOB,ID:'*':DAYS.LATE
***         TPFID = ID:'*':DAYS.LATE
***         MATBUILD TPREC FROM ODJR.REC
***         CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"OVERDUE.JOB",TPFID,TPREC,OVERDUE.JOB,PSS.JOURNAL,ERRMSG)
**********************************************************************
         END
199*
      REPEAT
      GOTO 999999
*
*---- ERROR ROUTINES
*
90000*
      ERR.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
999999*
      END
