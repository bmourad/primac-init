   SUBROUTINE GET.INPUT (TYPE, WND, DATA, TIMEOUT, MAT WCOM)
*********************************************************************
*
* PROGRAM  - GET.INPUT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 01/04/91
*
* DESCRIPTION
*
* This subroutine is used to get input data from a window.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   TIMEOUT           Seconds to wait        Optional
*                                            Default = infinite
*
*   TYPE              Data type              Options:
*                                            M = menu option selected
*                                            S = data selected
*                                            U = data selected
*                                            D = dialog input
*                                            X = terminate
*
*   TYPE       WND               DATA                 Comments       
*   ----   ------------   ------------------   ----------------------
*    M     MENU           menu option ID
*
*    S     window ID      object ID
*    U     window ID      object ID
*
*    D     control ID     selection number     if list box control
*    D     control ID     keyed data           if edit control
*    D     control ID     keyed data           if edit control
*    D     control ID     button text          if button control
*    D     101            OK                   if OK button
*    D     102            CANCEL               if CANCEL button
*
*    X     MAIN                                terminate session
*
*********************************************************************
*
   INCLUDE WINCOM FROM PSSBP
   INCLUDE WINCOM2 FROM PSSBP
*
*---- INITIALIZATION
*
   WCOM.STATUS = ""
*
*---- MAIN PROCESSING
*
100 *
   BEGIN CASE
      CASE WCOM.MODE = "T"
         LOOP
            PRINT
            PRINT "INPUT: ":
            INPUT WINDATA
            TYPE = FIELD(WINDATA,"-",1)
            WND  = FIELD(WINDATA,"-",2)
            DATA = FIELD(WINDATA,"-",3)
         WHILE TYPE = "" DO
         REPEAT
      CASE 1
         PMSG = BCMD        : "GI"
         PMSG = PMSG : CSEP : TIMEOUT
         PMSG = PMSG : ECMD
         PRINT PMSG :
         LOOP
            ECHO OFF
            INPUT WINDATA:
            ECHO ON
            IF WINDATA[1,1] = BCMD THEN
               WINDATA = WINDATA[2,LEN(WINDATA)-2]
            END
            TYPE = FIELD(WINDATA,"-",1)
            WND  = FIELD(WINDATA,"-",2)
            DATA = FIELD(WINDATA,"-",3)
         WHILE TYPE = "" DO
         REPEAT
   END CASE
   RETURN
END
