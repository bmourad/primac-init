 SUBROUTINE JOB.SCHED.UPD (CONO, ACTION, EST.NUM, JOB.NUM, COMP.NUM, CUST.NAME)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>JES.CPYLIB>COM.ESTIMATE
*COPY>JCS.CPYLIB>COM.JOB
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - JOB.SCHED.UPD
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE     - 05/04/86
* DESCRIPTION
* This program will create JOB.SCHED record after estimate is booked
* to a job.
*T22102 renee 12/08/1997 * Add logic for defining JBS.COMP.NO
*ENDDOC
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*--- INITIALIZATION
*
      READ TP.DIV FROM PSS.LOCK, @TTY ELSE TP.DIV = ""
*
*---- MAIN PROCESSING
*
   MAT JBS.REC = ""
   JBS.DUE.DATE = JOB.TRACK.DATE<1,4>
   JBS.CUST.NAME = CUST.NAME
   JBS.DIV = JOB.DIV
   JBS.CUST.ID = JOB.CUST
   JBS.JOB.COMMENT = JOB.COMMENTS<1,1>
   DCNT = COUNT(EST.DEPT,VM) + (EST.DEPT # "")
   IF COMP.NUM = "ALL" THEN
      FOR D = 1 TO DCNT
         FOR COMP.NUM = 1 TO EST.COMPONENT.CNT
            GOSUB 1000
         NEXT COMP.NUM
      NEXT D
   END ELSE
      COMP.AVAIL = COMP.NUM
      COMP.CNT = COUNT(COMP.AVAIL<1,1>,SM) + (COMP.AVAIL<1,1> # "")
      FOR D = 1 TO DCNT
         FOR C = 1 TO COMP.CNT
            COMP.NUM = COMP.AVAIL<1,1,C>
            GOSUB 1000
         NEXT C
      NEXT D
   END
   IF JBS.DEPT = "" THEN
      RELEASE JOB.SCHED, CONO : JOB.NUM
   END ELSE
***   MATWRITE JBS.REC ON JOB.SCHED, CONO:JOB.NUM
      TPFID = CONO:JOB.NUM
      MATBUILD TPREC FROM JBS.REC
      CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"JOB.SCHED",TPFID,TPREC,JOB.SCHED,PSS.JOURNAL,ERRMSG)
**********************************************************************
   END
   GOTO 999999
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- LOAD JBS.REC FROM ESTD.REC
*
1000*
   KEY = D :"!": COMP.NUM :"!": EST.BOOK.QTY
   LOCATE KEY IN EST.DEPT.COMP<1>,1 SETTING LN ELSE LN = 0
   IF LN = 0 THEN GOTO 1999
   MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.NUM:"!":KEY ELSE MAT ESTD.REC = ""
   TYPE.CNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
   ADD = 1
   FOR T = 1 TO TYPE.CNT
      IF ESTD.TYPE<1,T> = "L" THEN
         JCNT = COUNT(JBS.DEPT,VM) + (JBS.DEPT # "")
         BEGIN CASE
         CASE JCNT = 0 OR ADD = 1
            GOSUB 10100
            ADD = 0
         CASE JBS.DEPT<1,JCNT> # ESTD.DEPT
            GOSUB 10100
         CASE JBS.CCTR<1,JCNT> # ESTD.CCTR<1,T>
            GOSUB 10100
         CASE CO.PSS.OPER.LVL # "Y"
            JBS.OPER<1,JCNT> = "ALL"
            JBS.OPER.SCH.HRS<1,JCNT> = JBS.OPER.SCH.HRS<1,JCNT> + ESTD.HRS<1,T>
            JBS.SCH.HRS<1,JCNT> = JBS.SCH.HRS<1,JCNT> + ESTD.HRS<1,T>
         CASE 1
            OCNT = COUNT(JBS.OPER<1,JCNT>,SVM) + (JBS.OPER<1,JCNT> # "")
            BEGIN CASE
            CASE OCNT = 0
               GOSUB 10200
            CASE JBS.OPER<1,JCNT,OCNT> # ESTD.OPV<1,T>
               GOSUB 10200
            CASE 1
               JBS.OPER.SCH.HRS<1,JCNT,OCNT> = JBS.OPER.SCH.HRS<1,JCNT,OCNT> + ESTD.HRS<1,T>
               JBS.SCH.HRS<1,JCNT> = JBS.SCH.HRS<1,JCNT> + ESTD.HRS<1,T>
            END CASE
         END CASE
      END
      JBS.REQ.HRS = JBS.REQ.HRS + ESTD.HRS<1,T>
   NEXT T
1999*
   RETURN
*
*--- ADD A MULTI VALUE
*
10100*
   JCNT = JCNT + 1
   JBS.DEPT<1,JCNT> = ESTD.DEPT
   JBS.CCTR<1,JCNT> = ESTD.CCTR<1,T>
   JBS.SCH.HRS<1,JCNT> = ESTD.HRS<1,T>
   IF CO.PSS.OPER.LVL = "Y" THEN
      JBS.OPER<1,JCNT> = ESTD.OPV<1,T>
      * T22102 v
      JBS.COMP.NO<1,JCNT> = COMP.NUM
      * T22102 ^
   END ELSE
      JBS.OPER<1,JCNT> = "ALL"
   END
   JBS.OPER.SCH.HRS<1,JCNT> = ESTD.HRS<1,T>
   RETURN
*
*--- ADD A SUB MULTI VALUE
*
10200*
   OCNT = OCNT + 1
   JBS.OPER<1,JCNT,OCNT> = ESTD.OPV<1,T>
   * T22102 v
   JBS.COMP.NO<1,JCNT,OCNT> = COMP.NUM
   * T22102 ^
   JBS.OPER.SCH.HRS<1,JCNT,OCNT> = ESTD.HRS<1,T>
   JBS.SCH.HRS<1,JCNT> = JBS.SCH.HRS<1,JCNT> + ESTD.HRS<1,T>
   RETURN
999999*
   RETURN
END
