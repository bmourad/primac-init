********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - CCTR.SCHED.HIST.RPT
* AUTHOR   - ANN POWLEY, C.B.A.
* DATE     - 09/01/87
* DESCRIPTION
* This program will print Cost Center Schedule History Report.
*********************************************************************
*
***** FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SET UP SYSCOM
*
      SYS.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CCTR.SCHED.HIST" TO CCTR.SCHED.HIST ELSE
      ERRMSG = "CCTRSCHED.HIST FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","JOB.SCHED" TO JOB.SCHED ELSE
      ERRMSG = "JOB.SCHED FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","PEND.JOB.SCHED" TO PEND.JOB.SCHED ELSE
      ERRMSG = "PEND.JOB.SCHED FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "DEPARTMENT FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","COST.CNTR" TO COST.CNTR ELSE
      ERRMSG = "COST.CNTR FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
      ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
      ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
      GOSUB 90000
      GOTO 99999
   END
*
**** PERFORM PROCREAD
*
   PROCREAD BUFFER ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   CONO = BUFFER<1>
   CO.NAME = BUFFER<2>
   ST.DATE = BUFFER<5>
   END.DATE = BUFFER<6>
   REPORT.NAME = "COST CENTER SCHEDULE HISTORY REPORT"
   REPORT.NUMBER = "XXXX"
   READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
   REPORT.DATE = TODAY
   HD1 = "";HD2 = "";PG.NO = 0
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** INTIALIZATION
*
   LINE.CNT = 99
   SP = SPACE(1)
   HD3 = SPACE(63):'FROM ':ST.DATE:' TO ':END.DATE
   HD4 = "DEPARTMENT  : "
   HD5 = "COST CENTER : "
   HD6 = "             <------ SHIFT HOURS -----> AVAIL  SCHED    REM                                      <--- SHIFT HOURS -->  TOTAL"
   HD7 = "  DATE   DAY   1ST      2ND      3RD    HOURS  HOURS   HOURS  JOB NUM          CUSTOMER            1ST    2ND    3RD   HOURS"
   HD8 = "-------- --- -------- -------- -------- ------ ------ ------- -------- ------------------------- ------ ------ ------ ------"
*
**** MAIN PROCESSING
*
   DATA = 1
   PRINTER ON
   READNEXT ID ELSE DATA = 0
   MATREAD CCTR.SCHED.REC FROM CCTR.SCHED.HIST, ID ELSE MAT CCTR.SCHED.REC = ''
   CCS.CCTR = ID[4,3]
   CCTR = CCS.CCTR
   DEPT = CCS.DEPT
   GOSUB 200
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD CCTR.SCHED.REC FROM CCTR.SCHED.HIST, ID ELSE MAT CCTR.SCHED.REC = ''
      CCS.CCTR = ID[4,3]
      IF DEPT # CCS.DEPT THEN 
         DEPT = CCS.DEPT
         CCTR = CCS.CCTR
         GOSUB 500
      END
      IF CCTR # CCS.CCTR THEN 
         DEPT = CCS.DEPT
         CCTR = CCS.CCTR
         GOSUB 500
      END
      GOSUB 200
   REPEAT
   GOTO 99999
*
***** PROCESS THE JOB
*
200 *
   IF LINE.CNT > 55 THEN GOSUB 500
   ID.DATE = FIELD(ID,'*',2)
   DAY = OCONV(ID.DATE,"DWA")
   SHIFT1 = ''; SHIFT2 = ''; SHIFT3 = ''
   AVL.HRS = ''
   IF (CCS.AVL.SHR<1,1> # '') AND (CCS.MULT<1,1> # '') THEN
      SHIFT1 = OCONV(CCS.AVL.SHR<1,1>,"MD2") : "x" : CCS.MULT<1,1>"L#2"
   END
   IF (CCS.AVL.SHR<1,2> # '') AND (CCS.MULT<1,2> # '') THEN
      SHIFT2 = OCONV(CCS.AVL.SHR<1,2>,"MD2") : "x" : CCS.MULT<1,2>"L#2"
   END
   IF (CCS.AVL.SHR<1,3> # '') AND (CCS.MULT<1,3> # '') THEN
      SHIFT3 = OCONV(CCS.AVL.SHR<1,3>,"MD2") : "x" : CCS.MULT<1,3>"L#2"
   END
   AVL.HRS = CCS.REM.HRS
   GOSUB 300
   IF JOB.LNS = 0 THEN JOB.LNS = 1
   FOR I = 1 TO JOB.LNS
      IF I = 1 THEN
         H.LINE = OCONV(ID.DATE,"D2/") "L#8" : SP
         H.LINE = H.LINE : DAY[1,3] : SP
         H.LINE = H.LINE : SHIFT1 "R#8" : SP
         H.LINE = H.LINE : SHIFT2 "R#8" : SP
         H.LINE = H.LINE : SHIFT3 "R#8" : SP
         H.LINE = H.LINE : OCONV(CCS.AVL.HRS,"MD2Z") "R#6" : SP
         H.LINE = H.LINE : OCONV(CCS.SCH.HRS,"MD2Z") "R#6" : SP
         H.LINE = H.LINE : OCONV(AVL.HRS,"MD2Z") "R#7" : SP
         H.LINE = H.LINE : JOBS<1,I> "L#8" : SP
         H.LINE = H.LINE : CUST<1,I> "L#25" : SP
         H.LINE = H.LINE : OCONV(SHR1<1,I>,"MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(SHR2<1,I>,"MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(SHR3<1,I>,"MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(TOT.JOB.HRS<1,I>,"MD2") "R#6" : SP
      END ELSE
         H.LINE = SPACE(62)
         H.LINE = H.LINE : JOBS<1,I> "L#8" : SP
         H.LINE = H.LINE : CUST<1,I> "L#25" : SP
         H.LINE = H.LINE : OCONV(SHR1<1,I>,"MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(SHR2<1,I>,"MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(SHR3<1,I>,"MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(TOT.JOB.HRS<1,I>,"MD2") "R#6" : SP
      END
      PRINT H.LINE
      LINE.CNT = LINE.CNT + 1
   NEXT I
   RETURN
*
**** CALCULATE JOB LINES
*
300 *
   JOB.CNT = COUNT(CCS.JOB,VM) + (CCS.JOB # '')
   JOBS = ''; CUST = ''; SHR1=''; SHR2=''; SHR3=''; TOT.JOB.HRS = ''
   FOR N = 1 TO JOB.CNT
      JOB.NO = CCS.JOB<1,N>
      LOCATE JOB.NO IN JOBS<1>,1 SETTING JFND ELSE
         JOBS<1,JFND> = JOB.NO
         MATREAD JBS.REC FROM JOB.SCHED, CONO:JOB.NO ELSE
            MAT JBS.REC = ""
            JBS.CUST.NAME = "???????????????"
         END
         CUST<1,JFND> = JBS.CUST.NAME
      END
      BEGIN CASE
         CASE CCS.JOB.SHIFT<1,N> = 1
            SHR1<1,JFND> = SHR1<1,JFND> + CCS.JOB.HRS<1,N>
         CASE CCS.JOB.SHIFT<1,N> = 2
            SHR2<1,JFND> = SHR2<1,JFND> + CCS.JOB.HRS<1,N>
         CASE 1
            SHR3<1,JFND> = SHR3<1,JFND> + CCS.JOB.HRS<1,N>
      END CASE
      TOT.JOB.HRS<1,JFND> = TOT.JOB.HRS<1,JFND> + CCS.JOB.HRS<1,N>
   NEXT N
   JOB.LNS = COUNT(JOBS,VM) + (JOBS # "")
   RETURN
*
**** PRINT HEADINGS
*
500 *
   PG.NO = PG.NO + 1
   MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE
      MAT DEPT.REC = ''
      DEPT.DESC = '????????????????'
   END
   MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE
      MAT CCTR.REC = ''
      CCTR.DESC = '????????????????'
   END
   PRINT CHAR(12)
   PRINT HD1 : PG.NO "R#4"
   PRINT HD2
   PRINT HD3
   PRINT
   PRINT HD4 : DEPT "L#2" : SP : DEPT.DESC "L#30"
   PRINT HD5 : CCTR "L#3" : SP : CCTR.DESC "L#30"
   PRINT
   PRINT HD6
   PRINT HD7
   PRINT HD8
   LINE.CNT = 10
   RETURN
*
***** ERROR ROUTINE
90000 *
91000*
      ERR.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
*
***** END OF PROGRAM
99999*
   PRINTER OFF
   PRINT @(-1):
END
