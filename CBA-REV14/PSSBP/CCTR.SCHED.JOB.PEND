      SUBROUTINE CCTR.SCHED.JOB (CONO, DEPT, DDESC, CCTR, CDESC, CDATE)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - CCTR.SCHED.JOB
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 05/16/85
* MODIFIED - 02/07/96, NA, TASK 19937, JOB DESC BASED ON COMPANY FLAG.
* DESCRIPTION
* Provide inquiry of jobs scheduled on a specified day for a
* specified cost center.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>JCS.CPYLIB>OPERATION
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      OPEN "PSS.SCREENS" TO PSS.SCREENS ELSE
         ERRMSG = "CANNOT LOCATE PSS.SCREENS FILE"
         GOSUB 90000
         RETURN
      END
      SCREEN DEFINE;CCTR.SCHED.JOB
      SCREEN FORMAT
      SCREEN COUNT;;7
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
      MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ""
      IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
         OPEN "","JOB" TO JOB ELSE
            ERRMSG = "CANNOT LOCATE JOB FILE"
            GOSUB 90000
            RETURN
         END
      END
*----------------------
*
*---- MAIN PROCESSING
*
100*
      SCREEN CLEAR;;D
      S$DATA(1)<S$SCR> = TODAY
      S$DATA(2)<S$SCR> = DEPT
      S$DATA(3)<S$SCR> = DDESC
      S$DATA(4)<S$SCR> = CDATE
      S$DATA(5)<S$SCR> = CCTR
      S$DATA(6)<S$SCR> = CDESC
      SCREEN DISPLAY;;ALL
*
      LINE.CNT = DCOUNT(CCS.JOB,VM)
      GOSUB 40000
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 50000
      PLINE.CNT = DCOUNT(PCCS.JOB,VM)
      GOSUB 45000
      PREF.NO = 1
      PCURR.REF.NO = ""
      GOSUB 55000
*
*---- GET OPERATOR REQUEST
*
500*
      SCREEN FIELD;;15
      SCREEN INPUT;;15
      OPTION = S$VALUE
      BEGIN CASE
      CASE OPTION = "SS" OR OPTION = "SF"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > LINE.CNT THEN REF.NO = 1
         GOSUB 50000
      CASE OPTION = "ST"
         REF.NO = 1
         GOSUB 50000
      CASE OPTION = "SB"
         REF.NO = LINE.CNT
         GOSUB 50000
      CASE OPTION = "SR"
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPTION = "" OR OPTION = "END" OR OPTION = "E"
         GOTO 99999
      CASE OPTION = "SP"
         PREF.NO = PCURR.REF.NO + LINE.COUNT
         IF PREF.NO > PLINE.CNT THEN PREF.NO = 1
         GOSUB 55000
      CASE 1
         ERRMSG = "Invalid Selection"
         GOSUB 90000
      END CASE
      GOTO 500
*
*---- LOAD S$DATA MATRIX
*
40000*
      FOR N = 1 TO LINE.CNT
         JOB.NO = CCS.JOB<1,N>
         LOCATE JOB.NO IN S$DATA(8)<S$SCR>,1 SETTING JFND ELSE
            S$DATA(8)<S$SCR,JFND> = JOB.NO
            MATREAD JBS.REC FROM JOB.SCHED, CONO:JOB.NO ELSE
               MAT JBS.REC = ""
               JBS.CUST.NAME = "???????????????"
            END
*---- TASK # 19937 ----
            JDESC = JBS.CUST.NAME
            IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
               MATREAD JOB.REC FROM JOB, CONO:JOB.NO THEN
                  IF TRIM(JOB.DESC<1,1>) # "" THEN
                     IF CO.PSS.JDESC.FLAG = "J" THEN
                        JDESC = JOB.DESC<1,1>
                     END ELSE
                        JDESC = JDESC[1,10]:":":JOB.DESC<1,1>
                     END
                  END
               END
            END
*           S$DATA(9)<S$SCR,JFND> = JBS.CUST.NAME
            S$DATA(9)<S$SCR,JFND> = JDESC
*----------------------
         END
*
         BEGIN CASE
         CASE CCS.JOB.SHIFT<1,N> = 1
            S$DATA(10)<S$SCR,JFND> = S$DATA(10)<S$SCR,JFND> + CCS.JOB.HRS<1,N>
         CASE CCS.JOB.SHIFT<1,N> = 2
            S$DATA(11)<S$SCR,JFND> = S$DATA(11)<S$SCR,JFND> + CCS.JOB.HRS<1,N>
         CASE 1
            S$DATA(12)<S$SCR,JFND> = S$DATA(12)<S$SCR,JFND> + CCS.JOB.HRS<1,N>
         END CASE
         S$DATA(13)<S$SCR,JFND> = S$DATA(13)<S$SCR,JFND> + CCS.JOB.HRS<1,N>
         BEGIN CASE
         CASE S$DATA(14)<S$SCR,JFND> = ""
            BEGIN CASE
            CASE CCS.OPER.STATUS<1,N> = "C"
               S$DATA(14)<S$SCR,JFND> = "CMP"
            CASE CCS.OPER.STATUS<1,N> = "I"
               S$DATA(14)<S$SCR,JFND> = "INP"
            CASE 1
               S$DATA(14)<S$SCR,JFND> = "SCH"
            END CASE
         CASE S$DATA(14)<S$SCR,JFND> = "CMP"
            BEGIN CASE
            CASE CCS.OPER.STATUS<1,N> = "C"
            CASE 1
               S$DATA(14)<S$SCR,JFND> = "INP"
            END CASE
         CASE S$DATA(14)<S$SCR,JFND> = "INP"
         CASE 1
            BEGIN CASE
            CASE CCS.OPER.STATUS<1,N> # ""
               S$DATA(14)<S$SCR,JFND> = "INP"
            CASE 1
               S$DATA(14)<S$SCR,JFND> = "SCH"
            END CASE
         END CASE
      NEXT N
      LINE.CNT = COUNT(S$DATA(8)<S$SCR>,VM) + (S$DATA(8)<S$SCR> # "")
      RETURN
*
*---- LOAD S$DATA MATRIX
*
45000*
      FOR N = 1 TO PLINE.CNT
         JOB.NO = PCCS.JOB<1,N>
         LOCATE JOB.NO IN S$DATA(17)<S$SCR>,1 SETTING JFND ELSE
            S$DATA(17)<S$SCR,JFND> = JOB.NO
            MATREAD JBS.REC FROM JOB.SCHED, CONO:JOB.NO ELSE
               MAT JBS.REC = ""
               JBS.CUST.NAME = "???????????????"
            END
*---- TASK # 19937 ----
            JDESC = JBS.CUST.NAME
            IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
               MATREAD JOB.REC FROM JOB, CONO:JOB.NO THEN
                  IF TRIM(JOB.DESC<1,1>) # "" THEN
                     IF CO.PSS.JDESC.FLAG = "J" THEN
                        JDESC = JOB.DESC<1,1>
                     END ELSE
                        JDESC = JDESC[1,10]:":":JOB.DESC<1,1>
                     END
                  END
               END
            END
*           S$DATA(18)<S$SCR,JFND> = JBS.CUST.NAME
            S$DATA(18)<S$SCR,JFND> = JDESC
*----------------------
         END
*
         BEGIN CASE
         CASE PCCS.JOB.SHIFT<1,N> = 1
            S$DATA(19)<S$SCR,JFND> = S$DATA(19)<S$SCR,JFND> + PCCS.JOB.HRS<1,N>
         CASE PCCS.JOB.SHIFT<1,N> = 2
            S$DATA(20)<S$SCR,JFND> = S$DATA(20)<S$SCR,JFND> + PCCS.JOB.HRS<1,N>
         CASE 1
            S$DATA(21)<S$SCR,JFND> = S$DATA(21)<S$SCR,JFND> + PCCS.JOB.HRS<1,N>
         END CASE
         S$DATA(22)<S$SCR,JFND> = S$DATA(22)<S$SCR,JFND> + PCCS.JOB.HRS<1,N>
         S$DATA(23)<S$SCR,JFND> = "PND"
      NEXT N
      PLINE.CNT = DCOUNT(S$DATA(17)<S$SCR>,VM)
      RETURN
*
*---- DISPLAY MULTI-VALUE DATA (SCHEDULED)
*
50000*
      START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = LINE.CNT
      SCREEN MULTI;;C;7;8;9;10;11;12;13;14
      RETURN
*
*---- DISPLAY MULTI-VALUE DATA (PENDING)
*
55000*
      PSTART.REF.NO = 1 + INT((PREF.NO-1)/LINE.COUNT)*LINE.COUNT
      IF PSTART.REF.NO = PCURR.REF.NO THEN RETURN
      PCURR.REF.NO = PSTART.REF.NO
      S$VAL = PSTART.REF.NO
      S$CNT = PLINE.CNT
      SCREEN MULTI;;C;16;17;18;19;20;21;22;23
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
      SCREEN CLEAR;;D
      RETURN
   END
