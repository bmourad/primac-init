      SUBROUTINE WS.CCTR.CALENDAR(CONO, SDATE, EDATE, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.CCTR.CALENDAR
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/01/91
*
* DESCRIPTION
*
* This subroutine displays summary information in calendar format
* for the cost center and date range specified.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      MONTHS = ""
      MONTHS<1>  = "J A N U A R Y"
      MONTHS<2>  = "F E B R U A R Y"
      MONTHS<3>  = "M A R C H"
      MONTHS<4>  = "A P R I L"
      MONTHS<5>  = "M A Y"
      MONTHS<6>  = "J U N E"
      MONTHS<7>  = "J U L Y"
      MONTHS<8>  = "A U G U S T"
      MONTHS<9>  = "S E P T E M B E R"
      MONTHS<10> = "O C T O B E R"
      MONTHS<11> = "N O V E M B E R"
      MONTHS<12> = "D E C E M B E R"
*
      DAYS = ""
      DAYS<1> = "SUN"
      DAYS<2> = "MON"
      DAYS<3> = "TUE"
      DAYS<4> = "WED"
      DAYS<5> = "THU"
      DAYS<6> = "FRI"
      DAYS<7> = "SAT"
*
      ODATE = OCONV(SDATE,"D2-")
      ODATE = ODATE[1,3]:"01":ODATE[6,3]
      CSDATE = ICONV(ODATE,"D")
      ODATE = OCONV(SDATE+3*31,"D2-")
      ODATE = ODATE[1,3]:"01":ODATE[6,3]
      CEDATE = ICONV(ODATE,"D")-1
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      CCTR = WIN.CURR.CCTR
      LOCATE CCTR IN WIN.CCTR<1>,1 SETTING CPTR ELSE NULL
      WIN.CSEL<WPTR> = CCTR
*
*---- OPEN WINDOW
*
      HEAD.CNT = 1
      COL2 = 5
      ROW2 = HEAD.CNT
*
      WCOM.CAPTION = WIN.CDESC<1,CPTR>:" (":CCTR:")"
      WCOM.COL = "C"
      WCOM.ROW = "3"
      WCOM.WIDTH = 75
      WCOM.HEIGHT = 21
      WCOM.BCOLOR = "WHITE"
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.WIDTH = 75
      CALL SET.HSCROLL.COUNT(MAT WCOM)
*
      WCOM.COL = COL2
      WCOM.ROW = ROW2
      WCOM.WIDTH = 70
      WCOM.HEIGHT = 19
      WCOM.FCOLOR = "IWHITE"
      WCOM.BCOLOR = "IWHITE"
      WCOM.RCOLOR = "IWHITE"
      CALL LOAD.DATA (MAT WCOM)
*
*---- DISPLAY CALENDAR
*
      PMONTH = 0
      MPTR = 0
      FOR CDATE = CSDATE TO CEDATE
         CMONTH = OCONV(CDATE,"DM")
         IF CMONTH = PMONTH THEN
            CDAY = CDAY + 1
         END ELSE
            PMONTH = CMONTH
            OFFSET = MOD(OCONV(CDATE,"DW"),7)
            MPTR = MPTR + 1
            CDAY = 1
            WCOM.TYPE = "H"
            WCOM.COL = (MPTR-1)*75
            WCOM.ROW = 0
            DMONTH = MONTHS<CMONTH>
            WCOM.DATA = SPACE((75-LEN(DMONTH))/2):DMONTH
            CALL LOAD.DATA(MAT WCOM)           ;* MONTH HEADING
            WCOM.STYLE = "S"
            WCOM.TYPE = "H"
            WCOM.COL = (COL2+(MPTR-1)*75)
            WCOM.ROW = ROW2
            WCOM.HEIGHT = 19
            WCOM.LWIDTH = 2
            CALL DRAW.LINE(MAT WCOM)          ;* LEFT VERTICAL LINE
            WCOM.STYLE = "S"
            WCOM.TYPE = "H"
            WCOM.COL = (COL2+(MPTR-1)*75)
            WCOM.ROW = ROW2
            WCOM.WIDTH = 70
            WCOM.LWIDTH = 2
            CALL DRAW.LINE(MAT WCOM)          ;* TOP HORIZ LINE
            WCOM.STYLE = "S"
            WCOM.TYPE = "H"
            WCOM.COL = COL2+(MPTR-1)*75
            WCOM.ROW = (ROW2+1+18):"+"
            WCOM.WIDTH = 70
            WCOM.LWIDTH = 2
            CALL DRAW.LINE(MAT WCOM)          ;* BOTTOM HORIZ LINE
            FOR DOW = 1 TO 7
               WCOM.TYPE = "H"
               WCOM.COL = COL2 + (MPTR-1)*75 + (DOW-1)*10
               WCOM.ROW = ROW2
               WCOM.WIDTH = 10
               WCOM.HEIGHT = 1
               WCOM.FCOLOR = "BLACK"
*              WCOM.BCOLOR = "CYAN"
               WCOM.BCOLOR = "YELLOW"
               WCOM.RCOLOR = "BLACK"
               WCOM.DATA = "   ":DAYS<DOW>
               CALL LOAD.DATA(MAT WCOM)       ;* DAY OF WEEK
               WCOM.STYLE = "S"
               WCOM.TYPE = "H"
               WCOM.COL = (COL2+(MPTR-1)*75+(DOW)*10)
               WCOM.ROW = ROW2
               WCOM.HEIGHT = 19
               WCOM.LWIDTH = 2
               CALL DRAW.LINE(MAT WCOM)       ;* DAILY VERT LINE
            NEXT DOW
            FOR WOM = 1 TO 6
               WCOM.TYPE = "H"
               WCOM.COL = 1 + (MPTR-1)*75
               WCOM.ROW = ROW2 + 2 + (WOM-1)*3
               WCOM.WIDTH = 3
               WCOM.DATA = "Sch"
               CALL LOAD.DATA(MAT WCOM)       ;* SCH HEADING
               WCOM.TYPE = "H"
               WCOM.COL = 1 + (MPTR-1)*75
               WCOM.ROW = ROW2 + 3 + (WOM-1)*3
               WCOM.WIDTH = 3
               WCOM.DATA = "Avl"
               CALL LOAD.DATA(MAT WCOM)       ;* AVL HEADING
               WCOM.STYLE = "S"
               WCOM.TYPE = "H"
               WCOM.COL = COL2+(MPTR-1)*75
               WCOM.ROW = ROW2 + 4 + (WOM-1)*3
               WCOM.WIDTH = 70
               CALL DRAW.LINE(MAT WCOM)          ;* BOTTOM HORIZ LINE
            NEXT WOM
         END
*
         BASE.COL = COL2 + (MPTR-1)*75 + MOD((CDAY+OFFSET-1),7)*10
         BASE.ROW = ROW2 + INT((CDAY+OFFSET-1)/7)*3+1
*
         MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         MATREAD PEND.CCTR.SCHED.REC FROM PEND.CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT PEND.CCTR.SCHED.REC = ""
         END
         SHRS = CCS.SCH.HRS + 0
         AHRS = CCS.AVL.HRS - CCS.SCH.HRS
         PHRS = PCCS.SCH.HRS
         JOBS = ""
         JCNT = DCOUNT(CCS.JOB,VM)
         FOR JPTR = 1 TO JCNT
            IF CCS.JOB.HRS<1,JPTR>+0 > 0 THEN
               JOBNO = CCS.JOB<1,JPTR>
               LOCATE JOBNO IN JOBS<1>,1 SETTING JP ELSE
                  JOBS = INSERT(JOBS,1,JP,0,JOBNO)
               END
            END
         NEXT JPTR
         JCNT = DCOUNT(JOBS,VM)
*
         PJOBS = ""
         PJCNT = DCOUNT(PCCS.JOB,VM)
         FOR PJPTR = 1 TO PJCNT
            IF PCCS.JOB.HRS<1,PJPTR>+0 > 0 THEN
               JOBNO = PCCS.JOB<1,PJPTR>
               LOCATE JOBNO IN PJOBS<1>,1 SETTING JP ELSE
                  PJOBS = INSERT(PJOBS,1,JP,0,JOBNO)
               END
            END
         NEXT PJPTR
         PJCNT = DCOUNT(PJOBS,VM)
*
         WCOM.TYPE = "H"
         WCOM.COL = BASE.COL
         WCOM.ROW = BASE.ROW
         WCOM.DATA = "   ":CDAY"R#2"
         WCOM.WIDTH = 10
         WCOM.HEIGHT = 1
         WCOM.FCOLOR = "BLACK"
*        WCOM.BCOLOR = "CYAN"
         WCOM.BCOLOR = "CYAN"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = CDATE
         CALL LOAD.DATA(MAT WCOM)             ;* DAY NUMBER
*
         IF JCNT > 0 THEN
            WCOM.TYPE = "H"
            WCOM.COL = BASE.COL:"+"
            WCOM.ROW = BASE.ROW+1
            WCOM.DATA = JCNT"R#2"
            WCOM.WIDTH = 2
            WCOM.BCOLOR = "IWHITE"
            WCOM.RCOLOR = "IWHITE"
            CALL LOAD.DATA(MAT WCOM)          ;* JOB COUNT
         END
*
         IF SHRS # 0 THEN
            WCOM.TYPE = "H"
            WCOM.COL = BASE.COL+3
            WCOM.ROW = BASE.ROW+1
            WCOM.DATA = OCONV(SHRS,"MD2")"R#6"
            WCOM.WIDTH = 6
            WCOM.BCOLOR = "IWHITE"
            WCOM.RCOLOR = "IWHITE"
            CALL LOAD.DATA(MAT WCOM)          ;* SCHEDULED HOURS
         END
*
         IF PJCNT > 0 THEN
            WCOM.TYPE = "H"
            WCOM.ROW = BASE.ROW+2
            IF PJCNT <= 9 THEN
               WCOM.DATA = PJCNT
               WCOM.COL = (BASE.COL+1):"+"
               WCOM.WIDTH = 1
            END ELSE
               WCOM.DATA = PJCNT"R#2"
               WCOM.COL = BASE.COL:"+"
               WCOM.WIDTH = 2
            END
            IF AHRS >= 0 AND (AHRS-PHRS) < 0 THEN
               WCOM.BCOLOR = "YELLOW"
               WCOM.RCOLOR = "BLACK"
            END ELSE
               WCOM.BCOLOR = "IWHITE"
               WCOM.RCOLOR = "IWHITE"
            END
            CALL LOAD.DATA(MAT WCOM)          ;* PENDING JOB COUNT
         END
*
         IF AHRS # 0 THEN
            WCOM.TYPE = "H"
            WCOM.COL = BASE.COL+3
            WCOM.ROW = BASE.ROW+2
            WCOM.WIDTH = 6
            IF AHRS < 0 THEN
               AHRS = AHRS * (-1)
               WCOM.FCOLOR = "IWHITE"
               WCOM.BCOLOR = "RED"
               WCOM.RCOLOR = "BLACK"
            END ELSE
               WCOM.BCOLOR = "IWHITE"
               WCOM.RCOLOR = "IWHITE"
            END
            WCOM.DATA = OCONV(AHRS,"MD2")"R#6"
            CALL LOAD.DATA(MAT WCOM)          ;* AVAILABLE HOURS
         END
      NEXT CDATE
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
      END
