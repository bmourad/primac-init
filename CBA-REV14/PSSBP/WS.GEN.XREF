      SUBROUTINE WS.GEN.XREF (CONO,XHEAD,XKEYS,XFILE,XFLD,XCOL,XROW,XSORT,XDISP,XCODE,MAT WCOM)
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - WS.GEN.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/10/91
*
* DESCRIPTION
*
* This program is used to display the data in field XFLD from file
* XFILE using CONO and the multi-valued keys provided in XKEYS.
*
*   XHEAD - Caption to be displayed in the Dialog Box.
*
*   XSORT - "CL" = sort by code left  justified
*           "CR" = sort by code right justified
*           "D"  = sort by description left justified
*           ""   = no sort
*
*   XDISP - "C"  = display code only
*           "D"  = display description only
*           ""   = display code and description
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XCODE = ""
      XREF.CODE = ""
      XREF.DESC = ""
      ICNT = 0
      CODELEN = 0
      DESCLEN = LEN(XHEAD)+4
*
*---- MAIN PROCESSING
*
      XCNT = DCOUNT(XKEYS, VM)
      FOR XPTR = 1 TO XCNT
         CODE = XKEYS<1,XPTR>
         READV DESC FROM XFILE, CONO:CODE,XFLD ELSE DESC = ""
         IF TRIM(DESC) # "" THEN
            IF LEN(CODE) > CODELEN THEN CODELEN = LEN(CODE)
            IF LEN(DESC) > DESCLEN THEN DESCLEN = LEN(DESC)
            ICNT = ICNT + 1
            BEGIN CASE
            CASE XSORT= "CL"
               LOCATE CODE IN XREF.CODE<1>,1 BY "AL" SETTING P ELSE NULL
            CASE XSORT= "CR"
               LOCATE CODE IN XREF.CODE<1>,1 BY "AR" SETTING P ELSE NULL
            CASE XSORT= "D"
               LOCATE DESC IN XREF.DESC<1>,1 BY "AL" SETTING P ELSE NULL
               XREF.DESC = INSERT(XREF.DESC,1,P,0,DESC)
            CASE 1
               P = ICNT
            END CASE
            XREF.CODE = INSERT(XREF.CODE,1,P,0,CODE)
         END
      NEXT XPTR
*
      IF XREF.CODE = "" THEN
         WCOM.STATUS = "No selections available"
         GOSUB 90000
         GOTO 99999
      END
      LINE.CNT = DCOUNT(XREF.CODE,VM)
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
      XCODE = ""
      DONE = 0
      LOOP
      UNTIL DONE DO
         CALL GET.INPUT(TYPE,WND,DATA,0,MAT WCOM)
         BEGIN CASE
         CASE DATA = "OK"
            DONE = 1
         CASE DATA = "CANCEL"
            DONE = 1
         CASE WND = 101 AND DATA >= 1 AND DATA <= LINE.CNT
            REF = DATA
            IF REF > 1 THEN XCODE = XREF.CODE<1,REF>
         CASE 1
            WCOM.CAPTION = "WS.CCTR.XREF"
            WCOM.TYPE = "Q"
            WCOM.STATUS = "Received message: ":TYPE:"-":WND:"-":DATA
            GOSUB 90000
         END CASE
      REPEAT
      GOTO 99999
*
*---- DISPLAY DIALOG BOX
*
50000 *
      BEGIN CASE
      CASE XSORT = "CL"
         FMT = "L#":CODELEN
      CASE XSORT = "CR"
         FMT = "R#":CODELEN
      CASE 1
         FMT = "L#":CODELEN
      END CASE
*
      BEGIN CASE
      CASE XDISP = "C"
         LISTLEN = CODELEN
      CASE XDISP = "D"
         LISTLEN = DESCLEN
      CASE 1
         LISTLEN = CODELEN+1+DESCLEN
      END CASE
*
      WCOM.CAPTION = XHEAD
      WCOM.STYLE   = "X"
      WCOM.COL     = XCOL
      WCOM.ROW     = XROW
      WCOM.WIDTH   = LISTLEN+3+8
      WCOM.HEIGHT  = 12
      WCOM.FSIZE   = 8
      WCOM.FTYPE   = "SYSTEM"
      CALL DEFINE.DIALOG.BOX(MAT WCOM)
*
      WCOM.CLASS   = "BUTTON"
      WCOM.DATA    = "OK"
      WCOM.IDENT   = 1
      WCOM.COL     = LISTLEN+2
      WCOM.ROW     = 3
      WCOM.WIDTH   = 8
      WCOM.HEIGHT  = 1.5
      WCOM.FCOLOR  = "BLACK"
      CALL DEFINE.DIALOG.CONTROL(MAT WCOM)
*
      WCOM.CLASS   = "BUTTON"
      WCOM.DATA    = "CANCEL"
      WCOM.IDENT   = 2
      WCOM.COL     = LISTLEN+2
      WCOM.ROW     = 7
      WCOM.WIDTH   = 8
      WCOM.HEIGHT  = 1.5
      WCOM.FCOLOR  = "BLACK"
      CALL DEFINE.DIALOG.CONTROL(MAT WCOM)
*
      WCOM.CLASS   = "LISTBOX"
      WCOM.IDENT   = 101
      WCOM.COL     = 1
      WCOM.ROW     = 1
      WCOM.WIDTH   = LISTLEN
      WCOM.HEIGHT  = 10+1
      WCOM.FCOLOR  = "BLACK"
      CALL DEFINE.DIALOG.CONTROL(MAT WCOM)
*
      FOR REF = 1 TO LINE.CNT
         XCODE = XREF.CODE<1,REF>
         XDESC = XREF.DESC<1,REF>
         BEGIN CASE
         CASE XDISP = "C"
            XDESC = ""
         CASE XDESC = ""
            READV XDESC FROM XFILE, CONO:XCODE,XFLD ELSE XDESC = STR("?",DESCLEN)
         CASE 1
            XDESC = XREF.DESC<1,REF>
         END CASE
         BEGIN CASE
         CASE XDISP = "C"
            DESC = XCODE
         CASE XDISP = "D"
            DESC = XDESC
         CASE 1
            DESC = XCODE FMT:" ":XDESC
         END CASE
         WCOM.IDENT  = 101
         WCOM.DATA   = DESC
         WCOM.SELECT = REF
         CALL LOAD.CONTROL.DATA(MAT WCOM)
      NEXT REF
      CALL PROCESS.DIALOG(MAT WCOM)
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
      END
