      SUBROUTINE SET.VSCROLL.AREA(MAT WCOM)
*********************************************************************
*
* PROGRAM  - SET.VSCROLL.AREA
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/01/91
*
* DESCRIPTION
*
* This subroutine is used to define the area within the specified
* window to be scrolled when a vertical scroll request is made.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.WND          Window ID              Required
* 
*   WCOM.COL          Starting column        Optional
*                                            Default = 0
*                                            Range: 0 - 79
*
*   WCOM.ROW          Starting row           Optional
*                                            Default = 0
*                                            Range: 0 - 23
*
*   WCOM.WIDTH        Width                  Optional
*                                            Default = window last
*                                                      column
*                                            Range: 1 - 80
*
*   WCOM.HEIGHT       Height                 Optional
*                                            Default = window last
*                                                      row
*                                            Range: 1 - 24
*
*********************************************************************
*
      INCLUDE WINCOM FROM PSSBP
      INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
      EMSG = ""
*
      IF WCOM.WND    = "" THEN EMSG = EMSG : " " : "WCOM.WND"
*
      WCOM.STATUS = EMSG
*
      IF WCOM.STATUS = "" THEN
         PMSG = BCMD        : "VR"
         PMSG = PMSG : CSEP : WCOM.WND
         PMSG = PMSG : CSEP : WCOM.COL
         PMSG = PMSG : CSEP : WCOM.ROW
         PMSG = PMSG : CSEP : WCOM.WIDTH
         PMSG = PMSG : CSEP : WCOM.HEIGHT
         PMSG = PMSG : ECMD
         PRINT PMSG :
      END ELSE
         WCOM.STATUS = "(SET.VSCROLL.AREA) INVALID":WCOM.STATUS
      END
*
*---- RESET PARAMETERS USED
*
      WCOM.COL     = ""
      WCOM.ROW     = ""
      WCOM.WIDTH   = ""
      WCOM.HEIGHT  = ""
      RETURN
   END
