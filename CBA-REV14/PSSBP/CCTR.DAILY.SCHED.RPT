********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - CCTR.DAILY.SCHED.RPT
* AUTHOR   - WALID YAMOUT, C.B.A.
* DATE     - 09/20/88
* MODIFIED - 02/07/96, NA, TASK 19937, JOB DESC BASED ON COMPANY FLAG.
* DESCRIPTION
* This program will print Cost Center Daily Schedule Report.
*T22102 renee 07/28/1997 * Adding paper and ink information to this
*                          report.
*T25901 cmykleb 03/26/2002 * Change pgm to use GET.PROG.HEAD for heading.
*********************************************************************
*
*--- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>JCS.CPYLIB>JOB
*COPY>JES.CPYLIB>ESTIMATE                   ; * T22102
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SET UP SYSCOM
*
   SYS.TYPE = 1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*--- INITIALIZATION
*
   PROMPT ""
   ERRMSG = ""
   MAT FILE.VARS = ""
*
*--- PROC READ
*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST RUN FROM PROC"
      GOSUB 91000
      GOTO 999999
   END
   CONO = BUFFER<1>
   CO.NAME = BUFFER<2>
*
*--- OPEN FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "COMPANY FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "DEPARTMENT FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","COST.CNTR" TO COST.CNTR ELSE
      ERRMSG = "COST.CNTR FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","CCTR.SCHED" TO CCTR.SCHED ELSE
      ERRMSG = "CCTR.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","PEND.CCTR.SCHED" TO PEND.CCTR.SCHED ELSE
      ERRMSG = "PEND.CCTR.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","JOB.SCHED" TO JOB.SCHED ELSE
      ERRMSG = "JOB.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","PEND.JOB.SCHED" TO PEND.JOB.SCHED ELSE
      ERRMSG = "PEND.JOB.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","JOB" TO JOB ELSE
      ERRMSG = "JOB FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
* T22102 v   (open ESTIMATE file)
   OPEN "","ESTIMATE" TO ESTIMATE ELSE
      ERRMSG = "ESTIMATE FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
* T22102 ^
   OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
      ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
      ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
      GOSUB 91000
      GOTO 999999
   END
*
*--- SETUP HEADINGS
*
*---- TASK # 19937 ----
   MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ""
*----------------------
   READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
   LINE.CNT = 99
   PG.NO = 0
   PREV.CCTR = ""
   PREV.DEPT = ""
   PREV.DATE = ""
   TOT.DATE = 0
   HD1 = ""
   HD2 = ""
   HD3 = ""
   ST.DATE = BUFFER<3>
   EN.DATE = BUFFER<4>
   HD3 = ST.DATE : " THROUGH " : EN.DATE
   SP = SPACE(80)
   SP3 = SP[1,3]
   SP9 = SP[1,9]
*T25901 v
*  IF BUFFER<7> = "P" THEN
*     PAGE.SIZE = 53
*     BLN = INT((132-LEN(CO.NAME))/2)      ; * T22102 (lengthen heading)
*     HD1 = OCONV(TODAY, "D2/") "L#8"
*     HD1 = HD1 : SPACE(BLN-8)
*     HD1 = HD1 : CO.NAME
*     HD1 = HD1 : SPACE(BLN-10)
*     HD1 = HD1 : "PAGE "
*     HD2 = OCONV(TIME(), "MTS") "L#8"
*     HD2 = HD2 : SPACE(37) : "D A I L Y   S C H E D U L E   R E P O R T"
*     HD3 = SPACE(53) : HD3
*     PRINTER ON
*  END ELSE
*     PAGE.SIZE = 22
*     HD2 = OCONV(TODAY,"D2/") : SP[1,5]
*     HD2 = HD2 : "D A I L Y   S C H E D U L E   R E P O R T"
*     HD2 = HD2 : SP[1,5] : "PAGE "
*     HD3 = SP[1,21] : HD3
*  END
   PAGE.SIZE = 53
   CO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,"",HD1,HD2)
   PRINTER ON
*T25901 ^
   DASH = STR("-",132)   ; * T22102   (changed from 70 to 132)
   HLINE1 = STR(" ", 71):"<------------- Paper ------------>  <--------- Ink --------->"
   HLINE2 = "  Job" : SP[1,14] : "Description" : SP[1,12]
   HLINE2 = HLINE2 : "Date   Day  Hours   Accum"
* T22102   (added next line..... a new heading)
   HLINE2 = HLINE2 : "    Product ID        Quantity   Price  Product IDs"
   HLINE3 = "--------  " : DASH[1,30] : "  -----  ---  -----  ------"
* T22102   (added next line..... a new heading)
   HLINE3 = HLINE3 : "    --------------- ----------  ------  -------------------------"
*
*--- MAIN PROCESS
*
100*
   READNEXT ID ELSE GOTO 999999
   MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, ID ELSE GOTO 100
   IF CCS.JOB = "" THEN GOTO 100
   CCTR.NO = FIELD(ID,"*",1)
   CCTR.NO = CCTR.NO[4,3]
   CC.DATE = FIELD(ID,"*",2)
*  IF CC.DATE < TODAY THEN
   JCNT = DCOUNT(CCS.JOB,VM)
   BLK = 1
   FOR JB = 1 TO JCNT WHILE BLK
      BEGIN CASE
         CASE CCS.JOB<1,JB> = ""
         CASE CCS.JOB.HRS<1,JB> + 0 = 0
         CASE CCS.JOB.EQUIP<1,JB> + 0 = 0
         CASE CCS.OPER.STATUS<1,JB> # "C"
            BLK = 0
         CASE 1
      END CASE
   NEXT JB
   IF BLK THEN GOTO 100
*  END
   PREV.CCTR = CCTR.NO
   PREV.DEPT = CCS.DEPT
   PREV.DATE = CC.DATE
   MATREAD CCTR.REC FROM COST.CNTR, CONO : PREV.CCTR ELSE
      MAT CCTR.REC = ""
      CCTR.DESC = "CCTR - " : PREV.CCTR
   END
   MATREAD DEPT.REC FROM DEPARTMENT, CONO : PREV.DEPT ELSE
      MAT DEPT.REC = ""
      DEPT.DESC = "DEPT - " : PREV.DEPT
   END
   GOSUB 80000
   GOSUB 1000
*
*--- PROCESS ALL RECORDS
*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, ID ELSE GOTO 199
      IF CCS.JOB = "" THEN GOTO 199
      CCTR.NO = FIELD(ID,"*",1)
      CCTR.NO = CCTR.NO[4,3]
      CC.DATE = FIELD(ID,"*",2)
*     IF CC.DATE < TODAY THEN
      JCNT = DCOUNT(CCS.JOB,VM)
      BLK = 1
      FOR JB = 1 TO JCNT WHILE BLK
         BEGIN CASE
            CASE CCS.JOB<1,JB> = ""
            CASE CCS.JOB.HRS<1,JB> + 0 = 0
            CASE CCS.JOB.EQUIP<1,JB> + 0 = 0
            CASE CCS.OPER.STATUS<1,JB> # "C"
               BLK = 0
            CASE 1
         END CASE
      NEXT JB
      IF BLK THEN GOTO 199
*     END
      BEGIN CASE
         CASE CCS.DEPT # PREV.DEPT
            LINE.CNT = 99
            PREV.DEPT = CCS.DEPT
            PREV.CCTR = CCTR.NO
            PREV.DATE = CC.DATE
            TOT.DATE = 0
            MATREAD CCTR.REC FROM COST.CNTR, CONO : PREV.CCTR ELSE
               MAT CCTR.REC = ""
               CCTR.DESC = "CCTR - " : PREV.CCTR
            END
            MATREAD DEPT.REC FROM DEPARTMENT, CONO : PREV.DEPT ELSE
               MAT DEPT.REC = ""
               DEPT.DESC = "DEPT - " : PREV.DEPT
            END
         CASE CCTR.NO # PREV.CCTR
            LINE.CNT = 99
            PREV.CCTR = CCTR.NO
            PREV.DATE = CC.DATE
            TOT.DATE = 0
            MATREAD CCTR.REC FROM COST.CNTR, CONO : PREV.CCTR ELSE
               MAT CCTR.REC = ""
               CCTR.DESC = "CCTR - " : PREV.CCTR
            END
         CASE CC.DATE # PREV.DATE
            IF LINE.CNT < PAGE.SIZE THEN
               PRINT
               LINE.CNT = LINE.CNT + 1
            END
            PREV.DATE = CC.DATE
            TOT.DATE = 0
      END CASE
      GOSUB 1000
199*
   REPEAT
   PRINTER OFF
   GOTO 999999
*
*--- PRINT LINES
*
1000*
   ALL.JOB = ""
   ALL.HRS = ""
*--- SUMMARY BY EQUIP BY SHIFT
   JCNT = DCOUNT(CCS.JOB,VM)
   FOR JB = JCNT TO 1 STEP - 1
      IF CCS.JOB<1,JB> = "" THEN GOTO 1099
      IF CCS.JOB.HRS<1,JB> + 0 = 0 THEN GOTO 1099
      IF CCS.JOB.EQUIP<1,JB> + 0 = 0 THEN GOTO 1099
*        IF CC.DATE < TODAY AND CCS.OPER.STATUS<1,JB> = "C" THEN GOTO 1099
      IF CCS.OPER.STATUS<1,JB> = "C" THEN GOTO 1099
      EQP = CCS.JOB.EQUIP<1,JB>
      BEGIN CASE
         CASE CCS.JOB.SHIFT<1,JB> = 1
            SFT = 1
         CASE CCS.JOB.SHIFT<1,JB> = 2
            SFT = 2
         CASE 1
            SFT = 3
      END CASE
      LOCATE CCS.JOB<1,JB> IN ALL.JOB<EQP,SFT>,1 SETTING FND ELSE FND = 0
      IF FND THEN
         ALL.HRS<EQP,SFT,FND> = ALL.HRS<EQP,SFT,FND> + CCS.JOB.HRS<1,JB>
      END ELSE
         ALL.JOB = INSERT(ALL.JOB,EQP,SFT,1,CCS.JOB<1,JB>)
         ALL.HRS = INSERT(ALL.HRS,EQP,SFT,1,CCS.JOB.HRS<1,JB>)
      END
1099*
   NEXT JB
*--- SUMMARY BY EQUIP
1100*
   XEQP = ""
   XJOB = ""
   XHRS = ""
   ECNT = DCOUNT(ALL.JOB,AM)
   FOR EQP = 1 TO ECNT
      SCNT = DCOUNT(ALL.JOB<EQP>,VM)
      IF SCNT > 2 THEN
         JCNT = DCOUNT(ALL.JOB<EQP,3>,SVM)
         FOR JB = JCNT TO 1 STEP - 1
            JOB.NO = ALL.JOB<EQP,3,JB>
            HRS = ALL.HRS<EQP,3,JB>
            LOCATE JOB.NO IN ALL.JOB<EQP,2>,1 SETTING FND ELSE FND = 0
            IF FND THEN
               HRS = HRS + ALL.HRS<EQP,2,FND>
               ALL.JOB = DELETE(ALL.JOB,EQP,3,JB)
               ALL.HRS = DELETE(ALL.HRS,EQP,3,JB)
               ALL.JOB = DELETE(ALL.JOB,EQP,2,FND)
               ALL.HRS = DELETE(ALL.HRS,EQP,2,FND)
               TF = DCOUNT(ALL.JOB<EQP,2>,VM) + 1
               ALL.JOB = INSERT(ALL.JOB,EQP,2,TF,JOB.NO)
               ALL.HRS = INSERT(ALL.HRS,EQP,2,TF,HRS)
            END
         NEXT JB
      END
      IF SCNT > 1 THEN
         JCNT = DCOUNT(ALL.JOB<EQP,1>,SVM)
         FOR JB = JCNT TO 1 STEP - 1
            JOB.NO = ALL.JOB<EQP,1,JB>
            HRS = ALL.HRS<EQP,1,JB>
            LOCATE JOB.NO IN ALL.JOB<EQP,2>,1 SETTING FND ELSE FND = 0
            IF FND THEN
               HRS = HRS + ALL.HRS<EQP,2,FND>
               ALL.JOB = DELETE(ALL.JOB,EQP,1,JB)
               ALL.HRS = DELETE(ALL.HRS,EQP,1,JB)
               ALL.JOB = DELETE(ALL.JOB,EQP,2,FND)
               ALL.HRS = DELETE(ALL.HRS,EQP,2,FND)
               TF = 1
               ALL.JOB = INSERT(ALL.JOB,EQP,2,TF,JOB.NO)
               ALL.HRS = INSERT(ALL.HRS,EQP,2,TF,HRS)
            END
         NEXT JB
      END
      FOR SFT = SCNT TO 1 STEP - 1
         JCNT = DCOUNT(ALL.JOB<EQP,SFT>,SVM)
         FOR JB = JCNT TO 1 STEP - 1
            XEQP<EQP> = EQP
            XJOB = INSERT(XJOB,EQP,1,0,ALL.JOB<EQP,SFT,JB>)
            XHRS = INSERT(XHRS,EQP,1,0,ALL.HRS<EQP,SFT,JB>)
         NEXT JB
      NEXT SFT
   NEXT EQP
   ALL.JOB = ""
   ALL.HRS = ""
   ECNT = DCOUNT(XEQP,AM)
   FOR EQP = ECNT TO 1 STEP - 1
      IF XEQP<EQP> = "" THEN
         XEQP = DELETE(XEQP,EQP,0,0)
         XJOB = DELETE(XJOB,EQP,0,0)
         XHRS = DELETE(XHRS,EQP,0,0)
      END
   NEXT EQP
*--- PRINT THE DAY
1200*
   ECNT = DCOUNT(XEQP,AM)
   FOR EQP = 1 TO ECNT
      JCNT = DCOUNT(XJOB<EQP>,VM)
      FOR JB = 1 TO JCNT
         IF LINE.CNT >= PAGE.SIZE THEN
            GOSUB 80000
         END
         JOB.NO = XJOB<EQP,JB>
         TOT.DATE = TOT.DATE + XHRS<EQP,JB>
         MATREAD JBS.REC FROM JOB.SCHED, CONO : JOB.NO ELSE
            MAT JBS.REC = ""
            JBS.CUST.NAME = "NOT ON FILE"
         END
*---- TASK # 19937 ----
         JDESC = JBS.CUST.NAME
         IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
            MATREAD JOB.REC FROM JOB, CONO:JOB.NO THEN
               IF TRIM(JOB.DESC<1,1>) # "" THEN
                  IF CO.PSS.JDESC.FLAG = "J" THEN
                     JDESC = JOB.DESC<1,1>
                  END ELSE
                     JDESC = JDESC[1,10]:":":JOB.DESC<1,1>
                  END
               END
            END
         END
*----------------------
         V.LINE = ""
*---- TASK # 19937 ----
*           V.LINE = V.LINE : JOB.NO "L#8  " : JBS.CUST.NAME "L#30  "
         V.LINE = V.LINE : JOB.NO "L#8  " : JDESC "L#30  "
*----------------------
         V.LINE = V.LINE : OCONV(PREV.DATE, "D2/")[1,5] "L#5  "
         V.LINE = V.LINE : OCONV(PREV.DATE, "DWA")[1,3] "L#3 "
         V.LINE = V.LINE : OCONV(XHRS<EQP,JB>, "MD2") "R#6  "
         V.LINE = V.LINE : OCONV(TOT.DATE, "MD2") "R#6  "
         IF XEQP<EQP> > 1 THEN
            V.LINE = V.LINE : XEQP<EQP> "R#1"
* T22102 v   (get and print paper and ink info)
         END ELSE
            V.LINE = V.LINE : " "
         END
            *
         GOSUB GET.PAPER.AND.INK
* T22102 ^
         PRINT V.LINE
         LINE.CNT = LINE.CNT + 1
      NEXT JB
      TOT.DATE = 0
   NEXT EQP
   RETURN
* T22102 v    (new subroutine....append paper and ink info to V.LINE)
******************
GET.PAPER.AND.INK: 
******************
   MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE MAT JOB.REC = ""
   MATREAD EST.REC FROM ESTIMATE, CONO:JOB.EST ELSE MAT EST.REC = ""
*
* Get the paper product number from the first component
   PAPER.ID = EST.PROD.INV.ID<1,1,1>
   IF PAPER.ID = "" THEN PAPER.ID = EST.PROD.OS.PROD<1,1,1>
*
   T.QTY = JOB.QTY<1,1,1>
   LOCATE T.QTY IN EST.QTY<1> SETTING POS ELSE POS = 1
*
   PAPER.QTY = ""
   T.QTY = EST.PROD.PQTY<1,1,1>
   PAPER.QTY = FIELD(T.QTY, "!", POS)
*
   PAPER.COST = ""
   T.COST = EST.PROD.PCST<1,1,1>
   PAPER.COST = FIELD(T.COST, "!", POS)
   PAPER.COST = OCONV(PAPER.COST,'MD2')
*
   INK.LIST = ""
   T.INK = EST.PROD.INK.ID<1,1,1>
   NUM.INK = DCOUNT(T.INK, "!")
   FOR INK.NO = 1 TO NUM.INK
      ONE.INK = FIELD(T.INK, "!", INK.NO)
      INK.LIST = INK.LIST:",":ONE.INK
   NEXT INK.NO
   INK.LIST = INK.LIST[2,99]
*
   V.LINE = V.LINE:" ":PAPER.ID"L#15":" ":PAPER.QTY"R,#10":"  ":PAPER.COST"R2#6"
   V.LINE = V.LINE:"  ":INK.LIST"L#25"
*
   RETURN
* T22102 ^
*
*--- HEADING ROUTINE
*
80000*
   PRINT CHAR(12)
   PG.NO = PG.NO + 1
   LINE.CNT = 1
   PRINT HD1 : PG.NO
   PRINT HD2
   PRINT HD3
   PRINT
   CDESC = "(":CCTR.NO:") ":CCTR.DESC
   DDESC = "(":CCS.DEPT:") ":DEPT.DESC
   PRINT CDESC "L#36" : SP3 : DDESC "R#30"
   PRINT
   PRINT DASH
   PRINT HLINE1
   PRINT HLINE2
   PRINT HLINE3                      ; * T22102
   LINE.CNT = LINE.CNT + 9           ; * T22102 (changed from 8 to 9)
   RETURN
*
*--- ERROR ROUTINES
*
91000*
   ERR.TYPE = 1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
999999*
END
