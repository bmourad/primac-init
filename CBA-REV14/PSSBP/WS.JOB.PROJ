      SUBROUTINE WS.JOB.PROJ(CONO, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.JOB.PROJ
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 03/03/94
*
* DESCRIPTION
*
* This subroutine displays the status of the specified job in a
* graphical format. The program displays percent complete and
* additional hours required based upon the reported quantities
* reported via the shop-floor data collection system.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>PSSBP>WINCOM2
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      JOBNO = WIN.CURR.JOB
      MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      MATREAD PJBS.REC FROM PEND.JOB.SCHED, CONO:JOBNO ELSE MAT PJBS.REC = ""
      MATREAD JOB.REC FROM JOB, CONO:JOBNO ELSE
         MAT JOB.REC = ""
      END
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
         MAT CUST.REC = ""
         CUST.NAME = JBS.CUST.NAME
      END
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      WIN.JSEL<WPTR> = WIN.CURR.JOB
      LOCATE WCOM.WND IN WIN.SEL.ID,1 SETTING SPTR ELSE WIN.SEL.ID<SPTR>=WCOM.WND
      WIN.SEL.JOB<SPTR>  = WIN.CURR.JOB
*
      LCNT = 0
      CCNT = DCOUNT(JBS.CCTR,VM)
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         LCNT = LCNT + 1
         SHRS = INT(JBS.SCH.HRS<1,CPTR>/10+0.5)
         AHRS = INT(JBS.ACT.HRS<1,CPTR>/10+0.5)
         SQTY = INT(JBS.SCH.IMP<1,CPTR>/100)
         AQTY = INT(JBS.ACT.IMP<1,CPTR>/100)
      NEXT CPTR
*
*---- OPEN WINDOW
*
      HEAD.CNT=5
      COL2=45
      MAX2=12
      WCOM.CAPTION="J O B   S T A T U S"
      WCOM.COL="C"
      WCOM.ROW="C"
      WCOM.WIDTH=COL2+MAX2+15
      BEGIN CASE
      CASE (HEAD.CNT+LCNT) > 18
         WHEIGHT = HEAD.CNT+14
      CASE 1
         WHEIGHT = HEAD.CNT+LCNT
      END CASE
      WCOM.HEIGHT = WHEIGHT
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.ROW = HEAD.CNT
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY DEMOGRAPHIC DATA
*
      WCOM.COL=1
      WCOM.ROW=0
      WCOM.HEIGHT=1:YADJ
      WCOM.RCOLOR="BLACK"
      WCOM.DATA = " Job # ":JOBNO:" "
      WCOM.SELECT = "101"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=20
      WCOM.ROW=0
      WCOM.WIDTH=30:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = CUST.NAME[1,30]
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=20
      WCOM.ROW=1
      WCOM.WIDTH=45:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = JOB.DESC<1,1>
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=6
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = "Scheduled"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=16
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = OCONV(JBS.SCH.DATE<1,1>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=26
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = " Forecast"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=36
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = OCONV(JBS.EXP.DATE<1,CCNT>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=46
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = " Required"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=56
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      IF JBS.DUE.DATE < JBS.EXP.DATE<1,CCNT> THEN
         WCOM.FCOLOR="YELLOW"
         WCOM.BCOLOR="RED"
         WCOM.RCOLOR="BLACK"
      END
      WCOM.DATA = OCONV(JBS.DUE.DATE,"D2/")
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY COST CENTER STATUS
*
      WCOM.DATA = " Cost Center"
      WCOM.COL = 0
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 30:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* COST CENTER HEADING
*
      WCOM.DATA = "Sch Hr"
      WCOM.COL = 30
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 7:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* SCHEDULED HOURS HEADING
*
      WCOM.DATA = "Sch Qty"
      WCOM.COL = 37
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 8:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* SCHEDULED QUANTITY HEADING
*
      HDR = " % COMPLETE "
      WCOM.DATA = HDR
      WCOM.COL = COL2
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = MAX2:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* CHART HEADING
*
      WCOM.DATA = "Rem Hr"
      WCOM.COL = 30 + 15 + MAX2
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 7:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* REMAINING HOURS HEADING
*
      WCOM.DATA = "Rem Qty"
      WCOM.COL = 30 + 15 + MAX2 + 7
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 8:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* REMAINING QUANTITY HEADING
*
      FOR N = 0 TO 3
         WCOM.STYLE = "S"
         WCOM.COL = COL2+N*(MAX2/4)
         WCOM.ROW = HEAD.CNT
         WCOM.WIDTH = 0
         WCOM.HEIGHT = WHEIGHT
         WCOM.FCOLOR = "GREY"
         CALL DRAW.LINE(MAT WCOM)
      NEXT N
*
      ROW = HEAD.CNT-1
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "??????????"
         END
         ROW = ROW + 1
         WCOM.DATA = " ":CCTR"L#3":" ":CCTR.DESC
         WCOM.TYPE = "V"
         WCOM.COL = 0
         WCOM.ROW = ROW
         WCOM.WIDTH = 30:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = ""
         CALL LOAD.DATA(MAT WCOM)
         STAT = JBS.CCTR.STATUS<1,CPTR>
         SHRS = INT(JBS.SCH.HRS<1,CPTR>/10+0.5)
         AHRS = INT(JBS.ACT.HRS<1,CPTR>/10+0.5)
         TOTHRS = SHRS + AHRS
         SQTY = INT(JBS.SCH.IMP<1,CPTR>/100)
         AQTY = INT(JBS.ACT.IMP<1,CPTR>/100)
*
         BEGIN CASE
         CASE SHRS = 0 AND AHRS = 0
            SH = SHRS
            SQ = SQTY
            W1 = 0
            W2 = 0
            RH = 0
            RQ = 0
         CASE STAT = "C" AND SHRS >= AHRS
            SH = SHRS
            SQ = SQTY
            W1 = MAX2 * AHRS / SHRS;         C1 = "GREEN"
            W2 = 0
            RH = 0
            RQ = 0
         CASE STAT = "C" AND SHRS < AHRS
            SH = SHRS
            SQ = SQTY
            W1 = MAX2;                       C1 = "GREEN"
            W2 = 0
            RH = 0
            RQ = 0
         CASE SHRS >= AHRS AND SQTY = 0
            SH = SHRS
            SQ = SQTY
            W1 = MAX2 * AHRS / SHRS;         C1 = "IBLUE"
            W2 = 0
            RH = SHRS - AHRS
            RQ = 0
         CASE SHRS < AHRS AND SQTY = 0
            SH = SHRS
            SQ = SQTY
            W1 = MAX2;                       C1 = "IBLUE"
            W2 = 0
            RH = SHRS - AHRS
            RQ = 0
         CASE SQTY = 0 OR AQTY = 0
            SH = SHRS
            SQ = SQTY
            W1 = 0
            W2 = 0
            RH = SHRS - AHRS
            RQ = SQTY - AQTY
         CASE SQTY >= AQTY
            SH = SHRS
            SQ = SQTY
            W1 = MAX2 * AQTY / SQTY;         C1 = "IBLUE"
            W2 = 0
            RH = (AHRS * SQTY / AQTY) - AHRS
            RQ = SQTY - AQTY
         CASE SQTY < AQTY
            SH = SHRS
            SQ = SQTY
            IF SQTY = 0 THEN
               W1 = 0
               W2 = 0
            END ELSE
               W1 = MAX2;                    C1 = "IBLUE"
               W2 = 0
            END
            RH = (AHRS * SQTY / AQTY) - AHRS
            RQ = SQTY - AQTY
         END CASE
*
         WCOM.DATA = OCONV(SH,"MD1")"R#6"
         WCOM.TYPE = "V"
         WCOM.COL = 30
         WCOM.ROW = ROW
         WCOM.WIDTH = 7:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = ""
         CALL LOAD.DATA(MAT WCOM)
*
         IF SQ > 0 THEN
            WCOM.DATA = OCONV(SQ,"MD0")"R#7"
            WCOM.TYPE = "V"
            WCOM.COL = 37
            WCOM.ROW = ROW
            WCOM.WIDTH = 8:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = "WHITE"
            WCOM.RCOLOR = "BLACK"
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
         END
*
         IF W1 > 0 THEN
            WCOM.DATA = ""
            WCOM.TYPE = "V"
            WCOM.LEVEL = 5
            WCOM.COL = COL2+0.2
            WCOM.ROW = ROW+0.1
            WCOM.WIDTH = W1:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "GREY"
            WCOM.BCOLOR = "GREY"
            WCOM.RCOLOR = "GREY"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 1
            WCOM.DATA = ""
            WCOM.TYPE = "V"
            WCOM.LEVEL = 10
            WCOM.COL = COL2
            WCOM.ROW = ROW
            WCOM.WIDTH = W1:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = C1
            WCOM.RCOLOR = "BLACK"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 1
         END
         IF W2 > 0 THEN
            WCOM.DATA = ""
            WCOM.TYPE = "V"
            WCOM.LEVEL = 5
            WCOM.COL = COL2+W1+0.2
            WCOM.ROW = ROW+0.1
            WCOM.WIDTH = W2:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "GREY"
            WCOM.BCOLOR = "GREY"
            WCOM.RCOLOR = "GREY"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 2
            WCOM.DATA = ""
            WCOM.TYPE = "V"
            WCOM.LEVEL = 10
            WCOM.COL = COL2+W1
            WCOM.ROW = ROW
            WCOM.WIDTH = W2:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = C2
            WCOM.RCOLOR = "BLACK"
            CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 2
         END
*
         IF RH # 0 THEN
            WCOM.TYPE = "V"
            WCOM.COL = 30 + 15 + MAX2
            WCOM.ROW = ROW
            WCOM.WIDTH = 7:XADJ
            IF RH > 0 THEN
               WCOM.DATA = OCONV(RH,"MD1")"R#6"
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
            END ELSE
               WCOM.DATA = OCONV((0-RH),"MD1")"R#6"
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "IRED"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
            END
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
         END
*
         IF RQ # 0 THEN
            WCOM.TYPE = "V"
            WCOM.COL = 30 + 15 + MAX2 + 7
            WCOM.ROW = ROW
            WCOM.WIDTH = 8:XADJ
            IF RQ > 0 THEN
               WCOM.DATA = OCONV(RQ,"MD0")"R#7"
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
            END ELSE
               WCOM.DATA = OCONV((0-RQ),"MD0")"R#7"
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "IRED"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
            END
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
         END
      NEXT CPTR
*
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
