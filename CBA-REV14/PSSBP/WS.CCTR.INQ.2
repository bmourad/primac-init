      SUBROUTINE WS.CCTR.INQ.2(CONO, SDATE, EDATE, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.CCTR.INQ.2
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 03/02/93
*
* DESCRIPTION
*
* This subroutine displays available hours for the specified cost center.
* If the scheduled hours exceed the available hours for the shift, then
* the hours are display within a red rectangle.
* If the scheduled hours for any resource exceeds the available hours
* for the resource, the hours are displayed within a yellow rectangle.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      DAYS = ""
      DAYS<1> = "SUN"
      DAYS<2> = "MON"
      DAYS<3> = "TUE"
      DAYS<4> = "WED"
      DAYS<5> = "THU"
      DAYS<6> = "FRI"
      DAYS<7> = "SAT"
*
      DIM AHRS(28)
      DIM MULT(28)
      DIM SHRS(28)
      DIM PHRS(28)
*
      HCOL = 24
*
      MAXHRS = ""
      CSDATE = ""
      LOCATE "C1" IN WIN.ID,1 SETTING WPTR THEN
         CSDATE = WIN.DSEL<WPTR>
*        MAXHRS = WIN.TEMP<WPTR,1>
      END
      IF CSDATE = "" THEN
         LOCATE "C2" IN WIN.ID,1 SETTING WPTR THEN CSDATE = WIN.DSEL<WPTR>
      END
      IF CSDATE = "" THEN
         CSDATE = WIN.CURR.DATE
      END
      CEDATE = CSDATE
**    CSDATE = SDATE
**    CEDATE = SDATE+28-1
*     ODATE = OCONV(SDATE,"D2-")
*     ODATE = ODATE[1,3]:"01":ODATE[6,3]
*     CSDATE = ICONV(ODATE,"D")
*     ODATE = OCONV(SDATE+3*31,"D2-")
*     ODATE = ODATE[1,3]:"01":ODATE[6,3]
*     CEDATE = ICONV(ODATE,"D")-1
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      CCTR = WIN.CURR.CCTR
      LOCATE CCTR IN WIN.CCTR<1>,1 SETTING CPTR ELSE NULL
      WIN.CSEL<WPTR> = CCTR
*
      IF MAXHRS = "" THEN MAXHRS = 800
      MAT AHRS = ""
      MAT MULT = ""
      MAT SHRS = ""
      MAT PHRS = ""
      FOR CDATE = CSDATE TO CEDATE
         DPTR = CDATE - CSDATE + 1
         MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         MATREAD PEND.CCTR.SCHED.REC FROM PEND.CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT PEND.CCTR.SCHED.REC = ""
         END
         FOR SFT = 1 TO 3
            AHRS(DPTR)<SFT> = CCS.AVL.SHR<1,SFT>
            IF CCS.MULT<1,SFT>+0 = 0 THEN
               MULT(DPTR)<SFT> = 1
            END ELSE
               MULT(DPTR)<SFT> = CCS.MULT<1,SFT>
            END
            SHRS(DPTR)<SFT> = SUM(CCS.SCH.SM.HRS<1,SFT>)
            PHRS(DPTR)<SFT> = SUM(PCCS.SCH.SM.HRS<1,SFT>)
            AH = AHRS(DPTR)<SFT> * MULT(DPTR)<SFT>
            SH = SHRS(DPTR)<SFT>+0
            PH = PHRS(DPTR)<SFT>+0
            IF AH > MAXHRS THEN MAXHRS = AH
            IF (SH+PH) > MAXHRS THEN MAXHRS = (SH+PH)
         NEXT SFT
      NEXT CDATE
      MAXHRS = INT(MAXHRS / 800 + 0.99) * 800
*
*---- OPEN WINDOW
*
      HEAD.CNT=1
      WCOM.CAPTION=WIN.CDESC<1,CPTR>:" (":CCTR:")"
      WCOM.COL="C"
      WCOM.ROW="C"
      WCOM.WIDTH=68
**    WCOM.HEIGHT = HEAD.CNT+14
      WCOM.HEIGHT = HEAD.CNT+3
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.ROW = HEAD.CNT
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY COST CENTER DATA
*
      SCOL = 25
      WCOM.DATA = "   Date   "
      WCOM.COL = 0
      WCOM.ROW = 0
      WCOM.WIDTH = 10:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* DATE HEADING
*
      WCOM.DATA = " Day "
      WCOM.COL = 10
      WCOM.ROW = 0
      WCOM.WIDTH = 5:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* DAY HEADING
*
      WCOM.DATA = "  Total"
      WCOM.COL = 15
      WCOM.ROW = 0
      WCOM.WIDTH = 9:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* AVAILABLE HOURS HEADING
*
      WCOM.COL = SCOL
      WCOM.ROW = 1
      WCOM.HEIGHT = HEAD.CNT+14+1
      CALL DRAW.LINE(MAT WCOM)
*
      WCOM.COL = 49
      WCOM.ROW = 1
      WCOM.HEIGHT = HEAD.CNT+14+1
      CALL DRAW.LINE(MAT WCOM)
*
      HDR = INT(MAXHRS/100):" HOURS"
      LH = LEN(HDR)
      D1 = STR("-",INT((HCOL-LH-4)/2))
      D2 = STR("-",(HCOL-LH-4-LEN(D1)))
      WCOM.DATA = "<":D1:" ":HDR:" ":D2:">"
      WCOM.COL = SCOL
      WCOM.ROW = 0
      WCOM.WIDTH = HCOL
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* GANT CHART HEADING
*
      WCOM.DATA = "  Sched"
      WCOM.COL = 50
      WCOM.ROW = 0
      WCOM.WIDTH = 9:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* SCHEDULED HOURS HEADING
*
      WCOM.DATA = " Pending"
      WCOM.COL = 59
      WCOM.ROW = 0
      WCOM.WIDTH = 9:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* PENDING HOURS HEADING
*
      WIN.DATE<WPTR> = ""
      FOR CDATE = CSDATE TO CEDATE
         MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, CONO:CCTR:"*":CDATE ELSE
            MAT CCTR.SCHED.REC = ""
         END
         DPTR = CDATE - CSDATE + 1
         RPTR = (DPTR-1) * 3 + 1
         WIN.DATE<WPTR,DPTR> = CDATE
         WCOM.DATA = " ":OCONV(CDATE,"D2/")
         WCOM.TYPE = "V"
         WCOM.COL = 0
         WCOM.ROW = RPTR
         WCOM.WIDTH = 10:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = CDATE
         CALL LOAD.DATA(MAT WCOM)          ;* DATE
*
         WCOM.DATA = " ":DAYS<MOD(CDATE,7)+1>
         WCOM.TYPE = "V"
         WCOM.COL = 10
         WCOM.ROW = RPTR
         WCOM.WIDTH = 5:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = ""
         CALL LOAD.DATA(MAT WCOM)          ;* DAY OF WEEK
*
         FOR SFT = 1 TO 3
            AH = AHRS(DPTR)<SFT> * MULT(DPTR)<SFT>
            SH = SHRS(DPTR)<SFT>+0
            PH = PHRS(DPTR)<SFT>+0
*
            IF AH > 0 THEN
               WCOM.DATA = OCONV(AH,"MD2")"R#8"
               WCOM.TYPE = "V"
               WCOM.COL = 15
               WCOM.ROW = (RPTR+SFT-1)
               WCOM.WIDTH = 9:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)          ;* AVAILABLE HOURS
            END
*
            W1 = 0; W2 = 0; W3 = 0; W4 = 0; W5 = 0; W6 = 0
            BEGIN CASE
            CASE PH = 0
               BEGIN CASE
               CASE AH = 0 AND SH = 0
               CASE AH > SH
                  W1 = SH * HCOL / MAXHRS;            C1 = "GREEN"
                  W3 = AH * HCOL / MAXHRS - W1;       C3 = "WHITE"
               CASE AH <= SH
                  W1 = AH * HCOL / MAXHRS;            C1 = "GREEN"
                  W3 = SH * HCOL / MAXHRS - W1;       C3 = "RED"
               END CASE
            CASE PH > 0
               BEGIN CASE
               CASE AH <= SH AND AH < (SH+PH)
                  W1 = AH * HCOL / MAXHRS;            C1 = "GREEN"
                  W3 = SH * HCOL / MAXHRS - W1;       C3 = "RED"
                  W6 = PH * HCOL / MAXHRS;            C6 = "RED"
               CASE AH > SH AND AH < (SH+PH)
                  W1 = SH * HCOL / MAXHRS;            C1 = "GREEN"
                  W3 = AH * HCOL / MAXHRS - W1;       C3 = "WHITE"
                  W4 = W3;                            C4 = "GREEN"
                  W6 = PH * HCOL / MAXHRS - W4;       C6 = "RED"
               CASE AH > SH AND AH >= (SH+PH)
                  W1 = SH * HCOL / MAXHRS;            C1 = "GREEN"
                  W3 = AH * HCOL / MAXHRS - W1;       C3 = "WHITE"
                  W4 = PH * HCOL / MAXHRS;            C4 = "GREEN"
*                 W6 = W3 - W4;                       C6 = "WHITE"
               END CASE
            END CASE
            IF W1 > 0 THEN
               WCOM.TYPE = "V"
               WCOM.COL = SCOL
               WCOM.ROW = (RPTR+SFT-1)
               WCOM.WIDTH = W1:XADJ
               IF W2 = 0 THEN WCOM.HEIGHT = 1:YADJ ELSE WCOM.HEIGHT = 0.7:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C1
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)
            END
            IF W2 > 0 THEN
               WCOM.TYPE = "V"
               WCOM.COL = SCOL
               WCOM.ROW = (RPTR+SFT-1) + 0.7
               WCOM.WIDTH = W2:XADJ
               WCOM.HEIGHT = 0.3:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C2
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)
            END ELSE
               W2 = W1
            END
            IF W3 > 0 THEN
               WCOM.TYPE = "V"
               WCOM.COL = SCOL + W1
               WCOM.ROW = (RPTR+SFT-1)
               WCOM.WIDTH = W3:XADJ
               IF W4 = 0 THEN WCOM.HEIGHT = 1:YADJ ELSE WCOM.HEIGHT = 0.7:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C3
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)
            END
            IF W4 > 0 THEN
               WCOM.TYPE = "V"
               WCOM.COL = SCOL + W2
               WCOM.ROW = (RPTR+SFT-1) + 0.7
               WCOM.WIDTH = W4:XADJ
               WCOM.HEIGHT = 0.3:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C4
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)
            END ELSE
               W4 = W3
            END
            IF W5 > 0 THEN
               WCOM.TYPE = "V"
               WCOM.COL = SCOL + W1 + W3
               WCOM.ROW = (RPTR+SFT-1)
               WCOM.WIDTH = W5:XADJ
               IF W6 = 0 THEN WCOM.HEIGHT = 1:YADJ ELSE WCOM.HEIGHT = 0.7:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C5
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)
            END
            IF W6 > 0 THEN
               WCOM.TYPE = "V"
               WCOM.COL = SCOL + W2 + W4
               WCOM.ROW = (RPTR+SFT-1) + 0.7
               WCOM.WIDTH = W6:XADJ
               WCOM.HEIGHT = 0.3:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C6
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)
            END
*
            IF SH > 0 THEN
               WCOM.DATA = OCONV(SH,"MD2")"R#8"
               WCOM.TYPE = "V"
               WCOM.COL = 50
               WCOM.ROW = (RPTR+SFT-1)
               WCOM.WIDTH = 9:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)          ;* SCHEDULED HOURS
            END
*
            IF PH > 0 THEN
               WCOM.DATA = OCONV(PH,"MD2")"R#8"
               WCOM.TYPE = "V"
               WCOM.COL = 59
               WCOM.ROW = (RPTR+SFT-1)
               WCOM.WIDTH = 9:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)          ;* PENDING HOURS
            END
         NEXT SFT
      NEXT CDATE
*
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
