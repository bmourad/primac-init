      SUBROUTINE TRANSACTION.TERM (CONO,DIV,PSS.JOURNAL,PSS.LOCK,ERRMSG)
**************************************************************************
*
* PROGRAM - TRANSACTION.TERM
*
* AUTHOR  - NICK AMENDOLA, NASTech, Inc.
*
* DATE    - 06/10/96
*
* DESCRIPTION
*
* This program terminates the transaction processing provided that
* there are no transactions in process.
*
*    PARAM        DESCRIPTION
*    -----------  --------------------------------------------------------
*    CONO         Company ID
*    DIV          Division ID
*    PSS.JOURNAL  File variable of Journal file
*    PSS.LOCK     File variable of Lock file
*    ERRMSG       Status
*
**************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSS.CPYLIB>PSS.JOURNAL
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      ERRMSG = ""
      READU REC FROM PSS.LOCK, CONO:"TP" ELSE NULL
*
*---- MAIN PROCESSING
*
      MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!":DIV ELSE
         ERRMSG = "NO TRANSACTIONS IN PROGRESS"
         GOTO 99999
      END
      IF JRNLC.JCNT = 0 THEN
         DELETE PSS.JOURNAL, CONO:"CONTROL!":DIV
      END ELSE
         ERRMSG = "TRANSACTION IN PROGRESS"
      END
*
*---- CHECK FOR PENDING JOB EXTRACTS FROM JOB.MAINT
*
      READU JREC FROM PSS.LOCK, CONO:"JOB.SCHED.EXTRACT" THEN
         PARAM = ""
         PARAM<1,1> = CONO
         JCNT = DCOUNT(JREC,VM)
         FOR JPTR = 1 TO JCNT
            JOB.NO = JREC<1,1>
            JREC = DELETE(JREC,1,1,0)
            WRITEU JREC ON PSS.LOCK, CONO:"JOB.SCHED.EXTRACT"
            PARAM<1,2> = JOB.NO
PARAM<1,4> = ""
            CALL SI_PERFORM.PROG("JOB.SCHED.EXTRACT",PARAM)
            READU JREC FROM PSS.LOCK, CONO:"JOB.SCHED.EXTRACT" ELSE JREC=""
         NEXT JPTR
         IF JREC = "" THEN
            DELETE PSS.LOCK, CONO:"JOB.SCHED.EXTRACT"
         END ELSE
            RELEASE PSS.LOCK, CONO:"JOB.SCHED.EXTRACT"
         END
      END ELSE
         RELEASE PSS.LOCK, CONO:"JOB.SCHED.EXTRACT"
      END
*
*---- CHECK FOR PENDING UPDATES TO PSS FROM TIME/MATL POSTING
*
      READU JREC FROM PSS.LOCK, CONO:"POST.JOB.TRANS" THEN
         DELETE PSS.LOCK, CONO:"POST.JOB.TRANS"
         PARAM = ""
         PARAM<1,1> = CONO
         CALL PERFORM.PROG("POST.JOB.TRANS",PARAM)
      END
*
*---- END OF PROGRAM
*
99999 *
      RELEASE PSS.JOURNAL, CONO:"CONTROL!":DIV
      RELEASE PSS.LOCK, CONO:"TP"
      RETURN
   END
