********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - CCTR.AVL.RPT
* AUTHOR   - ANN POWLEY, C.B.A.
* DATE     - 09/01/87
* DESCRIPTION
* This program will print Cost Center Schedule Availability Report.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*
***** FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SET UP SYSCOM
*
      SYS.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","CCTR.SCHED" TO CCTR.SCHED ELSE
      ERRMSG = "CCTR.SCHED FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","PEND.CCTR.SCHED" TO PEND.CCTR.SCHED ELSE
      ERRMSG = "PEND.CCTR.SCHED FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "DEPARTMENT FILE IS MISSING"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
      ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
      ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
      GOSUB 90000
      GOTO 99999
   END
*
**** PERFORM PROCREAD
*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST RUN FROM PROC"
      GOSUB 90000
      GOTO 99999
   END
   CONO = BUFFER<1>
   ST.DATE = BUFFER<3>
   END.DATE = BUFFER<4>
*T26493 v
*  CO.NAME = BUFFER<2>
*  REPORT.NAME = "COST CENTER AVAILABILITY"
*  REPORT.NUMBER = "XXXX"
   CO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
*T26493 ^
   READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
   REPORT.DATE = TODAY
   HD1 = "";HD2 = "";PG.NO = 0
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
**** INTIALIZATION
*
   DIM DATES(365)
   MAT DATES = ''
   CCTRS = ''
   Y = 1
   SP = SPACE(1)
   HD3 = SPACE(62):'PERIOD ':ST.DATE:' TO ':END.DATE
   HD4 = "DEPARTMENT  : "
   HEAD5 = "  DATE   DAY "
   HEAD6 = "-------- --- "
*
**** MAIN PROCESSING
*
   DATA = 1
   PRINTER ON
   READNEXT ID ELSE GOTO 99999
   MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, ID ELSE MAT CCTR.SCHED.REC = ''
   CCTR = ID[4,3]
   CCS.DATE = FIELD(ID,"*",2)
   DEPT = CCS.DEPT
   DATES(Y)<1,1> = CCS.DATE
   GOSUB 200
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, ID ELSE MAT CCTR.SCHED.REC = ''
      CCTR = ID[4,3]
      CCS.DATE = FIELD(ID,"*",2)
      IF DEPT # CCS.DEPT THEN
         X.1 = 2
         GOSUB 400
         GOSUB 500
         MAT DATES = ''
         CCTRS = ''
         Y = 1
         DATES(Y)<1,1> = CCS.DATE
         DEPT = CCS.DEPT
      END
      GOSUB 200
   REPEAT
   X.1 = 2
   GOSUB 400
   GOSUB 500
   GOTO 99999
*
***** PROCESS THE JOB
*
200*
   IF CCS.DATE # DATES(Y)<1,1> THEN
      Y = Y + 1
      DATES(Y)<1,1> = CCS.DATE
      X = COUNT(CCTRS,VM) + (CCTRS # '')
      FOR XX = 1 TO X
         DATES(Y)<1,XX+1> = ''
      NEXT XX
   END
   LOCATE CCTR IN CCTRS<1>,1 BY 'AL' SETTING X ELSE NULL
   IF CCTRS<1,X> = CCTR THEN
      DATES(Y)<1,X+1> = CCS.REM.HRS
   END ELSE
      CCTRS = INSERT(CCTRS,1,X,0,CCTR)
      XX = X + 1
      FOR YY = 1 TO Y
         DATES(YY) = INSERT(DATES(YY),1,XX,0,"")
      NEXT Y
      DATES(Y)<1,XX> = CCS.REM.HRS
   END
   RETURN
*
**** PRINT HEADINGS
*
400*
   PG.NO = PG.NO + 1
   MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE
      MAT DEPT.REC = ''
      DEPT.DESC = '????????????????'
   END
   PRINT CHAR(12)
   PRINT HD1 : PG.NO "R#4"
   PRINT HD2
   PRINT HD3
   PRINT
   PRINT HD4 : DEPT "L#5" : SP : DEPT.DESC "L#30"
   PRINT
   HD5 = HEAD5
   HD6 = HEAD6
   X = COUNT(CCTRS,VM) + (CCTRS # '')
   LINE.FULL = 0
   XX.1 = X.1 - 1
   FOR XX = XX.1 TO X UNTIL LINE.FULL > 23
      HD5 = HD5 : CCTRS<1,XX> "R#4" : SP
      HD6 = HD6 : "----" : SP
      LINE.FULL = LINE.FULL + 1
   NEXT XX
   PRINT HD5
   PRINT HD6
   LINE.CNT = 8
   RETURN
*
***** PRINT DATE LINES
*
500*
   FOR YY = 1 TO Y
      IF LINE.CNT > 55 THEN
         GOSUB 400
      END
      H.LINE = OCONV(DATES(YY)<1,1>, "D2/") : SP
      DAY = OCONV(DATES(YY)<1,1>, "DWA")
      H.LINE = H.LINE : DAY[1,3] : SP
      LINE.FULL = 0
      FOR XX = X.1 TO X+1 UNTIL LINE.FULL > 23
         HOURS = INT(OCONV(DATES(YY)<1,XX>, "MD2"))
         H.LINE = H.LINE : HOURS "R#4" : SP
         XX.1 = XX
         LINE.FULL = LINE.FULL + 1
      NEXT XX
      PRINT H.LINE
      LINE.CNT = LINE.CNT + 1
   NEXT YY
   IF LINE.FULL > 23 THEN
      PRINT
      PRINT SPACE(20):"* * * DEPARTMENT IS CONTINUED ON NEXT PAGE * * *"
      X.1 = XX.1 + 1
      GOSUB 400
      GOSUB 500
   END
   RETURN
*
***** ERROR ROUTINE
90000*
   ERR.TYPE = 1 ; CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
*
***** END OF PROGRAM
99999*
   PRINTER OFF
   PRINT @(-1):
END
