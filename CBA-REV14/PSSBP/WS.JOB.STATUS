      SUBROUTINE WS.JOB.STATUS(CONO, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.JOB.STATUS
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 05/26/93
*
* DESCRIPTION
*
* This subroutine displays the status of the specified job in a
* graphical format.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>PSSBP>WINCOM2
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      JOBNO = WIN.CURR.JOB
      MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      MATREAD PJBS.REC FROM PEND.JOB.SCHED, CONO:JOBNO ELSE MAT PJBS.REC = ""
      MATREAD JOB.REC FROM JOB, CONO:JOBNO ELSE
         MAT JOB.REC = ""
      END
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
         MAT CUST.REC = ""
         CUST.NAME = JBS.CUST.NAME
      END
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      WIN.JSEL<WPTR> = WIN.CURR.JOB
      LOCATE WCOM.WND IN WIN.SEL.ID,1 SETTING SPTR ELSE WIN.SEL.ID<SPTR>=WCOM.WND
      WIN.SEL.JOB<SPTR>  = WIN.CURR.JOB
*
      LCNT = 0
      MAXHRS = 800
      CCNT = DCOUNT(JBS.CCTR,VM)
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         OCNT = DCOUNT(JBS.OPER<1,CPTR>,SM)
         IF JBS.OPER<1,CPTR,1> = "ALL" THEN
            LCNT = LCNT + 1
         END ELSE
            LCNT = LCNT + 1 + OCNT
         END
         FOR OPTR = 1 TO OCNT
            OPER = JBS.OPER<1,CPTR,OPTR>
            SHRS = JBS.OPER.SCH.HRS<1,CPTR,OPTR>+0
            AHRS = JBS.OPER.ACT.HRS<1,CPTR,OPTR>+0
            IF AHRS > SHRS THEN MHRS = AHRS ELSE MHRS = SHRS
            IF MHRS > MAXHRS THEN MAXHRS = MHRS
         NEXT OPTR
      NEXT CPTR
      MAXHRS = INT(MAXHRS/800+0.99) * 800
*
*---- OPEN WINDOW
*
      HEAD.CNT=5
      COL2=49
      MAXW=24
      WCOM.CAPTION="J O B   S T A T U S"
      WCOM.COL="C"
      WCOM.ROW="C"
      WCOM.WIDTH=COL2+MAXW
      BEGIN CASE
      CASE (HEAD.CNT+LCNT) > 18
         WHEIGHT = HEAD.CNT+14
      CASE 1
         WHEIGHT = HEAD.CNT+LCNT
      END CASE
      WCOM.HEIGHT = WHEIGHT
      CALL OPEN.WINDOW(MAT WCOM)
*
      WCOM.ROW = HEAD.CNT
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY DEMOGRAPHIC DATA
*
      WCOM.COL=1
      WCOM.ROW=0
      WCOM.HEIGHT=1:YADJ
      WCOM.RCOLOR="BLACK"
      WCOM.DATA = " Job # ":JOBNO:" "
      WCOM.SELECT = "101"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=20
      WCOM.ROW=0
      WCOM.WIDTH=30:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = CUST.NAME[1,30]
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=20
      WCOM.ROW=1
      WCOM.WIDTH=45:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = JOB.DESC<1,1>
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=6
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = "Scheduled"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=16
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = OCONV(JBS.SCH.DATE<1,1>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=26
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = " Forecast"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=36
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = OCONV(JBS.EXP.DATE<1,CCNT>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=46
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = " Required"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=56
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      IF JBS.DUE.DATE < JBS.EXP.DATE<1,CCNT> THEN
         WCOM.FCOLOR="YELLOW"
         WCOM.BCOLOR="RED"
         WCOM.RCOLOR="BLACK"
      END
      WCOM.DATA = OCONV(JBS.DUE.DATE,"D2/")
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY COST CENTER AND OPERATION STATUS
*
      WCOM.DATA = " Cost Center / Operation"
      WCOM.COL = 0
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 41:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* COST CENTER / OPER HEADING
      WCOM.DATA = "  Hours"
      WCOM.COL = 41
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = 8:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* HOURS HEADING
      HDR = " ":INT(MAXHRS/100):" HOURS "
      W1 = INT((MAXW-LEN(HDR)-2)/2)
      W2 = (MAXW-LEN(HDR)-2-W1)
      HDR = "<":STR("-",W1):HDR:STR("-",W2):">"
      WCOM.DATA = HDR
      WCOM.COL = COL2
      WCOM.ROW = HEAD.CNT-1
      WCOM.WIDTH = MAXW:XADJ
      WCOM.HEIGHT = 1:YADJ
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "GREY"
      WCOM.RCOLOR = "BLACK"
      WCOM.SELECT = ""
      CALL LOAD.DATA(MAT WCOM)             ;* CHART HEADING
      FOR N = 0 TO 3
         WCOM.STYLE = "S"
         WCOM.COL = COL2+N*(MAXW/4)
         WCOM.ROW = HEAD.CNT
         WCOM.WIDTH = 0
         WCOM.HEIGHT = WHEIGHT
         WCOM.FCOLOR = "GREY"
         CALL DRAW.LINE(MAT WCOM)
      NEXT N
*
      ROW = HEAD.CNT-1
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "??????????"
         END
         ROW = ROW + 1
         WCOM.DATA = " ":CCTR"L#3":" ":CCTR.DESC
         WCOM.TYPE = "V"
         WCOM.COL = 0
         WCOM.ROW = ROW
         WCOM.WIDTH = 41:XADJ
         WCOM.HEIGHT = 1:YADJ
         WCOM.FCOLOR = "BLACK"
         WCOM.BCOLOR = "WHITE"
         WCOM.RCOLOR = "BLACK"
         WCOM.SELECT = ""
         CALL LOAD.DATA(MAT WCOM)
         OCNT = DCOUNT(JBS.OPER<1,CPTR>,SM)
         FOR OPTR = 1 TO OCNT
            OPER = JBS.OPER<1,CPTR,OPTR>
            IF OPER # "ALL" THEN
               MATREAD OPER.REC FROM OPERATION, CONO:OPER ELSE
                  MAT OPER.REC = ""
                  OPER.DESC = "??????????"
               END
               ROW = ROW + 1
               WCOM.DATA = "     ":OPER"L#4":" ":OPER.DESC
               WCOM.TYPE = "V"
               WCOM.COL = 0
               WCOM.ROW = ROW
               WCOM.WIDTH = 41:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = "WHITE"
               WCOM.RCOLOR = "BLACK"
               WCOM.SELECT = ""
               CALL LOAD.DATA(MAT WCOM)          ;* OPERATION DESCRIPTION
            END
            STAT = JBS.OPER.STATUS<1,CPTR,OPTR>
            SHRS = JBS.OPER.SCH.HRS<1,CPTR,OPTR>
            AHRS = JBS.OPER.ACT.HRS<1,CPTR,OPTR>
            IF AHRS > SHRS THEN MHRS = AHRS ELSE MHRS = SHRS
*
            BEGIN CASE
            CASE SHRS = 0 AND AHRS = 0
               DH = 0
               W1 = 0
               W2 = 0
            CASE STAT = "C" AND SHRS >= AHRS
               DH = AHRS
               W1 = MAXW * AHRS / MAXHRS;       C1 = "GREEN"
               W2 = 0
            CASE STAT = "C" AND SHRS < AHRS
               DH = SHRS
               W1 = MAXW * SHRS / MAXHRS;       C1 = "GREEN"
               W2 = MAXW * AHRS / MAXHRS - W1;  C2 = "GREEN"
            CASE SHRS >= AHRS
               DH = SHRS
               W1 = MAXW * AHRS / MAXHRS;       C1 = "GREEN"
               W2 = MAXW * SHRS / MAXHRS - W1;  C2 = "WHITE"
            CASE SHRS < AHRS
               DH = SHRS
               W1 = MAXW * SHRS / MAXHRS;       C1 = "GREEN"
               W2 = MAXW * AHRS / MAXHRS - W1;  C2 = "RED"
            END CASE
            IF STAT = "I" THEN
               IF C1 = "GREEN" THEN C1 = "IBLUE"
               IF C2 = "GREEN" THEN C2 = "IBLUE"
            END
*
            WCOM.DATA = OCONV(DH,"MD2")"R#7"
            WCOM.TYPE = "V"
            WCOM.COL = 41
            WCOM.ROW = ROW
            WCOM.WIDTH = 8:XADJ
            WCOM.HEIGHT = 1:YADJ
            WCOM.FCOLOR = "BLACK"
            WCOM.BCOLOR = "WHITE"
            WCOM.RCOLOR = "BLACK"
            WCOM.SELECT = ""
            CALL LOAD.DATA(MAT WCOM)
*
            IF W1 > 0 THEN
               WCOM.DATA = ""
               WCOM.TYPE = "V"
               WCOM.LEVEL = 5
               WCOM.COL = COL2+0.2
               WCOM.ROW = ROW+0.1
               WCOM.WIDTH = W1:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "GREY"
               WCOM.BCOLOR = "GREY"
               WCOM.RCOLOR = "GREY"
               CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 1
               WCOM.DATA = ""
               WCOM.TYPE = "V"
               WCOM.LEVEL = 10
               WCOM.COL = COL2
               WCOM.ROW = ROW
               WCOM.WIDTH = W1:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C1
               WCOM.RCOLOR = "BLACK"
               CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 1
            END
            IF W2 > 0 THEN
               WCOM.DATA = ""
               WCOM.TYPE = "V"
               WCOM.LEVEL = 5
               WCOM.COL = COL2+W1+0.2
               WCOM.ROW = ROW+0.1
               WCOM.WIDTH = W2:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "GREY"
               WCOM.BCOLOR = "GREY"
               WCOM.RCOLOR = "GREY"
               CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 2
               WCOM.DATA = ""
               WCOM.TYPE = "V"
               WCOM.LEVEL = 10
               WCOM.COL = COL2+W1
               WCOM.ROW = ROW
               WCOM.WIDTH = W2:XADJ
               WCOM.HEIGHT = 1:YADJ
               WCOM.FCOLOR = "BLACK"
               WCOM.BCOLOR = C2
               WCOM.RCOLOR = "BLACK"
               CALL LOAD.DATA(MAT WCOM)          ;* CHART SEGMENT 2
            END
         NEXT OPTR
      NEXT CPTR
*
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA = WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
