      SUBROUTINE JOB.SCHED.SUB(CONO,CO.NAME,JOB.NUM,PG.NO,JOB.DESC)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - JOB.SCHED.SUB
* AUTHOR   - ANN POWLEY, C.B.A.
* DATE     - 09/14/87
* DESCRIPTION
* This program will print jobs from JOB.SCHED file.
*********************************************************************
*
***** FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
**** INITIALIZATION
*
      REPORT.NAME = "JOB SCHEDULE REPORT"
      REPORT.NUMBER = "XXXX"
      READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
      REPORT.DATE = TODAY
      SP = SPACE(1)
      LINE.CNT = 99
      HD1 = "";HD2 = ""
      CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
      HD3 = "JOB NUMBER : ":JOB.NUM "L#8"
      HD3 = HD3 : SPACE(10): 'CUSTOMER    : ' : JBS.CUST.NAME 'L#30'
      HD4 = "DUE DATE   : ": OCONV(JBS.DUE.DATE , 'D2/') "L#8"
      IF JBS.JOB.COMMENT # '' THEN
         HD4 = HD4 : SPACE(10): 'DESCRIPTION : ': JBS.JOB.COMMENT 'L#60'
      END ELSE
         DESC = JOB.DESC<1,1> :' ': JOB.DESC<1,2> :' ': JOB.DESC<1,3>
         HD4 = HD4 : SPACE(10): 'DESCRIPTION : ': DESC 'L#60'
      END
      HD5 = SPACE(67) : 'HOURS  SCHEDULE DAYS'
      HD6 = "        DEPARTMENT            COST CENTER / OPERATION     SCH DATE REQ'D  COMPLETE LATE"
      HD7 = '-------------------------- ------------------------------ -------- ------ -------- ----'
*
***** MAIN PROCESSING
*
      PREV.DEPT = ""
      DP.CNT = DCOUNT(JBS.DEPT,VM)
      FOR DP = 1 TO DP.CNT
         IF LINE.CNT > 55 THEN GOSUB 200
         MATREAD DEPT.REC FROM DEPARTMENT, CONO : JBS.DEPT<1,DP> ELSE MAT DEPT.REC = '???????????'
         MATREAD CCTR.REC FROM COST.CNTR, CONO : JBS.CCTR<1,DP> ELSE MAT CCTR.REC = '???????????'
         BEGIN CASE
            CASE JBS.CCTR.STATUS<1,DP> = 'C'
               DAYS.LATE = ''
            CASE JBS.EXP.DATE<1,DP> = ''
               DAYS.LATE = ''
            CASE JBS.EXP.DATE<1,DP> >= TODAY
               DAYS.LATE = ''
            CASE 1
               DAYS.LATE = TODAY - JBS.EXP.DATE<1,DP>
         END CASE
         H.LINE = ''
         IF JBS.DEPT<1,DP> # PREV.DEPT THEN
            H.LINE = H.LINE : DEPT.DESC "L#26" : SP
            PREV.DEPT = JBS.DEPT<1,DP>
         END ELSE
            H.LINE = H.LINE : SPACE(27)
         END
         H.LINE = H.LINE : CCTR.DESC "L#30" : SP
         H.LINE = H.LINE : OCONV(JBS.SCH.DATE<1,DP>, "D2/") "L#8" : SP
         H.LINE = H.LINE : OCONV(JBS.SCH.HRS<1,DP>, "MD2") "R#6" : SP
         H.LINE = H.LINE : OCONV(JBS.EXP.DATE<1,DP>, "D2/") "L#8" : SP
         H.LINE = H.LINE : DAYS.LATE "R#4"
         PRINT H.LINE
         OPER.CNT = DCOUNT(JBS.OPER<1,DP>,SVM)
         FOR CC = 1 TO OPER.CNT
            IF JBS.OPER<1,DP,CC> = 'ALL' THEN
               OPER.DESC = 'ALL'
            END ELSE
               MATREAD OPER.REC FROM OPERATION, CONO : JBS.OPER<1,DP,CC> ELSE MAT OPER.REC = '???????????'
            END
            H.LINE = SPACE(29) : OPER.DESC "L#28" : SPACE(10)
            H.LINE = H.LINE : OCONV(JBS.OPER.SCH.HRS<1,DP,CC>, "MD2") "R#6"
            PRINT H.LINE
         NEXT CC
      NEXT DP
      GOTO 99999
*
***** PRINT HEADINGS
*
200 *
      PG.NO = PG.NO + 1
      PRINT CHAR(12)
      PRINT HD1 : PG.NO "R#4"
      PRINT HD2
      PRINT
      PRINT HD3
      PRINT HD4
      PRINT
      PRINT HD5
      PRINT HD6
      PRINT HD7
      LINE.CNT = 9
      RETURN
*
**** ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*    PRINT @(0,23) : CL : ERRMSG :
*    INPUT REPLY,1 :
*    PRINT @(0,23) : CL :
*    RETURN
99999*
      RETURN
   END
