*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - CCTR.CLNDR.SCHED.RPT
* AUTHOR   - WALID YAMOUT, C.B.A.
* DATE     - 09/20/88
* DESCRIPTION
* This program will print Cost Center Calendar Schedule Report.
*T25901 cmykleb 03/26/2002 * Change pgm to use GET.PROG.HEAD for heading.
*********************************************************************
*
*--- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PSS.CPYLIB>CCTR.AVAIL
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- INITIALIZATION
*
   DIM TOT.REC(5)
   EQU TOT.DAYS   TO TOT.REC(1)
   EQU TOT.JOBS   TO TOT.REC(2)
   EQU TOT.SCHD   TO TOT.REC(3)
   EQU TOT.AVAL   TO TOT.REC(4)
   PROMPT ""
   ERRMSG = ""
   MAT FILE.VARS = ""
   MAT COMP.REC = ""
*
*--- PROC READ
*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST RUN FROM PROC"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   CONO = BUFFER<1>
   CO.NAME = BUFFER<2>
   READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
   ST.DATE = ICONV(BUFFER<3>, "D2/")
   IF ST.DATE = "" THEN GOTO 999999
   MON = OCONV(ST.DATE,"DM")
*CSF 22175
   YR = OCONV(ST.DATE,"DY")
*     IF MON < OCONV(TODAY, "DM") THEN GOTO 999999
   IF MON < OCONV(TODAY, "DM") AND YR <= OCONV(TODAY,"DY") THEN GOTO 999999
*
   LOOP
   UNTIL MON # OCONV(ST.DATE-1, "DM") DO
      ST.DATE = ST.DATE - 1
   REPEAT
   EN.DATE = ICONV(BUFFER<4>, "D2/")
   IF EN.DATE = "" THEN GOTO 999999
   MON = OCONV(EN.DATE,"DM")
*CSF 22175
   YR = OCONV(EN.DATE,"DY")
*     IF MON < OCONV(TODAY, "DM") THEN GOTO 999999
   IF MON < OCONV(TODAY, "DM") AND YR <= OCONV(TODAY,"DY") THEN GOTO 999999
*
   LOOP
   UNTIL MON # OCONV(EN.DATE+1, "DM") DO
      EN.DATE = EN.DATE + 1
   REPEAT
   IF EN.DATE < ST.DATE THEN GOTO 999999
*
*--- OPEN FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "COMPANY FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "DEPARTMENT FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","COST.CNTR" TO COST.CNTR ELSE
      ERRMSG = "COST.CNTR FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","CCTR.AVAIL" TO CCTR.AVAIL ELSE
      ERRMSG = "CCTR.AVAIL FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","CCTR.SCHED" TO CCTR.SCHED ELSE
      ERRMSG = "CCTR.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","PEND.CCTR.SCHED" TO PEND.CCTR.SCHED ELSE
      ERRMSG = "PEND.CCTR.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","CCTR.SCHED.HIST" TO CCTR.SCHED.HIST ELSE
      ERRMSG = "CCTR.SCHED.HIST FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","HOLIDAY.SCHED" TO HOLIDAY.SCHED ELSE
      ERRMSG = "HOLIDAY.SCHED FILE IS MISSING"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
      ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
      GOSUB 91000
      GOTO 999999
   END
   OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
      ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
      GOSUB 91000
      GOTO 999999
   END
*
*--- SETUP HEADINGS
*
   PG.NO = 0
   PRINT.CNT = 9
   PREV.DATE = ""
   PREV.MON  = ""
   PREV.DEPT = ""
   FIRST = 1
   HD1 = ""
   HD2 = ""
   SP = SPACE(80)
   SP9 = SP[1,9]
   SP4 = SP[1,4]
   SP3 = SP[1,3]
   SP1 = SP[1,1]
*T25901 v
*  IF BUFFER<7> = "P" THEN
*     HD1 = OCONV(TODAY, "D2/") "L#8"
*     LN = INT((71 - LEN(CO.NAME)) / 2) - 4
*     IF LN < 1 THEN LN = 3
*     HD1 = HD1 : SP[1,LN] : CO.NAME
*     LN = 69 - LEN(HD1)
*     IF LN < 1 THEN LN = 3
*     HD1 = HD1 : SP[1,LN] : "PAGE "
*     HD2 = OCONV(TIME(), "MTS") "L#8"
*     HD2 = HD2 : SP[1,15] : "S C H E D U L E   C A L E N D A R"
*     PRINTER ON
*  END
   PRINTER ON
   CO.NAME = ""
   REPORT.NAME = ""
   REPORT.NUMBER = BUFFER<2>
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,REPORT.NAME,REPORT.NUMBER,"",HD1,HD2)
*T25901 ^
   DASH = STR("-",70)
   D9 = DASH[1,9]
   D4 = DASH[1,4]
   D3 = DASH[1,3]
   HLINE1 = "+"
   FOR I = 1 TO 7
      HLINE1 = HLINE1 : D9 : "+"
   NEXT I
   HLINE2 = "|" : SP3 : "SUN" : SP3 : "|"
   HLINE2 = HLINE2 : SP3 : "MON" : SP3 : "|"
   HLINE2 = HLINE2 : SP3 : "TUE" : SP3 : "|"
   HLINE2 = HLINE2 : SP3 : "WED" : SP3 : "|"
   HLINE2 = HLINE2 : SP3 : "THU" : SP3 : "|"
   HLINE2 = HLINE2 : SP3 : "FRI" : SP3 : "|"
   HLINE2 = HLINE2 : SP3 : "SAT" : SP3 : "|"
*
*--- MAIN PROCESS
*
100*
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD CCTR.REC FROM COST.CNTR, ID ELSE GOTO 199
      CCTR = ID[4,3]
      DEPT = CCTR.DEPT
      MATREAD DEPT.REC FROM DEPARTMENT, CONO : DEPT ELSE GOTO 199
      MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:DEPT:"*":CCTR ELSE
         MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:DEPT ELSE
            GOTO 199
         END
      END
      IF FIRST THEN
         PREV.DEPT = DEPT
         FIRST = 0
      END
      BEGIN CASE
         CASE DEPT # PREV.DEPT
            PRINT.CNT = 9
            PREV.DEPT = DEPT
      END CASE
      GOSUB 1000
199*
   REPEAT
   PRINTER OFF
   GOTO 999999
*
*--- BUILD THE MONTH DATA
*
1000*
   PREV.DATE = ST.DATE
   PREV.MON = OCONV(PREV.DATE, "DMA")
   GOSUB 2000
   FOR AAA = ST.DATE TO EN.DATE
      IF PREV.MON # OCONV(AAA, "DMA") THEN
         GOSUB 20000
         PREV.DATE = AAA
         PREV.MON = OCONV(PREV.DATE, "DMA")
         GOSUB 2000
      END
      XX = OCONV(AAA, "DD")
      TOT.DAYS<XX+DAYS> = XX
      MATREAD CCTR.SCHED.REC FROM CCTR.SCHED, CONO : CCTR : "*" : AAA ELSE
         IF AAA < TODAY THEN
            MATREAD CCTR.SCHED.REC FROM CCTR.SCHED.HIST, CONO : CCTR : "*" : AAA ELSE
               MAT CCTR.SCHED.REC = ""
               CCS.DEPT = DEPT
            END
         END ELSE
            MAT CCTR.SCHED.REC = ""
            CCS.DEPT = DEPT
            ERRMSG = ""
            CALL GEN.CCTR.SCHED.SUB(CONO,CCTR,AAA,ERRMSG)
            ERRMSG = ""
         END
      END
      TOT.SCHD<XX+DAYS> = CCS.SCH.HRS
      TOT.AVAL<XX+DAYS> = CCS.REM.HRS
      JCNT = DCOUNT(CCS.JOB,VM)
      FOR JJ = 1 TO JCNT
         LOCATE CCS.JOB<1,JJ> IN CCS.JOB<1>,1 SETTING JFND ELSE JFND = JJ
         IF JFND = JJ THEN
            TOT.JOBS<XX+DAYS> = TOT.JOBS<XX+DAYS> + 1
         END
      NEXT JJ
   NEXT AAA
   GOSUB 20000
   RETURN
*
*--- FIND MONTH SPAN
*
2000*
   MAT TOT.REC = ""
   MON = OCONV(PREV.DATE,"DM")
   SDATE = PREV.DATE + 31
   LOOP
   WHILE MON # OCONV(SDATE, "DM") DO
      SDATE = SDATE - 1
   REPEAT
   DAYS = OCONV(PREV.DATE, "DW")
   IF DAYS = 7 THEN DAYS = DAYS - 7
   RETURN
*
*--- REPORT PRINT ROUTINE
*
20000*
   IF PRINT.CNT > 1 THEN GOSUB 80000
   IF PRINT.CNT = 1 THEN
      PRINT
      PRINT
      PRINT
      PRINT
   END
   GOSUB 30000
   PRINT.CNT = PRINT.CNT + 1
   RETURN
*
*--- PRINT ROUTINE
*
30000*
   XLEN = LEN(PREV.MON)
   MONTH = PREV.MON[1,1]
   FOR LN = 2 TO XLEN
      MONTH = MONTH : SP1 : PREV.MON[LN,1]
   NEXT LN
   MONTH = MONTH : SP3 : OCONV(PREV.DATE, "DY")
   XLEN = LEN(MONTH)
   LN = INT((71 - XLEN) / 2) + 4
   PRINT SP[1,LN] : MONTH
   PRINT SP4 : CCTR.DESC "L#30" : SP9 : DEPT.DESC "R#30"
   PRINT SP4 : HLINE1
   PRINT SP4 : HLINE2
   SPAN = INT((DCOUNT(TOT.DAYS,AM)/7) + .99)
   LN = 0
   FOR I = 1 TO SPAN
      V.LINE = SP4 : "+"
      V.LINE2 = "Sch |"
      V.LINE3 = "Avl |"
      FOR J = 1 TO 7
         LN = LN + 1
         BEGIN CASE
            CASE TOT.DAYS<LN> = ""
               V.LINE = V.LINE : D9
            CASE LEN(TOT.DAYS<LN>) = 2
               V.LINE = V.LINE : D4 : TOT.DAYS<LN> : D3
            CASE 1
               V.LINE = V.LINE : D4 : TOT.DAYS<LN> : D4
         END CASE
         IF TOT.JOBS<LN> + 0 = 0 THEN
            V.LINE2 = V.LINE2 : "   "
         END ELSE
            V.LINE2 = V.LINE2 : TOT.JOBS<LN> "R#2" : "-"
         END
         V.LINE2 = V.LINE2 : OCONV(TOT.SCHD<LN>, "MD2Z") "R#6"
         V.LINE3 = V.LINE3 : OCONV(TOT.AVAL<LN>, "MD2Z") "R#9"
         V.LINE = V.LINE : "+"
         V.LINE2 = V.LINE2 : "|"
         V.LINE3 = V.LINE3 : "|"
      NEXT J
      PRINT V.LINE
      PRINT V.LINE2
      PRINT V.LINE3
   NEXT I
   PRINT SP4 : HLINE1
   RETURN
*
*--- HEADING ROUTINE
*
80000*
   PRINT CHAR(12):
   PG.NO = PG.NO + 1
   PRINT HD1 : PG.NO
   PRINT HD2
   PRINT
   PRINT
   PRINT
   PRINT
   PRINT.CNT = 0
   RETURN
*
*--- ERROR ROUTINES
*
90000 *
   ERRMSG = "HIT <RETURN> TO CONTINUE"
   PRINT @(0,23): CL : ERRMSG :
   INPUT REPLY :
   PRINT @(0,23) : CL :
   RETURN
91000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
999999*
END
