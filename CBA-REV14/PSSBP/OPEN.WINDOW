      SUBROUTINE OPEN.WINDOW(MAT WCOM)
*********************************************************************
*
* PROGRAM  - OPEN.WINDOW
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/09/91
*
* DESCRIPTION
*
* This subroutine is used to open a window on the WINCOM workstation.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.WND          Window ID              Required
* 
*   WCOM.CAPTION      Window heading         Optional
*
*   WCOM.STYLE        Window style           Optional
*
*   WCOM.COL          Starting column        Required
*                                            Range: 0 - 999
*
*   WCOM.ROW          Starting row           Required
*                                            Range: 0 - 999
*
*   WCOM.WIDTH        Width                  Required
*                                            Range: 0 - 999
*
*   WCOM.HEIGHT       Height                 Required
*                                            Range: 0 - 999
*
*   WCOM.FCOLOR       Foreground color       Optional
*                                            Default = BLACK
*
*   WCOM.BCOLOR       Background color       Optional
*                                            Default = WHITE
*
*   WCOM.MSELECT      Multiple select        Optional
*                                            Default = "N"
*
*********************************************************************
*
      INCLUDE WINCOM FROM PSSBP
      INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
      EMSG = ""
*
      IF WCOM.WND    = "" THEN EMSG = EMSG : " " : "WCOM.WND"
      IF WCOM.COL    = "" THEN EMSG = EMSG : " " : "WCOM.COL"
      IF WCOM.ROW    = "" THEN EMSG = EMSG : " " : "WCOM.ROW"
      IF WCOM.WIDTH  = "" THEN EMSG = EMSG : " " : "WCOM.WIDTH"
      IF WCOM.HEIGHT = "" THEN EMSG = EMSG : " " : "WCOM.HEIGHT"
*
      BEGIN CASE
         CASE WCOM.FCOLOR = ""
            WCOM.FCOLOR = 0
         CASE NUM(WCOM.FCOLOR)
            IF WCOM.FCOLOR < 0 OR WCOM.FCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.FCOLOR"
            END
         CASE 1
            LOCATE WCOM.FCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.FCOLOR"
            END
            WCOM.FCOLOR = C-1
      END CASE
*
      BEGIN CASE
         CASE WCOM.BCOLOR = ""
            WCOM.BCOLOR = 15
         CASE NUM(WCOM.BCOLOR)
            IF WCOM.BCOLOR < 0 OR WCOM.BCOLOR > 15 THEN
               EMSG = EMSG : " " : "WCOM.BCOLOR"
            END
         CASE 1
            LOCATE WCOM.BCOLOR IN WCOM.COLORS,1 SETTING C ELSE
               EMSG = EMSG : " " : "WCOM.BCOLOR"
            END
            WCOM.BCOLOR = C-1
      END CASE
*
      BEGIN CASE
         CASE WCOM.MSELECT = ""
            WCOM.MSELECT = "N"
         CASE WCOM.MSELECT = "Y"
         CASE WCOM.MSELECT = "N"
         CASE 1
            EMSG = EMSG : " " : "WCOM.MSELECT"
      END CASE
*
      WCOM.STATUS = EMSG
*
      IF WCOM.STATUS = "" THEN
         PMSG = BCMD        : "WC"
         PMSG = PMSG : CSEP : WCOM.WND
         PMSG = PMSG : CSEP : WCOM.CAPTION
         PMSG = PMSG : CSEP : WCOM.STYLE
         PMSG = PMSG : CSEP : WCOM.COL
         PMSG = PMSG : CSEP : WCOM.ROW
         PMSG = PMSG : CSEP : WCOM.WIDTH
         PMSG = PMSG : CSEP : WCOM.HEIGHT
         PMSG = PMSG : CSEP : WCOM.FCOLOR
         PMSG = PMSG : CSEP : WCOM.BCOLOR
         PMSG = PMSG : CSEP : WCOM.MSELECT
         PMSG = PMSG : ECMD
         PRINT PMSG :
      END ELSE
         WCOM.STATUS = "(OPEN.WINDOW) INVALID":WCOM.STATUS
      END
*
*---- RESET PARAMETERS USED
*
      WCOM.CAPTION = ""
      WCOM.STYLE   = ""
      WCOM.COL     = ""
      WCOM.ROW     = ""
      WCOM.WIDTH   = ""
      WCOM.HEIGHT  = ""
      WCOM.FCOLOR  = ""
      WCOM.BCOLOR  = ""
      WCOM.MSELECT = ""
      WCOM.CURR.WND = WCOM.WND
      RETURN
   END
