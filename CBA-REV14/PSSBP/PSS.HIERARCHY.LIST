*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PSSBP
* PROGRAM     - PSS.HIERARCHY.LIST
* AUTHOR      - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 01/13/89
* DESCRIPTION -
*     This program prints the hierarchy listing.
*********************************************************************
*COPY>PSS.CPYLIB>PSS.HIERARCHY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET UP SYSCOM
*
      SYS.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN "PSS.HIERARCHY" TO PSS.HIERARCHY ELSE
         ERRMSG = "CANNOT OPEN PSS.HIERARCHY FILE"
         GOTO 93000
      END
      OPEN "DEPARTMENT" TO DEPARTMENT ELSE
         ERRMSG = "CANNOT OPEN DEPARTMENT FILE"
         GOTO 93000
      END
      OPEN "COST.CNTR" TO COST.CNTR ELSE
         ERRMSG = "CANNOT OPEN COST.CNTR FILE"
         GOTO 93000
      END
      OPEN "COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOTO 93000
      END
      OPEN "CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOTO 93000
      END
      OPEN "PSS.CONTROL" TO PSS.CONTROL ELSE
         ERRMSG = "CANNOT OPEN PSS.CONTROL FILE"
         GOTO 93000
      END
      OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
         ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
         GOTO 93000
      END
      OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
         ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
         GOTO 93000
      END
*
*---- PROCREAD
*
      PROCREAD ID ELSE
         ERRMSG = "MUST BE RUN FROM MENU"
         GOSUB 91000
         GOTO 99999
      END
*
*--- CHECK SYSTEM HIERARCHY RECORD
*
      MATREAD PSHR.REC FROM PSS.CONTROL, "HIERARCHY" ELSE
         ERRMSG = "SYSTEM HIERARCHY RECORD IS MISSING"
         GOSUB 91000
         GOTO 99999
      END
*
*---- HEADING INITIALIZATION
*
      CONO = ID<1>
      CONO.NAME = ID<2>
      HD1 = ""
      HD2 = ""
      PAGE.NO = 0
      REPORT.NAME = "HIERARCHY LISTING"
      REPORT.NUM = "XXXX"
      READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
      REPORT.DATE = TODAY
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUM,REPORT.DATE,HD1,HD2)
      HD5 = "----- ---- ------------------------------ -- -- -----  ----- ---- ------------------------------ -- --"
      HD4 = "DEPT  CCTR          DISCRIPTION           CD DY CODE   DEPT  CCTR          DISCRIPTION           CD DY"
      HD3 = "                                          DEPTS CCTRS  <----- SPECIFIC DEPARTMENTS/COST CENTERS ----->"
*
*---- INITIALIZATION
*
      LINE.CNT = 99
      PREV.DEPT = ""
      DESC = ""
      MAT DEPT.REC = ""
      DEPT = ""
      CCTR = ""
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP5 = SPACE(5)
      SP53 = SPACE(53)
*
*---- MAIN PROCESSING
*
      PRINTER ON
*
*--- PRINT COMPANY LEVEL
*
      PREV.DEPT = "HIERARCHY"
      MATREAD PSHR.REC FROM PSS.HIERARCHY, CONO:PREV.DEPT ELSE
         MAT PSHR.REC = ""
      END
      DESC = "COMPANY"
      GOSUB 1000
      LINE.CNT = 99
100*
      DATA = 1
      LOOP
         READNEXT KEY ELSE DATA = 0
      WHILE DATA DO
         MATREAD PSHR.REC FROM PSS.HIERARCHY, KEY ELSE
            MAT PSHR.REC = ""
            GOTO 199
         END
         DEPT = FIELD(KEY,"*",1)
*        DEPT = DEPT[4,2]
         DEPT = DEPT[4,99]
         CCTR = FIELD(KEY,"*",2)
         IF PREV.DEPT # DEPT THEN
            MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE
               MAT DEPT.REC = ""
               DEPT.DESC = "NOT ON FILE"
            END
            PREV.DEPT = DEPT
            DESC = DEPT.DESC
            LINE.CNT = 99
         END
         GOSUB 1000
199*
      REPEAT
      PRINTER OFF
      GOTO 99999
*
*---- CCTR LINE
*
1000*
      IF LINE.CNT + 2 > 54 THEN
         GOSUB 8000
      END ELSE
         PRINT
         LINE.CNT = LINE.CNT + 1
      END
      IF CCTR = "" THEN
         MAT CCTR.REC = ""
      END ELSE
         MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "NOT ON FILE"
         END
      END
      LINES = DCOUNT(PSHR.TO,VM)
      VLINE = ""
      IF CCTR = "" THEN
         VLINE = VLINE :"MAST  MAST":SP1
      END ELSE
         VLINE = VLINE :CCTR.DEPT "L#5" :SP1:CCTR "R#4":SP1
      END
      VLINE = VLINE : CCTR.DESC "L#30" :SP1
      VLINE = VLINE : PSHR.DEF.CODE "L#2" :SP1: PSHR.DEF.DAYS "R#2"
      VLINE = VLINE : SP2:PSHR.CCTR.CODE "L#2":SP2
      IF LINES < 1 THEN GOTO 1050
      LN = 1
      GOSUB 2000
1050*
      PRINT VLINE
      LINE.CNT = LINE.CNT + 1
      FOR LN = 2 TO LINES
         IF LINE.CNT + 1 > 54 THEN GOSUB 8000
         VLINE = SP53
         GOSUB 2000
         PRINT VLINE
         LINE.CNT = LINE.CNT + 1
      NEXT LN
      RETURN
*
*--- LINE ITEM
*
2000*
      VLINE = VLINE : SP2
      IF CCTR # "" THEN
         MATREAD CCTR.REC FROM COST.CNTR, CONO:PSHR.TO<1,LN> ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "NOT ON FILE"
         END
         VLINE = VLINE : CCTR.DEPT "L#5" :SP1: PSHR.TO<1,LN> "R#4"
         VLINE = VLINE : SP1:CCTR.DESC "L#30" : SP1
      END ELSE
         MATREAD DEPT.REC FROM DEPARTMENT, CONO:PSHR.TO<1,LN> ELSE
            MAT DEPT.REC = ""
            DEPT.DESC = "NOT ON FILE"
         END
         VLINE = VLINE : PSHR.TO<1,LN> "L#5" :SP5
         VLINE = VLINE : SP1:DEPT.DESC "L#30" : SP1
      END
      VLINE = VLINE : PSHR.CODE<1,LN> "L#2" :SP1: PSHR.DAYS<1,LN> "R#2"

      RETURN
*
*---- HEADINGS
*
8000*
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PLINE = "MASTER DEPARTMENT : ":DEPT "L#5":SP1:DESC "L#20"
      PRINT PLINE
      PRINT
      PRINT HD3
      PRINT HD4
      PRINT HD5
      PRINT
      LINE.CNT = 9
      RETURN
*
*---- ERROR ROUTINE
*
91000*
      ERR.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      ERR.TYPE = 3
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
99999*
   END
