      SUBROUTINE JOB.SCHED.TRANS.UPD (CONO,JOB.NUM,MAT JTRN.REC,MAT JTRK.REC)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PMC.CPYLIB>COM.COMPANY
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - JOB.SCHED.TRANS.UPD
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE     - 07/18/88
* DESCRIPTION
* This subroutine is used to update job scheduling/errors
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>JCS.CPYLIB>OPERATION
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>JCS.CPYLIB>JOB.TRANS
*COPY>JCS.CPYLIB>JOB.TRACKING
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- INITIALIZATION
*
      JBS.POST.FLAG = ""
*
*--- MAIN PROCESS
*
      LOOP
         TCNT = DCOUNT(JTRN.TRANS,VM)
      WHILE TCNT DO
         MATREAD CCTR.REC FROM COST.CNTR, CONO : JTRN.CCTR<1,1> ELSE
            MAT CCTR.REC = ""
            CCTR.DEPT = JTRN.DEPT<1,1>
         END
         DEPT = CCTR.DEPT
         IF CCTR.MASTER # "" AND CCTR.MASTER # JTRN.CCTR<1,1> THEN
            CCTR = CCTR.MASTER
            MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR ELSE
               MAT CCTR.REC = ""
               CCTR.DEPT = DEPT
            END
            DEPT = CCTR.DEPT
         END ELSE
            CCTR = JTRN.CCTR<1,1>
         END
         MATREAD OPER.REC FROM OPERATION, CONO : JTRN.OPER<1,1> ELSE
            MAT OPER.REC = ""
         END
         IF OPER.SCHED # "" THEN
            OPER = OPER.SCHED
         END ELSE
            OPER = JTRN.OPER<1,1>
         END
         LOCATE OPER IN CCTR.OPER<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN
            OTYPE = CCTR.OPER.TYPE<1,FND>
         END ELSE
            OTYPE = ""
         END
         SDATE = INT(JTRN.TIME.STAMP<1,1>/100000)
         HRS = JTRN.HRS<1,1>
         IMP = JTRN.IMP<1,1>
         GOSUB 1000
         GOSUB 3000
      REPEAT
      GOTO 999999
*-------------------*
*--- SUBROUTINES ---*
*-------------------*
*
*--- UPDATE SCHEDULE
*
1000*
      DPTR = 0
      OPTR = 0
      DD = 0
      OO = 0
      PTR = 1
1100*
      LOOP
      WHILE PTR DO
         LOCATE DEPT IN JBS.DEPT<1>,PTR SETTING DD ELSE DD = 0
         BEGIN CASE
         CASE DD = 0
            PTR = 0
         CASE CCTR # JBS.CCTR<1,DD>
            PTR = DD + 1
         CASE JBS.OPER<1,DD> = "ALL" OR CO.PSS.OPER.LVL # "Y"
            OO = 1
            PTR = 0
         CASE 1
            LOCATE OPER IN JBS.OPER<1,DD>,1 SETTING OO THEN
               PTR = 0
            END ELSE
               PTR = DD + 1
            END
         END CASE
      REPEAT
      BEGIN CASE
      CASE DD = 0 AND DPTR AND OPTR
         DD = DPTR
         OO = OPTR
      CASE DD AND OO
         BEGIN CASE
         CASE JBS.OPER.ACT.DATE<1,DD,OO> = ""
         CASE JBS.ACT.DATE<1,DD> = ""
         CASE JBS.CMP.DATE<1,DD> = ""
         CASE 1
            PTR = DD + 1
            DPTR = DD
            OPTR = OO
            GOTO 1100
         END CASE
      CASE 1
         GOSUB 2000
         GOTO 1999
      END CASE
      JBS.POST.FLAG<1,DD,OO> = "N"
      IF JBS.ACT.DATE<1,DD> = "" THEN JBS.ACT.DATE<1,DD> = SDATE
      IF JBS.OPER.ACT.DATE<1,DD,OO> = "" THEN JBS.OPER.ACT.DATE<1,DD,OO> = SDATE
      JBS.ACT.HRS<1,DD> = JBS.ACT.HRS<1,DD> + HRS
      IF IMP+0 > 0 THEN JBS.ACT.IMP<1,DD> = JBS.ACT.IMP<1,DD> + IMP
      JBS.OPER.ACT.HRS<1,DD,OO> = JBS.OPER.ACT.HRS<1,DD,OO> + HRS
      IF JBS.OPER.STATUS<1,DD,OO> # "C" THEN
         BEGIN CASE
         CASE JTRN.CMP.FLG<1,1> = "Y" AND JBS.OPER<1,DD,OO> # "ALL" AND CO.PSS.OPER.LVL = "Y"
            JBS.OPER.STATUS<1,DD,OO> = "C"
**       CASE JBS.OPER.ACT.HRS<1,DD,OO> >= JBS.OPER.SCH.HRS<1,DD,OO>
         CASE JTRN.CMP.FLG<1,1> = "Y" AND JBS.OPER<1,DD,OO> = "ALL"
            JBS.OPER.STATUS<1,DD,OO> = "C"
         CASE 1
            JBS.OPER.STATUS<1,DD,OO> = "I"
         END CASE
      END
      OCNT = DCOUNT(JBS.OPER<1,DD>,SVM)
      BLK = 0
      FOR OO = 1 TO OCNT UNTIL BLK
         IF JBS.OPER.STATUS<1,DD,OO> # "C" THEN BLK = 1
      NEXT OO
      IF BLK THEN
         JBS.CCTR.STATUS<1,DD> = "I"
         JBS.DEPT.STATUS<1,DD> = "I"
      END ELSE
         JBS.CCTR.STATUS<1,DD> = "C"
         JBS.DEPT.STATUS<1,DD> = "C"
         IF SDATE > JBS.CMP.DATE<1,DD> THEN JBS.CMP.DATE<1,DD> = SDATE
      END
      DCNT = DCOUNT(JBS.DEPT,VM)
      BLK = 0
      FOR II = 1 TO DCNT UNTIL BLK
         IF JBS.DEPT.STATUS<1,II> # "C" THEN BLK = 1
      NEXT II
      IF BLK THEN
         JBS.STATUS = "I"
      END ELSE
         JBS.STATUS = "C"
         IF SDATE > JBS.COMPL.DATE THEN JBS.COMPL.DATE = SDATE
      END
1999*
      RETURN
*
*--- UPDATE JOB.TRACKING
*
2000*
      TS = JTRN.TIME.STAMP<1,1>
      LOCATE (TS+1) IN JTRK.TIME.STAMP<1>,1 BY "AR" SETTING FND ELSE NULL
      INS TS BEFORE JTRK.TIME.STAMP<1,FND>
      INS DEPT BEFORE JTRK.DEPT<1,FND>
      INS CCTR BEFORE JTRK.CCTR<1,FND>
      INS OPER BEFORE JTRK.OPER<1,FND>
      INS HRS BEFORE JTRK.HRS<1,FND>
      INS IMP BEFORE JTRK.IMP<1,FND>
2999*
      RETURN
*
*--- UPDATE JOB.TRANS
*
3000*
      DEL JTRN.TIME.STAMP<1,1>
      DEL JTRN.TRANS<1,1>
      DEL JTRN.DEPT<1,1>
      DEL JTRN.CCTR<1,1>
      DEL JTRN.OPER<1,1>
      DEL JTRN.EMP<1,1>
      DEL JTRN.HRS<1,1>
      DEL JTRN.IMP<1,1>
      DEL JTRN.SHIFT<1,1>
      DEL JTRN.CMP.FLG<1,1>
3999*
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*--- END OF PROGRAM
*
999999*
      RETURN
   END
