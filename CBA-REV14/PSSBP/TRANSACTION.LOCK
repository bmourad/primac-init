      SUBROUTINE TRANSACTION.LOCK (CONO, REQDIV, ACTDIV, ERRMSG)
**************************************************************************
*
* PROGRAM - TRANSACTION.LOCK
*
* AUTHOR  - NICK AMENDOLA, NASTech, Inc.
*
* DATE    - 05/29/96
*
* DESCRIPTION
*
* This program is used to check id the specified division(s) conflict
* with transactions which may be in process.
*
*    PARAM     DESCRIPTION
*    --------  -----------------------------------------------------------
*    CONO      Company ID.
*    REQDIV    (MV) Division(s) to be checked.
*    ACTDIV    Active division
*              null = no divisions are active
*              "X"  = division active, revert to inquiry mode
*              nn   = transactions will be logged to this division
*    ERRMSG    Status returned, NULL = OK else error message.
*
**************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PSS.CPYLIB>PSS.JOURNAL
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      USER.ID = @LOGNAME
*     PORT.NO = @TTY
      PORT.NO = 'TTY'
      CALL SYSVARS.SUB(PORT.NO)
      ACTDIV = ""
      ERRMSG = ""
      OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
         ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
         GOTO 99999
      END
      OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
         ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
         GOTO 99999
      END
      IF REQDIV = "ALL" OR REQDIV = "" THEN
         OPEN "","DIVISION" TO DIVISION ELSE
            ERRMSG = "CANNOT OPEN DIVISION FILE"
            GOTO 99999
         END
         REQDIV = ""
         SDONE = 0
         SELECT DIVISION
         LOOP
            READNEXT ID ELSE SDONE = 1
         UNTIL SDONE DO
            IF ID[1,3] = CONO THEN
               REQDIV<1,-1> = ID[4,99]
            END
         REPEAT
      END
*
*---- MAIN PROCESSING
*
      MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!00" THEN
*        IF JRNLC.PORT = PORT.NO THEN
         IF JRNLC.USER = USER.ID AND JRNLC.PORT = PORT.NO THEN
            ACTDIV = "00"
         END ELSE
            ERRMSG = "All divisions disabled by ":JRNLC.USER
         END
         GOTO 99999
      END LOCKED
         ERRMSG = "Division locked! "
         GOTO 99999
         RELEASE PSS.JOURNAL, CONO:"CONTROL!00"
      END
      DCNT = DCOUNT(REQDIV,VM)
      FOR DPTR = 1 TO DCNT
         CDIV = REQDIV<1,DPTR>
         MATREADU JRNLC.REC FROM PSS.JOURNAL, CONO:"CONTROL!":CDIV THEN
*           IF JRNLC.PORT = PORT.NO THEN
            IF JRNLC.USER = USER.ID AND JRNLC.PORT = PORT.NO THEN
               ACTDIV = CDIV
            END ELSE
               ERRMSG = "Division ":CDIV:" disabled by ":JRNLC.USER
            END
            GOTO 99999
         END LOCKED
            ERRMSG = "Division locked! "
            GOTO 99999
         END
      NEXT DPTR
      RELEASE PSS.JOURNAL, CONO:"CONTROL!00"
*
*---- END OF PROGRAM
*
99999 *
      IF ERRMSG # "" THEN ACTDIV = "X"
      WRITE ACTDIV ON PSS.LOCK, @TTY
      RELEASE PSS.JOURNAL
      RETURN
   END
