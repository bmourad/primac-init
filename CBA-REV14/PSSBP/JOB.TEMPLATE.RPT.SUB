      SUBROUTINE JOB.TEMPLATE.RPT.SUB(CONO,JOB.NUM,PG.NO,TODAY,HD1,HD2)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - JOB.TEMPLATE.RPT.SUB
* AUTHOR   - ANN POWLEY, C.B.A.
* DATE     - 09/14/87
* DESCRIPTION
* This program will print the path and the schedule date of all cost
* centers for a job from JOB.SCHED.TEMPLATE file.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      SP6 = SPACE(6)
      SP7 = SPACE(7)
      SP9 = SP7 : SP1 : SP1
      SP10 = SPACE(10)
      SP26 = SPACE(26)
      LINE.CNT = 99
      HD3 = "TEMPLATE # : ":JOB.NUM "L#8"
      HD3 = HD3 : SP10: "CUSTOMER : " : JBS.CUST.NAME "L#30"
      IF JBS.JOB.COMMENT = "" THEN
         HD4 = ""
      END ELSE
         HD4 = SPACE(31) : "COMMENTS : ": JBS.JOB.COMMENT "L#60"
      END
      HD5="                                                         "
      HD6="        DEPARTMENT            COST CENTER / OPERATION    "
      HD7="-------------------------- ------------------------------"
      HD5=HD5:"  CCTR   OPER                SAME HOLD     RSRC OVER HOLD"
      HD6=HD6:"  HOURS  HOURS   COUNT  CONT RSRC CODE SFT  NO  LOAD DAYS"
      HD7=HD7:" ------ ------ -------- ---- ---- ---- --- ---- ---- ----"
*
*---- MAIN PROCESSING
*
      PREV.DEPT = ""
      DP.CNT = DCOUNT(JBS.DEPT,VM)
      FOR DP = 1 TO DP.CNT
         IF LINE.CNT > 55 THEN GOSUB 200
         MATREAD DEPT.REC FROM DEPARTMENT, CONO : JBS.DEPT<1,DP> ELSE
            MAT DEPT.REC = ""
            DEPT.DESC = "NOT ON FILE"
         END
         MATREAD CCTR.REC FROM COST.CNTR, CONO : JBS.CCTR<1,DP> ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "NOT ON FILE"
         END
         H.LINE = ""
         IF JBS.DEPT<1,DP> # PREV.DEPT THEN
            H.LINE = H.LINE : DEPT.DESC "L#26" : SP1
            PREV.DEPT = JBS.DEPT<1,DP>
         END ELSE
            H.LINE = H.LINE : SP26 : SP1
         END
         H.LINE = H.LINE : CCTR.DESC "L#30" : SP1
         H.LINE = H.LINE : OCONV(JBS.SCH.HRS<1,DP>,"MD2") "R#6" : SP1
         H.LINE = H.LINE : SP6 : SP1
         H.LINE = H.LINE : JBS.SCH.IMP<1,DP> "R#8" : SP1
         H.LINE = H.LINE : SP1 : JBS.CONT.SCH<1,DP> "L#3" : SP1
         H.LINE = H.LINE : SP1 : JBS.SAME.EQUIP<1,DP> "L#3" : SP1
         H.LINE = H.LINE : JBS.HOLD.CODE<1,DP> "L#4" : SP1
         H.LINE = H.LINE : SP1 : JBS.SHIFT<1,DP> "L#2" : SP1
         H.LINE = H.LINE : SP1 : JBS.EQUIP<1,DP> "L#3" : SP1
         H.LINE = H.LINE : SP1 : JBS.OVER.LOAD<1,DP> "L#3" : SP1
         H.LINE = H.LINE : SP1 : JBS.HOLD.DAYS<1,DP> "R#2" : SP1
         PRINT
         PRINT H.LINE
         LINE.CNT = LINE.CNT + 2
         OPER.CNT = DCOUNT(JBS.OPER<1,DP>,SVM)
         IF OPER.CNT = 1 AND JBS.OPER<1,DP> = "ALL" THEN OPER.CNT = 0
         FOR CC = 1 TO OPER.CNT
            IF LINE.CNT > 55 THEN GOSUB 200
            IF JBS.OPER<1,DP,CC> = "ALL" THEN
               OPER.DESC = "ALL"
            END ELSE
               MATREAD OPER.REC FROM OPERATION, CONO : JBS.OPER<1,DP,CC> ELSE
                  MAT OPER.REC = ""
                  OPER.DESC = "NOT ON FILE"
               END
            END
            H.LINE = SP26 : SP1 : SP2 : OPER.DESC "L#28" : SP1
            H.LINE = H.LINE : SP6 : SP1 : OCONV(JBS.OPER.SCH.HRS<1,DP,CC>,"MD2") "R#6"
            PRINT H.LINE
            LINE.CNT = LINE.CNT + 1
         NEXT CC
      NEXT DP
      H.LINE = SP26:SP1:"TOTAL HOURS  " "R#30":SP1:OCONV(SUM(JBS.SCH.HRS),"MD2")"R#6"
      PRINT
      PRINT H.LINE
      GOTO 999999
*
*---- PRINT HEADINGS
*
200 *
      PG.NO = PG.NO + 1
      PRINT CHAR(12)
      PRINT HD1 : PG.NO "R#4"
      PRINT HD2
      PRINT
      PRINT HD3
      PRINT HD4
      PRINT
      PRINT HD5
      PRINT HD6
      PRINT HD7
      LINE.CNT = 9
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23) : CL : ERRMSG :
*       INPUT REPLY,1 :
*       PRINT @(0,23) : CL :
*       RETURN
999999 *
      RETURN
      END
