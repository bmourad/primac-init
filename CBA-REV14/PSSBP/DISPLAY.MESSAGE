      SUBROUTINE DISPLAY.MESSAGE(MAT WCOM)
*********************************************************************
*
* PROGRAM - DISPLAY.MESSAGE
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/09/91
*
* DESCRIPTION
*
* This subroutine will open a window and display the specified message.
*
*
*   PARAMETER         DESCRIPTION            COMMENTS
*   ---------------   --------------------   --------------------
*
*   WCOM.CAPTION      Window heading         Optional
*                                            Default = HELP
*
*   WCOM.DATA         Message (multi-val)    Required
*
*********************************************************************
*
INCLUDE WINCOM FROM PSSBP
INCLUDE WINCOM2 FROM PSSBP
*
*---- MAIN PROCESSING
*
      EMSG = ""
      SAVE.WND = WCOM.WND
*
      IF WCOM.CAPTION = "" THEN WCOM.CAPTION = "HELP"
      IF WCOM.DATA = "" THEN EMSG = EMSG : " " : "WCOM.DATA"
*
      WCOM.STATUS = EMSG
*
      IF WCOM.STATUS = "" THEN
         MLEN = LEN(WCOM.CAPTION)+4
         DDATA = WCOM.DATA
         MCNT = DCOUNT(DDATA, CHAR(253))
         FOR MPTR = 1 TO MCNT
            IF LEN(DDATA<1,MPTR>) > MLEN THEN MLEN = LEN(DDATA<1,MPTR>)
         NEXT MPTR
         IF MLEN > 76 THEN MLEN = 76
*
         BEGIN CASE
         CASE MLEN < 18
            WIDTH = 20
            BEG.COL = INT((20-MLEN)/2)
         CASE MLEN > 75
            WIDTH = 75
            BEG.COL = 0
         CASE 1
            WIDTH = MLEN+2
            BEG.COL = 1
         END CASE
*
         BEGIN CASE
         CASE MCNT < 3
            HEIGHT = MCNT+4
            BEG.ROW = 2
         CASE MCNT > 20
            HEIGHT = 20
            BEG.ROW = 0
         CASE 1
            HEIGHT = MCNT+2
            BEG.ROW = 1
         END CASE
*
*---- OPEN WINDOW
*
         WCOM.WND = "HELP"
         WCOM.COL = "C"
         WCOM.ROW = "C"
         WCOM.WIDTH = WIDTH
         WCOM.HEIGHT = HEIGHT
         WCOM.BCOLOR = "WHITE"
         CALL OPEN.WINDOW(MAT WCOM)
*
*---- DISPLAY MESSAGE
*
         FOR MPTR = 1 TO MCNT
            WCOM.TYPE = "VH"
            WCOM.COL = BEG.COL
            WCOM.ROW = BEG.ROW+MPTR-1
            WCOM.DATA = DDATA<1,MPTR>
            CALL LOAD.DATA(MAT WCOM)
         NEXT MPTR
         CALL SET.WSCROLL(MAT WCOM)
      END ELSE
         WCOM.STATUS = "(DISPLAY.MESSAGE) INVALID":WCOM.STATUS
      END
*
*---- RESET PARAMETERS USED
*
      WCOM.WND = SAVE.WND
      WCOM.CAPTION = ""
      WCOM.DATA = ""
      RETURN
   END
