*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - JOB.SCHED.REC.DUE.UPD
* AUTHOR   - WALID YAMOUT, C.B.A.
* DATE     - 01/16/89
* MODIFIED - 02/07/96, NA, TASK 19937, JOB DESC BASED ON COMPANY FLAG.
* DESCRIPTION
* This program will load the JOB.SCHED.REC.DUE file to print the
* outside receipts due report.
*T21657 rik 04/15/1997 * LOSING SELECT LIST WHEN CALLING
*                        TRANSACTION.LOCK
*********************************************************************
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>PSS.CPYLIB>JOB.SCHED.REC.DUE
*COPY>PMC.CPYLIB>PO
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>ICS.CPYLIB>INVENTORY
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZE SYSCOM
*
  MAT SYSCOM.REC = ""
  SYS.TYPE=1
  CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- PROC READ
*
  PROCREAD BUFFER ELSE
    ERRMSG = "MUST RUN FROM PROC"
    GOSUB 90000
    GOTO 999999
  END
  CONO = BUFFER<1>
  DIV.OWNER = BUFFER<3>
*
*---- OPEN FILES
*
  OPEN "CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "JOB" TO JOB ELSE
    ERRMSG = "CANNOT OPEN JOB FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "JOB.SCHED" TO JOB.SCHED ELSE
    ERRMSG = "CANNOT OPEN JOB.SCHED FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "PO" TO PO ELSE
    ERRMSG = "CANNOT OPEN PO FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "OUTSIDE.PO" TO OUTSIDE.PO ELSE
    ERRMSG = "CANNOT OPEN OUTSIDE.PO FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "JOB.SCHED.REC.DUE" TO JOB.SCHED.REC.DUE ELSE
    ERRMSG = "CANNOT OPEN JOB.SCHED.REC.DUE FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
    ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
    GOSUB 90000
    GOTO 98888
  END
  OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
    ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
    GOSUB 90000
    GOTO 98888
  END
*
*---- INITIALIZATION
*
  READ DEMODATE FROM CONTROL,"DEMODATE" THEN TODAY=ICONV(DEMODATE,"D") ELSE TODAY=DATE()
  CURRENT.DATE = TODAY - 1
*---- TASK # 19937 ----
  MATREAD COMP.REC FROM COMPANY, CONO ELSE MAT COMP.REC = ""
*----------------------
*T21657 ERRMSG="DIV.OWNER=(":DIV.OWNER:")";GOSUB 90000
*T21657 v
  LISTVAR = ''
  DONE = 0
  LOOP
    READNEXT ID ELSE DONE = 1
  UNTIL DONE DO
    LISTVAR<-1> = ID
  REPEAT
*T21657 ^
  GOSUB 89900;* CHECK FOR ACTIVE TRANSACTION
  IF ERRMSG # "" THEN
    GOSUB 90000
    GOTO 98888
  END
*
*---- MAIN PROCESSING
*
  DATA = 1
  PTR= 0;*T21657
  LOOP
*T21657      READNEXT ID ELSE DATA = 0
    PTR += 1;*T21657
    ID = LISTVAR<PTR>;*T21657
    IF ID = '' THEN DATA = 0;*T21657
  WHILE DATA DO
    IF ID[1,3] # CONO THEN GOTO 199
    JOB.NUM = ID[4,99]
    PREV.DEPT = ""
    DAYS.LATE = 0
    X = 0
    Y = 0
    MAT JSRDR.REC = ""
    TEMP.REC = ""
    MATREAD JBS.REC FROM JOB.SCHED,ID ELSE GOTO 199
    JSRDR.DUE.DATE = JBS.DUE.DATE
*---- TASK # 19937 ----
    JDESC = JBS.CUST.NAME
    IF CO.PSS.JDESC.FLAG = "J" OR CO.PSS.JDESC.FLAG = "X" THEN
      MATREAD JOB.REC FROM JOB, ID THEN
        IF TRIM(JOB.DESC<1,1>) # "" THEN
          IF CO.PSS.JDESC.FLAG = "J" THEN
            JDESC = JOB.DESC<1,1>
          END ELSE
            JDESC = JDESC[1,10]:":":JOB.DESC<1,1>
          END
        END
      END
    END
*     JSRDR.CUST.NAME = JBS.CUST.NAME
    JSRDR.CUST.NAME = JDESC
*----------------------
    DEPT.CNT = DCOUNT(JBS.DEPT,VM)
    FOR DPTR = 1 TO DEPT.CNT
      DESC = ""
      LATE.DAYS = 0
      EXP.DATE = ""
      BEGIN CASE
        CASE JBS.CCTR.STATUS<1,DPTR> = "C"
        CASE JBS.HOLD.CODE<1,DPTR> = "PO"
          DESC = "REGULAR"
          POCODE = JBS.HOLD.PO<1,DPTR>
          GOSUB 1000
        CASE JBS.HOLD.CODE<1,DPTR> = "OPO"
          DESC = "OUTSIDE"
          POCODE = JBS.HOLD.PO<1,DPTR>
          GOSUB 2000
      END CASE
      IF DESC # "" THEN
        BEGIN CASE
          CASE LAST.DEL.DATE = ""
            LATE.DAYS = 9999
          CASE JBS.SCH.DATE<1,DPTR> = ""
            IF JBS.HOLD.DATE<1,DPTR> < LAST.DEL.DATE THEN
              LATE.DAYS = LAST.DUE.DATE - JBS.HOLD.DATE<1,DPTR>
              EXP.DATE = LAST.DEL.DATE
            END
          CASE JBS.SCH.DATE<1,DPTR> < LAST.DEL.DATE
            LATE.DAYS = LAST.DEL.DATE - JBS.SCH.DATE<1,DPTR>
            EXP.DATE = LAST.DEL.DATE
        END CASE
        IF LATE.DAYS > 0 THEN
          IF LATE.DAYS > DAYS.LATE THEN
            DAYS.LATE = LATE.DAYS
          END
          LOCATE LATE.DAYS IN TEMP.REC<4> BY "DR" SETTING POS ELSE NULL
          INS LATE.DAYS BEFORE TEMP.REC<4,POS>
          INS JBS.DEPT<1,DPTR> BEFORE TEMP.REC<3,POS>
          INS JBS.CCTR<1,DPTR> BEFORE TEMP.REC<5,POS>
          INS JBS.SCH.DATE<1,DPTR> BEFORE TEMP.REC<6,POS>
          INS EXP.DATE BEFORE TEMP.REC<7,POS>
          INS DESC BEFORE TEMP.REC<8,POS>
          INS JBS.HOLD.PO<1,DPTR> BEFORE TEMP.REC<9,POS>
        END
      END
    NEXT DPTR
    DEPT.CNT = DCOUNT(TEMP.REC<3>,VM)
    FOR J = 1 TO DEPT.CNT
      IF PREV.DEPT # TEMP.REC<3,J> THEN
        Y = 0
        X = X + 1
        JSRDR.DEPT<1,X> = TEMP.REC<3,J>
        PREV.DEPT = TEMP.REC<3,J>
      END
      Y = Y + 1
      JSRDR.DAY.LATE<1,X,Y> = TEMP.REC<4,J>
      JSRDR.CCTR<1,X,Y> = TEMP.REC<5,J>
      JSRDR.SCH.DATE<1,X,Y> = TEMP.REC<6,J>
      JSRDR.EXP.DATE<1,X,Y> = TEMP.REC<7,J>
      JSRDR.PO.TYPE<1,X,Y> = TEMP.REC<8,J>
      JSRDR.PO<1,X,Y> = TEMP.REC<9,J>
    NEXT J
    IF DAYS.LATE > 0 THEN
***      MATWRITE JSRDR.REC ON JOB.SCHED.REC.DUE,ID:"*":DAYS.LATE
      TPFID = ID:"*":DAYS.LATE
      MATBUILD TPREC FROM JSRDR.REC
      CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"JOB.SCHED.REC.DUE",TPFID,TPREC,JOB.SCHED.REC.DUE,PSS.JOURNAL,ERRMSG)
**********************************************************************
    END
199 *
  REPEAT
  GOTO 999999
*
*---- GET REGULAR P/O DELIVERY DATE
*
1000 *
  FRST.DEL.DATE = ""
  LAST.DEL.DATE = ""
  MATREAD PO.REC FROM PO, CONO:POCODE ELSE RETURN
  PROD.CNT = DCOUNT(PO.PROD.NUM,VM)
  FOR PPTR = 1 TO PROD.CNT
    LOCATE JOB.NUM IN PO.JOB.NO<1,PPTR> SETTING POS THEN
      IF LAST.DEL.DATE = "" OR PO.DEL.DATE<1,PPTR> > LAST.DEL.DATE THEN
        LAST.DEL.DATE = PO.DEL.DATE<1,PPTR>
      END
      IF FRST.DEL.DATE = "" OR PO.DEL.DATE<1,PPTR> < FRST.DEL.DATE THEN
        FRST.DEL.DATE = PO.DEL.DATE<1,PPTR>
      END
    END
  NEXT PPTR
  RETURN
*
*---- GET OUTSIDE P/O DELIVERY DATE
*
2000 *
  FRST.DEL.DATE = ""
  LAST.DEL.DATE = ""
*CSF 24030 v
  MATREAD OPO.REC FROM OUTSIDE.PO, CONO:POCODE ELSE RETURN
  PROD.CNT = DCOUNT(OPO.JOB.NO,VM)
  FOR PPTR = 1 TO PROD.CNT
    IF JOB.NUM = OPO.JOB.NO<1,PPTR> THEN
      IF LAST.DEL.DATE = "" OR OPO.EXP.DATE<1,PPTR> > LAST.DEL.DATE THEN
        LAST.DEL.DATE = OPO.EXP.DATE<1,PPTR>
      END
      IF FRST.DEL.DATE = "" OR OPO.EXP.DATE<1,PPTR> < FRST.DEL.DATE THEN
        FRST.DEL.DATE = OPO.EXP.DATE<1,PPTR>
      END
    END
  NEXT PPTR
*  IF OPO.JOB.NO = JOB.NUM THEN
*     FRST.DEL.DATE = OPO.EXP.DATE
*     LAST.DEL.DATE = OPO.EXP.DATE
*  END
*CSF 24030 ^
  RETURN
*
***(TPCHECKBEG)***************************************************
*
*---- CHECK FOR ACTIVE TRANSACTION
*
89900 *
  READU LREC FROM PSS.LOCK, CONO:"TP" ELSE NULL
  REQDIV = DIV.OWNER
  CALL TRANSACTION.LOCK(CONO, REQDIV, TP.DIV, ERRMSG)
  BEGIN CASE
    CASE TP.DIV = "X"
    CASE TP.DIV = ""
      DCNT = DCOUNT(REQDIV,VM)
      FOR DPTR = 1 TO DCNT
        CDIV = REQDIV<1,DPTR>
        LOCK.ID = "[":CONO:",":CDIV:",":@LOGNAME:" @ ":@TTY:"]"
        READU LREC FROM PSS.LOCK, LOCK.ID ELSE NULL
      NEXT DPTR
  END CASE
  RELEASE PSS.LOCK, CONO:"TP"
  RETURN
***(TPCHECKEND)***************************************************
*
*---- ERROR ROUTINES
*
90000 *
  ERR.TYPE = 1
  CALL SI_SYSCOM(MAT SYSCOM.REC)
  RETURN
*
*---- PROCWRITE
*
98888 *
  LINE = "END" : AM
  PROCWRITE LINE
*
*---- END OF PROGRAM
*
999999 *
END
