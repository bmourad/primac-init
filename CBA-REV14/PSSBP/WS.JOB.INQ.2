      SUBROUTINE WS.JOB.INQ.2(CONO, MAT WCOM)
*********************************************************************
*
* PROGRAM - WS.JOB.INQ.2
*
* AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE    - 04/01/91
* MODIFIED - 02/21/96, NA, TASK 19938, DISPLAY COST CENTER DESCRIPTION
*
* DESCRIPTION
*
* This subroutine is used to display the specific scheduling
* requirements of the job specified by WIN.JPTR.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*COPY>PSS.CPYLIB>COM.WIN
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSSBP>WINCOM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      XADJ = "-"
      YADJ = "-"
      JOBNO = WIN.CURR.JOB
      MATREAD JBS.REC FROM JOB.SCHED, CONO:JOBNO ELSE
         WCOM.STATUS = "Cannot locate Job # ":JOBNO
         GOSUB 90000
         GOTO 99999
      END
      MATREAD PJBS.REC FROM PEND.JOB.SCHED, CONO:JOBNO ELSE MAT PJBS.REC = ""
      LOCATE WCOM.WND IN WIN.ID,1 SETTING WPTR ELSE WIN.ID<WPTR>=WCOM.WND
      WIN.JSEL<WPTR> = WIN.CURR.JOB
      LOCATE WCOM.WND IN WIN.SEL.ID,1 SETTING SPTR ELSE WIN.SEL.ID<SPTR>=WCOM.WND
      WIN.SEL.JOB<SPTR>  = WIN.CURR.JOB
*
      SP = SPACE(1)
      CCNT = DCOUNT(JBS.CCTR,VM)
      MATREAD JOB.REC FROM JOB, CONO:JOBNO ELSE
         MAT JOB.REC = ""
      END
      MATREAD CUST.REC FROM CUSTOMER, CONO:JOB.CUST ELSE
         MAT CUST.REC = ""
         CUST.NAME = JBS.CUST.NAME
      END
*
*---- OPEN WINDOW
*
      HEAD.CNT=5
      COL2 = 32
      ROW2 = HEAD.CNT
      WCOM.CAPTION="Job # ":JOBNO
      WCOM.COL="C"
      WCOM.ROW="C"
      WCOM.WIDTH=69
      BEGIN CASE
      CASE CCNT > 10
         WCOM.HEIGHT = HEAD.CNT+10
      CASE 1
         WCOM.HEIGHT = HEAD.CNT+CCNT
      END CASE
      WCOM.FCOLOR = "BLACK"
      WCOM.BCOLOR = "IWHITE"
      CALL OPEN.WINDOW(MAT WCOM)
      WCOM.COL = COL2
      CALL SET.HSCROLL.AREA(MAT WCOM)
      WCOM.ROW = ROW2
      CALL SET.VSCROLL.AREA(MAT WCOM)
*
*---- DISPLAY DEMOGRAPHIC DATA
*
      WCOM.COL=1
      WCOM.ROW=0
      WCOM.HEIGHT=1:YADJ
      WCOM.RCOLOR="BLACK"
      WCOM.DATA = " Job # ":JOBNO:" "
      WCOM.SELECT = "101"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=20
      WCOM.ROW=0
      WCOM.WIDTH=30:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = CUST.NAME[1,30]
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=20
      WCOM.ROW=1
      WCOM.WIDTH=45:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = JOB.DESC<1,1>
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=6
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = "Scheduled"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=16
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = OCONV(JBS.SCH.DATE<1,1>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=26
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = " Forecast"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=36
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = OCONV(JBS.EXP.DATE<1,CCNT>,"D2/")
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=46
      WCOM.ROW=3
      WCOM.WIDTH=10:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.DATA = " Required"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.COL=56
      WCOM.ROW=3
      WCOM.WIDTH=8:XADJ
      WCOM.HEIGHT=1:YADJ
      IF JBS.DUE.DATE < JBS.EXP.DATE<1,CCNT> THEN
         WCOM.FCOLOR="YELLOW"
         WCOM.BCOLOR="RED"
         WCOM.RCOLOR="BLACK"
      END
      WCOM.DATA = OCONV(JBS.DUE.DATE,"D2/")
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY COLUMN HEADINGS
*
      WCOM.COL=0
      WCOM.ROW=4
      WCOM.WIDTH=32:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.FCOLOR="BLACK"
*     WCOM.BCOLOR="WHITE"
      WCOM.BCOLOR="GREY"
      WCOM.RCOLOR="BLACK"
      WCOM.DATA = SP:" Cost Center"
      CALL LOAD.DATA(MAT WCOM)
      WCOM.TYPE="H"
      WCOM.COL=COL2
      WCOM.ROW=4
      WCOM.WIDTH=37:XADJ
      WCOM.HEIGHT=1:YADJ
      WCOM.FCOLOR="BLACK"
*     WCOM.BCOLOR="WHITE"
      WCOM.BCOLOR="GREY"
      WCOM.RCOLOR="BLACK"
      WCOM.DATA = "Hours"
      WCOM.DATA = WCOM.DATA:SP:" Start  "
      WCOM.DATA = WCOM.DATA:SP:"Sft"
      WCOM.DATA = WCOM.DATA:SP:" Compl  "
      WCOM.DATA = WCOM.DATA:SP:"Sft"
      WCOM.DATA = WCOM.DATA:SP:"Sts"
      CALL LOAD.DATA(MAT WCOM)
*
*---- DISPLAY COST CENTER DATA
*
      FOR CPTR = 1 TO CCNT
         CCTR = JBS.CCTR<1,CPTR>
         MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "???????????????"
         END
         WCOM.TYPE="V"
         WCOM.COL=0
         WCOM.ROW=HEAD.CNT+CPTR-1
         WCOM.WIDTH=32:XADJ
         WCOM.HEIGHT=1:YADJ
         WCOM.FCOLOR="BLACK"
         WCOM.BCOLOR="WHITE"
         WCOM.RCOLOR="BLACK"
*        WCOM.DATA=SP:CCTR.DESC[1,30]
         WCOM.DATA=SP:(CCTR:SP:CCTR.DESC)[1,30]
         CALL LOAD.DATA(MAT WCOM)
         WCOM.TYPE = "HV"
         WCOM.COL=COL2
         WCOM.ROW=HEAD.CNT+CPTR-1
         WCOM.WIDTH=37:XADJ
         WCOM.HEIGHT=1:YADJ
         WCOM.FCOLOR="BLACK"
         WCOM.BCOLOR="WHITE"
         WCOM.RCOLOR="BLACK"
         IF PJBS.SCH.DATE<1,CPTR> # "" AND JBS.SCH.DATE<1,CPTR> = "" THEN
            PENDING = 1
            HRS=INT(PJBS.SCH.HRS<1,CPTR>/10+0.5)
            WCOM.DATA=OCONV(HRS,"MD1Z")"R#5"
            WCOM.DATA=WCOM.DATA:SP:OCONV(PJBS.SCH.DATE<1,CPTR>,"D2/")"L#8"
            WCOM.DATA=WCOM.DATA:SP:PJBS.SCH.SHIFT<1,CPTR>"R#2":SP
            WCOM.DATA=WCOM.DATA:SP:OCONV(PJBS.EXP.DATE<1,CPTR>,"D2/")"L#8"
            WCOM.DATA=WCOM.DATA:SP:PJBS.EXP.SHIFT<1,CPTR>"R#2":SP
         END ELSE
            PENDING = 0
            HRS=INT(JBS.SCH.HRS<1,CPTR>/10+0.5)
            WCOM.DATA=OCONV(HRS,"MD1Z")"R#5"
            WCOM.DATA=WCOM.DATA:SP:OCONV(JBS.SCH.DATE<1,CPTR>,"D2/")"L#8"
            WCOM.DATA=WCOM.DATA:SP:JBS.SCH.SHIFT<1,CPTR>"R#2":SP
            WCOM.DATA=WCOM.DATA:SP:OCONV(JBS.EXP.DATE<1,CPTR>,"D2/")"L#8"
            WCOM.DATA=WCOM.DATA:SP:JBS.EXP.SHIFT<1,CPTR>"R#2":SP
         END
         BEGIN CASE
         CASE PENDING
            WCOM.DATA=WCOM.DATA:SP:"PND""L#4"
         CASE JBS.CCTR.STATUS<1,CPTR> = "" AND JBS.SCH.DATE<1,CPTR> # ""
            WCOM.DATA=WCOM.DATA:SP:"SCH""L#4"
         CASE JBS.CCTR.STATUS<1,CPTR> = "I"
            WCOM.DATA=WCOM.DATA:SP:"INP""L#4"
         CASE JBS.CCTR.STATUS<1,CPTR> = "C"
            WCOM.DATA=WCOM.DATA:SP:"CMP""L#4"
         CASE 1
            WCOM.DATA=WCOM.DATA:SP:JBS.CCTR.STATUS<1,CPTR>"L#4"
         END CASE
         CALL LOAD.DATA(MAT WCOM)
      NEXT CPTR
      CALL SET.WSCROLL(MAT WCOM)
      GOTO 99999
*
*---- ERROR ROUTINE
*
90000 *
      WCOM.DATA=WCOM.STATUS
      CALL MESSAGE.BOX(MAT WCOM)
      WCOM.STATUS = ""
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      RETURN
   END
