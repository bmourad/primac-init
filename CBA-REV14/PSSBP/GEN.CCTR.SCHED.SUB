      SUBROUTINE GEN.CCTR.SCHED.SUB (CONO, CCTR, XDATE, ERRMSG)
*COPY>CPYLIB>SCOMMON1
*COPY>PSS.CPYLIB>COM.PSS.FILE.VARS
*COPY>PSS.CPYLIB>COM.CCTR.SCHED
*COPY>PSS.CPYLIB>COM.JOB.SCHED
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM   - PRIMAC
* SOURCE   - PSSBP
* PROGRAM  - GEN.CCTR.SCHED.SUB
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 05/12/85
* DESCRIPTION
* This subroutine will generate the normal cost center available
* hours based upon the available hours specified in file CCTR.AVAIL
* and the defined holiday schedule.
*
* CSF21193 IF NO COST CENTER AVAILABLE HOURS AND NO DEPT. AVAILABLE HOURS
* ARE FILLED IN IN THE CCTR.AVAIL FILE THEN SEND ERROR MESSAGE TO FILL
* IN THE GENERAL AVAILABILITY IN FILE MAINTENANCE.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PSS.CPYLIB>PSS.FILE.VARS
*COPY>PSS.CPYLIB>CCTR.SCHED
*COPY>PSS.CPYLIB>CCTR.AVAIL
*COPY>PSS.CPYLIB>HOLIDAY.SCHED
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:CCS.DEPT:"*":CCTR ELSE
         MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, CONO:CCS.DEPT ELSE
            ERRMSG = "Cannot locate availability for cost center ":CCTR
            GOTO 999999
         END
      END
*CSF21193
     CT = ""
     CT = DCOUNT(CCA.AVAIL.HRS,VM)
     TOT.HRS = ""
     FOR I = 1 TO CT
        TOT.HRS = TOT.HRS + CCA.AVAIL.HRS<1,I>
     NEXT I
     IF TOT.HRS = "" THEN
        ERRMSG = "No CCTR General Available Hours have been filled in for Dept. ":CCS.DEPT:" CCTR ":CCTR
        GOTO 999999
     END
*
      DAY = MOD(XDATE,7)+1
      CCS.AVL.SHR = ""
      CCS.MULT = ""
      CCS.AVL.HRS = ""
      CCS.REM.HRS = ""
      READ TP.DIV FROM PSS.LOCK, @TTY ELSE TP.DIV = ""
*
*---- MAIN PROCESSING
*
100*
      FOR SFT = 1 TO 3
         IF CCA.HOL.SCHED<1,SFT> = "" THEN
            MAT HOL.SCHED.REC = ""
         END ELSE
            MATREAD HOL.SCHED.REC FROM HOLIDAY.SCHED, CONO:CCA.HOL.SCHED<1,SFT> ELSE
               MAT HOL.SCHED.REC = ""
            END
         END
*
         FND = 1
         LOCATE XDATE IN HOL.DATE<1>,1 BY "AR" SETTING P ELSE FND = 0
         IF FND AND (HOL.HRS<1,P>+0) < (CCA.AVAIL.SHR<1,DAY,SFT>+0) THEN
            CCS.AVL.SHR<1,SFT> = HOL.HRS<1,P>
            CCS.MULT<1,SFT> = CCA.MULT<1,DAY,SFT>
            CCS.AVL.HRS = CCS.AVL.HRS + (HOL.HRS<1,P> * CCA.MULT<1,DAY,SFT>)
         END ELSE
            CCS.AVL.SHR<1,SFT> = CCA.AVAIL.SHR<1,DAY,SFT>
            CCS.MULT<1,SFT> = CCA.MULT<1,DAY,SFT>
            CCS.AVL.HRS = CCS.AVL.HRS + (CCA.AVAIL.SHR<1,DAY,SFT> * CCA.MULT<1,DAY,SFT>)
         END
         IF CCS.AVL.SHR<1,SFT>+0 = 0 THEN
            CCS.AVL.SHR<1,SFT> = ""
            CCS.MULT<1,SFT> = ""
         END ELSE
            IF CCS.MULT<1,SFT> + 0 = 0 THEN CCS.MULT<1,SFT> = 1
         END
         CCS.REM.SM.HRS<1,SFT> = ""
         MCNT = DCOUNT(CCS.SCH.SM.HRS<1,SFT>,SVM)
         IF CCS.MULT<1,SFT> > MCNT THEN MCNT = CCS.MULT<1,SFT>
         FOR MLT = 1 TO MCNT
            BEGIN CASE
            CASE MLT > CCS.MULT<1,SFT> AND CCS.SCH.SM.HRS<1,SFT,MLT> + 0 # 0
               CCS.REM.SM.HRS<1,SFT,MLT> = 0 - CCS.SCH.SM.HRS<1,SFT,MLT>
            CASE MLT > CCS.MULT<1,SFT>
            CASE 1
               CCS.REM.SM.HRS<1,SFT,MLT> = CCS.AVL.SHR<1,SFT> - CCS.SCH.SM.HRS<1,SFT,MLT>
            END CASE
            IF CCS.REM.SM.HRS<1,SFT,MLT> = 0 THEN CCS.REM.SM.HRS<1,SFT,MLT> = ""
            CCS.REM.HRS = CCS.REM.HRS + CCS.REM.SM.HRS<1,SFT,MLT>
         NEXT MLT
      NEXT SFT
*
      READU AVLCTR FROM CONTROL, CONO:"AVLCTR" ELSE AVLCTR=""
      LOCATE CCTR IN AVLCTR,1 BY "AL" SETTING AVPTR THEN
         RELEASE CONTROL, CONO:"AVLCTR"
      END ELSE
         AVLCTR = INSERT(AVLCTR,AVPTR,0,0,CCTR)
***      WRITE AVLCTR ON CONTROL, CONO:"AVLCTR"
         TPFID = CONO:"AVLCTR"
         TPREC = AVLCTR
         CALL TRANSACTION.WRITE("WRITE",CONO,TP.DIV,"CONTROL",TPFID,TPREC,CONTROL,PSS.JOURNAL,ERRMSG)
**********************************************************************
      END
999999*
      RETURN
      END
