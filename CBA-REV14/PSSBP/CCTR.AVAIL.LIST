*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PSSBP
* PROGRAM     - CCTR.AVAIL.LIST
* AUTHOR      - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 11/26/88
* DESCRIPTION -
*     This program prints the general cost center availability.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*********************************************************************
*COPY>PSS.CPYLIB>CCTR.AVAIL
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET UP SYSCOM
*
      SYS.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN "CCTR.AVAIL" TO CCTR.AVAIL ELSE
         ERRMSG = "CANNOT OPEN CCTR.AVAIL FILE"
         GOTO 93000
      END
      OPEN "DEPARTMENT" TO DEPARTMENT ELSE
         ERRMSG = "CANNOT OPEN DEPARTMENT FILE"
         GOTO 93000
      END
      OPEN "COST.CNTR" TO COST.CNTR ELSE
         ERRMSG = "CANNOT OPEN COST.CNTR FILE"
         GOTO 93000
      END
      OPEN "COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOTO 93000
      END
      OPEN "CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOTO 93000
      END
OPEN "","PSS.JOURNAL" TO PSS.JOURNAL ELSE
   ERRMSG = "CANNOT OPEN PSS.JOURNAL FILE"
GOTO 93000
END
OPEN "","PSS.LOCK" TO PSS.LOCK ELSE
   ERRMSG = "CANNOT OPEN PSS.LOCK FILE"
GOTO 93000
END
*
*---- PROCREAD
*
      PROCREAD ID ELSE
         ERRMSG = "MUST BE RUN FROM MENU"
         GOSUB 91000
         GOTO 99999
      END
*
*---- HEADING INITIALIZATION
*
      CONO = ID<1>
*T26493 v
*     CONO.NAME = ID<2>
*     REPORT.NAME = "GENERAL CCTR AVAILABILITY LISTING"
*     REPORT.NUM = "XXXX"
      CONO.NAME = ""
      REPORT.NAME = ""
      REPORT.NUM = ID<2>
*T26493 ^
      HD1 = ""
      HD2 = ""
      PAGE.NO = 0
      READ DEMODATE FROM CONTROL,"DEMODATE" THEN REPORT.DATE=ICONV(DEMODATE,"D") ELSE REPORT.DATE=DATE()
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUM,REPORT.DATE,HD1,HD2)
      HD5 = "-------- -------- -------- -------- -------- -------- -------- -------"
      HD3 = "CCTR           DESCRIPTION          C S SHIFT HOL   SUN      MON      TUE      WED      THU      FRI      SAT     TOTAL"
      HD4 = "---- ------------------------------ - - ----- --- "
*
*---- INITIALIZATION
*
      LINE.CNT = 99
      PREV.DEPT = ""
      FIRST = 1
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      SP9 = SPACE(9)
      SP30 = SPACE(30)
*
*---- MAIN PROCESSING
*
      PRINTER ON
      DATA = 1
      LOOP
         READNEXT KEY ELSE DATA = 0
      WHILE DATA DO
         MATREAD CCTR.AVAIL.REC FROM CCTR.AVAIL, KEY ELSE
            MAT CCTR.AVAIL.REC = ""
            GOTO 199
         END
         DEPT = FIELD(KEY,"*",1)
         DEPT = DEPT[4,99]
         CCTR = FIELD(KEY,"*",2)
         BEGIN CASE
         CASE FIRST
            MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE
               MAT DEPT.REC = ""
               DEPT.DESC = "NOT ON FILE"
            END
            PREV.DEPT = DEPT
            FIRST = 0
         CASE PREV.DEPT # DEPT
            MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE
               MAT DEPT.REC = ""
               DEPT.DESC = "NOT ON FILE"
            END
            PREV.DEPT = DEPT
            LINE.CNT = 99
         END CASE
         GOSUB 1000
199*
      REPEAT
      GOTO 99999
*
*---- CCTR LINE
*
1000*
      IF LINE.CNT + 7 > 54 THEN
         GOSUB 8000
      END ELSE
         PRINT
         LINE.CNT = LINE.CNT + 1
      END
      GTOT = 0
      IF CCTR = "" THEN
         CCTR = "MAST"
         MAT CCTR.REC = ""
      END ELSE
         MATREAD CCTR.REC FROM COST.CNTR, CONO : CCTR ELSE
            MAT CCTR.REC = ""
            CCTR.DESC = "NOT ON FILE"
         END
      END
      FOR SFT = 1 TO 3
         VLINE = ""
         LTOT = 0
         BEGIN CASE
         CASE SFT = 1
            VLINE = VLINE : CCTR "R#4" : SP1
            VLINE = VLINE : CCTR.DESC "L#30" : SP1
            VLINE = VLINE : CCA.CONT.SCHD "L#1" : SP1
            VLINE = VLINE : CCA.SAME.EQP "L#1" : SP1
            VLINE = VLINE : SP1 : "1st" : SP2
         CASE SFT = 2
            VLINE = VLINE : SP9 : SP30 : SP1
            VLINE = VLINE : SP1 : "2nd" : SP2
         CASE 1
            VLINE = VLINE : SP9 : SP30 : SP1
            VLINE = VLINE : SP1 : "3rd" : SP2
         END CASE
         VLINE = VLINE : CCA.HOL.SCHED<1,SFT> "L#3"
         FOR DAY = 1 TO 7
            VLINE = VLINE : SP1 : OCONV(CCA.AVAIL.SHR<1,DAY,SFT>, "MD2") "R#5"
            IF CCA.MULT<1,DAY,SFT> # "" THEN
               VLINE = VLINE : "x"
            END ELSE
               VLINE = VLINE : SP1
            END
            VLINE = VLINE : CCA.MULT<1,DAY,SFT> "L#2"
            LTOT = LTOT + CCA.AVAIL.SHR<1,DAY,SFT> * CCA.MULT<1,DAY,SFT>
         NEXT DAY
         VLINE = VLINE : SP1 : OCONV(LTOT, "MD2Z") "R#7"
         PRINT VLINE
         LINE.CNT = LINE.CNT + 1
      NEXT SFT
      PRINT SP9 : SP30 : SP9 : SP2 : HD5
      LINE.CNT = LINE.CNT + 1
      VLINE = SP9 : SP30 : SP9 : SP1
      FOR DAY = 1 TO 7
         VLINE = VLINE :SP1: OCONV(CCA.AVAIL.HRS<1,DAY>, "MD2") "R#7":SP1
         GTOT = GTOT + CCA.AVAIL.HRS<1,DAY>
      NEXT DAY
      VLINE = VLINE : SP1 : OCONV(GTOT, "MD2") "R#7"
      PRINT VLINE
      LINE.CNT = LINE.CNT + 1
      RETURN
*
*---- HEADINGS
*
8000*
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PLINE = "DEPT : ":DEPT "L#5":SP1:DEPT.DESC "L#20"
      PRINT PLINE
      PRINT
      PRINT HD3
      PRINT HD4:HD5
      PRINT
      LINE.CNT = 8
      RETURN
*
*---- ERROR ROUTINE
*
91000*
      ERR.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      ERR.TYPE = 3
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
99999*
      PRINTER OFF
   END
