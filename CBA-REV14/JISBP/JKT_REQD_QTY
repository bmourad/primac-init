      SUBROUTINE JKT_REQD_QTY(CONO,ERRMSG,I_FNAME,I_FPTR,I_VNAME,I_VREC,I_SNAME,I_SREC,REQD_QTY)
*COPY>CPYLIB>COM1
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 06/13/95
* System        - PRIMAC
* Library       - JISBP/JKT_INKDSC
* Author        - Ziad Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
      FCNT = 2
      DIM FPTR(FCNT)
      EQU JOB_FILE  TO FPTR(1)
      EQU STAT_FILE  TO FPTR(2)
*
      ICNT = 3
      DIM VREC(ICNT)
      EQU JOB_ID TO VREC(1)
      EQU RESV_PROD TO VREC(2)
      EQU RESV_WHSE TO VREC(3)
*
      ERRMSG = ""
101*
      FNAME = ""
      FNAME<1> = "JOB"
      FNAME<2> = "INV.JOB.STATS"
*
      FOR I = 1 TO FCNT
         LOCATE FNAME<I> IN I_FNAME,1 SETTING FND THEN
            FPTR(I) = I_FPTR<FND>
         END ELSE
            ERRMSG = "File (":FNAME<I>:") is required for this Subroutine"
            FCNT = 0
         END
      NEXT I
      IF FCNT = 0 THEN GOTO 99999
201*
      VNAME = ""
      VNAME<1> = "IP_JOB_NO"
      VNAME<2> = "JOB_RESV_MATL"
      VNAME<3> = "JOB_RESV_WHSE"
      FOR I = 1 TO ICNT
         LOCATE VNAME<I> IN I_VNAME,1 SETTING FND THEN
            VREC(I) = I_VREC<FND>
         END ELSE
            ERRMSG = "Data Field (":VNAME<I>:") is required for this Subroutine"
            ICNT = 0
         END
      NEXT I
      IF ICNT = 0 THEN GOTO 99999
1000*
      MATREAD JOB.REC FROM FILE.VARS(JOB_FILE), CONO:JOB_ID ELSE
         ERRMSG = "Cannot locate the correct job info. Check for VSI_JOB in JKT_REQD_QTY subroutine"
         * If you receive this error message, change the VSI_JOB variable
         * to the correct customer's file _JOB variable.
         GOTO 99999
      END
      REQD_QTY = ""
      MCNT = DCOUNT(JOB.RESV.MATL,VM)
      FOR M = 1 TO MCNT
        MATL = JOB.RESV.MATL<1,M>
        WHSE = JOB.RESV.WHSE<1,M>
        STAT.ID = MATL:"!":WHSE:"!":JOB_ID
        MATREAD INV.JS.REC FROM FILE.VARS(STAT_FILE), CONO:STAT.ID ELSE
          MAT INV.JS.REC = ""
        END
        REQD_QTY<1,M> = IJS.REQ.QTY
      NEXT M
*
99999*
      RETURN
      END
