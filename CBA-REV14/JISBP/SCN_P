*COPY>CPYLIB>COM1
*******************************************
* REVISION      - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* SOURCE        - DEVBP
* PROGRAM       - SCN.PRINT
* BY            - BILAL MOURAD
* DATE          - 6/9/95
* DESCRIPTION   -
* T26493 cmykleb 05/06/2002 * Change report to use GET.PROG.HEAD and get
*                             the report # from the proc.
*******************************************
*
*--- Data Base Libraries
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JIS.CPYLIB>SYS_SCN_DEF
*COPY>JIS.CPYLIB>SYS_FIELDS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- Open Files
*
   OPEN "","CUSTOMER" TO CUSTOMER ELSE
      ERRMSG = "CUSTOMER FILE IS MISSING"
      GOSUB 93000
   END
   OPEN "","SYS_FIELDS" TO SYS_FIELDS ELSE
      ERRMSG = "SYS_FIELDS FILE IS MISSING"
      GOSUB 93000
   END
   OPEN "","SYS_SCN_DEF" TO SYS_SCN_DEF ELSE
      ERRMSG = "SYS_SCN_DEF FILE IS MISSING"
      GOSUB 93000
   END
*
*--- Read Proc Values
*
   PROCREAD ID ELSE
      ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC."
      GOSUB 91000
      GOTO 99999
   END
   CONO = ID<1>; CONO.NAME = ID<2>
*
*--- Get key and read record
*
   KEY = ID<3>
   MATREAD SSD.REC FROM SYS_SCN_DEF,KEY ELSE
      ERRMSG = "CANNOT ACCESS RECORD ":KEY:" IN SYS_SCN_DEF FILE."
      GOSUB 91000
      GOTO 99999
   END
*
*--- Get The First Two Lines of Heading
*
   LINE.FILL = STR(" ",43)
*T26493 v
*  HD1 = ""
*  REPORT.NAME = "F O R M    D E F I N I T I O N   ( ":ID<3>:"  / ":SSD_M_TABLE:" )"
*  REPORT.NUMBER = "REPORT # : XXXX"
*  REPORT.HD = ID<2>:ID<3>:" )"
*  REPORT.HD2 = REPORT.NUMBER:LINE.FILL:REPORT.NAME
   RPT.NAME = ""
   CO.NAME = ""
   RPT.NUM = ID<2>
   HD1 = ""
   HD2 = ""
   CALL GET.PROG.HEAD(CONO,CO.NAME,RPT.NAME,RPT.NUM,"",HD1,HD2)
   HD3 = "FORM : ":ID<3>:"/":SSD_M_TABLE
*T26493 ^
*
*--- INT
*
   S.HD1 ="Att             Field                  Table      Prefix  MVNO/Lnk Ref   SVNO/Lnk Fld  C H T SEQ REF X  Y  L S FMT/CNV Hd Dist List"
   S.HD2 ="--- ------------------------------ -------------- ------ --------------- ------------- - - - --- --- -- -- - - ------- -- ----------"
   S.HD3 = STR(" ",129)
*
*--- Main Processing
*
   VCNT = DCOUNT(SSD_FIELDS,@VM)
   CNT = 0
   PAGE.CNT = 0
   PAGE.SIZE = 53
   P.HD = "PAGE"
   PRINTER ON
   GOSUB 100
   FOR I = 1 TO VCNT
      BEGIN CASE
         CASE SSD_FLD_EXPR<1,I> = ""
            LINE = 2
         CASE SSD_FLD_EXPR<1,I> = "@SUBR"
            LINE = 5
         CASE 1
            LINE = 2 + DCOUNT(SSD_FLD_EXPR<1,I>,@SM)
      END CASE
      FOR J = 1 TO LINE 
         P.LINE = ""
         BEGIN CASE
            CASE J = 1
               P.LINE = P.LINE:SSD_FLD_ATTNO<1,I,J> "R#3":" "
               P.LINE = P.LINE:SSD_FIELDS<1,I,J> "L#30":" "
               P.LINE = P.LINE:SSD_FLD_TABLE<1,I,J> "L#14":" "
               P.LINE = P.LINE:SSD_FLD_PFX<1,I,J> "L#6":" "
               P.LINE = P.LINE:SSD_FLD_MVNO<1,I,J> "L#15":" "
               P.LINE = P.LINE:SSD_FLD_SMVNO<1,I,J> "L#13":" "
               P.LINE = P.LINE:SSD_FLD_LCHR<1,I,J> "L#1":" "
               P.LINE = P.LINE:SSD_FLD_HIDE<1,I,J> "L#1":" "
               P.LINE = P.LINE:SSD_FLD_TYPE<1,I,J> "L#1":" "
               P.LINE = P.LINE:SSD_FLD_SEQ<1,I,J> "R#3":" "
               P.LINE = P.LINE:SSD_FLD_REF<1,I,J> "R#3":" "
               P.LINE = P.LINE:SSD_FLD_X<1,I,J> "R#2":" "
               P.LINE = P.LINE:SSD_FLD_Y<1,I,J> "R#2":" "
               P.LINE = P.LINE:SSD_FLD_LINES<1,I,J> "L#1":" "
               P.LINE = P.LINE:SSD_FLD_LNSPC<1,I,J> "L#1":" "
               P.LINE = P.LINE:SSD_FLD_FMT<1,I,J> "L#7":" "
               P.LINE = P.LINE:SSD_FLD_HIDE<1,I,J> "L#2":" "
               P.LINE = P.LINE:SSD_FLD_DSTLST<1,I,J>[1,10] "L#10"
            CASE J = 2
               P.LINE = STR(" ",4)
               MATREAD DFD.REC FROM SYS_FIELDS, SSD_FIELDS<1,I,J-1> THEN
                  P.LINE = P.LINE:DFD_DESC "L#30":STR(" ",21)
               END ELSE
                  P.LINE = P.LINE:STR(" ",51)
               END
               P.LINE = P.LINE:SSD_FLD_LNK<1,I,J-1> "L#23":" "
               P.LINE = P.LINE:SSD_FLD_LNKREF<1,I,J-1> "L#32":" "
               P.LINE = P.LINE:SSD_FLD_CNV<1,I,J-1> "R#6":"    "
               P.LINE = P.LINE:SSD_FLD_DSTLST<1,I,J-1>[11,10] "L#10"
            CASE J = 3
               IF SSD_FLD_EXPR<1,I> = "@SUBR" THEN
                  P.LINE = STR(" ",4):"SUBROUTINE = ":SSD_FLD_SUBR<1,I> "L#60"
                  P.LINE = P.LINE:STR(" ",45)
               END ELSE
                  P.LINE = STR(" ",4):SSD_FLD_EXPR<1,I,1> "L#60":STR(" ",58)
               END
               P.LINE = P.LINE:SSD_FLD_DSTLST<1,I,J-2>[21,10] "L#10"
            CASE J = 4
               IF SSD_FLD_EXPR<1,I> = "@SUBR" THEN
                  P.LINE = STR(" ",4):SSD_SBR_FNAME<1,I,1>
                  FOR P = 2 TO DCOUNT(SSD_SBR_FNAME<1,I>,@SM)
                     P.LINE = P.LINE:" , ":SSD_SBR_FNAME<1,I,P>
                  NEXT P
                  P.LINE = P.LINE:STR(" ",125-LEN(P.LINE))
               END ELSE
                  P.LINE = STR(" ",4):SSD_FLD_EXPR<1,I,2> "L#60":STR(" ",65)
               END
            CASE J = 5
               IF SSD_FLD_EXPR<1,I> = "@SUBR" THEN
                  P.LINE = STR(" ",4):SSD_SBR_VNAME<1,I,1>
                  FOR P = 2 TO DCOUNT(SSD_SBR_VNAME<1,I>,@SM)
                     P.LINE = P.LINE:" , ":SSD_SBR_VNAME<1,I,P>
                  NEXT P
                  P.LINE = P.LINE:STR(" ",125-LEN(P.LINE))
               END ELSE
                  P.LINE = STR(" ",4):SSD_FLD_EXPR<1,I,3> "L#60":STR(" ",65)
               END
            CASE J > 5
               P.LINE = STR(" ",4):SSD_FLD_EXPR<1,I,J-2> "L#60":STR(" ",65)
         END CASE
         IF P.LINE # S.HD3 AND P.LINE # HD1 THEN
            IF CNT > PAGE.SIZE THEN
*T26493 v
*              PRINT HD1
*              PRINT HD1
               PRINT CHAR(12)
*T26493 ^
               GOSUB 100
            END
            CNT = CNT + 1
            PRINT P.LINE
         END
      NEXT J
      IF CNT < PAGE.SIZE THEN
         CNT = CNT + 1
         PRINT S.HD3
      END
   NEXT I
   GOTO 99999
*
*--- Print Page headers
*
100*
   PAGE.CNT = PAGE.CNT + 1
   PAGE.NO = PAGE.CNT "R#4"
*T26493 v
*  PRINT HD1
*  PRINT HD1
*  PRINT HD1
*  REPORT.HD = FIELD(REPORT.HD,P.HD,1,1):"PAGE":PAGE.NO
*  PRINT REPORT.HD
*  PRINT REPORT.HD2
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT HD3
   PRINT
*T26493 ^
   PRINT S.HD1
   PRINT S.HD2
   PRINT S.HD3
   CNT = 0
   RETURN
*
*--- Print Error Messages
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
*
*--- End of Program
*
99999*
   PRINTER OFF
   PRINTER CLOSE
END
