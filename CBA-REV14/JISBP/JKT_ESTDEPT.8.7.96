      SUBROUTINE JKT_INKDSC(CONO,ERRMSG,I_FNAME,I_FPTR,I_VNAME,I_VREC,I_SNAME,I_SREC,I_RET_VALUE)
*COPY>CPYLIB>COM1
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 06/13/95
* System        - PRIMAC
* Library       - UTLBP/JKT_INKDSC
* Author        - Ziad Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
      FNAME = "ESTIMATE.DEPT"
*
      FCNT = 1
      DIM FPTR(FCNT)
      EQU ESTDEPT_FPTR  TO FPTR(1)
*
      VNAME = "JOB_EST"
      VNAME<2> = "EST_DEPT"
      VNAME<3> = "ESTJ_COMP"
      VNAME<4> = "ESTJ_QTY"
*     VNAME<4> = "JOB_QTY"
*
      VCNT = 4
      DIM VREC(VCNT)
      EQU EST_NO   TO VREC(1)
      EQU DEPT_NOS TO VREC(2)
      EQU COMP_NOS TO VREC(3)
      EQU QTY      TO VREC(4)
*
      SNAME = "SELECTION"
      SNAME<2> = "DEPT"
      SNAME<3> = "COMP"
      SNAME<4> = "CCTR"
      SNAME<5> = "OPER"
      SNAME<6> = "ETRN_TYPE"
*
      SCNT = 6
      DIM SREC(SCNT)
      EQU SELNO     TO SREC(1)
      EQU DP_SEL    TO SREC(2)
      EQU CP_SEL    TO SREC(3)
      EQU CC_SEL    TO SREC(4)
      EQU OP_SEL    TO SREC(5)
      EQU TP_SEL    TO SREC(6)
*
      ONAME = "ESTD_QTY"
*
      ERRMSG = ""
      I_RET_VALUE = ""
101*
      FOR I = 1 TO FCNT
         LOCATE FNAME<I> IN I_FNAME,1 SETTING FND THEN
            FPTR(I) = I_FPTR<FND>
         END ELSE
            ERRMSG = "File (":FNAME<I>:") is required for this Subroutine"
            FCNT = 0
         END
      NEXT I
      IF FCNT = 0 THEN GOTO 99999
201*
      FOR I = 1 TO VCNT
         LOCATE VNAME<I> IN I_VNAME,1 SETTING FND THEN
            VREC(I) = I_VREC<FND>
         END ELSE
            ERRMSG = "Data Field (":VNAME<I>:") is required for this Subroutine"
            VCNT = 0
         END
      NEXT I
      IF VCNT = 0 THEN GOTO 99999
301*
      MAT SREC = ""
      FOR I = 1 TO SCNT
         PTR = 1
         LOOP
            LOCATE SNAME<I> IN I_SNAME,PTR SETTING FND THEN
               SREC(I)<-1> = I_SREC<FND>
            END ELSE
               PTR = 0
            END
         WHILE PTR DO
            PTR = FND + 1
         REPEAT
      NEXT I
*
      IF SELNO = "" OR SELNO = "ALL" THEN
         SELNO = 1
      END ELSE
         LOCATE SELNO IN ONAME,1 SETTING SELNO ELSE
            SELNO = 1
         END
      END
1000*
      IF DP_SEL = "" OR DP_SEL = "ALL" THEN
         DPCNT = DCOUNT(DEPT_NOS,VM)
         FOR DPNO = 1 TO DPCNT
            GOSUB 1100
         NEXT DPNO
      END ELSE
         DPCNT = DCOUNT(DP_SEL,AM)
         FOR DP = 1 TO DPCNT
            LOCATE DP_SEL<DP> IN DEPT_NOS<1>,1 SETTING DPNO THEN
               GOSUB 1100
            END
         NEXT DP
      END
     GOTO 99999
*
1100*
      IF CP_SEL = "" OR CP_SEL = "ALL" THEN
         CPCNT = DCOUNT(COMP_NOS<1,1>,SVM)
         FOR CP = 1 TO CPCNT
            CPNO = COMP_NOS<1,1,CP>
            GOSUB 2000
         NEXT CP
      END ELSE
         CPCNT = DCOUNT(CP_SEL,AM)
         FOR CP = 1 TO CPCNT
            LOCATE CP_SEL<CP> IN COMP_NOS<1,1>,1 SETTING CP THEN
               CPNO = COMP_NOS<1,1,CP>
               GOSUB 2000
            END
         NEXT CP
      END
      RETURN
2000*
      ID = CONO : EST_NO : "!" : DPNO : "!" : CPNO : "!" : QTY<1,1>
      MATREAD ESTD.REC FROM FILE.VARS(ESTDEPT_FPTR), ID ELSE
         GOTO 2099
      END
2010*
      IF TP_SEL = "" OR TP_SEL = "ALL" THEN
         TPCNT = DCOUNT(ESTD.TYPE,VM)
         FOR TPNO = 1 TO TPCNT
            GOSUB 2200
         NEXT TPNO
      END ELSE
         TPCNT = DCOUNT(TP_SEL,AM)
         FOR TP = 1 TO TPCNT
            TPPTR = 1
            LOOP
               LOCATE TP_SEL<TP> IN ESTD.TYPE<1>,TPPTR SETTING TPNO THEN
                  GOSUB 2200
               END ELSE
                  TPPTR = 0
               END
            WHILE TPPTR DO
               TPPTR = TPNO + 1
            REPEAT
         NEXT TP
      END
2099*
      RETURN
*
2200*
      IF CC_SEL # "" AND CC_SEL # "ALL" THEN
         LOCATE ESTD.CCTR<1,TPNO> IN CC_SEL,1 SETTING FND ELSE GOTO 2299
      END
      IF OP_SEL # "" AND OP_SEL # "ALL" THEN
         LOCATE ESTD.OPV<1,TPNO> IN OP_SEL,1 SETTING FND ELSE GOTO 2299
      END
      ON SELNO GOSUB 3100
2299*
      RETURN
*
3100*
      I_RET_VALUE = I_RET_VALUE + INT(ESTD.QTY<1,TPNO> / 100)
      RETURN
*
99999*
      RETURN
      END
