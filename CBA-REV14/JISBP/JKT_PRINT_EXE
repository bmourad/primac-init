      SUBROUTINE JKT_PRINT_EXE ( RET_VALUE, AS_ID)
*COPY>CPYLIB>COM1
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 04/28/94
* System        - PRIMAC
* Library       - JISBP/JKT_PRINT_EXE
* Author        - Walid Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JIS.CPYLIB>SYS_TKT_DEF
*COPY>CPYLIB>CHAR
*
*--- INIT
*
      RET_VALUE = ""
*
*---- Open required Files
*
      OPEN "","CONTROL" TO CONTROL ELSE
         RET_VALUE = "Cannot locate the CONTROL file"
         GOTO 99999
      END
      OPEN "","COMPANY" TO COMPANY ELSE
         RET_VALUE = "Cannot locate the COMPANY file"
         GOTO 99999
      END
      OPEN "","SYS_TKT_DEF" TO SYS_TKT_DEF ELSE
         RET_VALUE = "Cannot locate the SYS_TKT_DEF file"
         GOTO 99999
      END
*
*--- VALIDATE
*
      THIS_FUNC = "JKT_PB_PRINT"
      AS_CONO = ""
      AS_TKTID = ""
      AS_TKTDEF = ""
      AS_CLIENT = ""
      AS_SERVER = ""
      AS_PRINTER = ""
      IF TRIM (AS_ID) = "" THEN
         RET_VALUE = "Ticket/Form ID was not specified"
         GOTO 99999
      END
      AS_CONO = FIELD (AS_ID, "!", 1)[1,3]
      AS_TKTID = FIELD (AS_ID, "!", 1)[4,99]
      AS_TKTDEF = FIELD (AS_ID, "!", 2)
*
      MATREAD COMP.REC FROM COMPANY, AS_CONO ELSE
         RET_VALUE = "Company record is missing"
         GOTO 99999
      END
      IF CO.JIS # "Y" THEN
         RET_VALUE = "Job Information System functionality not purchased"
         GOTO 99999
      END
*
      IF TRIM(AS_TKTDEF) = "" THEN
         RET_VALUE = "Form Control Number was not specified"
         GOTO 99999
      END ELSE
         MATREAD STKD.REC FROM SYS_TKT_DEF, AS_TKTDEF ELSE
            RET_VALUE = "Cannot locate screen names for Job Jacket "
            GOTO 99999
         END
      END
*
*---- Process the requested screen
*
      READU ALL_IDS FROM CONTROL, THIS_FUNC ELSE ALL_IDS = ""
      IDCNT = DCOUNT (ALL_IDS, AM) + 1
      ALL_IDS<IDCNT> = AS_CONO
      ALL_IDS<IDCNT,2> = AS_TKTID
      ALL_IDS<IDCNT,3> = AS_TKTDEF
      ALL_IDS<IDCNT,4> = AS_CLIENT
      ALL_IDS<IDCNT,5> = AS_SERVER
      ALL_IDS<IDCNT,6> = AS_PRINTER
      WRITE ALL_IDS ON CONTROL, THIS_FUNC
      UDTEXECUTE THIS_FUNC CAPTURING RET_VALUE
*
*--- END OF PROGRAM
*
99999*
      RETURN
   END
