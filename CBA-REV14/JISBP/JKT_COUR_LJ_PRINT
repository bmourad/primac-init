SUBROUTINE JKT_COUR_LJ_PRINT(CONO,ERRMSG,I_FNAME,I_FPTR,I_VNAME,I_VREC,I_SNAME,I_SREC,HP_PCL)
*COPY>CPYLIB>COM1

******************************************************************************
*                 written for Courier Corporation
*  
*  Revision Date -  10/09/98
*
*  Library       -  JISBP/JKT_COUR_LJ_PRINT
*
*  Author        -  Dave Markell
*
******************************************************************************
*
*  For use by JIS for Courier Job Tickets for use in the first print form
*  in each docuset to generate a HP PCL 5 character string in order to
*  insure that the printer is set up for Courier font (10 or 12 cpi), 
*  portrait, 6 lines per inch (60 lines per page)
*
*  Should be the first non-hidden field on the first form.
*  
*  Even tho no external files are invoked, an entry is required in the
*  Passing File Names section of the Screen Subroutine Definition form.
*
*
*  JIS always requires an entry in the Passing Field Names section.   
*  The BCJOB option requies that this field name be COURIER_JOB_NO
*  so it probably makes sense to always use this field name for this
*  subroutine
*  
*                                          
*  A variable must be established and set to 1,2 or 999 in the
*  Passing Variable Names section of the Screen Subroutine Definition form:
*
*     LJFUNCTION     
*
*  The value of this variable should be set as follows:
*
*    INIT  = The field referencing this value (COURIER_FLD_59?)
*            should be placed in the first column of the first line of the
*            print form.  This will initialize the printer to portrait,
*            12 cpi, 66 lines per page. It should be defined as a hidden
*            field on the HEADER entry screen and referenced from this
*            field on the first page of each docuset's first print form.
*
*    BCJOB = This will place a bar code for the Job Number in column 72
*            of the current print form line.  No other printed area should
*            cause a conflic with this & the text Job Number should be lined
*            up to print undernead it.  It should be repeated on 2 successive
*            lines of the print form. It should be associated with
*            COURIER_FLD_59? and defined as a hidden field on the HEADER
*            field on the HEADER entry screen and referenced from this
*            field on the first page of each docuset's first print form.
*            entry screen.  It should be the last entry on the line. 
*      
*    BCOFF = This will reset the printer to print normal characters
*            after printing a barcode.  If there is enough room on the
*            line - it can be placed directly after the BCJOB field.
*            If not - it should be the first field on the next line.
*
*    LINE  = This will simply return a string of 80 horizontal line
*            characters  (ASCII 196) to be used for separator lines
*            on print forms instead of dashes
*
*    RESET = This will cause a LaserJet reset command to be issued to
*            return the LaserJet to its factory default settings.  It
*            should be associated with COURIER_FLD_59? on the HEADER entry
*            form as a hidden field.  It should be placed as the last
*            field on the last page of any docuset's last print form.
*
*  This subroutine will return the proper PCL as a character string in the
*  HP_PCL variable.
*      
*  It is used instead of a JIS formula field to make it easier to generate
*  the proper none alphanumeric codes (the Esc character).
*
*  If these are set up as visible (NOT HIDDEN) fields on any form that is
*  used for viewing and not limited to the print form section of the docuset
*  they can contain a string of characters that will cause the screen display
*  to be totally blank from the point at which they first appear.
*
******************************************************************************
*
*
*---  Data Structure Libraries  ---
*
*  -  no file CPYLIB's required since no files are referenced
*
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*   no use of I_FNAMES, I_FPTR required so no set up of FNAME or FPTR arrays
*
      VNAME    = "COURIER_JOB_NO"
      VCNT = 1
      DIM VREC(VCNT)
      EQU JOB_NBR     TO VREC(1)
*
      SNAME = "LJFUNCTION"
      SCNT = 1
      DIM SREC(SCNT)
      EQU LJFUNCTION TO SREC(1)
*
* set up blank error message
* set up ??? in return value (so can check if was a valid LJFUNCTION
*
      ERRMSG = ""
      HP_PCL = "???"
*
* scan array of variable names passed on Screen Subroutine Definition
*   for each variable name in SNAME dynamic array
*
      FOR I = 1 TO SCNT
        LOCATE SNAME<I> IN I_SNAME,1 SETTING FND THEN
          SREC(I) = I_SREC<FND>
        END ELSE
          ERRMSG = "Variable Field (":SNAME<I>:") is required for this Subroutine"
          SCNT = 0
        END
      NEXT I
      IF SCNT = 0 THEN GOTO 99999
*
* scan array of field names passed on Screen Subroutine Definition
*   for each field name in VNAME dynamic array
*
*  NOTE: looks for JOB for Job Number only if is BCJOB function
*        otherwise don't care what field is passed
*
      ICNT = 9
      IF LJFUNCTION = "BCJOB" OR LJFUNCTION = "BCOFF"  THEN
        FOR I = 1 TO VCNT
          LOCATE VNAME<I> IN I_VNAME,1 SETTING FND THEN
            VREC(I) = I_VREC<FND>
          END ELSE
            ERRMSG = "Data Field (":VNAME<I>:") is required for this Subroutine"
            ICNT = 0
          END
        NEXT
        IF ICNT = 0 THEN GOTO 99999
      END
*
* now that have all required passed arguments established execute the logic
*
*
* DEFINITION OF HP LASERJET PCL CODES, ETC.
* ------------------------------------------
*
      ESC = CHAR(27)
*     ESC = "_"          ;*  for showing PCL strings on printer only
* 
      PCLIN = ESC:"%-12345X@PJL ENTER LANGUAGE = PCL"   ;* len = 34
      PCLOUT = ESC:"%-12345X"
*
      LJRESET = ESC:"E"                                 ;* len = 2
*
      PORTRAIT = ESC:"&l0O"
      LPI6 = ESC:"&l6D"             ;*  6 LPI
      LPP66 = ESC:"&l7.27C"         ;*  Vertical Motion in 1-48 inches
      PORTLPP66 = ESC:"&l0o7.27C"   ;*  len = 10
*
      FIXEDSPACE = ESC:"(s0P"
      UPRIGHT=ESC:"(s0S"
      FIXEDUPRIGHT = ESC:"(s0p0S"   ;*  len = 7
*
      LEFTMARG  = ESC:"&a8L"        ;*  len = 5   10-09-98
*
      BCCOL = ESC:"&a69C"        ;* 6  moves cursor to col 69 on same line
*                                     col size based on HMI below
*
      HMI=ESC:"&k10H"               ;*  Horiz Motion Increment in 1-120 inches
*                                       10-120 = 12cpi
      LINETERM=ESC:"&k3G"           ;*  3= Treat LF or CR as  CR-LF
      HMILINETERM=ESC:"&k10h3G"     ;*  len = 8
*
      SETUP = LJRESET:PORTLPP66:FIXEDUPRIGHT:HMILINETERM  ;* len= 2+10+7+8=27
*
      PC8 = ESC:"(10U"              ;*  PC-8 symbol set   len = 5
      PITCH10 = ESC:"(s10H"         ;*  10 pitch = 12cpi
      PITCH12 = ESC:"(s12H"         ;*  12 pitch = 10cpi

      BOLDON=ESC:"(s3B"
      BOLDOFF=ESC:"(s0B"
      COURIER=ESC:"(s4099T"         ;*  Courier typeface
      TENPOINT=ESC:"(s10V"          ;*  10 point
      COUR12PTCH=ESC:"(s12h10v4099T"  ;*  len = 14

      NORMAL=PC8:COUR12PTCH         ;*  len = 5 + 14 = 19
*
*-      P12=PC8:FIXEDSPACE:PITCH12:BOLDOFF:COURIER
*      
      BARCODEON=BCCOL:ESC:"(0Y":ESC:"(s12.00v4.06h0T"  ;* len =  20+6=26
      CR = CHAR(13)
*
      HP_PCL = "???"
*  determine what function to perform & return appropriate value
      BEGIN CASE
        CASE LJFUNCTION = "INIT"
          HP_PCL = PCLIN:SETUP:LEFTMARG:NORMAL  ;* len =  34+27+5+19=85  10-09-98
        CASE LJFUNCTION = "BCJOB"
          HP_PCL = BARCODEON:"*":JOB_NBR:"*" ;* len = 26+10=36
        CASE LJFUNCTION = "BCOFF"
          HP_PCL = NORMAL                    ;* len = 19
        CASE LJFUNCTION = "LINE"
          HP_PCL = STR(CHAR(196),80)
        CASE LJFUNCTION = "RESET"
          HP_PCL = RESET:PCL_OUT
      END CASE
      IF HP_PCL = "???" THEN
        ERRMSG = "INVALID PASSED VALUE IN LJFUNCTION [":LJFUNCTION:"]"
      END
99999*
      RETURN
      END
