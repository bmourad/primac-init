      SUBROUTINE DATA_ID_M(TABLE_ID,PFX)
*COPY>CPYLIB>COM1
*COPY>JIS.CPYLIB>COM_DATA_STRUC_M
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 03/22/94
* System        - PRIMAC
* Library       - JISBP/DATA_ID_M
* Author        - Ziad Yamout, VERCOM Software, Inc.
*T21177 diane 02/10/1997 * REV11 UPG
*********************************************************************
*
*---- Data Structure Libraries
*
* *COPY>PMC.CPYLIB>MENUS.CONTROL
*COPY>JIS.CPYLIB>SYS_FILES
*COPY>JIS.CPYLIB>PFX_FILES
*COPY>JIS.CPYLIB>SYS_FIELDS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- Setup variables
*
      ESN = ECD.SCRN.NO
*
      LINE.SPACE = 1
      BEGIN.PAGE = 7; PAGE.SIZE = 13
      LN = 1; LINES = DCOUNT(SFR.ID,VM)
*
*---- Display screen header & data
*
      ECD.ACTION = 2; CALL SCRN.EDIT
*
      SCV.REC(1)<ESN> = TABLE_ID
      GOSUB 8000
      SCV.REC(49)<ESN> = INT(LINES/PAGE.SIZE+.9) "R%2"
      GOSUB 10000
*
      IF LINES < 1 THEN
         OPTION = "A"
         LOOP
            LN = LINES + 1
            OLD.LINES = LINES
            GOSUB 7000
         WHILE LINES > OLD.LINES DO
            GOSUB 7950
         REPEAT
         LN = LINES
         GOSUB 7900; GOSUB 7955
      END
*
*---- Prompt line
*
      MORE = 1
      LOOP
         ECD.NUM = 51; SCV.REC(ECD.NUM)<ESN> = ""
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
            CASE OPTION = "E" OR OPTION = "END"
               MORE = 0
            CASE OPTION = "A"
               LOOP
                  LN = LINES + 1
                  OLD.LINES = LINES
                  GOSUB 7000
               WHILE LINES > OLD.LINES DO
                  GOSUB 7950
               REPEAT
               LN = LINES; GOSUB 7900; GOSUB 7955
            CASE OPTION = "C"
               GOSUB 7800
               IF LN.NO THEN
                  LN = LN.NO
                  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
                  GOSUB 7010
               END
            CASE OPTION = "D"
               GOSUB 7800
               IF LN.NO THEN
                  LN = LN.NO
                  SFR.ID = DELETE(SFR.ID,1,LN,0)
                  SFR.ID.TYPE = DELETE(SFR.ID.TYPE,1,LN,0)
                  LINES = DCOUNT(SFR.ID,VM)
                  IF LN > LINES THEN LN = LN - 1
                  GOSUB 8000
                  ECD.ACTION = 5; CALL SCRN.EDIT
                  OLD.START.LINE = 0; GOSUB 7900; GOSUB 7955
               END
            CASE OPTION = "S" OR OPTION = "SF"
               LN = 1+INT((LN-1)/PAGE.SIZE)*PAGE.SIZE+PAGE.SIZE
               IF LN > LINES THEN LN = 1
               GOSUB 7900
            CASE OPTION = "SR"
               LN = 1+INT((LN-1)/PAGE.SIZE)*PAGE.SIZE-PAGE.SIZE
               IF LN < 1 THEN LN = 1
               GOSUB 7900
            CASE OPTION = "ST"
               LN = 1
               GOSUB 7900
            CASE OPTION = "SB"
               LN = LINES
               GOSUB 7900
            CASE OPTION[1,1] = "S" AND NUM(OPTION[2,99])
               LN.NO = OPTION[2,99] + 0
               IF LN.NO < 1 OR LN.NO > LINES THEN
                  ERRMSG = "** Invalid selection **"
                  GOSUB 91000
               END ELSE
                  LN = LN.NO
                  GOSUB 7900
               END
            CASE NOT(NUM(OPTION))
               ERRMSG = "Invalid entry, please re-enter"
               GOSUB 91000
            CASE OPTION < 1 OR OPTION > LINES
               ERRMSG = "Out of range, please re-enter"
               GOSUB 91000
            CASE SFR.ID.TYPE<1,OPTION> # "D" AND SFR.ID.TYPE<1,OPTION> # "I"
               ERRMSG = "Cannot define a type (":SFR.ID.TYPE:") field"
               GOSUB 91000
            CASE 1
               ECD.SCRN.NO = ESN + 1
               FLD_ID = SFR.ID<1,OPTION>
               FLD_TYP = SFR.ID.TYPE<1,OPTION>
               ID_PFX = ""
               CALL DATA_FIELD_M(TABLE_ID,ID_PFX,FLD_ID,FLD_TYP)
               ECD.SCRN.NO = ESN
               ECD.ACTION = 2; CALL SCRN.EDIT
               ECD.ACTION = 3; CALL SCRN.EDIT
               OLD.START.LINE = 0; GOSUB 7900
            CASE 1
               ERRMSG = "Invalid entry, please re-enter"
               GOSUB 91000
         END CASE
      WHILE MORE DO REPEAT
      GOTO 99999
*
*---- Scrolling line prompt
7000*
      GOSUB 7900
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
      P_X = 1 ; P_Y = SLN ; P_VALUE = LN "R%2" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
*---- Field type
7010*
      X = 5; Y = SLN; TYP = 1; MAXL = 1; O.R = "O"
      HMSG = "Enter the field type, This file (I)d, (D)ata field, or (T)ext"
      VALDAT = "I":VM:"D":VM:"T"
      IF SFR.ID.TYPE<1,LN> # "" THEN
         DEFAULT = SFR.ID.TYPE<1,LN>
      END
      CALL EDIT.SUB
      IF VALUE = "" OR VALUE = "END" THEN
         IF OPTION = "A" THEN
            P_X = 1 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            SFR.ID.TYPE = DELETE(SFR.ID.TYPE,1,LN,0)
            SFR.ID = DELETE(SFR.ID,1,LN,0)
         END ELSE
            N = LN; GOSUB 7910
         END
         GOTO 7099
      END
      ID_TYPE = VALUE
      IF ID_TYPE = "I" THEN
         LOCATE ID_TYPE IN SFR.ID.TYPE<1>,1 SETTING FND THEN
            IF FND <> LN THEN
               ERRMSG = "The file ID field is already setup on line # ":FND
               GOSUB 91000; GOTO 7010
            END
         END
      END
      SFR.ID.TYPE<1,LN> = ID_TYPE
*
*---- Field name
7020*
      X = 8; Y = SLN; TYP = 1; MAXL = 25; O.R = "O"
      IF SFR.ID.TYPE<1,LN> = "I" THEN
         VALUE = PFX
         VFMT = "L#":MAXL
         P_X = X ; P_Y = Y ; P_VALUE = VALUE VFMT ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         GOTO 7025
      END
      HMSG = "Enter the Field name"
      IF SFR.ID<1,LN> # "" THEN
         DEFAULT = SFR.ID<1,LN>
      END
      CALL EDIT.SUB
      IF VALUE = "" OR VALUE = "???" THEN
         MAT GEN.XREF.REC = ""
         GXR.NAME = "PFX_FILES"
         GXR.FILE = PFX_FILES
         CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
         ECD.ACTION = 2; CALL SCRN.EDIT
         GOSUB 10000
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
         P_X = 1 ; P_Y = SLN ; P_VALUE = LN "R%2" ; P_OPT = "CL"
         P_X := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:"" SFR.ID.TYPE<1,LN> "L#1"
         P_X := AM:8 ; P_Y := AM:SLN ; P_VALUE := AM:GXR.ID "L#25"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         IF GXR.ID = "" THEN GOTO 7020
         VALUE = GXR.ID
      END
7025*
      ID_NAME = CONVERT(".","_",VALUE)
      BEGIN CASE
         CASE ID_NAME = "END"
            N = LN; GOSUB 7910
            GOTO 7010
         CASE SFR.ID.TYPE<1,LN> = "T"
            MAT PFR.REC = ""
         CASE 1
            MATREAD PFR.REC FROM PFX_FILES, ID_NAME THEN
               IF SFR.ID.TYPE<1,LN> = "I" AND PFR_FILE # TABLE_ID THEN
                  ERRMSG = "Field (":ID_NAME:") is already setup as a ID/KEY for file - ":PFR_FILE
                  GOSUB 91000; GOTO 7010
               END
            END ELSE
               IF SFR.ID.TYPE<1,LN> # "I" THEN
                  ERRMSG = "Field (":ID_NAME:") is not setup as a file ID/KEY"
                  GOSUB 91000; GOTO 7020
               END
               MAT PFR.REC = ""
            END
      END CASE
      SFR.ID<1,LN> = ID_NAME
      GOSUB 7911
      LINES = DCOUNT(SFR.ID,VM)
      GOSUB 8000
      ECD.ACTION = 5; CALL SCRN.EDIT
7099  RETURN
*
*---- Line # to change or delete
7800*
      GOSUB 7900
      ECD.NUM = 52; SCV.REC(ECD.NUM)<ESN> = ""
      ECD.MINV = OLD.START.LINE; ECD.MAXV = LAST.LINE
      ECD.ACTION = 4; CALL SCRN.EDIT
      LN.NO = ECD.RET.VALUE
      IF LN.NO = "" OR LN.NO = "END" THEN
         LN.NO = 0
      END
      RETURN
*
*---- Display scrolling lines
7900*
      START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
      LAST.LINE = START.LINE + PAGE.SIZE - 1
      IF LAST.LINE > LINES THEN LAST.LINE = LINES
      IF START.LINE = OLD.START.LINE THEN GOTO 7090
      OLD.START.LINE = START.LINE
      ECD.NUM = 48
      SCV.REC(ECD.NUM)<ESN> = INT(LAST.LINE/PAGE.SIZE+.9) "R%2"
      ECD.ACTION = 5; CALL SCRN.EDIT
      CNT = 1
      FOR N = START.LINE TO LAST.LINE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
         GOSUB 7910
         CNT = CNT + 1
      NEXT N
      FOR M = CNT TO PAGE.SIZE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
         P_X = 1 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT M
7090  RETURN
7910*
      ID_NAME = SFR.ID<1,N>
      ID_TYPE = SFR.ID.TYPE<1,N>
      P_X = 1 ; P_Y = SLN ; P_VALUE = N "R%2" ; P_OPT = "CL"
      P_X := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:ID_TYPE "L#1"
      P_X := AM:8 ; P_Y := AM:SLN ; P_VALUE := AM:ID_NAME "L#25"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
7911*
      IF ID_TYPE # "D" AND ID_TYPE # "I" THEN
         MAT DFD.REC = ""
      END ELSE
         MATREAD DFD.REC FROM SYS_FIELDS, ID_NAME ELSE
ERRMSG = 'CANNOT READ SYS_FIELDS ':ID_NAME ; GOSUB 91000
            MAT DFD.REC = ""
         END
      END
      BEGIN CASE
         CASE DFD_TYPE = "T"
            DESC = "Text"
         CASE DFD_TYPE = "C"
            DESC = "deCimal"
         CASE DFD_TYPE = "N"
            DESC = "Numeric"
         CASE DFD_TYPE = "A"
            DESC = "Alpha"
         CASE DFD_TYPE = "D"
            DESC = "Date"
         CASE DFD_TYPE = "P"
            DESC = "Pattern"
         CASE DFD_TYPE = "L"
            DESC = "Logical"
         CASE DFD_TYPE = "U"
            DESC = "Counter"
         CASE 1
            DESC = DFD_TYPE
      END CASE
      P_X = 34 ; P_Y = SLN ; P_VALUE = DESC "L#12" ; P_OPT = ""
      P_X := AM:47 ; P_Y := AM:SLN ; P_VALUE := AM:DFD_LEN "R#3"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      RETURN
*
7950*
      ECD.NUM = 48
      SCV.REC(ECD.NUM)<ESN> = INT(LN/PAGE.SIZE+.9) "R%2"
      ECD.ACTION = 5; CALL SCRN.EDIT
7955*
      ECD.NUM = 49
      SCV.REC(ECD.NUM)<ESN> = INT(LINES/PAGE.SIZE+.9) "R%2"
      ECD.ACTION = 5; CALL SCRN.EDIT
      RETURN
*
*---- Build variables
8000*
      IF SFR.ID.TYPE<1,1> = "T" THEN
         ID_DESC = '"':SFR.ID<1,1>:'"'
      END ELSE
         ID_DESC = SFR.ID<1,1>
      END
      FOR I = 2 TO LINES
         IF SFR.ID.TYPE<1,I> = "T" THEN
            ID_DESC = ID_DESC:':"':SFR.ID<1,I>:'"'
         END ELSE
            ID_DESC = ID_DESC:":":SFR.ID<1,I>
         END
      NEXT I
      ECD.NUM = 5; SCV.REC(ECD.NUM)<ESN> = ID_DESC
      RETURN
*
*---- Display screen
10000*
      ECD.ACTION = 3; CALL SCRN.EDIT
      OLD.START.LINE = 0; GOSUB 7900
      RETURN
*
*---- Error routines
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
*       CRT @(0,23):ERRMSG:CL:
*       INPUT REPLY,1_:
*       CRT @(0,23):CL:
*       RETURN
99999*
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      RETURN
   END
