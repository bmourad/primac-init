      SUBROUTINE JKT_SCHDATES(CONO,ERRMSG,I_FNAME,I_FPTR,I_VNAME,I_VREC,I_SNAME,I_SREC,SCH_DATES)
*COPY>CPYLIB>COM1
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 06/13/95
* System        - PRIMAC
* Library       - UTLBP/JKT_SCHDATES
* Author        - Ziad Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>PSS.CPYLIB>JOB.SCHED
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
      FNAME = "JOB.SCHED"
*
      FCNT = 1
      DIM FPTR(FCNT)
      EQU JBS_FILE  TO FPTR(1)
*
*      VNAME = "AETNA_JOB_NO"
      VNAME = "VSI_JOB_NO"
*
      VCNT = 1
      DIM VREC(VCNT)
      EQU JOBNO     TO VREC(1)
*
      SNAME = "SELECTION"
      SNAME<2> = "DEPT"
      SNAME<3> = "CCTR"
*
      SCNT = 3
      DIM SREC(SCNT)
      EQU SELNO     TO SREC(1)
      EQU DP_SEL    TO SREC(2)
      EQU CC_SEL    TO SREC(3)
*
      ONAME = "JBS_SCH_DATE"
      ONAME<2> = "JBS_CCTR"
*
      ERRMSG = ""
      SCH_DATES = ""
101*
      FOR I = 1 TO FCNT
         LOCATE FNAME<I> IN I_FNAME,1 SETTING FND THEN
            FPTR(I) = I_FPTR<FND>
         END ELSE
            ERRMSG = "File (":FNAME<I>:") is required for this Subroutine"
            FCNT = 0
         END
      NEXT I
      IF FCNT = 0 THEN GOTO 99999
201*
      FOR I = 1 TO VCNT
         LOCATE VNAME<I> IN I_VNAME,1 SETTING FND THEN
            VREC(I) = I_VREC<FND>
         END ELSE
            ERRMSG = "Data Field (":VNAME<I>:") is required for this Subroutine"
            VCNT = 0
         END
      NEXT I
      IF VCNT = 0 THEN GOTO 99999
301*
      MAT SREC = ""
      NO_SEL = 1
      FOR I = 1 TO SCNT
         PTR = 1
         LOOP
            LOCATE SNAME<I> IN I_SNAME,PTR SETTING FND THEN
               SREC(I)<-1> = I_SREC<FND>
            END ELSE
               PTR = 0
            END
         WHILE PTR DO
            PTR = FND + 1
         REPEAT
         IF SREC(I) = "" THEN
            SREC(I) = "ALL"
         END ELSE
            NO_SEL = 0
         END
      NEXT I
      IF NO_SEL THEN
         ERRMSG = "No selection criteria was specified"
         GOTO 99999
      END
      IF SELNO = "ALL" OR SELNO = "" THEN
         SELNO = 1
      END ELSE
         LOCATE SELNO IN ONAME,1 SETTING SELNO ELSE
            SELNO = 1
         END
      END
1000*
      MATREAD JBS.REC FROM FILE.VARS(JBS_FILE), CONO:JOBNO ELSE
         MAT JBS.REC = ""
      END
      DCNT = DCOUNT(DP_SEL,AM)
      BEGIN CASE
      CASE DP_SEL = "ALL" AND CC_SEL = "ALL"
      CASE CC_SEL = "ALL"
         FOR I = 1 TO DCNT
            DNO = DP_SEL<I>
            PTR = 1
            LOOP
               LOCATE DNO IN JBS.DEPT<1>,PTR SETTING LOC THEN
                  ON SELNO GOSUB 3100,3200
               END ELSE
                  LOC = 0
               END
            WHILE LOC DO
               PTR = LOC + 1
            REPEAT
         NEXT I
      CASE 1
         FOR I = 1 TO DCNT
            DNO = DP_SEL<I>
            PTR = 1
            LOOP
               LOCATE DNO IN JBS.DEPT<1>,PTR SETTING LOC THEN
                  LOCATE JBS.CCTR<1,LOC> IN CC_SEL,1 SETTING CLOC THEN
                     ON SELNO GOSUB 4100,4200
                  END
               END ELSE
                  LOC = 0
               END
            WHILE LOC DO
               PTR = LOC + 1
            REPEAT
         NEXT I
      END CASE
      GOTO 99999
3100*
      IF JBS.SCH.DATE<1,LOC> # "" THEN
         LOCATE JBS.SCH.DATE<1,LOC> IN SCH_DATES<1>,1 BY "AR" SETTING SLOC ELSE NULL
         INS JBS.SCH.DATE<1,LOC> BEFORE SCH_DATES<1,SLOC>
      END
      RETURN
3200*
      IF JBS.SCH.DATE<1,LOC> # "" THEN
         LOCATE JBS.CCTR<1,LOC> IN SCH_DATES<1>,1 BY "AR" SETTING SLOC ELSE NULL
         INS JBS.CCTR<1,LOC> BEFORE SCH_DATES<1,SLOC>
      END
      RETURN
*
4100*
      IF JBS.SCH.DATE<1,LOC> # "" THEN
         LOCATE JBS.SCH.DATE<1,LOC> IN SCH_DATES<1>,1 BY "AR" SETTING SLOC ELSE
            INS JBS.SCH.DATE<1,LOC> BEFORE SCH_DATES<1,SLOC>
         END
      END
      RETURN
4200*
      IF JBS.SCH.DATE<1,LOC> # "" THEN
         LOCATE JBS.CCTR<1,LOC> IN SCH_DATES<1>,1 BY "AR" SETTING SLOC ELSE
            INS JBS.CCTR<1,LOC> BEFORE SCH_DATES<1,SLOC>
         END
      END
      RETURN
*
99999*
*CRT @(0,23):@(-4):SCH_DATES:;INPUT XOXO:
      RETURN
      END
