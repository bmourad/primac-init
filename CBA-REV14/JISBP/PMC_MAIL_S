      SUBROUTINE PMC_MAIL_S(CONO,MAIL.F.ID,MAIL.LIST,SECURITY,USER.MAIL,M_MSG,ERRMSG)
********************************************************************
* REVISION    - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - JISBP
* PROGRAM     - PMC_MAIL_S
* AUTHOR      - Ziad Yamout, VERCOM Software Inc.
* DATE        - 10/27/94
* DESCRIPTION
* This subroutine is utilized to distribute prefixed messages, from
* within PRIMAC application.
********************************************************************
*
*---- Data structure libraries
*
*COPY>PMC.CPYLIB>SECURITY
*COPY>PMC.CPYLIB>USER.MAIL
*COPY>CPYLIB>CHAR
*
*---- Validate the USER & MAIL record
*
      ERRMSG = ""
      LCNT = DCOUNT(MAIL.LIST,AM)
      IF LCNT < 1 THEN
         ERRMSG = "Cannot locate any users !!"
         GOTO 99999
      END
      MAIL.ID = CONO:MAIL.LIST<1>
      MATREAD SEC.REC FROM SECURITY, MAIL.ID ELSE
         ERRMSG = "Cannot locate user # ":MAIL.ID[4,99]
         GOTO 99999
      END
*
*---- Initialization
*
      MDATE = DATE()
      MTIME = TIME()
      IF MTIME < 30 THEN MDATE = DATE() + 1
*
      MCNT = INDEX(M_MSG<3>,VM,1)
      VCNT = INDEX(M_MSG<3>,SVM,1)
      IF MCNT OR VCNT THEN
         T_MSG = "Field ":M_MSG<1>:" Has been revised from:"
         PTR = 1
         MCNT = DCOUNT(M_MSG<2>,VM)
         FOR MNO = 1 TO MCNT
            VCNT = DCOUNT(M_MSG<2,MNO>,SVM)
            FOR VNO = 1 TO VCNT
               PTR = PTR + 1
               T_MSG<PTR> = M_MSG<2,MNO,VNO>
            NEXT VNO
         NEXT MNO
         PTR = PTR + 1
         T_MSG<PTR> = "          >>>>> to: <<<<<"
         MCNT = DCOUNT(M_MSG<3>,VM)
         FOR MNO = 1 TO MCNT
            VCNT = DCOUNT(M_MSG<3,MNO>,SVM)
            FOR VNO = 1 TO VCNT
               PTR = PTR + 1
               T_MSG<PTR> = M_MSG<3,MNO,VNO>
            NEXT VNO
         NEXT MNO
      END ELSE
         T_MSG = "Field ":M_MSG<1>:" has been revised from (":M_MSG<2>:") to (":M_MSG<3>:")"
      END
      ML = 65
      PTR = 0; U_MSG = ""
      CNT = DCOUNT(T_MSG,AM)
      FOR T = 1 TO CNT
         MSG = T_MSG<T>
         LOOP
            L = LEN(MSG)
         WHILE L > ML DO
            PL = ML
            LOOP WHILE PL AND MSG[PL,1] # " " DO
               PL = PL - 1
            REPEAT
            IF PL THEN
               PTR = PTR + 1
               U_MSG<PTR> = MSG[1,PL-1]
               MSG = MSG[PL+1,L]
            END ELSE
               PL = ML - 2
               PTR = PTR + 1
               U_MSG<PTR> = MSG[1,PL]:"--"
               MSG = "--":MSG[PL+1,L]
            END
         REPEAT
         IF L > 0 THEN
            PTR = PTR + 1
            U_MSG<PTR> = MSG
         END
      NEXT T
*
*---- Process users
*
      GOSUB 1000
      FOR LNO = 2 TO LCNT
         MAIL.ID = CONO:MAIL.LIST<LNO>
         MATREAD SEC.REC FROM SECURITY, MAIL.ID THEN
            GOSUB 1000
         END
      NEXT LNO
      GOTO 99999
*
*---- Update User mail
1000*
      MATREADU UMDR.REC FROM USER.MAIL, MAIL.ID ELSE
         MAT UMDR.REC = ""
         UMDR.DET = 0
         UMDR.SRC = MAIL.ID
         UMDR.TYPE = 0
         UMDR.DATE = MDATE
         UMDR.TIME = MTIME
      END
      IF UMDR.DET<1,1> # "0" THEN
         UMDR.DET  = INSERT(UMDR.DET,1,1,0,"0")
         UMDR.SRC  = INSERT(UMDR.SRC,1,1,0,MAIL.ID)
         UMDR.TYPE = INSERT(UMDR.TYPE,1,1,0,"0")
         UMDR.DATE = INSERT(UMDR.DATE,1,1,0,MDATE)
         UMDR.TIME = INSERT(UMDR.TIME,1,1,0,MTIME)
         UMDR.LIST = INSERT(UMDR.LIST,1,1,0,"")
      END
      UMDR.SEQ = UMDR.SEQ + 1
      IF UMDR.SEQ > 9999 THEN UMDR.SEQ = 1
      MP = DCOUNT(UMDR.DET,VM) + 1
      UMDR.DET<1,MP>  = UMDR.SEQ
      UMDR.SRC<1,MP>  = CONO:MAIL.F.ID
      UMDR.TYPE<1,MP> = 1
      UMDR.LIST<1,MP> = "_":UMDR.SEQ
      UMDR.DATE<1,MP> = MDATE
      UMDR.TIME<1,MP> = MTIME
      MATWRITE UMDR.REC ON USER.MAIL, MAIL.ID
      WRITE U_MSG ON USER.MAIL, MAIL.ID:"!":UMDR.SEQ
      WRITE MAIL.ID ON USER.MAIL, "L!":CONO:MAIL.F.ID:"!_":UMDR.SEQ
      RETURN
*
*---- End of job
99999*
      RETURN
END
