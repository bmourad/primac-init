      SUBROUTINE JKT_INKDSC(CONO,ERRMSG,I_FNAME,I_FPTR,I_VNAME,I_VREC,I_SNAME,I_SREC,INK_DESC)
*COPY>CPYLIB>COM1
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 06/13/95
* System        - PRIMAC
* Library       - JISBP/JKT_INKDSC
* Author        - Ziad Yamout, VERCOM Software, Inc.
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
      FCNT = 1
      DIM FPTR(FCNT)
      EQU INK_FILE  TO FPTR(1)
*
      ICNT = 1
      DIM VREC(ICNT)
      EQU INK_ID TO VREC(1)
*
      ERRMSG = ""
101*
      FNAME = "ESTIMATE.MATL"
*
      FOR I = 1 TO FCNT
         LOCATE FNAME<I> IN I_FNAME,1 SETTING FND THEN
            FPTR(I) = I_FPTR<FND>
         END ELSE
            ERRMSG = "File (":FNAME<I>:") is required for this Subroutine"
            FCNT = 0
         END
      NEXT I
      IF FCNT = 0 THEN GOTO 99999
201*
      VNAME = "EST_PROD_INK_ID"
      FOR I = 1 TO ICNT
         LOCATE VNAME<I> IN I_VNAME,1 SETTING FND THEN
            VREC(I) = I_VREC<FND>
         END ELSE
            ERRMSG = "Data Field (":VNAME<I>:") is required for this Subroutine"
            ICNT = 0
         END
      NEXT I
      IF ICNT = 0 THEN GOTO 99999
1000*
      MATREAD ESTM.REC FROM FILE.VARS(INK_FILE), CONO:"I" ELSE
         ERRMSG = "Cannot locate the Estimating Ink description data"
         GOTO 99999
      END
      INK_DESC = ""
      MCNT = DCOUNT(INK_ID,VM)
      FOR M = 1 TO MCNT
         SCNT = DCOUNT(INK_ID<1,M>,SVM)
         FOR S = 1 TO SCNT
            LCNT = DCOUNT(INK_ID<1,M,S>,"!")
            FOR L = 1 TO LCNT
               ID = FIELD(INK_ID<1,M,S>,"!",L)
               LOCATE ID IN ESTM.PROD<1>,1 SETTING IFND THEN
                  DSC = ESTM.DESC<1,IFND>
               END ELSE
                  DSC = ID
               END
               IF INK_DESC<1,M,S> = "" THEN
                  INK_DESC<1,M,S> = INK_DESC<1,M,S> : DSC
               END ELSE
                  INK_DESC<1,M,S> = INK_DESC<1,M,S> : "!" : DSC
               END
            NEXT L
         NEXT S
      NEXT M
*
99999*
      RETURN
      END
