      SUBROUTINE SCN_CRT_BLD(SCN_NAME,MAT SCN_HDR_CRT,MAT SCN_CLR_CRT,ERRMSG)
*COPY>CPYLIB>COM1
*COPY>CPYLIB>TCC
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>NEW.BUILD.SCREEN
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
      MAX_SNO = 2; SCN_X = 79
      DIM SCN_HDR_CRT(MAX_SNO)
      DIM SCN_CLR_CRT(MAX_SNO)
      X_ADJ = 0; X_ADJ<2> = 80
      ERRMSG = ""
      READ SCN_HDR_DEF FROM M.SCREENS, SCN_NAME:"_DEF" ELSE
         ERRMSG = "Cannot access Screen Header Definition (":SCN_NAME:")"
         GOTO 99999
      END
      READ SCN_CLR_DEF FROM M.SCREENS, SCN_NAME:"_CLR" ELSE
         ERRMSG = "Cannot access Screen Clear Definition (":SCN_NAME:")"
         GOTO 99999
      END
*
      DPTR = DCOUNT(SCN_HDR_DEF,AM)
      BEGIN CASE
      CASE PC.PORT.TYPE = "termulator"
***************************************
*     CASE PC.PORT.TYPE = "termIte"
*        MAT SCN_HDR_CRT = ""
*        MAT SCN_CLR_CRT = ""
*        DEPTH = 1
*        FONTS = "Helv10,Right"
*        FOR DEF = 1 TO DPTR
*           CTL_ID = "LAB":DEF
*           X = SCN_HDR_DEF<DEF,2>
*           Y = SCN_HDR_DEF<DEF,1>
*           STMT = SCN_HDR_DEF<DEF,3>
*           L = LEN(STMT)
*           IF X > SCN_X THEN
*              S_NO = 2
*           END ELSE
*              S_NO = 1
*           END
*           X = X - X_ADJ<S_NO>
*           GUI_CONTROL = CTL_ID:",LABEL,":X:",":Y:",":L:",":DEPTH:",":STMT:",":FONTS
*           SCN_HDR_CRT(S_NO)<-1> = GUI_CONTROL
*        NEXT DEF
*        GOTO 99999
***************************************
         CASE GRAPH.PORT.TYPE = "STD" OR HORZ.CHAR = "-" OR VERT.CHAR = "|"
            MAT SCN_HDR_CRT = CS
            FOR DEF = 1 TO DPTR
               Y = SCN_HDR_DEF<DEF,1> + 1
               X = SCN_HDR_DEF<DEF,2>
               STMT = SCN_HDR_DEF<DEF,3>
               L = LEN(STMT)
               IF X > SCN_X THEN
                  S_NO = 2
               END ELSE
                  S_NO = 1
               END
               X = X - X_ADJ<S_NO>
               SCN_HDR_CRT(S_NO) = SCN_HDR_CRT(S_NO):@(X,Y-1):STMT
            NEXT DEF
            GOTO 500
      END CASE
200*
      MAT SCN_HDR_CRT = CS
      FOR DEF = 1 TO DPTR
         Y = SCN_HDR_DEF<DEF,1> + 1
         X = SCN_HDR_DEF<DEF,2>
         STMT = SCN_HDR_DEF<DEF,3>
         L = LEN(STMT)
         IF X > SCN_X THEN
            S_NO = 2
         END ELSE
            S_NO = 1
         END
         POS = INDEX(STMT,"--",1)
         IF POS THEN
            STMT.LEN = LEN(STMT)
            IF STMT = STR("-",STMT.LEN) THEN
               STMT = BOX.ON:STR(HORZ.CHAR,STMT.LEN):BOX.OFF
            END ELSE
               OSTMT = STMT
               IF POS > 1 THEN
                  STMT = OSTMT[1,POS-1]
               END ELSE
                  STMT = ""
               END
               BLEN = 2; POS = POS + 2
               FOR I = POS TO STMT.LEN
                  CHR = OSTMT[I,1]
                  BEGIN CASE
                     CASE CHR = "-"
                        BLEN = BLEN + 1
                     CASE BLEN
                        STMT = STMT:BOX.ON:STR(HORZ.CHAR,BLEN):BOX.OFF:CHR
                        BLEN = 0
                     CASE 1
                        STMT = STMT:CHR
                  END CASE
               NEXT I
               OSTMT = ""
               IF BLEN THEN
                  STMT = STMT:BOX.ON:STR(HORZ.CHAR,BLEN):BOX.OFF
               END
            END
         END
         IF VERT.CHAR # "|" THEN
         POS = INDEX(STMT,"|",1)
         IF POS THEN
            IF STMT = "|" THEN
               STMT = BOX.ON:VERT.CHAR:BOX.OFF
            END ELSE
               LOOP
                  STMT = STMT[1,POS-1]:BOX.ON:VERT.CHAR:BOX.OFF:STMT[POS+1,999]
                  POS = INDEX(STMT,"|",1)
               WHILE POS DO REPEAT
            END
         END
         END
         SCN_HDR_CRT(S_NO) = SCN_HDR_CRT(S_NO):@(X-X_ADJ<S_NO>,Y-1):STMT
      NEXT DEF
500*
      DCNT = DCOUNT(SCN_CLR_DEF,AM)
      IF DCNT < 1 THEN
         MAT SCN_CLR_CRT = MAT SCN_HDR_CRT
         GOTO 99999
      END
      MAT SCN_CLR_CRT = ""
      FOR ROW = 1 TO DCNT
         Y = SCN_CLR_DEF<ROW,1>
         X = SCN_CLR_DEF<ROW,2>
         ML = SCN_CLR_DEF<ROW,3>
         IF X > SCN_X THEN
            S_NO = 2
         END ELSE
            S_NO = 1
         END
         X = X - X_ADJ<S_NO>
         IF NUM(ML) THEN
            SCN_CLR_CRT(S_NO) = SCN_CLR_CRT(S_NO):@(X,Y):SPACE(ML)
         END ELSE
            SCN_CLR_CRT(S_NO) = SCN_CLR_CRT(S_NO):@(X,Y):CL
         END
      NEXT ROW
99999*
      RETURN
   END
