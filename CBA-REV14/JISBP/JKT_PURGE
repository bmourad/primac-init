*COPY>CPYLIB>COM1
************************************************************************
* REVISION     - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM       - PRIMAC
* SOURCE       - DEVBP
* PROGRAM      - JKT_PURGE
* BY           - BILAL MOURAD
* DATE         -
* DESCRIPTION  -
*T21177 diane 02/10/1997 * REV11 UPG
************************************************************************
*
*---- Data Structure Libraries
*
*COPY>JIS.CPYLIB>JKT_NOTIFY
*COPY>JCS.CPYLIB>JOB_TKT
*COPY JIS.CPYLIB>JOB_TKT_TAG
*COPY>CPYLIB>EDIT.COM
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*---- SET UP SYSCOM
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- Initialization
*
      MAT SCV.REC = ""
*
*---- Open Files
*
      OPEN "", "COMPANY" TO COMPANY ELSE
         ERRMSG = "COMPANY FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
      OPEN "", "CONTROL" TO CONTROL ELSE
         ERRMSG = "CONTROL FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
      OPEN "", "JIS.SCREENS" TO M.SCREENS ELSE
         ERRMSG = "JIS.SCREENS FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
      OPEN "", "JOB_TKT" TO JOB_TKT ELSE
         ERRMSG = "JOB_TKT FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
      OPEN "", "JOB_JKT_IMG" TO JOB_JKT_IMG ELSE
         ERRMSG = "JOB_JKT_IMG FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
      OPEN "", "JOB_TKT_TAG" TO JOB_TKT_TAG ELSE
         ERRMSG = "JOB_TKT_TAG FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
      OPEN "", "JKT_NOTIFY" TO JKT_NOTIFY ELSE
         ERRMSG = "JKT_NOTIFY FILE IS MISSING"
         GOSUB 91000; GOTO 99999
      END
*
*---- Get Company Number
*
      CONO = ""
      CALL GET.CONO(CONO,MAT COMP.REC)
      IF CONO = "END" THEN GOTO 99999
      P_X = 1 ; P_Y = 23 ; P_VALUE = "CONO = ":CONO ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
*---- Paint the screen
*
      ECD.SCRN.CNT = 1
      ECD.SCRN.NAME = "JKT_PURGE"
      ECD.ACTION = 1; CALL SCRN.EDIT
      ECD.SCRN.NO = 1; ECD.ACTION = 2; CALL SCRN.EDIT
*
*---- Write the DATE
*
      ECD.NUM = 1
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = OCONV(DATE(),"D2/")
      ECD.ACTION = 5; CALL SCRN.EDIT
*
*---- Check JOB_TKT_TAG
      SELECT JOB_TKT_TAG
50*
      READNEXT JTT_ID THEN
         IF JTT_ID[1,3] = CONO THEN
            CLEARSELECT
         END ELSE
            GOTO 50
         END
      END ELSE
         ERRMSG = "Could not locate any selected Tickets for the purge process"
         GOSUB 91000
         GOTO 99990
      END
*
*---- Check for Printed Report; End otherwise
*
      ECD.NUM = 22; ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "N" THEN 
         GOTO 99990
      END
*
*---- Confirm The Purge Job
*
      ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE = "N" THEN GOTO 99990
*
*---- Select JKT_NOTIFY File and Purge
*
      SELECT JKT_NOTIFY
      MORE = 1
      P_X = 15 ; P_Y = 4 ; P_VALUE = "Purging File JKT_NOTIFY : " ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      LOOP
         READNEXT SSJN_ID ELSE MORE = 0
      WHILE MORE DO
         IF SSJN_ID[1,3] # CONO THEN GOTO 190
         MATREADU SSJN.REC FROM JKT_NOTIFY, SSJN_ID ELSE
            RELEASE JKT_NOTIFY, SSJN_ID; GOTO 190
         END
         P = DCOUNT(SSJN_TICKET,VM)
         DONE = 0
         FOR I = P TO 1 STEP -1 UNTIL DONE
            P_X = 40 ; P_Y = 4 ; P_VALUE = SSJN_ID ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            JTT_ID = CONO: FIELD(SSJN_TICKET<1,I>,"-",1)
            MATREAD JTT_REC FROM JOB_TKT_TAG, JTT_ID ELSE GOTO 190
            LOCATE SSJN_TICKET<1,I> IN JTT_REV_ID<1>, 1 SETTING LOC THEN
               DEL SSJN_TICKET<1,I>
               DEL SSJN_TKTDEF<1,I>
               DEL SSJN_SCREEN<1,I>
            END ELSE
               DONE = 1
            END
         NEXT I
         IF DCOUNT(SSJN_TICKET,VM) # P THEN
            MATWRITE SSJN.REC ON JKT_NOTIFY, SSJN_ID
         END ELSE
            RELEASE JKT_NOTIFY, SSJN_ID
         END
190*
      REPEAT
      P_X = 0 ; P_Y = 4 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
*---- Purge JOB_TKT and JOB_JKT_IMG Files
*
      SELECT JOB_TKT_TAG
      MORE = 1
      P_X = 15 ; P_Y = 4 ; P_VALUE = "Purging Files JOB_TKT and JOB_TKT_IMG @ : " ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      LOOP
         READNEXT JTT_ID ELSE MORE = 0
      WHILE MORE DO
         IF JTT_ID[1,3] # CONO THEN
            GOTO 290
         END
         MATREADU JTT_REC FROM JOB_TKT_TAG, JTT_ID ELSE 
            RELEASE JOB_TKT_TAG, JTT_ID
            GOTO 290
         END
         P = DCOUNT(JTT_REV_ID,VM)
         FOR I = 1 TO P
            P_X = 57 ; P_Y = 4 ; P_VALUE = JTT_REV_ID<1,I> ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            READU JKT_ID FROM JOB_TKT, CONO: JTT_REV_ID<1,I> THEN
               DELETE JOB_TKT, CONO: JTT_REV_ID<1,I>
            END ELSE
               RELEASE JOB_TKT, CONO: JTT_REV_ID<1,I>
            END
            READU JKT_IMG FROM JOB_JKT_IMG, CONO: JTT_REV_ID<1,I> THEN
               DELETE JOB_JKT_IMG, CONO: JTT_REV_ID<1,I>
            END ELSE
               RELEASE JOB_JKT_IMG, JTT_REV_ID<1,I>
            END
            JKT_IMG_ID = JTT_REV_ID<1,I>: "!1"
            READU JKT_IMG FROM JOB_JKT_IMG, CONO: JKT_IMG_ID THEN
               DELETE JOB_JKT_IMG, CONO: JKT_IMG_ID
            END ELSE
               RELEASE JOB_JKT_IMG, CONO: JKT_IMG_ID
            END
         NEXT I
         DELETE JOB_TKT_TAG, JTT_ID
290*
      REPEAT
      GOTO 99990
*
*---- Print Error Messages
*
91000*
      ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000*
      ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
*
*---- END
*
99990 *
      ECD.ACTION = 99 ; CALL SCRN.EDIT
*
99999*
   END
