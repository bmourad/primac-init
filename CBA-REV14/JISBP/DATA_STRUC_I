      SUBROUTINE DATA_STRUC_I(TABLE_ID)
*COPY>CPYLIB>COM1
*COPY>JIS.CPYLIB>COM_DATA_STRUC_M
*********************************************************************
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* Revision      - [10.0]
* Revision Date - 03/22/94
* System        - PRIMAC
* Library       - JISBP/DATA_STRUC_I
* Author        - Ziad Yamout, VERCOM Software, Inc.
*T21177 diane 02/10/1997 * REV11 UPG
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>PMC.CPYLIB>MENUS.CONTROL
*COPY>JIS.CPYLIB>SYS_FILES
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- Setup variables
*
      ESN = ECD.SCRN.NO
      BEGIN.PAGE = 14; PAGE.SIZE = 6
      LINE.SPACE = 1
*
*---- Display screen header & data
*
      ECD.ACTION = 2; CALL SCRN.EDIT
*
      CBA_ACCT = FIELD(CBA.PATH,"/",(COUNT(CBA.PATH,"/")+1))
      CNT = DCOUNT(SFR.Q.NAME,VM)
      LINES = 0; LN = 1
      Q_NAME = ""; Q_ACCT = ""
      FOR I = 1 TO CNT
         QCNT = DCOUNT(SFR.Q.NAME<1,I>,SVM)
         FOR Q = 1 TO QCNT
            LINES = LINES + 1
            Q_NAME<LINES> = SFR.Q.NAME<1,I,Q>
            Q_ACCT<LINES> = SFR.Q.ACCT<1,I>
         NEXT Q
      NEXT I
*
      SCV.REC(1)<ESN> = TABLE_ID
      SCV.REC(2)<ESN> = SFR.ACCT
      SCV.REC(5)<ESN> = SFR.CPYLIB
      BEGIN CASE
         CASE SFR.TYPE = "D"
            SCV.REC(7)<ESN> = "Data"
         CASE SFR.TYPE = "O"
            SCV.REC(7)<ESN> = "Formula"
         CASE SFR.TYPE = "Q"
            SCV.REC(7)<ESN> = "Link pointer"
         CASE SFR.TYPE = "B"
            SCV.REC(7)<ESN> = "Basic Program"
         CASE SFR.TYPE = "P"
            SCV.REC(7)<ESN> = "Procdure"
         CASE SFR.TYPE = "S"
            SCV.REC(7)<ESN> = "Screen"
         CASE SFR.TYPE = "C"
            SCV.REC(7)<ESN> = "Data Structure"
         CASE SFR.TYPE = "M"
            SCV.REC(7)<ESN> = "Menu"
         CASE 1
            SCV.REC(7)<ESN> = SFR.TYPE
      END CASE
      IF SFR.DTYPE = "F" THEN
         SCV.REC(11)<ESN> = "Hashed file"
      END ELSE
         SCV.REC(11)<ESN> = "Directory"
      END
      BEGIN CASE
         CASE SFR.ACCT = "CBA"
            F_PATH = CBA.PATH
         CASE SFR.ACCT = "PMC"
            F_PATH = PRIMAC.PATH
         CASE 1
            F_PATH = PRIMAC.PATH:"-":SFR.ACCT
      END CASE
      SCV.REC(13)<ESN> = F_PATH:"/":TABLE_ID
      SCV.REC(15)<ESN> = F_PATH:"/":SFR.D.NAME
      SCV.REC(49)<ESN> = INT(LINES/PAGE.SIZE+.9) "R%2"
      ECD.ACTION = 3; CALL SCRN.EDIT
      OLD.START.LINE = 0; GOSUB 7900
*
*---- Prompt line
*
      MORE = 1
      LOOP
         ECD.NUM = 51; SCV.REC(ECD.NUM)<ESN> = ""
         ECD.ACTION = 4; CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
            CASE OPTION = "E" OR OPTION = "END"
               MORE = 0
            CASE OPTION = "S" OR OPTION = "SF"
               LN = 1+INT((LN-1)/PAGE.SIZE)*PAGE.SIZE+PAGE.SIZE
               IF LN > LINES THEN LN = 1
               GOSUB 7900
            CASE OPTION = "SR"
               LN = 1+INT((LN-1)/PAGE.SIZE)*PAGE.SIZE-PAGE.SIZE
               IF LN < 1 THEN LN = 1
               GOSUB 7900
            CASE OPTION = "ST"
               LN = 1
               GOSUB 7900
            CASE OPTION = "SB"
               LN = LINES
               GOSUB 7900
            CASE OPTION[1,1] = "S" AND NUM(OPTION[2,99])
               LN.NO = OPTION[2,99] + 0
               IF LN.NO < 1 OR LN.NO > LINES THEN
                  ERRMSG = "** Invalid selection **"
                  GOSUB 91000
               END ELSE
                  LN = LN.NO
                  GOSUB 7900
               END
            CASE OPTION = "V"
               MAT GEN.XREF.REC = ""
               GXR.NAME = "XREF.CPYLIB"
               GXR.XREF = CPYLIB_FILE(CPLB_LOC)
               GXR.FILE = GXR.XREF
               GXR.SRCH.ID = TABLE_ID
               CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
               ECD.ACTION = 2; CALL SCRN.EDIT
               ECD.ACTION = 3; CALL SCRN.EDIT
               OLD.START.LINE = 0; GOSUB 7900
         END CASE
      WHILE MORE DO REPEAT
      GOTO 99999
*
*---- Display scrolling lines
7900*
      START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
      LAST.LINE = START.LINE + PAGE.SIZE - 1
      IF LAST.LINE > LINES THEN LAST.LINE = LINES
      IF START.LINE = OLD.START.LINE THEN GOTO 7090
      OLD.START.LINE = START.LINE
      ECD.NUM = 48
      SCV.REC(ECD.NUM)<ESN> = INT(LAST.LINE/PAGE.SIZE+.9) "R%2"
      ECD.ACTION = 5; CALL SCRN.EDIT
      CNT = 1
      FOR N = START.LINE TO LAST.LINE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
         P_X = 0 ; P_Y = SLN ; P_VALUE = N "R%3" ; P_OPT = "CL"
         P_X := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:Q_NAME<N> "L#25"
         QACCT = Q_ACCT<N>
         BEGIN CASE
            CASE QACCT = "PMC"
               QACCT = M.ACCOUNT
            CASE QACCT = "CBA"
               QACCT = CBA_ACCT
            CASE 1
               QACCT = M.ACCOUNT:"-":QACCT
         END CASE
         P_X := AM:30 ; P_Y := AM:SLN ; P_VALUE := AM:QACCT "L#20"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
         CNT = CNT + 1
      NEXT N
      FOR M = CNT TO PAGE.SIZE
         SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
         P_X = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT M
7090  RETURN
*
*---- Error routines
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 91000*
*       CRT @(0,23):ERRMSG:CL:
*       INPUT REPLY,1_:
*       CRT @(0,23):CL:
*       RETURN
99999*
      ECD.ACTION = 99 ; CALL SCRN.EDIT
      RETURN
   END
