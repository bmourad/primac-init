* WIN.DDEDEMO
* Demonstration of some of the DDE routines
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
PROMPT ''
*
PRINT @(-1):"WIN.DDEDEMO":
PRINT @(20,0):"Dynamic Data Exchange demonstration":
PRINT @(71,0):OCONV(DATE(),"D2"):
CALL WIN.BOX(0,1,79,1,1)
*
PRINT @(0,3):"This program demonstrates DDE. You must have Microsoft Excel installed"
PRINT @(0,4):"If Excel is not in your DOS PATH, you should start it manually now"
*                                             
OK = 0
PRINT @(0,6):"To start our conversation with another application we use:-"
PRINT "   CALL WIN.DDEOPEN(NAME, APP.NAME, TOPIC, STARTED.OK)"
PRINT
PRINT "If the link opens ok, we can do the following:-"
PRINT "   CALL WIN.DDEREQ(NAME, ITEM, VALUE) to fetch data"
PRINT "   CALL WIN.DDEPOKE(NAME, ITEM, VALUE) to send data"
PRINT "   CALL WIN.DDEEXEC(NAME, MACRO) to execute a macro"
PRINT
PRINT "Finally we close the link with"
PRINT "   CALL WIN.DDECLOSE(NAME)"
PRINT
PRINT "The script language allows further control over DDE"
PRINT
GOSUB 100; * Check if demo required
IF CONTINUE THEN GOSUB 200; * Excel demo
*
STOP
*
* Check if demo required
100 CONTINUE = 0
LOOP
PRINT @(0,22):@(-4):"Do you want to run the demonstration? (Y/N):":
INPUT DUM:
DUM = OCONV(DUM, "MCU")
UNTIL DUM = "Y" OR DUM = "N" DO                                
   PRINT @(0,23):"Please answer Y for yes or N for no":
REPEAT
*
PRINT @(0,20):@(-3):
IF DUM = "Y" THEN CONTINUE = 1
*
RETURN
*
* Excel DDE demonstration
200 CALL WIN.COLOR("Yellow","Blue")
CALL WIN.TWOPEN("ddedemo", "Excel Dynamic Data Exchange demo",10, 5, 70, 16,1)
PRINT "To find out the topics Excel supports:-"
PRINT "First open a link to the system topic as follows:-"
PRINT '  CALL WIN.DDEOPEN("EXCEL_LINK","excel","System", OK)'
CALL WIN.DDEOPEN("EXCEL_LINK","excel","System", OK)
*
IF NOT(OK) THEN                
   PRINT
   PRINT "We couldn't start the link so we shall try to start Excel"
   PRINT 'and try again'
   PRINT '  CALL WIN.PCRUN2("EXCEL","","",RESP)'
   PRINT '  CALL WIN.DDEOPEN("EXCEL_LINK","excel","System", OK)'
   CALL WIN.PCRUN2("EXCEL","","",RESP)
   IF RESP > 32 THEN
      CALL WIN.DDEOPEN("EXCEL_LINK","excel","System", OK)
   END ELSE OK = 0
   IF NOT(OK) THEN
      PRINT
      CALL WIN.COLOR("lightred","")
      PRINT 'We are unable to start Excel, so the demo will stop.'
      PRINT "If you do own Excel, try starting it and then re-running the demo."
   END
END
*
IF OK THEN
   PRINT
   PRINT "Press <CR> to continue ":
   INPUT DUM:
   PRINT @(-1):"Link opened to Excel system topic"
   PRINT
   PRINT "Ask for the Topics items data"
   PRINT '   CALL WIN.DDEREQ("EXCEL_LINK", "Topics", TOPICS)'
   TOPICS = ''
   CALL WIN.DDEREQ("EXCEL_LINK", "Topics", TOPICS)           
   PRINT
   VALUE = TOPICS; S =  CHAR(9); R = ","; GOSUB 500; * Replace tabs
   PRINT "The topics known by Excel are"
   PRINT VALUE
   PRINT
   PRINT 'Finally we close the link with'
   PRINT '  CALL WIN.DDECLOSE("EXCEL_LINK")'
   CALL WIN.DDECLOSE("EXCEL_LINK")
*
   PRINT
   PRINT "Press <CR> to continue ":
   INPUT DUM
*
   PRINT @(-1):"The next part of the demo requires the Excel sheet Sheet1."
   PRINT
   PRINT "If this does not exists already, close and restart Excel and it should appear"
   PRINT
   PRINT "Press <CR> when you are ready ":
   INPUT DUM:
*
   PRINT @(-1):"First we open a link to the spread sheet 'Sheet1'"
   PRINT '  CALL WIN.DDEOPEN("EXCEL_SHEET","EXCEL","SHEET1",OK)'
   CALL WIN.DDEOPEN("EXCEL_SHEET","EXCEL","SHEET1",OK)
   IF NOT(OK) THEN
      CALL WIN.COLOR("LightRed","")
      PRINT "We could not open the link so we will exit the demonstration."
   END ELSE
      PRINT "We then add the titles to the spreadsheet"
      PRINT '   CALL WIN.DDEPOKE("EXCEL_SHEET", "R2C2", "Fruit")'
      PRINT '   CALL WIN.DDEPOKE("EXCEL_SHEET", "R2C3", "Quantity")'
      CALL WIN.DDEPOKE("EXCEL_SHEET", "R2C2", "Fruit")
      CALL WIN.DDEPOKE("EXCEL_SHEET", "R2C3", "Quantity")           
      RQM
*
      PRINT "and some data in exactly the same way"   
      R.DATA = "Apple"; R.DATA<1,2> = 20
      R.DATA<2,1> = "Banana"; R.DATA<2,2> = 30
      R.DATA<3,1> = "Pear"; R.DATA<3,2> = 10
      FOR J = 1 TO 3
         CALL WIN.DDEPOKE("EXCEL_SHEET", "R":(J+3):"C2", R.DATA<J,1>)
         CALL WIN.DDEPOKE("EXCEL_SHEET", "R":(J+3):"C3", R.DATA<J,2>)
      NEXT J
      RQM
*
      PRINT "To add the total line"
      PRINT '   CALL WIN.DDEPOKE("EXCEL_SHEET", "R8C2","Total")'
      PRINT '   CALL WIN.DDEPOKE("EXCEL_SHEET", "R8C3","=SUM(C4:C6)")'
      CALL WIN.DDEPOKE("EXCEL_SHEET", "R8C2","Total")
      CALL WIN.DDEPOKE("EXCEL_SHEET", "R8C3","=SUM(C4:C6)")           
*
      PRINT
      PRINT 'Press <CR> to continue ':
      INPUT DUM:
*
      PRINT @(-1):"Now to run a few Excel macros to tidy the data up"
      PRINT
      PRINT '   CALL WIN.DDEEXEC("EXCEL_SHEET",' : "'" :' [SELECT("R2C2:R8C3","R2C2")]':"')"
      PRINT '   CALL WIN.DDEEXEC("EXCEL_SHEET", "[COLUMN.WIDTH(1,,,3)]")'
      CALL WIN.DDEEXEC("EXCEL_SHEET", '[SELECT("R2C2:R8C3","R2C2")]')
      CALL WIN.DDEEXEC("EXCEL_SHEET", "[COLUMN.WIDTH(1,,,3)]")
*
      PRINT "These two Excel commands select our data then size it to for the best fit"
*            
      PRINT
      PRINT 'Finally we close our link'
      PRINT '   CALL WIN.DDECLOSE("EXCEL_SHEET")'
      CALL WIN.DDECLOSE("EXCEL_SHEET")
   END
END
*     
PRINT
PRINT "Press <CR> to continue ":
INPUT DUM:
CALL WIN.TWCLOSE("DDEDEMO")
CALL WIN.COLOR("OFF","")
*
RETURN
*
* Replace S with R in VALUE
500 LOOP
      C=INDEX(VALUE, S, 1)
    WHILE C DO
      VALUE = VALUE[1,C-1]:R:VALUE[C+1,32000]
    REPEAT
*
    RETURN
*
END
