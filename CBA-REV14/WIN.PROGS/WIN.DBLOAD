SUBROUTINE WIN.DBLOAD(DBX, ERR)
* Load a dialog box to the PC
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
EQU TRUE TO 1, FALSE TO 0  
STX=CHAR(2)
PROMPT ''
ECHO OFF                           
*
NAME = FIELD(DBX<1>,' ',3)
ERR=0
*                     
BUILD=FALSE
*
IF NAME = '' THEN ERR = 1
*
* Check if dialog is already displayed
IF ERR = 0 THEN
   CALL WIN.COMSUB("Enter stx:IsShown(":NAME:")")
   GOSUB 100; * Get response
   IF RESP = '1' THEN ERR = 2
END         
*                                     
* Check if dialog has already been sent to the PC
IF ERR = 0 THEN
   CALL WIN.COMSUB("Enter stx:IsDialog(":NAME:")")
   GOSUB 100; * Get response
   IF RESP = '1' THEN ERR = 3 ELSE BUILD=TRUE
END
*            
SCRIPT = ''
IF BUILD THEN   
   SCRIPT = DBX
   SCRIPT<-1>='EndCreate'                
   CALL WIN.HSCRIPTC(SCRIPT)
   CALL WIN.COMSUB("Enter stx:IsDialog(":NAME:")")
   GOSUB 100; * Get response
   IF RESP # '1' THEN ERR = 4
END
*
ECHO ON
*       
RETURN
*  
* Get response from input
100 LOOP
      INPUT RESP:
    UNTIL INDEX(RESP,STX,1) DO
    REPEAT                 
    RESP = FIELD(RESP,STX,2)
*
    RETURN
*
END
