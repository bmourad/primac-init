* WIN.DBDEMO
* Dialogbox demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
      PROMPT ''
      EQU TRUE TO 1, FALSE TO 0
*
      C22 = @(0,22):@(-4)
      C23 = @(0,23):@(-4)
      CMSG = @(0,20):@(-4)
*
      PRINT @(-1):"WIN.DBDEMO":
      PRINT @(26,0):"Dialog boxes demonstration":
      PRINT @(71,0):OCONV(DATE(),"D2"):
      CALL WIN.BOX(0,1,79,1,1)
*
      GOSUB 50; * Print brief description
*   
STATE=''
CALL WIN.SSTATE(STATE,'')
*
GOSUB 100; * Create and show dialog
IF ERR = 0 THEN
*                                   
DLG=''
CTRL=''
EVENT=''
FINISHED=FALSE
*
CALL WIN.COLOR("Yellow","Blue")
CALL WIN.TWOPEN("DLGDEMO", "Events received", 10,8,70,20,"SINGLE")
CALL WIN.COLOR("White","")
*
LOOP
   CALL WIN.DBEVENT(DLG,CTRL,EVENT)
*
   BEGIN CASE
   CASE EVENT = 'U'; PRINT 'Unknown':
   CASE EVENT = 'M'; PRINT 'Menu':
   CASE EVENT = 'V'; PRINT 'Validation':
   CASE EVENT = 'D'; PRINT 'Default':
   CASE EVENT = 'C'; PRINT 'Button Click':
   CASE EVENT = 'DC'; PRINT 'Double Click':
   CASE EVENT = 'S'; PRINT 'Scroll':
   CASE 1
        PRINT 'Event ':EVENT:
   END CASE
*
   IF DLG # '' THEN
      PRINT ' dialog ':DLG:' Control ':CTRL
   END ELSE
      PRINT ' data ':CTRL
   END
*
   BEGIN CASE
   CASE DLG = ''; IF CTRL ='*' THEN FINISHED=TRUE
   CASE DLG = DLG.NAME; GOSUB 1000; * Process DlgDemo events
   END CASE
*
UNTIL FINISHED DO
REPEAT
*
CALL WIN.TWCLOSE("DLGDEMO")
*
CALL WIN.COLOR("White","Black")
PRINT @(0,3):@(-3):"You exited with ":CTRL
IF CTRL = "OK" THEN GOSUB 1100; * Display details
CALL WIN.DBDEL(DLG.NAME)
*
END
*
CALL WIN.SSTATE('',STATE)
PRINT C22:'Press <CR> to Exit demonstration':
INPUT DUM:                                 
*
STOP
*
* Brief description of dialog boxes
50 PRINT @(0,3):
PRINT "The first step is to define the dialog box"
PRINT "   CALL WIN.DBNEW(DBX,NAME,TITLE, X,Y,W,D,STYLE,OPTS)"
PRINT "starts the definition, and then the individual controls are added"
PRINT "by for example:-"
PRINT "   CALL WIN.DBLABEL(DBX,TEXT,X,Y,W,D,JUST)"
PRINT "   CALL WIN.DBBUTTON(DBX,NAME,X,Y,W,D,DEF)"
PRINT "   CALL WIN.DBEDIT(DBX,NAME,X,Y,W,D,OPTS)"
PRINT
PRINT "The dialog box is the loaded to the PC with:-"
PRINT "   CALL WIN.DBLOAD(DBX, ERR)"
PRINT "and displayed with:-"
PRINT "   CALL WIN.DBSHOW(NAME, TYPE, ERR)"
PRINT
PRINT "Events are then monitored within a loop by:-"
PRINT "   CALL WIN.DBEVENT(DLG,CTRL,TYPE)"                         
PRINT
PRINT "and finally the dialog box can be closed and deleted with:-"
PRINT "   CALL WIN.DBEND(NAME, UPDATE) and CALL WIN.DBDEL(NAME)"
PRINT C22:'Press <CR> to continue with the demonstration':
INPUT DUM:
RETURN
*
* Create and show dialog
100 DBX=''
DLG.NAME="DemoDlg"
CALL WIN.TWMSG("Creating Dialog ":DLG.NAME:"...", "Red", "Yellow", "DOUBLE")
*
VER = ''
CALL WIN.VERSION(VER)
OPTS = ""
IF VER >= "2.5" THEN OPTS<2> = 1
*
CALL WIN.DBNEW(DBX, DLG.NAME, "Demonstration of Dialog boxes", 10,10,200,160, '',OPTS)              
*
CALL WIN.DBLABEL(DBX,"Label 1",4,4,'','','')
CALL WIN.DBEDIT(DBX,"E1",40,4,40,'','')
CALL WIN.DBTEXT(DBX,"T1",4,24,80,'','C')
CALL WIN.DBCHECK(DBX,"C1","Check one",4,36,60,'')
*
CALL WIN.DBGROUP(DBX,"G1","Group box",4,50,60,52)
CALL WIN.DBRADIO(DBX,"R1","Radio One",6,58,50,'',1)
CALL WIN.DBRADIO(DBX,"R2","Radio Two",6,72,50,'',0)
CALL WIN.DBRADIO(DBX,"R3","Radio Three",6,88,50,'',0)
*
CALL WIN.DBLIST(DBX, "L1",68,52,50,52,"WS_BORDER|WS_TABSTOP|LBS_NOTIFY")
CALL WIN.DBCOMBO(DBX,"CB1",122,52,50,52,'')
CALL WIN.DBSCROLL(DBX,"S1", 4,116,60,'','WS_TABSTOP','','')
CALL WIN.DBIMAGE(DBX,"I1", "image\computer.wmf",68,116,100,40)
*
CALL WIN.DBBUTTON(DBX,"OK","OK",158,4,'','',1)
CALL WIN.DBBUTTON(DBX,"Cancel","Cancel",158,20,'','',0)
CALL WIN.DBIMBUT(DBX,"IB","image\normal.bmp",158,36,'','')
*
CALL WIN.DBLOAD(DBX,ERR)
IF ERR THEN       
   ERR.MSG='Loading with WIN.DBLOAD'
* Just delete dialog and try again if we get already exist error
   IF ERR = 3 THEN
      CALL WIN.DBDEL(DLG.NAME)
      CALL WIN.DBLOAD(DBX,ERR)
   END                                                          
END
*         
IF ERR = 0 THEN
   CALL WIN.DBSHOW(DLG.NAME, 1, ERR)
   IF ERR THEN
      CALL WIN.DBDEL(DLG.NAME)
      ERR.MSG = 'Displaying with WIN.DBSHOW'
   END
END
*
IF ERR = 0 THEN
   CALL WIN.DBSET(DLG.NAME,"T1","Centered Text")
   CALL WIN.DBSET(DLG.NAME,"R1",1)
   R.LIST = "Option 1"
   R.LIST<-1>="Option 2"
   R.LIST<-1>="Option 3"
   CALL WIN.DBSET(DLG.NAME,"L1", R.LIST)
   CALL WIN.DBSELECT(DLG.NAME,"L1", "Option 2")
   CALL WIN.DBSET(DLG.NAME,"CB1",R.LIST)
   CALL WIN.DBSELECT(DLG.NAME,"CB1", "Option 3")
END
*
CALL WIN.TWCLOSE("MSG")
*
IF ERR THEN
   CALL WIN.COLOR("Red","White")
   PRINT @(0,18):@(-4):ERR.MSG:' failed, Error Number ':ERR:
END
*
RETURN
*
* Process dialog DlgDemo events
1000 BEGIN CASE
     CASE CTRL='OK'
          CALL WIN.DBEND(DLG.NAME, TRUE)
          FINISHED=TRUE
     CASE CTRL='Cancel'
          CALL WIN.DBEND(DLG.NAME,FALSE)
          FINISHED=TRUE
     END CASE
*
     RETURN
*
* Display result of dialog
1100 PRINT @(0,3):@(-3):"You clicked OK or pressed return to end the dialog"
PRINT                                
PRINT 'On the dialog you had set:-'
CALL WIN.DBGET(DLG.NAME, "E1", VALUE)
PRINT 'The Edit field to "':VALUE:'"'
CALL WIN.DBGET(DLG.NAME, "C1", VALUE)
PRINT "The check box was ":
IF VALUE THEN PRINT "Checked" ELSE PRINT "Unchecked"
CALL WIN.DBGET(DLG.NAME, "G1", VALUE)
PRINT 'You selected radio option "':
*
BEGIN CASE
CASE VALUE = "R1"; PRINT "Radio one":
CASE VALUE = "R2"; PRINT "Radio two":
CASE VALUE = "R3"; PRINT "Radio three":
END CASE
PRINT '"'
*                            
CALL WIN.DBGET(DLG.NAME, "L1", VALUE)
PRINT 'List box option "': VALUE : '"'
CALL WIN.DBGET(DLG.NAME, "CB1", VALUE)
PRINT 'Combo box value "': VALUE : '"'
CALL WIN.DBGET(DLG.NAME, "S1", VALUE)
PRINT 'Scroll bar position "': VALUE : '"'
*
RETURN
*
END
