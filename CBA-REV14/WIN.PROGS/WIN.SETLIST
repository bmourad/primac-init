      SUBROUTINE WIN.SETLIST(VARNAME, VALUE)
* Set a wIntegrate variable from a list
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
* Assumes Printable characters only (not backquote)
* Converts am to cr, vm to tab
*                      
      EQU MAXLENGTH TO 200
      WC = 'WIN.COMSUB'
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
      CONVERTED = 0
      SEND.VAL = VALUE
      S = '\'; R = '\\'; GOSUB 100; * Replace
      S = AM; R = '\r'; GOSUB 100; * Replace
      S = VM; R = '\t'; GOSUB 100; * Replace
*
      LEN.VALUE = LEN(SEND.VAL)
*
      IF LEN.VALUE + LEN(VARNAME) < MAXLENGTH THEN
         SCRIPT.VALUE = "`":SEND.VAL:"`"
      END ELSE
         CALL @WC("Global TRV=''")
         SCRIPT.VALUE = "TRV"
*
         POS = 1
         LOOP
            NO.CHARS = MAXLENGTH - 6
            IF NO.CHARS > LEN.VALUE THEN NO.CHARS = LEN.VALUE
            CALL @WC("TRV:=`":SEND.VAL[POS,NO.CHARS]:"`")
            LEN.VALUE = LEN.VALUE - NO.CHARS
         WHILE LEN.VALUE > 0 DO
            POS = POS + NO.CHARS
         REPEAT
*                                           
      END                                   
*
      IF CONVERTED THEN SCRIPT.VALUE = "BSToAscii(":SCRIPT.VALUE:")"
      CALL @WC("Global ":VARNAME:"=":SCRIPT.VALUE)
*
      RETURN
*
* Convert S to R in VALUE, Set CONVERTED if S if found
100 POS = 1
    LOOP
      C = INDEX(SEND.VAL[POS,99999],S,1)
    WHILE C DO      
      POS = POS + C - 1
      CONVERTED = 1
      SEND.VAL = SEND.VAL[1,POS-1] : R : SEND.VAL[POS+1,99999]
      POS = POS + LEN(R)
    REPEAT
*
    RETURN
*
   END
