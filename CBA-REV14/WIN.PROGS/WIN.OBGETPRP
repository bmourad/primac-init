      SUBROUTINE WIN.OBGETPRP(NAME, PROP, VALUE)
* Get an objects property value
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
* Assumes var contains printable chars, cr and tab only.
* cr and tab are converted to am and vm.
*                        
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
STX=CHAR(2)                    
VARNAME = NAME:'.':PROP
PROMPT ""
ECHO OFF
*
CALL WIN.COMSUB("Host Send ":VARNAME)
VALUE = ""
CNV = ""
LOOP
  INPUT DUM:
  IF INDEX(DUM,STX,1) THEN
    DUM = FIELD(DUM,STX,2)
    IF CNV = "" THEN
      CNV = DUM[1,2]
      VALUE = VALUE : DUM[3,9999]
    END ELSE
      VALUE = VALUE : DUM
    END
  END ELSE
    DUM = "X"
  END
UNTIL DUM = "" DO
REPEAT
IF CNV # "no" THEN GOSUB 200;* Convert
ECHO ON
RETURN
*
* Convert data
200 TYPE = CNV[1,1]
SUBTYPE= CNV[2,1]
IF TYPE = "b" THEN
  POS = 1
  LOOP
    C = INDEX(VALUE[POS,99999],'\',1)
  UNTIL C = 0 DO
    POS = POS + C - 1
    CH = VALUE[POS+1,1]
    BEGIN CASE
      CASE CH = 'r'; CH = AM
      CASE CH = 't'; CH = VM
      CASE CH = '\'; CH = '\'
      CASE CH = 'd'; CH = CHAR(127)
    END CASE
    VALUE = VALUE[1,POS-1]:CH:VALUE[POS+2,99999]
    POS = POS + 1
  REPEAT
END ELSE
  PRINT "Invalid conversion type ":CNV
END
RETURN
*
END
