SUBROUTINE WIN.TWPUSH(TITLE,LEFT.X,TOP.Y,RIGHT.X,BOT.Y,STYLE)
* Open a text window on the screen
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
SCROLL.REGION = LEFT.X:',':TOP.Y:',':RIGHT.X:',':BOT.Y
*
IF STYLE # "" THEN
   BORDER = LEFT.X-1:',':TOP.Y-1:',':RIGHT.X+1:',':BOT.Y+1
   IF NOT(NUM(STYLE)) THEN GOSUB 100; * Convert style to number
END ELSE BORDER = SCROLL.REGION
*
SCRIPT = "If Not(tws_next) Then Global tws_next = 0"
SCRIPT = SCRIPT : ";tws_next+=1"
SCRIPT = SCRIPT : ";SN SR"
CALL WIN.COMSUB(SCRIPT)
SCRIPT = 'twn = "stack":tws_next'
SCRIPT = SCRIPT : ';SN ST $twn,':BORDER
SCRIPT = SCRIPT : ";twn='tw_':twn;Global $twn='":SCROLL.REGION:"'"
IF STYLE # "" THEN SCRIPT = SCRIPT : ';Dp Box ':BORDER:',':STYLE
CALL WIN.COMSUB(SCRIPT)
IF TITLE # '' THEN PRINT @(LEFT.X+1,TOP.Y-1):TITLE:
CALL WIN.COMSUB('Sn SR ':SCROLL.REGION)
*
RETURN
*
* Convert style to numerical equivalent
100 N = DCOUNT(STYLE, ",")
   STYLE = OCONV(STYLE, "MCU")
   S = 1                  
   NS = 0
*
   FOR J = 1 TO N
      WORD = FIELD(STYLE, ",",J)
      BEGIN CASE
      CASE WORD = "SINGLE"; NS = NS + S; S = 16
      CASE WORD = "DOUBLE"; NS = NS + S * 2; S = 16
      CASE WORD = "MIX"   ; NS = NS + S * 3; S = 16
      CASE WORD = "BLOCK" ; NS = NS + S * 4; S = 16
      CASE WORD = "NOFILL"; NS = NS + 256
      CASE WORD = "MERGE" ; NS = NS + 512
      END CASE
   NEXT J
*
   STYLE = NS
*
RETURN
*
END
