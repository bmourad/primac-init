SUBROUTINE WIN.INPBOX(TEXT,TITLE,LEN,RESP)
* Display a dialog box and wait for a response
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-96. Impact Business Systems
*
EQU XSCALE TO 4, YSCALE TO 8
*
STX=CHAR(2)
*
XSIZE = LEN
LTEXT = LEN(TEXT)
IF LTEXT > XSIZE THEN XSIZE = LTEXT
IF LEN(TITLE) > XSIZE THEN XSIZE = LEN(TITLE)
XSIZE = XSIZE * XSCALE + 4 + 34
YSIZE = 4 * YSCALE
ROW1 = 4
ROW2 = 16
*        
SCRIPT = 'DialogBox Create InpBox 40,80,':XSIZE:',':YSIZE
SCRIPT<-1>='Font 8,"helv"'
SCRIPT<-1>='Caption "':TITLE:'"'
SCRIPT<-1>='If Version >= "2.5" Then Option "M"'
SCRIPT<-1>='LText "':TEXT:'",None,4,':ROW1:',':LTEXT * XSCALE:",":YSCALE
SCRIPT<-1>='EditText inp, 4,':ROW2:',':(LEN+1) * XSCALE :",":YSCALE+4:",ES_AutoHScroll | WS_TABSTOP|WS_BORDER" 
SCRIPT<-1>='LimitText inp,':LEN
SCRIPT<-1>='DefPushButton "&OK",OK,':XSIZE - 28:',4,26,14'
SCRIPT<-1>='ControlCommand OK,"DialogBox End Inpbox,True"'
SCRIPT<-1>='EndCreate'
IF RESP # '' THEN SCRIPT<-1>='InpBox.inp="':RESP:'"'
SCRIPT<-1>='DialogBox Show InpBox'
SCRIPT<-1>='Enter stx:InpBox.inp'
SCRIPT<-1>='DialogBox Delete InpBox'
*
PROMPT ''
ECHO OFF
CALL WIN.HSCRIPT(SCRIPT)
LOOP
INPUT RESP:
UNTIL INDEX(RESP,STX,1) DO
REPEAT
RESP = FIELD(RESP,STX,2)
ECHO ON
*
RETURN
*
END
