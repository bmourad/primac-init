* WIN.CEDEMO
* Chiselled effect demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
*
      PROMPT ''
      EQU TRUE TO 1, FALSE TO 0
*
      C22 = @(0,22):@(-4)
      C23 = @(0,23):@(-4)
      CMSG = @(0,20):@(-4)
      TEXT.COL = "Yellow"
      CODE.COL = "LightCyan"
      OLD.STATE = ''
      WINTVER = ""
      CALL WIN.VERSION(WINTVER)
      BKIMAGE = FALSE
      IF WINTVER >= "4.0.3" THEN BKIMAGE = TRUE
*
      CALL WIN.SSTATE(OLD.STATE,"")
      CALL WIN.COLOR("White","Blue")
*
      PRINT @(-1):"WIN.CEDEMO":
      PRINT @(26,0):"Chiselled effects demonstration":
      PRINT @(71,0):OCONV(DATE(),"D2"):
      CALL WIN.BOX(0,1,79,1,1)
*
      GOSUB 50; * Print brief description
      GOSUB 100; * Save effects we will change
*
      GOSUB 200; * Set up demo screen
*
* Set up box to display information
      CALL WIN.COLOR("White","Blue")
      CALL WIN.TWOPEN("INFO","Information",1,18,78,22,"DOUBLE")
      CALL WIN.COLOR("Black","White")
      CALL WIN.TWFOOT("INFO","Press <CR> to continue","R")
      CALL WIN.COLOR(TEXT.COL,"Blue")
      TEXT.LEN = 77
*
      GOSUB 220; * Background
      GOSUB 230; * Simple raised input fields
      GOSUB 240; * Text fields
      GOSUB 260; * Emphasis text prompts
      GOSUB 280; * Box static text
      IF BKIMAGE THEN GOSUB 290; * Background image
      GOSUB 300; * Sign off
*
      GOSUB 120; * Restore effect we changed
      CALL WIN.TWCLOSE("INFO")
      CALL WIN.SSTATE("",OLD.STATE)
*   
STOP
*
* Print a brief description
50 PRINT @(0,3):
   PRINT "wIntegrate can convert the display of terminal effects to produce"
   PRINT "3D effects which give a host program a more GUI look."
   PRINT
   PRINT "Using the Setup Colors menu option you can set these styles up"
   PRINT "so that existing programs which currently use Dim, Bold, Reverse"
   PRINT "automatically have a GUI look."
   PRINT
   PRINT "This demonstration uses 3 host routines to convert a dummy input"
   PRINT "screen into a 3D looking screen."
   IF BKIMAGE THEN
      PRINT
      PRINT "A background image can also be put behind the text."
   END
   PRINT
   PRINT "The host routines are:-"
   PRINT
   CALL WIN.COLOR(CODE.COL,"")
   PRINT "  WIN.SETEFFCT - Alters the colors and style for an effect"
   PRINT "  WIN.USESTYLE - Turn GUI effect drawing on"
   PRINT "  WIN.EFILL    - Fill an area with an effect"
   IF BKIMAGE THEN PRINT "  WIN.BKIMAGE  - Display a background image"
*
   CALL WIN.COLOR("White","")
   PRINT C22:'Press <CR> to continue with the demonstration': 
   INPUT DUM:
   RETURN
*
* Save the things our demo will change
100 OLD.STYLE = ''
    CALL WIN.GETPARAM("UseStyle",OLD.STYLE)
    EFFS.USED = "Normal"
    EFFS.USED<-1> = "Dim"
    EFFS.USED<-1> = "Reverse"
    EFFS.USED<-1> = "Bold"
    NO.EFFS = DCOUNT(EFFS.USED, CHAR(254))
    EFFS.VALS = ''
    VALUE = ''
    FOR J = 1 TO NO.EFFS
       CALL WIN.GETPARAM("Effect_":EFFS.USED<J>, VALUE)
       EFFS.VALS<J> = VALUE
    NEXT J
*
    RETURN
*
* Restore setting we changed
120 CALL WIN.SETPARAM("UseStyle",OLD.STYLE)
    SCRIPT = ''
    FOR J = 1 TO NO.EFFS
       SCRIPT<-1> = "Store Effect_":EFFS.USED<J>:"='":EFFS.VALS<J>:"'"
    NEXT J
    SCRIPT<-1> = "Update"
    CALL WIN.HSCRIPT(SCRIPT)
    RETURN
*
* Dummy customer maintenance program
200 CALL WIN.EFFECT("NORMAL")
    PRINT @(0,3):@(-3)
    PRINT @(30,3):"Customer maintenance":
    PRINT @(5,5):"Id:":
    PRINT @(5,7):"1. Name:":
    PRINT @(5,10):"2. Address:":
    PRINT @(5,15):"3. Contact:"
*
    PRINT @(25,5):
    CALL WIN.EFFECT("REVERSE")
    PRINT "     ":
    CALL WIN.EFFECT("NORMAL")
    PRINT @(25,7):
    CALL WIN.EFFECT("REVERSE")
    PRINT "                        ":
    CALL WIN.EFFECT("NORMAL")
    FOR J = 10 TO 13
        PRINT @(25,J):
        CALL WIN.EFFECT("REVERSE")
        PRINT "                        ":
        CALL WIN.EFFECT("NORMAL")
    NEXT J
    PRINT @(25,15):
    CALL WIN.EFFECT("REVERSE")
    PRINT "                        ":
    CALL WIN.EFFECT("NORMAL")
    PRINT @(52,7):"This area has some useful":
    PRINT @(52,8):"or important static text":
    PRINT @(52,9):"displayed in it.":
    RETURN
*
* Change background
220 TEXT = "The first thing to do is convert the normal text color to black on"
    TEXT = TEXT : " light gray."
    GOSUB 2000; * Print Text
    GOSUB 1000; * Continue prompt
    CALL WIN.SETEFFCT("Normal","Black","LightGray","",0)
    PRINT "This was achieved with the following subroutine call:"
    CALL WIN.COLOR(CODE.COL,"")
    PRINT '   CALL WIN.SETEFFCT("Normal","Black","LightGray","",0)'
    CALL WIN.COLOR(TEXT.COL,"")
    GOSUB 1000; * Continure prompt
    RETURN
*
* Raised input only
230 TEXT = "For an easy Chiselled effect we will just raise the input fields"
    TEXT = TEXT : " which are currently displayed in REVERSE."
    GOSUB 2000;* Print text
    PRINT
    TEXT = "Before any Chiselled effects will be displayed they must be"
    TEXT = TEXT : " enabled first."
    GOSUB 2000;* Print Text
    GOSUB 1000;* Continue prompt
    CALL WIN.USESTYLE(TRUE)
    CALL WIN.SETEFFCT("Reverse","Black","LightGray","Raised",0)
    PRINT 'Enable Styles:'
    PRINT 'Reverse to Raised:'
    CALL WIN.COLOR(CODE.COL,"")
    PRINT @(18,0):"CALL WIN.USESTYLE(TRUE)":
    PRINT @(18,1):'CALL WIN.SETEFFCT("Reverse","Black","LightGray","Raised",0)'
    CALL WIN.COLOR(TEXT.COL,"")
    GOSUB 1000;* Continue prompt
    RETURN
*
* Convert input fields
240 TEXT = "We now want to give the input fields which are displayed in"
    TEXT = TEXT : " Reverse a text box look."
    GOSUB 2000;* Print Text
    GOSUB 1000; * Continue prompt
    CALL WIN.SETEFFCT("Reverse","Black","White","Textbox",0)
    PRINT 'Reverse to Textbox:'
    CALL WIN.COLOR(CODE.COL,"")
    PRINT '  CALL WIN.SETEFFCT("Reverse","Black","White","Textbox",0)'
    CALL WIN.COLOR(TEXT.COL,"")
    GOSUB 1000; * Continue prompt
    TEXT = "To space the fields out vertically you can add spaceing between"
    TEXT = TEXT : " the lines of the display. If you wish to try this go to the"
    TEXT = TEXT : " Setup Character menu option and set the Extra Line Space"
    TEXT = TEXT : " to 2 or 4."
    GOSUB 2000;* Print Text
    GOSUB 1000; * Continue prompt
    RETURN
*
* Emphasise prompt text
260 TEXT = "Emphasis for the title and field text can be added by setting up"
    TEXT = TEXT : " Bold to be a raised style and then paint the bold effect"
    TEXT = TEXT : " on the drawn screen."
    GOSUB 2000; * Print text
    GOSUB 1000; * Continue prompt
    CALL WIN.SETEFFCT("Bold","Black","LightGray","Raised",0)
* Have to temporarily put scrollregion back to whole screen
    CALL WIN.COMSUB("Screen ScrollRegion")
    CALL WIN.EFILL("Bold", 30,3,49,3)
    CALL WIN.EFILL("Bold", 5,5,15,5)
    CALL WIN.EFILL("Bold", 5,7,15,7)
    CALL WIN.EFILL("Bold", 5,10,15,10)
    CALL WIN.EFILL("Bold", 5,15,15,15)
    CALL WIN.TWUSE("INFO")
    PRINT "The emphasis is achieved with:-"
    CALL WIN.COLOR(CODE.COL,"")
    PRINT 'CALL WIN.SETEFFCT("Bold","Black","LightGray","Raised",0)'
    PRINT 'CALL WIN.EFILL("Bold", 30,3,49,3)'
    PRINT 'CALL WIN.EFILL("Bold", 5,5,15,5)'
    CALL WIN.COLOR(TEXT.COL,"")
    PRINT 'etc.':
    GOSUB 1000; * Continue prompt
    RETURN
*
* Box static text
280 TEXT = "We shall paint the text box to the right using the DIM"
    TEXT = TEXT : " effect. Here we require that there are no 3d effects drawn"
    TEXT = TEXT : " between the lines of the box so we set the last parameter"
    TEXT = TEXT : " of WIN.SETEFFCT to 1"
    GOSUB 2000; * Print Text
    GOSUB 1000; * Continue prompt 
    CALL WIN.SETEFFCT("Dim","Blue","LightGray","Inset", 1)
* Have to temporarily put scrollregion back to whole screen
    CALL WIN.COMSUB("Screen ScrollRegion")
    CALL WIN.EFILL("Dim",52,7,77,9)
    CALL WIN.TWUSE("INFO")
    PRINT "The statements used were:"
    CALL WIN.COLOR(CODE.COL,"")
    PRINT '   CALL WIN.SETEFFCT("Dim","Blue","","Inset", 1)'
    PRINT '   CALL WIN.EFILL("Dim",52,7,77,9)'
    CALL WIN.COLOR(TEXT.COL,"")
    GOSUB 1000; * Continue prompt
    RETURN
*
* Background image
290 TEXT = "Finally we shall use an image to show as the background."
    GOSUB 2000; * Print Text
    GOSUB 1000; * Continue prompt 
* Save current background settings
    BACK.IMAGE = ""; BACK.DISP = ""
    CALL WIN.GETPARAM("BackgroundImage", BACK.IMAGE)
    CALL WIN.GETPARAM("BackgroundDisplay", BACK.DISP)
*
    CALL WIN.BKIMAGE("WintBack.bmp","Tile","")
*
    PRINT "The statement used was:"
    CALL WIN.COLOR(CODE.COL,"")
    PRINT '   CALL WIN.BKIMAGE("WintBack.bmp","Tile","")'
    CALL WIN.COLOR(TEXT.COL,"")
    GOSUB 1000; * Continue prompt
* Restore the users settings
    CALL WIN.SETPARAM("BackgroundImage", BACK.IMAGE)
    CALL WIN.SETPARAM("BackgroundDisplay", BACK.DISP)
    RETURN
*
* Sign off
300 PRINT "This finishes this demonstration of Chiselled effects."
    GOSUB 1000;* Continue prompt
    RETURN
*
* Prompt to continue
1000 INPUT DUM:
     PRINT @(-1):
     RETURN
*
* Format text within TEXT.LEN
2000 LOOP
     UNTIL LEN(TEXT) <= TEXT.LEN DO
       POS = TEXT.LEN
       LOOP
       WHILE TEXT[POS,1] # ' ' DO
          POS = POS - 1
       REPEAT
       PRINT TEXT[1,POS-1]
       TEXT = TEXT[POS+1,9999]
     REPEAT
     PRINT TEXT:
     RETURN
*
END
