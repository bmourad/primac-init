SUBROUTINE WIN.DBATTACH(DLGNAME, PARENT, ERR)
* Attach a modal dialog box to an existing dialog
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
EQU TRUE TO 1, FALSE TO 0  
STX=CHAR(2)
PROMPT ''
ECHO OFF                           
*
ERR=0
*                     
SHOW=FALSE
*
IF DLGNAME = '' THEN ERR = 1
*
* Check if dialog is already displayed
IF ERR = 0 THEN
   CALL WIN.COMSUB("Enter stx:IsShown(":DLGNAME:")")
   GOSUB 100; * Get response
   IF RESP = '1' THEN ERR = 2
END         
*                                     
* Check if dialog exists
IF ERR = 0 THEN
   CALL WIN.COMSUB("Enter stx:IsDialog(":DLGNAME:")")
   GOSUB 100; * Get response
   IF RESP = '0' THEN ERR = 3 ELSE SHOW=TRUE
END
*            
SCRIPT = ''
*
IF SHOW THEN
   SCRIPT='DialogBox Show ':DLGNAME:',':PARENT
   CALL WIN.HSCRIPT(SCRIPT)           
   CALL WIN.COMSUB("If Not(IsShown(":DLGNAME:")) Then Wait Delay 50;Enter stx:IsShown(":DLGNAME:")")
   GOSUB 100; * Get response
   IF RESP[1,1]="D" THEN GOSUB 100;* Ignore Default
   IF RESP # '1' THEN ERR = 4
END
*
ECHO ON
*       
RETURN
*  
* Get response from input
100 LOOP
      INPUT RESP:
    UNTIL INDEX(RESP,STX,1) DO
    REPEAT                 
    RESP = FIELD(RESP,STX,2)
*
    RETURN
*
END
