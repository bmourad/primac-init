SUBROUTINE WIN.DBGETM(DLG.NAME, NAMES, VALUES)
* Get multiple dialog box variables
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
*
* Assumes var contains printable chars, cr and tab only.
* cr and tab are both converted to value mark and sub value mark
*                        
VMSVM=1; VMSVM<1,2>=2; VMSVM<1,2,2>=3; VMSVM<2>=4
AM = VMSVM[6,1]
SVM=VMSVM[4,1]
VM=VMSVM[2,1]
STX=CHAR(2)                    
*
SCRIPT = "With ":DLG.NAME
NUM.NAMES = COUNT(NAMES,AM) + 1
FOR J = 1 TO NUM.NAMES
   SCRIPT = SCRIPT : ";HT SN .":NAMES<J>
NEXT J
*
VALUES = ''
ECHO OFF
PROMPT ''
*
IF LEN(SCRIPT) < 120 THEN
   CALL WIN.COMSUB(SCRIPT)
END ELSE
   CALL WIN.FSCRIPT(SCRIPT)
END
*
FOR J = 1 TO NUM.NAMES
   GOSUB 100;* Get next value
   VALUES<J> = VALUE
NEXT J
*
ECHO ON
*
RETURN
*
* Get the next value from Host Send
100 VALUE = ""
CNV = ""
LOOP
  INPUT DUM:
  IF INDEX(DUM,STX,1) THEN
    DUM = FIELD(DUM,STX,2)
    IF CNV = "" THEN
      CNV = DUM[1,2]
      VALUE = VALUE : DUM[3,9999]
    END ELSE
      VALUE = VALUE : DUM
    END
  END ELSE
    DUM = "X"
  END
UNTIL DUM = "" DO
REPEAT
IF CNV # "no" THEN GOSUB 200;* Convert
RETURN
*
* Convert data
200 TYPE = CNV[1,1]
SUBTYPE= CNV[2,1]
IF TYPE = "B" OR TYPE = "N" THEN
   TYPE = OCONV(TYPE,"MCL")
   VALUE = OCONV(VALUE,"MCL")
END
IF TYPE = "b" THEN
  POS = 1
  LOOP
    C = INDEX(VALUE[POS,99999],'\',1)
  UNTIL C = 0 DO
    POS = POS + C - 1
    CH = VALUE[POS+1,1]
    BEGIN CASE
      CASE CH = 'r'; CH = VM
      CASE CH = 't'; CH = SVM
      CASE CH = '\'; CH = '\'
      CASE CH = 'd'; CH = CHAR(127)
    END CASE
    VALUE = VALUE[1,POS-1]:CH:VALUE[POS+2,99999]
    POS = POS + 1
  REPEAT
END ELSE
  IF TYPE # "n" THEN PRINT "Invalid conversion type ":CNV
END
RETURN
*
END
