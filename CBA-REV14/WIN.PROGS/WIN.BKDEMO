* WIN.BKDEMO
* Background image demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
PROMPT ''
EQU TRUE TO 1, FALSE TO 0
*
C22 = @(0,22):@(-4)
C23 = @(0,23):@(-4)
CMSG = @(0,20):@(-4)
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
PRINT @(-1):"WIN.BKDEMO":
PRINT @(26,0):"Background image demonstration":
PRINT @(71,0):OCONV(DATE(),"D2"):
CALL WIN.BOX(0,1,79,1,1)
*
GOSUB 50;  * Print brief description
*   
GOSUB 100; * Show main demonstation screen
GOSUB 200; * Change backgrounds
*
STOP
*
* Brief description of dialog boxes
50 PRINT @(0,3):
PRINT "wIntegrate can show an image as the background of your emulation screen."
PRINT "This image can be tiled or stretched to fit the whole screen area."
PRINT
PRINT "To change the image from a host program use:-"
PRINT "   CALL WIN.BKIMAGE(IMAGE.NAME, DISP, OPTS)"
PRINT
PRINT "   IMAGE.NAME - Name of the image file"
PRINT '   DISP       - Display option "Stretch" or "Tile"'
PRINT '   OPTS       - Set to "". Reserved for future expansion'
PRINT
PRINT "You can use the host routine WIN.SETEFFCT to change the text colors to give"
PRINT "a better contrast between the text and the image."
PRINT
PRINT "To see different colors, right-click the icon bar area to show the bar_tech"
PRINT "icon bar, choose a color setting from the drop down, then click the rainbow"
PRINT "to apply. Otherwise just use Setup Colors."
*
PRINT C22:'Press <CR> to continue with the demonstration':
INPUT DUM:
RETURN
*
* Main demonstration screen
100 PRINT @(0,3):@(-3):
*
    WINIMAGES = ""
    WINIMAGES<1> = "Clouds.bmp":VM:"Stretch":VM:"Blue"
    WINIMAGES<2> = "Prairie Wind.bmp":VM:"Tile":VM:"White"
*
    ROW = 3
*
    WINDIR = ""
    CALL WIN.EVAL("WindowsDir",WINDIR)
    APPDIR = ""
    CALL WIN.APPDIR(APPDIR)
*
    WIN.CNT = 0
    IMAGES = ""
    N = COUNT(WINIMAGES,AM) + 1
* 
    FOR J = 1 TO N
       CALL WIN.PCFILE(WINDIR:WINIMAGES<J,1>, OK)
       IF OK THEN
          WIN.CNT = WIN.CNT + 1
          IF WIN.CNT = 1 THEN
             PRINT @(0,3):"Images from the Windows folder"
             ROW = ROW + 2
          END
          PRINT @(1,ROW):WIN.CNT:") ":WINIMAGES<J,1>:
          PRINT @(30,ROW):"(":WINIMAGES<J,2>:", ":WINIMAGES<J,3>:")":
          ROW = ROW + 1
*
          IMAGES<WIN.CNT> = WINIMAGES<J>
       END
    NEXT J
*
    FOLDER = APPDIR:"Backgrnd\"
    CALL WIN.PCLIST(FOLDER:"*.*","",BKIMAGES)
    N = COUNT(BKIMAGES,AM) + (BKIMAGES # "")
    IF N THEN
       PRINT @(0,ROW+1):"Images from the BackGrnd folder"
       ROW = ROW + 3
       TOP.BK = ROW - WIN.CNT - 1
       MAX.BK = 14
       IF WIN.CNT THEN MAX.BK = 11 - WIN.CNT
       IF N > MAX.BK THEN N = MAX.BK
       CNT = WIN.CNT
       FOR J = 1 TO N
          EXTN = FIELD(OCONV(BKIMAGES<J>,"MCU"),".",2)
 *
          IF EXTN = "BMP" OR EXTN = "JPG" THEN
             CNT = CNT + 1
             IMAGES<CNT> = BKIMAGES<J>
             PRINT @(1,ROW):CNT:") ":BKIMAGES<J>
             PRINT @(30,ROW):"(Stretch, Black)":
             ROW = ROW + 1
          END
       NEXT J
    END
*
* Set up appropriate display and foreground colour for a
* selection of the images
    COLIMAGES = "";COLS = "";TILE = ""
    COLIMAGES<-1> = "BluePrnt.bmp";COLS<-1> = "LightCyan";TILE<-1> = "Stretch"
    COLIMAGES<-1> = "Coal.jpg"    ;COLS<-1> = "Yellow"   ;TILE<-1> = "Tile"
    COLIMAGES<-1> = "Fedup"       ;COLS<-1> = "Black"    ;TILE<-1> = "Stretch"
    COLIMAGES<-1> = "Gray.jpg"    ;COLS<-1> = "Black"    ;TILE<-1> = "Tile"
    COLIMAGES<-1> = "sand.jpg"    ;COLS<-1> = "Magenta"  ;TILE<-1> = "Tile"
    COLIMAGES<-1> = "Wintback.bmp";COLS<-1> = "Black"    ;TILE<-1> = "Tile"
*
    N = COUNT(COLIMAGES,AM) + 1
    UPIMAGES = IMAGES
    FOR K = 1 TO CNT
       UPIMAGES<K,1> = OCONV(UPIMAGES<K,1>,"MCU")
    NEXT K
*
    FOR J = 1 TO N
       IMNAME = OCONV(COLIMAGES<J>,"MCU")
       FOUND = 0
       FOR K = 1 TO CNT UNTIL FOUND
          IF IMNAME = UPIMAGES<K> THEN FOUND = K
       NEXT K
       IF FOUND THEN
          IMAGES<FOUND,2> = TILE<J>
          IMAGES<FOUND,3> = COLS<J>
          PRINT @(30,TOP.BK+FOUND):"(":TILE<J>:", ":COLS<J>:")":@(-4):
       END
    NEXT J     
*
    RETURN
*
* Change backgrounds on request
200 BACK.IMAGE = "";BACK.DISP = "";EFF.NORMAL = ""
* Save old images
    CALL WIN.GETPARAM("BackgroundImage",BACK.IMAGE)
    CALL WIN.GETPARAM("BackgroundDisplay",BACK.DISP)
    CALL WIN.GETPARAM("Effect_Normal", EFF.NORMAL)
    NORM.COL = FIELD(EFF.NORMAL,",",1)
*
    IMAGE = IMAGES<1,1>
    DISP  = IMAGES<1,2>
    COL   = IMAGES<1,3>
*
    PRINT @(0,19):"Current Image shown with:"
*
    LOOP
       PRINT C22:"Enter number of image, T to tile, S to stretch, or * to exit ":
       INPUT DUM:
       PRINT C23:
    UNTIL DUM = "*" DO
       REDISP = 0
*
       BEGIN CASE
       CASE DUM MATCH "1N0N"
          IF DUM > 0 AND DUM <= CNT THEN
             IMAGE = ""
             IF DUM <= WIN.CNT THEN IMAGE = WINDIR
             IMAGE = IMAGE : IMAGES<DUM,1>
             DISP = IMAGES<DUM,2>
             COL = 0
             IF IMAGES<DUM,3> # "" THEN COL = IMAGES<DUM,3>
             REDISP = 1
          END ELSE
             PRINT C23:"Please enter a number from 1 to ":CNT:" or * to exit":
          END
*
       CASE DUM = "t" OR DUM = "T"
          DISP = "Tile"
          REDISP = 1
*
       CASE DUM = "s" OR DUM = "S"
          DISP = "Stretch"
          REDISP = 1
*
       CASE 1
          PRINT C23:"Invalid entry, Please try again":
       END CASE
*
       IF REDISP THEN
          IF DISP = "" THEN DISP = "Stretch"
          CALL WIN.BKIMAGE(IMAGE, DISP,"")
          CALL WIN.SETEFFCT("Normal",COL,0,"",0)
          PRINT CMSG:' CALL WIN.BKIMAGE("':IMAGE:'","':DISP:'","")':
       END
*
    REPEAT
*
* Restore old images
    CALL WIN.SETPARAM("BackgroundImage",BACK.IMAGE)
    CALL WIN.SETPARAM("BackgroundDisplay",BACK.DISP)
    CALL WIN.SETPARAM("Effect_Normal",EFF.NORMAL)
    RETURN
*
END
