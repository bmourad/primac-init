SUBROUTINE WIN.PCRUN2(PROG, ARGS, OPTS, RESP)
* Run a program on the PC with full options and returns the status
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-98. Impact Business Systems
*
* OPTS are: <1> Show type, <2> Verb, <3> Start in folder
* RESP <= 32 error, > 32 ok
*
STX=CHAR(2)
PROMPT ''
*
SCRIPT = 'Run '
VALUE  = PROG; GOSUB 100;* Quote value and add to script
SCRIPT = SCRIPT : ","
VALUE  = ARGS; GOSUB 100;* Quote value and add to script
SCRIPT = SCRIPT : ",resp"
SCRIPT = SCRIPT : "," : OPTS<1>
SCRIPT = SCRIPT : ","
VALUE  = OPTS<2>; GOSUB 100;* Quote value and add to script
SCRIPT = SCRIPT : ","
VALUE  = OPTS<3>; GOSUB 100;* Quote value and add to script
SCRIPT = SCRIPT : ";Enter stx:resp"
*
CALL WIN.HSCRIPT(SCRIPT)
*
ECHO OFF
LOOP
INPUT RESP:
UNTIL INDEX(RESP,STX,1) DO
REPEAT                    
RESP=FIELD(RESP,STX,2)
ECHO ON
*
RETURN
*
* Quote value and add to script
100 IF VALUE # "" THEN
       QT = '"'
       IF INDEX(VALUE,QT,1) THEN
          QT = "'"
          IF INDEX(VALUE,QT,1) THEN QT = STX
       END
       SCRIPT = SCRIPT : QT : VALUE : QT
    END
*
RETURN
*
END
