SUBROUTINE WIN.HOTSPOT2(R.SPOTS)
* Define/Delete hotspots
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
IF R.SPOTS = '' THEN
   CALL WIN.COMSUB("HotSpot DeleteAll")
END ELSE
   R.SCRIPT = ""
   N = DCOUNT(R.SPOTS, AM)
   FOR J = 1 TO N
     X1=R.SPOTS<J,1>
     Y1=R.SPOTS<J,2>
     X2=R.SPOTS<J,3>
     Y2=R.SPOTS<J,4>
     RVAL=R.SPOTS<J,5>
     DVAL=R.SPOTS<J,6>
     STYL=R.SPOTS<J,7>
     FCOL=R.SPOTS<J,8>
     BCOL=R.SPOTS<J,9>
     IF X2 = '' THEN X2=X1
     IF Y2 = '' THEN Y2=Y1
     IF RVAL = '' THEN RVAL = J
     SCRIPT = "HotSpot Add ": X1 :",": Y1 :",": X2 :",": Y2 :',`':RVAL:'\r`'
*
     IF DVAL # "" OR STYL # "" OR FCOL # "" OR BCOL # "" THEN
        SCRIPT = SCRIPT : ","
        IF DVAL # "" THEN SCRIPT = SCRIPT : '`':DVAL:'\r`'
        SCRIPT = SCRIPT : ","
        IF STYL # "" THEN SCRIPT = SCRIPT : STYL
        SCRIPT = SCRIPT : ","
        IF FCOL # "" THEN
           IF NOT(NUM(FCOL)) AND OCONV(FCOL[1,3],"MCU") # "COL" THEN FCOL = "COL_":FCOL
           SCRIPT = SCRIPT : FCOL
        END
        SCRIPT = SCRIPT : ","
        IF BCOL # "" THEN
           IF NOT(NUM(BCOL)) AND OCONV(BCOL[1,3],"MCU") # "COL" THEN BCOL = "COL_":BCOL
           SCRIPT = SCRIPT : BCOL
        END
     END
*
     R.SCRIPT<-1> = SCRIPT
  NEXT J
*
  CALL WIN.COMSUB(R.SCRIPT)
*
END
*
RETURN
*
END
