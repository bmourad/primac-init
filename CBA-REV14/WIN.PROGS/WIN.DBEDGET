      SUBROUTINE WIN.DBEDGET(DLG.NAME, NAME, VALUE, RET.MV)
* Get a multi line dialogbox edit variable
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
* Assumes var contains printable chars and cr/lf only
* lf is deleted, cr is converted to am or vm depending on RET.MV
*                        
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
      STX=CHAR(2)                    
*
      IF RET.MV THEN SEP = VM ELSE SEP = AM
      VARNAME = DLG.NAME:'.':NAME
*
      SCRIPT = "T=":VARNAME:";Convert T,lf,''"
      SCRIPT = SCRIPT : ";T=AsciiToBS(T)"
      SCRIPT = SCRIPT : ';Enter stx'
      SCRIPT = SCRIPT : ';l=length(T)'
      SCRIPT = SCRIPT : ';j=1'
      SCRIPT = SCRIPT : ';Loop'       
      SCRIPT = SCRIPT : ';While j <= l'
      SCRIPT = SCRIPT : ';Enter stx:Mid(T,j,120),0'
      SCRIPT = SCRIPT : ';j+=120'
      SCRIPT = SCRIPT : ';Repeat'
      SCRIPT = SCRIPT : ';Enter stx'
      CALL WIN.COMSUB(SCRIPT)
*
      VALUE = ''
      ECHO OFF
      PROMPT ''
*
      LOOP
         INPUT DUM:
      UNTIL DUM = STX DO
      REPEAT
*
      LOOP                             
         LOOP
            INPUT DUM:
         UNTIL INDEX(DUM,STX,1) DO
         REPEAT
         DUM =FIELD(DUM,STX,2)
      UNTIL DUM = '' DO
         VALUE = VALUE : DUM
      REPEAT
*
      ECHO ON
*
      POS = 1
      LOOP
         C = INDEX(VALUE[POS,99999],'\',1)
      UNTIL C = 0 DO
         POS = POS + C - 1
         CH = VALUE[POS+1,1]
         BEGIN CASE
            CASE CH = 'r'; CH = SEP
         END CASE
         VALUE = VALUE[1,POS-1]:CH:VALUE[POS+2,99999]
         POS = POS + 1
      REPEAT
*
      RETURN
*
   END
