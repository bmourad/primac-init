* WIN.EDIT
* Invoke the wIntegrate host program editor
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-1998. Impact Business Systems
*
* Set up machine specific subroutines
OPEN '','WIN.PARAMS' TO F.BP ELSE
  OPEN '','WIN.PROGS' TO F.BP ELSE
    PRINT "UNABLE TO OPEN WIN.PARAMS OR WIN.PROGS"
    STOP
  END
END
*
READ MACHINE.TYPE FROM F.BP, "MACHINE.TYPE" ELSE MACHINE.TYPE = "GENERIC"
MACHINE.TYPE = MACHINE.TYPE<1>
*
SUB.SUFFIX = ''
IF MACHINE.TYPE # 'GENERIC' THEN SUB.SUFFIX = '.' : MACHINE.TYPE
ARGSUB = "WIN.ARGS" : SUB.SUFFIX
*
ARGS = ""
CALL @ARGSUB(ARGS)
FILENAME = ARGS<2>
ITEM = ARGS<3>
*
* Check Filename is valid
IF FILENAME # "" THEN
OPEN '',FILENAME TO F.BP ELSE PRINT "UNABLE TO OPEN FILE";STOP
  * Check item exist
  IF ITEM # "" THEN
    READV CHECK FROM F.BP,ITEM,1 ELSE
      PRINT "Item ":ITEM:" not on file. Create (Y/N)":
      INPUT RESP
      IF OCONV(RESP,"MCU") # "Y" THEN STOP
      WRITE "" ON F.BP,ITEM
    END
  END
END
*
* Build and run script to startup the editor
SCRIPT = 'Run AppDir:"WintEdit.exe"'
SCRIPT = SCRIPT : ',`'
SCRIPT = SCRIPT : '-session "`:FullName:`"'
SCRIPT = SCRIPT : ' -file "':FILENAME:'"'
SCRIPT = SCRIPT : ' -item "':ITEM:'"'
SCRIPT = SCRIPT : "`"
CALL WIN.FSCRIPT(SCRIPT)
*
END
