* WIN.DBDEMO4
* Dialogbox demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
PROMPT ''
EQU TRUE TO 1, FALSE TO 0
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
C22 = @(0,22):@(-4)
DT = DATE()
MIN.DATE = DT - 730
MAX.DATE = DT + 730
*
PRINT @(-1):"WIN.DBDEMO4":
PRINT @(18,0):"Dialog box demonstration":
PRINT @(71,0):OCONV(DATE(),"D2"):
CALL WIN.BOX(0,1,79,1,1)
*
GOSUB 50; * Print brief description
*   
STATE=''
CALL WIN.SSTATE(STATE,'')
*
GOSUB 100; * Create and show dialog
IF ERR = 0 THEN
*                                   
  DLG=''
  CTRL=''
  EVENT=''
  FINISHED=FALSE
*
  CALL WIN.COLOR("Yellow","Blue")
  CALL WIN.TWOPEN("DLGDEMO", "Events received", 10,8,70,20,"SINGLE")
  CALL WIN.COLOR("White","")
*
  ESTACK = ""
  ARGS = ""
*
  LOOP
    CALL WIN.DBEVENT2(DLG,CTRL,EVENT,ARGS,ESTACK)
*
    BEGIN CASE
      CASE EVENT = 'U'; EMSG = 'Unknown'
      CASE EVENT = 'C'; EMSG = 'Button Click'
      CASE CTRL = "TB" AND EVENT = "1"; EMSG = "Scroll"
      CASE CTRL = "DT" AND EVENT = "1"; EMSG = "Changed"
      CASE 1
        EMSG = 'Event ':EVENT
    END CASE
*
    IF DLG # '' THEN
      EMSG = EMSG : ' ':DLG:' Control ':CTRL
    END ELSE
      EMSG = EMSG : ' data ':CTRL
    END
    PRINT EMSG
*
    BEGIN CASE
      CASE DLG = ''; IF CTRL ='*' THEN FINISHED=TRUE
      CASE DLG = DLG.NAME; GOSUB 1000; * Process DlgDemo events
    END CASE
*
  UNTIL FINISHED DO
  REPEAT
*
  CALL WIN.TWCLOSE("DLGDEMO")
*
  CALL WIN.DBDEL(DLG.NAME)
*
END
*
CALL WIN.SSTATE('',STATE)
PRINT C22:'Press <CR> to Exit demonstration':
INPUT DUM:                                 
*
STOP
*
* Brief description of dialog boxes
50 PRINT @(0,3):
PRINT "This demonstration creates a dialog box on the host that contains a track"
PRINT "bar control and a date control."
PRINT
PRINT "Changing the value of either these controls updates the other and also"
PRINT "moves some static line controls around the dialog."
PRINT
PRINT "The specific routines demonstrated here are:"
PRINT "  WIN.DBTRACK  - Create a track bar control"
PRINT "  WIN.DBDTTIME - Create a date time control"
PRINT "  WIN.DBCTRL   - Create a generic control"
PRINT "  WIN.DBRECT   - Return the size and position of a control"
PRINT "  WIN.DBMVCTRL - Move or resize a control"
PRINT
PRINT "This program also shows how to initialize/set/get a controls property"
PRINT "with the use of:"
PRINT "  CALL WIN.DBINIPRP - Set an initial value for a controls property"
PRINT "  CALL WIN.DBSETPRP - Set a property after the dialog has been shown"
PRINT "  CALL WIN.DBGETPRP - Get a property after the dialog has been shown"
*
PRINT C22:"Press <CR> to continue demonstration":
INPUT DUM:
RETURN
*
* Create and show dialog
100 DBX=''
DLG.NAME="DemoDlg4"
CALL WIN.TWMSG("Creating Dialog ":DLG.NAME:"...", "Red", "Yellow", "DOUBLE")
*
VER = ''
CALL WIN.VERSION(VER)
OPTS = ""
IF VER >= "2.5" THEN OPTS<2> = 1
*
DEMO.TEXT = "This example show the use of the Track bar and date time controls"
DEMO.TEXT = DEMO.TEXT : " and how to move controls around the dialog"
CALL WIN.DBNEW(DBX, DLG.NAME, "Dialog box date/time control and moving controls", 0,0,308,106, 'WS_CAPTION|WS_BORDER|WS_SYSMENU',OPTS)
CALL WIN.DBTRACK(DBX, "TB",16,31,276,20,"TBS_HORZ|TBS_AUTOTICKS|TBS_BOTTOM|TBS_RIGHT")
CALL WIN.DBINIPRP(DBX, "TB","RangeMin",MIN.DATE)
CALL WIN.DBINIPRP(DBX, "TB","RangeMax",MAX.DATE)
CALL WIN.DBINIPRP(DBX, "TB","TickFrequency",56)
CALL WIN.DBEVENTS(DBX,"TB", "Scroll")
CALL WIN.DBDTTIME(DBX, "DT",111,86,69,12,"DTS_CALENDAR|DTS_UPDOWN|WS_TABSTOP")
CALL WIN.DBINIPRP(DBX, "DT", "ValidMode",2)
CALL WIN.DBINIPRP(DBX, "DT","DateMin", OCONV(MIN.DATE,"D2/"))
CALL WIN.DBINIPRP(DBX, "DT","DateMax", OCONV(MAX.DATE,"D2/"))
CALL WIN.DBEVENTS(DBX, "DT","Changed")
CALL WIN.DBLABEL(DBX,DEMO.TEXT,16,4,220,24,"L")
CALL WIN.DBCTRL(DBX, "None","static","",140,70,6,12,"SS_ETCHEDVERT|WS_VISIBLE")
CALL WIN.DBCTRL(DBX, "HL","static","",140,70,41,12,"SS_ETCHEDHORZ|WS_VISIBLE")
CALL WIN.DBCTRL(DBX, "VL","static","",100,58,6,14,"SS_ETCHEDVERT|WS_VISIBLE")
CALL WIN.DBLABEL(DBX, "Min date:",16,74,40,10,"L")
CALL WIN.DBLABEL(DBX, "Max date:",200,74,92,10,"R")
CALL WIN.DBLABEL(DBX, OCONV(MIN.DATE,"D2/"), 16,86,40,12,"L")
CALL WIN.DBLABEL(DBX, OCONV(MAX.DATE,"D2/"), 200,86,92,12,"R")
*
CALL WIN.DBBUTTON(DBX, "Cancel","&Close",242,7,50,14,0)
*
CALL WIN.DBLOAD(DBX,ERR)
IF ERR THEN       
  ERR.MSG='Loading with WIN.DBLOAD'
* Just delete dialog and try again if we get already exist error
  IF ERR = 3 THEN
    CALL WIN.DBDEL(DLG.NAME)
    CALL WIN.DBLOAD(DBX,ERR)
  END                                                          
END
*         
IF ERR = 0 THEN
  CALL WIN.DBSHOW(DLG.NAME, 1, ERR)
  IF ERR THEN
    CALL WIN.DBDEL(DLG.NAME)
    ERR.MSG = 'Displaying with WIN.DBSHOW'
  END ELSE
* Get sizes for positioning controls
    CALL WIN.DBRECT(DLG.NAME, "TB", 0, TB.LEFT, TB.TOP, TB.RIGHT, TB.BOTTOM)
    CALL WIN.DBGETPRP(DLG.NAME,"TB","THUMBLENGTH",TB.THUMB)
    TB.WIDTH = TB.RIGHT - TB.LEFT - TB.THUMB
    TB.LEFT = TB.LEFT + INT(TB.THUMB/2)
    CALL WIN.DBRECT(DLG.NAME, "HL", 0, HL.LEFT, HL.TOP, HL.RIGHT, HL.BOTTOM)
    CALL WIN.DBSETPRP(DLG.NAME, "TB","Position",DATE())
    GOSUB 1100; * Set initial positions
  END
END
*
*
CALL WIN.TWCLOSE("MSG")
*
IF ERR THEN
  CALL WIN.COLOR("Red","White")
  PRINT @(0,18):@(-4):ERR.MSG:' failed, Error Number ':ERR:
END
*
RETURN
*
* Process dialog DlgDemo events
1000 BEGIN CASE
  CASE CTRL='Cancel'
    CALL WIN.DBEND(DLG.NAME,FALSE)
    FINISHED=TRUE
  CASE CTRL="TB"; * Only getting Scroll event for this
* Throw away extra track bar events
    LOOP
      CALL WIN.DBESTACK(ESTACK)
    UNTIL ESTACK = "" DO
      ESTACK = ""
    REPEAT
    CALL WIN.DBGET(DLG.NAME,"TB", DT)
* Update date control
    CALL WIN.DBSETPRP(DLG.NAME,"DT","Value",OCONV(DT,"D2/"))
    GOSUB 1100;* Move controls
  CASE CTRL = "DT"
    CALL WIN.DBGETPRP(DLG.NAME,"DT","Value",DT)
* Update track bar
    DT = ICONV(DT,"D")
    IF DT = "" THEN PRINT "Warning! Host and PC date format mismatch"
    CALL WIN.DBSETPRP(DLG.NAME,"TB","POSITION",DT)
    GOSUB 1100;* Move controls
END CASE
*
RETURN
*
* Move controls depending on the date
1100 POS = (DT - MIN.DATE) * TB.WIDTH / (MAX.DATE - MIN.DATE) + TB.LEFT
POS = INT(POS)
CALL WIN.DBMVCTRL(DLG.NAME, "VL", POS, "","","",0)
IF POS <= HL.LEFT  THEN
  NEW.LEFT = POS
  NEW.WIDTH = HL.LEFT - POS + 1
END ELSE
  NEW.LEFT = HL.LEFT
  NEW.WIDTH = POS - HL.LEFT + 1
END
CALL WIN.DBMVCTRL(DLG.NAME, "HL", NEW.LEFT,"",NEW.WIDTH,"",0)
RETURN
END
