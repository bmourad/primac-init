SUBROUTINE WIN.SSUB(ACTION, MAT SS.ARGS, ARG)
* Service subroutine for WIN.SERVER/WIN.TRANSFER
* Compile for:
* (C) Copyright IBM Corporation 2003. All rights reserved
*
* This is an example Service routine.
*
* This routine reads the record on WIN.PROGS/WIN.PARAMS
* "FILE.SELECT" to determine which file/items
* are selected to show in the query builder
* FILE.SELECT
*   Field 1 - Alternative file to SSELECT
*   Field 2 - Alternative SELECTION to use
*   Field 3 - Alternative dictionary selection
*      <FILE> replaced by real filename
*
* Arguments for Service subroutine
DIM SS.ARGS(30)
EQU SS.PROGS  TO SS.ARGS(1)
EQU SS.PARAM  TO SS.ARGS(2)
EQU SS.RESULT TO SS.ARGS(3)
EQU SS.EXTRA1 TO SS.ARGS(4)
EQU SS.EXTRA2 TO SS.ARGS(5)
*
* User defined attributes
EQU FILE.INFO TO SS.ARGS(10)
*
EQU SSA.INIT    TO 1
EQU SSA.FILESEL TO 2
EQU SSA.FILECHK TO 3
EQU SSA.FIELDSEL TO 4
EQU SSA.FIELDCHK TO 5
EQU SSA.EXECCHK  TO 6
*
BEGIN CASE
CASE ACTION = SSA.INIT; GOSUB 100; * Initialise
CASE ACTION = SSA.FILESEL; GOSUB 200; * File selection
CASE ACTION = SSA.FIELDSEL; GOSUB 400;* Field selection
END CASE
*
RETURN
*
* Initialise subroutine
* Set up when we want this routine called
100 ARG = ''
OPEN.OK = 1
OPEN '','WIN.PARAMS' TO F.BP ELSE
  OPEN '','WIN.PROGS' TO F.BP ELSE OPEN.OK = 0
END
IF OPEN.OK THEN
   READ FILE.INFO FROM F.BP, "FILE.SELECT" THEN
      ARG<SSA.FILESEL> = 1
      IF FILE.INFO<3> # "" THEN ARG<SSA.FIELDSEL> = 1
   END
END
RETURN
*
* Set up file selection
* FILE.INFO<1> is the filename
* FILE.INFO<2> is the selection which totaly replaces default
200 BEGIN CASE
    CASE FILE.INFO<2> # ""
      ARG = FILE.INFO<2>
    CASE FILE.INFO<1> # ""
      ARG = "SSELECT ":FILE.INFO<1> 
    END CASE
    SS.RESULT = 1
RETURN
*
* Set up field selections
* Replace <FILE> in FILE.SELECT field 3 and then use this
* as selection executed.
* Filename is in SS.EXTRA1
* Just replace ARG
400 ARG = ""
    VALUE = FILE.INFO<3>
    S = "<FILE>"; R = SS.EXTRA1
    GOSUB 650;* Replace
    SS.RESULT = 1
    ARG = VALUE
*
   RETURN
*
* Replace character S with character R in VALUE  
650 LS = LEN(S)
      LOOP                                       
         C = INDEX(VALUE, S,1)                   
      WHILE C DO                                 
         VALUE = VALUE[1,C-1]:R:VALUE[C+LS, 99999]
      REPEAT                                     
      RETURN
