* WIN.DBDEMO2
* Dialog box demo of menus, fonts and colors
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
      PROMPT ''
      EQU TRUE TO 1, FALSE TO 0
*
      C22 = @(0,22):@(-4)
      C23 = @(0,23):@(-4)
      CMSG = @(0,20):@(-4)
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
      OPEN '','WIN.PROGS' TO F.BP ELSE
         PRINT "Unable to open WIN.PROGS"
         STOP
      END
*
      PRINT @(-1):"WIN.DBDEMO2":
     PRINT @(18,0):"Dialog box demo of menus, fonts and colors":
      PRINT @(71,0):OCONV(DATE(),"D2"):
      CALL WIN.BOX(0,1,79,1,1)
*
      GOSUB 300; * Set up how to text
*
      GOSUB 50; * Print brief description
*   
STATE=''
CALL WIN.SSTATE(STATE,'')
*
GOSUB 200; * Create Menu for dialog box
GOSUB 100; * Create and show dialog
IF ERR = 0 THEN
*                                   
DLG=''
CTRL=''
EVENT=''
FINISHED=FALSE
*
CALL WIN.COLOR("Yellow","Blue")
CALL WIN.TWOPEN("DLGDEMO", "Events received", 10,8,70,20,"SINGLE")
CALL WIN.COLOR("White","")
*
LOOP
   CALL WIN.DBEVENT(DLG,CTRL,EVENT)
*
   BEGIN CASE
   CASE EVENT = 'U'; EMSG = 'Unknown'
   CASE EVENT = 'M'; EMSG = 'Menu'
   CASE EVENT = 'V'; EMSG = 'Validation'
   CASE EVENT = 'D'; EMSG = 'Default'
   CASE EVENT = 'C'; EMSG = 'Button Click'
   CASE EVENT = 'DC'; EMSG = 'Double Click'
   CASE EVENT = 'S'; EMSG= 'Scroll'
   CASE 1
        EMSG = 'Event ':EVENT
   END CASE
*
   IF DLG # '' THEN
      IF EVENT = "M" THEN
         EMSG = EMSG : " ":DLG:" Option ":CTRL
      END ELSE
         EMSG = EMSG : ' ':DLG:' Control ':CTRL
      END
   END ELSE
      EMSG = EMSG : ' data ':CTRL
   END
   PRINT EMSG
   CALL WIN.DBSET(DLG.NAME, "Event", EMSG)
*
   BEGIN CASE
   CASE DLG = ''; IF CTRL ='*' THEN FINISHED=TRUE
   CASE DLG = DLG.NAME; GOSUB 1000; * Process DlgDemo events
   CASE DLG = "DDFileMenu"; GOSUB 4100; * File Menu options
   CASE DLG = "DDHowToMenu"; GOSUB 4200; * How to Menu options
   END CASE
*
UNTIL FINISHED DO
REPEAT
*
CALL WIN.TWCLOSE("DLGDEMO")
*
CALL WIN.DBDEL(DLG.NAME)
CALL WIN.MENUDEL("DDMenuBar")
CALL WIN.MENUDEL("DDFileMenu")
CALL WIN.MENUDEL("DDHowToMenu")
*
END
*
CALL WIN.SSTATE('',STATE)
PRINT C22:'Press <CR> to Exit demonstration':
INPUT DUM:                                 
*
STOP
*
* Brief description of dialog boxes
50 PRINT @(0,3):
PRINT "The advanced options covered in this program were first introduced"
PRINT "in wIntegrate 3.0. They allow you to set the color and font for"
PRINT "controls and to add a menu to your dialog box."
PRINT "See the WIN.DBDEMO demonstration for details of how to create"
PRINT "a dialog and process its events. See the WIN.MENUDEMO demo for how"
PRINT "to create a menu."
PRINT
*
FOR J = 1 TO 3
   N = COUNT(HOWS<J>,VM) + 1
   FOR K = 1 TO N
     PRINT HOWS<J,K>
   NEXT K
NEXT J
*
PRINT C22:"Press <CR> to continue demonstration":
INPUT DUM:
RETURN
*
* Create and show dialog
100 DBX=''
DLG.NAME="DemoDlg2"
CALL WIN.TWMSG("Creating Dialog ":DLG.NAME:"...", "Red", "Yellow", "DOUBLE")
*
VER = ''
CALL WIN.VERSION(VER)
OPTS = ""
IF VER >= "2.5" THEN OPTS<2> = 1
*
CALL WIN.DBNEW(DBX, DLG.NAME, "Dialog box menus, fonts and colors", 10,10,200,160, 'WS_CAPTION|WS_BORDER|WS_SYSMENU',OPTS)
CALL WIN.DBSETMNU(DBX, "DDMenuBar")
*
CALL WIN.DBTEXT(DBX, "T1",56,0,84,20,"L")
CALL WIN.DBSETFNT(DBX, "T1", "Times New Roman", 20, "FONT_Bold|FONT_Italic")
CALL WIN.DBSETCOL(DBX, "T1", "Blue","")
CALL WIN.DBBUTTON(DBX, "Cancel","&Close",148,7,50,14,0)
CALL WIN.DBLABEL(DBX, "Host &Program:",7,24,"","","")
CALL WIN.DBEDIT(DBX, "ProgName",56,23,84,12, "")
CALL WIN.DBBUTTON(DBX, "Summary","&Summary",148,23,50,14,0)
CALL WIN.DBGROUP(DBX,"None", "Description",7,45,179,79)
CALL WIN.DBEDIT(DBX, "Desc",13,57,169,62,"ES_LEFT|ES_MULTILINE|ES_READONLY|WS_BORDER|WS_VSCROLL|WS_TABSTOP")
CALL WIN.DBSETCOL(DBX, "Desc", "LightCyan", "Magenta")
CALL WIN.DBLABEL(DBX, "Last Event:",7,130,40,"","")
CALL WIN.DBTEXT(DBX, "Event", 51,130,134,"","")
CALL WIN.DBSETCOL(DBX, "Event", "White", "Blue")
CALL WIN.DBSETFNT(DBX, "Event", "Times New Roman",10,"")
CALL WIN.DBSETCOL(DBX, "None", "Blue","")
*
CALL WIN.DBLOAD(DBX,ERR)
IF ERR THEN       
   ERR.MSG='Loading with WIN.DBLOAD'
* Just delete dialog and try again if we get already exist error
   IF ERR = 3 THEN
      CALL WIN.DBDEL(DLG.NAME)
      CALL WIN.DBLOAD(DBX,ERR)
   END                                                          
END
*         
IF ERR = 0 THEN
   CALL WIN.DBSHOW(DLG.NAME, 1, ERR)
   IF ERR THEN
      CALL WIN.DBDEL(DLG.NAME)
      ERR.MSG = 'Displaying with WIN.DBSHOW'
   END
END
*
IF ERR = 0 THEN
   CALL WIN.DBSET(DLG.NAME,"T1","wIntegrate")
   CTRL = NO.HOWS; GOSUB 4200
END
*
CALL WIN.TWCLOSE("MSG")
*
IF ERR THEN
   CALL WIN.COLOR("Red","White")
   PRINT @(0,18):@(-4):ERR.MSG:' failed, Error Number ':ERR:
END
*
RETURN
*
* Create Menu for dialog box
200 FILEMENU = 'E&xit'
    HOWMENU = ''
    FOR J = 1 TO NO.HOWS
       HOWMENU<J> = HOWS<J,1>
    NEXT J
    MENUBAR = '>&File':VM:'DDFileMenu'
    MENUBAR<-1> = ">&How To":VM:'DDHowToMenu'
    CALL WIN.MENULOAD("DDHowToMenu", HOWMENU)
    CALL WIN.MENULOAD("DDFileMenu", FILEMENU)
    CALL WIN.MENULOAD("DDMenuBar", MENUBAR)
    RETURN
*
* Set up descriptions for HOWMENU
300 HOWS = ''
    HOWS<1> = "To put a menu on a dialog box:"
    HOWS<1,-1> = "CALL WIN.DBSETMNU(DBX, MENUNAME)"
    HOWS<1,-1> = "Call this after the dialog box has been defined with WIN.DBNEW. The Menu must be created before the dialog box is loaded with WIN.DBLOAD"
*
    HOWS<2> = "To set the font for a control:"
    HOWS<2,-1> = "CALL WIN.DBSETFNT(DBX, CTRL, FONTNAME, PTSIZE, STYLE)"
    HOWS<2,-1> = "Call this after you have defined the control."
*
    HOWS<3> = "To set the color for a control:"
    HOWS<3,-1> = "CALL WIN.DBSETCOL(DBX, CTRL, FORECOL, BACKCOL)"
    HOWS<3,-1> = "Call this after you have defined the control."
    HOWS<3,-1> = "Colors are specified in the same way they are for WIN.DRPEN"
*
    HOWS<4> = "To create the menu:"
    HOWS<4,-1> = "The menu is created using the standard menu creation routine WIN.MENULOAD"
*
* Last option is also the startup description
    HOWS<5> = "How to use this program:"
    HOWS<5,-1> = "This program shows some of the more advanced dialog box features."
    HOWS<5,-1> = "A dialog box can have menu options. Most controls can use any color or font."
    HOWS<5,-1> = "Choose an item from the How To menu for summary information."
    HOWS<5,-1> = "You can also enter a host program name in the edit box above, then click on the Summary button to see a brief summary of the program from the file WIN.PROGS."
*
    NO.HOWS = COUNT(HOWS, AM) + 1
    RETURN
*
* Process dialog DlgDemo events
1000 BEGIN CASE
     CASE CTRL='Cancel'
          CALL WIN.DBEND(DLG.NAME,FALSE)
          FINISHED=TRUE
     CASE CTRL = "Summary"
          GOSUB 1100; * Get parameters Summary for a host program
     END CASE
*
     RETURN
*
* Display first two lines of program
1100 PROG.ID = ''
     CALL WIN.DBGET(DLG.NAME, "PROGNAME", PROG.ID)
     IF PROG.ID = "" THEN
        DESC = "No program name specified"
     END ELSE
        READV DUM FROM F.BP, PROG.ID,1 ELSE DUM = ""
        IF DUM = "" THEN
           DESC = PROG.ID:" is not a valid host program name"
        END ELSE
           DESC = DUM
           READV DUM FROM F.BP, PROG.ID,2 ELSE DUM = ""
           DESC = DESC : AM : DUM
        END
     END
*
     CALL WIN.DBEDSET(DLG.NAME, "DESC", DESC)
*
     RETURN
*
* File Menu options
4100 BEGIN CASE
     CASE CTRL = 1; * Exit
        CALL WIN.DBEND(DLG.NAME, FALSE)
        FINISHED = TRUE
     END CASE
*
     RETURN
*
* How to menu options
4200 CALL WIN.DBEDSET(DLG.NAME, "Desc", HOWS<CTRL>)
     RETURN
*
END
