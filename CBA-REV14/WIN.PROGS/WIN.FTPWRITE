      SUBROUTINE WIN.FTPWRITE(BYTES, CNVT)
* Write to a fTP file
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
   EQU MAXLENGTH TO 200
   HQ = CHAR(2)
   WC = "WIN.COMSUB"  
   SEND.VAL = BYTES
   AMVM=1; AMVM<1,2>=2; AMVM<2>=3
   AM=AMVM[4,1]
   VM=AMVM[2,1]
*
   CNVT=OCONV(CNVT,"MCU")
*
   BEGIN CASE
     CASE CNVT = ""
       CALL WIN.ASCTOFT(SEND.VAL)
       FRMT="DF_FT"
     CASE CNVT = "TEXT"
       S=AM
       R=CHAR(13):CHAR(10)
       GOSUB 100;* Convert AM to CR/LF
       CALL WIN.ASCTOFT(SEND.VAL)
       FRMT="DF_FT"
     CASE CNVT = "HEX"; FRMT="DF_Hex"
     CASE CNVT = "FT"; FRMT="DF_FT"
   END CASE
*                
   LEN.SEND.VAL = LEN(SEND.VAL)
*
   IF LEN.SEND.VAL + 32 < MAXLENGTH THEN
     SCRIPT.SEND.VAL = HQ:SEND.VAL:HQ
   END ELSE
     CALL @WC("Global TRV=''")
     SCRIPT.SEND.VAL = "TRV"
*
     POS = 1
     LOOP
       NO.CHARS = MAXLENGTH - 6
       IF NO.CHARS > LEN.SEND.VAL THEN NO.CHARS = LEN.SEND.VAL
       CALL @WC("TRV:=":HQ:SEND.VAL[POS,NO.CHARS]:HQ)
       LEN.SEND.VAL = LEN.SEND.VAL - NO.CHARS
     WHILE LEN.SEND.VAL > 0 DO
       POS = POS + NO.CHARS
     REPEAT
*                                           
   END                                   
*
   CALL @WC("Ftp Write ":SCRIPT.SEND.VAL:",":FRMT)
*
   RETURN
*
* Convert S to R in SEND.VAL
100 POS = 1
   LOOP
     C = INDEX(SEND.VAL[POS,99999],S,1)
   WHILE C DO      
     POS = POS + C - 1
     SEND.VAL = SEND.VAL[1,POS-1] : R : SEND.VAL[POS+1,99999]
     POS = POS + LEN(R)
   REPEAT                                             
*
   RETURN
*
   END
