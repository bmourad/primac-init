SUBROUTINE WIN.IMPORT(PCFILE,FILE,ITEMS,FIELDS,OPTS,RET.STATUS)
* File Import, i.e. Host to PC
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-2000 Impact Business Systems.
*                     
PROMPT ''
STX=CHAR(2)
*
OPEN '','WIN.PARAMS' TO F.BP ELSE
  OPEN '','WIN.PROGS' TO F.BP ELSE STOP 'NO WIN.PROGS'
END
READV MACHINE.TYPE FROM F.BP,"MACHINE.TYPE",1 ELSE MACHINE.TYPE =''
IF MACHINE.TYPE ='' THEN MACHINE.TYPE ='GENERIC'
SUB.SUFFIX = ''
IF MACHINE.TYPE # 'GENERIC' THEN SUB.SUFFIX = '.':MACHINE.TYPE
*
EXECSUB = "WIN.EXEC":SUB.SUFFIX
*
R.SCRIPT = "Dialog RunImportFile"
ACCOUNT = ''
IF FILE[1,1] = '(' THEN
   ACCOUNT = FIELD(FILE,' ',1)[2,99]
   FILE = FIELD(FILE,' ',2)
END
*
R.SCRIPT<-1> = "Set Account = '":ACCOUNT:"'"
R.SCRIPT<-1> = "Set File ='":FILE:"'"
R.SCRIPT<-1> = "Set Items = '":ITEMS[1,240]:"'"
L = LEN(ITEMS)
FOR J = 241 TO L STEP 240
   R.SCRIPT<-1> = "Set Items := '":ITEMS[J,240]:"'"
NEXT J
R.SCRIPT<-1> = "Set Fields = '":FIELDS[1,240]:"'"
L = LEN(FIELDS)
FOR J = 241 TO L STEP 240
   R.SCRIPT<-1> = "Set Fields := '":FIELDS[J,240]:"'"
NEXT J
R.SCRIPT<-1> = "Set DosFile = '":PCFILE:"'"
*
* Set format
DUM = OPTS<1>
IF DUM = '' THEN
   DOT.CNT = COUNT(PCFILE,".")
   IF DOT.CNT THEN DUM = FIELD(PCFILE, ".",DOT.CNT+1)
   IF OCONV(DUM,"MCU") = "TXT" THEN DUM = "ASC"
END
R.SCRIPT<-1> = "Set Format = '":DUM:"'"
*
* Overwrite
DUM = OPTS<2>
IF DUM = '' THEN DUM = "Yes"           
R.SCRIPT<-1> = "Set Overwrite='":DUM:"'"
* Mode
DUM = OPTS<3>
IF DUM = '' THEN DUM = "Normal"
R.SCRIPT<-1> = "Set Mode = '":DUM:"'"
* Suppress Id
DUM = OPTS<4>
IF DUM = '' THEN DUM = 0
R.SCRIPT<-1> = "Set SuppressId=":DUM     
* Field descriptions
DUM = OPTS<5>
IF DUM = '' THEN DUM = 0
R.SCRIPT<-1>="Set FieldDescriptions=":DUM
* Translate
DUM = OPTS<6>
IF DUM = '' THEN DUM = "Ascii"
R.SCRIPT<-1>="Set Translate='":DUM:"'"
* Translation table
DUM =OPTS<7>
IF DUM = '' THEN DUM="\255,\r\n\f\r\n,\254,\r\n"
R.SCRIPT<-1>="Set Translation='":DUM:"'"
* Auto Exit
DUM = OPTS<8>
IF DUM = '' THEN DUM = 1
R.SCRIPT<-1>="Set AutoExit=":DUM
* Inform
DUM = OPTS<9>
IF DUM = '' THEN DUM = 0
R.SCRIPT<-1>="Set Inform=":DUM
* Time out
DUM = OPTS<10>
IF DUM='' THEN DUM = 5
R.SCRIPT<-1>="Set Timeout=":DUM
* Retries
DUM = OPTS<11>     
IF DUM = '' THEN DUM = 3
R.SCRIPT<-1>="Set Retries=":DUM
* Numeric conversion
R.SCRIPT<-1> = "If Version > '3.0.00' Then"
DUM = OPTS<13,1>; IF DUM = "" THEN DUM = "0"
R.SCRIPT<-1>= 'Set NumberConversion=':DUM
IF DUM = "1" THEN
   DUM = OPTS<13,2>; IF DUM = "" THEN DUM = ","
   R.SCRIPT<-1> = 'Set NumberSeparator="':DUM:'"'
   DUM = OPTS<13,3>; IF DUM = "" THEN DUM = "$"
   R.SCRIPT<-1> = 'Set NumberCurrency="':DUM:'"'
   DUM = OPTS<13,4>; IF DUM = "" THEN DUM = "."
   R.SCRIPT<-1> = 'Set NumberDecimal="':DUM:'"'
END
* Explode Multi-values
R.SCRIPT<-1> = "If Version >= '3.0.03' Then"
DUM = OPTS<14,1>; IF DUM = "" THEN DUM = "0"
R.SCRIPT<-1> = 'Set ExplodeValues=':DUM
DUM = OPTS<14,2>; IF DUM = "" THEN DUM = "0"
R.SCRIPT<-1> = 'Set RepeatValues=':DUM
*
* Formatting information
R.SCRIPT<-1> = "If Version >= '4.1' Then"
IF OPTS<15,1> # "" THEN R.SCRIPT<-1> = "Set UseFormattingInformation=":OPTS<15,1>
IF OPTS<15,2> # "" THEN R.SCRIPT<-1> = "Set LeftJustifiedIsText=":OPTS<15,2>
IF OPTS<15,3> # "" THEN R.SCRIPT<-1> = "Set RightJustifiedIsNumeric=":OPTS<15,3>
*
R.SCRIPT<-1> = "EndIf" ;* >= 4.1 test
R.SCRIPT<-1> = "EndIf" ;* >= 3.0.03
R.SCRIPT<-1> = "EndIf" ;* > 3.0 test
*
R.SCRIPT<-1>="Invoke"
* Mac file type creator
MTYPE = OPTS<12,1>
MCREATOR = OPTS<12,2>
IF MTYPE # "" OR MCREATOR # "" THEN
   R.SCRIPT<-1> = 'If DesktopType = "MAC" And ReturnValue = "OK" Then'
   IF MTYPE # "" THEN R.SCRIPT<-1> = 'File SetType Get(DosFile), "':MTYPE:'"'
   IF MCREATOR # "" THEN R.SCRIPT<-1> = 'File SetCreator Get(DosFile), "':MCREATOR:'"'
   R.SCRIPT<-1> = "EndIf"
END
R.SCRIPT<-1>="Enter stx:ReturnValue"   
*                         
* Start PC side of transfer
CALL WIN.HSCRIPTC(R.SCRIPT)
*
* Wait for PC to send down WIN.TRANSFER program name
* or error code if import fails to start
ECHO OFF
LOOP
  INPUT RET.STATUS:
UNTIL RET.STATUS # "" OR INDEX(RET.STATUS,STX,1) DO
REPEAT
ECHO ON
*
IF INDEX(RET.STATUS,STX,1) = 0 THEN
* Start host size of transfer
   CALL @EXECSUB("WIN.TRANSFER",'','')
*
* Get result
   ECHO OFF
   LOOP
     INPUT RET.STATUS:
   UNTIL INDEX(RET.STATUS,STX,1) DO
   REPEAT
   ECHO ON
END
RET.STATUS = FIELD(RET.STATUS,STX,2)
*
RETURN
*
END
