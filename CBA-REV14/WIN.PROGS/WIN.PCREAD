      SUBROUTINE WIN.PCREAD(NAME, VALUE, MAX.BYTES, CNVT)
* Get a data from a PC file
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
      STX=CHAR(2)
      CNVT = OCONV(CNVT,"MCU")
*
      BEGIN CASE           
      CASE CNVT = "" OR CNVT = "TEXT"; FRMT="DF_FT"
      CASE CNVT = "FT";FRMT ="DF_FT"
      CASE CNVT = "HEX";FRMT = "DF_Hex"            
      CASE 1; FRMT=""
      END CASE
*               
SCRIPT = "File Read ":NAME:",T,":MAX.BYTES:",":FRMT
IF CNVT = "TEXT" THEN SCRIPT = SCRIPT : ';T=Change(T,"}-}*","~")'
      SCRIPT = SCRIPT : ';l=length(T)'
      SCRIPT = SCRIPT : ';j=1'
      SCRIPT = SCRIPT : ';Loop'       
      SCRIPT = SCRIPT : ';While j <= l'
      SCRIPT = SCRIPT : ';Enter stx:Mid(T,j,120),0'
      SCRIPT = SCRIPT : ';j+=120'
      SCRIPT = SCRIPT : ';Repeat'
      SCRIPT = SCRIPT : ';Enter stx'
      CALL WIN.COMSUB(SCRIPT)
*
      VALUE = ''
      ECHO OFF
      PROMPT ''
*
      LOOP                  
         GOSUB 100; * Get input
      UNTIL DUM = '' DO
         VALUE = VALUE : DUM
      REPEAT
*                 
      IF CNVT = "" OR CNVT = "TEXT" THEN CALL WIN.FTTOASC(VALUE)
*
      ECHO ON
*
      RETURN
*                           
* Get input from script
100 LOOP
       INPUT DUM:
    UNTIL INDEX(DUM,STX,1) DO
    REPEAT
    DUM = FIELD(DUM,STX,2)
    RETURN
*
   END
