SUBROUTINE WIN.RFMT.UC(F.DATA, R.RFMT, SV.LIST, TIDYUP, NOITEMS, ERR)
* Subroutine to reformat data to a particular file
* Compile for: UC
* Copyright (c) 1991-1997. Impact Business Systems
* Copyright (c) 1993-1997. Unidata Inc.
*
EQU TRUE TO 1, FALSE TO 0
EQU FRAME.SIZE TO 1024
CR.FILE = FALSE
*
FNAME = R.RFMT<1>
ITEMS = R.RFMT<2>
ITEMLIST = R.RFMT<3>
FLDS = R.RFMT<4>
FLDS.LEN = R.RFMT<5>
PORT = R.RFMT<6>
USE.DICT = R.RFMT<7>
ITEMS2 = R.RFMT<8>
*
LST.NAME = 'WIN.RFMT':PORT
RF.NAME = 'WIN.RFMT':PORT
OPEN RF.NAME TO F.CHECK ELSE CR.FILE = TRUE
*
BEGIN CASE
  CASE TIDYUP = 0; GOSUB 50; * Create reformat file
  CASE TIDYUP = 1
    IF CR.FILE = FALSE THEN CLEARFILE F.CHECK
  CASE TIDYUP = 2
    IF CR.FILE = FALSE THEN
      CLOSE F.CHECK
      DATA "Y"
      EXECUTE 'DELETE-FILE ':RF.NAME CAPTURING RESULT
      EXECUTE 'DELETE-LIST ':LST.NAME CAPTURING RESULT
    END
END CASE
*
RETURN
*
* Create work file for import
50 GOSUB 100; * Select items to process
IF ERR = '' THEN
  IF CR.FILE THEN
    GOSUB 200; * Create the work file
  END ELSE CLEARFILE F.CHECK
*
  IF ERR= '' THEN GOSUB 300; * Reformat the file
  IF ERR = '' THEN
    EXECUTE 'GET-LIST ':LST.NAME RTNLIST SV.LIST CAPTURING RESULT
    F.DATA = F.CHECK
  END
  TIDYUP = 1 + CR.FILE
END
*
RETURN
*
* Select items to transfer
100 BEGIN CASE
  CASE ITEMLIST
    SEL = 'SELECT ':FNAME:ITEMS
  CASE ITEMS = "*"
    SEL = "SELECT ":FNAME
  CASE ITEMS[1,4] = "XEQ "
    SEL = ITEMS[5,99999]
  CASE 1
    SEL = ITEMS
END CASE
*
SEL<-1> = 'SAVE.LIST ':LST.NAME
*
EXECUTE SEL CAPTURING RESULT
GOSUB 1150; * Get number of items
IF NOITEMS = 0 THEN
   ERR = 1
END ELSE
   IF ITEMS2 # "" THEN
      IF ITEMS2[1,4]= "XEQ " THEN ITEMS2 = ITEMS2[5,9999]
      SEL = "GET-LIST ":LST.NAME
      SEL<-1> = ITEMS2
      SEL<-1> = "SAVE-LIST ":LST.NAME
      EXECUTE SEL CAPTURING RESULT
      GOSUB 1150; * Get number of items
      IF NOITEMS = 0 THEN ERR = 1
   END
END
*
RETURN
*
* Create work file
200 FSIZE = NOITEMS * FLDS.LEN /FRAME.SIZE
FSIZE = INT(FSIZE * 0.75) + 1;* Assume 25% wastage
IF REM(FSIZE,2) = 0 THEN FSIZE = FSIZE + 1
PRIME = FALSE
IF FSIZE > 7 THEN
  LOOP  
    PRIME = TRUE
    FOR J = 3 TO 7 STEP 2 WHILE PRIME
      IF REM(FSIZE,J) = 0 THEN PRIME = FALSE
    NEXT J
  UNTIL PRIME DO
    FSIZE =FSIZE + 2
  REPEAT
END
*
EXECUTE 'CREATE-FILE ':RF.NAME:' ':FSIZE CAPTURING RESULT
OPEN RF.NAME TO F.CHECK ELSE ERR = 2
RETURN
*
300 EXECUTE 'GET-LIST ':LST.NAME CAPTURING RESULT
DATA RF.NAME
SEL = 'REFORMAT ':FNAME:' @ID ':FLDS
IF USE.DICT # "" THEN SEL = SEL : " USING ":USE.DICT
EXECUTE SEL CAPTURING RESULT
*
RETURN
*
* Count items. In output field 2 or 1 for QSELECT etc
1150 CNT = DCOUNT(RESULT, @FM)
NOITEMS = 0
LOOP
WHILE CNT > 0 AND NOITEMS = 0
   CHECK = FIELD(RESULT<CNT>,' ',1)
   IF CHECK MATCH "1N0N" THEN NOITEMS = CHECK
   CNT = CNT - 1
REPEAT
*
RETURN
*
END
