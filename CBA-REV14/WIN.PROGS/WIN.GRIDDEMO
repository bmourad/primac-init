* WIN.GRIDDEMO
* Dialogbox demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
  PROMPT ''
  EQU TRUE TO 1, FALSE TO 0
*
  AMVM=1; AMVM<1,2>=2; AMVM<2>=3
  AM=AMVM[4,1]
  VM=AMVM[2,1]
*
DIM ARGS(20)
MAT ARGS = ""
*
PRINT @(-1):"WIN.GRIDDEMO":
PRINT @(20,0):"Simple demonstration of the Grid control in a dialog box"
*
DLGNAME = "GRIDDEMO"
ERR = 0
GOSUB 100; * Create dialog box
IF ERR = 0 THEN
   CALL WIN.DBSHOW(DLGNAME,1,ERR)
   IF ERR THEN
      PRINT "Unable to show the dialog box, Error = ":ERR
   END ELSE
      * Load up some dummy data
      REC = "Cell 1,1":VM:"Cell 1,2":VM:"Cell 1,3"
      REC<-1> = "Cell 2,1":VM:"Cell 2,2":VM:"Cell 2,3"
      CALL WIN.DBSETPRP(DLGNAME, "Grid1","Data",REC)
   END
END
*
IF ERR = 0 THEN
   * Arguments for WIN.DBEVENT2
   DLG = ''  ; * Dialog where event occurred
   CTRL = '' ; * Control where the event occurred
   ETYPE = ''; * Event type or index
   EARGS = ''; * Event arguments
   ESTACK = '';* Events stacked up for future processing
*
   CALL WIN.TWOPEN("GRIDEVENTS","Events received", 1,3,78,20, "SINGLE")
*
   LOOP
      CALL WIN.DBEVENT2(DLG,CTRL,ETYPE,EARGS,ESTACK)
   UNTIL CTRL = "Cancel" AND ETYPE = "C" DO
      PRINT "Dialog ":DLG:", Control ":CTRL:", Event Type ":ETYPE:", ARGS = ":EARGS
      IF DLG = DLGNAME AND CTRL = "Grid1" THEN GOSUB 200;* Process grid events
   REPEAT
*
   CALL WIN.TWCLOSE("GRIDEVENTS")
   CALL WIN.DBDEL("GRIDDEMO")
END
*
STOP
*
* Create the dialog box
100 * Set up events we are interested in the grid
GRE.CELLCLICK    = 6
GRE.CURRENTCELL  = 8
GRE.STARTEDITING = 1
GRE.VALIDATECELL = 2
GRID.EVENTS = ""
GRID.EVENTS<1> = GRE.CELLCLICK:VM:"row,col"      ; * CellClick = 6
GRID.EVENTS<2> = GRE.CURRENTCELL:VM:"row,col"    ; * CurrentCell = 8
GRID.EVENTS<3> = GRE.STARTEDITING:VM:"row,col"   ; * StartEditing = 1
GRID.EVENTS<4> = GRE.VALIDATECELL:VM:"row,col"   ; * Validate Cell = 2
*
CALL WIN.DBNEW(DBX,DLGNAME, "Grid test caption", 0,0,400,110,"","")
CALL WIN.DBGRID(DBX, "Grid1", 10,10,380,80,"")
CALL WIN.DBEVENTS(DBX,"Grid1",GRID.EVENTS)
CALL WIN.DBBUTTON(DBX,"Cancel","Close",180,92,40,14,0)
CALL WIN.DBLOAD(DBX,ERR)
IF ERR THEN PRINT "Unable to load dialog box, Error = ":ERR
RETURN
*
* Process grid events
200 * ETYPE is an index into the registered events
ROW = FIELD(EARGS,",",1)
COL = FIELD(EARGS,",",2)
PRINT "  ":
BEGIN CASE
CASE GRID.EVENTS<ETYPE,1> = GRE.CELLCLICK;    PRINT "Cell click on":
CASE GRID.EVENTS<ETYPE,1> = GRE.CURRENTCELL;  PRINT "Current cell changed to":
CASE GRID.EVENTS<ETYPE,1> = GRE.STARTEDITING; PRINT "Started editing":
CASE GRID.EVENTS<ETYPE,1> = GRE.VALIDATECELL; PRINT "Validate cell":
* Call WIN.DBESTACK to stack up events immediatly following the validate
* cell so they don't interfere with the method call
CALL WIN.DBESTACK(ESTACK)
ARGS(1) = ROW;ARGS(2)=COL
CALL WIN.DBMETHOD(DLGNAME,"Grid1","GetCellValue","II",MAT ARGS, 1, VALUE)
PRINT " Value = '":VALUE:"',"
END CASE
PRINT " row ":ROW:", column = ":COL
*
RETURN 
*
END
