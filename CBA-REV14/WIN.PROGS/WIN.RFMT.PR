SUBROUTINE WIN.RFMT.PR(F.DATA, R.RFMT, SV.LIST, TIDYUP, NOITEMS, ERR)
* Subroutine to reformat data to a particular file
* Compile for: PR.SUB
* Copyright (c) 1991-1994. Impact Business Systems
* Copyright (c) 1994. Unidata Inc.
*
EQU TRUE TO 1, FALSE TO 0
EQU FRAME.SIZE TO 2000
CR.FILE = FALSE
SV.LIST = 0
*
FNAME = R.RFMT<1>
ITEMS = R.RFMT<2>
ITEMLIST = R.RFMT<3>
FLDS = R.RFMT<4>
FLDS.LEN = R.RFMT<5>
PORT = R.RFMT<6>
USE.DICT = R.RFMT<7>
*
LST.NAME = 'WIN.RFMT':PORT
RF.NAME = 'WIN.RFMT':PORT
OPEN RF.NAME TO F.CHECK ELSE CR.FILE = TRUE
*
BEGIN CASE
  CASE TIDYUP = 0; GOSUB 50; * Create reformat file
  CASE TIDYUP = 1
    IF CR.FILE = FALSE THEN CLEARFILE F.CHECK
  CASE TIDYUP = 2
    IF CR.FILE = FALSE THEN
      CLOSE F.CHECK
      CLOSE F.DATA
      EXECUTE 'DELETE.FILE ':RF.NAME CAPTURING RESULT
      EXECUTE 'DELETE.LIST ':LST.NAME CAPTURING RESULT
    END
END CASE
*
RETURN
*
* Create work file for import
50 GOSUB 100; * Select items to process
IF ERR = '' THEN
  IF CR.FILE THEN GOSUB 200; * Create the work file
  IF ERR= '' THEN GOSUB 300; * Reformat the file
  IF ERR = '' THEN
    EXECUTE 'GET.LIST ':LST.NAME  : " TO ":SV.LIST CAPTURING RESULT
    F.DATA = F.CHECK
  END
  TIDYUP = 1 + CR.FILE
END
*
RETURN
*
* Select items to transfer
100 BEGIN CASE
  CASE ITEMLIST
    SEL = 'SELECT ':FNAME:ITEMS
  CASE ITEMS = "*"
    SEL = "SELECT ":FNAME
  CASE ITEMS[1,4] = "XEQ "
    SEL = ITEMS[5,99999]
  CASE 1
    SEL = ITEMS
END CASE
*
EXECUTE SEL CAPTURING RESULT
GOSUB 1150; * Get number of items
EXECUTE 'SAVE.LIST ':LST.NAME CAPTURING RESULT
IF NOITEMS = 0 THEN ERR = 1
RETURN
*
* Create work file
200 FSIZE = NOITEMS * FLDS.LEN / FRAME.SIZE
FSIZE = INT(FSIZE * 0.75) + 1
IF REM(FSIZE,2) = 0 THEN FSIZE = FSIZE + 1
IF FSIZE > 7 THEN
  PRIME = FALSE
  LOOP
    PRIME = TRUE
    FOR J = 3 TO 7 STEP 2 WHILE PRIME
      IF REM(FSIZE,J) = 0 THEN PRIME = FALSE
    NEXT J
  UNTIL PRIME DO
    FSIZE =FSIZE + 2
  REPEAT
END
*
EXECUTE 'CREATE.FILE ':RF.NAME:' 17 ':FSIZE CAPTURING RESULT
OPEN RF.NAME TO F.CHECK ELSE ERR = 2
RETURN
*
300 SEL = 'GET.LIST ':LST.NAME
SEL<-1> = 'REFORMAT ':FNAME:' TO ':RF.NAME
FLDS = " @ID ":FLDS
SEL := FLDS
IF USE.DICT # "" THEN SEL = SEL : " USING ":USE.DICT
EXECUTE SEL CAPTURING RESULT
*
RETURN
*
*
* Get Number of items selected
*
1150 READLIST INLIST FROM SV.LIST ELSE INLIST = ''
DUMMY = INLIST
FORMLIST DUMMY TO SV.LIST
*
* Count items. Might be @IM or @FM
NOITEMS = DCOUNT(INLIST,@IM)
IF INLIST # '' AND NOITEMS LE 1 THEN
  NOITEMS = DCOUNT(INLIST,@FM)
END
IF INLIST = '' THEN NOITEMS = 0
*
*
RETURN
*
END
