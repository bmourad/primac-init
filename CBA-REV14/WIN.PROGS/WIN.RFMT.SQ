SUBROUTINE WIN.RFMT.SQ(F.DATA, R.RFMT, SV.LIST, TIDYUP, NOITEMS, ERR)
* Subroutine to reformat data to a particular file
* Compile for: SQ
* Copyright (c) 1991-1994. Impact Business Systems
* Copyright (c) 1994. Unidata Inc.
*
EQU TRUE TO 1, FALSE TO 0
EQU FRAME.SIZE TO 512
CR.FILE = FALSE
*
FNAME = R.RFMT<1>
ITEMS = R.RFMT<2>
ITEMLIST = R.RFMT<3>
FLDS = R.RFMT<4>
FLDS.LEN = R.RFMT<5>
PORT = R.RFMT<6>
USE.DICT = R.RFMT<7>
*
LST.NAME = 'WIN.RFMT':PORT
RF.NAME = 'WIN.RFMT':PORT
OPEN RF.NAME TO F.CHECK ELSE CR.FILE = TRUE
*
BEGIN CASE
  CASE TIDYUP = 0; GOSUB 50; * Create reformat file
  CASE TIDYUP = 1
    IF CR.FILE = FALSE THEN CLEARFILE F.CHECK
  CASE TIDYUP = 2
    IF CR.FILE = FALSE THEN
      CLOSE F.CHECK
      EXECUTE 'DELETE-FILE ':RF.NAME CAPTURING RESULT
      EXECUTE 'DELETE-LIST ':LST.NAME CAPTURING RESULT
    END
END CASE
*
RETURN
*
* Create work file for import
50 GOSUB 100; * Select items to process
IF ERR = '' THEN
  IF CR.FILE THEN GOSUB 200; * Create the work file
  IF ERR= '' THEN GOSUB 300; * Reformat the file
  IF ERR = '' THEN
    EXECUTE 'GET-LIST ':LST.NAME CAPTURING RESULT
    F.DATA = F.CHECK
    SELECT F.DATA TO SV.LIST
  END
  TIDYUP = 1 + CR.FILE
END
*
RETURN
*
* Select items to transfer
100 DATA 'SAVE-LIST ':LST.NAME
BEGIN CASE
  CASE ITEMLIST
    SEL = 'SELECT ':FNAME:ITEMS
  CASE ITEMS = "*"
    SEL = "SELECT ":FNAME
  CASE ITEMS[1,4] = "XEQ "
    SEL = ITEMS[5,99999]
  CASE 1
    SEL = ITEMS
END CASE
*
EXECUTE SEL CAPTURING RESULT
GOSUB 1150; * Get number of items
IF NOITEMS = 0 THEN ERR = 1
RETURN
*
* Create work file
200 FSIZE = NOITEMS * FLDS.LEN / FRAME.SIZE
FSIZE = INT(FSIZE * 0.75) + 1
IF REM(FSIZE,2) = 0 THEN FSIZE = FSIZE + 1
IF FSIZE > 7 THEN
  PRIME = FALSE
  LOOP  
    PRIME = TRUE
    FOR J = 3 TO 7 STEP 2 WHILE PRIME
      IF REM(FSIZE,J) = 0 THEN PRIME = FALSE
    NEXT J
  UNTIL PRIME DO
    FSIZE =FSIZE + 2
  REPEAT
END
*
EXECUTE 'CREATE-FILE ':RF.NAME:' 1,1 ':FSIZE:",1" CAPTURING RESULT
OPEN RF.NAME TO F.CHECK ELSE ERR = 2
RETURN
*
*
300 FLDS = "*A0 ":FLDS
SEL = 'REFORMAT ':FNAME:' ':FLDS                
IF USE.DICT # "" THEN SEL = SEL : " USING ":USE.DICT
DATA SEL, RF.NAME                                   
EXECUTE 'GET-LIST ':LST.NAME CAPTURING RESULT
RETURN
*
*
* Get Number of items selected
*
1150 NOITEMS = FIELD(RESULT<2>,' ',1)
IF (NOT(NOITEMS MATCH "1N0N")) THEN
   NOITEMS = FIELD(RESULT<1>,' ',1)
   IF (NOT(NOITEMS MATCH "1N0N")) THEN NOITEMS = 0
END
*
RETURN
*
END
