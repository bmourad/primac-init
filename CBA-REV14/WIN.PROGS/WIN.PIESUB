      SUBROUTINE WIN.PIESUB(TITLE,LABELS,REGIONS, OPTS)
* Draw a Pie chart
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
* OPTS as follows
* <1>   - Style (not implemented)
* <2>   - Left, Top, right, bottom in m/vs
* <3,1> - 1 Suppress values
* <3,2> - 1 Suppress percent
* <4>   - Colours
*
      EQU TRUE TO 1, FALSE TO 0
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*      
      GOSUB 200; * Calculate Totals
      GOSUB 100; * Initialise (Need label size from sub 200)
      GOSUB 300; * Rescale Regions
      GOSUB 400; * Draw pie chart 
      IF SHOW.PC THEN GOSUB 500; * Draw percentages around the chart
      GOSUB 600; * Add Title and Key
*
      RETURN                   
*
* Initialise variable
100   WC = 'WIN.COMSUB' 
      IF OPTS<4> = '' THEN
         COLS = 'RGB_LightRed'
         COLS<1,-1> = 'RGB_Yellow'
         COLS<1,-1> = 'RGB_LightGreen'
         COLS<1,-1> = 'RGB_LightBlue'
         COLS<1,-1> = 'RGB_LightMagenta'
         COLS<1,-1> = 'RGB_LightCyan'
      END ELSE COLS = OPTS<4>
*
      NO.COLS = DCOUNT(COLS, VM)
*                
      X.LEFT = OPTS<2,1>
      Y.TOP = OPTS<2,2>
      X.RIGHT = OPTS<2,3>
      Y.BOT = OPTS<2,4>
*
      IF X.LEFT = '' THEN X.LEFT = 0
      IF X.RIGHT = '' THEN X.RIGHT = 79
      IF Y.TOP = '' THEN Y.TOP = 0
      IF Y.BOT = '' THEN Y.BOT = 23
*
      TITLE.SIZE = 15 
      BLOCK.SIZE = 15
      NAME.SIZE = NAME.SIZE * 10; * Scale
*
      IF OPTS<3,1> = '1' THEN
         SHOW.VALS = FALSE
         LABEL.SIZE = NAME.SIZE + BLOCK.SIZE
      END ELSE
         SHOW.VALS = TRUE
         LABEL.SIZE = LABEL.SIZE * 10 + NAME.SIZE + 10 + BLOCK.SIZE
      END
*
      LABEL.X = X.RIGHT * 10 - LABEL.SIZE
*
      IF OPTS<3,2> = '1' THEN
         SHOW.PC = FALSE
         PC.X = 10
         PC.Y = 5
      END ELSE
         SHOW.PC = TRUE
         PC.X = 40
         PC.Y = 15
      END
*                                
      WIDTH = X.RIGHT - X.LEFT + 1
      DEPTH = Y.BOT - Y.TOP + 1
*
      XSIZE = WIDTH * 5 - PC.X * 2 - INT(LABEL.SIZE/2)
      YSIZE = DEPTH * 5 - PC.Y * 2 - INT(TITLE.SIZE/2)
*
      CX = X.LEFT * 10 + PC.X + XSIZE
      CY = Y.TOP * 10 + PC.Y + TITLE.SIZE +YSIZE 
*
      RETURN
*
* Calculate totals and size for labels
200   NO.REGIONS = DCOUNT(LABELS, AM)
      REGIONS.TOTAL = 0
      LABEL.SIZE = 0
      NAME.SIZE = 0                                                       
*
      FOR J = 1 TO NO.REGIONS
         IF LEN(LABELS<J>) > NAME.SIZE THEN NAME.SIZE = LEN(LABELS<J>)
         IF LEN(REGIONS<J>) > LABEL.SIZE THEN LABEL.SIZE = LEN(REGIONS<J>)
         REGIONS.TOTAL = REGIONS.TOTAL + REGIONS<J>
      NEXT J                          
*                                      
      RETURN
*
* Rescale regions into degrees and percentages
300   DEGREES = ''        
      PCS = ''
      FOR J = 1 TO NO.REGIONS
         IF REGIONS.TOTAL+0 = 0 THEN T = 0 ELSE T = REGIONS<J> * 360 / REGIONS.TOTAL
         DEGREES<J> = INT(T+0.5)
         IF REGIONS.TOTAL+0 = 0 THEN T = 0 ELSE T = REGIONS<J> * 100 / REGIONS.TOTAL
         PCS<J> = INT(T+0.5)
      NEXT J
      RETURN
*
* Draw Pie Chart
400   COL = 1
      ANGLE = 0
      X = CX + INT(XSIZE * COS(0) + 0.5)
      Y = CY + INT(YSIZE * SIN(0) + 0.5)
      PIE.RECT = CX-XSIZE:',':CY-YSIZE:',':CX+XSIZE:',':CY+YSIZE:','
*
      FOR J = 1 TO NO.REGIONS
         IF J = NO.REGIONS THEN ANGLE = 0 ELSE ANGLE = ANGLE + DEGREES<J>
         IF DEGREES<J> THEN
            SCRIPT = 'Draw Pen ':COLS<1,COL>
            SCRIPT<-1> = 'Draw Brush ':COLS<1,COL>
            DRAW = X:',':Y
            X = CX + INT(XSIZE * COS(ANGLE) + 0.5)
            Y = CY + INT(YSIZE * SIN(ANGLE) + 0.5)
            DRAW = 'Draw PIE ':PIE.RECT:X:',':Y:',':DRAW
            SCRIPT<-1> = DRAW
            CALL @WC(SCRIPT)
         END
         COL = COL + 1
         IF COL > NO.COLS THEN COL = 1
      NEXT J 
*
      RETURN
*
* Add Percentages around the chart
500   ANGLE = 0
      SCRIPT = 'Draw Pen RGB_White'
      SCRIPT<-1> = 'Draw Brush RGB_Black'
      SCRIPT<-1> = 'Draw Font "IBSFont",10,10'
      CALL @WC(SCRIPT)
      PC.XSIZE = XSIZE + PC.X - 20; * Adjust size to centre
      PC.YSIZE = YSIZE + PC.Y - 5;  * of word
*
      FOR J = 1 TO NO.REGIONS
         NEXT.ANGLE = ANGLE + DEGREES<J>
         PC.ANGLE = INT((NEXT.ANGLE + ANGLE)/2)
         X = CX + INT(PC.XSIZE * COS(PC.ANGLE) + 0.5) - LEN(PCS<J>) * 5 - 5
         Y = CY + INT(PC.YSIZE * SIN(PC.ANGLE) + 0.5)
         SCRIPT = 'Draw Text ':X:',':(Y-5):',"':PCS<J>:'%"'
         CALL @WC(SCRIPT)
         ANGLE = NEXT.ANGLE
      NEXT J
*
      RETURN 
*
* Add Title and Key
600   X = X.LEFT * 10 + WIDTH * 5 - LEN(TITLE) * 5
      Y = Y.TOP * 10
      SCRIPT = "Draw Pen RGB_White"
      SCRIPT<-1> = "Draw Brush RGB_Black"
      SCRIPT<-1> = "Draw Font 'IBSFont',10,10,FONT_Underline"
      SCRIPT<-1> = "Draw Text ":X:",":Y:",'":TITLE:"'"
      CALL @WC(SCRIPT)
*
      COL = 1       
      Y = Y + TITLE.SIZE
*                                  
      DRAW.NAME  = "Draw Text ":(LABEL.X+BLOCK.SIZE):","
      DRAW.VALUE = "Draw Text ":(LABEL.X+BLOCK.SIZE + 10+NAME.SIZE):","
*
      SCRIPT = "Draw Font 'IBSFont',10,10"
      SCRIPT<-1> = "Draw Text ":LABEL.X:",":Y:",'Key'"
      IF SHOW.VALS THEN SCRIPT<-1> = DRAW.VALUE:Y:",'Values'"
*
      CALL @WC(SCRIPT)                            
      Y = Y + 10
*
      FOR J = 1 TO NO.REGIONS
         SCRIPT = 'Draw Pen ':COLS<1,COL>
         SCRIPT<-1> = 'Draw Brush ':COLS<1,COL>
         SCRIPT<-1> = 'Draw Rect ':LABEL.X:',': Y  :',': (LABEL.X+10) :',': (Y+10)
         SCRIPT<-1> = 'Draw Pen RGB_White'
         SCRIPT<-1> = 'Draw Brush RGB_Black'
         SCRIPT<-1> = DRAW.NAME:Y:",'":LABELS<J>:"'"
         IF SHOW.VALS THEN SCRIPT<-1> = DRAW.VALUE:Y:",'":REGIONS<J>:"'"
         CALL @WC(SCRIPT)
*
         Y = Y + 10
         COL = COL + 1
         IF COL > NO.COLS THEN COL = 1
      NEXT J
*
      RETURN
*
   END
