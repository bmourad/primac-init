SUBROUTINE WIN.MENULOAD(NAME, R.DEF)
* Load a windows menu
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
SCRIPT = 'Menu Create ':NAME
N = DCOUNT(R.DEF, AM)
FOR J = 1 TO N
    TEXT=R.DEF<J,1>
    CMND=R.DEF<J,2>
    HELP=R.DEF<J,3>
*
    ID = NAME:'_':J
*
    BEGIN CASE
    CASE TEXT[1,1]='-'
         SCRIPT<-1>='Separator'
    CASE TEXT[1,1]='>'
         TEXT = TEXT[2,999]
         IF CMND # '' THEN ID = CMND
         SCRIPT<-1>='Attach ':ID:',"':TEXT:'"'
    CASE 1
         TEXT = TRIM(TEXT)                              
         IF CMND = '' THEN
            CMND = "stx:'M;":NAME:";":J:"'"
         END ELSE  CMND = "'":CMND:"'"      
         CMND='Enter ':CMND
         SCRIPT<-1>='AddItem ':ID:',"':TEXT:'","':CMND:'"'
    END CASE
*    
    IF HELP # '' THEN SCRIPT<-1>='Help ':ID:',"':HELP:'"'
NEXT J
*
SCRIPT<-1>='OnClose "Enter stx:':"'M;;ESC'":'"'
SCRIPT<-1>='EndCreate'
*
CALL WIN.HSCRIPT(SCRIPT)
*
RETURN
*
END
