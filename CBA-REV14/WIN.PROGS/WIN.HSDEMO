* WIN.HSDEMO
* Hot Spots demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
      PROMPT ''
      C22 = @(0,22):@(-4)
      C23 = @(0,23):@(-4)
      CMSG = @(0,20):@(-4)
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
      PRINT @(-1):"WIN.HSDEMO":
      PRINT @(26,0):"HotSpots demonstration":
      PRINT @(71,0):OCONV(DATE(),"D2"):
      CALL WIN.BOX(0,1,79,1,1)
*
      PRINT @(0,3):"This program demonstrates setting Areas of the screen to return specific"
      PRINT "values to the host when clicked upon by the mouse."
      PRINT
      PRINT "The Hot spots are defined by creating a record which contains a line for"
      PRINT "Each Hot spot definition and then using:-"
      PRINT "   CALL WIN.HOTSPOT(R.SPOTS)"
      PRINT "The record holds the positions of the hotspots and the return value"
      PRINT "as multivalues. Hotspots are removed by:-"
      PRINT '   CALL WIN.HOTSPOT("")'                  
*    
R.SPOTS=''
R.COLS=''                                                      
* Set up array of areas for hot spots
R.SPOTS<-1>=0:VM:13:VM:79:VM:20;R.COLS<-1>="Blue"
R.SPOTS<-1>=15:VM:15:VM:39:VM:16;R.COLS<-1>="LightRed"
R.SPOTS<-1>=40:VM:15:VM:65:VM:16;R.COLS<-1>="Yellow"
R.SPOTS<-1>=15:VM:17:VM:39:VM:18;R.COLS<-1>="LightCyan"
R.SPOTS<-1>=40:VM:17:VM:65:VM:18;R.COLS<-1>="LightGreen"
*
* Cover the remaining area so we can tell people whats going on
R.SPOTS<-1>=0:VM:0:VM:79:VM:12:VM:"DESC"
R.SPOTS<-1>=0:VM:21:VM:79:VM:23:VM:"PROMPT"
R.SPOTS<-1>=58:VM:23:VM:78:VM:23:VM:"*"
*
STATE=''
CALL WIN.SSTATE(STATE,'')
CALL WIN.COLOR("White","")
N=DCOUNT(R.SPOTS,AM)-3
FOR J = 1 TO N
   IF J = 2 THEN CALL WIN.COLOR("Black","")
   CALL WIN.COLOR("",R.COLS<J>)
   CALL WIN.BOX(R.SPOTS<J,1>, R.SPOTS<J,2>, R.SPOTS<J,3>, R.SPOTS<J,4>, "SPACES")
   PRINT @(R.SPOTS<J,1>,R.SPOTS<J,2>):"Area ":J:" ":R.COLS<J>:
NEXT J                                
*
CALL WIN.COLOR("Red","White")
PRINT @(58,23):" Click HERE to exit ":
*
CALL WIN.SSTATE('',STATE)
*
* Define hotspots
CALL WIN.HOTSPOT(R.SPOTS)
C22=@(0,22):@(-4)
*
LOOP
  PRINT C22:"Click mouse on area or Enter * to exit ":
  INPUT RESP:    
UNTIL RESP = "*" DO
*
  PRINT @(0,21):@(-4):"Returned ":RESP:", You clicked in the ":
  BEGIN CASE
  CASE RESP = "PROMPT"; PRINT "area of the PROMPT":
  CASE RESP = "DESC";   PRINT "description of hotspots":
  CASE RESP MATCH "1N" AND RESP > "0" AND RESP < "6"
       PRINT R.COLS<RESP>:" area.":
  CASE 1
       PRINT @(0,21):@(-4):"You entered data via the keyboard":
  END CASE
*
REPEAT
*
CALL WIN.HOTSPOT("")
*
END
