* WIN.COMLINE
* Enter individual wIntegrate statements manually
* Compile for: GENERIC AP UL MD PR UD UP UV ME SQ UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-2002. Impact Business Systems
*
* Initialise
*
      SPACES = SPACE(8)
      PROMPT '>'
      EQU TRUE TO 1, FALSE TO 0
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
      DIM HLP(12)
      DIM STATE(14)
*
*
* Open files
*
      SCRIPT.FILE = TRUE
      OPEN '','WIN.SCRIPT' TO F.SCRIPT ELSE SCRIPT.FILE = FALSE
      OPEN '','WIN.PARAMS' TO F.PROGS ELSE
         OPEN '','WIN.PROGS' TO F.PROGS ELSE STOP 'CANT OPEN FILE WIN.PROGS'
      END
*
      GOSUB 100; * Load Help Text
      READV MACHINE.TYPE FROM F.PROGS, "MACHINE.TYPE",1 ELSE MACHINE.TYPE = "GENERIC"
*        
      SUB.SUFFIX = ''
      IF MACHINE.TYPE # "GENERIC" THEN SUB.SUFFIX = '.' : MACHINE.TYPE
*
      EXECSUB = "WIN.EXEC" : SUB.SUFFIX            
*
*
* Main loop
*
      PRINT 'Enter  ?  for help'
      LOOP
*
         PRINT "wIntegrate Command ":
         INPUT CMND
*
      UNTIL CMND = "" DO
*
         BEGIN CASE
*
            CASE CMND[1,1] = '?' OR CMND = 'HELP'
               GOSUB 1000                          ;* Display help
*
            CASE TRIM(CMND[1,3]) MATCH "'E '1X"
               GOSUB 2000                          ;* Edit a script
*
            CASE 1
               GOSUB 3000                          ;* Run a script
*
         END CASE
*
      REPEAT
*
      STOP
*
* Set up Help text
100   MAT HLP = ''
      HLPMAX = 9
*
      HLP(1) = 'You may enter:'
      HLP(2) = SPACES : '1)  The name of a script held in file WIN.SCRIPT'
      HLP(3) = SPACES : '    Example:   DEMO.SCRIPT'
      HLP(4) = SPACES : '2)  Any wIntegrate script statement'
      HLP(5) = SPACES : '    Example:   Set Effect_Normal RGB_Red, RGB_Black'
      HLP(6) = SPACES : '3)  E and a script name to edit a script'
      HLP(7) = SPACES : '    Example:   E DEMO.SCRIPT'
      HLP(8) = SPACES : '4)  ?S   To see a list of scripts in WIN.SCRIPT'
      HLP(9) = SPACES : '5)  ?T   To see a list of typical statements'
*
      IF NOT(SCRIPT.FILE) THEN
         HLP(10) = 'File WIN.SCRIPT cannot be opened. You must create'
         HLP(11) = 'WIN.SCRIPT before using options 1,3 and 4'
         HLPMAX = 11
      END
*
      MAT STATE = ''
      STATEMAX = 14
*
      STATE(1) = 'These are typical wIntegrate statements. See the Windows'
      STATE(2) = 'Help for a complete list of statements. and session'
      STATE(3) = 'variables.'
      STATE(4) = SPACES : 'Dialog FileOpen'
      STATE(5) = SPACES : 'Set Filename="config\colgreen.wis"'
      STATE(6) = SPACES : 'Invoke'
      STATE(7) = SPACES : 'Set Title "Less than 5 minutes to system shutdown"'
      STATE(8) = SPACES : 'MessageBox "File transfer completed"'
      STATE(9) = SPACES : 'Script "example\script\demo.wis"'
      STATE(10) = SPACES : 'Set Font= "IBSfont",3'
      STATE(11) = SPACES : 'Infobox "This is a message",B'
      STATE(12) = SPACES : 'Invoke ScrollPrevPage'
      STATE(13) = SPACES : 'Invoke RSP       (ie Run Scroll PrevPage)'
      STATE(14) = SPACES : 'Type "This came from the host\r"'
      RETURN
*
* Display help
*
1000  BEGIN CASE
*
         CASE CMND = '?S'
*
            CALL @EXECSUB('SORT WIN.SCRIPT','','')
            PRINT
*
         CASE CMND = '?T'
*
            PRINT
            FOR J = 1 TO STATEMAX
               PRINT SPACES : STATE(J)
            NEXT J
            PRINT
*
         CASE CMND = '?' OR CMND = 'HELP'
*
            PRINT
            FOR J = 1 TO HLPMAX
               PRINT SPACES : HLP(J)
            NEXT J
            PRINT
*
      END CASE
*
      RETURN
*
*
*
* Edit a script
*
2000  CMND = TRIM(CMND)
      CMND = CMND[3,999]
      PRINT
*
      CALL @EXECSUB('ED WIN.SCRIPT ' : CMND, '','')
*
      PRINT
      RETURN
*
*
* Run a script
* At this point, we are assuming that the user input was either a
* script statement, such as:   Set Title "This is a window title"
* a script ID, as held in the file WIN.SCRIPT.
* or WIN.TRANSFER sent from the PC as a file transfer has been run.
*
3000  IF CMND = "WIN.TRANSFER" THEN
         CALL @EXECSUB("WIN.TRANSFER",'','')
      END ELSE
         IF SCRIPT.FILE AND LEN(CMND) < 100 THEN
            READ DUM FROM F.SCRIPT, CMND THEN  ;* Did the user enter a script ID?
               CMND = DUM                      ;* Yes and its now in variable CMND
            END                  
         END
*
         IF COUNT(CMND,AM) THEN
            CALL WIN.HSCRIPTC(CMND)
         END ELSE
            CALL WIN.COMSUB(CMND)
         END
*      
END
         RETURN
*
      END
