* WIN.ILDEMO
* Image list demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
PROMPT ''
EQU TRUE TO 1, FALSE TO 0
*
DIM ARGS(20)
MAT ARGS = ""
C22 = @(0,22):@(-4)
C23 = @(0,23):@(-4)
CMSG = @(0,20):@(-4)
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
PRINT @(-1):"WIN.ILDEMO":
PRINT @(26,0):"Image list demonstration":
PRINT @(71,0):OCONV(DATE(),"D2"):
CALL WIN.BOX(0,1,79,1,1)
*
GOSUB 50;  * Print brief description
*   
DLG.NAME = "ILDEMO"
ERR = 0
ILCREATED = 0
IL2CREATED = 0
*
GOSUB 100; * Create a new image list and add a few images 
IF ERR = 0 THEN GOSUB 110; * Setup dialog box for demonstration
IF ERR = 0 THEN GOSUB 120; * Show images
IF ERR = 0 THEN GOSUB 130; * Load image list
IF ERR = 0 THEN GOSUB 140; * Info for the imagelist
IF ERR = 0 THEN GOSUB 150; * Remove and delete
*
PRINT C22:"Press <CR> to exit the demonstration":
INPUT DUM:
CALL WIN.DBEND(DLG.NAME,0)
CALL WIN.DBDEL(DLG.NAME)
IF ILCREATED THEN CALL WIN.ILDELETE(ILNAME)
IF IL2CREATED THEN CALL WIN.ILDELETE(ILNAME2)
*
STOP
*
* Brief description of dialog boxes
50 PRINT @(0,3):
PRINT "Image lists are used to group together a group of icons to be used by"
PRINT "various dialog box control types. e.g. The ListView and TreeView controls."
PRINT
*
PRINT "The routines are:-"
PRINT "WIN.ILADD     add a bitmap to the image list"
PRINT "WIN.ILCOUNT   get the number of icons in an image list"
PRINT "WIN.ILDELETE  delete the image list"
PRINT "WIN.ILICON    add an icon file (.ico) to the image list"
PRINT "WIN.ILINFO    retrieve planes and bits per pixel for an icon in the list"
PRINT "WIN.ILISIZE   get the size of the icons in an image list"
PRINT "WIN.ILLOAD    load an image list from a file"
PRINT "WIN.ILNEW     create a new empty image list"
PRINT "WIN.ILREMOVE  remove an image from an image list"
PRINT
CALL WIN.APPDIR(APPDIR)
BMPDIR = APPDIR:"image\"
PRINT
PRINT "for this demonstration we shall use images from wIntegrates 'image' folder"
PRINT "and the 'Example\Demo98' folder"
*
GOSUB 1000;* Continue prompt
RETURN
*
* Load images
100 PRINT @(0,3):@(-3):
    ILNAME = "ILDemo"
    OPTS = ""
    OPTS<2> = 1
    CALL WIN.ILNEW(ILNAME,16,16,OPTS,RESP)
    IF RESP = 1 THEN
       PRINT "A new image list called ILDemo to hold 16x16 pixel images was created with:"
       PRINT '   CALL WIN.ILNEW("ILDemo",16,16,OPTS,RESP)'
       PRINT
       PRINT 'OPTS<2> was set to 1 so that the image list was created with a mask'
       PRINT
       PRINT "The images are added with:-"
       PRINT '   CALL WIN.ILADD("ILDemo", IMNAME, "RGB_LightGray",0,RESP)' 
       PRINT
       PRINT "IMNAME is the file name of the image"
       PRINT "RGB_LightGray is a mask color so the background of the list view control"
       PRINT "              is visible rather than the light gray of the initial bitmap"
       PRINT "RESP returns the index number of the image or -1 if the it fails"
       PRINT
*
       ILCREATED = 1
       GOSUB 105;* Add images
    END ELSE
       ERR = 1
       PRINT "Unable to create the image list, so exiting the demonstration"
    END
    GOSUB 1000
    RETURN
*
* Add images
105 IMAGE.CNT = 0
    TEST.IMAGES = ""
    TEST.IMAGES<-1> = "i_copy.bmp"
    TEST.IMAGES<-1> = "i_lineup.bmp"
    TEST.IMAGES<-1> = "i_linedn.bmp"
*
    NUM.IMAGES = COUNT(TEST.IMAGES,AM) + 1
    IMAGES = ""
    FOR J = 1 TO NUM.IMAGES
       IMNAME = BMPDIR:TEST.IMAGES<J>
       CALL WIN.ILADD(ILNAME, IMNAME,"RGB_LightGray",0,RESP)
       PRINT TEST.IMAGES<J>:" added, index :":RESP:
       IF RESP = -1 THEN
          PRINT " image file does not exist":
       END ELSE
          IMAGE.CNT = IMAGE.CNT + 1
          IMAGES<IMAGE.CNT> = TEST.IMAGES<J>
       END
       PRINT
    NEXT J
*
    IF IMAGE.CNT = 0 THEN
       PRINT "Unable to load any images, so exiting the demonstration"
       ERR = 1
    END
    RETURN
*
* Setup and show dialog
110 PRINT @(0,3):@(-3):
    PRINT "Create a dialog box with a list view control in it which we will"
    PRINT "set the image list to so we can show the images we are using."
    PRINT
    PRINT "When the dialog has been shown, please move it to a position where you"
    PRINT "can see both it and the host screen and set the focus to the host screen."
    PRINT C22:"Creating dialog box, please wait...":
    GOSUB 2000;* Create the simple dialog box
    SNAME = ""
    CALL WIN.GETPARAM("NAME",SNAME)
    CALL WIN.DBFOCUS(SNAME,"")
    GOSUB 1000;* Continue prompt
    RETURN
*
* Put image list on dialog box
120 PRINT @(0,3):@(-3):
    PRINT "To use the image list with a list control use:"
    PRINT
    PRINT '  CALL WIN.DBMETHOD(DLG.NAME,"LV","SetImageList","IN",MAT ARGS,0,RESP)'
    PRINT
    ARGS(1) = 0
    ARGS(2) = ILNAME
    RESP = ""
    CALL WIN.DBMETHOD(DLG.NAME,"LV","SetImageList","IN",MAT ARGS,0,RESP)
    PRINT "and add entries using it with"
    PRINT '  CALL WIN.DBMETHOD(DLG.NAME,"LV","InsertItem","I", MAT ARGS,1,IDX)'
    PRINT
    ARGS(1) = ""
    ARGS(1)<1> = 1 + 2        ;* Text and image are filled in
    FOR J = 1 TO IMAGE.CNT
       ARGS(1)<6> = IMAGES<J>    ;* Text to display
       ARGS(1)<8> = J - 1        ;* index of image in image list
       CALL WIN.DBMETHOD(DLG.NAME,"LV","InsertItem","I", MAT ARGS,1,IDX)
       IF IDX = -1 THEN PRINT "Unable to insert ":IMAGES<J>:" item"
    NEXT J
*
    GOSUB 1000;* Continue prompt
RETURN
*
* Load image list
130 PRINT @(0,3):@(-3):
    PRINT "A series of images can be put together in a single bitmap and loaded"
    PRINT "with WIN.ILLOAD specifying the size of the images with:"
    PRINT
    PRINT '  CALL WIN.ILLOAD("ILDemo2",APPDIR:"Example\Demo98\menuopts.bmp",22, RESP)'
    PRINT
    PRINT "22 is the width of each icon in the imagelist"
    PRINT
    CALL WIN.ILLOAD("ILDemo2",APPDIR:"Example\Demo98\menuopts.bmp",22, RESP)
    ILNAME2 = "ILDemo2"
    IF RESP THEN
       PRINT "Image list loaded OK"
       IL2CREATED = 1
       PRINT
       PRINT "Use WIN.DBMETHOD with the SetImageList property to change the image list for"
       PRINT "the list view control in exactly the same way as the original image list was"
       PRINT "assigned"
       ARGS(1) = 0
       ARGS(2) = ILNAME2
       RESP = ""
       CALL WIN.DBMETHOD(DLG.NAME,"LV","SetImageList","IN",MAT ARGS,0,RESP)
       PRINT
       PRINT "Note how the images have changed, but not the names displayed."
       GOSUB 1000;* Continue prompt
    END ELSE
       PRINT "Unable to load the image list. Exiting the demonstration"
       ERR = 1
    END
*
    RETURN
*
* Info for the image list
140 PRINT @(0,3):@(-3):"Various function exist of interrogating the image list"
PRINT
PRINT "Retrieving information on our or original image list"
PRINT
PRINT "Number of images: ":
ICNT = 0
CALL WIN.ILCOUNT(ILNAME,ICNT)
PRINT ICNT
PRINT "   CALL WIN.ILCOUNT(ILNAME,IMAGE.COUNT)"
PRINT
PRINT "Size of each icon in the image list: ":
WDTH = ""
DPTH = ""
CALL WIN.ILISIZE(ILNAME,WDTH,DPTH)
PRINT WDTH:" by ":DPTH
PRINT "   CALL WIN.ILISIZE(ILNAME,WIDTH,DEPTH)"
PRINT
PRINT "Planes and bits per pixel for image 0: ":
PLANES = "";BPP = ""
CALL WIN.ILINFO(ILNAME, 0, PLANES, BPP)
PRINT PLANES:", ":BPP
PRINT "   CALL WIN.ILNAME,0, PLANES, BPP)"
PRINT
GOSUB 1000;* Continue prompt
RETURN
*
* Remove and delete page
150 PRINT @(0,3):@(-3):
PRINT "Individual images can be removed from the image list with:"
PRINT "  CALL WIN.ILREMOVE(ILNAME, 1)"
PRINT "where ILNAME is the name of the image list"
PRINT "and   1 is the index of the image to be removed"
PRINT
PRINT "If the list is being used the control using it should be refreshed"
PRINT
PRINT "Number of images in ":ILNAME2:" before image removed ":
CNT = 0
CALL WIN.ILCOUNT(ILNAME2,CNT)
PRINT CNT
CALL WIN.ILREMOVE(ILNAME2,1)
PRINT "Number of images in ":ILNAME2:" after image removed ":
CALL WIN.ILCOUNT(ILNAME2,CNT)
PRINT CNT
PRINT
ARGS(1) = 0
ARGS(2) = ILNAME2
RESP = ""
CALL WIN.DBMETHOD(DLG.NAME,"LV","SetImageList","IN",MAT ARGS,0,RESP)
PRINT "When an image list has been finished with it should be deleted with:"
PRINT "   CALL WIN.ILDELETE(ILNAME)"
PRINT
PRINT "In this example the image lists are deleted just before the program exits."
RETURN
*
* Continue prompt
1000 PRINT C22:"Press <CR> to continue":
INPUT DUM:
PRINT C22:
RETURN
*
* Create a simple dialog box with a tree view in
2000 *
    OPTS = ""
    DBX = ""
    CALL WIN.DBNEW(DBX, DLG.NAME, "Demonstration of Image Lists", 500,64,146,100, 'WS_CAPTION|WS_POPUP|WS_VISIBLE',OPTS)
    CALL WIN.DBLISTVW(DBX, "LV", 2,2,144,96,"")
    CALL WIN.DBLOAD(DBX,ERR)
    IF ERR = 0 THEN CALL WIN.DBSHOW(DLG.NAME, 0, ERR)
    IF ERR THEN PRINT;PRINT "Unable to create the demonstration dialog box"
    RETURN
*
END
