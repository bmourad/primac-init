* WIN.DEMO
* Demonstration of a few of wIntegrates many features
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-96. Impact Business Systems
*   
      GOSUB 100; * Initialise       
*
      CALL WIN.COLOR("Yellow","Blue") ;* Set text colours
      PRINT @(-1):"WIN.DEMO":
      PRINT @(27,0):"wIntegrate Demonstration":
      PRINT @(71,0):OCONV(DATE(),'D2'):
      CALL WIN.BOX(0,1,79,1,1) ;* Underline heading
*
      PRINT @(0,4):
      CALL WIN.COLOR("LightCyan","");* Set foreground colour
*
      PRINT "Welcome to a demonstration of some of the features of wIntegrate."
      PRINT @(0,6):"What would you like to see in action:-":
      CALL WIN.COLOR("White","") ;* Set foreground colour
*
      PRINT @(5,8):"1.  Bar charts":
      PRINT @(5,9):"2.  Pie charts":
      PRINT @(5,10):"3.  Images":
      PRINT @(5,11):"4.  Drawings"
*                                
      PRINT @(5,13):"5.  Colors":
      PRINT @(5,14):"6.  Text windows":
      PRINT @(5,15):"7.  Hot spots":
*
      PRINT @(5,17):"8.  DDE demo"
      PRINT @(5,18):"9.  Menus demo"
*
      PRINT @(40,8) :"10.  DialogBox lookup"
      PRINT @(40,9) :"11.  Dialog boxes"
      PRINT @(40,10):"12.  Dialog boxes: menus, fonts, colors"
*
      PRINT @(40,12):"13.  Chiselled effects"
      PRINT @(40,13):"14.  Background images"
*
      PRINT @(40,15):"15.  FTP"
*
      CALL WIN.COLOR("Yellow","") ;* Set foreground colour
      CALL WIN.BOX(0,19,79,19,1)  ;* Draw line across bottom
*
      PRINT @(5,20):"Enter M to see how the mouse can be programmed to select options":
      PRINT @(0,22):"Enter your choice or * to quit:"
      CALL WIN.SSTORE("demo");* Store current screen in memory
* 
      QUIT = 0
*
      LOOP
         CALL WIN.COLOR("RED","wHITE"); * Set to red on white text
         PRINT @(35,22):"     ":@(35,22):
         INPUT RESP:
         RESP = OCONV(RESP,'MCU')
*
         BEGIN CASE
            CASE RESP = '*'; QUIT = 1
            CASE RESP = '1'; PROG = "WIN.BARDEMO"; GOSUB 1000; * run
            CASE RESP = '2'; PROG = "WIN.PIEDEMO"; GOSUB 1000; * run
            CASE RESP = '3'; PROG = "WIN.IMDEMO"; GOSUB 1000; * run
            CASE RESP = '4'; PROG = "WIN.DRDEMO"; GOSUB 1000; * run
            CASE RESP = '5'; PROG = "WIN.COLDEMO"; GOSUB 1000; * run
            CASE RESP = '6'; PROG = "WIN.TWDEMO"; GOSUB 1000; * run
            CASE RESP = '7'; PROG = "WIN.HSDEMO"; GOSUB 1000; * run
            CASE RESP = '8' AND DESKTYPE = "MAC"; GOSUB 1100; * Not available
            CASE RESP = '8'; PROG = "WIN.DDEDEMO"; GOSUB 1000; * run
            CASE RESP = '9'; PROG = "WIN.MENUDEMO"; GOSUB 1000; * run
            CASE RESP = '10'; PROG = "WIN.LUDEMO"; GOSUB 1000; * run
            CASE RESP = '11'; PROG = "WIN.DBDEMO"; GOSUB 1000; * run
            CASE RESP = '12'; PROG = "WIN.DBDEMO2"; CV = "3.0.00"; GOSUB 1200; * Check and run 
            CASE RESP = '13'; PROG = "WIN.CEDEMO"; GOSUB 1000; * run
            CASE RESP = '14'; PROG = "WIN.BKDEMO"; CV = "4.0.3"; GOSUB 1200; * Check and run 
            CASE RESP = '15'; PROG = "WIN.FTPDEMO"; CV = "4.0.4"; GOSUB 1200; * Check and run 
            CASE RESP = 'M'; GOSUB 2000; * Mouse information
         END CASE
*                                                   
      UNTIL QUIT DO
      REPEAT
*
      CALL WIN.SREMOVE("demo") ;* Delete saved screeb
      CALL WIN.COLOR("off","") ;* Turn off colouring of text
*
      PRINT @(-1) :
      STOP
*
* Initialise the program
100 OPEN '','WIN.PARAMS' TO F.BP ELSE
      OPEN '','WIN.PROGS' TO F.BP ELSE STOP 'NO WIN.PROGS'
    END
      READV MACHINE.TYPE FROM F.BP, "MACHINE.TYPE",1 ELSE MACHINE.TYPE = ''
      SUB.SUFFIX = ''
      IF MACHINE.TYPE # '' AND MACHINE.TYPE # 'GENERIC' THEN SUB.SUFFIX = '.' : MACHINE.TYPE
      EXECSUB = 'WIN.EXEC':SUB.SUFFIX
      DESKTYPE = ""
      CALL WIN.DESKTYPE(DESKTYPE)
      WINTVER = ''
      CALL WIN.VERSION(WINTVER)
      PROMPT ''
      RETURN
*
* Run a demonstration
1000  CALL WIN.COLOR("off",""); * Turn off text colouring
      CALL @EXECSUB(PROG, '',0); * Execute/Perform program
      PRINT @(-1):
      CALL WIN.SRESTORE("Demo");* Restore menu screen
      RETURN
*
* Unavailable message
1100  CALL WIN.COLOR("White","Red")
      CALL WIN.TWOPEN("NA", "Not available", 20,6,60,10,2)
      PRINT
      PRINT " This option is currently unavailable"
      PRINT " on this client platform."
      PRINT
      PRINT "Press return to go back to the menu:":
      INPUT DUM:
      CALL WIN.TWCLOSE("NA") ;* Close popup text window
      RETURN
*
* Version check and then run
1200 IF CV > WINTVER THEN
        CALL WIN.COLOR("White","Red")
        CALL WIN.TWOPEN("NA", "Not available", 20,6,60,10,2)
        PRINT
        PRINT " This option is not available in this"
        PRINT " version, it was added to ":CV
        PRINT
        PRINT "Press return to go back to the menu:":
        INPUT DUM:
        CALL WIN.TWCLOSE("NA") ;* Close popup text window
     END ELSE
        GOSUB 1000; * Run
     END
     RETURN
*
* Mouse Information
2000  CALL WIN.COLOR("Red", "Grey");* Red on Grey text
      CALL WIN.TWOPEN("Mouse","Mouse setup", 10,4,70,11,2);* Open text window for mouse information
      CALL WIN.COLOR("White","");* White text
      PRINT "To set the mouse up to enter the word under the cursor "
      PRINT "use Setup Mouse now to enter the following text as the"
      PRINT "Definition of the mouse button/action you want to use."
      PRINT "Click on any of the mouse actions to use that action."
      PRINT "Don't forget to turn on the Macro checkbox."
      CALL WIN.COLOR("LightCyan","");* Change foreground colour
      PRINT "   Enter ScreenWord(Mouse(Get_X), Mouse(Get_Y), 0)"
      CALL WIN.COLOR("White",""); * White text
      PRINT "When you've done this, you can click on the option numbers."
      PRINT "Press return to go back to the menu:":
      INPUT DUM:
      CALL WIN.TWCLOSE("Mouse") ;* Close popup text window
      RETURN
*
   END
