      SUBROUTINE WIN.XLADDWS(XLFILE, ADDFILE, OPTS, RESP)
* Subroutine to issue commands to wIntegrate PC
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
*
AFTER.SHEET = OPTS<1>
IF AFTER.SHEET = "" THEN AFTER.SHEET = 1
*
* Object names
XLMAIN = "xlmain"
XLMAINSHEET = "xlmainsheet"
XLADD = "xladd"
XLADDSHEET = "xladdsheet"
*
DIM METHOD.ARGS(20)
MAT METHOD.ARGS = ""
*
RESP = 0
TEMP.NAME = ""
*
OBERR = ""
* Get xl interface to first file
CALL WIN.OBGET(XLMAIN,XLFILE,"Excel.Sheet", OBERR)
IF OBERR THEN
   RESP = 2
   IF OBERR = 7 THEN RESP = 1
END ELSE
   * Get xl interface to second file
   CALL WIN.OBGET(XLADD, ADDFILE, "Excel.Sheet", OBERR)
   IF OBERR THEN
      RESP = 4
      IF OBERR = 7 THEN RESP = 3
   END ELSE
*
      * Need to make main worksheet visible
      XLWIND = "xlwind"
      CALL WIN.OBSET(XLMAIN, "windows.item(1)", XLWIND)
      CALL WIN.OBSETPRP(XLWIND, "visible", 1)
      CALL WIN.OBREL(XLWIND)
*
      * Get interface to actual worksheets
      CALL WIN.OBSET(XLMAIN, "sheets.item(":AFTER.SHEET:")", XLMAINSHEET)
      CALL WIN.OBSET(XLADD, "sheets.item(1)", XLADDSHEET)
*
      * Copy Work sheet to correct position
      METHOD.ARGS(1) = 'VarType("OPT")'; * Specify optional argument
      METHOD.ARGS(2) = XLMAINSHEET
      RV = ""
      CALL WIN.OBMETHOD(XLADDSHEET,"Copy","ND", MAT METHOD.ARGS, 0, RV)
*
      * Release added sheet
      CALL WIN.OBREL(XLADDSHEET)
*
      * Need to save with a temporary name
      CALL WIN.EVAL("FileTempName()", TEMP.NAME)
      IF TEMP.NAME # "" THEN
         CALL WIN.PCDELETE(TEMP.NAME)
         * Save the new combined sheet
         METHOD.ARGS(1) = TEMP.NAME
         METHOD.ARGS(2) = 1;* Save in workbook format
         CALL WIN.OBMETHOD(XLMAIN, "SaveAs", "IN", MAT METHOD.ARGS, 0, RV)
   *
         * Close file so we can replace it
         METHOD.ARGS(1) = 0; * Stop save on close prompt
         CALL WIN.OBMETHOD(XLMAIN, "Close", "I", MAT METHOD.ARGS, 0, RV)
      END
*
*     Release main sheet
      CALL WIN.OBREL(XLMAINSHEET)
*
      * Release addtofile
      CALL WIN.OBREL(XLADD)      
   END
   * Release main file
   CALL WIN.OBREL(XLMAIN)
*
* Move Work file to original name
   IF TEMP.NAME # "" THEN
      CALL WIN.PCMOVE(TEMP.NAME, XLFILE, 1)
   END
*
END
*
RETURN
*
END
