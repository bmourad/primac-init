* WIN.FTPDEMO
* FTP demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
PROMPT ''
EQU TRUE TO 1, FALSE TO 0
*
AMVM=1; AMVM<1,2>=2; AMVM<2>=3
AM=AMVM[4,1]
VM=AMVM[2,1]
*
C22 = @(0,22):@(-4)
*
CALL WIN.SSTATE(OLD.STATE,"")
CALL WIN.COLOR("White","Blue")
*
PRINT @(-1):"WIN.FTPDEMO":
PRINT @(26,0):"FTP demonstration":
PRINT @(71,0):OCONV(DATE(),"D2"):
CALL WIN.BOX(0,1,79,1,1)
*
OK = TRUE
CONNECTED = FALSE
*
GOSUB 100; * Introduction
IF OK THEN GOSUB 200; * Logon screen
IF OK THEN GOSUB 300; * Main demonstration screen
*
IF CONNECTED THEN CALL WIN.FTPDISC
*
CALL WIN.SSTATE("",OLD.STATE)
*   
STOP
*
* Print a brief description
100 PRINT @(0,3):
   PRINT "You can use the FTP routines to control a remote FTP server from your host."
   PRINT
   PRINT "This allows full FTP capabilities, including fast transfer of files between a"
   PRINT "remote server and your local PC."
   PRINT
   PRINT "For FTP to work the Windows PC should be running Windows Sockets or equivalent"
   PRINT "and the remote server you wish to contact must be set up as an FTP server."
   PRINT
   PRINT "This demonstration implements a simple character-based FTP program. It allows"
   PRINT "you to view directories/files on the remote server, get their details, and"
   PRINT "transfer a file between the PC and server."
*
   PRINT C22:'Press <CR> to continue with the demonstration': 
   INPUT DUM:
*
   PRINT @(0,3):@(-3):
   PRINT "The FTP host programs used in this demonstration are:-"
   PRINT
   PRINT "Make the initial connection:"
   PRINT "  CALL WIN.FTPCON(SERVER,USERNAME,PASSWORD,ERRMSG)"
   PRINT
   PRINT "Set/Get the current directory on the server:"
   PRINT "  CALL WIN.FTPGETDR(DIRNAME)"
   PRINT "  CALL WIN.FTPSETDR(DIRNAME)"
   PRINT "To get the directory listing:"
   PRINT "  CALL WIN.FTPLIST('*',3+32, DIRLIST)"
   PRINT "To get information on a remote directory or file:"
   PRINT "  CALL WIN.FTPINFO(FILE, INFO)"
   PRINT "To transfer files:"
   PRINT "   CALL WIN.FTPGET(REMOTE.FILE, PC.FILE, TYPE, RESP)"
   PRINT "   CALL WIN.FTPPUT(PC.FILE, REMOTE.FILE, TYPE, RESP)"
   PRINT
   PRINT "Finally the connection is disconnected with:"
   PRINT "   CALL WIN.FTPDISC"
*   
   LOOP
      PRINT C22:"Do you want to run the main demonstration? (Y/N) ":
      INPUT DUM:
      DUM = OCONV(DUM,"MCU")
   UNTIL DUM = "Y" OR DUM = "N" OR DUM = "*" DO
   REPEAT
   IF DUM = "N" OR DUM = "*" THEN OK = 0
*
   RETURN
*
* Logon screen
200 PRINT @(0,3):@(-3):
    PRINT "Before using any of the other FTP routines we must first connect to the"
    PRINT "FTP server."
*
    PRINT C22:"Enter your server details, or * to exit the demonstration"
*
    CALL WIN.COLOR("LightCyan","")
    PRINT @(5,8) :"Server name:":
    PRINT @(5,10):"User name:":
    PRINT @(5,12):"Password:":
    PRINT @(5,13):"(Your password will not be displayed on the screen)":
    CALL WIN.COLOR("Yellow","")
*
    FLDNO = 1
    LOOP
       BEGIN CASE
       CASE FLDNO = 1; GOSUB 210;* Server
       CASE FLDNO = 2; GOSUB 220;* User name
       CASE FLDNO = 3; GOSUB 230;* Password
       CASE FLDNO = 4; GOSUB 240;* Connect
       END CASE
    UNTIL RESP = "*" OR FLDNO = 4 DO
       FLDNO = FLDNO + 1
    REPEAT
*
    IF RESP = "*" THEN OK = FALSE
    CALL WIN.COLOR("WHITE","")
*
    RETURN
*
* Get server
210 PRINT @(20,8):@(-4):
    INPUT RESP:
    IF RESP # "*" THEN SERVER = RESP
    IF RESP = "" THEN
       FLDNO = FLDNO - 1
       CALL WIN.COLOR("LightRED","")
       PRINT @(20,9):"You must enter a server name or * to exit":
       CALL WIN.COLOR("YELLOW","")
    END ELSE
       PRINT @(20,9):@(-4):
    END
    RETURN
*
* Get user name
220 PRINT @(20,10):@(-4):
    INPUT RESP:
    IF RESP # "*" THEN USERNAME = RESP
    IF RESP = "" THEN PRINT "(anonymous)":
    RETURN
*
* Get password
230 ECHO OFF
    PRINT @(20,12):
    INPUT RESP:
    ECHO ON
    IF RESP # "*" THEN PASSWORD = RESP
    RETURN
*
* Make connection
240 PRINT @(0,15):"Connecting to FTP server, please wait...":
CALL WIN.FTPCON(SERVER,USERNAME,PASSWORD,ERRMSG)
   IF ERRMSG = "" THEN
      CONNECTED = TRUE
      PRINT @(0,15):@(-4):"Connection to server established":
      PRINT C22:"Press <CR> to continue":
      INPUT DUM:
   END ELSE
     CALL WIN.COLOR("LightRED","")
     PRINT @(0,15):@(-4):"Unable to connect to the server"
     PRINT ERRMSG:
     CALL WIN.COLOR("Yellow","")
     LOOP
       PRINT C22:"Do you wish to re-enter your parameters? (Y/N) ":
       INPUT RESP:
       RESP = OCONV(RESP,"MCU")
     UNTIL RESP = "Y" OR RESP = "N" DO
     REPEAT
     PRINT @(0,15):@(-3):
     IF RESP = "Y" THEN
       PRINT C22:"Re-enter server parameters or * to exit":
       FLDNO = 0
     END ELSE
       RESP = "*"
     END
   END
*
   RETURN
*
* Main demonstration screen
300 PRINT @(0,3):@(-3):
    CALL WIN.COLOR("White","Cyan")
    CALL WIN.TWOPEN("PROMPT","Options", 53,3,78,22,"SINGLE")
    PRINT @(-1):
    PRINT "*    to exit"
    PRINT "U    up line"
    PRINT "D    down line"
    PRINT "<CR> go into directory"
    PRINT "P    up page"
    PRINT "N    down page"
    PRINT "I    file information"
*    PRINT "H    Show first few bytes"
    PRINT "G    Get file"
    PRINT "S    Send file"
    PRINT
    PRINT "Up/down arrow keys and"
    PRINT "page up/down can also"
    PRINT "be used"
    PRINT
    PRINT "Directories start '>'"
*
    CALL WIN.COLOR("White","Blue")
*
    GOSUB 310;* Display directory
    GOSUB 330;* Hilight entry
*
    GOSUB 350;* Reprogram keys
*
    LOOP
      PRINT @(35,19):"Option:":@(-4):
      INPUT OPT:
    UNTIL OPT = "*" DO
      OPT = OCONV(OPT,"MCU")
      BEGIN CASE
      CASE OPT = "U" AND DIRPOS > 1
         IF DIRPOS = PAGE.START THEN
            PAGE.START = PAGE.START - ENTRIES.PER.PAGE
            GOSUB 320;* Display page
         END ELSE GOSUB 340;* Unhilight
         DIRPOS = DIRPOS - 1
         GOSUB 330;* Hilight
*
      CASE OPT = "D" AND DIRPOS < DIRCOUNT
        IF DIRPOS = PAGE.START + ENTRIES.PER.PAGE -1 THEN
           PAGE.START = PAGE.START + ENTRIES.PER.PAGE
           GOSUB 320;* Display page
        END ELSE GOSUB 340;* Unhilight
        DIRPOS = DIRPOS + 1
        GOSUB 330;* Hilight
*
      CASE OPT = "P" AND PAGE.START > 1
           PAGE.START = PAGE.START - ENTRIES.PER.PAGE
           DIRPOS = DIRPOS - ENTRIES.PER.PAGE
           IF DIRPOS < 1 THEN DIRPOS = 1
           GOSUB 320;* Display page
           GOSUB 330;* Hilight
*
      CASE OPT = "N" AND PAGE.START + ENTRIES.PER.PAGE < DIRCOUNT
           PAGE.START = PAGE.START + ENTRIES.PER.PAGE
           DIRPOS = DIRPOS + ENTRIES.PER.PAGE
           IF DIRPOS > DIRCOUNT THEN DIRPOS = DIRCOUNT
           GOSUB 320;* Display page
           GOSUB 330;* Hilight
*
      CASE OPT = "" AND DIRCOUNT
         DIRNAME = DIRLIST<DIRPOS>
         IF DIRNAME[1,1] = ">" THEN
            DIRNAME = DIRNAME[2,999]
            IF DIRNAME = "[parent]" THEN DIRNAME = ".."
            CALL WIN.FTPSETDR(DIRNAME)
            GOSUB 310;* Redo directory listing
            GOSUB 330;* Hilight entry
         END
*
      CASE OPT = "I"; GOSUB 360;* Show information on selected file
*
*      CASE OPT = "H"; GOSUB 370;* Show first few bytes of file
*
      CASE OPT = "G"; GOSUB 380;* Get file
*
      CASE OPT = "S"; GOSUB 390;* Send file
* 
      END CASE
*
    REPEAT
*
    CALL WIN.TWCLOSE("DIRLIST")
    CALL WIN.TWCLOSE("PROMPT")
    GOSUB 355;* Restore keys
    RETURN
*
* Get and display remote directory
310 DIRECTORY = ""
    CALL WIN.FTPGETDR(DIRECTORY)
    CALL WIN.TWOPEN("DIRLIST",DIRECTORY,1,3,50,22,"DOUBLE")
    CALL WIN.FTPLIST("*",3+32, DIRLIST)
    IF DIRLIST<1> = ".." THEN DIRLIST<1> = ">[parent]"
    DIRPOS = 1
    PAGE.START = 1
    DIRCOUNT = COUNT(DIRLIST,AM) + (DIRLIST # "")
    ENTRIES.PER.PAGE = 20
    GOSUB 320;*Display page
    RETURN
*
* Display one page of directory listing
320 PRINT @(-1):
    IF DIRCOUNT = 0 THEN
       PRINT "No entries to display"
    END ELSE
       FOR J = 1 TO ENTRIES.PER.PAGE
          DIRNAME = DIRLIST<J+PAGE.START-1>
          PRINT @(0,J-1):
          IF DIRNAME[1,1] # ">" THEN PRINT " ":
          PRINT DIRNAME:
       NEXT J
       CALL WIN.TWFOOT("DIRLIST","Page ":(INT(DIRPOS/ENTRIES.PER.PAGE) + 1):" of ":(INT(DIRCOUNT/ENTRIES.PER.PAGE) + 1),"R")
   END
*
RETURN
*
* Hilight entry
330 IF DIRCOUNT THEN
    PRINT @(1,DIRPOS-PAGE.START):
    CALL WIN.COLOR("Blue","Yellow")
      IF DIRLIST<DIRPOS>[1,1] = ">" THEN
        PRINT DIRLIST<DIRPOS>[2,50]:
      END ELSE
        PRINT DIRLIST<DIRPOS>:
      END
    CALL WIN.COLOR("White","Blue")
    END
    RETURN
*
* Un-hilight entry
340 IF DIRCOUNT THEN
      PRINT @(1,DIRPOS-PAGE.START):
      IF DIRLIST<DIRPOS>[1,1] = ">" THEN
        PRINT DIRLIST<DIRPOS>[2,50]:
      END ELSE
        PRINT DIRLIST<DIRPOS>:
      END
    END
    RETURN
*
* Re-program keys
350 KEY.LIST = "UpArrow"
    KEY.LIST<-1> = "DownArrow"
    KEY.LIST<-1> = "PageUp"
    KEY.LIST<-1> = "PageDown"
    KEY.SAVE = ""
    NO.KEYS = COUNT(KEY.LIST,AM) + 1
    FOR J = 1 TO NO.KEYS
      CALL WIN.GETPARAM("Key_":KEY.LIST<J>,VALUE)
      KEY.SAVE<J> = VALUE
    NEXT J
*
    CALL WIN.SETPARAM("Key_UpArrow","U\r")
    CALL WIN.SETPARAM("Key_DownArrow","D\r")
    CALL WIN.SETPARAM("Key_PageUp","P\r")
    CALL WIN.SETPARAM("Key_PageDown","N\r")
RETURN
*
* Restore keys
355 FOR J = 1 TO NO.KEYS
      CALL WIN.SETPARAM("Key_":KEY.LIST<J>,KEY.SAVE<J>)
    NEXT J
    RETURN
*
* Show information on selected file
360 IF DIRPOS >= 1 AND DIRPOS <= DIRCOUNT THEN
 DIRNAME = DIRLIST<DIRPOS>
 IF DIRNAME[1,1] = ">" THEN DIRNAME = DIRNAME[2,999]
 CALL WIN.COLOR("Yellow","Magenta")
 CALL WIN.TWPUSH("Information",10,5,60,12,"SINGLE")
 PRINT DIRNAME
 PRINT
 INFO = ""
 CALL WIN.FTPINFO(DIRNAME, INFO)
 PRINT "Length     = ":FIELD(INFO," ",1)
 PRINT "Date       = ":FIELD(INFO," ",2)
 PRINT "Time       = ":FIELD(INFO," ",3)
 PRINT "Attributes = ":FIELD(INFO," ",4)
 PRINT
 PRINT "Press <CR> to return to main screen":
 INPUT DUM:
 CALL WIN.TWPULL
 CALL WIN.TWUSE("DIRLIST")
 CALL WIN.COLOR("White","Blue")
END
RETURN
*
* Show header
370  IF DIRPOS >= 1 AND DIRPOS <= DIRCOUNT THEN
 DIRNAME = DIRLIST<DIRPOS>
 CALL WIN.COLOR("BLACK","Cyan")
 CALL WIN.TWPUSH("Header",10,5,60,12,"SINGLE")
 CALL WIN.COLOR("WHITE","")
*
 IF DIRNAME[1,1] = ">" THEN
   PRINT "The entry selected is directory"
   PRINT DIRNAME[2,999]
   PRINT "so can't be read"
 END ELSE
   PRINT DIRNAME
   PRINT "First few bytes of file"
   PRINT "(non-ascii character replaces with full stop)"
   CALL WIN.FTPOPEN(DIRNAME,1, OK)
   IF OK THEN
     CALL WIN.FTPREAD(VALUE,150,"")
     L = LEN(VALUE)
     FOR J = 1 TO L
       IF SEQ(VALUE[J,1]) < 32 THEN
         PRINT ".":
       END ELSE
         PRINT VALUE[J,1]:
       END
     NEXT J 
     PRINT
     PRINT
     CALL WIN.FTPCLOSE
   END ELSE
     PRINT "Unable to open file on server"
   END
 END
 PRINT "Press <CR> to return to main screen":
 INPUT DUM:
 CALL WIN.TWPULL
 CALL WIN.TWUSE("DIRLIST")
 CALL WIN.COLOR("White","Blue")
END
*
* Get file
380  IF DIRPOS >= 1 AND DIRPOS <= DIRCOUNT THEN
 DIRNAME = DIRLIST<DIRPOS>
 CALL WIN.COLOR("BLACK","Cyan")
 CALL WIN.TWPUSH("Copy file from FTP Server to PC",10,5,60,12,"SINGLE")
 CALL WIN.COLOR("WHITE","")
*
 IF DIRNAME[1,1] = ">" THEN
   PRINT "The entry selected is directory"
   PRINT DIRNAME[2,999]
   PRINT "so can't be copied to the PC"
 END ELSE
   PRINT "Get :":DIRNAME
   PRINT "Enter name of file on the PC or * to exit, <CR> for name on server:"
   PRINT "If directory isn't specified it defaults to C:\ "
   PRINT "?":
   INPUT FIL:
   IF FIL = "" THEN FIL = DIRNAME
   PRINT
   IF FIL # "*" THEN
      IF FIL[1,2] # "\\" AND INDEX(FIL,":",1) = 0 THEN FIL = "C:\":FIL
      CALL WIN.FTPGET(DIRNAME,FIL,1,RESP)
      IF RESP # "" THEN
         CALL WIN.COLOR("RED","")
         PRINT RESP<1>
         CALL WIN.COLOR("WHITE","")
      END ELSE
         PRINT "File copied successfully"
      END
   END
 END
*
 PRINT
 PRINT "Press <CR> to return to main screen":
 INPUT DUM:
 CALL WIN.TWPULL
 CALL WIN.TWUSE("DIRLIST")
 CALL WIN.COLOR("White","Blue")
END
*
RETURN
*
* Send file
390 CALL WIN.COLOR("BLACK","Cyan")
CALL WIN.TWPUSH("Copy file from PC to Server",10,5,60,12,"SINGLE")
CALL WIN.COLOR("WHITE","")
REFRESH = 0
*
PRINT "Enter name of file on the PC, * or <CR> to exit:"
PRINT "If directory isn't specified it defaults to C:\ "
PRINT "?":
INPUT FIL
IF FIL = "" THEN FIL = "*"
IF FIL # "*" THEN
  IF FIL[1,2] # "\\" AND INDEX(FIL,":",1) = 0 THEN FIL = "C:\":FIL
  PRINT "Enter name of file on the server, * exits, <CR> use PC leaf name"
  PRINT "?":
  INPUT TFIL
  IF TFIL = "" THEN TFIL = FIELD(FIL,"\",COUNT(FIL,"\")+1)
  IF TFIL # "*" THEN
    CALL WIN.FTPPUT(FIL, TFIL,1,RESP)
    IF RESP # "" THEN
      CALL WIN.COLOR("RED","")
      PRINT RESP<1>
      CALL WIN.COLOR("WHITE","")
    END ELSE
      PRINT "File copied successfully"
      REFRESH = 1
    END
  END
END
PRINT "Press <CR> to return to main screen":
INPUT DUM:
CALL WIN.TWPULL
CALL WIN.TWUSE("DIRLIST")
CALL WIN.COLOR("White","Blue")
IF REFRESH THEN GOSUB 310;* Re-read directory
*
RETURN
* 
END
