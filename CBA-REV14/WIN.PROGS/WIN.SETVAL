  SUBROUTINE WIN.SETVAL(VALUE,OPTS)
* Sends data up to a script which is running a Host Get statement
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
* Remember to run Host Get on the PC before calling this routine
*
* Assumes var contains printable chars, cr and tab only.
* cr and tab are converted to am and vm.
*
  AMVM=1; AMVM<1,2>=2; AMVM<2>=3
  AM=AMVM[4,1]
  VM=AMVM[2,1]
  STX=CHAR(2)
  EOL=CHAR(13)
*
  REC = VALUE
*
  S = "\";R ="\\"; GOSUB 100
  S = AM;R = "\r"; GOSUB 100
  S = VM;R = "\t"; GOSUB 100
  L = LEN(REC)
  PRINT STX:"bs":REC[1,118]:EOL:
  IF L > 118 THEN
    FOR P = 119 TO L STEP 120
      PRINT STX:REC[P,120]:EOL:
    NEXT P
  END
  PRINT STX:EOL:
  RETURN
*
* Convert S to R in REC
100 POS = 1
  LOOP
    C = INDEX(REC[POS,99999],S,1)
  WHILE C DO
    POS = POS + C - 1
    REC = REC[1,POS-1] : R : REC[POS+1,99999]
    POS = POS + LEN(R)
  REPEAT
*
  RETURN
*
END
