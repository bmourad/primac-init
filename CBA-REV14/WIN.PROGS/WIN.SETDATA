      SUBROUTINE WIN.SETDATA(VARNAME, VALUE)
* Set a wIntegrate "Global" variable
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
      EQU MAXLENGTH TO 200
      HQ = CHAR(2)
      WC = 'WIN.COMSUB'
*
      SEND.VALUE = VALUE
      CALL WIN.ASCTOFT(SEND.VALUE); * Convert to 7 bit value
*
      LEN.SEND.VALUE = LEN(SEND.VALUE)
*
      IF LEN.SEND.VALUE + LEN(VARNAME) < MAXLENGTH THEN
         SCRIPT.SEND.VALUE = HQ:SEND.VALUE:HQ
      END ELSE
         CALL @WC("Global TRV=''")
         SCRIPT.SEND.VALUE = "TRV"
*
         POS = 1
         LOOP
            NO.CHARS = MAXLENGTH - 6
            IF NO.CHARS > LEN.SEND.VALUE THEN NO.CHARS = LEN.SEND.VALUE
            CALL @WC("TRV:=":HQ:SEND.VALUE[POS,NO.CHARS]:HQ)
            LEN.SEND.VALUE = LEN.SEND.VALUE - NO.CHARS
         WHILE LEN.SEND.VALUE > 0 DO
            POS = POS + NO.CHARS
         REPEAT
*                                           
      END                                   
*
      SCRIPT.SEND.VALUE = "FTtoAscii(":SCRIPT.SEND.VALUE:")"
      CALL @WC("Global ":VARNAME:"=":SCRIPT.SEND.VALUE)
*
      RETURN
*
   END
