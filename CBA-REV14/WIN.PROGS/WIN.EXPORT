SUBROUTINE WIN.EXPORT(PCFILE,FILE,ITEMS,FIELDS,OPTS,STATUS)
* File Export, i.e. PC to Host
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-2000 Impact Business Systems.
*                     
PROMPT ''
*
STX=CHAR(2)
*
OPEN '','WIN.PARAMS' TO F.BP ELSE
  OPEN '','WIN.PROGS' TO F.BP ELSE STOP 'NO WIN.PROGS'
END
READV MACHINE.TYPE FROM F.BP,"MACHINE.TYPE",1 ELSE MACHINE.TYPE =''
IF MACHINE.TYPE ='' THEN MACHINE.TYPE ='GENERIC'
SUB.SUFFIX = ''
IF MACHINE.TYPE # 'GENERIC' THEN SUB.SUFFIX = '.':MACHINE.TYPE
*
EXECSUB = "WIN.EXEC":SUB.SUFFIX
*
R.SCRIPT = "Dialog RunExportFile"
ACCOUNT = ''
IF FILE[1,1] = '(' THEN
   ACCOUNT = FIELD(FILE,' ',1)[2,99]
   FILE = FIELD(FILE,' ',2)
END
*
R.SCRIPT<-1> = "Set Account = '":ACCOUNT:"'"
R.SCRIPT<-1> = "Set File ='":FILE:"'"
R.SCRIPT<-1> = "Set Items = '":ITEMS[1,240]:"'"
L = LEN(ITEMS)
FOR J = 241 TO L STEP 240
   R.SCRIPT<-1> = "Set Items := '":ITEMS[J,240]:"'"
NEXT J
R.SCRIPT<-1> = "Set Fields = '":FIELDS[1,240]:"'"
L = LEN(FIELDS)
FOR J = 241 TO L STEP 240
   R.SCRIPT<-1> = "Set Fields := '":FIELDS[J,240]:"'"
NEXT J
R.SCRIPT<-1> = "Set DosFile = '":PCFILE:"'"
*
* Set format
DUM = OPTS<1>
IF DUM = '' THEN
   DOT.CNT = COUNT(PCFILE,".")
   IF DOT.CNT THEN DUM = FIELD(PCFILE, ".",DOT.CNT+1)
   IF OCONV(DUM,"MCU") = "TXT" THEN DUM = "ASC"
END
R.SCRIPT<-1> = "Set Format = '":DUM:"'"
*
* Overwrite
DUM = OPTS<2>
IF DUM = '' THEN DUM = "Yes"           
R.SCRIPT<-1> = "Set Overwrite='":DUM:"'"
* MultipleDisks
DUM = OPTS<3>
IF DUM = '' THEN DUM = 0
R.SCRIPT<-1> = "Set MultipleDisks=":DUM     
* Translate
DUM = OPTS<6>
IF DUM = '' THEN DUM = "Ascii"
R.SCRIPT<-1>="Set Translate='":DUM:"'"
* Translation table
DUM =OPTS<7>
IF DUM = '' THEN DUM="\255,\r\n\f\r\n,\254,\r\n"
R.SCRIPT<-1>="Set Translation='":DUM:"'"
* Auto Exit
DUM = OPTS<8>
IF DUM = '' THEN DUM = 1
R.SCRIPT<-1>="Set AutoExit=":DUM
* Inform
DUM = OPTS<9>
IF DUM = '' THEN DUM = 0
R.SCRIPT<-1>="Set Inform=":DUM
* Time out
DUM = OPTS<10>
IF DUM='' THEN DUM = 5
R.SCRIPT<-1>="Set Timeout=":DUM
* Retries
DUM = OPTS<11>     
IF DUM = '' THEN DUM = 3
R.SCRIPT<-1>="Set Retries=":DUM
* Use formatting information
DUM = OPTS<12>
IF DUM = '' THEN DUM = '0'
R.SCRIPT<-1>="If Version >= '4.1' Then Set UseFormattingInformation=":DUM
R.SCRIPT<-1>="Invoke"
R.SCRIPT<-1>="Enter stx:ReturnValue"
*                         
* Start PC side of transfer
CALL WIN.HSCRIPTC(R.SCRIPT)
*
* Start host size of transfer
CALL @EXECSUB("WIN.TRANSFER",'','')
*
* Get result
ECHO OFF
LOOP
INPUT STATUS:
UNTIL INDEX(STATUS,STX,1) DO
REPEAT
STATUS = FIELD(STATUS,STX,2)
ECHO ON
*
RETURN
*
END
