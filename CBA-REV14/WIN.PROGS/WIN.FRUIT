* WIN.FRUIT
* Example of a DDE Link to a visual basic program
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
PROMPT ''
*
OPEN '','FRUIT' TO F.FRUIT ELSE
   PRINT "This program needs a small file called 'FRUIT'"
   PRINT "Please create this file and re-run the program."
   PRINT
   STOP
END
*
DIM NAME(3)
NAME(1) = 'USA'
NAME(2) = 'CANADA'
NAME(3) = 'UK'
*
PRINT @(-1) : "WIN.FRUIT" : @(30,0) : "Fruit server" : @(71,0) : OCONV(DATE(), "D2")
PRINT @(0,2):"This program uses wIntegrate as a DDE server to a Visual Basic program"
PRINT "fruity.exe. Data is held in a simple host file called FRUIT which contains"
PRINT "the number of apples, oranges and bananas eaten in three countries."
PRINT "You can drag and drop fruit onto countries, and retrieve/update the host."
PRINT
PRINT "This program requires Visual Basic 2.0 support DLLs"
*
GOSUB 300;* Start up fruity.exe
IF NOT(RUNNING) THEN STOP
*
PRINT @(24,18):"Enter EXIT to stop this program" :
*
CALL WIN.COLOR("Yellow","Blue")
CALL WIN.BOX(20,9,60,16,1)
PRINT @(23,9):"Data sent from Visual Basic program":
CALL WIN.COMSUB("Screen ScrollRegion 21,10,59,15")
*
CALL WIN.COMSUB('DDE Initiate fruit,"Fruity","Fruit"')
CALL WIN.COMSUB('DDE Advise fruit,"action",fruit_action')
CALL WIN.COMSUB("DDE HostLink fruit, '%d':cr")
*
* Wait for action to change
LOOP
   INPUT ACTION
   ACTION = OCONV(ACTION, "MCU")
*
   BEGIN CASE
   CASE ACTION = "READ" ; GOSUB 100
   CASE ACTION = "WRITE"; GOSUB 200
   END CASE
*
UNTIL ACTION = "EXIT" OR ACTION = "CANCEL" DO
REPEAT
*                      
CALL WIN.COMSUB("Screen ScrollRegion")
CALL WIN.COLOR("Off","")
PRINT @(0,20):
IF ACTION = "EXIT" THEN
   CALL WIN.COMSUB('DDE Unadvise fruit,"action"')
END
CALL WIN.COMSUB("DDE Terminate fruit")
*
STOP
*
* Read Record and set visual basic values
100 FOR J = 1 TO 3
      READ R.FRUIT FROM F.FRUIT, NAME(J) THEN
         LINE = R.FRUIT<1>:",":R.FRUIT<2>:",":R.FRUIT<3>
         CALL WIN.COMSUB("DDE Poke fruit,'":NAME(J):"Data','":LINE:"'")
      END
    NEXT J
    CALL WIN.COMSUB("DDE Poke fruit,'Action','ReadOK'")
    RETURN
*
* Write record with visual basic data
200 FOR J = 1 TO 3
       LINE = "Enter DDERequest(fruit,'":NAME(J):"Data')"
       CALL WIN.COMSUB(LINE)
       PRINT NAME(J):" value = ":
       INPUT VALUE
       R.FRUIT = ''
       R.FRUIT<1> = FIELD(VALUE,",",1)
       R.FRUIT<2> = FIELD(VALUE,",",2)
       R.FRUIT<3> = FIELD(VALUE,",",3)
       WRITE R.FRUIT ON F.FRUIT, NAME(J)
    NEXT J
    RETURN
*
* Check if fruity.exe is running, If not try to start it
300 CALL WIN.COMSUB("Enter IsTask('Fruit Feast')")
PRINT @(0,18):
INPUT RUNNING:
IF RUNNING THEN
   PRINT @(0,18):"  ":
END ELSE
   CALL WIN.COMSUB("Screen ScrollRegion")
   PRINT @(0,18):'Please wait - starting the FRUITY.EXE program...':
   SCRIPT = 'Dialog RunProgram'
   SCRIPT<-1> = 'Set Filename = AppDir:"example\vb\fruity.exe"'
   SCRIPT<-1> = 'Invoke'
   SCRIPT<-1> = "Enter IsTask('Fruit Feast')"
   CALL WIN.HSCRIPTC(SCRIPT)
   INPUT RUNNING:
   PRINT @(0,18):@(-4):
   IF NOT(RUNNING) THEN                              
      PRINT 'Unable to start the WIN.FRUIT program'
   END
END
*
RETURN
*
END
