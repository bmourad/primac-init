      SUBROUTINE WIN.ASCTOFT(VALUE)
* Convert 8 bit ascii to file transfer format
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-2001. Impact Business Systems
*
      ITEMSEP = "|"; * Character to separate pick items        
      CHARSUB1 = "}"; * Shift following byte to 0-31 or 214-255
      CHARSUB2 = "{"; * Shift following byte to 123-213        
      AMCHAR = "~"; * Replace AM with this character           
*                                                              
      ITEMSEPRPL = "{!"                                        
      AMCHARRPL = "{#"                                         
      CHARSUB1RPL = '{"'                                       
      CHARSUB2RPL = "{ "                                       
*
      NEWVAL = ''
*
      J = 0                                               
      LOOP                                                
*
         J = J + 1
         K = J
         LOOP
           CH = VALUE[K,1]                                    
         UNTIL CH < ' ' OR CH > 'z' OR CH = '' DO
           K = K + 1
         REPEAT
         IF K > J THEN NEWVAL = NEWVAL : VALUE[J, K - J]
         J = K
*
      UNTIL LEN(CH) = 0 DO
*
         CH = SEQ(CH)                                  
         BEGIN CASE                                    
           CASE CH = 255; CH = ITEMSEP                
           CASE CH = 254; CH = AMCHAR                 
           CASE CH  < 32                              
              CH = CHARSUB1 : CHAR(CH+32)             
           CASE CH > 213                              
              CH = CHARSUB1 : CHAR(CH - 150)          
           CASE CH > 122                              
              CH = CHARSUB2 : CHAR(CH - 91)           
           CASE 1                                     
              CH = CHAR(CH)                           
         END CASE                                      
         NEWVAL = NEWVAL : CH                               
*
     REPEAT                                              
*                                                         
     VALUE = NEWVAL
     RETURN
*
END
