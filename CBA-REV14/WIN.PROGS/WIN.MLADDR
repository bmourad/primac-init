SUBROUTINE WIN.MLADDR(MAIL.TO, MAIL.CC, MAIL.BCC, OPTS, DELIM)
* Show dialog to lookup email addresses
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-98. Impact Business Systems
*
  AMVM=1; AMVM<1,2>=2; AMVM<2>=3
  AM=AMVM[4,1]
  VM=AMVM[2,1]
  STX=CHAR(2)
*
* Convert out am's and vm's
REC = MAIL.TO
S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
SEND.TO = "`":REC:"`"
REC = MAIL.CC
S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
SEND.CC = "`":REC:"`"
REC = MAIL.BCC
S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
SEND.BCC = "`":REC:"`"
*
SCRIPT = ""
SCRIPT<-1> = "send_to=":SEND.TO
SCRIPT<-1> = "cc_to=":SEND.CC
SCRIPT<-1> = "bcc_to=":SEND.BCC
SCRIPT<-1> = "Mail Address send_to,cc_to,bcc_to,":OPTS
BEGIN CASE
CASE DELIM = 1; * Attribute marks
  SCRIPT<-1> = "Convert send_to,';',cr"
  SCRIPT<-1> = "Convert cc_to,';',cr"
  SCRIPT<-1> = "Convert bcc_to,';',cr"
CASE DELIM = 2; * Value marks
  SCRIPT<-1> = "Convert send_to,';',tab"
  SCRIPT<-1> = "Convert cc_to,';',tab"
  SCRIPT<-1> = "Convert bcc_to,';',tab"
END CASE
*
SCRIPT<-1> = "Host Send send_to"
SCRIPT<-1> = "Host Send cc_to"
SCRIPT<-1> = "Host Send bcc_to"
CALL WIN.FSCRIPT(SCRIPT)
*
* Get any results
CALL WIN.GETVAL(MAIL.TO)
CALL WIN.GETVAL(MAIL.CC)
CALL WIN.GETVAL(MAIL.BCC)
*
RETURN
*
* Convert S to R in REC
100 POS = 1
  LOOP
    C = INDEX(REC[POS,99999],S,1)
  WHILE C DO
    POS = POS + C - 1
    REC = REC[1,POS-1] : R : REC[POS+1,99999]
    POS = POS + LEN(R)
  REPEAT
*
  RETURN
END        
