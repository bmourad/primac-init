SUBROUTINE WIN.MLSEND(MAIL.TO, MAIL.CC, MAIL.BCC, SUBJECT, TEXT, ATTACHMENTS, OPTS, RESP)
* Send an email message
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-98. Impact Business Systems
*
  AMVM=1; AMVM<1,2>=2; AMVM<2>=3
  AM=AMVM[4,1]
  VM=AMVM[2,1]
  STX=CHAR(2)
*
SCRIPT = ""
* Convert out am's and vm's
REC = MAIL.TO; S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
SEND.TO = "`":REC:"`"
REC = MAIL.CC; S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
SEND.CC = "`":REC:"`"
REC = MAIL.BCC; S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
SEND.BCC = "`":REC:"`"
REC = ATTACHMENTS; S= AM; R = ";"; GOSUB 100
S = VM; R = ";"; GOSUB 100
FILE.LIST = "`":REC:"`"
*
* Put text in the script
N = COUNT(TEXT,AM)
SCRIPT<-1> = "Text=``"
IF N THEN
  N = N + 1
  FOR J = 1 TO N
    IF J > 1 THEN SCRIPT = SCRIPT : ":cr:lf"
    SCRIPT<-1> = "Text:=`":TEXT<J>:"`"
  NEXT J
END ELSE
  N = COUNT(TEXT,VM) + (TEXT # "")
  IF N THEN
    FOR J = 1 TO N
      IF J > 1 THEN SCRIPT = SCRIPT : ":cr:lf"
      SCRIPT<-1> = "Text:=`":TEXT<1,J>:"`"
    NEXT J
  END
END
*
SCRIPT<-1> = "Mail Send ":SEND.TO:",":SEND.CC:",":SEND.BCC:",`":SUBJECT:"`,Text,":FILE.LIST:",":OPTS:",Resp"
SCRIPT<-1> = "Enter stx:Resp"
*
PROMPT ""
ECHO OFF
CALL WIN.FSCRIPT(SCRIPT)
LOOP
   INPUT DUM:
UNTIL INDEX(DUM,STX,1) DO
REPEAT
*
ECHO ON
RESP = FIELD(DUM,STX,2)
RETURN
*
* Convert S to R in REC
100 POS = 1
  LOOP
    C = INDEX(REC[POS,99999],S,1)
  WHILE C DO
    POS = POS + C - 1
    REC = REC[1,POS-1] : R : REC[POS+1,99999]
    POS = POS + LEN(R)
  REPEAT
*
  RETURN
END      
