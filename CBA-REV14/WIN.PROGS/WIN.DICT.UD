      SUBROUTINE WIN.DICT.UD(DNAME, DREC, IREC, MODE, BAD)
* Convert Dictionary item to WIN.CONNECT's internal format
* Compile for: UD
* Copyright (c) 1991-1999. Impact Business Systems
* Copyright (c) 1993-1999. Ardent Software, Inc
*
      EQU TRUE TO 1, FALSE TO 0
      EQU EXPORT TO 2
      VM = CHAR(253)
*
*
      BAD = FALSE
      IREC = ''
      DTYPE = DREC<1>[1,1]
*
      IF DTYPE # "D" AND DTYPE # "I" AND DTYPE # "V" THEN BAD = TRUE
      IF MODE = EXPORT AND DTYPE # "D" THEN BAD = TRUE
*
      IF NOT(BAD) THEN
         IREC = DREC<2>
         DESC = DREC<4>
         L = 0
         CONVERT "}" TO VM IN DESC
         MVCNT = DCOUNT(DESC, VM)
         FOR J = 1 TO MVCNT
           IF LEN(DESC<1,J>) > L THEN L = LEN(DESC<1,J>)
         NEXT J
         IF L = 0 THEN L = LEN(DNAME)
*
         DESCLEN = DREC<5>
         MAX = LEN(DESCLEN)
IF DESCLEN[MAX,1] = "R" THEN IREC<1,9>=1
         FOR J = 1 TO MAX WHILE DESCLEN[J,1] MATCH "1N"
         NEXT J
         IF NOT(DESCLEN[J,1] MATCH "1N") THEN DESCLEN = DESCLEN[1,J-1]
*
* Early version of UniData 4.1 and later versions with UDT.OPTIONS 108
* set should replace the following line with: L = DESCLEN
* if they do not get the correct columns parsed out
* i.e. Get rid of the IF L < DESCLEN THEN
         IF L < DESCLEN THEN L = DESCLEN
         IREC<1,2> = DREC<3>
         IREC<1,3> = L
         CONVERT VM TO " " IN DESC
         IREC<1,4> = DESC
         IF IREC<1,4> = "" THEN IREC<1,4> = DNAME
         IF DTYPE = "I" OR DTYPE = "V" THEN IREC<1,5> = DREC<2>
      END
*
      RETURN
*
   END
