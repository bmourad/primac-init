SUBROUTINE WIN.FTTOASC(VALUE)
* Convert VALUE from FT format to 8 bit ascii
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-97. Impact Business Systems
*
      AM = CHAR(254)
      VM = CHAR(253)
* 
      CHARSUB1 = "}"; * Shift following byte to 0-31 or 214-255
      CHARSUB2 = "{"; * Shift following byte to 123-213
      AMCHAR = "~"; * Replace AM with this character
* 
      S = AMCHAR
      R = AM
      GOSUB 150; * Replace attribute marks
      GOSUB 170; * Replace "Escaped out" characters
      RETURN
* 
* Replace character S with character R in VALUE
150   LOOP
         C = INDEX(VALUE, S,1)
      WHILE C DO
         VALUE = VALUE[1,C-1]:R:VALUE[C+1, 99999]
      REPEAT
      RETURN
* 
* Convert control values from a value
170   POS = 0
      LOOP 
         C = INDEX(VALUE[POS+1, 99999], CHARSUB1,1)
      WHILE C DO
         C = C + POS
         CH = SEQ(VALUE[C+1,1])
         POS = C
         IF CH < 64 THEN CH = CH - 32 ELSE CH = CH + 150
         VALUE = VALUE[1,C-1]: CHAR(CH) : VALUE[C+2,99999]
      REPEAT
* 
      POS = 0
      LOOP 
         C = INDEX(VALUE[POS+1, 99999], CHARSUB2,1)
      WHILE C DO
         C = C + POS
         POS = C
         VALUE = VALUE[1,C-1]: CHAR(SEQ(VALUE[C+1,1]) + 91) : VALUE[C+2,99999]
      REPEAT
* 
      RETURN
* 
END
