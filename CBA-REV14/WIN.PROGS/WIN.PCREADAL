SUBROUTINE WIN.PCREADAL(NAME, VALUE, CNVT)
* Read all data from current position from a PC file
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-2000. Impact Business Systems
*
STX=CHAR(2)
CNVT = OCONV(CNVT,"MCU")
*
BEGIN CASE           
  CASE CNVT = "" OR CNVT = "TEXT"; FRMT="DF_FT"
  CASE CNVT = "FT";FRMT ="DF_FT"
  CASE CNVT = "HEX";FRMT = "DF_Hex"            
  CASE 1; FRMT=""
END CASE
*               
SCRIPT = "Loop"
SCRIPT = SCRIPT :";File Read ":NAME:",T,,":FRMT
SCRIPT = SCRIPT : ";While T"
IF CNVT = "TEXT" THEN SCRIPT = SCRIPT : ';T=Change(T,"}-}*","~")'
SCRIPT = SCRIPT : ';Host Send T'
SCRIPT = SCRIPT : ';Repeat'
SCRIPT = SCRIPT : ';Host Send ""'
CALL WIN.COMSUB(SCRIPT)
*
VALUE = ''
ECHO OFF
PROMPT ''
*
LOOP                  
  GOSUB 100; * Get input
UNTIL BLK = '' DO
  VALUE = VALUE : BLK
REPEAT
*                 
ECHO ON
*
RETURN
*                           
* Get input from script
100 BLK = ""
CNV = ""
LOOP
  INPUT DUM:
  IF INDEX(DUM,STX,1) THEN
    DUM = FIELD(DUM,STX,2)
    IF CNV = "" THEN
      CNV = DUM[1,2]
      BLK = BLK : DUM[3,9999]
    END ELSE
      BLK = BLK : DUM
    END
  END ELSE
    DUM = "X"
  END
UNTIL DUM = "" DO
REPEAT
IF CNV # "no" THEN GOSUB 200;* Convert
IF CNVT = "" OR CNVT = "TEXT" THEN CALL WIN.FTTOASC(BLK)
RETURN
*
* Convert data
200 TYPE = CNV[1,1]
SUBTYPE= CNV[2,1]
IF TYPE = "B" OR TYPE = "N" THEN
  TYPE = OCONV(TYPE,"MCL")
  VALUE = OCONV(VALUE,"MCL")
END
IF TYPE = "b" THEN
  POS = 1
  LOOP
    C = INDEX(VALUE[POS,99999],'\',1)
  UNTIL C = 0 DO
    POS = POS + C - 1
    CH = VALUE[POS+1,1]
    BEGIN CASE
      CASE CH = 'r'; CH = CHAR(13)
      CASE CH = 't'; CH = CHAR(9)
      CASE CH = '\'; CH = '\'
      CASE CH = 'd'; CH = CHAR(127)
    END CASE
    VALUE = VALUE[1,POS-1]:CH:VALUE[POS+2,99999]
    POS = POS + 1
  REPEAT
END ELSE
  IF TYPE # "n" THEN PRINT "Invalid conversion type ":CNV
END
RETURN
*
END
