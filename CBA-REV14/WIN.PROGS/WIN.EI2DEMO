* WIN.EI2DEMO
* Demonstrate the WIN.EI2 host routine
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2002. All rights reserved.
*
PRINT @(-1):"WIN.EI2DEMO":
PRINT @(20,0):"Demonstrate the WIN.EI2 subroutine":
PRINT @(0,2):"The WIN.EI2 subroutine is used as an alternative to the WIN.EI"
PRINT "subroutine to input data to the host with local editing on the PC."
PRINT
PRINT "To use it call WIN.EI2(X,Y,COLS,LINES, PARAMS, VALUE, EXITKEY, STATE)"
PRINT "The parameters are described in the Host Subroutines Reference."
PRINT
PRINT "To summarise:"
PRINT "   X,Y         The column,row to do the input"
PRINT "   COLS, LINES The number of columns and lines to do the input on"
PRINT "   PARAMS      Extra parameters to control the input"
PRINT "   EXITKEY     The key used to exit the subroutine"
PRINT "   STATE       The state of the input when it finished"
PRINT
PRINT "In the following screen we three simple fields, a text field, a number"
PRINT "field that take an integer and a multi-line comment field."
PRINT
PRINT "You can move between the fields by pressing TAB or Shift TAB."
PRINT
PRINT @(0,22):"Press <CR> to continue":
INPUT DUM:
PRINT @(-1):"WIN.EI2DEMO":
PRINT @(20,0):"Simple input screen demonstration"
PRINT @(0,2):"Product name:":
PRINT @(0,4):"Quantity:"
PRINT @(0,6):"Comment:"
PRINT @(0,20):"Press Escape, * or F8 to exit at any time":
*
FLDNO = 1
*
DPARAMS = ""
DPARAMS<9> = "*"
DPARAMS<10,1> = "Tab"
DPARAMS<10,2> = "S_Tab"
DPARAMS<10,3> = "F8"
*
* Set up fields
DIM R.DATA(3)
DIM STATE(3)
DIM Y(3), COLS(3), LINES(3)
DIM PARAMS(3)
Y(1) = 2;COLS(1) = 20; LINES(1) = 1; PARAMS(1) = DPARAMS
Y(2) = 4;COLS(2) = 8; LINES(2) = 1; PARAMS(2) = DPARAMS
Y(3) = 6;COLS(3) = 40; LINES(3) = 8; PARAMS(3) = DPARAMS
MAT R.DATA = ""
MAT STATE = ""
*
* Validation for number field
PARAMS(2)<8> = "0N"; * 0 or more numbers
*
* Only display 4 lines of the comment field
PARAMS(3)<6> = 4
*
LOOP
  CALL WIN.EI2(20,Y(FLDNO),COLS(FLDNO),LINES(FLDNO), PARAMS(FLDNO), R.DATA(FLDNO), EXITKEY, STATE(FLDNO))
UNTIL EXITKEY = "*" OR EXITKEY= CHAR(27) OR EXITKEY = "F8" DO
  BEGIN CASE
  CASE EXITKEY = "Tab" OR EXITKEY = ""
     FLDNO = FLDNO + 1
     IF FLDNO = 4 THEN FLDNO = 1
  CASE EXITKEY = "S_Tab"
     FLDNO = FLDNO - 1
     IF FLDNO = 0 THEN FLDNO = 3
  END CASE
REPEAT
*
PRINT @(0,20):@(-4):"Demonstration finished, Press <CR> to continue":
INPUT DUM:
*
END
