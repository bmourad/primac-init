      SUBROUTINE WIN.DICT.UP(DNAME, DREC, IREC, MODE, BAD)
* Convert Dictionary item to WIN.CONNECT's internal format
* Compile for: UP 
* Copyright (c) 1991-1993. Impact Business Systems
*
      EQU TRUE TO 1, FALSE TO 0
      EQU EXPORT TO 2
*
      BAD = FALSE
      IREC = ''
*
      BEGIN CASE
         CASE DREC<1> = 'A' OR DREC<1> = 'S'
            GOSUB 1000                  ;* Pick style dictionary
         CASE DREC<1>[1,1] = 'D' OR DREC<1>[1,1] = 'I'
            GOSUB 2000                  ;* Prime style dictionary
         CASE 1
            BAD = TRUE
      END CASE
*
      RETURN
*
*
* Pick style dictionaries
*
1000  IF MODE = EXPORT AND DREC<8> # "" THEN BAD = TRUE
*
      IF NOT(BAD) THEN
         IREC = DREC<2>
         L = DREC<10>
         IF L = 0 THEN L = LEN(DNAME)
         IREC<1,2> = DREC<7>
         IREC<1,3> = L
         IREC<1,4> = DREC<3,1>
         IF IREC<1,4> = "" THEN IREC<1,4> = DNAME
         IREC<1,5> = DREC<8>
IF DREC<9>[1,1] = "R" THEN IREC<1,9> = 1
      END
*
      RETURN
*
*
* Prime style dictionaries
*
2000  IF MODE = EXPORT AND DREC<1>[1,1] = "I" THEN BAD = TRUE
*
      IF NOT(BAD) THEN
         IREC = DREC<2>
         L = 1
*
IF DREC<5>[LEN(DREC<5>),1] = "R" THEN IREC<1,9>=1
         DESCLEN = MATCHFIELD(DREC<5>, "0N0X",1)
         IF L < DESCLEN THEN L = DESCLEN
         IREC<1,2> = DREC<3>
         IREC<1,3> = L
         IREC<1,4> = DREC<4,1>
         IF IREC<1,4> = "" THEN IREC<1,4> = DNAME
         IF DREC<1> = "I" THEN IREC<1,5> = DREC<2>
      END
*
      RETURN
*
   END  
