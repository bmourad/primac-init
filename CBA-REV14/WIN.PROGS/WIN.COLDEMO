* WIN.COLDEMO
* Demonstrate Screen Colours
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-93. Impact Business Systems
*
      PROMPT ''
      USA = 0
      IF FIELD(OCONV(0,"D2/"),"/",1) = 12 THEN USA = 1
      IF USA THEN
         WCOL = "WIN.COLOR"
         COLOUR = "color"
         GREY = "Gray"
      END ELSE
         WCOL = "WIN.COLOUR"
         COLOUR = "colour"
         GREY = "Grey"
      END
*
      CALL WIN.COLOUR("White","Blue")
*
      PRINT @(-1):"WIN.COLDEMO":
      PRINT @(20,0):"Demonstration of C":COLOUR[2,9]:"s in wIntegrate":
      PRINT @(71,0):OCONV(DATE(),"D2")
      PRINT
      CALL WIN.COLOUR("Yellow","Blue")
      PRINT "wIntegrate supports 16 different ":COLOUR:"s. These ":COLOUR:"s can be individually"
      PRINT "modified in the Setup C":COLOUR[2,9]:"s dialog to be any ":COLOUR:" from your monitor's"
      PRINT "palette."
      PRINT
      PRINT "They default to the following:-"
*
      COLS = ''
      COLS<1> = 'Black'  ; COLS<9> = GREY
      COLS<2> = 'Blue'   ; COLS<10> = 'LightBlue'
      COLS<3> = 'Green'  ; COLS<11> = 'LightGreen'
      COLS<4> = 'Cyan'   ; COLS<12> = 'LightCyan'
      COLS<5> = 'Red'    ; COLS<13> = 'LightRed'
      COLS<6> = 'Magenta'; COLS<14> = 'LightMagenta'
      COLS<7> = 'Brown'  ; COLS<15> = 'Yellow'
      COLS<8> = 'Light':GREY     ; COLS<16> = 'White'
*
      Y = 8
      X = 0
*
      FOR J = 1 TO 16
         IF J < 9 THEN CON = "White" ELSE CON = "Black"
         COL = COLS<J>
*
         S = STR(' ',14-LEN(COL))
         CALL WIN.COLOUR(COL,CON)
         PRINT @(X,Y):J-1:@(X+3,Y):COL:S:
         CALL WIN.COLOUR(CON,COL)
         PRINT @(X+20,Y):"Background":
*
         IF J = 8 THEN
            X = 40
            Y = 8
         END ELSE
            Y = Y + 1
         END
      NEXT J
*
      CALL WIN.COLOUR("Black","LightGrey")
*
      PRINT @(0,20):@(-3):"To use ":COLOUR:" just call the subroutine ":WCOL:"(foreground,background)"
      PRINT "Passing it either the number or name of the ":COLOUR:"."
*                                                            
      CALL WIN.COLOUR("Red","")
      PRINT @(0,23):"Press <CR> to continue:":
      INPUT DUM:
      CALL WIN.COLOUR("OFF","")
*
   END
