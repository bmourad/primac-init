SUBROUTINE WIN.FTPSCRPT(SCRIPT,MAT FTP.OPTS,RESP)
* Transfer a script to the PC using FTP and run it
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-99. Impact Business Systems
*
DIM FTP.OPTS(10)
EQU F.TEMP TO FTP.OPTS(1)   ;* handle to temporary file
EQU TEMPFILE TO FTP.OPTS(2) ;* Ftp path to temporary file
*
PROMPT ''
STX = CHAR(2)
*
SQ=1
LEAF="FTPSC"
FOUND = 0
LOOP
  READV DUM FROM F.TEMP,LEAF:SQ,1 ELSE FOUND = 1
UNTIL FOUND DO
  SQ = SQ + 1
REPEAT
WRITE SCRIPT ON F.TEMP,LEAF:SQ
*
SCRIPT = 'Ftp Open `':TEMPFILE:'/':LEAF:SQ:'`,1,ok'
SCRIPT = SCRIPT : ";If ok Then"
SCRIPT = SCRIPT : ";hs='';Loop;Ftp Read blk;While blk;hs:=blk;Repeat"
SCRIPT = SCRIPT : ";Ftp Close"
SCRIPT = SCRIPT : ";Convert hs,char(254):char(253),cr:tab"
SCRIPT = SCRIPT : ";Enter stx:1"
SCRIPT = SCRIPT : ";Execute hs"
SCRIPT = SCRIPT : ";Else"
SCRIPT = SCRIPT : ";Enter stx:0"
SCRIPT = SCRIPT : ";EndIf"
*
CALL WIN.COMSUB(SCRIPT)
*
ECHO OFF
LOOP
INPUT RESP:
UNTIL INDEX(RESP,STX,1) DO
REPEAT
RESP = FIELD(RESP,STX,2)
ECHO ON
*
DELETE F.TEMP, LEAF:SQ
*
RETURN
*
END
