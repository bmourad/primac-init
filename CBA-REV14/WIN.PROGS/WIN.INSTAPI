* WIN.INSTAPI
* Installation of host API.
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* Copyright (c) 2000. Informix Corporation. All rights reserved.
* Copyright (c) 1991-2000. Impact Business Systems
* 
* Host machine type is read from MACHINE.TYPE record on WIN.PROGS
* 
DUM = @(-1); * Stops automatic screen paging
DUM = @(0,0); * As above but for UniVerse
*
EQU TRUE TO 1,FALSE TO 0
*
CS = CHAR(27):CHAR(1)
CR = CHAR(13)
*
OPEN '','WIN.PROGS' TO F.BP ELSE STOP "WIN.PROGS IS MISSING"
* 
PRINT
PRINT "Installing Host Application Programming Interface programs..."
PRINT
*
* Compile & catalog programs
READV MACHINE.TYPE FROM F.BP,'MACHINE.TYPE',1 ELSE MACHINE.TYPE='' 
IF MACHINE.TYPE='' THEN MACHINE.TYPE='GENERIC'
* 
BEGIN CASE
CASE MACHINE.TYPE = 'PR' OR MACHINE.TYPE = 'PI'
SEL='SELECT WIN.PROGS WITH F3 LIKE "... ':MACHINE.TYPE:'..." AND WITH @ID UNLIKE "$..."'
SELECT F.BP
EOF=FALSE
CMND=''
CMND<-1>='PA'
CMND<-1>='SLEEP 5'
CMND<-1>='BASIC WIN.PROGS WIN.FIX.PI'
CMND<-1>='RUN WIN.PROGS WIN.FIX.PI'
CMND<-1>=SEL
CMND<-1>='BASIC WIN.PROGS'
CMND<-1>='SLEEP 5'
* 
LOOP 
READNEXT KEY ELSE EOF=TRUE
UNTIL EOF DO
IF KEY[1,1] # "$" THEN
READV DUM FROM F.BP,KEY,3 THEN
IF INDEX(DUM,MACHINE.TYPE,1) # 0 THEN
IF INDEX(DUM, MACHINE.TYPE:'.SUB', 1) = 0 THEN LCL = ' LOCAL' ELSE LCL = ''
CMND<-1>='CATALOG WIN.PROGS ':KEY:LCL
END 
END 
END 
REPEAT 
*
CMND<-1>="RUN WIN.PROGS WIN.ENDBOOT"
* 
OPEN '','VOC' TO F.VOC ELSE CMND=''
IF CMND # '' THEN
WRITE CMND ON F.VOC,"WIN.BOOT.PR"
END 
* 
CASE MACHINE.TYPE = 'UV' OR MACHINE.TYPE = 'UP'
SEL='SELECT WIN.PROGS WITH F3 LIKE "... ':MACHINE.TYPE:'..."'
CMND='PA'
CMND<-1>=SEL
CMND<-1> = 'BASIC WIN.PROGS'
CMND<-1>=SEL
CMND<-1>='CATALOG WIN.PROGS'
CMND<-1>='DATA N'
*
CMND<-1>="RUN WIN.PROGS WIN.ENDBOOT"
*
OPEN '','VOC' TO F.VOC ELSE CMND=''
IF CMND # '' THEN
WRITE CMND ON F.VOC,"WIN.BOOT.UV"
END 
* 
CASE MACHINE.TYPE = 'UD' OR MACHINE.TYPE = 'UC'
SEL='select WIN.PROGS WITH f3 LIKE "... ':MACHINE.TYPE:'..." USING DICT VOC'
CMND='PA'
IF MACHINE.TYPE = 'UD' THEN
CMND<-1>="ECLTYPE 'U'"
CMND<-1>="BASICTYPE 'U'"
END ELSE
CMND<-1>="ECLTYPE 'P'"
CMND<-1>="BASICTYPE 'P'"
END
CMND<-1>=SEL
CMND<-1>='BASIC WIN.PROGS'
CMND<-1>=SEL
CMND<-1>='CATALOG WIN.PROGS FORCE'
CMND<-1>='DATA Y'
CMND<-1>="RUN WIN.PROGS WIN.ENDBOOT"
*
OPEN '','VOC' TO F.VOC ELSE CMND = ''
IF CMND # "" THEN
WRITE CMND ON F.VOC, "WIN.BOOT.UD"
END
* 
CASE MACHINE.TYPE = "MD"
OPEN '','MD' TO F.MD THEN
READ DREC FROM F.MD, "*A3" ELSE
DREC = "A"
DREC<2> = 3
DREC<9> = "L"
DREC<10> = 10
WRITE DREC ON F.MD, "*A3"
END
END
*
END CASE
* 
XFER = 'Script "host\bootcat"'
*
PRINT CS:XFER:CR:
*
END
