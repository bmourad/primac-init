* WIN.HELP
* Invoke the wIntegrate host program editor
* Compile for: GENERIC AP MD ME PI.SUB PR.SUB SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-1999. Impact Business Systems
*
* Set up machine specific subroutines
OPEN '','WIN.PARAMS' TO F.BP ELSE
  OPEN '','WIN.PROGS' TO F.BP ELSE
    PRINT "UNABLE TO OPEN WIN.PARAMS OR WIN.PROGS"
    STOP
  END
END
*
READ MACHINE.TYPE FROM F.BP, "MACHINE.TYPE" ELSE MACHINE.TYPE = "GENERIC"
MACHINE.TYPE = MACHINE.TYPE<1>
*
SUB.SUFFIX = ''
IF MACHINE.TYPE # 'GENERIC' THEN SUB.SUFFIX = '.' : MACHINE.TYPE
ARGSUB = "WIN.ARGS" : SUB.SUFFIX
*
ARGS = ""
CALL @ARGSUB(ARGS)
POS = 2
GOSUB 100;* Get entry
ID = PHRASE
POS = POS + 1
GOSUB 100;* Get entry
FILENAME = PHRASE
*
IF ID MATCH "1N0N" THEN
   CALL WIN.HELPID(ID,FILENAME)
END ELSE
   CALL WIN.HELPNAME(ID,FILENAME)
END
*
STOP
*
* Get phrase from command line
100 WORD = ARGS<POS>
    QT = WORD[1,1]
    PHRASE = ""
    IF QT = '"' OR QT = "'" THEN
       WORD = WORD[2,999]
       LOOP
       WHILE WORD # "" AND WORD[LEN(WORD),1] # QT DO
          PHRASE = PHRASE:WORD:" "
          POS = POS + 1
          WORD = ARGS<POS>
       REPEAT
       PHRASE = PHRASE : WORD[1,LEN(WORD)-1]
    END ELSE
       PHRASE = WORD
    END
    RETURN
*
END
