* WIN.LUDEMO
* Lookup dialog demonstration
* Compile for: GENERIC AP MD ME PI PR SQ UD UL UP UV UC IN GA
* (C) Copyright IBM Corporation 2003. All rights reserved
* Copyright (c) 1991-96. Impact Business Systems
*
PROMPT ''
PRINT @(-1):"WIN.LUDEMO":
PRINT @(20,0):"Dialog box data lookup demonstration":
PRINT @(71,0):OCONV(DATE(),"D2"):
CALL WIN.BOX(0,1,79,1,1)
*
PRINT @(0,3):"This program demonstrates popping up a dialog box to prompt for a choice"
PRINT "of an item from a list."
PRINT
PRINT "The item can be chosen with the mouse or cursor keys "
PRINT "then clicking on OK or pressing <CR>."
PRINT "Alternatively you can double click with the mouse on your choice."
*
* Set up lookup details
R.TABLE = "Choose a fruit"    ;* Lookup title
R.TABLE<2,1> = "Code"; R.TABLE<3,1> = 5   ;* Column titles and sizes
R.TABLE<2,2> = "Fruit"; R.TABLE<3,2> = 12 
R.TABLE<4> = 5                            ;* Number of lines to show
R.TABLE<5,1> = 50; R.TABLE<5,2> = 8       ;* Start position of dialog
VER = ""
CALL WIN.VERSION(VER)
IF VER >= "2.5.00" THEN R.TABLE<8> = 1
*
* Set up data
R.LIST = ''
R.LIST<1,1> = 'AP'; R.LIST<1,2> = 'Apple'
R.LIST<2,1> = 'BN'; R.LIST<2,2> = 'Bananna'
R.LIST<3,1> = 'KW'; R.LIST<3,2> = 'Kiwi'
R.LIST<4,1> = 'OR'; R.LIST<4,2> = 'Orange'
R.LIST<5,1> = 'PR'; R.LIST<5,2> = 'Pear'
*                                               
REF = ''
CALL WIN.LOOKUP(R.TABLE, R.LIST, REF)
*              
PRINT @(0,10):
*
IF REF = '' THEN
   PRINT "You 'Cancelled' the dialog without choosing a fruit":
END ELSE PRINT "You chose ":REF
*
PRINT @(0,12):"The dialogbox was created by:-"
PRINT
PRINT "   CALL WIN.LOOKUP(R.TABLE, R.LIST, REF)"
PRINT
PRINT "R.TABLE holds the lookup headings, sizes and positions"
PRINT "R.LIST  is the list of data"
PRINT "REF     is the value returned to the program"
*
PRINT @(0,22):"Press <CR> to continue ":
INPUT DUM:
*
END
