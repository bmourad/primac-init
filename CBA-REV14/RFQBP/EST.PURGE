***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.PURGE
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/26/87
*
* DESCRIPTION
*
* This program is used to purge estimates from system based on criteria
* specified on manual estimate selection screen.
*
*T23278 markt 11/19/1998 * Add check for divisional security.
*T25237 cm 06/16/2000 * Add process to purge Roll Label information.
*T25719 lanny 03/26/2001 * If Div 'ALL' entered it will not find a match.
*T26934 cmykleb 01/21/2003 * Add delete of RFQ record also.
**************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.PURGE
*COPY>JES.CPYLIB>ESTIMATE.RES        ; * T25237
*COPY>JES.CPYLIB>ESTIMATE.RFQ        ; * T26934
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
***** T23278 v
*
*--- PROCREAD
*
   PROCREAD XX ELSE  ERRMSG="MUST BE RUN FROM PROC";GOSUB 91000;GOTO 99999
*T23278      DIV.CODE = XX<3>
***** T23278 ^
*
*--- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*
*---- OPEN ALL FIILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG="CANNOT OPEN COMPANY FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG="CANNOT OPEN CONTROL FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","PREFIX" TO PREFIX ELSE
      ERRMSG="CANNOT OPEN PREFIX FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE" TO ESTIMATE ELSE
      ERRMSG="CANNOT OPEN ESTIMATE FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE
      ERRMSG="CANNOT OPEN ESTIMATE.DEPT FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE.PURGE.BU" TO ESTIMATE.PURGE.BU ELSE
      ERRMSG="CANNOT OPEN ESTIMATE.PURGE.BU FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","CUST.EST.XREF" TO CUST.EST.XREF ELSE
      ERRMSG="CANNOT OPEN CUST.EST.XREF FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","PROSPECT.EST.XREF" TO PROSPECT.EST.XREF ELSE
      ERRMSG="CANNOT OPEN PROSPECT.EST.XREF FILE"
      GOSUB 91000
      STOP
   END
*T25237 v
   OPEN "","ESTIMATE.RES" TO ESTIMATE.RES ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.RES FILE"
      GOSUB 91000
      STOP
   END
   OPEN "","ESTIMATE.RL" TO ESTIMATE.RL ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.RL FILE"
      GOSUB 91000
      STOP
   END
*T25237 ^
*T26934 v
   OPEN "","ESTIMATE.RFQ" TO ESTIMATE.RFQ ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.RFQ FILE"
      GOSUB 91000
      STOP
   END
*T26934 ^
*
*---- INITIALIZATION
*
   TODAY = DATE()
   CONO=""
   MAT COMP.REC=""
   CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
   MATREADU ESTP.REC FROM CONTROL, CONO:"MANUAL.PURGE" ELSE
      GOTO 99999
   END
   PCNT = COUNT(ESTP.FROM.EST,VM) + (ESTP.FROM.EST # "")
*
*---- MAIN PROCESSING
*
   SELECT ESTIMATE
100*
   RELEASE
   READNEXT ID ELSE
      DELETE CONTROL, CONO:"MANUAL.PURGE"
      GOTO 99999
   END
   IF ID[1,3] # CONO THEN GOTO 100
   EST.KEY = ID[4,99]
   EST.NUM = FIELD(EST.KEY,"-",1)
   PURGE.FLAG = 0
   FOR P = 1 TO PCNT UNTIL PURGE.FLAG
      FROM.EST = ESTP.FROM.EST<1,P>
      FROM.NUM = FIELD(FROM.EST,"-",1)
      TO.EST = ESTP.TO.EST<1,P>
      TO.NUM = FIELD(TO.EST,"-",1)
      DIV.CODE = ESTP.DIV<1,P> ; *T23278
      BEGIN CASE
         CASE EST.KEY = FROM.EST
            PURGE.FLAG = 1
         CASE EST.NUM >= FROM.NUM AND EST.NUM <= TO.NUM
            PURGE.FLAG = 1
      END CASE
   NEXT P
   IF NOT(PURGE.FLAG) THEN GOTO 100
   MATREADU EST.REC FROM ESTIMATE, CONO:EST.KEY ELSE GOTO 100
   IF EST.MASTER # "" AND EST.MASTER # EST.KEY THEN GOTO 100
   IF EST.PURGE.TYPE # "M" THEN GOTO 100
   IF EST.DIV[1,2] # DIV.CODE THEN GOTO 100;* T23278 T25719
   STATUS = EST.STATUS<1,1>
   STATUS.DATE = EST.STAT.DATE<1,1>
   BEGIN CASE
      CASE STATUS = "BOOKED" AND EST.JOB.PURGE.DATE = ""
         NULL
      CASE 1
         GOSUB 2000
   END CASE
   GOTO 100
*
*---- PURGE ESTIMATE
*
2000*
   P_X  = 0 ; P_Y = 21 ; P_VALUE = "Processing Estimate - ":EST.KEY ; P_OPT = "CL"
   CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   DEL.KEY = EST.KEY
   DEL.DEPT.COMP = EST.DEPT.COMP
   DEL.SUBS = EST.SUBS
   IF DEL.SUBS # "" THEN
      SAVE.KEY = DEL.KEY
      SAVE.DEPT.COMP = DEL.DEPT.COMP
      SC = COUNT(DEL.SUBS,VM) + (DEL.SUBS # "")
      FOR SP = 1 TO SC
         DEL.KEY = DEL.SUBS<1,SP>
         MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE GOTO 2090
         GOSUB 3000
2090  NEXT SP
      DEL.KEY = SAVE.KEY
      DEL.DEPT.COMP = SAVE.DEPT.COMP
      MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE
         MAT EST.REC = ""
      END
   END
   GOSUB 3000
   RETURN
*
*---- DELETE ESTIMATE RECORDS
*
3000*
   DC = COUNT(DEL.DEPT.COMP,VM) + (DEL.DEPT.COMP # "")
   FOR DP = 1 TO DC
      DDKEY = DEL.KEY:"!":DEL.DEPT.COMP<1,DP>
      READU REC FROM ESTIMATE.DEPT, CONO:DDKEY ELSE
         RELEASE ESTIMATE.DEPT, CONO:DDKEY
         GOTO 3090
      END
      WRITE REC ON ESTIMATE.PURGE.BU, CONO:DDKEY
      DELETE ESTIMATE.DEPT, CONO:DDKEY
3090 NEXT DP
   MATWRITE EST.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY
   BEGIN CASE
      CASE EST.CUST = "P"
         CALL GEN.XREF.MAINT(CONO,DEL.KEY,EST.CUST.NAME,"",PROSPECT.EST.XREF,PREFIX)
      CASE 1
         CALL GEN.XREF.MAINT(CONO,DEL.KEY,EST.CUST,"",CUST.EST.XREF,PREFIX)
   END CASE
   DELETE ESTIMATE, CONO:DEL.KEY
*T25237 v
   MATREADU EST.RL.REC FROM ESTIMATE.RES, CONO:DEL.KEY THEN
      MATWRITE EST.RL.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY:'!RES'
      DELETE ESTIMATE.RES, CONO:DEL.KEY
   END
   READU OLD.REC FROM ESTIMATE.RL, CONO:DEL.KEY THEN
      WRITE OLD.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY:'!RL'
      DELETE ESTIMATE.RL, CONO:DEL.KEY
   END
*T25237 ^
*T26934 v
   MATREADU RFQ.REC FROM ESTIMATE.RFQ, CONO:DEL.KEY THEN
      MATWRITE RFQ.REC ON ESTIMATE.RFQ, CONO:DEL.KEY:'!RFQ'
      DELETE ESTIMATE.RFQ, CONO:DEL.KEY
   END
*T26934 ^
   RETURN
*
*---- ERROR ROUTINE
*
91000 *
   ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
*
99999*
   STOP
END
