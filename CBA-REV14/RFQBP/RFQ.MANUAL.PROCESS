*COPY>CPYLIB>COM1
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RFQBP
* PROGRAM     - RFQ.MANUAL.PROCESS
* BY          - CHRIS MYKLEBUST
* DATE        - 11/20/2002
* DESCRIPTION -
*
*ENDDOC
**********************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>JES.CPYLIB>ESTIMATE.RFQ
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>JOB
*
*---- Setup for system error messages
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- File opens
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOTO 93000
   END
   OPEN 'ESTIMATE.RFQ' TO ESTIMATE.RFQ ELSE
      ERRMSG = 'ESTIMATE.RFQ file is missing'
      GOTO 93000
   END
   OPEN 'RFQ.SCREENS' TO M.SCREENS ELSE
      ERRMSG = 'RFQ.SCREENS file is missing'
      GOTO 93000
   END
   OPEN 'ESTIMATE.MATL' TO ESTIMATE.MATL ELSE
      ERRMSG = 'ESTIMATE.MATL file is missing'
      GOTO 93000
   END
   OPEN 'ESTIMATE' TO ESTIMATE ELSE
      ERRMSG = 'ESTIMATE file is missing'
      GOTO 93000
   END
   OPEN 'ESTIMATE.DEPT' TO ESTIMATE.DEPT ELSE
      ERRMSG = 'ESTIMATE.DEPT file is missing'
      GOTO 93000
   END
   OPEN 'COST.CNTR' TO COST.CNTR ELSE
      ERRMSG = 'COST.CNTR file is missing'
      GOTO 93000
   END
   OPEN 'JOB' TO JOB ELSE
      ERRMSG = 'JOB file is missing'
      GOTO 93000
   END
*T25282 ^
*
*---- Initialize
*
   CONO = ""
   CALL GET.CONO1(CONO, MAT COMP.REC, COMPANY, CONTROL)
   BEGIN.PAGE = 2
   LINE.SPACE = 1
   LINES = 0
   LN = 0
   OLD.START.LINE = 0
   PAGE.SIZE = 18
   RFQ.IDS = ''
   START.LINE = 0
*
*---- Main Processing
*
   GOSUB 1000                          ;* Enter Jobs
IF @LOGNAME = 'cmykleb' THEN DEBUG
   RFQ.CNT = DCOUNT(RFQ.IDS,VM)
   FOR RFQ.POS = 1 TO RFQ.CNT
      RFQ.ID = RFQ.IDS<1,RFQ.POS>
      MATREAD RFQ.REC FROM ESTIMATE.RFQ,CONO:RFQ.ID THEN
         CALL RFQ.TO.ESTIMATE(CONO,RFQ.ID,MAT RFQ.REC)
      END
   NEXT RFQ.POS
   GOTO 99999
*
1000*
*
*---- Data Entry Subroutine
*
   MAT EDIT.COM.DRIVER = ""
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'RFQ.MANUAL.PROCESS'
   ECD.ACTION = 1
   ECD.SCRN.NO = 1
   CALL SCRN.EDIT
   MAT SCV.REC = ''
   ECD.ACTION = 6
   CALL SCRN.EDIT
*
*---- Enter RFQ Id's
*
   LOOP
      LN=LINES + 1
      OLD.LINES=LINES
      GOSUB 1100
   WHILE LINES > OLD.LINES DO REPEAT
   LN=LINES
   LINES=DCOUNT(RFQ.IDS,VM)
   GOSUB 1200
   LOOP
      SCV.REC(4)<1>=''
      ECD.NUM=4
      ECD.ACTION=4
      CALL SCRN.EDIT
      REQUEST=ECD.RET.VALUE
      BEGIN CASE
         CASE REQUEST='E' OR REQUEST = 'END'
            GOTO 99999
         CASE REQUEST = 'F'
            EXIT
         CASE REQUEST='A'
            LINES=DCOUNT(RFQ.IDS,VM)
            LOOP
               LN=LINES + 1
               OLD.LINES=LINES
               GOSUB 1100
            WHILE LINES > OLD.LINES DO REPEAT
            LN=LINES
            GOSUB 1200
         CASE REQUEST='S'
            LN=LN + PAGE.SIZE
            IF LN > LINES THEN LN=1
            GOSUB 1200
         CASE REQUEST='C'
            GOSUB 1300
            IF LNO THEN
               LN=LNO
               SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
               GOSUB 1100
            END
            GOSUB 1200
         CASE REQUEST='D'
            GOSUB 1300
            IF LNO THEN
               LN=LNO
               RFQ.IDS=DELETE(RFQ.IDS,1,LN,0)
               LINES=DCOUNT(RFQ.IDS,VM)
               IF LN > 1 AND LN > LINES THEN LN=LN - 1
               IF LN < 1 THEN LN=1
               OLD.START.LINE=0
            END
            GOSUB 1200
         CASE 1
            ERRMSG="INVALID OPTION"
            GOSUB 91000
      END CASE
   REPEAT
   RETURN
1100*
   GOSUB 1200
   SLN=BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
   P_X  = 26 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
   P_X  := AM:26 ; P_Y := AM:SLN ; P_VALUE := AM:LN "R#3"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   LOOP
      X=30;Y=SLN;TYP=1;MAXL=8;O.R='O';DEFAULT=RFQ.IDS<1,LN>;HMSG="Enter Job Number"
      CALL EDIT.SUB
      IF VALUE = "END" OR VALUE = "" THEN RETURN
      MATREAD RFQ.REC FROM ESTIMATE.RFQ,CONO:VALUE ELSE
         ERRMSG = 'Invalid RFQ Number'
         GOSUB 91000
         CONTINUE
      END
   REPEAT
   RFQ.IDS<1,LN>=VALUE
   LINES=DCOUNT(RFQ.IDS,VM)
   RETURN
*
*---- SCROLL (KEYS) ROUTINE
*
1200*
   START.LINE=1 + INT((LN-1) / PAGE.SIZE) * PAGE.SIZE
   IF START.LINE=OLD.START.LINE THEN GOTO 1350
   OLD.START.LINE=START.LINE
   LAST.LINE=START.LINE + PAGE.SIZE - 1
   CNT=1
   FOR L=START.LINE TO LAST.LINE UNTIL L > LINES
      SLN=BEGIN.PAGE + LINE.SPACE * MOD(L-1,PAGE.SIZE)
      P_X  = 26 ; P_Y = SLN ; P_VALUE = L "R#3" ; P_OPT = ""
      P_X  := AM:30 ; P_Y := AM:SLN ; P_VALUE := AM:RFQ.IDS<1,L>"L#8"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT=CNT + 1
   NEXT L
   FOR M=CNT TO PAGE.SIZE
      SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 26 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
1350*
   RETURN
*
*---- GET LINE NUMBER
*
1300*
   GOSUB 1200
   ECD.NUM=3
   IF LAST.LINE < LINES THEN
      ECD.MAXV=LAST.LINE
   END ELSE
      ECD.MAXV=LINES
   END
   SCV.REC(3)<1>=''
   ECD.ACTION=4;CALL SCRN.EDIT
   P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   LNO=ECD.RET.VALUE
   IF LNO='' OR LNO="END" THEN LNO=0
   RETURN
*
*---- Error Handling Routines
*
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
   ECD.ACTION = 99; CALL SCRN.EDIT
END
