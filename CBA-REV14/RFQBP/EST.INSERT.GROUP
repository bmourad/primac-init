   SUBROUTINE EST.INSERT.GROUP (CONO, EQTY, DEPT, COMP, REF.NO, MAT ESTG.REC)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.INSERT.GROUP
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/04/86
*
* DESCRIPTION
*
* This subroutine updates the ESTIMATE.DEPT data with data from the
* specified group.
*
*T26564 lross 04/24/2002 * For OSP items get DESC from OPER GROUP DESC
*                          vs CATEGORY.OSP.
*T26934 cmykleb 01/21/2003 * Add RFQ logic to program.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>SHIP.VIA
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>JES.CPYLIB>ESTIMATE.GRP
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.TEMP2
*COPY>CPYLIB>FILE.VARS
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
*
*---- MAIN PROCESSING
*
100*
   GCNT = COUNT(ESTG.TYPE,VM) + (ESTG.TYPE # "")
   GRP.ID = TEMP.GRP.ID
   GRP.QTY = TEMP.QTY
   GRP.FCTR = TEMP.FCTR
   GPTR = 0
   LOOP
      GPTR = GPTR + 1
   UNTIL GPTR > GCNT DO
      MAT ESTD.TEMP = ""
      TEMP.GRP.ID = GRP.ID
      TEMP.GRP.QTY = GRP.QTY
      TEMP.GRP.FCTR = GRP.FCTR
      TEMP.TYPE = ESTG.TYPE<1,GPTR>
      TEMP.CCTR = ESTG.CCTR<1,GPTR>
*T26934 v
      TEMP.RFQ.YN = ESTG.RFQ.YN<1,GPTR>
      TEMP.RFQ.PID = ESTG.RFQ.PID<1,GPTR>
      IF TEMP.RFQ.YN THEN
         SUB.NAME = "EST.FORMULA.R":TEMP.RFQ.PID
         CALL @SUB.NAME(CONO,"",EQTY,DEPT,COMP)
         IF TEMP.QTY = "" THEN GOTO 390
      END
*T26934 ^
      BEGIN CASE
         CASE TEMP.CCTR = "SAD" OR TEMP.CCTR = "STT"
            TEMP.CCTR = EST.STITCHER.CCTR
            TEMP.GRP.CCTR = "STT"
         CASE TEMP.CCTR = "BND"
            TEMP.CCTR = EST.BINDER.CCTR
            TEMP.GRP.CCTR = "BND"
         CASE TEMP.CCTR = "GTR"
            TEMP.CCTR = EST.GATHERER.CCTR
            TEMP.GRP.CCTR = "GTR"
         CASE TEMP.CCTR = "TRM"
            TEMP.CCTR = EST.TRIMMER.CCTR
            TEMP.GRP.CCTR = "TRM"
         CASE TEMP.CCTR = "CSM"
            TEMP.CCTR = EST.CASEMAKER.CCTR
            TEMP.GRP.CCTR = "CSM"
         CASE TEMP.CCTR = "CSS"
            TEMP.CCTR = EST.CASESTAMP.CCTR
            TEMP.GRP.CCTR = "CSS"
         CASE TEMP.CCTR = "CSI"
            TEMP.CCTR = EST.CASEIN.CCTR
            TEMP.GRP.CCTR = "CSI"
         CASE TEMP.CCTR = "PRS"
            TEMP.CCTR = EST.PROD.PRESS.ID<1,COMP>
            TEMP.GRP.CCTR = "PRS"
      END CASE
      BEGIN CASE
         CASE TEMP.CCTR = "CUT"
         CASE TEMP.CCTR = "FLD"
         CASE 1
            MATREAD CCTR.REC FROM COST.CNTR, CONO:TEMP.CCTR ELSE GOTO 390
            TEMP.CCTR.TYPE = CCTR.TYPE
      END CASE
      BEGIN CASE
         CASE TEMP.TYPE[1,1] = "M"
            TEMP.GRP.QCALC = ESTG.OPER.QCALC<1,GPTR>
            TEMP.GRP.QPARAM = ESTG.OPER.QPARAM<1,GPTR>
            TEMP.GRP.QOR = ESTG.OPER.QOR<1,GPTR>
            TEMP.GRP.FCALC = ESTG.OPER.FCALC<1,GPTR>
            TEMP.GRP.FPARAM = ESTG.OPER.FPARAM<1,GPTR>
            TEMP.GRP.FOR = ESTG.OPER.FOR<1,GPTR>
            BEGIN CASE
               CASE TEMP.TYPE = "MS"
                  CALL EST.INSERT.STK (CONO, EQTY, DEPT, COMP, REF.NO)
                  GOTO 390
               CASE TEMP.TYPE = "MI"
                  CALL EST.INSERT.INK (CONO, EQTY, DEPT, COMP, REF.NO)
                  GOTO 390
               CASE TEMP.TYPE = "MC"
                  CALL EST.INSERT.CASE (CONO, EQTY, DEPT, COMP, REF.NO)
                  GOTO 390
               CASE 1
                  TEMP.OPV = ESTG.OPER<1,GPTR>
                  MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:TEMP.TYPE[2,1] ELSE GOTO 390
                  LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MT.PTR ELSE GOTO 390
            END CASE
            TEMP.STD = ESTM.COST<1,MT.PTR> / 100
            TEMP.STD.TYPE = "$/":ESTM.UM<1,MT.PTR>
            TEMP.UM = ESTM.COST.UNIT<1,MT.PTR>
            TEMP.IND.1 = ESTM.FOH.PCT<1,MT.PTR>
            TEMP.MARKUP = ESTM.MARKUP<1,MT.PTR>
            TEMP.DESC = ESTM.FULL.DESC<1,MT.PTR>
            IF TEMP.GRP.QCALC = "" THEN
               TEMP.GRP.QCALC = ESTM.QCALC<1,MT.PTR>
               TEMP.GRP.QPARAM = ESTM.QPARAM<1,MT.PTR>
               TEMP.GRP.QOR = ESTM.QOR<1,MT.PTR>
            END
            IF TEMP.GRP.FCALC = "" THEN
               TEMP.GRP.FCALC = ESTM.FCALC<1,MT.PTR>
               TEMP.GRP.FPARAM = ESTM.FPARAM<1,MT.PTR>
               TEMP.GRP.FOR = ESTM.FOR<1,MT.PTR>
            END
         CASE TEMP.TYPE[1,1] = "P"
            TEMP.OPV = ESTG.OPER<1,GPTR>
            MATREAD VEND.REC FROM VEND,CONO:TEMP.OPV ELSE GOTO 390
            TEMP.FCTR = 1000
            BEGIN CASE
               CASE TEMP.TYPE = "PM"
                  TEMP.STD.TYPE = "$/M"
                  TEMP.UM = 1000
               CASE TEMP.TYPE = "PC"
                  TEMP.STD.TYPE = "$/C"
                  TEMP.UM = 100
               CASE TEMP.TYPE = "PT"
                  TEMP.STD.TYPE = "$/TTL"
                  TEMP.UM = 1
               CASE 1
                  TEMP.STD.TYPE = "$/EA"
                  TEMP.UM = 1
            END CASE
            TEMP.DESC = VEND.DESC
            TEMP.CODE = ESTG.CAT.SP.CODE<1,GPTR>
            MATREAD CAOS.REC FROM CATEGORY.OSP,CONO:TEMP.CODE ELSE GOTO 390
            TEMP.IND.1 = CAOS.FOH.PCT
            TEMP.MARKUP = CAOS.MARKUP
*T26564     TEMP.DESC = CAOS.DESC
            TEMP.DESC = ESTG.OPER.DESC<1,GPTR>
            TEMP.STD = ESTG.CAT.SP.COST<1,GPTR> / 100
            TEMP.GRP.QCALC = ESTG.OPER.QCALC<1,GPTR>
            TEMP.GRP.QPARAM = ESTG.OPER.QPARAM<1,GPTR>
            TEMP.GRP.QOR = ESTG.OPER.QOR<1,GPTR>
            TEMP.GRP.FCALC = ESTG.OPER.FCALC<1,GPTR>
            TEMP.GRP.FPARAM = ESTG.OPER.FPARAM<1,GPTR>
            TEMP.GRP.FOR = ESTG.OPER.FOR<1,GPTR>
         CASE TEMP.TYPE = "S"
            TEMP.OPV = ESTG.OPER<1,GPTR>
            LOCATE TEMP.OPV IN CCTR.OPER<1>,1 SETTING OP.PTR ELSE GOTO 390
            TEMP.OPV.TYPE = CCTR.OPER.TYPE<1,OP.PTR>
            IF CCTR.OPER.STD.IMP<1,OP.PTR>+0=0 THEN TEMP.OPV.TYPE="F"
            TEMP.HRS = ""
            TEMP.STD = ESTG.CAT.SP.COST<1,GPTR> / 100
            TEMP.STD.TYPE = '$/LB'
            TEMP.IND.1 = CCTR.FOH.TYPE<1,1>:CCTR.FOH.PCT<1,1>
            TEMP.IND.2 = CCTR.FOH.TYPE<1,2>:CCTR.FOH.PCT<1,2>
            TEMP.IND.3 = CCTR.FOH.TYPE<1,3>:CCTR.FOH.PCT<1,3>
            TEMP.IND.4 = CCTR.FOH.TYPE<1,4>:CCTR.FOH.PCT<1,4>
            TEMP.CODE = ESTG.CAT.SP.CODE<1,GPTR>
            MATREAD SHIP.VIA.REC FROM SHIP.VIA,CONO:TEMP.CODE ELSE GOTO 390
            TEMP.DESC = SHPV.DESC
            TEMP.MARKUP = CCTR.OPER.MARKUP<1,OP.PTR>
            TEMP.DESC = ESTG.OPER.DESC<1,GPTR>
            TEMP.GRP.QCALC = ESTG.OPER.QCALC<1,GPTR>
            TEMP.GRP.QPARAM = ESTG.OPER.QPARAM<1,GPTR>
            TEMP.GRP.QOR = ESTG.OPER.QOR<1,GPTR>
            TEMP.GRP.FCALC = ESTG.OPER.FCALC<1,GPTR>
            TEMP.GRP.FPARAM = ESTG.OPER.FPARAM<1,GPTR>
            TEMP.GRP.FOR = ESTG.OPER.FOR<1,GPTR>
         CASE TEMP.TYPE = "L"
            BEGIN CASE
               CASE TEMP.CCTR = "CUT"
                  MAT ESTD.TEMP2 = ""
                  TDP = 0
                  GPTR = GPTR - 1
                  LOOP
                  WHILE ESTG.CCTR<1,GPTR+1> = "CUT" DO
                     TDP = TDP + 1
                     GPTR = GPTR + 1
                     TEMP2.GRP.ID<1,TDP> = GRP.ID
                     TEMP2.GRP.QTY<1,TDP> = GRP.QTY
                     TEMP2.GRP.FCTR<1,TDP> = GRP.FCTR
                     TEMP2.TYPE<1,TDP> = ESTG.TYPE<1,GPTR>
                     TEMP2.CCTR<1,TDP> = ESTG.CCTR<1,GPTR>
                     TEMP2.OPV<1,TDP> = ESTG.OPER<1,GPTR>
                     TEMP2.GRP.QCALC<1,TDP> = ESTG.OPER.QCALC<1,GPTR>
                     TEMP2.GRP.QPARAM<1,TDP> = ESTG.OPER.QPARAM<1,GPTR>
                     TEMP2.GRP.QOR<1,TDP> = ESTG.OPER.QOR<1,GPTR>
                     TEMP2.GRP.FCALC<1,TDP> = ESTG.OPER.FCALC<1,GPTR>
                     TEMP2.GRP.FPARAM<1,TDP> = ESTG.OPER.FPARAM<1,GPTR>
                     TEMP2.GRP.FOR<1,TDP> = ESTG.OPER.FOR<1,GPTR>
                  REPEAT
                  CALL EST.INSERT.CUT (CONO, EQTY, DEPT, COMP, REF.NO, MAT ESTD.TEMP2)
                  GOTO 390
               CASE TEMP.CCTR = "FLD"
                  MAT ESTD.TEMP2 = ""
                  TDP = 0
                  GPTR = GPTR - 1
                  LOOP
                  WHILE ESTG.CCTR<1,GPTR+1> = "FLD" DO
                     TDP = TDP + 1
                     GPTR = GPTR + 1
                     TEMP2.GRP.ID<1,TDP> = GRP.ID
                     TEMP2.GRP.QTY<1,TDP> = GRP.QTY
                     TEMP2.GRP.FCTR<1,TDP> = GRP.FCTR
                     TEMP2.TYPE<1,TDP> = ESTG.TYPE<1,GPTR>
                     TEMP2.CCTR<1,TDP> = ESTG.CCTR<1,GPTR>
                     TEMP2.OPV<1,TDP> = ESTG.OPER<1,GPTR>
                     TEMP2.GRP.QCALC<1,TDP> = ESTG.OPER.QCALC<1,GPTR>
                     TEMP2.GRP.QPARAM<1,TDP> = ESTG.OPER.QPARAM<1,GPTR>
                     TEMP2.GRP.QOR<1,TDP> = ESTG.OPER.QOR<1,GPTR>
                     TEMP2.GRP.FCALC<1,TDP> = ESTG.OPER.FCALC<1,GPTR>
                     TEMP2.GRP.FPARAM<1,TDP> = ESTG.OPER.FPARAM<1,GPTR>
                     TEMP2.GRP.FOR<1,TDP> = ESTG.OPER.FOR<1,GPTR>
                  REPEAT
                  CALL EST.INSERT.FLD (CONO, EQTY, DEPT, COMP, REF.NO, MAT ESTD.TEMP2)
                  GOTO 390
               CASE 1
                  TEMP.OPV = ESTG.OPER<1,GPTR>
                  LOCATE TEMP.OPV IN CCTR.OPER<1>,1 SETTING OP.PTR ELSE GOTO 390
                  TEMP.OPV.TYPE = CCTR.OPER.TYPE<1,OP.PTR>
                  IF CCTR.OPER.STD.IMP<1,OP.PTR>+0=0 THEN TEMP.OPV.TYPE="F"
                  IF TEMP.OPV.TYPE = "F" THEN
                     TEMP.STD = CCTR.OPER.STD.HRS<1,OP.PTR> / 100
                     TEMP.STD.TYPE = "HR/OP"
                  END ELSE
                     TEMP.STD = CCTR.OPER.STD.IMP<1,OP.PTR>
                     TEMP.STD.TYPE = "OP/HR"
                  END
                  TEMP.HRS = ""
                  TEMP.RATE = CCTR.OPER.HR.RATE<1,OP.PTR>
                  TEMP.IND.1 = CCTR.FOH.TYPE<1,1>:CCTR.FOH.PCT<1,1>
                  TEMP.IND.2 = CCTR.FOH.TYPE<1,2>:CCTR.FOH.PCT<1,2>
                  TEMP.IND.3 = CCTR.FOH.TYPE<1,3>:CCTR.FOH.PCT<1,3>
                  TEMP.IND.4 = CCTR.FOH.TYPE<1,4>:CCTR.FOH.PCT<1,4>
                  TEMP.MARKUP = CCTR.OPER.MARKUP<1,OP.PTR>
                  TEMP.DESC = ESTG.OPER.DESC<1,GPTR>
                  TEMP.GRP.QCALC = ESTG.OPER.QCALC<1,GPTR>
                  TEMP.GRP.QPARAM = ESTG.OPER.QPARAM<1,GPTR>
                  TEMP.GRP.QOR = ESTG.OPER.QOR<1,GPTR>
                  TEMP.GRP.FCALC = ESTG.OPER.FCALC<1,GPTR>
                  TEMP.GRP.FPARAM = ESTG.OPER.FPARAM<1,GPTR>
                  TEMP.GRP.FOR = ESTG.OPER.FOR<1,GPTR>
            END CASE
         CASE TEMP.TYPE = "O"
            TEMP.OPV = ESTG.OPER<1,GPTR>
            LOCATE TEMP.OPV IN CCTR.OPER<1>,1 SETTING OP.PTR ELSE GOTO 390
            TEMP.OPV.TYPE = CCTR.OPER.TYPE<1,OP.PTR>
            IF CCTR.OPER.STD.IMP<1,OP.PTR>+0=0 THEN TEMP.OPV.TYPE="F"
            TEMP.STD=CCTR.OPER.HR.RATE<1,OP.PTR>/100
            TEMP.STD.TYPE="$/EA"
            TEMP.HRS = ""
            TEMP.RATE = CCTR.OPER.HR.RATE<1,OP.PTR>
            TEMP.IND.1 = CCTR.FOH.TYPE<1,1>:CCTR.FOH.PCT<1,1>
            TEMP.IND.2 = CCTR.FOH.TYPE<1,2>:CCTR.FOH.PCT<1,2>
            TEMP.IND.3 = CCTR.FOH.TYPE<1,3>:CCTR.FOH.PCT<1,3>
            TEMP.IND.4 = CCTR.FOH.TYPE<1,4>:CCTR.FOH.PCT<1,4>
            TEMP.MARKUP = CCTR.OPER.MARKUP<1,OP.PTR>
            TEMP.DESC = ESTG.OPER.DESC<1,GPTR>
            TEMP.GRP.QCALC = ESTG.OPER.QCALC<1,GPTR>
            TEMP.GRP.QPARAM = ESTG.OPER.QPARAM<1,GPTR>
            TEMP.GRP.QOR = ESTG.OPER.QOR<1,GPTR>
            TEMP.GRP.FCALC = ESTG.OPER.FCALC<1,GPTR>
            TEMP.GRP.FPARAM = ESTG.OPER.FPARAM<1,GPTR>
            TEMP.GRP.FOR = ESTG.OPER.FOR<1,GPTR>
      END CASE
      BEGIN CASE
         CASE TEMP.GRP.QCALC = ""
            TEMP.QTY = ""
         CASE TEMP.GRP.QCALC = "C"
            TEMP.QTY = TEMP.GRP.QPARAM
         CASE TEMP.GRP.QCALC = "M"
            TEMP.QTY = GRP.QTY * TEMP.GRP.QPARAM
         CASE TEMP.GRP.QCALC = "F"
            TEMP.FPTR = REF.NO+1
            READV T.OPV.DESC FROM OPERATION, CONO:TEMP.OPV,1 ELSE T.OPV.DESC=''
            IF TEMP.GRP.QPARAM = '030' THEN
               P_X  = 20 ; P_Y = 22 ; P_VALUE = "Operation ":T.OPV.DESC'L#30' ; P_OPT = ""
               CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            END
            SUB.NAME = "EST.FORMULA.Q":TEMP.GRP.QPARAM
            CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
            P_X  = 20 ; P_Y = 22 ; P_VALUE = SPACE(43) ; P_OPT = ""
            CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            IF TEMP.QTY = "" THEN
               TEMP.GRP.QOR = ""
            END
      END CASE
      BEGIN CASE
         CASE TEMP.GRP.FCALC = ""
            TEMP.FCTR = 1000
         CASE TEMP.GRP.FCALC = "C"
            TEMP.FCTR = INT(TEMP.GRP.FPARAM*1000+0.5)
         CASE TEMP.GRP.FCALC = "M"
            TEMP.FCTR = INT(GRP.FCTR*TEMP.GRP.FPARAM+0.5)
         CASE TEMP.GRP.FCALC = "F"
            TEMP.FPTR = REF.NO+1
            SUB.NAME = "EST.FORMULA.F":TEMP.GRP.FPARAM
            CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
            IF TEMP.QTY = "" THEN TEMP.GRP.FOR = ""
         CASE TEMP.GRP.FCALC = "T"
            TBL.ID = TEMP.GRP.FPARAM
            CALL EST.TABLE.FCTR (CONO, TBL.ID, GRP.FCTR)
            IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
      END CASE
      CALL EST.CALC.COST (CONO)
      REF.NO = REF.NO + 1
      FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
         ESTD.REC(A) = INSERT(ESTD.REC(A),1,REF.NO,0,ESTD.TEMP(A))
      NEXT A
      ESTD.QTY<1,REF.NO> = TEMP.QTY * 100
      ESTD.STD<1,REF.NO> = TEMP.STD * 100
      ESTD.GRP.QTY<1,REF.NO> = TEMP.GRP.QTY * 100
390 REPEAT
   RETURN
END
