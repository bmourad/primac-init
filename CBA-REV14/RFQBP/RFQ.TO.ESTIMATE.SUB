   SUBROUTINE RFQ.TO.ESTIMATE.SUB(CONO,MAT RFQ.REC,EST.KEY,B,COMP,ERRMSG)
**********************************************
* REVISION    - [11.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - COURIERBP
* PROGRAM     - RFQ.TO.ESTIMATE.SUB
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DATE        - 10/25/2002
*
* DESCRIPTION - This program creates the ESTIMATE.DEPT records from the
*               RFQ information.
*
*T26934 cmykleb 10/25/2002 * Create Est Dept record from information
*                            supplied by the RFQ information.
**********************************************
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>VEND
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.GRP
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>JES.CPYLIB>ESTIMATE.RFQ
*
*--- Open Files
*
   CALL EST.MAINT.OPEN
   OPEN 'ESTIMATE.DEPT' TO ESTIMATE.DEPT.BU ELSE
      ERRMSG = 'ESTIMATE.DEPT FILE IS MISSING'
      GOTO 93000
   END
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
   GRP.SEQ = 0
*
*--- Create the Press Department record.
*
   MAT ESTD.TEMP = ''
   MAT ESTD.REC = ''
   EQTY = EST.QTY
   IF EST.PROD.BIND.ONLY<1,COMP> = 'Y' THEN GOTO BIND.DEPT
   REF.NO = 1
   DCNT = 1
   DEPT = '10'
*  RFQ.PAGE.WIDTH = ICONV(RFQ.PAGE.WIDTH,'MD2')
*  RFQ.SPINE.LENGTH = ICONV(RFQ.SPINE.LENGTH,'MD2')
*  RFQ.PAGE.WIDTH = RFQ.PAGE.WIDTH'L%4'
*  RFQ.SPINE.LENGTH = RFQ.SPINE.LENGTH'L%4'
   BEGIN CASE
      CASE RFQ.PAGE.WIDTH = 2750
         TYPE = "VP"
      CASE RFQ.PAGE.WIDTH GT 3000 AND RFQ.PAGE.WIDTH LE 5500
         TYPE = "58"
      CASE RFQ.PAGE.WIDTH GT 5500 AND RFQ.PAGE.WIDTH LE 7000
         TYPE = "10"
      CASE 1
         TYPE = "10"
   END CASE
   BEGIN CASE
      CASE TYPE = "VP"
         GRP = "VPSIGS"
      CASE TYPE = "58" AND RFQ.COLOR.CNT = '1CT' AND RFQ.GRAIN.DIR = "RG"
         GRP = "47-1C"
      CASE TYPE = "58" AND RFQ.COLOR.CNT = '1CT' AND RFQ.GRAIN.DIR = "CR"
         GRP = "53-1C"
      CASE TYPE = "58" AND RFQ.COLOR.CNT[1,1] = '2' AND RFQ.GRAIN.DIR = "RG"
         GRP = "47A2C"
      CASE TYPE = "58" AND RFQ.COLOR.CNT[1,1] = '2' AND RFQ.GRAIN.DIR = "CR"
         GRP = "53A2C"
      CASE TYPE = "10" AND RFQ.COLOR.CNT = '1CT'
         GRP = "53-1C"
      CASE TYPE = "10" AND RFQ.COLOR.CNT[1,2] = '2'
         GRP = "53A2C"
      CASE 1
         GRP = ""
   END CASE
   GRP.SEQ = 1
   GOSUB 1000 ; * GET GROUP/DEPT INFO
   CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,1,REF.NO,MAT ESTG.REC)
   ESTD.KEY = CONO:EST.KEY:'!':DCNT:'!':COMP:'!':EST.QTY
   MATWRITE ESTD.REC ON ESTIMATE.DEPT, ESTD.KEY
   MATWRITE ESTD.REC ON ESTIMATE.DEPT.BU, ESTD.KEY
   EST.DEPT<1,DCNT> = DEPT
   EST.DEPT.TYPE<1,DCNT> = DEPT.TYPE
   EST.DEPT.DESC<1,DCNT> = DEPT.DESC
   EST.COMP.FLAG<1,DCNT,COMP> = 'Y'
   EST.DEPT.COMP<1,-1> = DCNT:'!':COMP:'!':EST.QTY
   RETURN
*
*--- Create the Binding department and component records.
*
BIND.DEPT: 
   IF RFQ.PQTY + 0 = 0 THEN
      DCNT = 1
   END ELSE
      DCNT = 2
   END
   MAT ESTD.TEMP = ''
   MAT ESTD.REC = ''
   REF.NO = 1
   DEPT = "30"
   BEGIN CASE
      CASE RFQ.BIND.STYLE = "AD"
         BEGIN CASE
            CASE RFQ.PAGE.WIDTH GT 5500
               GRP = "ADH11"
            CASE 1
               GRP = "ADH21"
         END CASE
      CASE 1
         BEGIN CASE
            CASE RFQ.PAGE.WIDTH GT 5500
               GRP = "GATH11"
            CASE 1
               GRP = "GATH21"
         END CASE
   END CASE
   IF GRP # "" AND DEPT # "" THEN
      GRP.SEQ = GRP.SEQ + 1
      GOSUB 1000 ; * GET GROUP/DEPT INFO
*     CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,B,REF.NO,MAT ESTG.REC)
      CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,COMP,REF.NO,MAT ESTG.REC)
      IF RFQ.BIND.PROD<1,B> # "" THEN
         REF.NO = DCOUNT(ESTD.TYPE<1>,VM) + 1
         GRP = RFQ.BIND.PROD<1,B>
         GOSUB 1000
*        CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,B,REF.NO,MAT ESTG.REC)
         CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,COMP,REF.NO,MAT ESTG.REC)
      END
      *** CHECK INTERIOR EDGES ***
      IF RFQ.BIND.EDGES<1,B> # "" THEN
         BEGIN CASE
            CASE RFQ.BIND.EDGES<1,B> = "GI"
               GRP = "OP0002"
            CASE RFQ.BIND.EDGES<1,B> = "SG"
               GRP = "OP0001"
            CASE RFQ.BIND.EDGES<1,B> = "ST"
               GRP = "OP0001"
            CASE 1
               GRP = ""
         END CASE
         IF GRP # "" THEN
            REF.NO = DCOUNT(ESTD.TYPE<1>,VM) + 1
            GOSUB 1000
*           CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,B,REF.NO,MAT ESTG.REC)
            CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,COMP,REF.NO,MAT ESTG.REC)
         END
      END
      *** CHECK FOR RIBBONS ***
      IF RFQ.BIND.RIBBONS<1,B> # "" THEN
         GRP = "OP0003"
         REF.NO = DCOUNT(ESTD.TYPE<1>,VM) + 1
         GOSUB 1000
*        CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,B,REF.NO,MAT ESTG.REC)
         CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,COMP,REF.NO,MAT ESTG.REC)
      END
      *** CHECK FOR QTY TO INDEX (FOR INDEX CUTS) ***
      IF RFQ.QTY.TO.INDEX<1,B> + 0 GT 0 THEN
         GRP = "OP0007"
         REF.NO = DCOUNT(ESTD.TYPE<1>,VM) + 1
         GOSUB 1000
*        CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,B,REF.NO,MAT ESTG.REC)
         CALL EST.INSERT.GROUP(CONO,EQTY,DEPT,COMP,REF.NO,MAT ESTG.REC)
      END
      ESTD.KEY = CONO:EST.KEY:'!':DCNT:'!':COMP:'!':EST.QTY
      MATWRITE ESTD.REC ON ESTIMATE.DEPT, ESTD.KEY
      MATWRITE ESTD.REC ON ESTIMATE.DEPT.BU, ESTD.KEY
      EST.DEPT<1,DCNT> = DEPT
      EST.DEPT.TYPE<1,DCNT> = DEPT.TYPE
      EST.DEPT.DESC<1,DCNT> = DEPT.DESC
      EST.COMP.FLAG<1,DCNT,COMP> = 'Y'
      EST.DEPT.COMP<1,-1> = DCNT:'!':COMP:'!':EST.QTY
   END
   RETURN
*
*--- GET GROUP INFORMATION HERE
*
1000 *
   ESTG.ID = DEPT:'!':GRP
   MATREAD DEPT.REC FROM DEPARTMENT, CONO:DEPT ELSE MAT DEPT.REC = ''
   MATREAD ESTG.REC FROM ESTIMATE.GRP, CONO:ESTG.ID ELSE MAT ESTG.REC = ''
   TEMP.CCTR = 'GRP'
   TEMP.GRP.ID = GRP:'*':GRP.SEQ
   ESTD.DEPT = DEPT
   ESTD.DEPT.TYPE = DEPT.TYPE
   ESTD.GRP.SEQ = GRP.SEQ
   ESTD.TYPE<1,REF.NO> = 'G'
   ESTD.CCTR<1,REF.NO> = 'GRP'
   ESTD.OPV<1,REF.NO> = GRP
   ESTD.QTY<1,REF.NO> = 100
   ESTD.FCTR<1,REF.NO> = 1000
   ESTD.DESC<1,REF.NO> = ESTG.DESC
   ESTD.GRP.ID<1,REF.NO> = GRP:'*':GRP.SEQ
   RETURN
93000 *
   RETURN
