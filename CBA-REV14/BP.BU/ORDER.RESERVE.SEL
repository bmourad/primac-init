SUBROUTINE ORDER.RESERVE.SEL(CONO,ACTION,CUSTNO,ORDNO,SHPNO,PDNO,WHNO,RTYPE,RESV.TOT,TMP.FI.QTY,TMP.FI.NO,TMP.REL.NO,TMP.REL.QTY,RELNO,RELQTY,PROD.SEQ,KIT.TYPE,PICK.QTY)
*COPY>CPYLIB>COM1
*COPY>JCS.CPYLIB>COM.JCS.LINK  
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>JCS.CPYLIB>COM.INV.STATS 
*COPY>PMC.CPYLIB>COM.CUST
*COPY>OPS.CPYLIB>COM.ORDER
*COPY>ICS.CPYLIB>COM.INV.CNV
********************************************************************
* REVISION    - [09.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - OPSBP
* PROGRAM     - ORDER.RESERVE.SEL
* AUTHOR      - NICK AMENDOLA
* DATE        - 07/16/93
* DESCRIPTION
* This program is used to reserve inventory for an order and release
* reserved inventory for shipment.
* ACTION = "R" = Reserve inventory, or "R-n" where n = order detail line #
*          "S" = Release for shipment
* TASK
* 18606 02/27/95 LLH - ADD KITTING PROD.SEQ TO FNGD
*
*T20852 doug 08/20/1996 * Pick ticket tracking
*T21177 diane 11/06/1996 * REV11 UPG
*T21477 ct6 01/31/1997 * INCREASE THE QTY COLUMNS BY 2 CHARS, Remove the
*                        Cust # Column for space.
* 05/08/97 CLS BUG FIX FOR SCROLLING AREA 1 ; *T21888
*T21967 llh 06/12/1997 * CHANGE SORT FROM LAST TO FIRST TO FIRST TO LAST
*T25740 epitka 01/16/2002 * REV12
********************************************************************
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>FNGD.ORDER.STATS
*COPY>OPS.CPYLIB>ORDER.DETAIL
*COPY>OPS.CPYLIB>ORDER.DETAIL.INQ
*COPY>OPS.CPYLIB>ORDER.RELEASE
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
MAT GEN.XREF.REC = ""
GXR.CO = CONO
TOP.LINES = DCOUNT(IWH.QTY.FI,VM)
*
DLN = FIELD(ACTION,"-",2)+0
ACTION = FIELD(ACTION,"-",1)
*
*---- Display screen
*
TOP.LINE.SPACE = 1
TOP.PAGE.SIZE = 5
TOP.BEGIN.PAGE = 7
*
BOT.LINE.SPACE = 1
BOT.PAGE.SIZE = 4
BOT.BEGIN.PAGE = 16
*
ESN = ECD.SCRN.NO
*
MATREAD FOS.REC FROM FNGD.ORDER.STATS, CONO:PDNO:"!":WHNO:"!":ORDNO:"!":PROD.SEQ:"!":KIT.TYPE ELSE
  MAT FOS.REC = ""
END
MTOT.RSV = SUM(IWH.RSV.FI)
MTOT.QTY = SUM(IWH.QTY.FI)
*
*
MATREAD ORD.DET.REC FROM ORDER.DETAIL, CONO:ORDNO:"!":SHPNO ELSE
  MAT ORD.DET.REC = ""
END
PTR = 1
LOOP
  LOCATE PDNO IN OSD.PROD<1>,PTR SETTING PPTR ELSE PPTR = 0
  BEGIN CASE
    CASE PPTR = 0
      PTR = 0
    CASE (OSD.WHSE<1,PPTR> = WHNO) AND (OSD.PROD.SEQ<1,PPTR> = PROD.SEQ) AND (OSD.KIT<1,PPTR> = KIT.TYPE)
      PTR = 0
  END CASE
UNTIL PTR = 0 DO
  PTR = PPTR + 1
REPEAT
ORIG.RSV.QTY = ""
IF PPTR > 0 THEN
  RCNT = DCOUNT(OSD.RECP.NO<1,PPTR>,SM)
  FOR RPTR = 1 TO RCNT
    FREF = OSD.RECP.NO<1,PPTR,RPTR>
    LOCATE FREF IN IWH.RECP.NO<1>,1 SETTING FPTR THEN
      ORIG.RSV.QTY<1,FPTR> = ORIG.RSV.QTY<1,FPTR> + OSD.FI.QTY<1,PPTR,RPTR>
    END
  NEXT RPTR
END
*
RSV.QTY = ""
REL.FLG = ""
REL.NO  = ""
REL.DATE = ""
REL.QTY = ""
MIN.QTY = "";* T20852
RCNT = DCOUNT(TMP.FI.NO,SM)
FOR RPTR = 1 TO RCNT
  FREF = TMP.FI.NO<1,1,RPTR>
  LOCATE FREF IN IWH.RECP.NO<1>,1 SETTING FPTR THEN
    RSV.QTY<1,FPTR> = RSV.QTY<1,FPTR> + TMP.FI.QTY<1,1,RPTR>
    MIN.QTY<1,FPTR> = MIN.QTY<1,FPTR> + PICK.QTY<1,1,RPTR>;* T20852
    P = DCOUNT(REL.FLG<1,FPTR>,SM)+ 1
    REL.FLG<1,FPTR,P> = "X"
    REL.NO<1,FPTR,P> = TMP.REL.NO<1,1,RPTR>
    REL.QTY<1,FPTR,P> = TMP.REL.QTY<1,1,RPTR>
    IF REL.NO<1,FPTR,P> = "" THEN
      MAT ORR.REC = ""
    END ELSE
      MATREAD ORR.REC FROM ORDER.RELEASE, CONO:REL.NO<1,FPTR,P> ELSE
        MAT ORR.REC = ""
      END
    END
    REL.DATE<1,FPTR,P> = ORR.DATE
  END
NEXT RPTR
*
FOR N = 1 TO TOP.LINES
  BLN = DCOUNT(REL.FLG<1,N>,SM)
  BEGIN CASE
    CASE BLN = 0
      REL.FLG<1,N> = "X"
      REL.QTY<1,N> = RSV.QTY<1,N>
    CASE REL.NO<1,N,BLN> = ""
      REL.QTY<1,N,BLN> = ""
      REL.QTY<1,N,BLN> = RSV.QTY<1,N> - SUM(REL.QTY<1,N>)
    CASE 1
      BLN = BLN + 1
      REL.FLG<1,N,BLN> = "X"
      REL.QTY<1,N,BLN> = RSV.QTY<1,N> - SUM(REL.QTY<1,N>)
  END CASE
NEXT N
*
IF RTYPE = "B" THEN
  IF ACTION = "R" THEN
    GOSUB 2000
    GOSUB 2500
  END
  IF ACTION = "S" THEN
    RQTY = 0
    FOR TLN = 1 TO TOP.LINES
      BCNT = DCOUNT(REL.FLG<1,TLN>,SM)
      FOR BLN = 1 TO BCNT
        IF REL.NO<1,TLN,BLN> = RELNO THEN
          RQTY = RQTY + REL.QTY<1,TLN,BLN>
        END
      NEXT BLN
    NEXT TLN
    IF RQTY = RELQTY THEN
      GOTO 99999
    END ELSE
      FOR TLN = 1 TO TOP.LINES
        BCNT = DCOUNT(REL.FLG<1,TLN>,SM)
        FOR BLN = BCNT TO 1 STEP -1
          IF REL.NO<1,TLN,BLN> = RELNO THEN
            RQTY = REL.QTY<1,TLN,BLN>
            REL.FLG  = DELETE(REL.FLG,1,TLN,BLN)
            REL.NO   = DELETE(REL.NO,1,TLN,BLN)
            REL.DATE = DELETE(REL.DATE,1,TLN,BLN)
            REL.QTY  = DELETE(REL.QTY,1,TLN,BLN)
            BCNT = BCNT - 1
            REL.QTY<1,TLN,BCNT> = REL.QTY<1,TLN,BCNT> + RQTY
          END
        NEXT BLN
      NEXT TLN
      RQTY = RELQTY+0
      FOR TLN = 1 TO TOP.LINES UNTIL RQTY = 0
        BCNT = DCOUNT(REL.FLG<1,TLN>,SM)
        AQTY = REL.QTY<1,TLN,BCNT>
        BEGIN CASE
          CASE AQTY >= RQTY
            REL.QTY<1,TLN,BCNT> = REL.QTY<1,TLN,BCNT> - RQTY
            REL.FLG  = INSERT(REL.FLG,1,TLN,BCNT,"X")
            REL.NO   = INSERT(REL.NO,1,TLN,BCNT,RELNO)
            REL.DATE = INSERT(REL.DATE,1,TLN,BCNT,"")
            REL.QTY  = INSERT(REL.QTY,1,TLN,BCNT,RQTY)
            RQTY = 0
          CASE AQTY > 0
            REL.QTY<1,TLN,BCNT> = 0
            REL.FLG  = INSERT(REL.FLG,1,TLN,BCNT,"X")
            REL.NO   = INSERT(REL.NO,1,TLN,BCNT,RELNO)
            REL.DATE = INSERT(REL.DATE,1,TLN,BCNT,"")
            REL.QTY  = INSERT(REL.QTY,1,TLN,BCNT,AQTY)
            RQTY = RQTY - AQTY
        END CASE
      NEXT TLN
    END
    GOSUB 2500
  END
  GOTO 99999
END
*
FOR I = 1 TO SCV.REC.SIZE
  SCV.REC(I)<ESN> = ""
NEXT I
SCV.REC(1)<ESN> = PDNO
SCV.REC(2)<ESN> = WHNO
SCV.REC(3)<ESN> = INV.FULL.DESC
SCV.REC(21)<ESN> = MTOT.QTY - MTOT.RSV
SCV.REC(22)<ESN> = MTOT.RSV + SUM((ORIG.RSV.QTY)) - SUM((RSV.QTY))   ;* NA 03-01-94
SCV.REC(23)<ESN> = SUM(RSV.QTY)
SCV.REC(24)<ESN> = ""
FOR I = 21 TO 24
  SCV.REC(I)<ESN> = OCONV(INT(((SCV.REC(I)<ESN>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
NEXT I
ECD.ACTION = 3; CALL SCRN.EDIT
GOSUB 1800
TLN = 1; LIMIT = TOP.LINES
FOR I = 1 TO LIMIT
  IF IWH.RSV.FI<1,I> > 0 THEN
    TLN = I; LIMIT = 0
  END
NEXT I
TLN=1;TOP.START.LINE = 0; GOSUB 1900
BLN=1; BOT.LINES = 0
BOT.START.LINE = 0; GOSUB 51900
SCV.REC(29)<ESN> = INT(TOP.LINES/TOP.PAGE.SIZE+.9) "R%2"
ECD.NUM=29; ECD.ACTION=5; CALL SCRN.EDIT
*
*---- Prompt line
*
MORE = 1
LOOP
  ECD.NUM = 31; SCV.REC(ECD.NUM)<ESN> = ""
  ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "END" OR OPTION = "E" OR OPTION = ""
      GOSUB 2500
      MORE = 0
    CASE OPTION = "S" OR OPTION = "SF"
      TLN = 1 + INT((TLN-1)/TOP.PAGE.SIZE) * TOP.PAGE.SIZE + TOP.PAGE.SIZE
      IF TLN > TOP.LINES THEN TLN = 1
      GOSUB 1900
    CASE OPTION = "SR"
      TLN = 1 + INT((TLN-1)/TOP.PAGE.SIZE) * TOP.PAGE.SIZE - TOP.PAGE.SIZE
      IF TLN < 1 THEN TLN = 1
      GOSUB 1900
    CASE OPTION = "ST"
      TLN = 1
      GOSUB 1900
    CASE OPTION = "SB"
      TLN = TOP.LINES
      GOSUB 1900
    CASE OPTION[1,1] = "S" AND NUM(OPTION[2,99])
      TLN.NO = OPTION[2,99] + 0
      IF TLN.NO < 1 OR TLN.NO > TOP.LINES THEN
        ERRMSG = "** Invalid selection **"
        GOSUB 91000
      END ELSE
        TLN = TLN.NO
        GOSUB 1900
      END
    CASE NOT(NUM(OPTION))
      ERRMSG = "Invalid entry, please re-enter"
      GOSUB 91000
    CASE OPTION < 1 OR OPTION > TOP.LINES
      ERRMSG = "Out of range, please re-enter"
      GOSUB 91000
    CASE 1
      FI.NO = DCOUNT(IWH.RSV.FI,VM)
      TLN = FI.NO - OPTION + 1
      AVL = IWH.RSV.FI<1,TLN> + ORIG.RSV.QTY<1,TLN> - RSV.QTY<1,TLN>
      IF AVL < 1 THEN 
        ERRMSG = 'There is nothing to reserve'
        GOSUB 91000; GOTO 999
      END
      SCV.REC(10)<ESN> = ""
      SCV.REC(10)<ESN,OPTION> = "*"
      ECD.NUM=10; ECD.SUB.NUM=OPTION; ECD.ACTION=7; CALL SCRN.EDIT
      BLN = DCOUNT(REL.FLG<1,TLN>,SM)
      IF BLN = 0 OR REL.NO<1,TLN,BLN> # "" THEN
        BLN = BLN + 1
        REL.FLG<1,TLN,BLN> = "X"
        REL.QTY<1,TLN,BLN> = ""
        REL.QTY<1,TLN,BLN> = RSV.QTY<1,TLN> - SUM(REL.QTY<1,TLN>)
      END
      BLN = 1
      BOT.LINES = DCOUNT(REL.FLG<1,TLN>, SM)
      BOT.START.LINE = 0
      GOSUB 51900
      GOSUB 6700
      IF ACTION = "R" THEN
        GOSUB 1000
        IF VALUE # "END" THEN
          BEGIN CASE
            CASE RSV.QTY<1,TLN>+0 = 0
              REL.QTY<1,TLN> = ""
              REL.QTY<1,TLN,BOT.LINES> = 0
              BOT.START.LINE = 0
              GOSUB 51900
              GOSUB 6700
            CASE BOT.LINES = 1
              REL.QTY<1,TLN,1> = RSV.QTY<1,TLN>
              BOT.START.LINE = 0
              GOSUB 51900
              GOSUB 6700
              IF REL.NO<1,TLN> # "" THEN
                OPT2 = ""
                GOSUB 5000
              END
            CASE REL.NO<1,TLN> # ""
              OPT2 = ""
              GOSUB 5000
          END CASE
        END
      END ELSE
        OPT2 = ""
        GOSUB 5000
      END
  END CASE
*
999 *
*
WHILE MORE DO REPEAT
GOTO 99990
*
*---- Qty To Reserve
1000*
SLN = TOP.BEGIN.PAGE + TOP.LINE.SPACE * MOD(OPTION-1,TOP.PAGE.SIZE)
AVL = IWH.RSV.FI<1,TLN> + ORIG.RSV.QTY<1,TLN> - RSV.QTY<1,TLN>
TYP = ICR.TYPE; SCALER = ICR.SCAL
X = 56; Y = SLN; MAXL = 11
MINV = INT(((MIN.QTY<1,TLN>)/ICR.DV1*ICR.MT1)/ICR.DV2+.5);* T20852
MAXV = INT(((RSV.QTY<1,TLN>+AVL)/ICR.DV1*ICR.MT1)/ICR.DV2+.5)   ;* NA 03-01-94
OMAXV = MAXV
IF RSV.QTY<1,TLN> > 0 THEN
  O.R = "O"
  DEFAULT = OCONV(INT(((RSV.QTY<1,TLN>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV)
END
CALL EDIT.SUB
IF (VALUE # "END" AND VALUE <= OMAXV AND OMAXV > 0) OR VALUE = "0" THEN
  RSV.QTY<1,TLN> = INT(((VALUE/ICR.MT1)*ICR.DV1)*ICR.DV2+.5)
  P_X = 56 ; P_Y = SLN ; P_VALUE = OCONV(INT((RSV.QTY<1,TLN>/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LINDX = TLN
  GOSUB 7000 ;* Calculate already reseve on order per FIFO
  P_X = 44 ; P_Y = SLN ; P_VALUE = OCONV(INT((AVL/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  GOSUB 1700
  GOSUB 1800
END ELSE
  IF VALUE # "END" THEN
    ERRMSG = VALUE:" is not a valid amount"
    GOSUB 91000
    GOTO 1000
  END
END
RETURN
*
*---- Display Total Reserve
1700*
ECD.NUM = 22
AVL.TOT = MTOT.RSV + SUM((ORIG.RSV.QTY)) - SUM((RSV.QTY))
SCV.REC(ECD.NUM)<ESN> = OCONV(INT(((AVL.TOT/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
ECD.ACTION = 5; CALL SCRN.EDIT
ECD.NUM = 23
RESV.TOT = SUM(RSV.QTY)
SCV.REC(ECD.NUM)<ESN> = OCONV(INT(((RESV.TOT/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
ECD.ACTION = 5; CALL SCRN.EDIT
RETURN
*
*---- Display Total Release
1800*
REL.GTOT = 0
C1 = DCOUNT(REL.QTY,VM)
FOR N1 = 1 TO C1
  C2 = DCOUNT(REL.QTY<1,N1>,SM)
  FOR N2 = 1 TO C2
    IF REL.NO<1,N1,N2> # "" THEN
      REL.GTOT = REL.GTOT + REL.QTY<1,N1,N2>
    END
  NEXT N2
NEXT N1
ECD.NUM = 24
SCV.REC(ECD.NUM)<ESN> = OCONV(INT(((REL.GTOT/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
ECD.ACTION = 5; CALL SCRN.EDIT
RETURN
*
*---- Display scrolling lines
1900*
START.LINE = 1 + INT((TLN-1)/TOP.PAGE.SIZE)*TOP.PAGE.SIZE
LAST.LINE = START.LINE + TOP.PAGE.SIZE - 1
IF LAST.LINE > TOP.LINES THEN LAST.LINE = TOP.LINES
IF START.LINE = TOP.START.LINE THEN GOTO 1990
TOP.START.LINE = START.LINE
ECD.NUM = 28
SCV.REC(ECD.NUM)<ESN> = INT(LAST.LINE/TOP.PAGE.SIZE+.9) "R%2"
ECD.ACTION = 5; CALL SCRN.EDIT
CNT = 1
FI.NO = DCOUNT(IWH.RSV.FI,VM)
FOR N = START.LINE TO LAST.LINE
  SLN = TOP.BEGIN.PAGE + TOP.LINE.SPACE * MOD(N-1,TOP.PAGE.SIZE)
  ACT.FI = FI.NO - N + 1
  AVL = IWH.RSV.FI<1,ACT.FI> + ORIG.RSV.QTY<1,ACT.FI> - RSV.QTY<1,ACT.FI>   ;* NA 03-01-94
  QTY = IWH.QTY.FI<1,ACT.FI>   ;* NA 03-01-94
  RSV = IWH.QTY.FI<1,ACT.FI> - IWH.RSV.FI<1,ACT.FI>   ;* NA 03-01-94
  QTY = SUM(REL.QTY<1,ACT.FI>)
  L = DCOUNT(REL.FLG<1,ACT.FI>,SM)
  IF REL.NO<1,ACT.FI,L> = "" THEN
    QTY = QTY - REL.QTY<1,ACT.FI,L>
  END
  P_X = 0 ; P_Y = SLN ; P_VALUE = N "R#2"  ; P_OPT = "CL"
  P_X := AM:3 ; P_Y := AM:SLN ; P_VALUE := AM:SCV.REC(10)<ESN,N>
  P_X := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:IWH.PO.NO.FI<1,ACT.FI> "L#8"
  P_X := AM:14 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(IWH.COST.FI<1,ACT.FI>,"MD4") "R#8"
  P_X := AM:23 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(IWH.RECV.FI<1,ACT.FI>,"D2/") "L#8"
  P_X := AM:32 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((RSV/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11"
  P_X := AM:44 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((AVL/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11"
  P_X := AM:56 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((RSV.QTY<1,ACT.FI>/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11"
  P_X := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((QTY/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  CNT = CNT + 1
NEXT N
FOR M = CNT TO TOP.PAGE.SIZE
  SLN = TOP.BEGIN.PAGE + TOP.LINE.SPACE * MOD(M-1,TOP.PAGE.SIZE)
  P_X = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
NEXT M
1990*
RETURN
*
*---- Adjust reserve buckets
2000*
RESV.DIF = RESV.TOT - SUM(RSV.QTY)
BEGIN CASE
  CASE RESV.DIF > 0
    LIMIT = TOP.LINES
*
*---- Adjust current buckets
*
    FOR F = 1 TO LIMIT
      AVL = IWH.RSV.FI<1,F> + ORIG.RSV.QTY<1,F> - RSV.QTY<1,F>   ;* NA 03-01-94
      BEGIN CASE
        CASE AVL < 1
        CASE RSV.QTY<1,F> < 1
        CASE AVL < RESV.DIF
          RSV.QTY<1,F> = RSV.QTY<1,F> + AVL
          RESV.DIF = RESV.DIF - AVL
        CASE 1
          RSV.QTY<1,F> = RSV.QTY<1,F> + RESV.DIF
          RESV.DIF = 0; LIMIT = 0
      END CASE
    NEXT F
*
*---- Adjust new buckets
*
    FOR F = 1 TO LIMIT
      AVL = IWH.RSV.FI<1,F> + ORIG.RSV.QTY<1,F> - RSV.QTY<1,F>   ;* NA 03-01-94
      BEGIN CASE
        CASE AVL < 1
        CASE AVL < RESV.DIF
          RSV.QTY<1,F> = RSV.QTY<1,F> + AVL
          RESV.DIF = RESV.DIF - AVL
        CASE 1
          RSV.QTY<1,F> = RSV.QTY<1,F> + RESV.DIF
          RESV.DIF = 0; LIMIT = 0
      END CASE
    NEXT F
  CASE RESV.DIF < 0
    RESV.DIF = 0 - RESV.DIF
    LIMIT = TOP.LINES
    FOR F = LIMIT TO 1 STEP -1 UNTIL RESV.DIF = 0
      BEGIN CASE
        CASE RSV.QTY<1,F> < 1
        CASE RSV.QTY<1,F> < RESV.DIF
          RESV.DIF = RESV.DIF - RSV.QTY<1,F>
          RSV.QTY<1,F> = 0
        CASE 1
          RSV.QTY<1,F> = RSV.QTY<1,F> - RESV.DIF
          RESV.DIF = 0
      END CASE
    NEXT F
END CASE
RESV.TOT = SUM(RSV.QTY)
RETURN
*
*---- REBUILD ORDER DETAIL DATA
2500*
TMP.FI.NO = ""
TMP.FI.QTY = ""
TMP.REL.NO = ""
TMP.REL.QTY = ""
P = 0
FCNT = TOP.LINES
FOR FPTR = 1 TO FCNT
  RQTY = RSV.QTY<1,FPTR>
  IF RQTY > 0 THEN
    RCNT = DCOUNT(REL.FLG<1,FPTR>,SM)
    FOR RPTR = 1 TO RCNT            ;* NA 02-25-94
      IF REL.NO<1,FPTR,RPTR> = "" THEN REL.QTY<1,FPTR,RPTR> = ""            ;* NA 02-25-94
    NEXT RPTR            ;* NA 02-25-94
    FOR RPTR = 1 TO RCNT
      P = P + 1
      TMP.FI.NO<1,1,P> = IWH.RECP.NO<1,FPTR>
      TMP.FI.QTY<1,1,P> = REL.QTY<1,FPTR,RPTR>
      TMP.REL.NO<1,1,P> = REL.NO<1,FPTR,RPTR>
      TMP.REL.QTY<1,1,P> = REL.QTY<1,FPTR,RPTR>
      RQTY = RQTY - REL.QTY<1,FPTR,RPTR>
    NEXT RPTR
    IF RQTY > 0 THEN
      TMP.FI.NO<1,1,P> = IWH.RECP.NO<1,FPTR>
      TMP.FI.QTY<1,1,P> = TMP.FI.QTY<1,1,P> + RQTY
    END
  END
NEXT FPTR
RETURN
*
*---- Bottom Prompt line
*
5000*
BMORE = 1
LOOP
  IF OPT2 = "" THEN
    ECD.NUM = 33; SCV.REC(ECD.NUM)<ESN> = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPT2 = ECD.RET.VALUE
  END
  BEGIN CASE
    CASE OPT2 = "END" OR OPT2 = "E" OR OPT2 = ""
      IF SUM(REL.QTY<1,TLN>) <= RSV.QTY<1,TLN>+0 THEN
        BMORE = 0
        BLN = 1
        GOSUB 51900
        GOSUB 6700
      END ELSE
        ERRMSG = "Release quantity exceeds reserve quantity! "
        GOSUB 91000
      END
    CASE OPT2 = "S" OR OPT2 = "SF"
      BLN = 1 + INT((BLN-1)/BOT.PAGE.SIZE) * BOT.PAGE.SIZE + BOT.PAGE.SIZE
      IF BLN > BOT.LINES THEN BLN = 1
      GOSUB 51900
    CASE OPT2 = "SR"
      BLN = 1 + INT((BLN-1)/BOT.PAGE.SIZE) * BOT.PAGE.SIZE - BOT.PAGE.SIZE
      IF BLN < 1 THEN BLN = 1
      GOSUB 51900
    CASE OPT2 = "ST"
      BLN = 1
      GOSUB 51900
    CASE OPT2 = "SB"
      BLN = BOT.LINES
      GOSUB 51900
    CASE OPT2[1,1] = "S" AND NUM(OPT2[2,99])
      BLN.NO = OPT2[2,99] + 0
      IF BLN.NO < 1 OR BLN.NO > BOT.LINES THEN
        ERRMSG = "** Invalid selection **"
        GOSUB 91000
      END ELSE
        BLN = BLN.NO
        GOSUB 51900
      END
    CASE ACTION = "R" AND OPT2 = "CA"
      FOR BLN = 1 TO BOT.LINES
        GOSUB 51900
        GOSUB 6000
        IF VALUE = "END" THEN BLN = 999
      NEXT BLN
    CASE NOT(NUM(OPT2))
      ERRMSG = "Invalid entry, please re-enter"
      GOSUB 91000
    CASE OPT2 < 1 OR OPT2 > BOT.LINES
      ERRMSG = "Out of range, please re-enter"
      GOSUB 91000
    CASE 1
      BLN = OPT2
      IF ACTION = "S" AND REL.NO<1,TLN,BLN> # RELNO THEN
        ERRMSG = "Cannot modify existing Release quantities"
        GOSUB 91000
      END ELSE
        GOSUB 6000
      END
  END CASE
  OPT2 = ""
WHILE BMORE DO REPEAT
RETURN
*
*---- Get release quantity
*
6000*
SLN = BOT.BEGIN.PAGE + BOT.LINE.SPACE * MOD(BLN-1,BOT.PAGE.SIZE)
IF RELNO = "" THEN
  ADDREL = 0
END ELSE
  LOCATE RELNO IN REL.NO<1,TLN>,1 SETTING P THEN ADDREL = 0 ELSE ADDREL = 1
END
BEGIN CASE
  CASE ACTION = "S" AND REL.NO<1,TLN,BLN> = "" AND ADDREL
    TEMP.REL.NO = RELNO
    TEMP.REL.DATE = ""
    P_X = 4 ; P_Y = SLN ; P_VALUE = RELNO "L#11" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    MAT ORR.REC = ""
  CASE REL.NO<1,TLN,BLN> = ""
    REL.QTY<1,TLN,BLN> = ""
    REL.QTY<1,TLN,BLN> = RSV.QTY<1,TLN> - SUM(REL.QTY<1,TLN>)
    IF REL.QTY<1,TLN,BLN> < 0 THEN REL.QTY<1,TLN,BLN> = 0
    P_X = 27 ; P_Y = SLN ; P_VALUE = OCONV(INT((REL.QTY<1,TLN,BLN>/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
6010 TYP=1
    X=4; Y=SLN; MAXL=10
    O.R = "O"
    CALL EDIT.SUB
    IF VALUE = "END" THEN RETURN
    IF VALUE = "" THEN RETURN
    IF VALUE = "???" THEN
      GXR.NAME = "ORDER.RELEASE"
      GXR.XREF = ORDER
      GXR.FILE = ORDER.RELEASE
      GXR.SRCH.ID = ORDNO
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      IF GXR.ACTION = "X" THEN
        P_X = 4 ; P_Y = SLN ; P_VALUE = SPACE(11)
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        ERRMSG = "No Releases present for this order"
        GOSUB 91000
        RETURN
      END
      ECD.ACTION = 2; CALL SCRN.EDIT
      ECD.ACTION = 3; CALL SCRN.EDIT
      TOP.START.LINE = 0
      GOSUB 1900
      BOT.START.LINE = 0
      GOSUB 51900
      IF GXR.ID = "" THEN RETURN
      VALUE = GXR.ID
      SLN = BOT.BEGIN.PAGE + BOT.LINE.SPACE * MOD(BLN-1,BOT.PAGE.SIZE)
      P_X = 4 ; P_Y = SLN ; P_VALUE = VALUE "L#11"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    LOCATE VALUE IN REL.NO<1,TLN>,1 SETTING P THEN
      IF P # BLN THEN
        ERRMSG = "Duplicate Release number. Try again! "
        GOSUB 91000
        GOTO 6010
      END
    END
    MATREAD ORR.REC FROM ORDER.RELEASE, CONO:VALUE ELSE
      ERRMSG = "Invalid Release number. Try again! "
      GOSUB 91000
      GOTO 6010
    END
    TEMP.REL.NO = VALUE
    TEMP.REL.DATE = ORR.DATE
    P_X = 17 ; P_Y = SLN ; P_VALUE = OCONV(ORR.DATE,"D2/") "L#8" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  CASE 1
    TEMP.REL.NO = REL.NO<1,TLN,BLN>
    TEMP.REL.DATE = REL.DATE<1,TLN,BLN>
    MATREAD ORR.REC FROM ORDER.RELEASE, CONO:TEMP.REL.NO ELSE
      MAT ORR.REC = ""
    END
END CASE
TYP = ICR.TYPE; SCALER = ICR.SCAL
X=27; Y=SLN; MAXL=11
MINV = 0
BEGIN CASE
  CASE ACTION = "S"
    MAXV = OCONV(INT(((RELQTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV)
  CASE DLN > 0
    MAXV = OCONV(INT(((ORR.QTY<1,DLN>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV)
  CASE 1
    MAXV = RSV.QTY<1,TLN>
END CASE
IF REL.QTY<1,TLN,BLN> > 0 THEN
  O.R = "O"
  DEFAULT = OCONV(INT(((REL.QTY<1,TLN,BLN>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV)
END ELSE
  BEGIN CASE
    CASE ACTION = "S"
      O.R = "O"
      DEFAULT = OCONV(INT(((RELQTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV)
    CASE DLN > 0
      O.R = "O"
      DEFAULT = OCONV(INT(((ORR.QTY<1,DLN>/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV)
  END CASE
END
CALL EDIT.SUB
IF VALUE # "END" THEN
  REL.NO<1,TLN,BLN> = TEMP.REL.NO
  REL.DATE<1,TLN,BLN> = TEMP.REL.DATE
  REL.QTY<1,TLN,BLN> = INT(((VALUE/ICR.MT1)*ICR.DV1)*ICR.DV2+.5)
  P_X = 27 ; P_Y = SLN ; P_VALUE = OCONV(INT((REL.QTY<1,TLN,BLN>/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
END ELSE
  P_X = 4 ; P_Y = SLN ; P_VALUE = REL.NO<1,TLN,BLN> "L#11" ; P_OPT = ""
  P_X := AM:17 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(REL.DATE<1,TLN,BLN>,"D2/") "L#8"
  P_X := AM:27 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((REL.QTY<1,TLN,BLN>/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
END
GOSUB 6700
RETURN
*
*---- Display Total Release
*
6700*
REL.TOT = SUM(REL.QTY<1,TLN>)
L = DCOUNT(REL.FLG<1,TLN>,SM)
IF REL.NO<1,TLN,L> = "" THEN
  QTY = REL.TOT - REL.QTY<1,TLN,L>
  XQTY = RSV.QTY<1,TLN> - QTY
  IF XQTY # REL.QTY<1,TLN,L> THEN
    IF XQTY >= 0 THEN
      REL.QTY<1,TLN,L> = XQTY
    END ELSE
      REL.QTY<1,TLN,L> = 0
    END
    BOT.START.LINE = 0
    GOSUB 51900
  END
END ELSE
  QTY = REL.TOT
END
SLN = TOP.BEGIN.PAGE + TOP.LINE.SPACE * MOD(TLN-1,TOP.PAGE.SIZE)
P_X = 68 ; P_Y = SLN ; P_VALUE = OCONV(INT((QTY/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11" ; P_OPT = ""
CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
GOSUB 1800
RETURN
7000*
PTR = 1
LOOP
  LOCATE PDNO IN ODQ.PROD<1>,PTR SETTING PINDX ELSE PINDX = 0
  BEGIN CASE
    CASE PINDX = 0
      PTR = 0
    CASE ODQ.WHSE<1,PINDX> = WHNO
      PTR = 0
  END CASE
UNTIL PTR = 0 DO
  PTR = PTR + 1
REPEAT
FROM.BUCKET = 0
IF (PTR+PINDX) > 0 THEN
  NUM.FI.BUCKETS = DCOUNT(ODQ.RECP.NO<1,1>,SVM)
  FOR B = 1 TO NUM.FI.BUCKETS
    IF ODQ.RECP.NO<1,1,B> = IWH.RECP.NO<1,LINDX> THEN
      FROM.BUCKET =FROM.BUCKET + ODQ.FI.QTY<1,1,B>
    END
  NEXT B
END
AVL = IWH.RSV.FI<1,LINDX> + ORIG.RSV.QTY<1,LINDX> - RSV.QTY<1,LINDX>   ;* NA 03-01-94
AVL = AVL - FROM.BUCKET
IF AVL < 0 THEN AVL = 0
RETURN
*
*
*---- Display scrolling lines
51900*
START.LINE = 1 + INT((BLN-1)/BOT.PAGE.SIZE)*BOT.PAGE.SIZE
LAST.LINE = START.LINE + BOT.PAGE.SIZE - 1
IF LAST.LINE > BOT.LINES THEN LAST.LINE = BOT.LINES
IF START.LINE = BOT.START.LINE THEN GOTO 51990
BOT.START.LINE = START.LINE
SCV.REC(58)<ESN> = INT(LAST.LINE/BOT.PAGE.SIZE+.9) "R%2"
ECD.NUM=58; ECD.ACTION=5; CALL SCRN.EDIT
SCV.REC(59)<ESN> = INT(BOT.LINES/BOT.PAGE.SIZE+.9) "R%2"
ECD.NUM=59; ECD.ACTION=5; CALL SCRN.EDIT
CNT = 1
FOR N = START.LINE TO LAST.LINE
  SLN = BOT.BEGIN.PAGE + BOT.LINE.SPACE * MOD(N-1,BOT.PAGE.SIZE)
  P_X = 0 ; P_Y = SLN ; P_VALUE = N "R#2" ; P_OPT = "CL"
  P_X := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:REL.NO<1,TLN,N> "L#11"
  P_X := AM:17 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(REL.DATE<1,TLN,N>,"D2/") "L#8"
  P_X := AM:27 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((REL.QTY<1,TLN,N>/ICR.DV1*ICR.MT1)/ICR.DV2+.5),ICR.CNV1) "R#11"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  CNT = CNT + 1
NEXT N
FOR M = CNT TO BOT.PAGE.SIZE
  SLN = BOT.BEGIN.PAGE + BOT.LINE.SPACE * MOD(M-1,BOT.PAGE.SIZE)
  P_X = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
NEXT M
51990*
RETURN
*
*--- CALLS FOR SYSCOM
*
91000 *
ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- End of Program
*
99990 *
ECD.ACTION = 99 ; CALL SCRN.EDIT
99999 RETURN
END
