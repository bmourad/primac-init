*COPY>CPYLIB>COM1
**************************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - BHR.UPDATE.XML
* AUTHOR   - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DATE     - 08/24/2004
*
* DESCRIPTION
*
* This program will add new records to the BHR.XML record BHR when
* a new record is added to the BHR_OUTPUT file.
*
**************************************************************************
*
*---- COPY LIBRARY
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>BHR_OUTPUT
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- OPEN FILES
*
   OPEN 'CONTROL' TO CONTROL ELSE STOP
   OPEN 'BHR_OUTPUT' TO BHR_OUTPUT ELSE STOP
   OPEN 'BHR.XML' TO BHR.XML ELSE STOP
   OPEN 'COST.CNTR' TO COST.CNTR ELSE STOP
   OPEN 'EMPLOYEE' TO EMPLOYEE ELSE STOP
*
*---- INITIALIZATION
*
   READ BDATA FROM BHR.XML,'BHR' ELSE BDATA = ''
*
*---- MAIN PROCESSING
*
10 *
   READNEXT KEY ELSE GOTO 99999
   MATREAD BHR.REC FROM BHR_OUTPUT, KEY ELSE GOTO 10
   CONO = KEY[1,3]
   BEGIN CASE
      CASE BHR.FILE = 'COST.CNTR'
         MATREAD CCTR.REC FROM COST.CNTR, CONO:BHR.CODE ELSE
            MAT CCTR.REC = ''
         END
         *** FIND WHERE COST CENTERS START ***
         FINDSTR 'CostCenters' IN BDATA SETTING START THEN
         *** SCAN THE COST CENTERS AND INSERT ***
            B = START + 1
            ESTART = 0
            LOOP WHILE ESTART = 0 DO
               B += 1
               LINE = TRIM(BDATA<B>)
               IF LINE = '' THEN GOTO 10
               IF LINE[1,4] = '<ID>' OR LINE = '</CostCenters>' THEN
                  IF LINE # '</CostCenters>' THEN
                     CC.ID = FIELD(LINE,'<',2)[4,99]
                  END ELSE
                     CC.ID = ''
                  END
                  BEGIN CASE
                     CASE CC.ID = BHR.CODE
                     CASE CC.ID GT BHR.CODE
                        ESTART = B - 1
                        BDATA = INSERT(BDATA,ESTART,0,0,'        </CostCenter>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <Description>':CCTR.DESC:'</Description>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <CCNumber>':BHR.CODE:'</CCNumber>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <ID>':BHR.CODE:'</ID>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'        <CostCenter>')
                     CASE LINE = '</CostCenters>'
                        ESTART = B
                        BDATA = INSERT(BDATA,ESTART,0,0,'        </CostCenter>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <Description>':CCTR.DESC:'</Description>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <CCNumber>':BHR.CODE:'</CCNumber>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <ID>':BHR.CODE:'</ID>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'        <CostCenter>')
                  END CASE
               END
            REPEAT
         END
      CASE BHR.FILE = 'EMPLOYEE'
         MATREAD EMP.REC FROM EMPLOYEE, CONO:BHR.CODE ELSE
            MAT EMP.REC = ''
         END
         *** FIND WHERE EMPLOYEES START ***
         FINDSTR 'Employees' IN BDATA SETTING START THEN
         *** SCAN THE EMPLOYEES AND INSERT ***
            B = START + 1
            ESTART = 0
            LOOP WHILE ESTART = 0 DO
               B += 1
               LINE = TRIM(BDATA<B>)
               IF LINE = '' THEN GOTO 10
               IF LINE[1,4] = '<ID>' OR LINE = '</Employees>' THEN
                  IF LINE # '</Employees>' THEN
                     EMP.ID = FIELD(LINE,'<',2)[4,99]
                  END ELSE
                     EMP.ID = ''
                  END
                  BEGIN CASE
                     CASE EMP.ID = BHR.CODE
                     CASE EMP.ID GT BHR.CODE
                        ESTART = B - 1
                        BDATA = INSERT(BDATA,ESTART,0,0,'        </Employee>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <LastName>':EMP.LAST.NAME:'</LastName>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <FirstName>':EMP.FRST.NAME:'</FirstName>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <EmployeeNumber>':BHR.CODE:'</EmployeeNumber>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <ID>':BHR.CODE:'</ID>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'        <Employee>')
                     CASE LINE = '</Employees>'
                        ESTART = B
                        BDATA = INSERT(BDATA,ESTART,0,0,'        </Employee>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <LastName>':EMP.LAST.NAME:'</LastName>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <FirstName>':EMP.FRST.NAME:'</FirstName>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <EmployeeNumber>':BHR.CODE:'</EmployeeNumber>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'          <ID>':BHR.CODE:'</ID>')
                        BDATA = INSERT(BDATA,ESTART,0,0,'        <Employee>')
                  END CASE
               END
            REPEAT
         END
   END CASE
   GOTO 10
99999 *
   WRITE BDATA ON BHR.XML,'BHR'
END
