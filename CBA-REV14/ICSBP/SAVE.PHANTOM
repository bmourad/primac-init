*COPY>CPYLIB>COM1
*********************************************************************
*
*  PROGRAM - JANUS.PHANTOM
*
*  AUTHOR  - DUANE GREEN, COMPUTER BUSINESS ASSOCIATES
*
*  DATE    - 05/21/96
*
*  DESCRIPTION
*
*  This program is used to interface to the Intermec handheld barcode
*  reader.
*
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INTERMEC.TRAN
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*
*---- PRE-INITIALIZATION
*
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOTO 90000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOTO 90000
   END
   OPEN '','JANUS.CONTROL' TO JANUS.CONTROL ELSE
      ERRMSG = 'JANUS.CONTROL FILE MISSING'
      GOTO 90000
   END
   OPEN "","INTERMEC.TRAN" TO INTERMEC.TRAN ELSE
      ERRMSG = "CANNOT OPEN INTERMEC.TRAN FILE"
      GOTO 90000
   END
*
*---- INITIALIZATION
*
   BREAK OFF
   ECHO OFF
   TRACE = 1                                    ;*** T R A C E ***
   TMSG = ""
   TREC = ''
   RECCNT = 0
*
* Set The Record Lock On The Port
*
10 *
     READV PORTNO FROM JANUS.CONTROL,"MYPORT",1 ELSE
          TMSG = 'CANT FIND MY PORT'
          WRITEV TMSG ON JANUS.CONTROL,"TRACE",1
          GOTO 99999
     END
     READV CONO FROM JANUS.CONTROL,"MYPORT",2 ELSE
          TMSG = 'COMPANY NOT SET ON MYPORT'
          WRITEV TMSG ON JANUS.CONTROL,"TRACE",1
     END
     DELETE JANUS.CONTROL,"MYPORT"
     READU LREC FROM JANUS.CONTROL,'JANUS.SERVER.LOCK.':PORTNO ELSE NULL
     READVU LASTREC FROM JANUS.CONTROL,CONO:"INT.ID",1 ELSE LASTREC = '1'
     SLEEP 5
*
*-------------------------------------------------------------------*
*
*
*---- UPLOAD DATA FILE FROM INTERMEC READER
*
3000 *
   GOSUB 10200 ;* Contact The Reader
   TREC<-1> = RESP
   IF RESP # 'XFER.OK' THEN
   WRITE TREC ON JANUS.CONTROL,PORTNO:"TRACE"
      GOTO 99999
   END
   WRITE TREC ON JANUS.CONTROL,PORTNO:"TRACE"
*
3100 * Send Transaction Prompt
   PRINT 'TRAN'
   INPUT INTERMEC.REC:
   BEGIN CASE
      CASE INTERMEC.REC = ''
         GO 3100
      CASE INTERMEC.REC = "XFER.COMPLETE"
         TREC<-1> = INTERMEC.REC
         GOTO 3200
      CASE INDEX(INTERMEC.REC,"\",1) = 0
         TREC<-1> = INTERMEC.REC
         GOTO 3200
   END CASE
   RECCNT = RECCNT + 1
   TCNT = DCOUNT(INTERMEC.REC,"\")
   DATAREC = TRIM(FIELD(INTERMEC.REC,"\",1))
   FOR TPTR = 2 TO TCNT
      DATAREC<TPTR> = TRIM(FIELD(INTERMEC.REC,"\",TPTR))
   NEXT TPTR
   MAT INT.REC = ""
   Intermec.TTYPE = DATAREC<1>
   BEGIN CASE
      CASE Intermec.TTYPE = "TS"
         TDATE       = ICONV(DATAREC<2>,"D")
         TTIME       = ICONV(DATAREC<3>,"MTS")
         Intermec.TDATE   = TDATE
         Intermec.TTIME   = TTIME
      CASE Intermec.TTYPE = "M"
         XTIME       = TTIME + DATAREC<2>
         Intermec.TDATE   = TDATE + INT(XTIME/86400)
         Intermec.TTIME   = MOD(XTIME,86400)
         Intermec.MAN.ID  = DATAREC<3>
         IF DATAREC<4> # "" THEN
            Intermec.MAN.TOT = INT(DATAREC<4>*100+0.5)
         END
      CASE Intermec.TTYPE = "R"
         XTIME       = TTIME + DATAREC<2>
         Intermec.TDATE   = TDATE + INT(XTIME/86400)
         Intermec.TTIME   = MOD(XTIME,86400)
         Intermec.RS.ID   = DATAREC<3>
         Intermec.MILL.ID = DATAREC<4>
         Intermec.QTY     = INT(DATAREC<5>*100+0.5)
         Intermec.WHSE    = DATAREC<6>
         Intermec.LOC     = DATAREC<7>
      CASE Intermec.TTYPE = "T"
         XTIME       = TTIME + DATAREC<2>
         Intermec.TDATE   = TDATE + INT(XTIME/86400)
         Intermec.TTIME   = MOD(XTIME,86400)
         Intermec.RS.ID   = DATAREC<3>
         Intermec.WHSE    = DATAREC<4>
         Intermec.LOC     = DATAREC<5>
   END CASE
   MATWRITE INT.REC ON INTERMEC.TRAN, CONO:LASTREC:"*":PORTNO
   LASTREC += 1
   GOTO 3100
*
3200 * Transfer Is Complete
   WRITEV LASTREC ON JANUS.CONTROL,CONO:"INT.ID",1
*
   BEGIN CASE
      CASE RECCNT = 0 AND INTERMEC.REC = "XFER.COMPLETE"
         TREC<-1> = "NO TRANSACTIONS RECEIVED."
      CASE INTERMEC.REC = "XFER.COMPLETE"
         TREC<-1> = RECCNT:" TRANSACTIONS RECEIVED."
         CALL RS.JANUS.UPDATE(CONO,PORTNO)
      CASE 1
         TREC<-1> = XXX
         TREC<-1> = "UPLOAD ABORTED."
   END CASE
   WRITE TREC ON JANUS.CONTROL,PORTNO:"TRACE"
   STOP
*
*-------------------------------------------------------------------*
*                                                                   *
*               G E N E R I C   S U B R O U T I N E S               *
*                                                                   *
*-------------------------------------------------------------------*
*
*
*---- INITIATE Intermec COMMUNICATIONS
*
10200 *
   PRINT
   PRINT 'XFER.REQUEST'
   INPUT RESP:
   IF RESP = 'XFER.OK' THEN
      COMM.OPEN = 1
   END ELSE
      RESP = " Janus is not responding!"
   END
   RETURN
*
*---- ERROR ROUTINE
*
90000 *
   WRITEV ERRMSG ON JANUS.CONTROL,CONO:"TRACE",1
   STOP
*
*---- END OF PROGRAM
*
99999 *
   STOP
