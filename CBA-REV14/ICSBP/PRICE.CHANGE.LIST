*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>ICS.CPYLIB>COM.INV.LINK  
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - PRICE.CHANGE.LIST
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/24/84
* DESCRIPTION -
* This program produces a listing of those inventory items
* which will be changed when the PRICE.CHANGE.POST is run.
*
* TASK 19403 - ICS DIVISIONALIZATION, Bob Sherer  09/27/95
*T25740 edvard 10/25/2001 * REV12
*T26090 ajibaly 03/28/2002 * CONVERT TO R12 REPORT.SCRN
*T26090 cmykleb 04/11/2002 * If report is (V)iewed do not update records
*                            as printed.
*T27990 lross 03/11/2004 * Mods for ICS.IWH.SUB (ERRMSG)
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>PRICE.CHANGE
*
*---- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG="INVENTORY FILE MISSING";GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE MISSING";GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
   OPEN '','PRICE.CHANGE' TO PRICE.CHANGE ELSE ERRMSG="PRICE.CHANGE FILE MISSING";GOTO 93000
   OPEN '','INV.WHSE' TO INV.WHSE ELSE ERRMSG="INV.WHSE FILE MISSING";GOTO 93000
   OPEN '','WAREHOUSE' TO WAREHOUSE ELSE ERRMSG="WAREHOUSE FILE MISSING";GOTO 93000
*
*---- GET VALUES FROM PROC
*
   PROCREAD X ELSE 
      ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC" ; GOTO 93000
   END
   CONO = X<1>
   DIV.OWNER = X<9>
*T26090 v
*CONO.NAME = X<2>
   CONO.NAME = ""
*REPORT.NAME = "INVENTORY.PRICE.CHANGE.LISTING"
*REPORT.NUMBER = "XXXX"
   REPORT.NAME = ""
   REPORT.NUMBER = X<2>
   PRINT.FLAG = X<10>
*T26090 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
*---- INITIALIZATION
*
   MAT ORG.IWH.REC=''
   OPEN.FLAG=1
   GEN.DIV='00'
   PG.NO=0
   LINE.CNT=0
   DATE=OCONV(DATE(),"D2/")
   TIME=OCONV(TIME(),"MT2:")
   PRINT.FLAG=0
   FIRST.FLAG=1
   PREV.PROD.NO=''
   C.DIV = ''
   MATREADU PC.REC FROM PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER ELSE
      ERRMSG="PRICE.CHANGE FILE MISSING";GOTO 93000
   END
   PRINTER ON
*
*---- MAIN PROCESSING
*
10*
   DONE=0
   LOOP
      READNEXT ID ELSE  DONE=1
   UNTIL (DONE) DO
      PROD.NO=FIELD(ID,"!",1)
      WHSE=FIELD(ID,"!",2)
      IF PROD.NO # PREV.PROD.NO THEN
         MATREAD INV.REC FROM INVENTORY,PROD.NO ELSE CONTINUE
         PREV.PROD.NO=PROD.NO
      END
      IF FIRST.FLAG=1 THEN
         FIRST.FLAG=0;GOSUB 15
      END
      GOSUB 30
   REPEAT
   IF PRINT.FLAG=1 THEN
      GOSUB 50
   END ELSE
      RELEASE PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER
   END
   GOTO 99999
*
*----  GET.HEADING.INFO
*
15*
   IF PC.COST<1,1> + 0 # 0 THEN
      COST.NAME="STD COST"
      SIGN=PC.COST.ADJ<1,1>
      AMOUNT=PC.COST<1,1>
   END ELSE
      IF PC.COST<1,2> + 0 # 0 THEN
         COST.NAME="LIST PRC"
         SIGN=PC.COST.ADJ<1,2>
         AMOUNT=PC.COST<1,2>
      END ELSE
         COST.NAME=''
         SIGN=""
         AMOUNT=""
      END
   END
   BY.ENDING=""
   IF SIGN="$" THEN
      BY.ENDING="$ ":OCONV(AMOUNT,"MD4")
   END ELSE
      BY.ENDING=OCONV(AMOUNT,"MD4"):" %"
   END
*
*---- PRINT HEADING
*
20*
   PRINT CHAR(12)
   LINE.CNT=0
   PG.NO=PG.NO + 1
   PRINT HD1:PG.NO
   PRINT HD2
   PRINT "DIVISION : ":DIV.OWNER 'R#3':SPACE(46):"BY ":BY.ENDING
   PRINT
   H.LINE=''
   H.LINE=SPACE(7):"                                 ":SPACE(72):"OLD          NEW"
   PRINT H.LINE
   H.LINE=''
   H.LINE="WHSE   TYPE     MLINE    PLINE    BAS.WT   PRODUCT NUMBER ":SPACE(17):"FULL DESCRIPTION":SPACE(19):COST.NAME:'     ':COST.NAME
   PRINT H.LINE
   H.LINE="----  -------  -------   ------   ------   ---------------   ---------------------------------------------   ----------   ----------"
   PRINT H.LINE
   PRINT
   LINE.CNT=LINE.CNT + 8
   RETURN
*
*---- PRINT REPORT LINE
*
30*
   MATREAD IWH.REC FROM INV.WHSE,ID THEN
      INAH.PROD=PROD.NO
      INAH.WHSE=WHSE
      TMP.CNT='' ; LAST='' ; TMP.ARR=''               
      PERIOD='' ; ACTION=1
      MAT ORG.IWH.REC= MAT IWH.REC
*T27990 CALL ICS.IWH.SUB (CONO,PERIOD,TMP.ARR,TMP.CNT,LAST,ACTION,OPEN.FLAG)
      CALL ICS.IWH.SUB (CONO,PERIOD,TMP.ARR,TMP.CNT,LAST,ACTION,OPEN.FLAG,ERRMSG)
   END ELSE GOTO 35
   IF DIV.OWNER = "ALL" THEN GOSUB 60
   GOSUB 40
   PROD.NUM=PROD.NO[4,99]
   P.LINE=''
   P.LINE=P.LINE:WHSE"L#4":'  ':PROD.TYPE"L#7":'  ':MLINE"L#7":'   ':PLINE"L#6":'   ':BAS.WT"R#6":'   ':PROD.NUM"L#15":'   ':PROD.DESC"L#45":'   ':OCONV(STD.LIST.COST,"MD4")"R#10":'   ':OCONV(NEW.COST,"MD4")"R#10"
   PRINT P.LINE
   PRINT.FLAG=1
   LINE.CNT=LINE.CNT + 1
   IF LINE.CNT > 55 THEN GOSUB 20
35*
   RETURN
*
*---- CALCULATE NEW VALUES
*
40*
   PROD.TYPE=INV.PAP.TYPE
   MLINE=INV.M.LINE
   PLINE=INV.LINE
   BAS.WT=OCONV(INV.BAS.WT,"MD2")
   PROD.DESC=INV.DESC
   OLD.COST=0
   NEW.COST=0
   STD.LIST.COST=0
   IF PC.COST<1,1> # 0 THEN
      STD.LIST.COST=IWH.STD.COST
      BEGIN CASE
         CASE PC.BASIS='L'
            L.CNT=DCOUNT(IWH.COST.FI,VM)
            OLD.COST=IWH.COST.FI<1,L.CNT>
         CASE PC.BASIS='A'
            OLD.COST=IWH.AVG.COST
         CASE PC.BASIS='S'
            OLD.COST=IWH.STD.COST
         CASE 1
            ERRMSG="INVALID BASIS";GOTO 93000
      END CASE
      CONV="MD":4 - PC.COST.PREC<1,1>
      IF PC.COST.ADJ<1,1>="$" THEN
         NEW.COST=OLD.COST + PC.COST<1,1>
         NEW.COST=OCONV(NEW.COST,CONV)
         NEW.COST=ICONV(INT(NEW.COST + .5),CONV)
      END ELSE
         IF PC.COST.ADJ<1,1>="%" THEN
            NEW.COST=OLD.COST + INT(OLD.COST * (PC.COST<1,1> / 1000000))
            NEW.COST=OCONV(NEW.COST,CONV)
            NEW.COST=ICONV(INT(NEW.COST + .5),CONV)
         END
      END
   END ELSE
      IF PC.COST<1,2> # 0 THEN
         STD.LIST.COST=IWH.LIST.COST
         OLD.COST=IWH.LIST.COST
         IF OLD.COST + 0 = 0 THEN GOTO 45
         CONV="MD":4 - PC.COST.PREC<1,2>
         IF PC.COST.ADJ<1,2>="$" THEN
            NEW.COST=IWH.LIST.COST + PC.COST<1,2>
            NEW.COST=OCONV(NEW.COST,CONV)
            NEW.COST=ICONV(INT(NEW.COST + .5),CONV)
         END ELSE
            IF PC.COST.ADJ<1,2>="%" THEN
               NEW.COST=IWH.LIST.COST + INT(IWH.LIST.COST * (PC.COST<1,2> / 1000000))
               NEW.COST=OCONV(NEW.COST,CONV)
               NEW.COST=ICONV(INT(NEW.COST + .5),CONV)
            END
         END
      END
   END
45*
   RETURN
*
*---- UPDATE PRICE.CHANGE RECORD
*
50*
*T26090 v
*  PC.PRT.DATE=DATE()
   IF PRINT.FLAG # "V" THEN PC.PRT.DATE=DATE()
*T26090 ^
   MATWRITE PC.REC ON PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER
   RETURN
*
*---- BREAK BETWEEN DIVISIONS WHEN RUNNING FOR ALL DIVISIONS
*
60*
   MATREADU WHSE.REC FROM WAREHOUSE, CONO:WHSE THEN
      IF C.DIV # WHS.DIV THEN
         IF C.DIV # "" THEN PRINT ""
         PRINT "DIVISION ":WHS.DIV
         C.DIV = WHS.DIV
      END
   END
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   PRINTER OFF;ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);PRINTER ON;RETURN
92000*
   PRINTER OFF;ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);PRINTER ON;RETURN
93000*
   ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINTER OFF
*ECD.ACTION=99 ; CALL SCRN.EDIT
END
