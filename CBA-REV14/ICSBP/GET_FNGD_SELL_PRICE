  SUBROUTINE GET_FNGD_SELL_PRICE (ERRMSG, THIS_IN, THIS_OUT, MAT JFS.REC, MAT FILE.VARS)
*T29032 cmyklebu 12/29/2006 * Move pgm from rev12 to rev14.
*COPY>CPYLIB>FILE.VARS
*COPY>OPS.CPYLIB>JOB.FNGD.STATS
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>PRICE.TABLE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JES.CPYLIB>ESTIMATE ;*T28058
*COPY>CPYLIB>CHAR
  ERRMSG = ""
  THIS_OUT = ""
  CONO = ""; JOBNO = ""; PRODNO = ""; WHSENO = ""
  FLG = ""; COST_FI = ""; ESTNO = ""; CUSTNO = ""
  CONO = THIS_IN<1,1>
  JOBNO = THIS_IN<1,2>
  PRODNO = THIS_IN<1,3>
  WHSENO = THIS_IN<1,4>
  FLG = THIS_IN<1,5>
  COST_FI = THIS_IN<1,6>
  ESTNO = THIS_IN<1,7>
  CUSTNO = THIS_IN<1,8>
*
  PTR = 1
  FND = 0
  LOOP
    LOCATE PRODNO IN JFS.PROD<1>,PTR SETTING FND ELSE FND = 0
    BEGIN CASE
    CASE FND = 0
      PTR = 0
    CASE WHSENO # JFS.WHSE<1,FND>
    CASE 1
      PTR = 0
    END CASE
  UNTIL PTR = 0 DO
    PTR = FND + 1
  REPEAT
  IF FND = 0 THEN
    ERRMSG = "PROD/WHSE - ":PRODNO:"/":WHSENO:" IS NOT ON JOB - ":JOBNO
  END ELSE
    THIS_OUT = JFS.U.SELL<1,FND>
    MQTY = JFS.M.QTY<1,FND>
    IF THIS_OUT = "" AND FLG = "BUILD" THEN GOSUB BUILD_SELLPRICE
  END
  GOTO 999999
*
*--- BUILD SELLING PRICE
*
BUILD_SELLPRICE: 
  IF FILEINFO(CATEGORY,0) # 1 THEN
    OPEN '',"CATEGORY" TO CATEGORY ELSE
      ERRMSG = "CATEGORY FILE IS MISSING"
      RETURN
    END
  END
  IF FILEINFO(INVENTORY,0) # 1 THEN
    OPEN '',"INVENTORY" TO INVENTORY ELSE
      ERRMSG = "INVENTORY FILE IS MISSING"
      RETURN
    END
  END
  IF FILEINFO(INV.WHSE,0) # 1 THEN
    OPEN '',"INV.WHSE" TO INV.WHSE ELSE
      ERRMSG = "INV.WHSE FILE IS MISSING"
      RETURN
    END
  END
  IF FILEINFO(PRICE.TABLE,0) # 1 THEN
    OPEN '',"PRICE.TABLE" TO PRICE.TABLE ELSE
      ERRMSG = "PRICE.TABLE FILE IS MISSING"
      RETURN
    END
  END
  IF FILEINFO(ESTIMATE,0) # 1 THEN
    OPEN '',"ESTIMATE" TO ESTIMATE ELSE
      ERRMSG = "ESTIMATE FILE IS MISSING"
      RETURN
    END
  END
  MATREAD INV.REC FROM INVENTORY, CONO:PRODNO ELSE
    ERRMSG = PRODNO : " - INVENTORY RECORD IS MISSING"
    RETURN
  END
  MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
    ERRMSG = INV.LINE : " - CATEGORY RECORD IS MISSING"
    RETURN
  END
  MATREAD IWH.REC FROM INV.WHSE, CONO:PRODNO:"!":WHSENO ELSE
    ERRMSG = PRODNO:"/":WHSENO : " - INV.WHSE RECORD IS MISSING"
    RETURN
  END
*COPY>ICSBP>INV.UM.CNV
  BEGIN CASE
  CASE CATG.SALE.TYPE = "EP" AND MQTY > 0
    THIS_OUT = 0; *T28058
  CASE CATG.SALE.TYPE = "EP"
*T28058 v
    THIS_OUT = 0
    IF ESTNO # "" THEN
      MATREAD EST.REC FROM ESTIMATE, CONO:ESTNO THEN
        BEGIN CASE
        CASE INV.UNIT<1,5> = "M"
          UNIT = 1
        CASE INV.UNIT<1,5> = "C"
          UNIT = 10
        CASE 1
          UNIT = 1000
        END CASE
        QTY = OCONV(INT(((MQTY / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5), ICR.CNV)
        LOCATE QTY IN EST.QTY<1>,1 BY "AR" SETTING QPTR ELSE NULL
        IF QPTR > DCOUNT(EST.QTY,VM) THEN QPTR = QPTR - 1
        THIS_OUT = INT(EST.OM.PRICE.M<1,QPTR> * 100 / UNIT + .5)
      END
    END
*T28058 ^
  CASE CATG.SALE.TYPE = "PT"
    MATREAD PGT.REC FROM PRICE.TABLE, CONO:IWH.PRICE.GRP ELSE
      MAT PGT.REC=''
      ERRMSG = IWH.PRICE.GRP : " - PRICE.TABLE RECORD IS MISSING"
      RETURN
    END
    THIS_OUT = ""
    QTY = OCONV(INT(((MQTY / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV)
    QCNT = DCOUNT(PGT.QTY,VM)
    FOR Q = 1 TO QCNT WHILE THIS_OUT = ""
      IF QTY <= PGT.QTY<1,Q>+0 THEN
        THIS_OUT = PGT.PRICE<1,Q>+0
      END
    NEXT Q
  CASE CATG.SALE.TYPE = "ES"
    THIS_OUT = IWH.EST.SALE+0
  CASE CATG.SALE.TYPE = "SP"
    GOSUB GET_PRICECODE
    BEGIN CASE
    CASE NOT(NUM(CUST.PRICE.CODE))
      THIS_OUT = IWH.SELL<1,1>+0
    CASE CUST.PRICE.CODE > 0 AND CUST.PRICE.CODE < 5
      THIS_OUT = IWH.SELL<1,CUST.PRICE.CODE>+0
    CASE 1
      THIS_OUT = IWH.SELL<1,1>+0
    END CASE
  CASE CATG.SALE.TYPE = "SD"
    THIS_OUT = IWH.STD.COST+0
  CASE CATG.SALE.TYPE = "AV"
    THIS_OUT = IWH.AVG.COST+0
  CASE CATG.SALE.TYPE = "LP"
    IF COST_FI = "" THEN ERRMSG = "NO COST PASSED"
    THIS_OUT = COST_FI<1,DCOUNT(COST_FI,VM)>+0
*--- CATG.SALE.TYPE = "CB"
  CASE CATG.FNGD.TYPE = "ES"
    THIS_OUT = IWH.EST.SALE+0
  CASE CATG.FNGD.TYPE = "SP"
    GOSUB GET_PRICECODE
    BEGIN CASE
    CASE NOT(NUM(CUST.PRICE.CODE))
      THIS_OUT = IWH.SELL<1,1>+0
    CASE CUST.PRICE.CODE > 0 AND CUST.PRICE.CODE < 5
      THIS_OUT = IWH.SELL<1,CUST.PRICE.CODE>+0
    CASE 1
      THIS_OUT = IWH.SELL<1,1>+0
    END CASE
  CASE CATG.FNGD.TYPE = "SD"
    THIS_OUT = IWH.STD.COST+0
  CASE CATG.FNGD.TYPE = "AV"
    THIS_OUT = IWH.AVG.COST+0
  CASE CATG.FNGD.TYPE = "LP"
    IF COST_FI = "" THEN ERRMSG = "NO COST PASSED"
    THIS_OUT = COST_FI<1,DCOUNT(COST_FI,VM)>+0
  CASE 1
    THIS_OUT = 0 ;*T28058
  END CASE
  THIS_OUT = INT(THIS_OUT + .5)
  THIS_OUT = OCONV(THIS_OUT, "MD4")
  RETURN
*
*--- GET CUST.PRICE.CODE
*
GET_PRICECODE: 
  MAT CUST.REC = ""
  IF FILEINFO(CUSTOMER,0) # 1 THEN
    OPEN '',"CUSTOMER" TO CUSTOMER ELSE
      ERRMSG = "CUSTOMER FILE IS MISSING"
      RETURN
    END
  END
  MATREAD CUST.REC FROM CUSTOMER, CONO:CUSTNO ELSE
    ERRMSG = CUSTNO : " - CUSTOMER RECORD IS MISSING"
    RETURN
  END
  RETURN
999999*
  RETURN
END
