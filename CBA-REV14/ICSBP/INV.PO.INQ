  SUBROUTINE INV.PO.INQ(CONO)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - BP
* PROGRAM     - INV.PO.INQ
* DATE        - 01/31/84
* DESCRIPTION - This program is used to display all (PO) activities
*               for a product in a warehouse
*T25978 adelgado 02/18/2002 * Add the use of prompts (S,SR,SB,ST).
*T28779 lross 03/06/2006 * Allow multi-lines same prod/whse diff
*                          due-dates/prices.
*T29101 lross 05/11/2007 * Error in display.
*ENDDOC
*********************************************************************
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>PO ;*T28779
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*T28779 v
OPEN 'PO' TO PO ELSE
  ERRMSG='Cannot open PO file!'; GOSUB 91000; GOTO 99999
END
*T28779 ^
  PAGE.SIZE = 5
  BEGIN.PAGE = 7
  LINE.SPACE = 2
  OLD.START.LINE = 0
  S.VEND.NAME = ''
  PO.JOB.QTY = ''
*T28779 v
  TEMP.PO=''
  TEMP.VEND=''
  TEMP.VEND.DESC=''
  TEMP.EX.DATE=''
  TEMP.ON.ORD=''
  TEMP.ALLOC=''
  TEMP.BAL=''
  TEMP.PO.SEQ=''
  PROD=SCV.REC(1)<ECD.SCRN.NO>
  WHSE=SCV.REC(3)<ECD.SCRN.NO>
*T28779 ^
*
  UNKNOWN = STR('?',30)
*
  LINES = COUNT(ISTAT.PO,VM) + (ISTAT.PO # '')
*T28779 v
  TPTR=0
  FOR L = 1 TO LINES
    MATREAD PO.REC FROM PO,CONO:ISTAT.PO<1,L> THEN
      PTR=1
      LOOP
        LOCATE PROD IN PO.PROD.NUM<1>,PTR SETTING POFND THEN
          BEGIN CASE
          CASE WHSE = PO.WHSE<1,POFND>
            TPTR += 1
            TEMP.PO<1,TPTR> = ISTAT.PO<1,L>
            TEMP.VEND<1,TPTR> = ISTAT.PO.VEND<1,L>
            TEMP.EX.DATE<1,TPTR> = PO.DEL.DATE<1,POFND>
            TEMP.ON.ORD<1,TPTR> = PO.QTY.OPEN<1,POFND>
            TEMP.ALLOC<1,TPTR> = SUM(PO.JB.OPEN<1,POFND>)
            TEMP.BAL<1,TPTR> = TEMP.ON.ORD<1,TPTR> - TEMP.ALLOC<1,TPTR>
            TEMP.PO.SEQ<1,TPTR> = POFND
            READV DUMMY FROM VEND,CONO:PO.VEND.NO,1 ELSE
              DUMMY = ''
            END
            TEMP.VEND.DESC<1,TPTR> = DUMMY
*T29101 v   PTR += 1
            PTR = POFND + 1
          CASE 1
            POFND = 0
          END CASE
        END ELSE POFND = 0
      WHILE POFND DO
      REPEAT
    END
  NEXT L
*T28779 ^
  SCV.REC(4)<ECD.SCRN.NO> = 0
  SCV.REC(5)<ECD.SCRN.NO> = 0
  FOR I = 1 TO LINES
    JLINES = COUNT(ISTAT.PO.JOB.QTY<1,I>,SVM) + (ISTAT.PO.JOB.QTY<1,I> # '')
    FOR J = 1 TO JLINES
      PO.JOB.QTY<1,I> = PO.JOB.QTY<1,I> + ISTAT.PO.JOB.QTY<1,I,J>
    NEXT J
    SCV.REC(4)<ECD.SCRN.NO> = SCV.REC(4)<ECD.SCRN.NO> + ISTAT.PO.QTY<1,I>
    SCV.REC(5)<ECD.SCRN.NO> = SCV.REC(5)<ECD.SCRN.NO> + PO.JOB.QTY<1,I>
*T28779    MATREAD VEND.REC FROM VEND, CONO:ISTAT.PO.VEND<1,I> ELSE VEND.DESC = UNKNOWN
*T28779    S.VEND.NAME<1,I> = VEND.DESC
  NEXT I
  SCV.REC(6)<ECD.SCRN.NO> = SCV.REC(4)<ECD.SCRN.NO> - SCV.REC(5)<ECD.SCRN.NO>
  SCV.REC(4)<ECD.SCRN.NO> = OCONV(INT(((SCV.REC(4)<ECD.SCRN.NO>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
  SCV.REC(5)<ECD.SCRN.NO> = OCONV(INT(((SCV.REC(5)<ECD.SCRN.NO>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
  SCV.REC(6)<ECD.SCRN.NO> = OCONV(INT(((SCV.REC(6)<ECD.SCRN.NO>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
  ECD.ACTION = 3; CALL SCRN.EDIT
  LINES = DCOUNT(TEMP.PO<1>,VM) ;*T28779
  LN = 1; GOSUB 11000
  LOOP
    ECD.NUM = 7; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ''
    ECD.ACTION = 4; CALL SCRN.EDIT
    ACTION = ECD.RET.VALUE
    BEGIN CASE
      CASE ACTION = '' OR ACTION = 'END'
        ACTION = ''
      CASE NUM(ACTION)
        IF ACTION < START.LINE OR ACTION > LAST.LINE THEN
          ERRMSG = 'LINE NUMBER IS OUT OF RANGE'; GOSUB 91000
        END ELSE
          ECD.SCRN.NO = ECD.SCRN.NO + 1
          SCV.REC(1)<ECD.SCRN.NO> = SCV.REC(1)<ECD.SCRN.NO-1>
          SCV.REC(2)<ECD.SCRN.NO> = SCV.REC(2)<ECD.SCRN.NO-1>
          SCV.REC(3)<ECD.SCRN.NO> = SCV.REC(3)<ECD.SCRN.NO-1>
*T28779 v
          MATREAD PO.REC FROM PO,CONO:TEMP.PO<1,ACTION> ELSE MAT PO.REC = ''
*         CALL INV.PO.JOB.INQ(CONO,ACTION)
          CALL INV.PO.JOB.INQ(CONO,TEMP.PO.SEQ<1,ACTION>,MAT PO.REC)
*T28779 ^
          ECD.SCRN.NO = ECD.SCRN.NO - 1
          ECD.ACTION = 3; CALL SCRN.EDIT
          OLD.START.LINE = 0; GOSUB 11000
        END
      CASE ACTION = 'S'
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 11000
      * T25978 v
      CASE ACTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 11000
      CASE ACTION = 'ST'
        LN = 1
        GOSUB 11000
      CASE ACTION = 'SB'
        LN = LINES
        GOSUB 11000
      * T25978 ^
    END CASE
  WHILE ACTION # '' DO REPEAT
  GOTO 99999
11000 START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN GOTO 11999
  OLD.START.LINE = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  IF LAST.LINE > LINES THEN LAST.LINE = LINES
  CNT = 1
  FOR N = START.LINE TO LAST.LINE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
*T28779    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:ISTAT.PO<1,N> "L#11"
    P_X  := AM:4 ; P_Y := AM:SLN ; P_VALUE := AM:TEMP.PO<1,N> "L#11"
*T28779    P_X  := AM:16 ; P_Y := AM:SLN ; P_VALUE := AM:ISTAT.PO.VEND<1,N> "L#8"
    P_X  := AM:16 ; P_Y := AM:SLN ; P_VALUE := AM:TEMP.VEND<1,N> "L#8"
*T28779    P_X  := AM:25 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(ISTAT.PO.DATE<1,N>,"D2/") "L#8"
    P_X  := AM:25 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(TEMP.EX.DATE<1,N>,"D2/") "L#8"
*T28779    P_X  := AM:34 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((ISTAT.PO.QTY<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1) "R#12"
    P_X  := AM:34 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((TEMP.ON.ORD<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1) "R#12"
*T28779    P_X  := AM:47 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((PO.JOB.QTY<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1) "R#12"
    P_X  := AM:47 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((TEMP.ALLOC<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1) "R#12"
*T28779    P_X  := AM:60 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT((((ISTAT.PO.QTY<1,N>-PO.JOB.QTY<1,N>)/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1) "R#12"
    P_X  := AM:60 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(INT(((TEMP.BAL<1,N>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1) "R#12"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*T28779    P_X  = 16 ; P_Y = SLN+1 ; P_VALUE = S.VEND.NAME<1,N> "L#30" ; P_OPT = ""
    P_X  = 16 ; P_Y = SLN+1 ; P_VALUE = TEMP.VEND.DESC<1,N> "L#30" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
11999 RETURN
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC) 
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
