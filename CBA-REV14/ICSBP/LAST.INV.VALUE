*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - LAST.INV.VALUE
* DATE        - 12/12/85
* BY          - JIHAD YAMOUT , C.B.A
* DESCRIPTION - This report is used to eveluate inventory by last
*                 cost.
*T21638 doug 02/26/1997 * Add quantity conversion for FNGD
*T26190 ajibaly 03/08/2002 * CONVERT TO REV12 USING BUILD.IWH.FI SUB
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T25740 epitka 06/20/2002 * REV12
*T26676 epitka 06/20/2002 * REV12
*********************************************************************
***   INSERT EQUATE FROM CPYLIB
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
***   INITIALIZATION
LINE.COUNT        = 99
PAGE.NO           = 0
PREV.WHSE = "" ; PREV.M.P.LINE = "" ; PREV.BAS.WT = ""
TOT.W.COST = 0
TOT.M.P.COST = 0
M.P.ONHAND = 0
BS.ONHAND = 0
TOT.BS.COST = 0
GRD.TOT = 0
SP1 = SPACE(1)
***   SET UP THE ERRMSG ROUTINE FOR SYSCOM
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
***   OPEN THE FILE
OPEN '','INV.WHSE' TO INV.WHSE ELSE
  ERRMSG = 'INV.WHSE FILE IS MISSING'
  GOTO 93000
END
OPEN '','INVENTORY' TO INVENTORY ELSE
  ERRMSG = 'INVENTORY FILE IS MISSING'
  GOTO 93000
END
***   READ COMPANY NAME 
PROCREAD ID ELSE
  ERRMSG = ' CANNOT PERFORM PROCREAD'
  GOTO 93000
END
CONO = ID<1>
*T26493 v
*  CONO.NAME = ID<2>
*  REPORT.NAME = "CURRENT.INVENTORY.VALUE.AT  (LAST-COST)"
*  REPORT.NUMBER = "XXXX"
REPORT.NUMBER = ID<2>
REPORT.NAME = ""
CONO.NAME = ""
*T26493 ^
REPORT.DATE = DATE()
HD1 = "" ; HD2 = ""
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*T26190 v
OPEN.FLAG=1
ERR.FLAG = ''
PERIOD = ID<9>
SEL.PERIOD=ID<9>
*T26190 ^
*T26676 v              
IF PERIOD='ALL' THEN   
  PERIOD=''            
END ELSE               
  PERIOD<1,2>='EXCLUDE'
END                    
*T26676 ^              
***   PUT PRINTER ON
PRINTER ON
***   READ AND PROCESS FIRST RECORD
100*
READNEXT IWH.ID ELSE GOTO 999999
GOSUB 200
***   READ THE THREE FILE
MATREAD IWH.REC FROM INV.WHSE,IWH.ID ELSE
  MAT IWH.REC = ''
  GOTO 100
END
MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
  MAT INV.REC = ''
END
CALL BUILD.IWH.FI(IWH.ID,MAT IWH.REC,PERIOD,ERR.FLAG,ERRMSG,OPEN.FLAG) ; *T26190
*T26676 v                       
IF PERIOD#'' THEN               
  IWH.ON.HAND=SUM(IWH.QTY.FI<1>)
END                             
*T26676 ^                       
FIFO.CNT = COUNT(IWH.RECV.FI, VM) + (IWH.RECV.FI # "")
LA.COST = IWH.COST.FI<1,FIFO.CNT>
***   SET THE PRODUCT NUMBER
PREV.PRO = PROD
PREV.WHSE = WHSE
PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
PREV.BAS.WT = INV.BAS.WT
***   SET THE HEADING LINES
HH.LINE = "PRODUCT NUMBER                FULL DESCRIPTION                U/M BAS-WGHT  COSTWT   ON-HAND   LAST-COST LAST $VALUE "
DH.LINE = "--------------- --------------------------------------------- --- -------- ------- ----------- --------- ------------"
DH1.LINE = "                                                                                   -----------           ------------"
DH2.LINE = "                                                                                   ===========           ============"
***   PROCESS THE FILE
GOSUB 1000
***    READ AND PROCESS ALL THE FILES
DATA = 0
LOOP
  READNEXT IWH.ID ELSE DATA = 1
WHILE DATA = 0 DO
  GOSUB 200
***   READ THE THREE FILES
  MATREAD IWH.REC FROM INV.WHSE,IWH.ID ELSE
    MAT IWH.REC = ''
    GOTO 111
  END
  MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
    MAT INV.REC = ''
  END
  CALL BUILD.IWH.FI(IWH.ID,MAT IWH.REC,PERIOD,ERR.FLAG,ERRMSG,OPEN.FLAG) ; *T26190
*T26676 v                       
  IF PERIOD#'' THEN               
    IWH.ON.HAND=SUM(IWH.QTY.FI<1>)
  END                             
*T26676 ^                       
  FIFO.CNT = COUNT(IWH.RECV.FI, VM) + (IWH.RECV.FI # "")
  LA.COST = IWH.COST.FI<1,FIFO.CNT>
***   PROCESS THE RECORD
  GOSUB 1000
111*
REPEAT
***   PRINT THE TOTAL FOR LAST PRODUCT
GOSUB 25000 ; GOSUB 26000 ; GOSUB 27000
***   PRINT THE GRAND TOTAL
IF LINE.COUNT + 2 > 54 THEN GOSUB 80000
PRINT
PRINT SPACE(39):"GRAND TOTAL ":SPACE(53):OCONV(GRD.TOT, "MD2") "R#12"
***    END OF JOB
GOTO 999999
***   SEPERATE THE IWH.ID TO PRODUCT, WHSE 
200*
PROD.KEY = FIELD(IWH.ID,"!",1)
PROD = PROD.KEY[4,99]
WHSE = FIELD(IWH.ID,"!",2)
RETURN
***   PROCESS THE RECORD
1000*
BEGIN CASE
  CASE PREV.WHSE # WHSE
    GOSUB 25000
    GOSUB 26000
    GOSUB 27000
    LINE.COUNT = 99
  CASE PREV.M.P.LINE # INV.M.LINE:" / ":INV.LINE
    GOSUB 25000
    GOSUB 26000
    LINE.COUNT = 99
  CASE PREV.BAS.WT # INV.BAS.WT
    GOSUB 25000
END CASE
IF INV.COST.WT *1 = 0 THEN INV.COST.WT = 100
*COPY>ICSBP>INV.UM.CNV
*  MAT INV.CNV.REC = ""
*  BEGIN CASE
*  CASE INV.UNIT<1,2> = "SHT" AND INV.UNIT<1,3> = "LBS"
*     ICR.CNV = "MD0"; ICR.DV2 = 1
*     ICR.MT1 = 1; ICR.DV1 = INV.M.WT
*  CASE INV.UNIT<1,2> = "PC" AND INV.UNIT<1,3> = "MSI"
*     ICR.CNV = "MD0"; ICR.DV2 = 1
*     ICR.MT1 = 10; ICR.DV1 = INV.PAP.WIDTH/100
*  CASE INV.UNIT<1,2> = "FT" AND INV.UNIT<1,3> = "MSI"
*     ICR.CNV = "MD0"; ICR.DV2 = 12
*     ICR.MT1 = 100; ICR.DV1 = INV.PAP.WIDTH/100
*  CASE 1
*     ICR.CNV = "MD2"; ICR.DV2 = 1
*     ICR.MT1 = 1; ICR.DV1 = 10
*  END CASE
GOSUB 20000
TOT.BS.COST = TOT.BS.COST + COST
RETURN
***  ROUTINE TO PRINT THE RECORD
20000*
IF LINE.COUNT > 54 THEN GOSUB 80000
LINE.COUNT = LINE.COUNT + 1
IF INV.PAP.TYPE = '' OR INV.PAP.TYPE = 'REG' THEN INV.PAP.TYPE = 'REGULAR'
H.LINE = PROD "L#15":SP1:INV.FULL.DESC "L#45":SP1
H.LINE = H.LINE:INV.UNIT<1,2> "L#3":SP1
H.LINE = H.LINE:OCONV(PREV.BAS.WT, "MD2") "R#8":SP1
H.LINE = H.LINE:OCONV(INV.COST.WT, "MD2") "R#7":SP1
BEGIN CASE
  CASE IWH.ON.HAND < 0
    ROND = "-.5"
    ON.HAND = INT(((IWH.ON.HAND/ICR.DV1)*ICR.MT1)/ICR.DV2 - .5)
  CASE IWH.ON.HAND > 0
    ROND = .5
    ON.HAND = INT(((IWH.ON.HAND/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5)
  CASE 1
    ROND = 0
    ON.HAND = 0
END CASE
BS.ONHAND = BS.ONHAND + ON.HAND
H.LINE = H.LINE : OCONV(ON.HAND,ICR.CNV) "R#11":SP1
H.LINE = H.LINE : OCONV(LA.COST , "MD4") "R#9" :SP1
COST = INT((LA.COST/10000) * ((IWH.ON.HAND/10) / (INV.COST.WT/100)) + ROND)
H.LINE = H.LINE : OCONV(COST, 'MD2,') "R#12"
PRINT H.LINE
RETURN
*** PRINT TOTAL FOR BAS.WT
25000*
IF LINE.COUNT + 2 > 54 THEN GOSUB 80000 
LINE.COUNT = LINE.COUNT + 2
PRINT DH1.LINE
T.LINE = ''
T.LINE = T.LINE:SPACE(39):"TOTAL FOR BASIS WEIGHT  ":OCONV(PREV.BAS.WT, "MD2") "L#9":SPACE(11)
T.LINE = T.LINE:OCONV(BS.ONHAND,ICR.CNV) "R#11":SPACE(11)
T.LINE = T.LINE :OCONV(TOT.BS.COST, "MD2") "R#12"
PRINT T.LINE
PRINT
LINE.COUNT = LINE.COUNT + 1
TOT.M.P.COST = TOT.M.P.COST + TOT.BS.COST
M.P.ONHAND = M.P.ONHAND + BS.ONHAND
PREV.BAS.WT = INV.BAS.WT
TOT.BS.COST = 0
BS.ONHAND = 0
RETURN
*** PRINT TOTAL FOR M.LINE / P.LINE
26000*
IF LINE.COUNT + 2 > 54 THEN GOSUB 80000
LINE.COUNT = LINE.COUNT + 2
PRINT DH2.LINE
T.LINE = ''
T.LINE = T.LINE :SPACE(39):"TOTAL FOR MLINE / PLINE  ":PREV.M.P.LINE "L#18":SP1
T.LINE = T.LINE :OCONV(M.P.ONHAND,ICR.CNV) "R#11":SPACE(11)
T.LINE = T.LINE :OCONV(TOT.M.P.COST, "MD2") "R#12"
PRINT T.LINE
PRINT
LINE.COUNT = LINE.COUNT + 1
PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
TOT.W.COST = TOT.W.COST + TOT.M.P.COST
TOT.M.P.COST = 0
M.P.ONHAND = 0
RETURN
*** PRINT TOTAL FOR WHSE
27000*
IF LINE.COUNT + 2 > 54 THEN GOSUB 80000
LINE.COUNT = LINE.COUNT + 2
PRINT
T.LINE = ''
T.LINE = T.LINE :SPACE(39):"TOTAL FOR WAREHOUSE  ":PREV.WHSE "L#5":SPACE(39)
T.LINE = T.LINE :OCONV(TOT.W.COST, "MD2") "R#12"
PRINT T.LINE
PRINT
LINE.COUNT = LINE.COUNT + 1
PREV.WHSE = WHSE
GRD.TOT = GRD.TOT + TOT.W.COST
TOT.W.COST = 0
RETURN
***  ROUTINE TO PRINT HEADINGS
80000*
PRINT CHAR(12)
PAGE.NO = PAGE.NO + 1
PRINT HD1:PAGE.NO
PRINT HD2
IF PERIOD<1,1> # '' THEN                     
  PRINT SPACE(65):"FOR PERIOD : ":SEL.PERIOD
END ELSE                                     
  PRINT                                      
END                                          
PRINT "WAREHOUSE : ":PREV.WHSE
PRINT "MLINE / PLINE : ":PREV.M.P.LINE
PRINT
PRINT HH.LINE
PRINT DH.LINE
LINE.COUNT = 8
RETURN
***   CALLS FOR SYSCOM
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
999999*
PRINTER OFF
END
