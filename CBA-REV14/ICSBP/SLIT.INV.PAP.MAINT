      SUBROUTINE SLIT.INV.PAP.MAINT(CONO,PROD.NUM,MAT SAVE.INV.REC,MAT IWH.REC)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INVENTORY
*********************************************************************
* REVISION    - [10.0]
* Copyright 1994 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - SLIT.INV.PAP.MAINT
* BY          - BLAIR W. DICKERSON, VERCOM SOFTWARE, INC.
* DATE        - 10/6/94
* DESCRIPTION
* This program provides for maintenance of paper products.
*********************************************************************
*
**** INSERT FILES EQUATES
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>COLOR
*COPY>ICS.CPYLIB>FINISH
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*
**** SETUP FOR SYSTEM ERRMSGS
*
      SYS.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
      OPEN '','INVENTORY' TO INVENTORY ELSE
         ERRMSG = 'INVENTORY FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','WAREHOUSE' TO WAREHOUSE ELSE
         ERRMSG = 'WAREHOUSE FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','COLOR' TO COLOR ELSE
         ERRMSG = 'COLOR FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','FINISH' TO FINISH ELSE
         ERRMSG = 'FINISH FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','FOH.TABLE' TO FOH.TABLE ELSE
         ERRMSG = 'FOH.TABLE FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','INV.WHSE' TO INV.WHSE ELSE
         ERRMSG = 'INV.WHSE IS MISSING'
         GOTO 93000
      END
      OPEN '','CATEGORY' TO CATEGORY ELSE
         ERRMSG = 'CATEGORY FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','COMPANY' TO COMPANY ELSE
         ERRMSG = 'COMPANY FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','VEND' TO VEND ELSE
         ERRMSG = 'VENDOR FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','VENDOR.XREF' TO VENDOR.XREF ELSE
         ERRMSG = 'VENDOR.XREF FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CONTROL' TO CONTROL ELSE
         ERRMSG = 'CONTROL FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','INVENTORY.XREF' TO INVENTORY.XREF ELSE
         ERRMSG = 'INVENTORY.XREF FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','PRODUCT.XREF' TO PRODUCT.XREF ELSE
         ERRMSG = 'PRODUCT.XREF FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CUSTOMER.XREF' TO CUSTOMER.XREF ELSE
         ERRMSG = 'CUSTOMER.XREF FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','CUSTOMER' TO CUSTOMER ELSE
         ERRMSG = 'CUSTOMER FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','XREF.DATA' TO XREF.DATA ELSE
         ERRMSG = 'XREF.DATA FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','PREFIX' TO PREFIX ELSE
         ERRMSG = 'PREFIX FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','INV.STATS' TO INV.STATS ELSE
         ERRMSG = 'INV.STATS FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','INV.WHSE.LOC' TO INV.WHSE.LOC ELSE
         ERRMSG = 'INV.WHSE.LOC FILE IS MISSING'
         GOTO 93000
      END
*
***** MAIN PROCESSING
*
      DIM SAVE.INV.REC(INV.REC.SIZE)
      MAT INV.REC = MAT SAVE.INV.REC
      SAME.PROD = 1
      WNOF = 0
      WHSE.KEY = ''
**** PRINT SCREEN
1*
      ECD.SCRN.NO = 2
      MAT CATG.REC = ''
      MAT SCV.REC = ""
      ECD.ACTION=2;CALL SCRN.EDIT
*
***** ENTER PROD NUMBER
*
5*
      QTY.OH = 0
      OPTION = ''
      OLD.NAME.XREF = ""
      OLD.NAME = ''
      NEW.REC = ""
      STATUS = '0'
      OLD.NAME = INV.FULL.DESC
      OLD.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
      GOSUB 70000
      GOSUB 1000
      ECD.ACTION=3;CALL SCRN.EDIT
*     WH.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
*     IF WH.CNT > 0 THEN
*        FOR WW = 1 TO WH.CNT UNTIL QTY.OH > 0
*           MATREAD IWH.REC FROM INV.WHSE  , CONO:ECD.RET.VALUE:"!":INV.WHSE.CODE<1,WW> ELSE
*              IWH.ON.HAND = 0
*           END
*           QTY.OH = QTY.OH + IWH.ON.HAND
*        NEXT WW
*     END
      GOSUB 60000
      MAT CATG.REC = ''
      MAT SCV.REC = ""
      ECD.ACTION=6;CALL SCRN.EDIT
      GOTO 99999
*
*** ENTER SP.CODE
*
10*
      ECD.NUM = 6
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.SPEC = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER PROD LINE
*
15*
      GOSUB 80000
      IF QTY.FLAG THEN QTY.FLAG = "";GOTO 21
      IF QTY.OH <= 0 THEN
         ECD.NUM = 7
         ECD.VALDAT.CODE = 2
         ECD.VALDAT.FILE = CATEGORY
         ECD.PREFIX.ID = CONO
         ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE = 'END' THEN GOTO 20
         INV.LINE = ECD.RET.VALUE
         FOR SAVE = 1 TO CATG.REC.SIZE
            CATG.REC(SAVE) = ECD.VALDAT.ITEM<SAVE>
         NEXT SAVE
         BEGIN CASE
            CASE CATG.MAJ.LINE = "FNGD"
               IF CO.OPS = 'Y' THEN
                  ERRMSG = "Can't utilize a Finished Goods product line for a Paper product"
                  GOSUB 91000 ; GOTO 15
               END
            CASE CATG.TYPE # "S" AND CATG.TYPE # "L" AND CATG.TYPE # "PC" AND CATG.TYPE # "RL"
               ERRMSG = "Can't utilize a Regular product line for a Paper product"
               GOSUB 91000 ; GOTO 15
         END CASE
*
**** DISPLAY MAJOR LINE
*
20*
         INV.M.LINE = CATG.MAJ.LINE
         ECD.NUM = 8
         SCV.REC(8)<ECD.SCRN.NO,1> = CATG.MAJ.LINE
         ECD.ACTION=5;CALL SCRN.EDIT
         IF OPTION # "" THEN
            GOSUB 40 ; GOSUB 95 ; GOSUB 55 ; GOSUB 90 ; GOSUB 140
         END
      END ELSE
         ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
         GOSUB 91000
      END
21*
      RETURN
*
***** ENTER DESCRIPTION
*
35*
      ECD.NUM = 11
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.DESC = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER UNIT BUY
*
40*
      BEGIN CASE
         CASE CATG.TYPE = "S"
            ECD.VALDAT = "SHT,LBS" ; ECD.HMSG = "SHT , LBS" ; ECD.DEFAULT = "SHT"
            INV.UNIT<1,2> = "SHT" ; INV.UNIT<1,3> = "LBS" ; INV.PAP.TYPE = "SHEET"
         CASE CATG.TYPE = "L"
            ECD.VALDAT = "LBS" ; ECD.HMSG = "LBS" ; ECD.DEFAULT = "LBS"
            INV.UNIT<1,2> = "LBS" ; INV.UNIT<1,3> = "LBS" ; INV.PAP.TYPE = "ROLL"
         CASE CATG.TYPE = "RL"
            ECD.VALDAT = "FT,MSI,PC" ; ECD.HMSG = "FT , MSI , PC" ; ECD.DEFAULT = "PC"
            INV.UNIT<1,2> = "PC" ; INV.UNIT<1,3> = "MSI" ; INV.PAP.TYPE = "LROLL"
         CASE CATG.TYPE = "PC"
            ECD.VALDAT = "FT,MSI" ; ECD.HMSG = "FT , MSI" ; ECD.DEFAULT = "FT"
            INV.UNIT<1,2> = "FT" ; INV.UNIT<1,3> = "MSI" ; INV.PAP.TYPE = "PCOAT"
         CASE 1
            INV.FACTOR = ""
            INV.AVG.DIA = ""
            INV.CORE.DIA = ""
            SCV.REC(44)<ECD.SCRN.NO,1> = ""
            SCV.REC(45)<ECD.SCRN.NO,1> = ""
            SCV.REC(46)<ECD.SCRN.NO,1> = ""
            ECD.NUM = 44; ECD.ACTION = 5;CALL SCRN.EDIT
            ECD.NUM = 45; ECD.ACTION = 5;CALL SCRN.EDIT
            ECD.NUM = 46; ECD.ACTION = 5;CALL SCRN.EDIT
      END CASE
      ECD.NUM = 12
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.UNIT<1,1> = ECD.RET.VALUE
      END
*
***** ENTER UNIT STOCK
*
45*
      ECD.NUM = 13
      SCV.REC(13)<ECD.SCRN.NO,1> = INV.UNIT<1,2>
      ECD.ACTION = 5 ; CALL SCRN.EDIT
*
***** ENTER UNIT PRICE
*
50*
      ECD.NUM = 14
      SCV.REC(14)<ECD.SCRN.NO,1> = INV.UNIT<1,3>
      ECD.ACTION = 5 ; CALL SCRN.EDIT
      GOSUB 70000
      RETURN
*
***** ENTER IF PAPER
*
55*
      ECD.NUM = 15
      SCV.REC(15)<ECD.SCRN.NO,1> = INV.PAP.TYPE
      ECD.ACTION=5;CALL SCRN.EDIT
      RETURN
*
**** ENTER BASIS WEIGHT
*
80*
      IF QTY.OH <= 0 THEN
         ECD.NUM = 20
         ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE # 'END' THEN
            IF NUM(ECD.RET.VALUE) THEN
               INV.BAS.WT = ICONV(ECD.RET.VALUE,"MD2")
            END ELSE
               INV.BAS.WT = ECD.RET.VALUE
            END
         END
      END ELSE
         ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
         GOSUB 91000
      END
      RETURN
*
**** ENTER COSTING WEIGHT
*
85*
      IF QTY.OH <= 0 THEN
         ECD.NUM = 21
         ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE # 'END' THEN
            INV.COST.WT = ECD.RET.VALUE
         END
      END ELSE
         ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
         GOSUB 91000
      END
      RETURN
*
**** ENTER M/WEIGHT
*
90*
      ECD.NUM = 22
      IF INV.PAP.TYPE = 'SHEET' THEN
         IF QTY.OH <= 0 THEN
            ECD.ACTION=4;CALL SCRN.EDIT
            IF ECD.RET.VALUE # 'END' THEN
               INV.M.WT = ECD.RET.VALUE
            END
         END ELSE
            ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
            GOSUB 91000
         END
      END ELSE
         INV.M.WT = 0
         SCV.REC(ECD.NUM)<1> = INV.M.WT; ECD.ACTION=5;CALL SCRN.EDIT
      END
      RETURN
*
**** ENTER STOCK BUY RATIO
*
95*
      INV.SBR = 1
      ECD.NUM = 23; SCV.REC(ECD.NUM)<1> = INV.SBR; ECD.ACTION=5;CALL SCRN.EDIT
      RETURN
*
**** ENTER IF TAXABLE
*
110*
      ECD.NUM = 26
      ECD.DEFAULT = "N"
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.TAXABLE = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER SUGG ORD QTY
*
120*
      ECD.NUM = 28
      ECD.SCALER = ICR.CNV[3,1]
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.SOQ = ECD.RET.VALUE
      END
      RETURN
*
***** ENTER PAPER COLOR
*
130*
      ECD.NUM = 33
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.COLOR = ECD.RET.VALUE
         MATREAD COLOR.REC FROM COLOR, CONO:INV.COLOR ELSE
            ERRMSG = "INVALID COLOR CODE";GOSUB 91000
            GOTO 130
         END
      END
      RETURN
*
***** ENTER PAPER FINISH
*
135*
      ECD.NUM = 34
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.PAP.FINSH = ECD.RET.VALUE
         MATREAD FINISH.REC FROM FINISH, CONO:INV.PAP.FINSH ELSE
            ERRMSG = 'INVALID FINISH CODE'
            GOSUB 91000;GOTO 135
         END
      END
      RETURN
*
**** ENTER PAPER LENGTH
*
140*
      ECD.NUM = 35
      IF CATG.TYPE = "S" THEN
         ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE # 'END' THEN
            INV.PAP.LEN = ECD.RET.VALUE
         END
      END ELSE
         INV.PAP.LEN = 0
         SCV.REC(35)<ECD.SCRN.NO,1> = INV.PAP.LEN
         ECD.ACTION = 5 ; CALL SCRN.EDIT
      END
      RETURN
*
**** ENTER PAPER WIDTH
*
145*
      ECD.NUM = 36
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
* 12/04/97  I think all paper types should have a positive width (??)
*        IF INV.PAP.TYPE = 'LROLL' OR INV.PAP.TYPE = 'PCOAT' THEN
           IF ECD.RET.VALUE+0 <= 0 THEN
             ERRMSG='This Paper Type must have a positive Width!'
             GOSUB 91000
             GOTO 145
           END
*        END
         INV.PAP.WIDTH = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER PAPER BRIGHTNESS
*
150   ECD.NUM = 40
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.BRIGHT = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER PAPER SHADE
*
155   ECD.NUM = 41
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.SHADE = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER PAPER PAGES PER INCH
*
160   ECD.NUM = 42
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.PPI = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER GRAIN DIRECTION
*
170   ECD.NUM = 43
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.GRAIN = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER CUSTOMER NUMBER
*
180*
      GOSUB 80000 ;*CSF9259
      IF QTY.FLAG THEN QTY.FLAG = '';GOTO 189
      ECD.NUM=18; ECD.ACTION=4; CALL SCRN.EDIT
      BEGIN CASE
         CASE ECD.RET.VALUE = "END"
            GOTO 189
         CASE ECD.RET.VALUE = ""
            ECD.NUM=19; ECD.ACTION=4; CALL SCRN.EDIT
            IF ECD.RET.VALUE="" OR ECD.RET.VALUE="END" THEN INV.CUST = "";GOTO 189
            GXR.NAME = "CUSTOMER"
            GXR.XREF = CUSTOMER.XREF
            GXR.FILE=CUSTOMER
            GXR.SRCH.ID=ECD.RET.VALUE
            CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
            ECD.ACTION=2;CALL SCRN.EDIT
            IF GXR.ID='' THEN
               SCV.REC(18)<1> = ""
               ECD.ACTION=3; CALL SCRN.EDIT
               GOTO 180
            END ELSE
               MATREAD CUST.REC FROM CUSTOMER,CONO:GXR.ID ELSE
                  ERRMSG = "CUSTOMER ":GXR.ID:" IS MISSING"
                  GOSUB 91000
                  GOTO 180
               END
               SCV.REC(18)<1>=GXR.ID
               SCV.REC(19)<1>=CUST.NAME
               ECD.ACTION=3; CALL SCRN.EDIT
               INV.CUST=GXR.ID
            END
         CASE 1
            MATREAD CUST.REC FROM CUSTOMER,CONO:ECD.RET.VALUE ELSE
               ERRMSG = "CUSTOMER ":ECD.RET.VALUE:" IS MISSING"
               GOSUB 91000
               GOTO 180
            END
            SCV.REC(18)<1> = ECD.RET.VALUE
            SCV.REC(19)<1> = CUST.NAME
            ECD.ACTION = 3; CALL SCRN.EDIT
            INV.CUST=ECD.RET.VALUE
      END CASE
189*
      RETURN
*
**** ENTER FACTOR
*
190*
      BEGIN CASE
         CASE INV.PAP.TYPE = "ROLL"
            ECD.NUM = 44
            ECD.ACTION=4;CALL SCRN.EDIT
            IF ECD.RET.VALUE # 'END' THEN
               IF ECD.RET.VALUE = "" THEN
                  INV.FACTOR = ""
               END ELSE
                  INV.FACTOR = FIELD(ECD.RET.VALUE,".",1)*100000+FIELD(ECD.RET.VALUE,".",2)
               END
            END
         CASE INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
            ECD.PATRN = '0N,0N"."1N,0N"."2N'
            ECD.MINV = 0.10
            ECD.MAXV = 99.99
            ECD.HMSG = "Enter caliper in MILS"
            ECD.NUM = 44
            ECD.ACTION=4;CALL SCRN.EDIT
            IF ECD.RET.VALUE # 'END' THEN
               IF ECD.RET.VALUE = "" THEN
                  INV.FACTOR = ""
               END ELSE
                  INV.FACTOR = ECD.RET.VALUE * 100
                  SCV.REC(44)<ECD.SCRN.NO> = OCONV(INV.FACTOR,"MD2")
                  ECD.NUM=44; ECD.ACTION=5; CALL SCRN.EDIT
               END
            END
      END CASE
      RETURN
*
**** ENTER AVG SIZE
*
200 *
      BEGIN CASE
         CASE INV.PAP.TYPE = "ROLL"
         CASE INV.PAP.TYPE = "LROLL"
         CASE INV.PAP.TYPE = "PCOAT"
         CASE 1
            RETURN
      END CASE
      ECD.NUM = 45
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.AVG.DIA = ECD.RET.VALUE
      END
      RETURN
*
**** ENTER CORE SIZE
*
210 *
      BEGIN CASE
         CASE INV.PAP.TYPE = "ROLL"
         CASE INV.PAP.TYPE = "LROLL"
         CASE INV.PAP.TYPE = "PCOAT"
         CASE 1
            RETURN
      END CASE
      ECD.NUM = 46
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
         INV.CORE.DIA = ECD.RET.VALUE
      END
      RETURN
*
***** GET ALL VALUES FROM FILE
*
1000*
      SCV.REC(5)<ECD.SCRN.NO> = PROD.NUM
      SCV.REC(6)<ECD.SCRN.NO> = INV.SPEC
      SCV.REC(7)<ECD.SCRN.NO> = INV.LINE
      MATREAD CATG.REC FROM CATEGORY , CONO:INV.LINE ELSE
         MAT CATG.REC = ''
      END
      SCV.REC(8)<ECD.SCRN.NO> = INV.M.LINE
      SCV.REC(11)<ECD.SCRN.NO> = INV.DESC
      SCV.REC(12)<ECD.SCRN.NO> = INV.UNIT<1,1>
      SCV.REC(13)<ECD.SCRN.NO> = INV.UNIT<1,2>
      SCV.REC(14)<ECD.SCRN.NO> = INV.UNIT<1,3>
      SCV.REC(15)<ECD.SCRN.NO,1> = INV.PAP.TYPE
      SCV.REC(18)<ECD.SCRN.NO> = INV.CUST
      IF INV.CUST # "" THEN
         MATREAD CUST.REC FROM CUSTOMER,CONO:INV.CUST ELSE
            CUST.NAME = "??????????????????????????????"
         END
         SCV.REC(19)<ECD.SCRN.NO> = CUST.NAME
      END
      SCV.REC(20)<ECD.SCRN.NO> = OCONV(INV.BAS.WT,"MD2")
      SCV.REC(21)<ECD.SCRN.NO> = INV.COST.WT
      SCV.REC(22)<ECD.SCRN.NO> = INV.M.WT
      SCV.REC(23)<ECD.SCRN.NO> = INV.SBR
      SCV.REC(26)<ECD.SCRN.NO> = INV.TAXABLE
      SCV.REC(28)<ECD.SCRN.NO> = OCONV(INV.SOQ,ICR.CNV)
      SCV.REC(33)<ECD.SCRN.NO> = INV.COLOR
      SCV.REC(34)<ECD.SCRN.NO> = INV.PAP.FINSH
      SCV.REC(35)<ECD.SCRN.NO> = INV.PAP.LEN
      SCV.REC(36)<ECD.SCRN.NO> = INV.PAP.WIDTH
      SCV.REC(40)<ECD.SCRN.NO> = INV.BRIGHT
      SCV.REC(41)<ECD.SCRN.NO> = INV.SHADE
      SCV.REC(42)<ECD.SCRN.NO> = INV.PPI
      SCV.REC(43)<ECD.SCRN.NO> = INV.GRAIN
      BEGIN CASE
         CASE INV.FACTOR = ""
            SCV.REC(44)<ECD.SCRN.NO> = ""
         CASE INV.PAP.TYPE = "ROLL"
            FLEN = LEN(INV.FACTOR)
            IF FLEN > 5 THEN
               SCV.REC(44)<ECD.SCRN.NO> = INV.FACTOR[1,FLEN-5]:".":INV.FACTOR[FLEN-4,5]
            END ELSE
               SCV.REC(44)<ECD.SCRN.NO> = "0.":STR("0",5-FLEN):INV.FACTOR
            END
         CASE INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
            SCV.REC(44)<ECD.SCRN.NO> = OCONV(INV.FACTOR,"MD2")
      END CASE
      SCV.REC(45)<ECD.SCRN.NO> = INV.AVG.DIA
      SCV.REC(46)<ECD.SCRN.NO> = INV.CORE.DIA
      RETURN
*
***** ENTER OPTIONS
*
60000*
      MORE = 1
      LOOP
         ECD.NUM = 38
         SCV.REC(ECD.NUM)<1> = ''
         ECD.ACTION=4;CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
            CASE OPTION = 'END' OR OPTION = 'E'
               MORE = 1
            CASE NUM(OPTION)
               ON OPTION GOSUB 35,180,10,15,40,95,55,80,85,90,110,120,130,135,140,145,150,155,160,170,190,200,210
               MORE = 0
            CASE OPTION = 'V'
               ECD.SCRN.NO = 4
               ECD.ACTION = 2; CALL SCRN.EDIT
               CALL VEND.PRD.MAINT(STATUS,CONO,PROD.NUM)
               ECD.SCRN.NO = 2
               ECD.ACTION=2;CALL SCRN.EDIT
               ECD.ACTION=3;CALL SCRN.EDIT
               MORE = 0
            CASE OPTION = 'W'
               CALL INV.FULL.DESC.SUB(CONO,CONTROL)
               MATWRITE INV.REC ON INVENTORY, CONO:PROD.NUM
               GXR.CO = CONO
               CALL GEN.XREF.MAINT(GXR.CO,PROD.NUM,OLD.NAME,INV.FULL.DESC,INVENTORY.XREF,PREFIX)
               NEW.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
               NEW.REC = PROD.NUM : AM : INV.COLOR : AM : INV.PAP.LEN : AM : INV.PAP.WIDTH
               CALL MULTI.XREF.MAINT(NEW.REC,OLD.NAME.XREF,NEW.NAME.XREF,PRODUCT.XREF)
               CALL SLIT.WHSE.MAINT(PROD.NUM,CONO,WHSE.KEY,MAT IWH.REC)
               ECD.SCRN.NO = 2
               ECD.ACTION=2;CALL SCRN.EDIT
               ECD.ACTION=3;CALL SCRN.EDIT
               MORE = 0
            CASE OPTION = 'F'
               IF INV.PAP.TYPE='ROLL' OR INV.PAP.TYPE='LROLL' OR INV.PAP.TYPE='PCOAT' THEN
                  IF SAVE.INV.REC(1) = INV.REC(1) THEN
                     PMSG='WARNING!!! DESCRIPTION HAS NOT BEEN CHANGED, OKAY TO FILE(Y/N)?'
                     X=0; Y=23; TYP=8; O.R='R'
                     CALL EDIT.SUB
                     IF VALUE = 'N' THEN GOTO 60000
                  END
                  IF SAVE.INV.REC(33) = INV.REC(33) THEN
                     PMSG='WARNING!!! WIDTH HAS NOT BEEN CHANGED, OKAY TO FILE(Y/N)?'
                     X=0; Y=23; TYP=8; O.R='R'
                     CALL EDIT.SUB
                     IF VALUE = 'N' THEN GOTO 60000
                  END
               END ELSE
                  IF SAVE.INV.REC(1) = INV.REC(1) THEN
                     PMSG='WARNING!!! DESCRIPTION HAS NOT BEEN CHANGED, OKAY TO FILE(Y/N)?'
                     X=0; Y=23; TYP=8; O.R='R'
                     CALL EDIT.SUB
                     IF VALUE = 'N' THEN GOTO 60000
                  END
                  IF SAVE.INV.REC(33) = INV.REC(33) THEN
                     PMSG='WARNING!!! WIDTH HAS NOT BEEN CHANGED, OKAY TO FILE(Y/N)?'
                     X=0; Y=23; TYP=8; O.R='R'
                     CALL EDIT.SUB
                     IF VALUE = 'N' THEN GOTO 60000
                  END
                  IF SAVE.INV.REC(23) = INV.REC(23) THEN
                     PMSG='WARNING!!! M WEIGHT HAS NOT BEEN CHANGED, OKAY TO FILE(Y/N)?'
                     X=0; Y=23; TYP=8; O.R='R'
                     CALL EDIT.SUB
                     IF VALUE = 'N' THEN GOTO 60000
                  END
               END
               CALL INV.FULL.DESC.SUB(CONO,CONTROL)
               READ DUMMY FROM INV.WHSE, CONO:PROD.NUM:'!':WHSE.KEY ELSE WNOF = 1
               IF WNOF = 1 THEN INV.WHSE.CODE = ''
               MATWRITE INV.REC ON INVENTORY, CONO:PROD.NUM
               GXR.CO = CONO
               CALL GEN.XREF.MAINT(GXR.CO,PROD.NUM,OLD.NAME,INV.FULL.DESC,INVENTORY.XREF,PREFIX)
               NEW.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
               NEW.REC = PROD.NUM : AM : INV.COLOR : AM : INV.PAP.LEN : AM : INV.PAP.WIDTH
               CALL MULTI.XREF.MAINT(NEW.REC,OLD.NAME.XREF,NEW.NAME.XREF,PRODUCT.XREF)
               MORE = 1
         END CASE
      WHILE MORE = 0 DO REPEAT
      RETURN
*
*---- CONVERT
*
70000*
      MAT INV.CNV.REC = ''
      BEGIN CASE
         CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
            ICR.CNV = "MD0"
            ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
         CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
            ICR.CNV = "MD0"
            ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
         CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
            ICR.CNV = "MD0"
            ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
         CASE 1
            ICR.CNV = "MD2"
            ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
      END CASE
      RETURN
*
*** CHECK FOR QTY'S
*
80000 QTY.FLAG = ''  ;*CSF9259
      WHSE.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
      IF WHSE.CNT = 0 THEN RETURN
      FOR A = WHSE.CNT TO 1 STEP -1
*        MATREAD IWH.REC FROM INV.WHSE, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE
*           MAT IWH.REC = ""
*        END
         MATREAD INV.STAT.REC FROM INV.STATS, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE MAT INV.STAT.REC = ""
         PO.QTY = SUM(ISTAT.PO.QTY)
         IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND ISTAT.JOB = "" THEN
            LOC.CNT = COUNT(IWH.LOC,VM) + (IWH.LOC # "")
            FOR DL = LOC.CNT TO 1 STEP -1
               MATREAD IWLO.REC FROM INV.WHSE.LOC,CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL> ELSE MAT IWLO.REC = ""
               IF IWLO.LOC.ON.HAND # "" AND IWLO.LOC.ON.HAND * 0 # 0 THEN QTY.FLAG = 1
            NEXT DL
         END ELSE
            QTY.FLAG = 1
         END
      NEXT A
      IF QTY.FLAG THEN
         ERRMSG = "QTY OR JOB(S) ATTACHED TO THIS ITEM, MAY NOT CHANGE THIS FIELD!!"
         GOSUB 91000
      END
      RETURN
*
***** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 ERR.TYPE = 2
      CALL SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 ERR.TYPE = 3
      CALL SYSCOM(MAT SYSCOM.REC)
99999 *
      ECD.ACTION=99;CALL SCRN.EDIT
      RETURN
   END
