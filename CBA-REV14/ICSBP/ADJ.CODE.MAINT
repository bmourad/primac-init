*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - ADJ.CODE.MAINT
* BY          - ZIAD YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE        - 04/07/84
* DESCRIPTION
* This program create and maintain adjustments codes.
*T26126 adelgado 03/01/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*********************************************************************
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INV.ADJ.CODES
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
  MAT FILE.VARS = ''
  OPEN '', 'INV.ADJ.CODES' TO INV.ADJ.CODES ELSE
    ERRMSG = 'INV.ADJ.CODES FILE IS MISSING'
    GOTO 93000
  END
  OPEN '', 'ICS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'ICS.SCREENS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOTO 93000
  END
*
**** GET COMPANY
*
  MAT COMP.REC = ''
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
* INITIALIZATION
*
  MAT EDIT.COM = ''
  MAT EDIT.COM.DRIVER = ''
  DASHES = STR('-',80)
  ERRMSG = ''
*
* MAIN PROCESSING
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'ADJ.CODE.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  ECD.ACTION=2;CALL SCRN.EDIT
100 MAT IADJ.REC = ''
  RELEASE            ;* T26126
  MAT SCV.REC = ""
  ECD.ACTION=6;CALL SCRN.EDIT
  ECD.NUM = 5
  ECD.VALDAT.CODE = '4'
  ECD.VALDAT.FILE = INV.ADJ.CODES
  ECD.PREFIX.ID = CONO
  ECD.ACTION=4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
      IADJ.ID = ECD.RET.VALUE
      FOR REQUEST = 1 TO 2
        ON REQUEST GOSUB 1000,2000
        IF ECD.RET.VALUE = 'END' THEN GOTO 100
      NEXT REQUEST
    CASE ECD.VALDAT.ITEM # ''
      * T26126 v
      READU DUMMY FROM INV.ADJ.CODES, CONO:ECD.RET.VALUE LOCKED
        ERRMSG = 'ADJUSTMENT record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 100 
      END ELSE NULL
      * T26126 ^
      FOR I = 1 TO IADJ.REC.SIZE
        IADJ.REC(I) = ECD.VALDAT.ITEM<I>
      NEXT I
      IADJ.ID = ECD.RET.VALUE
      GOSUB 10000
      ECD.ACTION=3;CALL SCRN.EDIT
    CASE 1
      ERRMSG = '* * INVALID INV.ADJ.CODES # * *'
      GOSUB 91000
      GOTO 100
  END CASE
500 ECD.NUM = 9
  SCV.REC(ECD.NUM)<1> = ''
  ECD.ACTION=4;CALL SCRN.EDIT
  REQUEST = ECD.RET.VALUE
  BEGIN CASE
    CASE REQUEST = 'END' OR REQUEST = 'E'
      GOTO 100
    CASE NUM(REQUEST) AND REQUEST > 0
      ON REQUEST GOSUB 1000,2000
    CASE REQUEST = 'F'
      MATWRITE IADJ.REC ON INV.ADJ.CODES, CONO : IADJ.ID
      GOTO 100
    CASE REQUEST = 'D'
      DELETE INV.ADJ.CODES, CONO : IADJ.ID
      GOTO 100
  END CASE
  GOTO 500
1000 ECD.NUM = 6
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN IADJ.DESC = ECD.RET.VALUE
  RETURN
2000 ECD.NUM = 7
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    IF ECD.RET.VALUE = 'R' AND IADJ.ID # 'PHS' THEN
      ERRMSG = 'TYPE (R) CAN ONLY BE USED FOR (PHS) CODE'
      GOSUB 91000; GOTO 2000
    END
    IADJ.TYPE = ECD.RET.VALUE
  END
  RETURN
10000 SCV.REC(5)<1> = IADJ.ID
  SCV.REC(6)<1> = IADJ.DESC
  SCV.REC(7)<1> = IADJ.TYPE
  RETURN
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
