*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.COST.ADJ.RPT
* DATE        - 05/03/85
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DESCRIPTION - This program report all inventory cost adjustment
*              activities.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T26556 adelgado 04/22/2002 * Add Division selection to report heading.
*T25740 epitka 04/30/2002 * REV12
*ENDDOC
*********************************************************************
*
*COPY>ICS.CPYLIB>INV.COST.ADJ
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
*
DEFFUN CALC.STK.QTY(COST.QTY,MAT INV.CNV.REC,ROND,LN)
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
OPEN '','INV.COST.ADJ' TO INV.COST.ADJ ELSE
  ERRMSG = 'INV.COST.ADJ FILE IS MISSING'
  GOTO 93000
END
OPEN '','INVENTORY' TO INVENTORY ELSE
  ERRMSG = 'INVENTORY FILE IS MISSING'
  GOTO 93000
END
*
PROCREAD ID ELSE
  ERRMSG = ' CANNOT PERFORM PROCREAD'
  GOTO 93000
END
CONO = ID<1>
REPORT.NUMBER = ID<2>
CONO.NAME = ""
REPORT.NAME = ""
REPORT.DATE = DATE()
HD1 = "" ; HD2 = ""
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
F.DATE = OCONV(ID<5>,"D2/")
T.DATE = OCONV(ID<6>,"D2/")
S.PERIOD=ID<7>
DIV.ID = ID<8>    ;* T26556
LINE.COUNT = 0
PAGE.NO = 0
PREV.PROD = ""
PREV.JOB = ""
NEW.RECORD = 0
SP1=' '
;*
;* define heading lines
;*
HLINE1='PRODUCT NUMBER   RECEIPT  U/M WHSE PERIOD ADJ.QTY     REC COST   JOB    RSV QTY ORSV COST USD QTY  TRANS  TRN QTY OLD COST'
HLINE2='DESCRIPTION                                           ADJ COST                  NRSV COST                         NEW COST'
HLINE3='--------------- --------- --- ---- ------ ----------- -------- -------- ------- --------- ------- ------- ------- --------'
*
PRINTER ON
100*
FIRST.TIME=1
DONE=0
LOOP
  READNEXT KEY ELSE DONE=1
UNTIL DONE DO
  IF FIRST.TIME THEN
    FIRST.TIME=0
    GOSUB PRINT.HEADING
  END
  MATREAD ICA.REC FROM INV.COST.ADJ,KEY THEN
    GOSUB GET.FIELDS
    GOSUB PROCESS.RECORD
  END
REPEAT
GOTO 999999
*
************************************************************************
**** S U B R O U T I N E S *********************************************
************************************************************************
*
************
GET.FIELDS: 
************
*
NEW.RECORD = 1
PROD = ICA.PROD
WHSE = ICA.WHSE
QUANTITY = ICA.QTY
REC.COST = OCONV(ICA.REC.COST,"MD4")
ADJ.COST = OCONV(ICA.ADJ.COST,"MD4")
JOBS = ICA.JOB
RECP=ICA.RECP.NO
PERIOD=ICA.PERIOD
PROD.KEY = CONO:PROD
MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
  MAT INV.REC = ''
END
DESC=INV.FULL.DESC
RETURN
*
**********************
PROCESS.RECORD: 
**********************
*
GOSUB GET.INV.UM.CNV
U.M = INV.UNIT<1,2>
IF QUANTITY >= 0 THEN ROND = '.5' ELSE ROND = '-.5'
QTY=CALC.STK.QTY(QUANTITY,MAT INV.CNV.REC,ROND,'')
IF QTY = "0" OR QTY = "0.00" THEN QTY = ""
J.CNT = 0
J.CNT=DCOUNT(JOBS,VM)
FOR J = 1 TO J.CNT
  JOB = JOBS<1,J>
  OR.COST = OCONV(ICA.ORSV.COST<1,J>,"MD2")
  NR.COST = OCONV(ICA.NRSV.COST<1,J>,"MD2")
  R.QTY=CALC.STK.QTY(ICA.RSV.QTY<1,J>,MAT INV.CNV.REC,'','')
  R.QTY=OCONV(R.QTY,ICR.CNV)
  U.QTY=CALC.STK.QTY(ICA.USD.QTY<1,J>,MAT INV.CNV.REC,'','')
  U.QTY=OCONV(U.QTY,ICR.CNV)
  IF R.QTY+0=0 THEN R.QTY=''
  IF U.QTY+0=0 THEN U.QTY=''
  T.CNT = 0
  T.CNT=DCOUNT(ICA.TRAN<1,J>,SVM)
  FOR T = 1 TO T.CNT
    TRANS = ICA.TRAN<1,J,T>
    O.COST = OCONV(ICA.OLD.COST<1,J,T>,"MD2")
    N.COST = OCONV(ICA.NEW.COST<1,J,T>,"MD2")
    T.QTY=CALC.STK.QTY(ICA.TRAN.QTY<1,J,T>,MAT INV.CNV.REC,'','')
    T.QTY=OCONV(T.QTY,ICR.CNV)
    IF T.QTY+0=0 THEN T.QTY=''
    GOSUB 2000
    PREV.PROD = PROD
    PREV.JOB = JOB
  NEXT T
  IF T.CNT = 0 THEN
    TRANS = "";O.COST = "";N.COST = "";T.QTY = ""
    GOSUB 2000
    PREV.PROD = PROD
    PREV.JOB = JOB
  END
NEXT J
IF J.CNT = 0 THEN
  JOB = "";R.QTY = "";OR.COST = "";NR.COST = "";U.QTY = ""
  TRANS = "";O.COST = "";N.COST = "";T.QTY = ""
  GOSUB 2000
  PREV.PROD = PROD
  PREV.JOB = JOB
END
RETURN
*
**********
2000: 
**********
*
IF LINE.COUNT >= 55 THEN GOSUB PRINT.HEADING
LINE.COUNT +=2
JLINE1=JOB"L#8":SP1:R.QTY"R#7":SP1:OR.COST"R#9":SP1:U.QTY"R#7"
JLINE2=NR.COST"R#9"
*
TLINE1=TRANS"L#7":SP1:T.QTY"R#7":SP1:O.COST"R#8"
TLINE2=N.COST"R#8"
*
IF NEW.RECORD = 1 THEN
  NEW.RECORD = 0
  PLINE1=PROD"L#15":SP1:RECP"L#9":SP1:U.M"L#3":SP1:WHSE"L#4"
QTY = OCONV(QTY,ICR.CNV)
  PLINE1:=SP1:PERIOD"L#6":SP1:QTY"R#11":SP1:REC.COST"R#8"
  PLINE2=DESC"L#45":SPACE(9):ADJ.COST"R#8"
  DLINE1=PLINE1:SP1:JLINE1:SP1:TLINE1
  DLINE2=PLINE2:SPACE(18):JLINE2:SPACE(25):TLINE2
END ELSE
  IF PREV.JOB # JOB THEN
    DLINE1=SPACE(63):JLINE1:SP1:TLINE1
    DLINE2=SPACE(80):JLINE2:SPACE(25):TLINE2
  END ELSE
    DLINE1=SPACE(98):TLINE1
    DLINE2=SPACE(114):TLINE2
  END
END
PRINT DLINE1
PRINT DLINE2
RETURN
*
**************
GET.INV.UM.CNV: 
**************
*
*COPY>ICSBP>INV.UM.CNV
RETURN
*
***************
PRINT.HEADING: 
***************
*
PRINT CHAR(12)
PAGE.NO+=1
PRINT HD1:PAGE.NO
PRINT HD2
H.LINE = 'DIVISION : ':DIV.ID'L#4':SPACE(41):"FROM "
H.LINE:=F.DATE:" THRU ":T.DATE:" - PERIOD ":S.PERIOD
PRINT H.LINE
PRINT
PRINT HLINE1
PRINT HLINE2
PRINT HLINE3
PRINT
LINE.COUNT = 8
RETURN
*
***   CALLS FOR SYSCOM
*
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
999999*
PRINTER OFF
END
