*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INVENTORY.PRINT
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 01/21/86
* MODIFIED BY - NLB
* DATE        - 10/17/88
* COMMENTS    - ADDED CUST NUM & NAME TO  REPORT
* DESCRIPTION - This program list all inventory items selected.
*T26090 ajibaly 03/21/2002 * CONVERT TO REV12 RPT.SCRN
*********************************************************************
*
*--- INSERT FILE EQUATES
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INV.FNGD
*COPY>ICS.CPYLIB>FNGD.DIE
*COPY>ICS.CPYLIB>FNGD.BOM
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- OPEN FILES
*
OPEN "","INVENTORY" TO INVENTORY ELSE
  ERRMSG = "INVENTORY FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","INV.WHSE" TO INV.WHSE ELSE
  ERRMSG = "INV.WHSE FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","WAREHOUSE" TO WAREHOUSE ELSE
  ERRMSG = "WAREHOUSE FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","INV.FNGD" TO INV.FNGD ELSE
  ERRMSG = "INV.FNGD FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","FNGD.DIE" TO FNGD.DIE ELSE
  ERRMSG = "FNGD.DIE FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","FNGD.BOM" TO FNGD.BOM ELSE
  ERRMSG = "FNGD.BOM FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","VEND" TO VEND ELSE
  ERRMSG = "VEND FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","PRICE.TABLE" TO PRICE.TABLE ELSE
  ERRMSG = "PRICE.TABLE FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
END
OPEN "","CUSTOMER" TO CUSTOMER ELSE
  ERRMSG = "CUSTOMER FILE IS MISSING"; GOSUB 90000; GOTO 99999
END
OPEN "","CATEGORY" TO CATEGORY ELSE
  ERRMSG = "CATEGORY FILE IS MISSING"; GOSUB 90000; GOTO 99999
END
*
*--- READ PROC VALUES
PROCREAD ID ELSE
  ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC." ; GOSUB 90000 ; GOTO 99999
END
*
*--- GET THE FIRST TWO LINES OF HEADING
*
HD1 = "" ; HD2 = ""
*T26090 v
*CONO = ID<1> ; CONO.NAME = ID<2>
*REPORT.NAME = "INVENTORY EDIT LISTING"
CONO = ID<1> ; CONO.NAME = ""
REPORT.NAME = ""
*T26090 ^
REPORT.NUMBER = ID<2>
REPORT.DATE = DATE()
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
*--- INT
*
LINE.COUNT = 99
PAGE.NO = 0
S.HD1 = STR('<',41):" PRODUCT INFORMATION ":STR('>',42)
S.UN1 = STR("_",104)
S.HD2 = "<--------------------------------------- WAREHOUSE INFORMATION ---------------------------------------->"
S.HD3 = "WHSE          DESCRIPTION            STD COST   AVG COST   LIST COST  FOH % MARKUP  MIN-QTY    MAX-QTY  "
S.UN2 = "---- ------------------------------ ---------- ---------- ---------- ------ ------ ---------- ----------"
S.HD4 = "<---------------------------------------- VENDOR INFORMATION ------------------------------------------>"
S.HD5 = "VENDOR #          VENDOR NAME           LT  PKG QTY   PRICE     VENDOR PART # "
S.UN3 = "-------- ------------------------------ --- ------- ---------- ----------------"
S.HD6 = "<----------------------------------- FINISHED GOODS SPECIFICATIONS ------------------------------------>"
S.HD6A = "<----------------------------------- FINISHED GOODS SPECIFICATIONS - CONT'D --------------------------->"
S.HD7 = "PLATE NUM":SPACE(18):"PLATE DESCRIPTION"
S.UN7 = STR('-',10):" ":STR('-',50)
S.HD8 = "DEPT  SPEC INST":SPACE(17):"SPECIAL INSTRUCTIONS"
S.UN8 = STR("-",5):" ":STR('-',10):" ":STR('-',55)
S.HD9 = "INK COLORS":SPACE(10)
S.UN9 = STR('-',10):SPACE(10)
*
*---- MAIN PROCESSING
*
PRINTER ON
DATA = 1
LOOP
  READNEXT KEY ELSE DATA = 0
WHILE DATA = 1 DO
  MATREAD INV.REC FROM INVENTORY, KEY ELSE GOTO 111
  MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
    MAT CATG.REC = ""
  END
  GOSUB 9000
  GOSUB 1000
111 REPEAT
GOTO 99999
*
*--- PRINT INVENTORY INFORMATION
*
1000 *
IF LINE.COUNT + 11 > 55 THEN
  GOSUB 8000
END
PRINT
P.LINE = ""
P.LINE = S.HD1
PRINT P.LINE
PRINT
P.LINE = ""
P.LINE = "PRODUCT # : ":KEY[4,15] "L#15":" ":INV.DESC "L#30"
PRINT P.LINE
IF INV.BASE.SKU # "" THEN
  PRINT "BASE ITEM : ":INV.BASE.SKU"L#15"
END ELSE
  PRINT
END
P.LINE = ""
P.LINE = "SPECIAL   : ":INV.SPEC "L#3":"  "
P.LINE = P.LINE:"MAJOR LINE : ":INV.M.LINE "L#8":"  "
P.LINE = P.LINE:"PROD LINE : ":INV.LINE "L#8":"  "
P.LINE = P.LINE:"COLOR  : ":INV.COLOR "L#8":"  "
IF (INV.PAP.TYPE # "REGULAR" AND INV.PAP.TYPE # "" AND INV.PAP.TYPE # "REG") OR CATG.MAJ.LINE = "FNGD" THEN
  P.LINE = P.LINE :"FINISH : ":INV.PAP.FINSH "L#8"
END ELSE
  P.LINE = P.LINE :"WEIGHT : ":OCONV(INV.WEIGHT, "MD2") "R#8"
END
PRINT P.LINE
*
P.LINE = ""
P.LINE = "UNIT BUY  : ":INV.UNIT<1,1> "L#3":"  "
P.LINE = P.LINE:"S/B RATIO  : ":INV.SBR "L#8":"  "
P.LINE = P.LINE:"PROD TYPE : ":INV.PAP.TYPE "L#8":"  "
P.LINE = P.LINE:"LENGTH : ":OCONV(INV.PAP.LEN, "MD4") "R#8":"  "
IF (INV.PAP.TYPE # "REGULAR" AND INV.PAP.TYPE # "" AND INV.PAP.TYPE # "REG") OR CATG.MAJ.LINE = "FNGD" THEN
  P.LINE = P.LINE :"BRIGHT : ":OCONV(INV.BRIGHT, "MD2") "R#5"
END ELSE
  P.LINE = P.LINE :"VOLUME : ":OCONV(INV.VOLUME, "MD2") "R#8"
END
PRINT P.LINE
*
P.LINE = ""
P.LINE = "UNIT STOCK: ":INV.UNIT<1,2> "L#3":"  "
P.LINE = P.LINE:"BASIS WGHT : ":OCONV(INV.BAS.WT, "MD2") "L#8":"  "
P.LINE = P.LINE:"COSTING WT: ":OCONV(INV.COST.WT, "MD2") "R#8":"  "
P.LINE = P.LINE:"WIDTH  : ":OCONV(INV.PAP.WIDTH, "MD4") "R#8":"  "
IF (INV.PAP.TYPE # "REGULAR" AND INV.PAP.TYPE # "" AND INV.PAP.TYPE # "REG") OR CATG.MAJ.LINE = "FNGD" THEN
  P.LINE = P.LINE :"SHADE  : ":INV.SHADE "L#3"
END 
PRINT P.LINE
*
P.LINE = ""
P.LINE = "UNIT PRICE: ":INV.UNIT<1,3> "L#3":"  "
P.LINE = P.LINE:"M.WEIGHT   : ":INV.M.WT "R#8":"  "
P.LINE = P.LINE:"SUG-ORD-QT: ":OCONV(INV.SOQ, "MD2") "R#8":"  "
IF INV.FACTOR # "" THEN
  P.LINE = P.LINE:"FACTOR : ":OCONV(INV.FACTOR,"MD5")"R#8":"  "
END ELSE
  P.LINE = P.LINE:SPACE(19)
END
IF (INV.PAP.TYPE # "REGULAR" AND INV.PAP.TYPE # "" AND INV.PAP.TYPE # "REG") OR CATG.MAJ.LINE = "FNGD" THEN
  P.LINE = P.LINE:"PPI    : ":INV.PPI "L#3"
END
PRINT P.LINE
*
P.LINE = ""
P.LINE = "TAXABLE   : ":INV.TAXABLE "L#3":"  "
P.LINE = "UNIT SELL : ":INV.UNIT<1,4> "L#3":"  "
*REV12> P.LINE = P.LINE:"YIELD      : ":OCONV(INV.YIELD,"MD4") "R#7":"   ";*<R12
P.LINE = P.LINE:"GRAIN      : ": INV.GRAIN "L#8":"  "
IF INV.AVG.DIA # "" THEN
  P.LINE = P.LINE:"DIAM   : ":OCONV(INV.AVG.DIA,"MD2")"R#8":"  "
END
IF INV.CORE.DIA # "" THEN
  P.LINE = P.LINE:"CORE   : ":OCONV(INV.CORE.DIA,"MD2")"R#8"
END
PRINT P.LINE
*
CUST.NAME = ""
IF INV.CUST # "" THEN
  MATREAD CUST.REC FROM CUSTOMER,CONO:INV.CUST ELSE MAT CUST.REC = ""
END
P.LINE = ""
P.LINE = "TAXABLE   : ":INV.TAXABLE "L#3":"  "
PRINT P.LINE
P.LINE = ""
P.LINE = "CUSTOMER  : ": INV.CUST :" ":CUST.NAME "L#35"
PRINT P.LINE
LINE.COUNT = LINE.COUNT + 13
IF INV.VENDOR # "" THEN
  GOSUB 2000
END
IF INV.WHSE.CODE # "" THEN
  GOSUB 3000
END
IF INV.M.LINE = "FNGD" THEN
  MATREAD IOF.REC FROM INV.FNGD, KEY THEN
    GOSUB 4000
  END
END
RETURN
*
*--- PRINT VENDOR INFORMATION
*
2000 *
IF LINE.COUNT + 5 > 55 THEN
  GOSUB 8000
END
GOSUB 6000
CNT.VEND = COUNT(INV.VENDOR, VM) + (INV.VENDOR # "")
FOR V = 1 TO CNT.VEND
  IF LINE.COUNT > 55 THEN
    GOSUB 8000
    GOSUB 6000
  END
  MATREAD VEND.REC FROM VEND , CONO:INV.VENDOR<1,V> ELSE MAT VEND.REC = ""
  V.LINE = ""
  V.LINE = V.LINE:INV.VENDOR<1,V> "L#8":" "
  V.LINE = V.LINE:VEND.DESC "L#30":" " 
  V.LINE = V.LINE:INV.LEAD.TIME<1,V> "R#3":" "
  V.LINE = V.LINE:INV.PKG.QTY<1,V> "R#7":" "
  V.LINE = V.LINE:OCONV(INV.PRICE<1,V>, "MD4") "R#10":" "
  V.LINE = V.LINE:INV.PART.NO<1,V> "L#16"
  PRINT V.LINE
  LINE.COUNT = LINE.COUNT + 1
NEXT V
RETURN
*
*--- PRINT WAREHOUSE INFORMATION
*
3000 *
IF INV.M.LINE = "FNGD" THEN
  PRODNO = KEY[4,15]
  CALL FNGD.WHSE.RPT(CONO,PRODNO,HD1,HD2,S.HD2,PAGE.NO,LINE.COUNT)
  GOTO 3999
END
IF LINE.COUNT + 5 > 55 THEN
  GOSUB 8000
END
GOSUB 5000
CNT.WHSE = COUNT(INV.WHSE.CODE, VM) + (INV.WHSE.CODE # "")
FOR X = 1 TO CNT.WHSE
  IF LINE.COUNT > 55 THEN
    GOSUB 8000
    GOSUB 5000
  END
  MATREAD IWH.REC FROM INV.WHSE ,KEY:"!":INV.WHSE.CODE<1,X> ELSE MAT IWH.REC = ""
  MATREAD WHSE.REC FROM WAREHOUSE, CONO:INV.WHSE.CODE<1,X> ELSE
    WHS.DESC = STR("?",30)
  END
  W.LINE = ""
  W.LINE = INV.WHSE.CODE<1,X> "L#4":" "
  W.LINE = W.LINE:WHS.DESC "L#30":" "
  W.LINE = W.LINE:OCONV(IWH.STD.COST, "MD4") "R#10":" "
  W.LINE = W.LINE:OCONV(IWH.AVG.COST, "MD4") "R#10":" "
  W.LINE = W.LINE:OCONV(IWH.LIST.COST, "MD4") "R#10":" "
  W.LINE = W.LINE:OCONV(IWH.FOH.PCT, "MD2") "R#6":" "
  W.LINE = W.LINE:OCONV(IWH.MARKUP, "MD2") "R#6":" "
  IF ICR.DV1 + 0 # 0 THEN
    IF IWH.MIN.QTY >= 0 THEN
      IWH.MIN.QTY=INT((((IWH.MIN.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
    END ELSE
      IWH.MIN.QTY=INT((((IWH.MIN.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
    END
    IF IWH.MAX.QTY >= 0 THEN
      IWH.MAX.QTY=INT((((IWH.MAX.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
    END ELSE
      IWH.MAX.QTY=INT((((IWH.MAX.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
    END
  END ELSE
    IWH.MIN.QTY = 0 ; IWH.MAX.QTY = 0
  END
  W.LINE = W.LINE:OCONV(IWH.MIN.QTY, ICR.CNV) "R#10":" "
  W.LINE = W.LINE:OCONV(IWH.MAX.QTY, ICR.CNV) "R#10":" "
  PRINT W.LINE
  LINE.COUNT = LINE.COUNT + 1
NEXT X
3999 *
RETURN
*
*---- PRINT FINISHED GOODS SPECIFICATIONS
*
4000 *
COLOR.CNT = DCOUNT(IOF.INK.COLOR,VM)
PLATE.CNT = DCOUNT(IOF.PLATE,VM)
SPEC.CNT = DCOUNT(IOF.SI.DEPT,VM)
HEAD.CNT = 3
IF IOF.DIE # "" THEN
  HEAD.CNT = HEAD.CNT + 1
END
IF IOF.UPC # "" THEN
  HEAD.CNT = HEAD.CNT + 10
END
IF IOF.BOM # "" THEN
  HEAD.CNT = HEAD.CNT + 1
END
IF (HEAD.CNT + COLOR.CNT + SPEC.CNT + PLATE.CNT) = 3 THEN
  GOTO 4999
END
IF LINE.COUNT + HEAD.CNT > 55 THEN
  GOSUB 8000
END
PRINT
PRINT S.HD6
PRINT
IF IOF.DIE # '' THEN
  MATREAD DIE.REC FROM FNGD.DIE, CONO:IOF.DIE THEN
    PRINT "DIE NUMBER : ":IOF.DIE"L#10":" ":DIE.DESC"L#30"
    LINE.COUNT = LINE.COUNT + 1
  END
END
IF IOF.BOM # '' THEN
  MATREAD BOM.REC FROM FNGD.BOM, CONO:IOF.BOM THEN
    PRINT "BILL/MATLS : ":IOF.BOM"L#10":" ":BOM.DESC"L#30"
    LINE.COUNT = LINE.COUNT + 1
  END
END
IF IOF.UPC # "" THEN
  CALL PRINT.IOF(MAT IOF.REC)
  LINE.COUNT = LINE.COUNT + 10
END
IF COLOR.CNT THEN 
  IF LINE.COUNT + COLOR.CNT > 55 THEN
    GOSUB 8000
    PRINT; PRINT S.HD6A; PRINT
    LINE.COUNT = LINE.COUNT + 3
  END
  PRINT
  PRINT S.HD9
  PRINT S.UN9
  CALL PRINT.IOF.COLOR(IOF.INK.COLOR)
  LINE.COUNT = LINE.COUNT + COLOR.CNT + 3
END
IF PLATE.CNT THEN
  IF LINE.COUNT + PLATE.CNT > 55 THEN
    GOSUB 8000
    PRINT; PRINT S.HD6A; PRINT
    LINE.COUNT = LINE.COUNT + 3
  END
  PRINT
  PRINT S.HD7
  PRINT S.UN7
  CALL PRINT.IOF.PLATE(IOF.PLATE,IOF.PLATE.DESC)              
  LINE.COUNT = LINE.COUNT + PLATE.CNT + 3
END
IF SPEC.CNT THEN
  IF LINE.COUNT + SPEC.CNT > 55 THEN
    GOSUB 8000
    PRINT; PRINT S.HD6A; PRINT
    LINE.COUNT = LINE.COUNT + 3
  END
  PRINT
  PRINT S.HD8
  PRINT S.UN8
  CALL PRINT.IOF.SPEC(IOF.SI.DEPT,IOF.SI.NO,IOF.SI.DESC)
  LINE.COUNT = LINE.COUNT + SPEC.CNT + 3
END
4999 *
RETURN
*
*--- PRINT HEADING FOR WHAREHOUSE
*
5000 *
IF LINE.COUNT + 5 > 55 THEN GOSUB 8000
PRINT
PRINT S.HD2
PRINT
PRINT S.HD3
PRINT S.UN2
LINE.COUNT = LINE.COUNT + 5
RETURN
*
*--- PRINT HEADING FOR VENDOR
*
6000 *
IF LINE.COUNT + 5 > 55 THEN GOSUB 8000
PRINT
PRINT S.HD4
PRINT
PRINT S.HD5
PRINT S.UN3
LINE.COUNT = LINE.COUNT + 5
RETURN
*
*--- PRINT HEADING
*
8000 *
PRINT CHAR(12):
PAGE.NO = PAGE.NO + 1
H.LINE = ""
H.LINE = HD1:PAGE.NO
PRINT H.LINE
H.LINE = ""
H.LINE = HD2
PRINT H.LINE
LINE.COUNT = 2
RETURN
*** CONVERT SHEET/WGHT/QTY
9000 *
*COPY>ICSBP>INV.UM.CNV
RETURN
*
*--- PRINT ERRMSG 
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
* PRINT @(0,23):CL:ERRMSG:
* INPUT REPLY
* PRINT @(0,23):CL
* RETURN
*
*--- END OF REPORT
*
99999 *
PRINTER OFF
PRINTER CLOSE
END
