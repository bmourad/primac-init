  SUBROUTINE SER.TRAN.HIST.INQ(CONO,SERIAL.NUM)
*
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - SER.TRAN.HIST.INQ
* BY          - EDVARD PITKA
* DATE        - 05/04/2001
* DESCRIPTION -
*T25740 edvard 07/19/2001 * 
*T27366 adelgado 04/09/2003 * Allow to review serials found in
*                             INV_SERIAL_DELETED.
*ENDDOC
**********************************************
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.HIST
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>SYSCOM
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
  OPEN '','INV_AUDIT_BAL' TO INV_AUDIT_BAL ELSE ERRMSG = 'INV_AUDIT_BAL FILE IS MISSING'; GOTO 93000
  OPEN '','INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE ERRMSG = 'INV_AUDIT_HIST FILE IS MISSING'; GOTO 93000
  OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE ERRMSG = 'INV_RECEIPTS FILE IS MISSING'; GOTO 93000
  OPEN '','INV_SERIAL' TO INV_SERIAL ELSE ERRMSG = 'INV_SERIAL FILE IS MISSING'; GOTO 93000
  OPEN '','INV_SERIAL_DELETED' TO INV_SERIAL_DELETED ELSE ERRMSG = 'INV_SERIAL_DELETED FILE IS MISSING'; GOTO 93000     ;* T27366
  OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG = 'INVENTORY FILE IS MISSING'; GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE IS MISSING'; GOTO 93000
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'M.SCREENS FILE IS MISSING'; GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE IS MISSING'; GOTO 93000
  OPEN '','DIVISION' TO DIVISION ELSE ERRMSG = 'DIVISION FILE IS MISSING'; GOTO 93000
*
100 ESN=ECD.SCRN.NO
*
  PAGE.SIZE = 5
  START=1
  SYSLIST=''
  CNV = "MD0,"
  LN.CNT=0
  SAVE.SERIAL.NUM=SERIAL.NUM
  GOSUB GET.DATA
  GOSUB BUILD.SCREEN
  ECD.ACTION = 3; CALL SCRN.EDIT
  ;*
  ;* Main Prompt
  ;*
  MORE=1
  LOOP
  WHILE MORE DO
    ECD.NUM=21; ECD.ACTION=4; CALL SCRN.EDIT
    ACTION = ECD.RET.VALUE
    BEGIN CASE
      CASE ACTION = "E" OR ACTION = "END" OR ACTION='' ; * T22398
        MORE = 0
      CASE ACTION[1,1] = "S"
        GOSUB SCROLL
      CASE 1
        ERRMSG = "Invalid input - please re-enter."
        GOSUB 91000
    END CASE
  REPEAT
*
  ECD.ACTION = 99 ; CALL SCRN.EDIT     ;*T21177
  GOTO 99999
*
**************************************************************************
****** S U B R O U T I N E S *********************************************
**************************************************************************
*
************
GET.DATA: 
************
*
  MATREAD ISTK.REC FROM INV_SERIAL, CONO:SERIAL.NUM ELSE
    MATREAD ISTK.REC FROM INV_SERIAL_DELETED, CONO:SERIAL.NUM ELSE   ;* T27366
      ERRMSG = SERIAL.NUM:" is not a valid serial tracking ID."
      GOTO 93000
    END     ;* T27366
  END
  MATREAD INV.REC FROM INVENTORY, CONO:ISTK.PROD ELSE MAT INV.REC=""
  MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE MAT CATG.REC=""
*COPY>ICSBP>INV.UM.CNV
  SEL.WHSE = '';  SEL.LOC = ''
  SEL.MILL = ''
  SEL.QTY = '';    SEL.AMOUNT = ''
  SEL.DATE = '';  SEL.PERIOD = ''
  SEL.RECNO = ''; SEL.OPER.ID = ''
  SEL.DIV = '';   SEL.SOURCE = ''
*
  SEQ.ARR = ISTK.AUDIT.NO            
  ID.CNT = DCOUNT(SEQ.ARR,VM)        
  IF ID.CNT >=1 THEN
    FOR I = 1 TO ID.CNT                
      SEQ.ARR<1,I> = CONO:SEQ.ARR<1,I>
    NEXT I                             
    CONVERT VM TO AM IN SEQ.ARR        
    FORMLIST SEQ.ARR TO 0
    CMD = 'SSELECT INV_AUDIT_HIST BY INAH_DATE BY @ID BY DIV BY INAH_WHSE BY INAH_LOC'
    PERFORM CMD RTNLIST SYSLIST CAPTURING RESPONSE
*
    RESPONSE="" ; LN.CNT=0
    DONE = 0
    LOOP
      READNEXT SEL.ID FROM SYSLIST ELSE DONE = 1
    UNTIL (DONE) DO
      MATREAD INAH.REC FROM INV_AUDIT_HIST, SEL.ID THEN
        LN.CNT += 1
        SEL.SOURCE<1,LN.CNT> = INAH.SRC
        SEL.DATE<1,LN.CNT> = INAH.DATE
        SEL.RECNO<1,LN.CNT> = INAH.TRAN
        SEL.DIV<1,LN.CNT> = INAH.DV.DP.CC[1,2]
        SEL.WHSE<1,LN.CNT> = INAH.WHSE
        SEL.LOC<1,LN.CNT> = INAH.LOC
        SEL.QTY<1,LN.CNT> = INAH.QTY
        SEL.AMOUNT<1,LN.CNT> = INAH.EXT.COST
        SEL.PERIOD<1,LN.CNT> = INAH.PERIOD
        SEL.OPER.ID<1,LN.CNT> = INAH.OPER.ID
        IF CATG.TRK.LVL='S' THEN
* T27366 v
*         MATREAD ISTK.REC FROM INV_SERIAL, CONO:INAH.SERIAL ELSE MAT ISTK.REC = ""
          MATREAD ISTK.REC FROM INV_SERIAL, CONO:INAH.SERIAL ELSE
            MATREAD ISTK.REC FROM INV_SERIAL_DELETED, CONO:INAH.SERIAL ELSE
              MAT ISTK.REC = ''
            END
          END
* T27366 ^
          SEL.MILL<1,LN.CNT> = ISTK.MILL.ID
        END
      END ELSE
        MAT INAH.REC = ""
      END
    REPEAT
  END
  RETURN
*
***************
SCROLL: 
***************
*
  BEGIN CASE
    CASE ACTION = "S" OR ACTION = "SF"
      IF LN.CNT > PAGE.SIZE THEN
        START +=PAGE.SIZE
        IF START > LN.CNT THEN START = 1
      END
    CASE ACTION = "SR"
      IF LN.CNT > PAGE.SIZE THEN
        START -=PAGE.SIZE
        IF START<LN.CNT THEN START = 1
      END
    CASE ACTION = "ST"
      START = 1
    CASE ACTION = "SB"
      START=LN.CNT
    CASE ACTION[1,1] = "S" AND NUM(ACTION[2,99])
      START = ACTION[2,99]+0
      IF START < 1 OR START > LN.CNT THEN
        ERRMSG = "** Invalid selection **"
        GOSUB 91000
      END
  END CASE
*
  START=1+INT((START-1)/PAGE.SIZE)*PAGE.SIZE 
  ECD.NUM=11;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=12;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=13;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=17;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=14;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=15;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=16;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=18;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=19;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=22;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  ECD.NUM=24;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
  RETURN
*
**************
BUILD.SCREEN: 
**************
*
  SCV.REC(2)<ESN> = SERIAL.NUM
  SCV.REC(9)<ESN> = ISTK.PROD
  SCV.REC(10)<ESN> = INV.FULL.DESC
  SCV.REC(20)<ESN> = ISTK.RECP
  SCV.REC(11)<ESN> = SEL.SOURCE
  SCV.REC(12)<ESN> = SEL.DATE  
  SCV.REC(13)<ESN> = SEL.RECNO
  SCV.REC(17)<ESN> = SEL.MILL
  SCV.REC(14)<ESN> = SEL.DIV
  SCV.REC(15)<ESN> = SEL.WHSE
  SCV.REC(16)<ESN> = SEL.LOC
  SCV.REC(19)<ESN> = SEL.AMOUNT
  SCV.REC(22)<ESN> = SEL.PERIOD
  SCV.REC(24)<ESN> = SEL.OPER.ID
* SCV.REC(18)=''
  SCV.REC(18)<ESN>=''
  FOR L = 1 TO LN.CNT
    ROND = 0.5
    IF SEL.QTY<1,L> < 0 THEN ROND = -0.5 ELSE ROND = .5
    SCV.REC(18)<ESN,L>=OCONV(INT(((SEL.QTY<1,L>/ICR.DV1)*ICR.MT1)/ICR.DV2+ROND),ICR.CNV)
  NEXT L
  SCV.REC(25)<ESN>= INV.UNIT<1,2>
  RETURN
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
*
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999 RETURN
END
