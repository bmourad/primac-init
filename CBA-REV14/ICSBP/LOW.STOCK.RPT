*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - ICS
* SOURCE      - ICSBP
* PROGRAM     - LOW.STOCK.RPT
* DATE        - 02/17/86
* BY          - JIHAD YAMOUT , C.B.A
* DESCRIPTION - This program list all inventory item with the
*               quantity available less then minimum stock quantity.
*T21638 doug 02/26/1997 * Add quantity conversion for FNGD
*T26190 ajibaly 03/08/2002 * CONVERT TO REV12 USING BUILD.IWH.FI SUB
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T28981 lross 09/14/2006 * FNGD items on-order & allocate come from
*                          FNGD.STATS.
*********************************************************************
*** INSERT EQUATE FROM CPYLIB
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>FNGD.STATS ;*T28981
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*** INITIALIZATION
   LINE.COUNT = 99 ; PAGE.NO = 0 ; PREV.WHSE = '' ; PREV.M.P.LINE = ""
   TOT.ON.ORDER = 0 ; TOT.AVALBL = 0 ; TOT.RESRV = 0 ; TOT.ALLOC = 0 ; TOT.ON.HAND = 0
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   D.LINE = ''
   D.LINE = SPACE(77):"---------- ---------- ---------- ---------- ----------"
   T.LINE = ""
   T.LINE = "--------------- --------------------------------------------- --- ----------":D.LINE[77,99]
   HD.LINE = ""
   HD.LINE = "PRODUCT NUMBER               FULL DESCRIPTION                 U/M  MIN-QTY  "
   HD.LINE = HD.LINE:"  ON-HAND   RESERVED   ON-ORDER   ALLOCATED  AVAILABLE"
*** SET UP FOR SYSCOM
   SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
*** OPEN FILES
   OPEN '','INV.WHSE' TO INV.WHSE ELSE
      ERRMSG = 'INV.WHSE FILE IS MISSING'
      GOTO 93000
   END
*T28981 v
   OPEN '','FNGD.STATS' TO FNGD.STATS ELSE
      ERRMSG = 'FNGD.STATS FILE IS MISSING'
      GOTO 93000
   END
*T28981 ^
   OPEN '', 'INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'INVENTORY FILE IS MISSING'
      GOTO 93000
   END
*** GET PROC VALUES
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
*T26493 v
*   CONO.NAME = ID<2>
*   REPORT.NAME = "INVENTORY.LOW.STOCK.REPORT"
*   REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = ID<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26493 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
*T26190 v
   OPEN.FLAG=1
   ERR.FLAG = ''
   PERIOD = ID<8>
   SEL.PERIOD=ID<8>
   IF PERIOD = 'ALL' THEN PERIOD = ''
*T26190 ^
*** GET HEADING
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*** PRINTER ON
   PRINTER ON
*** READ AND PROCESS FIRST RECORD
100*
   READNEXT KEY ELSE GOTO 999999
   GOSUB 200
   MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
      MAT IWH.REC = ''
      GOTO 100
   END
   MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
      MAT INV.REC = ''
   END
   CALL BUILD.IWH.FI(KEY,MAT IWH.REC,PERIOD,ERR.FLAG,ERRMSG,OPEN.FLAG) ; *T26190
   GOSUB 6000
*** SET THE WHSE PRODUCT
   PREV.WHSE = WHSE
   PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
*** PROCESS THE FILE
   GOSUB 2000
*** PRINT THE RECORD
   GOSUB 3000
*** READ AND PROCESS ALL THE FILES
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      GOSUB 200
      MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
         MAT IWH.REC = ''
         GOTO 111
      END
      MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
         MAT INV.REC = ''
      END
      CALL BUILD.IWH.FI(KEY,MAT IWH.REC,PERIOD,ERR.FLAG,ERRMSG,OPEN.FLAG) ; *T26190
      GOSUB 6000
*** PROCESS THEN FILE
      GOSUB 2000
*** PRINT THE RECORD
      GOSUB 3000
111*
   REPEAT
*** PRINT TOTALS FOR THE LASE WHSE
   GOSUB 4000
*** END OF JOB
   PRINTER OFF
   GOTO 999999
*** SEPERATE THE KEY TO PRPDUCT , WHSE
200*
   PROD.KEY = FIELD(KEY,"!",1)
   PROD = PROD.KEY[4,99]
   WHSE    = FIELD(KEY,"!",2)
   RETURN
*** PRINT HEADINGS
1000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT "WAREHOUSE : ":PREV.WHSE "L#8"
   PRINT "MLINE / PLINE : ":PREV.M.P.LINE
   PRINT
   PRINT HD.LINE
   PRINT T.LINE
   LINE.COUNT = 8
   RETURN
*** PROCESS THE RECORD
2000*
   ON.HAND = 0 ; ON.ORDER = 0 ; RESERVE = 0 ; ALLOC = 0 ; MIN.QTY = 0
   BEGIN CASE
      CASE WHSE # PREV.WHSE
         GOSUB 4000
         PREV.WHSE = WHSE
         PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
         GOSUB 1000
      CASE INV.M.LINE:" / ":INV.LINE # PREV.M.P.LINE
         GOSUB 4000
         PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
         LINE.COUNT = 99
   END CASE
   FIFO.CNT = COUNT(IWH.COST.FI,VM) + (IWH.COST.FI # "")
   LA.COST = IWH.COST.FI<1,FIFO.CNT>
   IF ICR.DV1 + 0 # 0 AND ICR.DV2 + 0 # 0 THEN
      IF IWH.MIN.QTY >= 0 AND ICR.DV1 + 0 # 0 AND ICR.DV2 + 0 # 0 THEN
         MIN.QTY=INT((((IWH.MIN.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
      END ELSE
         MIN.QTY=INT((((IWH.MIN.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
      END
      IF IWH.ON.HAND >= 0 AND ICR.DV1 + 0 # 0 AND ICR.DV2 + 0 # 0 THEN
         ON.HAND=INT((((IWH.ON.HAND / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
      END ELSE
         ON.HAND=INT((((IWH.ON.HAND / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
      END
      IF IWH.RESV >= 0 AND ICR.DV1 + 0 # 0 AND ICR.DV2 + 0 # 0 THEN
         RESERVE=INT((((IWH.RESV / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
      END ELSE
         RESERVE=INT((((IWH.RESV / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
      END
*T28981 v
      IF INV.M.LINE = 'FNGD' THEN
        MATREAD FGS.REC FROM FNGD.STATS,KEY ELSE MAT FGS.REC = ''
        IWH.ON.ORDER = SUM(FGS.M.QTY)
        IWH.ALLOC = SUM(FGS.A.QTY)
      END
*T28981 ^
      IF IWH.ON.ORDER >= 0 AND ICR.DV1 + 0 # 0 AND ICR.DV2 + 0 # 0 THEN
         ON.ORDER = INT((((IWH.ON.ORDER / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
      END ELSE
         ON.ORDER = INT((((IWH.ON.ORDER / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
      END
      IF IWH.ALLOC >= 0 AND ICR.DV1 + 0 # 0 AND ICR.DV2 + 0 # 0 THEN
         ALLOC = INT((((IWH.ALLOC / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
      END ELSE
         ALLOC = INT((((IWH.ALLOC / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
      END
   END ELSE
      ON.HAND = 0 ; ON.ORDER = 0 ; ALLOC = 0 ; RESERVE = 0 ; MIN.QTY = 0
   END
* AVLB = (ON.HAND + ON.ORDER) - (RESERVE + ALLOC)
   AVLB = ON.HAND - RESERVE
   TOT.ON.HAND = TOT.ON.HAND + ON.HAND
   TOT.ON.ORDER = TOT.ON.ORDER + ON.ORDER
   TOT.ALLOC = TOT.ALLOC + ALLOC
   TOT.RESRV = TOT.RESRV + RESERVE
   TOT.AVALBL = TOT.AVALBL + AVLB
   RETURN
***   PRINT THE RECORD
3000*
   IF LINE.COUNT > 53 THEN GOSUB 1000
   LINE.COUNT = LINE.COUNT + 1
   H.LINE = ''
   H.LINE = H.LINE :PROD "L#15"
   H.LINE = H.LINE :SP1: INV.FULL.DESC "L#45"
   H.LINE = H.LINE :SP1: INV.UNIT<1,2> "L#3"
   H.LINE = H.LINE :SP1: OCONV(MIN.QTY, ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(ON.HAND,ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(RESERVE,ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(ON.ORDER,ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(ALLOC,ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(AVLB,ICR.CNV) "R#10"
   PRINT H.LINE
   RETURN
*** CHECK FOR END OF PAGE
4000*
   IF LINE.COUNT + 3 > 53 THEN GOSUB 1000
   LINE.COUNT = LINE.COUNT + 3
   H.LINE = ""
   H.LINE = SPACE(77)
   H.LINE = H.LINE : OCONV(TOT.ON.HAND, ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(TOT.RESRV, ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(TOT.ON.ORDER, ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(TOT.ALLOC, ICR.CNV) "R#10"
   H.LINE = H.LINE :SP1: OCONV(TOT.AVALBL, ICR.CNV) "R#10"
   PRINT D.LINE 
   PRINT H.LINE 
   PRINT
   TOT.ON.ORDER = 0 ; TOT.AVALBL = 0 ; TOT.RESRV = 0 ; TOT.ALLOC = 0 ; TOT.ON.HAND = 0
   RETURN
***        CONVERT SHEET/WGHT/QTY
6000*
*COPY>ICSBP>INV.UM.CNV
*     MAT INV.CNV.REC = ''
*     BEGIN CASE
*     CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
*        ICR.CNV = "MD0"
*        ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
*     CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
*        ICR.CNV = "MD0"
*        ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
*     CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
*        ICR.CNV = "MD0"
*        ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
*     CASE 1
*        ICR.CNV = "MD2"
*        ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
*     END CASE
   RETURN
***   CALLS FOR SYSCOM
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   PRINTER OFF
   ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
999999*
END
