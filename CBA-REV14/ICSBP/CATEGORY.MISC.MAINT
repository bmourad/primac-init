*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [10.0]
* Copyright 1182 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - CATEGORY.MISC.MAINT
* BY          - GRAHAM JARVIS, PRISYS
* DATE        - 09 FEB 90
* DESCRIPTION - Miscellaneous PO Categories Maintenance
*This program creates and updates CATEGORY.MISC file.
* MOD CLW 09/24/94 TASK 17919  Accrual accounting
*T26126 adelgado 03/01/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*********************************************************************
*
***** FILE EQUATES
*
*COPY>ICS.CPYLIB>CATEGORY.MISC
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>COA
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SETUP SYSTEM ERRMSGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
  OPEN '','CATEGORY.MISC' TO CATEGORY.MISC ELSE
    ERRMSG = 'CATEGORY.MISC FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'ICS.SCREENS FILES IS MISSING'
    GOSUB 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','COA' TO COA ELSE
    ERRMSG = 'COA FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOSUB 93000
  END
  OPEN '','PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOSUB 93000
  END
*
***** GET COMPANY NAME
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
  IN.ACCT.LEN = LEN(CO.ACCT.PIC)
  UNKNOWN = STR('?',30)
*
***** MAIN PROCESSING
*
  MAT EDIT.COM.DRIVER = ''
  MAT CAMISC.REC = ''
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME = 'CATEGORY.MISC.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
*
**** PRINT SCREEN
100*
  RELEASE               ;* T26126
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
*
**** ENTER PROD.LINE
*
200*
  ECD.NUM = 1
  ECD.VALDAT.CODE = 4
  ECD.VALDAT.FILE = CATEGORY.MISC
  ECD.PREFIX.ID = CONO
  ECD.ACTION=4;CALL SCRN.EDIT
  PROD.LINE = ECD.RET.VALUE
  MATREAD GLTABLE.REC FROM CONTROL,CONO:"GLTABLE" ELSE MAT GLTABLE.REC=""
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
      * T26126 v
      READU DUMMY FROM CATEGORY.MISC, CONO:ECD.RET.VALUE LOCKED
        ERRMSG = 'CATEGORY record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 200 
      END ELSE NULL
      * T26126 ^
      FOR SAVE = 1 TO CAMISC.REC.SIZE
        CAMISC.REC(SAVE) = ECD.VALDAT.ITEM<SAVE>
      NEXT SAVE
*
* Category Description
      SCV.REC(2)<ECD.SCRN.NO> = CAMISC.DESC
* Accounts Payable Account
      MATREAD COA.REC FROM COA, CONO:CAMISC.AP.ACCT ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(3)<ECD.SCRN.NO> = CAMISC.AP.ACCT CO.ACCT.MASK
      SCV.REC(4)<ECD.SCRN.NO> = COA.DESC
* Creditors Expense Account
      MATREAD COA.REC FROM COA, CONO:CAMISC.CRED.EXP ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(5)<ECD.SCRN.NO> = CAMISC.CRED.EXP CO.ACCT.MASK
      SCV.REC(6)<ECD.SCRN.NO> = COA.DESC
* Miscellaneous PO Expense Account
      MATREAD COA.REC FROM COA, CONO:CAMISC.EXP.ACCT ELSE
        MAT COA.REC = ''; COA.DESC = UNKNOWN
      END
      SCV.REC(7)<ECD.SCRN.NO> = CAMISC.EXP.ACCT CO.ACCT.MASK
      SCV.REC(8)<ECD.SCRN.NO> = COA.DESC
*
      ECD.ACTION=3;CALL SCRN.EDIT
    CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
      FOR II = 1 TO 4
        ON II GOSUB 1100,1200,1300,1400
        IF ECD.RET.VALUE = 'END' THEN GOTO 100
      NEXT II
    CASE 1
      GOTO 100
  END CASE
  LOOP
    MORE = 0
    ECD.NUM = 11; SCV.REC(ECD.NUM)<1> = ''
    ECD.ACTION=4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'E' OR OPTION = 'END'
      CASE NUM(OPTION)
        ON OPTION GOSUB 1100,1200,1300,1400
        MORE = 1
      CASE OPTION = 'F'
        MATWRITE CAMISC.REC ON CATEGORY.MISC , CONO:PROD.LINE
    END CASE
  WHILE MORE DO REPEAT
  GOTO 100
*
***** ENTER CATG DESC
*
1100*
  ECD.NUM = 2
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    CAMISC.DESC = ECD.RET.VALUE
  END
  RETURN
*
1200 ECD.NUM = 3
  GL.DEFAULT = GLTB.MS.AP.ACCT
  ACCT.NO = CAMISC.AP.ACCT; GOSUB 5000
  CAMISC.AP.ACCT = ACCT.NO
  RETURN
1300 ECD.NUM = 5
  GL.DEFAULT = GLTB.MS.CRED.EXP
  ACCT.NO = CAMISC.CRED.EXP; GOSUB 5000
  CAMISC.CRED.EXP = ACCT.NO
  RETURN
1400 ECD.NUM = 7
  GL.DEFAULT = GLTB.MS.EXP.ACCT
  ACCT.NO = CAMISC.EXP.ACCT; GOSUB 5000
  CAMISC.EXP.ACCT = ACCT.NO
  RETURN
5000 SCV.REC(ECD.NUM)<1> = ''; ECD.ACTION=5;CALL SCRN.EDIT
  ECD.MAXL = IN.ACCT.LEN
  ECD.DEFAULT = GL.DEFAULT
  ECD.VALDAT.CODE = '2'; ECD.VALDAT.FILE = COA; ECD.PREFIX.ID = CONO
  SCV.REC(ECD.NUM)<1> = ACCT.NO
  ECD.PATRN = CO.ACCT.MATCH
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    ACCT.NO = ECD.RET.VALUE
    FOR L = 1 TO COA.REC.SIZE; COA.REC(L) = ECD.VALDAT.ITEM<L>; NEXT L
    SCV.REC(ECD.NUM)<1> = ACCT.NO CO.ACCT.MASK; ECD.ACTION=5;CALL SCRN.EDIT
    ECD.NUM = ECD.NUM + 1; SCV.REC(ECD.NUM)<1> = COA.DESC; ECD.ACTION=5;CALL SCRN.EDIT
  END
  RETURN
*
***** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 *
END
