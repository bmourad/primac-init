SUBROUTINE SERIAL.CONVERT.SEL(CONO,PROD,WHSE,LOC,SELID)
*********************************************************************
*
* REVISION - [08.1B]
*
* PROGRAM  - RS.CONVERT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/11/91
*
* DESCRIPTION
*
* This program provides a list of Serial Identifiers and quantities to
* allow selection for assignment of Serial Barcodes.
*T28746 lross 12/12/2005 * This program did not exist for releases prior
*                          to REV12.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>ICS.CPYLIB>COM.INV.SERIAL
*COPY>ICS.CPYLIB>COM.INV.LINK  
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 1
  CALL SI_SYSCOM (MAT SYSCOM.REC)
*
  OPEN "","ICS.SCREENS" TO M.SCREENS ELSE
      ERRMSG = "CANNOT OPEN ICS.SCREENS FILE"
      GOSUB 90000
      STOP
  END
*
*---- INITIALIZATION
*
DEFFUN CALC.STK.QTY(QTY,MAT INV.CNV.REC,ROND,LN)
  S$SCR = 2
  SCREEN DEFINE;SERIAL.CONVERT.SEL
  SCREEN FORMAT
  GOSUB 30000
  GOSUB 81000
  SCREEN COUNT;;60
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
* LINE.CNT = 0
  REF.NO = ""
  CURR.REF.NO = ""
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  SCREEN CLEAR
110 *
  TODAY = DATE()
  S$DATA(1)<S$SCR> = PROD
  S$DATA(2)<S$SCR> = WHSE
  S$DATA(3)<S$SCR> = INV.FULL.DESC
  S$DATA(14)<S$SCR> = LOC
  SCREEN DISPLAY;;ALL
  GOSUB 50000
*
*
500 *
  SCREEN FIELD;;31
  SCREEN INPUT;;31
  OPT = S$VALUE
510 *
  BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
          GOTO 99999
      CASE NUM(OPT)
        IF OPT >= CURR.REF.NO AND OPT <= (CURR.REF.NO+LINE.COUNT-1) THEN
           SELID = SS.SERIAL<1,OPT>
           GOTO 99999
        END ELSE
           ERRMSG='** OUT OF RANGE **'
           GOSUB 90000
           GOTO 500
        END
      CASE OPT = "S" OR OPT = "SF"
          REF.NO = CURR.REF.NO + LINE.COUNT
          IF REF.NO > LINE.CNT THEN REF.NO = 1
          GOSUB 50000
      CASE OPT = "SR"
          REF.NO = CURR.REF.NO - LINE.COUNT
          IF REF.NO < 1 THEN REF.NO = 1
          GOSUB 50000
      CASE OPT = "ST"
          REF.NO = 1
          GOSUB 50000
      CASE OPT = "SB"
          REF.NO = LINE.CNT
          IF REF.NO < 1 THEN REF.NO = 1
          GOSUB 50000
  END CASE
  GOTO 500
*
*---- GET CONVERSION FACTOR
*
30000 *
  MAT INV.CNV.REC = ""
  BEGIN CASE
      CASE INV.UNIT<1,2> = "SHT" AND INV.UNIT<1,3> = "LBS"
          ICR.CNV = "MD0"
          ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = "PC" AND INV.UNIT<1,3> = "MSI"
          ICR.CNV = "MD0"
          ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = "FT" AND INV.UNIT<1,3> = "MSI"
          ICR.CNV = "MD0"
          ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
          ICR.CNV = "MD2"
          ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
  END CASE
  RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = DCOUNT(S$DATA(60)<S$SCR>,VM)
  SCREEN MULTI;;C;11;60;15;16
  NO.PAGES = INT(LINE.CNT/LINE.COUNT) + (REM(LINE.CNT,LINE.COUNT) # 0)
  PAGE.OF = INT(START.REF.NO/LINE.COUNT) + 1
  S$DATA(58)<S$SCR>=PAGE.OF
  S$DATA(59)<S$SCR>=NO.PAGES
  SCREEN DISPLAY;;58
  SCREEN DISPLAY;;59
  RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
80050 *
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
  SS.SERIAL=''
  SS.RDATE=''
  SS.AVAIL=''
  FOR R = DCOUNT(IWLO.SERIAL,VM) TO 1 STEP -1
    MATREAD ISTK.REC FROM INV_SERIAL,CONO:IWLO.SERIAL<1,R> THEN
      IF ISTK.LOC # LOC THEN
        DEL IWLO.SERIAL<1,R>
        CONTINUE
      END
      IF ISTK.CUR.QTY < 1 THEN
        DEL IWLO.SERIAL<1,R>
        CONTINUE
      END
    END ELSE
      DEL IWLO.SERIAL<1,R>
      CONTINUE
    END
  NEXT R
  FOR R = 1 TO DCOUNT(IWLO.SERIAL,VM)
    MATREAD ISTK.REC FROM INV_SERIAL,CONO:IWLO.SERIAL<1,R> THEN
      SS.SERIAL<1,R> = IWLO.SERIAL<1,R>
      SS.RDATE<1,R> = OCONV(ISTK.POST.DATE,'D2/')
      SS.AVAIL<1,R> = CALC.STK.QTY(ISTK.CUR.QTY,MAT INV.CNV.REC,"","")
      SS.AVAIL<1,R> = OCONV(SS.AVAIL<1,R>,ICR.CNV)
    END
  NEXT R
  S$DATA(60)<S$SCR> = SS.SERIAL
  S$DATA(15)<S$SCR> = SS.RDATE
  S$DATA(16)<S$SCR> = SS.AVAIL
  LINE.CNT = DCOUNT(SS.SERIAL,VM)
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999 *
  SCREEN CLEAR;;D
  PRINT @(-1)
END
