*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - FNGD.BOM.LIST
* BY          - JULIANNE RIVERA, COMPUTER BUSINESS ASSOCIATES
* DATE        - 08/28/91
* DESCRIPTION -
* This program produces a listing of finished goods die 
* information.
*
*T26090 ajibaly 03/22/2002 * Convert to Rev12.
*T27954 cmykleb 02/12/2004 * Report is not printing the Q Ratio correctly.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>ICS.CPYLIB>FNGD.BOM
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>INVENTORY   ; * T27954
*COPY>ICS.CPYLIB>INV.CNV     ; * T27954
*
*---- SETUP SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE MISSING";GOTO 93000
   OPEN '','COST.CNTR' TO COST.CNTR ELSE ERRMSG="COST.CNTR FILE MISSING";GOTO 93000
   OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG="INVENTORY FILE MISSING";GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
   OPEN '','FNGD.BOM' TO FNGD.BOM ELSE ERRMSG="FNGD.BOM FILE MISSING";GOTO 93000
*
*---- GET VALUES FROM PROC
*
   PROCREAD X ELSE 
      ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC" ; GOTO 93000
   END
   CONO = X<1>
*T26090 v
*     CONO.NAME = X<2>
*     REPORT.NAME = "FINISHED GOODS BILL OF MATERIALS LISTING"
   CONO.NAME = ''
   REPORT.NAME = ''
*T26090 ^
   REPORT.NUMBER = X<2>
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
*---- INITIALIZATION
*
   UNKNOWN = STR("?",45)
   SP1 = SPACE(1)
   PG.NO=0
   LINE.COUNT=99
   DATE=OCONV(DATE(),"D2/")
   TIME=OCONV(TIME(),"MT2:")
   PRINT.FLAG=0
   PRINTER ON
*
*---- GET HEADING INFO
*
   SUBHEAD1 = "BILL OF MATL# / DESCRIPTION    DEPT  CTR / DESCRIPTION              MATERIAL / DESC WHSE TYP Q RATIO"
   SUBHEAD2 = "------------------------------ ----- ------------------------------ --------------- ---- --- --------"
*
*---- MAIN PROCESSING
*
10 *
   READNEXT KEY ELSE GOTO 99999
   BOMNO = KEY[4,99]
   GOSUB 30
   GOTO 10
14 *
   GOTO 99999
*
*---- PRINT LINE
*
30 *
   MATREADU BOM.REC FROM FNGD.BOM, KEY ELSE
      ERRMSG = 'FNGD BOM ':KEY:' IS MISSING'
      GOSUB 91000
      GOTO 10
   END
   IF LINE.COUNT > 45 THEN GOSUB 60
   FIRST.LINE = 1
   PROD.CNT = DCOUNT(BOM.PROD,VM)
   IF PROD.CNT < 1 THEN PROD.CNT = 1
   FOR X = 1 TO PROD.CNT
      GOSUB 40
   NEXT X
   PRINT
   LINE.COUNT = LINE.COUNT + 1
   RETURN
*
*---- SET-UP LINE
*
40 *
   IF FIRST.LINE THEN
      PRINT
      LINE1 = BOMNO"L#10":SPACE(21)
      FIRST.LINE = 0
      LINE2 = BOM.DESC"L#30":SPACE(7)
   END ELSE
      LINE1 = SPACE(31)
      LINE2 = SPACE(37)
   END
   LINE1 = LINE1:BOM.DEPT<1,X>"L#5":SP1:BOM.CCTR<1,X>"L#3"
   MATREAD CCTR.REC FROM COST.CNTR, CONO:BOM.CCTR<1,X> ELSE
      CCTR.DESC = UNKNOWN
   END
   LINE2 = LINE2:CCTR.DESC"L#30":SP1
   LINE1 = LINE1:SPACE(28):BOM.PROD<1,X>"L#15":SP1
*T27954 v
*  READV DESC FROM INVENTORY, CONO:BOM.PROD<1,X>,2 ELSE
*     DESC = UNKNOWN
*  END
   MATREAD INV.REC FROM INVENTORY, CONO:BOM.PROD<1,X> ELSE
      MAT INV.REC = ''
   END
   DESC = INV.FULL.DESC
*COPY>ICSBP>INV.UM.CNV
*T27954 ^
   LINE2 = LINE2:DESC"L#45":SP1
   LINE1 = LINE1:BOM.WHSE<1,X>"L#4":SP1:SP1:BOM.Q.TYPE<1,X>"L#1":SP1
*T27954 v
*  LINE1 = LINE1:OCONV(BOM.Q.RATIO<1,X>,"MD4")"R#8"
   IF BOM.Q.TYPE<1,X> = "P" THEN
      RATIO = OCONV(BOM.Q.RATIO<1,X>,"MD4")
   END ELSE
      RATIO = OCONV(INT(((BOM.Q.RATIO<1,X> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5),ICR.CNV1)
   END
   LINE1 = LINE1:RATIO"R#8"
*T27954 ^
   PRINT LINE1
   PRINT LINE2
   PRINT.FLAG = 1
   LINE.COUNT = LINE.COUNT + 2
   RETURN
*
*---- PRINT HEADING
*
60 *
   PRINT CHAR(12)
   PG.NO = PG.NO + 1
   PRINT HD1:PG.NO
   PRINT HD2
   PRINT
   PRINT SUBHEAD1
   PRINT SUBHEAD2
   LINE.COUNT = 5
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
   PRINTER OFF;ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);PRINTER ON;RETURN
92000 *
   PRINTER OFF;ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);PRINTER ON;RETURN
93000 *
   ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999 *
   PRINTER OFF
END
