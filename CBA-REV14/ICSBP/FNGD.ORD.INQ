   SUBROUTINE FNGD.ORD.INQ(CONO,PROD,WHSE)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - FNGD.ORD.INQ
* BY          - EDVARD PITKA
* DATE        - 05/01/01
* DESCRIPTION -
*T25740  edvard  06/30/2001  Rev12 
*C40618 cmykleb 08/22/2002 * Screen problem corrections.
*T26817 cmykleb 08/22/2002 * When a line display "MULTI" as the job #,
*                            the user should be able to choose that line
*                            and a list of the multiple jobs for that
*                            order should display.
*ENDDOC
*********************************************************************
*
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>FNGD.STATS
*COPY>ICS.CPYLIB>FNGD.ORDER.STATS
*COPY>OPS.CPYLIB>ORDER
*COPY>OPS.CPYLIB>ORDER.DETAIL
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
   ESN = ECD.SCRN.NO
   PAGE.SIZE = 5
   S.O.CUST = ""
   S.O.DATE = ""
   S.O.QTY = ""
   S.R.QTY = ""
   S.A.QTY = ""
   S.B.QTY = ""
   S.JOB = ""
   S.R.NO = ""
   S.KIT  = ""
   S.CNAME = ""
   START = 1
   UNKNOWN = STR("?",30)
   MAT FOS.REC = ""
   MAT GEN.XREF.REC = ""
   GXR.CO = CONO
   PAGE.CNT = 1 ; * C40618
*
   IWH.ID = CONO:PROD:"!":WHSE
   MATREAD FGS.REC FROM FNGD.STATS, IWH.ID ELSE     
      ERRMSG = "No orders for this product and warehouse"
      GOSUB 91000
      GOTO 99999
   END
   GOSUB GET.DATA
   GOSUB BUILD.SCREEN
   ECD.ACTION = 3; CALL SCRN.EDIT
   ;*
   ;* Main prompt
   ;*
   LOOP
      ECD.NUM = 41; SCV.REC(ECD.NUM)<ESN> = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE ACTION = "" OR ACTION = "END"
            ACTION = "END"
         CASE ACTION[1,1] = "S"
            GOSUB SCROLL
         CASE NOT(NUM(ACTION))
            ERRMSG = "Invalid entry, please re-enter"
            GOSUB 91000
         CASE ACTION < 1 OR ACTION > LN.CNT
            ERRMSG = "Out of range, please re-enter"
            GOSUB 91000
*T26817 v
*        CASE S.JOB<ACTION,2> = ""
         CASE S.JOB<1,ACTION> # "MULTI"
*T26817 ^
         CASE 1
            GXR.NAME = "FOS.JOB"                                  
            GXR.XREF = FNGD.ORDER.STATS                           
*T26817 v
*           GXR.SRCH.ID = IWH.ID[4,99]:"!":FGS.ORDER<1,ACTION>    
            GXR.SRCH.ID = IWH.ID[4,99]:"!":FGS.ORDER<1,ACTION>:"!":FGS.SEQ<1,ACTION>:"!":FGS.KIT<1,ACTION>
*T26817 ^
            CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
            GOSUB SCROLL
      END CASE
   WHILE ACTION # "END" DO REPEAT
   GOTO 99999
*
**************************************************************************
**** S U B R O U T I N E S ***********************************************
**************************************************************************
*
************
GET.DATA: 
************
*
   LN.CNT = DCOUNT(FGS.ORDER,VM)
   GTOT.BQTY=0
   GTOT.OQTY=0
   GTOT.RQTY=0
   GTOT.AQTY=0
   FOR I = 1 TO LN.CNT
      MATREAD ORD.REC FROM ORDER,CONO:FGS.ORDER<1,I> ELSE
         MAT ORD.REC = ""
      END
      ST.CNT = DCOUNT(ORD.SHIP.TO,VM)
      TOT.O.QTY = ""
      TOT.R.QTY = ""
      TOT.A.QTY = ""
      TOT.S.QTY = ""
      TOT.RECP.NO = ""
      FOR S = 2 TO ST.CNT
         TOT.SHIP.O.QTY = ""
         TOT.SHIP.R.QTY = ""
         TOT.SHIP.A.QTY = ""
         TOT.SHIP.S.QTY = ""
         TOT.SHIP.RECP.NO = ""
         TOT.SHIP.SHIP.S.QTY=""
         MATREAD ORD.DET.REC FROM ORDER.DETAIL,CONO:FGS.ORDER<1,I>:"!":ORD.SHIP.TO<1,S> ELSE
            MAT ORD.DET.REC = ""
         END
         IF OSD.PROD.SEQ = "" THEN
*T26817 v
*           MATREAD FOS.REC FROM FNGD.ORDER.STATS, IWH.ID:"!":FGS.ORDER<1,I> ELSE
            FOS.ID = IWH.ID:"!":FGS.ORDER<1,I>:"!":FGS.SEQ<1,I>:"!":FGS.KIT<1,I>
            MATREAD FOS.REC FROM FNGD.ORDER.STATS, FOS.ID ELSE
*T26817 ^
               MAT FOS.REC = ""
               MATREAD ORD.REC FROM ORDER, CONO:FGS.ORDER<1,I> THEN
                  FOS.CUST = ORD.CUST
                  FOS.DATE = ORD.DATE
               END
            END
            S.O.CUST<1,I> = FOS.CUST
            MATREAD CUST.REC FROM CUSTOMER,CONO:FOS.CUST THEN
               S.CNAME<1,I> = CUST.NAME
            END
            S.O.DATE<1,I> = OCONV(FOS.DATE,"D2/")
            S.O.QTY<1,I>=OCONV(INT(((FOS.O.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
            S.R.QTY<1,I>=OCONV(INT(((FOS.R.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
            S.A.QTY<1,I>=OCONV(INT(((FOS.A.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
            IF FOS.JOB<1,2> = "" THEN
               S.JOB<1,I> = FOS.JOB
            END ELSE
               S.JOB<1,I> = "MULTI"
            END
            S.R.NO<1,I> = FOS.RECP.NO
            B.QTY = FOS.O.QTY-FOS.R.QTY-FOS.A.QTY-FOS.S.QTY
            IF B.QTY < 0 THEN B.QTY = 0
            S.B.QTY<1,I>=OCONV(INT(((B.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
*
            GTOT.BQTY +=B.QTY
            GTOT.OQTY +=FOS.O.QTY
            GTOT.RQTY +=FOS.R.QTY
            GTOT.AQTY +=GTOT.AQTY
         END ELSE
            KIT.PRINT = ""
            PTR = 1
            LOOP
               LOCATE PROD IN OSD.PROD<1>,PTR SETTING IX THEN

                  IF (WHSE=OSD.WHSE<1,IX>) AND (FGS.SEQ<1,I>=OSD.PROD.SEQ<1,IX>) AND (FGS.KIT<1,I>=OSD.KIT<1,IX>) THEN
                     IF FGS.KIT<1,I> = "M" THEN
                        KIT.PRINT = '*'
                     END ELSE
                        KIT.PRINT = ""
                     END
                     S.KIT<I> = KIT.PRINT
                     FOS.ID = CONO:PROD:"!":WHSE:"!":FGS.ORDER<1,I>:"!":OSD.PROD.SEQ<1,IX>:"!":OSD.KIT<1,IX>
                     MATREAD FOS.REC FROM FNGD.ORDER.STATS, FOS.ID ELSE
                        MAT FOS.REC = ""
                        MATREAD ORD.REC FROM ORDER, CONO:FGS.ORDER<1,I> THEN
                           FOS.CUST = ORD.CUST
                           FOS.DATE = ORD.DATE
                        END
                     END
                     TOT.O.QTY += OSD.O.QTY<1,IX>
                     TOT.R.QTY += OSD.R.QTY<1,IX>
                     TOT.A.QTY += OSD.A.QTY<1,IX>
                     TOT.S.QTY += OSD.S.QTY<1,IX>
                     TOT.SHIP.O.QTY += OSD.O.QTY<1,IX>
                     TOT.SHIP.R.QTY += OSD.R.QTY<1,IX>
                     TOT.SHIP.A.QTY += OSD.A.QTY<1,IX>
                     TOT.SHIP.SHIP.S.QTY += OSD.S.QTY<1,IX>
                     IF TOT.RECP.NO = "" THEN
                        TOT.RECP.NO = FOS.RECP.NO
                     END ELSE
                        TOT.RECP.NO = TOT.RECP.NO:VM:FOS.RECP.NO
                     END
                     PTR = 0
                  END
               END ELSE
                  PTR = 0
               END
            WHILE PTR DO
               PTR = IX+1
            REPEAT
            S.O.CUST<1,I> = FOS.CUST
            MATREAD CUST.REC FROM CUSTOMER,CONO:FOS.CUST THEN
               S.CNAME<1,I>=CUST.NAME
            END
            S.O.DATE<1,I> = OCONV(FOS.DATE,"D2/")
            S.O.QTY<1,I>=OCONV(INT(((TOT.O.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
            S.R.QTY<1,I>=OCONV(INT(((TOT.R.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
            S.A.QTY<1,I>=OCONV(INT(((TOT.A.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
            S.R.NO<1,I>  = TOT.RECP.NO
*T26817 v
            IF FOS.JOB<1,2> = "" THEN
               S.JOB<1,I> = FOS.JOB
            END ELSE
               S.JOB<1,I> = "MULTI"
            END
*T26817 ^
            B.QTY = TOT.O.QTY-TOT.R.QTY-TOT.A.QTY-TOT.S.QTY
            SHIP.B.QTY = TOT.SHIP.O.QTY-TOT.SHIP.R.QTY-TOT.SHIP.A.QTY-TOT.SHIP.SHIP.S.QTY
            IF B.QTY < 0 THEN B.QTY = 0
            IF SHIP.B.QTY < 0 THEN SHIP.B.QTY = 0
            S.B.QTY<1,I>=OCONV(INT(((B.QTY/ICR.DV1)*ICR.MT1)/ICR.DV2+.5),ICR.CNV1)
*
            GTOT.BQTY +=SHIP.B.QTY
            GTOT.OQTY +=TOT.SHIP.O.QTY
            GTOT.RQTY +=TOT.SHIP.R.QTY
            GTOT.AQTY +=TOT.SHIP.A.QTY
         END
      NEXT S
   NEXT I
   RETURN
*
*********
SCROLL: 
*********
*
   BEGIN CASE                                    
      CASE ACTION = "S" OR ACTION = "SF"          
         IF LN.CNT > PAGE.SIZE THEN                
            START +=PAGE.SIZE                       
            IF START > LN.CNT THEN START = 1        
         END                                       
      CASE ACTION = "SR"                          
         IF LN.CNT > PAGE.SIZE THEN                
            START -=PAGE.SIZE                       
*C40618 v
*           IF START< LN.CNT THEN START = 1          
            IF START < 1 THEN START = 1          
*C40618 ^
         END                                       
      CASE ACTION = "ST"                          
         START = 1                                 
      CASE ACTION = "SB"                          
         START=LN.CNT
      CASE ACTION[1,1] = "S" AND NUM(ACTION[2,99])
         START = ACTION[2,99]+0                    
         IF START < 1 OR START > LN.CNT THEN       
            ERRMSG = "** Invalid selection **"      
            GOSUB 91000                             
         END                                       
   END CASE
   START=1+INT((START-1)/PAGE.SIZE)*PAGE.SIZE 
*C40618 v
   PAGE.CNT = START - 1
   PAGE.CNT = PAGE.CNT / PAGE.SIZE
   PAGE.CNT = PAGE.CNT + 1
   SCV.REC(31)<ESN> = PAGE.CNT"R%2"
   ECD.NUM=31;ECD.ACTION = 5;CALL SCRN.EDIT
*C40618 ^
   ECD.NUM=11;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=12;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=13;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=14;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=15;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=16;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=17;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=18;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=6;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=7;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=8;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=9;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
1909 RETURN
*
*************
BUILD.SCREEN: 
*************
*
   SCV.REC(31)<ESN> = PAGE.CNT"R%2" ; * C40618
   SCV.REC(32)<ESN> = INT(LN.CNT/PAGE.SIZE+.9) "R%2"
   SCV.REC(21)<ESN> = GTOT.BQTY
   SCV.REC(22)<ESN> = GTOT.OQTY
   SCV.REC(23)<ESN> = GTOT.RQTY
   SCV.REC(24)<ESN> = GTOT.AQTY
   FOR I = 21 TO 24
      SCV.REC(I)<ESN> = OCONV(INT(((SCV.REC(I)<ESN>/ICR.DV1) * ICR.MT1) / ICR.DV2 +.5),ICR.CNV1)
   NEXT I
*
   LN.CNT = DCOUNT(FGS.ORDER,VM)
   LN.NUM = ""
   FOR LN = 1 TO LN.CNT
      LN.NUM<1,LN> = LN
   NEXT LN
   SCV.REC(11)<ESN> = LN.NUM
   SCV.REC(12)<ESN> = FGS.ORDER
   SCV.REC(13)<ESN> = S.O.DATE
   SCV.REC(14)<ESN> = S.B.QTY
   SCV.REC(15)<ESN> = S.O.QTY
   SCV.REC(16)<ESN> = S.R.QTY
   SCV.REC(17)<ESN> = S.A.QTY
   SCV.REC(18)<ESN> = S.KIT
   SCV.REC(6)<ESN> = S.O.CUST
   SCV.REC(7)<ESN> = S.CNAME
   SCV.REC(8)<ESN> = S.R.NO
   SCV.REC(9)<ESN> = S.JOB
   RETURN
*
*---- Error message routine
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
99999*
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
