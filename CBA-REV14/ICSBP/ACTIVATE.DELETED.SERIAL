   SUBROUTINE ACTIVATE.DELETED.SERIALS(ISTK.ID,ERR.MSG,ERR.FLG,AWHSE,ALOC)
*
*COPY>CPYLIB>COM1
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - ACTIVATE.DELETED.SERIAL
* BY          - epitka; CBA
* DATE        - 06/13/2002
* DESCRIPTION -IF AWHSE AND ALOC ARE PASSED IN PROGRAM SERIAL WILL
* BE ACTIVATED INTO THAT WHSE/LOC COMBINATION IF ALL SUPPORING RECORDS
* ARE PRESENT FOR THAT COMBINATION
* IRW,INVR, ISTK, AND IWLO REC HAVE TO BE REREAD WHEN RETURNING BACK 
* FROM THIS PROGRAM.
*
*T25740 epitka 06/13/2002 * ACTIVATE SERIALS DELETED AT THE EOM PROCESS.
*T25740 epitka 07/11/2002 * REV12
*T27484 lross 06/09/2003 * Not using INS to load SERIAL into IWLO.SERIAL
*                          causes duplicates, i.e., ICS.IWH.SUB locates
*                          by 'AR' and inserts serial.
*T28167 cmykleb 06/24/2004 * If the serials are out of order in the inv
*                            whse loc (IWLO.SERIAL) record, the serial
*                            could be added again to the record which
*                            causes the serial record to show up multiple
*                            times on some reports and screens.
*ENDDOC
**********************************************
*
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR     
*
   ERR.FLG=0 ; ERR.MSG=''
   CONO=ISTK.ID[1,3]
   SERIAL=ISTK.ID[4,99]
*
   DIM HOLD.INVR.REC(INVR.REC.SIZE); MAT HOLD.INVR.REC=''
   DIM HOLD.IRW.REC(IRW.REC.SIZE); MAT HOLD.IRW.REC=''
   DIM HOLD.IWLO.REC(IWLO.REC.SIZE); MAT HOLD.IWLO.REC=''
   HOLD.IRW=0 ; HOLD.INVR=0 ; HOLD.IWLO=0
*
   IF FILEINFO(INV_RECEIPTS,0)=0 THEN                  
      OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE       
         ERR.MSG="INV_RECEIPTS FILE IS MISSING";ERR.FLG='-2'
      END                                               
   END                                                 
   IF FILEINFO(INV_RECEIPTS_TEMP,0)=0 THEN                  
      OPEN '','INV_RECEIPTS_TEMP' TO INV_RECEIPTS_TEMP ELSE       
         ERR.MSG="INV_RECEIPTS_TEMP FILE IS MISSING";ERR.FLG='-2'
      END                                               
   END                                                 
   IF FILEINFO(INV_RECP_WHSE,0)=0 THEN                  
      OPEN '','INV_RECP_WHSE' TO INV_RECP_WHSE ELSE      
         ERR.MSG="INV_RECP_WHSE FILE IS MISSING";ERR.FLG='-2'
      END                                                
   END                                                  
   IF FILEINFO(INV_RECP_WHSE_TEMP,0)=0 THEN                  
      OPEN '','INV_RECP_WHSE_TEMP' TO INV_RECP_WHSE_TEMP ELSE      
         ERR.MSG="INV_RECP_WHSE_TEMP FILE IS MISSING";ERR.FLG='-2'
      END                                                
   END                                                  
   IF FILEINFO(INV_SERIAL,0)=0 THEN                   
      OPEN '','INV_SERIAL' TO INV_SERIAL ELSE          
         ERR.MSG="INV_SERIAL FILE  IS MISSING";ERR.FLG='-2'
      END                                              
   END                                                
   IF FILEINFO(INV_SERIAL_TEMP,0)=0 THEN                   
      OPEN '','INV_SERIAL_TEMP' TO INV_SERIAL_TEMP ELSE          
         ERR.MSG="INV_SERIAL_TEMP FILE  IS MISSING";ERR.FLG='-2'
      END                                              
   END                                                
   OPEN '','INV_SERIAL_DELETED' TO INV_SERIAL_DELETED ELSE
      ERR.MSG='INV_SERIAL_DELETED FILE IS MISSING';ERR.FLG='-2'
   END
*
   IF NOT(ERR.FLG) THEN
      MATREADU ISTK.REC FROM INV_SERIAL_DELETED,ISTK.ID THEN
         IF AWHSE='' THEN
            AWHSE=ISTK.WHSE
         END
         IF ALOC='' THEN
            ALOC=ISTK.LOC
         END
         INVR.ID=CONO:ISTK.RECP
         IF RECORDLOCKED(INV_RECEIPTS_TEMP,INVR.ID)=0 THEN
            DELETE INV_RECEIPTS_TEMP,INVR.ID
         END
         INVR.OK=1
         MATREADU INVR.REC FROM INV_RECEIPTS_TEMP,INVR.ID THEN
            MAT HOLD.INVR.REC= MAT INVR.REC
            HOLD.INVR=1
            MATREADU INVR.REC FROM INV_RECEIPTS,INVR.ID ELSE
               INVR.OK=0
            END
         END ELSE
            MATREADU INVR.REC FROM INV_RECEIPTS,INVR.ID ELSE
               INVR.OK=0
            END
         END
         IF INVR.OK THEN
            LOCATE SERIAL IN INVR.SERIAL.NO<1> SETTING SPOS ELSE
               INVR.SERIAL.NO<1,-1>=SERIAL
            END
            IRW.ID=CONO:ISTK.RECP:"!":AWHSE
            IF RECORDLOCKED(INV_RECP_WHSE_TEMP,IRW.ID)=0 THEN
               DELETE INV_RECP_WHSE_TEMP,IRW.ID
            END
            IRW.OK=1
            MATREADU IRW.REC FROM INV_RECP_WHSE_TEMP,IRW.ID THEN
               MAT HOLD.IRW.REC = MAT IRW.REC
               HOLD.IRW=1
               MATREADU IRW.REC FROM INV_RECP_WHSE,IRW.ID ELSE
                  IRW.OK=0
               END
            END ELSE
               MATREADU IRW.REC FROM INV_RECP_WHSE,IRW.ID ELSE
                  IRW.OK=0
               END
            END
            IF IRW.OK THEN
               LOCATE SERIAL IN IRW.SERIAL.NO<1> SETTING SPOS ELSE
                  IRW.SERIAL.NO<1,-1>=SERIAL
               END
               IWLO.ID=CONO:ISTK.PROD:"!":AWHSE:"!":ALOC
               IF RECORDLOCKED(INV.WHSE.LOC.TEMP,IWLO.ID)=0 THEN
                  DELETE INV.WHSE.LOC.TEMP,IWLO.ID
               END
               MATREADU IWLO.REC FROM INV.WHSE.LOC.TEMP,IWLO.ID THEN
                  MAT HOLD.IWLO.REC=MAT IWLO.REC
                  HOLD.IWLO=1
                  MATREADU IWLO.REC FROM INV.WHSE.LOC,IWLO.ID ELSE
                     MAT IWLO.REC=''
                  END
               END ELSE
                  MATREADU IWLO.REC FROM INV.WHSE.LOC,IWLO.ID ELSE
                     MAT IWLO.REC=''
                  END
               END
*T27484 v
*       LOCATE SERIAL IN IWLO.SERIAL<1> SETTING SPOS ELSE
*         IWLO.SERIAL<1,SPOS>=SERIAL
*T28167 v
*              LOCATE SERIAL IN IWLO.SERIAL<1>,1 BY 'AR' SETTING SPOS ELSE
*                 INS SERIAL BEFORE IWLO.SERIAL<1,SPOS>
*              END
               LOCATE SERIAL IN IWLO.SERIAL<1>,1 SETTING SPOS ELSE
                  IF NUM(SERIAL) THEN
                     LOCATE SERIAL IN IWLO.SERIAL<1>,1 BY 'AR' SETTING SPOS ELSE
                        INS SERIAL BEFORE IWLO.SERIAL<1,SPOS>
                     END
                  END ELSE
                     LOCATE SERIAL IN IWLO.SERIAL<1>,1 BY 'AL' SETTING SPOS ELSE
                        INS SERIAL BEFORE IWLO.SERIAL<1,SPOS>
                     END
                  END
               END
*T28167 ^
               MATWRITEU ISTK.REC ON INV_SERIAL,ISTK.ID
               MATWRITEU INVR.REC ON INV_RECEIPTS,INVR.ID
               MATWRITEU IRW.REC ON INV_RECP_WHSE,IRW.ID
               MATWRITEU IWLO.REC ON INV.WHSE.LOC,IWLO.ID
               DELETE INV_SERIAL_DELETED,ISTK.ID
               ;* now check if records existed in TEMP files
               IF HOLD.INVR THEN
                  MAT INVR.REC=MAT HOLD.INVR.REC
                  LOCATE SERIAL IN INVR.SERIAL.NO<1> SETTING SPOS ELSE
                     INVR.SERIAL.NO<1,-1>=SERIAL
                  END
                  MATWRITEU INVR.REC ON INV_RECEIPTS_TEMP,INVR.ID
               END
               IF HOLD.IRW THEN
                  MAT IRW.REC=MAT HOLD.IRW.REC
                  LOCATE SERIAL IN IRW.SERIAL.NO<1> SETTING SPOS ELSE
                     IRW.SERIAL.NO<1,-1>=SERIAL
                  END
                  MATWRITEU IRW.REC ON INV_RECP_WHSE_TEMP,IRW.ID
               END
               IF HOLD.IWLO THEN
                  MAT IWLO.REC = MAT HOLD.IWLO.REC
*T28167 v
*T27484 v
*         LOCATE SERIAL IN IWLO.SERIAL<1> SETTING SPOS ELSE
*                 LOCATE SERIAL IN IWLO.SERIAL<1>,1 BY 'AR' SETTING SPOS ELSE
*           IWLO.SERIAL<1,SPOS>=SERIAL
*                    INS SERIAL BEFORE IWLO.SERIAL<1,SPOS>
*T27484 ^
*                 END
                  LOCATE SERIAL IN IWLO.SERIAL<1>,1 SETTING SPOS ELSE
                     IF NUM(SERIAL) THEN
                        LOCATE SERIAL IN IWLO.SERIAL<1>,1 BY 'AR' SETTING SPOS ELSE
                           INS SERIAL BEFORE IWLO.SERIAL<1,SPOS>
                        END
                     END ELSE
                        LOCATE SERIAL IN IWLO.SERIAL<1>,1 BY 'AL' SETTING SPOS ELSE
                           INS SERIAL BEFORE IWLO.SERIAL<1,SPOS>
                        END
                     END
                  END
*T28167 ^
                  MATWRITEU IWLO.REC ON INV.WHSE.LOC.TEMP,IWLO.ID
               END
            END ELSE
               ERR.MSG='INV_RECP_WHSE RECORD IS MISSING FOR SERIAL ':ISTK.ID
               ERR.FLG='-1'
            END
         END ELSE
            ERR.MSG='INV_RECEIPTS RECORD IS MISSING FOR SERIAL ':ISTK.ID
            ERR.FLG='-1'
         END
      END ELSE
         ERR.MSG='SERIAL DOES NOT EXIST ON THE INV_SERIAL_DELETED FILE ':ISTK.ID
         ERR.FLG='-1'
      END
   END
