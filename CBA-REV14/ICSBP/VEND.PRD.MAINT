  SUBROUTINE VEND.PRD.MAINT(NEWREC,CONO,PDNO)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INVENTORY
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - VEND.PRD.MAINT
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 10/15/83
* DESCRIPTION
* This program is use to maintain Vendor purchasing information for a
* given product.
*T25978 adelgado 02/18/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*
*---- Data Structure Libraries
*
*COPY>PMC.CPYLIB>VEND
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- Setup SYSCOM and GEN.XREF
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
*
*---- Initialization
*
  LINE.SPACE = 2
  BEGIN.PAGE = 6
  PAGE.SIZE = 7
  OLD.START = 0
*
  ESN = ECD.SCRN.NO
*
*---- Display Data
*
  ECD.NUM = 5; SCV.REC(ECD.NUM)<ESN> = PDNO
  ECD.ACTION = 5; CALL SCRN.EDIT
  ECD.NUM = 6; SCV.REC(ECD.NUM)<ESN> = INV.FULL.DESC
  ECD.ACTION = 5; CALL SCRN.EDIT
  LINES = DCOUNT(INV.VENDOR,VM)
  IF NEWREC AND NOT(LINES) THEN
    OPTION = "A"
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 10000
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
  END ELSE
    LN = 1
  END
  GOSUB 80000
*
*---- Prompt Line
*
  MORE = 1
  LOOP
    ECD.NUM = 29; SCV.REC(ECD.NUM)<ESN> = ""
    ECD.ACTION = 4; CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "E" OR OPTION = "END"
        MORE = 0
      CASE OPTION = "A"
        LOOP
          LN = LINES + 1
          OLD.LINES = LINES
          GOSUB 10000
        WHILE LINES > OLD.LINES DO REPEAT
        LN = LINES
      CASE OPTION = "C"
        GOSUB 30000
        IF LNO THEN
          LN = LNO
          SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          GOSUB 10100
        END
      CASE OPTION = "D"
        GOSUB 30000
        IF LNO THEN
          LN = LNO
          INV.VENDOR = DELETE(INV.VENDOR,1,LN,0)
          INV.LEAD.TIME = DELETE(INV.LEAD.TIME,1,LN,0)
          INV.PKG.QTY = DELETE(INV.PKG.QTY,1,LN,0)
          INV.PRICE = DELETE(INV.PRICE,1,LN,0)
          INV.PART.NO = DELETE(INV.PART.NO,1,LN,0)
          LINES = DCOUNT(INV.VENDOR,VM)
          IF LN > LINES THEN LN = LINES
          IF LN < 1 THEN LN = 1
          OLD.START = 0
        END
      CASE OPTION = "S"
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
      CASE OPTION = 'ST'
        LN = 1
      CASE OPTION = 'SB'
        LN = LINES
      * T25978 ^
    END CASE
    GOSUB 80000
  WHILE MORE DO REPEAT
  GOTO 99999
*
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*---- Multi Line Screen
10000*
  GOSUB 80000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 1 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  P_X  = 0 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10100*
  X = 6; Y = SLN; TYP = 1; MAXL = 8; O.R = "O"
  HMSG = "Vendor Number"
  IF INV.VENDOR<1,LN> # "" THEN
    DEFAULT = INV.VENDOR<1,LN>
  END
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END"
      IF OPTION = "A" THEN
        INV.VENDOR = DELETE(INV.VENDOR,1,LN,0)
        INV.LEAD.TIME = DELETE(INV.LEAD.TIME,1,LN,0)
        INV.PKG.QTY = DELETE(INV.PKG.QTY,1,LN,0)
        INV.PRICE = DELETE(INV.PRICE,1,LN,0)
        INV.PART.NO = DELETE(INV.PART.NO,1,LN,0)
      END
      OLD.START = 0
      GOTO 19999
    CASE VALUE = ""
      X = 6; Y = SLN+1; TYP = 1; MAXL = 30; O.R = "O"
      HMSG = "Vendor Name"
      CALL EDIT.SUB
      IF VALUE = "" OR VALUE = "END" THEN
        P_X  = 6 ; P_Y = SLN+1 ; P_VALUE = "" ; P_OPT = "CL"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        GOTO 10100
      END
      GXR.NAME = "VENDOR"
      GXR.XREF = VENDOR.XREF
      GXR.FILE = VEND
      GXR.SRCH.ID = VALUE
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION = 2; CALL SCRN.EDIT
      ECD.ACTION = 3; CALL SCRN.EDIT
      OLD.START = 0; GOSUB 80000
      P_X  = 1 ; P_Y = SLN ; P_VALUE = LN "R#3" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      IF GXR.ID = "" THEN GOTO 10100
      VALUE = GXR.ID
      P_X  = 6 ; P_Y = SLN ; P_VALUE = VALUE "L#8" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END CASE
  LOCATE VALUE IN INV.VENDOR<1>,1 SETTING FND ELSE FND = 0
  IF FND THEN
    IF FND # LN THEN
      ERRMSG = "* * ":VALUE:" ALREADY EXIST ON LINE # ":FND:" * *"
      GOSUB 91000; GOTO 10100
    END
  END
  MATREAD VEND.REC FROM VEND, CONO : VALUE ELSE
    ERRMSG = VALUE:" IS NOT ON FILE TRY AGAIN !"; GOSUB 91000; GOTO 10100
  END
  INV.VENDOR<1,LN> = VALUE
  P_X  = 6 ; P_Y = SLN+1 ; P_VALUE = VEND.DESC "L#30" ; P_OPT = ""
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10200*
  X = 19; Y = SLN; TYP = 1; MAXL = 3; O.R = "O"
  HMSG = "Lead Time"
  IF INV.LEAD.TIME<1,LN> # "" THEN
    DEFAULT = INV.LEAD.TIME<1,LN>
  END ELSE
    DEFAULT = 0
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  INV.LEAD.TIME<1,LN> = VALUE
10300*
  X = 27; Y = SLN; TYP = 1; MAXL = 7; O.R = "O"
  HMSG = "Package quantity"
  IF INV.PKG.QTY<1,LN> # "" THEN
    DEFAULT = INV.PKG.QTY<1,LN>
  END ELSE
    DEFAULT = 0
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  INV.PKG.QTY<1,LN> = VALUE
10400*
  X = 36; Y = SLN; TYP = 4; SCALER = 4; MAXL = 10; O.R = "O"
  HMSG = "Cost charged by vendor"
  IF INV.PRICE<1,LN> # "" THEN
    DEFAULT = OCONV(INV.PRICE<1,LN>, "MD4")
  END ELSE
    DEFAULT = "0.0000"
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  INV.PRICE<1,LN> = VALUE
10500*
  X = 48; Y = SLN; TYP = 1; MAXL = 15; O.R = "O"
  HMSG = "Vendor's product number"
  IF INV.PART.NO<1,LN> # "" THEN
    DEFAULT = INV.PART.NO<1,LN>
  END ELSE
    DEFAULT = 0
  END
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 10100
  INV.PART.NO<1,LN> = VALUE
  LINES = DCOUNT(INV.VENDOR,VM)
19999*
  RETURN
*
*---- Get Line Number
30000*
  GOSUB 80000
  ECD.MINV = ST.LINE
  ECD.MAXV = LST.LINE
  ECD.NUM = 30
  ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*---- Display Multi Line Area
80000*
  ST.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
  LST.LINE = ST.LINE + PAGE.SIZE -1
  IF LST.LINE > LINES THEN LST.LINE = LINES
  IF ST.LINE = OLD.START THEN RETURN
  OLD.START = ST.LINE
  CNT = 1
  FOR N = ST.LINE TO LST.LINE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 1 ; P_Y = SSLN ; P_VALUE = N "R#3" ; P_OPT = "CL"
    P_X  := AM:6 ; P_Y := AM:SSLN ; P_VALUE := AM:INV.VENDOR<1,N> "L#8"
    P_X  := AM:19 ; P_Y := AM:SSLN ; P_VALUE := AM:INV.LEAD.TIME<1,N> "L#3"
    P_X  := AM:27 ; P_Y := AM:SSLN ; P_VALUE := AM:INV.PKG.QTY<1,N> "L#7"
    P_X  := AM:36 ; P_Y := AM:SSLN ; P_VALUE := AM:OCONV(INV.PRICE<1,N>, "MD4") "R#10"
    P_X  := AM:48 ; P_Y := AM:SSLN ; P_VALUE := AM:INV.PART.NO<1,N> "L#15"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    MATREAD VEND.REC FROM VEND, CONO:INV.VENDOR<1,N> ELSE
      MAT VEND.REC = ""; VEND.DESC = STR("?",30)
    END
    P_X  = 6 ; P_Y = SSLN+1 ; P_VALUE = VEND.DESC "L#30" ; P_OPT = ""
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SSLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SSLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    P_X  = 0 ; P_Y = SSLN+1 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT N
  RETURN
*
*---- Error Routines
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
99999 *
  ECD.ACTION=99;CALL SCRN.EDIT
  RETURN
END
