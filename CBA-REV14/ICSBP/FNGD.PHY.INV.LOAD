*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - ICS
* SOURCE      - ICSBP
* PROGRAM     - FNGD.PHY.INV.LOAD
* DATE        - 08/10/2004
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DESCRIPTION - This program builds the physical inventory data records
*               for inital products so they will come up in the phyiscal
*               inventory maintenance screen.
*
*T28207 cmykleb 08/10/2004 * Initial programming.
*********************************************************************
*
*-----   INSERT EQUATES FROM CPYLIB
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>PHY.INV
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
   DEFFUN CURRENT.PERIOD(CONO,CONTROL.FILE,DIV.POS,FISCAL.FLAG)
   DEFFUN DIVISION.POSITION(CONO,CONTROL.FILE,DIV.CODE)
*
*-----   INITIALIZATION
*
   GEN.DIV = '00'
   TODAY = DATE()
   OMLINE = ''
   OPLINE = ''
   OWHSE = ''
   OPROD = ''
   FIRST = 1
   REC.CNT = 0
   MAT PHYSI.REC = ''
*
*-----   SET UP THE ERRMSG ROUTINE FOR SYSCOM
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*-----   OPEN THE FILES
*
   OPEN "","INV.WHSE" TO INV.WHSE ELSE
      ERRMSG = "INV.WHSE FILE IS MISSING"
      GOTO 93000
   END
   OPEN "","INVENTORY" TO INVENTORY ELSE
      ERRMSG = "INVENTORY FILE IS MISSING"
      GOTO 93000
   END
   OPEN "","WAREHOUSE" TO WAREHOUSE ELSE
      ERRMSG = "WAREHOUSE FILE IS MISSING"
      GOTO 93000
   END
   OPEN "","PHY.INV" TO PHY.INV ELSE
      ERRMSG = "PHY.INV FILE IS MISSING"
      GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING"
      GOTO 93000
   END
*
*-----   READ COMPANY NAME
*
   PROCREAD ID ELSE
      ERRMSG = " CANNOT PERFORM PROCREAD"
      GOTO 93000
   END
   CONO = ID<1>
100 *
   READNEXT KEY, MV.NO ELSE
      IF REC.CNT # "0" THEN
         GOSUB 3000
      END
      GOTO 99999
   END
   GOSUB 2000
   MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
      MAT IWH.REC = ""
      GOTO 100
   END
   MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
      MAT INV.REC = ""
   END
   WHSE.ID = CONO:WHSE
   MATREAD WHSE.REC FROM WAREHOUSE,WHSE.ID THEN
      IF WHS.DIV='' THEN WHS.DIV=GEN.DIV
      DIV.POS=DIVISION.POSITION(CONO,CONTROL,WHS.DIV)
      IF DIV.POS<1,1>='' THEN DIV.POS=DIV.POS<1,2>
      PERIOD=CURRENT.PERIOD(CONO,CONTROL,DIV.POS,"IC")
      IF PERIOD<1,1>='' THEN PERIOD=PERIOD<1,2>
   END
   MULTI.RSK = 1
   IF FIRST THEN
      OMLINE = INV.M.LINE
      OPLINE = INV.LINE
      OWHSE = WHSE
      OPROD = PROD
      FIRST = 0
   END
   BEGIN CASE
      CASE INV.M.LINE # OMLINE
         GOSUB 3000
         GOSUB 1000
      CASE INV.LINE # OPLINE
         GOSUB 3000
         GOSUB 1000
      CASE WHSE # OWHSE
         GOSUB 3000
         GOSUB 1000
      CASE PROD # OPROD AND MULTI.RSK
         GOSUB 3000
         GOSUB 1000
      CASE 1
         GOSUB 1000
   END CASE
   OMLINE = INV.M.LINE
   OPLINE = INV.LINE
   OWHSE = WHSE
   OPROD = PROD
   GOTO 100
*
*----- UPDATE THE RECORD
*
1000 *
   REC.CNT = REC.CNT + 1
   PHYSI.PERIOD = PERIOD
   PHYSI.PRODUCT<1,REC.CNT> = PROD
   PHYSI.STATUS = "PRINT"
   PHYSI.PRINT.DATE = TODAY
   PHYSI.MLINE = INV.M.LINE
   PHYSI.DESC<1,REC.CNT> = INV.DESC
   PHYSI.COLOR<1,REC.CNT> = INV.COLOR
   PHYSI.BSWT<1,REC.CNT> = INV.BAS.WT
   PHYSI.MWT<1,REC.CNT> = INV.M.WT
   PHYSI.RECP.NO<1,REC.CNT> = 'NEW'
   PHYSI.PLINE = INV.LINE
   PHYSI.WHSE = WHSE
   RETURN
*
*----- GET KEYS TO PRODUCT, WHSE
*
2000 *
   PROD.KEY = FIELD(KEY,"!",1)
   PROD = PROD.KEY[4,99]
   WHSE = FIELD(KEY,"!",2)
   RETURN
*
*----- WRITE PHY.INV
*
3000 *
   FND = 1
   LINE.CNT = 0
   READU REF.NUM FROM CONTROL,"PHY.INV" ELSE REF.NUM = 100
   REF.NUM = REF.NUM + 1
   READU PHYI.REC FROM PHY.INV,CONO:REF.NUM ELSE FND = 0
   IF FND = 1 THEN
      LOOP
      UNTIL FND = 0 DO
         RELEASE PHY.INV,CONO:REF.NUM
         REF.NUM = REF.NUM + 1
         READU PHYI.REC FROM PHY.INV,CONO:REF.NUM ELSE FND = 0
      REPEAT
   END
   MATWRITE PHYSI.REC ON PHY.INV,CONO:REF.NUM
   WRITE REF.NUM ON CONTROL,"PHY.INV"
   REC.CNT = 0
   MAT PHYSI.REC = ""
   RETURN
*
*-----   CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 *
END
