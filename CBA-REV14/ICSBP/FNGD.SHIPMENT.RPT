**********************************************
* REVISION    - [12.0]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PUGBP
* PROGRAM     - FNGD.SHIPMENT.RPT
* BY          - wyamout; CBA
* DATE        - 04/28/2004
* DESCRIPTION -
*
*T28059 wyamout 04/28/2004 * New fngd shipment report.
*T29032 cmyklebu 12/29/2006 * Move pgm from rev12 to rev14.
*ENDDOC
**********************************************
*   CPYLIBS
*COPY>OPS.CPYLIB>BOL
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
***   SET UP THE ERRMSG ROUTINE FOR SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
***   OPEN THE FILE
   OPEN '','INV.WHSE' TO INV.WHSE ELSE
      ERRMSG = 'INV.WHSE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'INVENTORY FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','INV_RECEIPTS' TO INV_RECEIPTS ELSE
      ERRMSG = 'INV_RECEIPTS FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','BOL' TO BOL ELSE
      ERRMSG = 'BOL FILE IS MISSING'
      GOTO 93000
   END
***   READ COMPANY NAME
   PROCREAD BUFFER ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = BUFFER<1>
   REPORT.NUMBER = BUFFER<2>
   SEL.WHSE = BUFFER<3>
   SEL.PROD = BUFFER<4>
   SEL.PLINE = BUFFER<5>
   SEL.CUST = BUFFER<6>
   SEL.PERIOD = BUFFER<7>
* SETUP HEADING
   SP1 = " "
   REPORT.NAME = ""
   CONO.NAME = ""
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
   HD3 = SPACE(57) : "FOR PERIOD = " : SEL.PERIOD
   HD3 := " AND CUSTOMER = " : SEL.CUST
   HH.LINE = ""
   HH.LINE := SP1 "L#11" : "PRODUCT FULL DESCRIPTION" : SP1 "L#11"
   HH.LINE := "  BOL   "
   HH.LINE := " CUST  "
   HH.LINE := "SHP-DATE "
   HH.LINE := " ORDER  "
   HH.LINE := "INVOICE  "
   HH.LINE := "SHP-QUANTITY"
   HH.LINE := SP1
   HH.LINE := "RECEIPT NUMBER "
   HH.LINE := SP1
   HH.LINE := "JOB NUM "
   HD.LINE = STR("-",45) "L#45 "
   HD.LINE := STR("-",7) "L#7 "
   HD.LINE := STR("-",6) "L#6 "
   HD.LINE := STR("-",8) "L#8 "
   HD.LINE := STR("-",7) "L#7 "
   HD.LINE := STR("-",8) "L#8 "
   HD.LINE := STR("-",12) "L#12"
   HD.LINE := SP1
   HD.LINE := STR("-",15) "L#15"
   HD.LINE := SP1
   HD.LINE := STR("-",8) "L#8"
   BD.LINE = SP1 "L#87"
   BD.LINE := STR("-",12)
*
   LINE.COUNT = 99
   PAGE.NO = 0
   PREV.WHSE = "!@#$%^&*"
   PREV.PLINE = ""
   PREV.PROD = ""
   PREV.PROD.DESC = ""
   PREV.RECP.NO = ""
   PREV.JOB = ""
   TOT.PROD = 0
   TOT.PLINE = 0
   PRT_DESC = "Y"
*   BODY OF PROGRAM SECTION
   MORE = 1
   LOOP
      READNEXT ID,LN_MV ELSE MORE = 0
   WHILE MORE DO
      IF CONO # ID[1,3] THEN CONTINUE
      MATREAD BOL.REC FROM BOL, ID ELSE CONTINUE
      IF BOL.STATUS = 'CANCEL' THEN CONTINUE
      IF BOL.PROD<1,LN_MV> = "" THEN CONTINUE
      IF SEL.WHSE # "ALL" AND BOL.WHSE<1,LN_MV> # SEL.WHSE THEN CONTINUE
      IF SEL.PROD # "ALL" AND BOL.PROD<1,LN_MV> # SEL.PROD THEN CONTINUE
      MATREAD INV.REC FROM INVENTORY, CONO:BOL.PROD<1,LN_MV> ELSE CONTINUE
      IF SEL.PLINE # "ALL" AND INV.LINE # SEL.PLINE THEN CONTINUE
      IF MORE = 1 THEN
         GOSUB 800
         MORE += 1
         PRINTER ON
      END
      BEGIN CASE
      CASE PREV.WHSE # BOL.WHSE<1,LN_MV>
         GOSUB 2000
         GOSUB 3000
         GOSUB 4000
         GOSUB 800
         LINE.COUNT = 99
      CASE PREV.PLINE # INV.LINE
         GOSUB 2000
         GOSUB 3000
         GOSUB 800
         LINE.COUNT = 99
      CASE PREV.PROD # BOL.PROD<1,LN_MV>
         GOSUB 2000
         GOSUB 800
         PRINT
         LINE.COUNT += 1
      END CASE
      IF BOL.FI.NO<1,LN_MV,1> # "" THEN
*        IWH_ID = CONO : BOL.PROD<1,LN_MV> :"!": BOL.WHSE<1,LN_MV>
*        MATREAD IWH.REC FROM INV.WHSE, IWH_ID ELSE MAT IWH.REC = ""
*        PREV.RECP.NO = IWH.RECP.NO<1,BOL.FI.NO<1,LN_MV,1>>
         PREV.RECP.NO = BOL.FI.NO<1,LN_MV,1>
      END ELSE
         PREV.RECP.NO = ""
      END
      IF PREV.RECP.NO # "" THEN
         MATREAD INVR.REC FROM INV_RECEIPTS, CONO : PREV.RECP.NO ELSE MAT INVR.REC = ""
      END ELSE
         MAT INVR.REC = ""
      END
      PREV.JOB = INVR.JOB
      GOSUB 1000
   REPEAT
   IF PREV.WHSE # "!@#$%^&*" THEN
      GOSUB 2000
      GOSUB 3000
      GOSUB 4000
      PRINTER OFF
   END
   GOTO 999999
*
*   STANDARD SUBROUTINES
*
* INV CNV
800*
*COPY>ICSBP>INV.UM.CNV
   PREV.WHSE = BOL.WHSE<1,LN_MV>
   PREV.PLINE = INV.LINE
   PREV.PROD = BOL.PROD<1,LN_MV>
   PREV.PROD.DESC = INV.FULL.DESC
   PRT_DESC = "Y"
   RETURN
* PRINT LINE
1000*
   IF LINE.COUNT > 56 THEN GOSUB 8000
   LINE.COUNT += 1
   IF PRT_DESC = "Y" THEN
      VLINE = PREV.PROD.DESC "L#45 "
      PRT_DESC = "N"
   END ELSE
      VLINE = SP1 "L#45 "
   END
   VLINE := ID[4,7] "L#7 "
   VLINE := BOL.CUST "L#6 "
   VLINE := OCONV(BOL.SHP.DATE, "D2/") "L#8 "
   VLINE := BOL.ORDER<1,1> "L#7 "
   VLINE := BOL.INVOICE "L#8 "
   SQTY = SUM(BOL.QTY<1,LN_MV>)
   IF SQTY < 0 THEN
      CQTY = INT(((SQTY/ICR.DV1)*ICR.MT1)/ICR.DV2 - 0.5)
   END ELSE
      CQTY = INT(((SQTY/ICR.DV1)*ICR.MT1)/ICR.DV2 + 0.5)
   END
   VLINE := OCONV(CQTY,ICR.CNV) "R#12"
   VLINE := SP1 "L#1"
   VLINE := PREV.RECP.NO "L#15"
   VLINE := SP1 "L#1"
   VLINE := PREV.JOB "L#10"
   PRINT VLINE
   TOT.PROD += CQTY
   TOT.PLINE += CQTY
   RETURN
* PRODUCT BREAK
2000*
   IF LINE.COUNT + 2 > 56 THEN GOSUB 8000
   LINE.COUNT += 2
   PRINT BD.LINE
   VLINE = SP1 "L#50"
   VLINE := "TOTAL FOR PRODUCT -  " : PREV.PROD "L#15 "
   VLINE := OCONV(TOT.PROD,ICR.CNV) "R#12"
   PRINT VLINE
   TOT.PROD = 0
   RETURN
* CATEGORY BREAK
3000*
   IF LINE.COUNT + 2 > 56 THEN GOSUB 8000
   LINE.COUNT += 2
   PRINT BD.LINE
   VLINE = SP1 "L#53"
   VLINE := "TOTAL FOR PRODUCT LINE - " : PREV.PLINE "L#8 "
   VLINE := OCONV(TOT.PLINE,ICR.CNV) "R#12"
   PRINT VLINE
   TOT.PLINE = 0
   RETURN
* WAREHOUSE BREAK
4000*
   IF LINE.COUNT > 56 THEN GOSUB 8000
   RETURN
* PRINT HEADING
8000*
   PRINT NEW.PAGE:
   PAGE.NO=PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT HD3
   PRINT "WAREHOUSE : ":PREV.WHSE
   PRINT "PLINE     : ":PREV.PLINE
   PRINT
   PRINT HH.LINE
   PRINT HD.LINE
   LINE.COUNT=8
   PRT_DESC = "Y"
   RETURN
*
***   CALLS FOR SYSCOM
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
END
