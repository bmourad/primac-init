*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - ICS
* SOURCE      - ICSBP
* PROGRAM     - PHY.DAT.COL.RPT
* DATE        - 12 /11/85
* BY          - JIHAD YAMOUT , C.B.A
* DESCRIPTION - This report is used for physical inventory data 
*                   collection.
*  C26123 GAT 7/2/96 * FIX PRINT PROBLEM
*T26090 ajibaly 03/27/2002 * CONVERT TO R12 REPORT.SCRN
*T27705 lross 10/23/2003 * Expand serial to 15.
*********************************************************************
*
***   INSERT EQUATE FROM CPYLIB
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>CHAR
*
***   INITIALIZATION
*
      LINE.COUNT        = 99
      PAGE.NO           = 0
      SP1 = SPACE(1)
      D5 = STR('_',5)
      D10 = STR('_',10)
      D11 = STR('_',11)
      D15 = STR('_',15)
      D20 = STR('_',20)
*
***   SET UP THE ERRMSG ROUTINE FOR SYSCOM
*
           SYS.TYPE = 1
             CALL SYSCOM(MAT SYSCOM.REC)
*
***   OPEN THE FILE
*
   OPEN '','INV.WHSE' TO INV.WHSE ELSE
       ERRMSG = 'INV.WHSE FILE IS MISSING'
       GOTO 93000
   END
   OPEN '','INV.WHSE.LOC' TO INV.WHSE.LOC ELSE
       ERRMSG = 'INV.WHSE.LOC FILE IS MISSING'
       GOTO 93000
   END
   OPEN '','INVENTORY' TO INVENTORY ELSE
       ERRMSG = 'INVENTORY FILE IS MISSING'
       GOTO 93000
   END
*
***   READ COMPANY NAME 
*
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
*T26090v
*  CONO.NAME = ID<2>
   CONO.NAME = ""
*  REPORT.NAME = "PHYSICAL INVENTORY DATA COLLECTION"
   REPORT.NAME = ""
*  REPORT.NUM = "XXXX"
   REPORT.NUM = ID<2>
*T26090^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUM,REPORT.DATE,HD1,HD2)
*
***   PUT PRINTER ON
*
   PRINTER ON
*
***   READ AND PROCESS FIRST RECORD
*
100*
   READNEXT KEY ELSE GOTO 999999
   GOSUB 200
*
***   READ THE THREE FILE
*
   MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
      MAT IWH.REC = ''
      GOTO 100
   END
   MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
      MAT INV.REC = ''
   END
*
*** SET WAREHOUSE NUMBER
*
   PREV.W = WHSE
   PREV.PL = INV.LINE
*
***   SET THE HEADING LINES
*
*T27705v HH.LINE = "PRODUCT NUMBER           DESCRIPTION            COLOR   BAS WT  M/WHT LOC #   ROLL/SKID #   QTY/WHT/SHT       COMMENTS      "
   HH.LINE = "PRODUCT NUMBER           DESCRIPTION            COLOR   BAS WT  M/WHT LOC #      SERIAL #   QTY/WHT/SHT       COMMENTS      "
   DH.LINE = "--------------- ------------------------------ -------- ------ ------ ----- --------------- ----------- --------------------"
*
***   PROCESS THE FILE
*
   GOSUB 1000
*
***    READ AND PROCESS ALL THE FILES
*
   DATA = 0
LOOP
   READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
     GOSUB 200
***   READ THE THREE FILES
     MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
        MAT IWH.REC = ''
        GOTO 111
     END
     MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
        MAT INV.REC = ''
     END
***   PROCESS THE RECORD
     GOSUB 1000
111*
REPEAT
***    END OF JOB
   GOTO 999999
***   SEPERATE THE KEY TO PRODUCT, WHSE 
200*
   PROD.KEY = FIELD(KEY,"!",1)
   PROD = PROD.KEY[4,99]
   WHSE = FIELD(KEY,"!",2)
RETURN
***   PROCESS THE RECORD
1000*
   IF WHSE # PREV.W OR PREV.PL # INV.LINE THEN
      LINE.COUNT = 99
      PREV.W = WHSE
      PREV.PL = INV.LINE
   END
***  ROUTINE TO PRINT THE RECORD
20000*
   P.LINE = PROD "L#15" :SP1: INV.DESC "L#30":SP1
   P.LINE = P.LINE:INV.COLOR "L#8":SP1
   P.LINE = P.LINE:OCONV(INV.BAS.WT, "MD2") "R#6":SP1
   P.LINE = P.LINE:INV.M.WT "R#6":SP1
   LOC.CNT = COUNT(IWH.LOC,VM) + (IWH.LOC # "")
   FOR L = 1 TO LOC.CNT
     IF L = 1 THEN
       P.LINE = P.LINE:D5 :SP1
     END ELSE
       P.LINE = SPACE(70):D5:SP1
     END
     MATREAD IWLO.REC FROM INV.WHSE.LOC , KEY:"!":IWH.LOC<1,L> ELSE
        MAT IWLO.REC = ""
     END
*T26090v
*    IF IWLO.R.S.ID = "" THEN
     IF IWLO.SERIAL = "" THEN
*T26090^
        RS.CNT = 1
     END ELSE
*T26090v
*       RS.CNT = DCOUNT(IWLO.R.S.ID,VM)
        RS.CNT = DCOUNT(IWLO.SERIAL,VM)
*T26090^
     END
     FOR RS = 1 TO RS.CNT
       IF RS = 1 THEN
          P.LINE = P.LINE :D15:SP1:D11:SP1:D20
       END ELSE
          P.LINE = SPACE(76):D15:SP1:D11:D20
       END
       IF LINE.COUNT + 2 > 56 THEN GOSUB 80000
       PRINT P.LINE
       PRINT
       LINE.COUNT = LINE.COUNT + 2
     NEXT RS
   NEXT L
RETURN
***  ROUTINE TO PRINT HEADINGS
80000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   H.LINE = HD1:PAGE.NO
   PRINT H.LINE
   H.LINE = HD2
   PRINT H.LINE'L#132'
   H.LINE = ""
   H.LINE = H.LINE:"MLINE / PLINE : ":D10:" / ":D10:SPACE(71):"SEQUENCE   : ":D10
   PRINT H.LINE'L#132'
   H.LINE = ''
   H.LINE = H.LINE:"WAREHOUSE     : ":D5:SP1:D20:SPACE(67):"DATE TAKEN : ":D10
   PRINT H.LINE'L#132'
   PRINT
   PRINT HH.LINE
   PRINT DH.LINE
   PRINT
   LINE.COUNT = 7
   RETURN
***   CALLS FOR SYSCOM
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINTER OFF
   END
