*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - ICS.EOM.POST
* BY          - EDVARD PITKA
* DATE        - 08/17/01
* DESCRIPTION - This program used to run inventory system end of
*             month post.
*T26685 lhelms 07/03/2002 * REV12 DIVISION SECURITY
*T27454 lross 05/23/2003 * Need to reconcile COA.LEVEL for
*                          INV.GLA.TRANS.
*T28032 lross 03/26/2004 * If 2nd screen is 'E'xited, blank out FR.NEXT.PER
*                          so PROCWRITE 'END' will occur.
*T28249 lross 10/13/2004 * Check INAH.TO.DVDPCC for INTR.DIV.XFERS.
*ENDDOC
*********************************************************************
*
***** INSERT FILE EQUATE
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>GLTABLE
*COPY>PMC.CPYLIB>SALESDATES
*COPY>PMC.CPYLIB>FISCAL
*COPY>PMC.CPYLIB>COA
*COPY>PMC.CPYLIB>POST.REJECTS
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>PMC.CPYLIB>EOM.TRANS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
**** INTITIALIZATION
*
MAT FILE.VARS=''
MAT EDIT.COM.DRIVER=''
GEN.DIV = '00';GEN.DEPT = '00';GEN.CCTR = '000'
*
***** SETUP ERRMSG ROUTINE
*
SYS.TYPE=1
CALL SYSCOM(MAT SYSCOM.REC)
*
*--- PROCREAD
*
PROCREAD INBUFF ELSE
   ERRMSG = "MUST RUN FROM PROC"
   GOSUB 91000; GOTO 99999
END
CONO = INBUFF<1>
DIV.CODE = INBUFF<7>;* T23278
FR.NEXT.PER = ""
*
***** OPEN FILES
*
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
OPEN '','PMC.SCREENS' TO M.SCREENS ELSE ERRMSG='PMC.SCREENS FILE IS MISSING';GOTO 93000
OPEN '','COA' TO COA ELSE ERRMSG='COA FILE IS MISSING';GOTO 93000
OPEN '','INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE ERRMSG='INV_AUDIT_HIST FILE IS MISSING';GOTO 93000
OPEN '','INV_AUDIT_TAG' TO INV_AUDIT_TAG ELSE ERRMSG='INV_AUDIT_TAG FILE IS MISSING';GOTO 93000
OPEN '','INVENTORY' TO INVENTORY ELSE ERRMSG='INVENTORY FILE MISSING';GOTO 93000
OPEN '','CATEGORY' TO CATEGORY ELSE ERRMSG='CATEGORY FILE MISSING';GOTO 93000
OPEN '','WAREHOUSE' TO WAREHOUSE ELSE ERRMSG='WAREHOUSE FILE MISSING';GOTO 93000
OPEN '','EOD.HIST' TO EOD.HIST ELSE ERRMSG='EOD.HIST FILE MISSING';GOTO 93000
OPEN '',INBUFF<3> TO EOM.TRANS ELSE ERRMSG=INBUFF<3>:' FILE MISSING';GOTO 93000
OPEN '',INBUFF<4> TO POST.REJECTS ELSE ERRMSG=INBUFF<4>:' FILE IS MISSING';GOTO 93000
*
***** GET CONO
*
MATREAD COMP.REC FROM COMPANY, CONO ELSE
   ERRMSG = "COMPANY RECORD IS MISSING"
   GOTO 93000
END
SYS.FISCAL = "ICFISCAL"
STMT.HEAD = "INVENTORY"
*COPY>PMCBP>EOD.POST
IF ECD.RET.VALUE = "END" THEN
   FR.NEXT.PER<1,POS> = '' ;*T28032
   ECD.ACTION = 99 ; CALL SCRN.EDIT
   GOTO 99000
END
CLEARFILE EOM.TRANS
CLEARFILE POST.REJECTS
PRR.SEQ = 10000
OLD.PROD.WHSE = "!@#$%^&*"; OLD.PROD = "!@#$%^&*"; OLD.CATG = "!@#$%^&*"
MATREAD COA.REC FROM COA, CONO : GLTB.INV ELSE COA.LEVEL = 0
PD.INV.ACCT = GLTB.INV
PD.INV.LEVEL = COA.LEVEL
MATREAD COA.REC FROM COA, CONO : GLTB.INV.ADJ ELSE COA.LEVEL = 0
PD.ADJ.ACCT = GLTB.INV.ADJ
PD.ADJ.LEVEL = COA.LEVEL
DATA = 1
LOOP
   READNEXT INAH.ID ELSE DATA = 0
WHILE DATA DO
   IF CONO # INAH.ID[1,3] THEN GOTO 999
   IAHNO = INAH.ID[4,99]
   MATREADU INAH.REC FROM INV_AUDIT_HIST, INAH.ID ELSE
      RELEASE INV_AUDIT_HIST, INAH.ID
      MAT PRR.REC = ''
      PRR.JOB = IAHNO
      PRR.FILE = 'INV_AUDIT_HIST'
      PRR.ERR = 'CANNOT LOCATE'
      PRR.SEQ = PRR.SEQ + 1
      MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
      GOTO 999
   END
   IF INAH.GLA.DATE # "" AND INAH.GLA.DATE # "P" THEN
      RELEASE INV_AUDIT_HIST, INAH.ID
      GOTO 999
   END
200 
   IF INAH.PERIOD = '' THEN INAH.PERIOD = PERIOD
   IF INAH.PERIOD # PERIOD THEN
      RELEASE INV_AUDIT_HIST, INAH.ID
      GOTO 999
   END
   IF INAH.SRC = "MR" OR INAH.SRC="MA" THEN GOTO 209
   IF OLD.PROD # INAH.PROD THEN
      MATREAD INV.REC FROM INVENTORY, CONO:INAH.PROD ELSE
         RELEASE INV_AUDIT_HIST, INAH.ID
         MAT PRR.REC = ""
         PRR.JOB = IAHNO
         PRR.FILE = "INVENTORY"
         PRR.ID = INAH.PROD
         PRR.ERR = "CANNOT LOCATE"
         PRR.SEQ = PRR.SEQ + 1
         MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
         GOTO 999
      END
      OLD.PROD = INAH.PROD
   END
   IF OLD.CATG # INV.LINE THEN
      MATREAD CATG.REC FROM CATEGORY, CONO:INV.LINE ELSE
         RELEASE INV_AUDIT_HIST, INAH.ID
         MAT PRR.REC = ""
         PRR.JOB = IAHNO
         PRR.FILE = "CATEGORY"
         PRR.ID = INV.LINE
         PRR.ERR = "CANNOT LOCATE"
         PRR.SEQ = PRR.SEQ + 1
         MATWRITE PRR.REC ON POST.REJECTS, PRR.SEQ
         GOTO 999
      END
      OLD.CATG = INV.LINE
   END
209*
   BEGIN CASE
      CASE INAH.EXT.COST + 0 GE 0
         TRAN.COST = INAH.EXT.COST
         DB.CR = 2
      CASE INAH.EXT.COST + 0 < 0
         TRAN.COST = 0 - INAH.EXT.COST
         DB.CR = 1
      CASE 1
         MATWRITE INAH.REC ON INV_AUDIT_HIST, INAH.ID
         GOTO 999
   END CASE
   PROD.WHSE = INAH.PROD : "!" : INAH.WHSE
   IF OLD.PROD.WHSE # PROD.WHSE THEN
      OLD.PROD.WHSE = PROD.WHSE
      NEW.REC = 0; NEW.PTR = 0
   END
   P_X  = 3 ; P_Y = 23 ; P_VALUE = 'NOW PROCESSING INVENTORY TRANSACTION - ':IAHNO "L#10" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   BEGIN CASE
      CASE INAH.ACCT = PD.INV.ACCT
      CASE INAH.ACCT # ""
         INAH.ACCT :=STR("0",IN.ACCT.LEN-LEN(INAH.ACCT)) 
         INAH.ACCT = INAH.ACCT[1,IN.ACCT.LEN]
         MATREAD COA.REC FROM COA, CONO : INAH.ACCT ELSE COA.LEVEL = 0
         PD.INV.ACCT = INAH.ACCT
         PD.INV.LEVEL = COA.LEVEL
      CASE GLTB.INV = PD.INV.ACCT
      CASE 1
         MATREAD COA.REC FROM COA, CONO : GLTB.INV ELSE COA.LEVEL = 0
         PD.INV.ACCT = GLTB.INV
         PD.INV.LEVEL = COA.LEVEL
   END CASE
   BEGIN CASE
      CASE INAH.ADJ.ACCT = PD.ADJ.ACCT
      CASE INAH.ADJ.ACCT # ""
         INAH.ADJ.ACCT:=STR("0",IN.ACCT.LEN-LEN(INAH.ADJ.ACCT))
         INAH.ADJ.ACCT = INAH.ADJ.ACCT[1,IN.ACCT.LEN]
         MATREAD COA.REC FROM COA, CONO : INAH.ADJ.ACCT ELSE COA.LEVEL = 0
         PD.ADJ.ACCT = INAH.ADJ.ACCT
         PD.ADJ.LEVEL = COA.LEVEL
      CASE GLTB.INV.ADJ = PD.ADJ.ACCT
      CASE INAH.ADJ.ACCT=''
         NULL
      CASE 1
         MATREAD COA.REC FROM COA, CONO : GLTB.INV.ADJ ELSE COA.LEVEL = 0
         PD.ADJ.ACCT = GLTB.INV.ADJ
         PD.ADJ.LEVEL = COA.LEVEL
   END CASE
*T28249 v Should only be on the "OUT" side of INTR.DIV.XFER
   IF INAH.TO.DVDPCC # '' THEN INAH.DV.DP.CC = INAH.TO.DVDPCC
*T28249 ^
   IF INAH.DV.DP.CC = '' THEN INAH.DV.DP.CC = GEN.DIV:GEN.DEPT:GEN.CCTR ;*T27454
   IF INAH.ADJ.ACCT # "" THEN
      BEGIN CASE
         CASE PD.ADJ.LEVEL < 1
            T.ACCT=GEN.DIV:GEN.DEPT:GEN.CCTR
*T27454 v
         CASE PD.ADJ.LEVEL < 2
            T.ACCT=INAH.DV.DP.CC[1,2]:GEN.DEPT:GEN.CCTR
         CASE PD.ADJ.LEVEL < 3
            T.ACCT=INAH.DV.DP.CC[1,4]:GEN.CCTR
*        T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : PD.ADJ.ACCT
         CASE 1
*T27454 T.ACCT=STR("X",7-LEN(INAH.DV.DP.CC)):INAH.DV.DP.CC:PD.ADJ.ACCT
            T.ACCT=INAH.DV.DP.CC
      END CASE
      T.ACCT=STR("X",7-LEN(T.ACCT)):T.ACCT
      T.ACCT=T.ACCT:PD.ADJ.ACCT
      ON DB.CR GOSUB 1000,2000
   END
   BEGIN CASE
      CASE PD.INV.LEVEL < 1
         T.ACCT=GEN.DIV:GEN.DEPT:GEN.CCTR
*T27454 v
      CASE PD.INV.LEVEL < 2
         IF INAH.TO.DVDPCC # '' THEN
            T.ACCT=INAH.TO.DVDPCC[1,2]:GEN.DEPT:GEN.CCTR
         END ELSE T.ACCT=INAH.DV.DP.CC[1,2]:GEN.DEPT:GEN.CCTR
      CASE PD.INV.LEVEL < 3
         IF INAH.TO.DVDPCC # '' THEN
            T.ACCT=INAH.TO.DVDPCC[1,4]:GEN.CCTR
         END ELSE T.ACCT=INAH.DV.DP.CC[1,4]:GEN.CCTR
*T27454 T.ACCT=STR("X",7-LEN(T.ACCT)):T.ACCT
*T27454 T.ACCT=T.ACCT:PD.INV.ACCT
*      T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : PD.INV.ACCT
      CASE 1
         IF INAH.TO.DVDPCC # "" THEN               
*T27454 T.ACCT=STR("X",7-LEN(INAH.TO.DVDPCC)):INAH.TO.DVDPCC:PD.INV.ACCT
            T.ACCT=INAH.TO.DVDPCC
         END ELSE                                  
*T27454 T.ACCT=STR("X",7-LEN(INAH.DV.DP.CC)):INAH.DV.DP.CC:PD.INV.ACCT
            T.ACCT=INAH.DV.DP.CC
         END                                       
   END CASE
   T.ACCT=STR("X",7-LEN(T.ACCT)):T.ACCT
   T.ACCT=T.ACCT:PD.INV.ACCT
*T27454 ^
   ON DB.CR GOSUB 2000,1000
   HOLD.INAH.ACCR.DVDPCC = INAH.ACCR.DVDPCC ;*T27454
   GOSUB 3000
   INAH.ACCR.DVDPCC = HOLD.INAH.ACCR.DVDPCC ;*T27454
   INAH.GLA.DATE = "P"
   MATWRITE INAH.REC ON INV_AUDIT_HIST, INAH.ID
999 REPEAT
*
ECD.ACTION = 99 ; CALL SCRN.EDIT
GOTO 99999
*
**************************************************************************
*
*******
1000: 
*******
*
ETR.ID = CONO : T.ACCT : OLD.PROD.WHSE : "!D*" : NEW.REC
MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
   MAT ETR.REC = ""
END
ETR.AMT = ETR.AMT + TRAN.COST
LOCATE IAHNO IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
INS IAHNO BEFORE ETR.TRAN<1,PTR>
INS INAH.DATE BEFORE ETR.DATE<1,PTR>
INS TRAN.COST BEFORE ETR.TAMT<1,PTR>
IF PTR > 99 THEN NEW.PTR = 1
MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
RETURN
*
*******
2000: 
********
*
ETR.ID = CONO : T.ACCT : OLD.PROD.WHSE : "!C*" : NEW.REC
MATREAD ETR.REC FROM EOM.TRANS, ETR.ID ELSE
   MAT ETR.REC = ""
END
ETR.AMT = ETR.AMT - TRAN.COST
LOCATE IAHNO IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL
INS IAHNO BEFORE ETR.TRAN<1,PTR>
INS INAH.DATE BEFORE ETR.DATE<1,PTR>
INS (0 - TRAN.COST) BEFORE ETR.TAMT<1,PTR>
IF PTR > 99 THEN NEW.PTR = 1
MATWRITE ETR.REC ON EOM.TRANS, ETR.ID
RETURN
*
*********
3000: 
*********
*
IF INAH.ACCR.ACCT # "" THEN 
*T27454 v
   MATREAD COA.REC FROM COA, CONO:INAH.ACCR.ACCT ELSE COA.LEVEL = 0
   TEMP.DVDPCC=''
   IF INAH.ACCR.DVDPCC = '' THEN INAH.ACCR.DVDPCC = INAH.DV.DP.CC
   IF INAH.ACCR.DVDPCC # '' THEN
     BEGIN CASE
     CASE COA.LEVEL < 1
       TEMP.DVDPCC=GEN.DIV:GEN.DEPT:GEN.CCTR
     CASE COA.LEVEL < 2
       TEMP.DVDPCC=INAH.ACCR.DVDPCC[1,2]:GEN.DEPT:GEN.CCTR
     CASE COA.LEVEL < 3
       TEMP.DVDPCC=INAH.ACCR.DVDPCC[1,4]:GEN.CCTR
     CASE 1
       TEMP.DVDPCC=INAH.ACCR.DVDPCC
     END CASE
     INAH.ACCR.DVDPCC=TEMP.DVDPCC
   END
*T27454 ^
   IF INAH.ACCR.DVDPCC # "" THEN               
      T.ACCT=STR("X",7-LEN(INAH.ACCR.DVDPCC)):INAH.ACCR.DVDPCC:INAH.ACCR.ACCT
   END ELSE
      T.ACCT=GEN.DIV:GEN.DEPT:GEN.CCTR
      T.ACCT=STR("X",7-LEN(T.ACCT)):T.ACCT
      T.ACCT=T.ACCT:INAH.ACCR.ACCT
*    T.ACCT = GEN.DIV : GEN.DEPT : GEN.CCTR : INAH.ACCR.ACCT
   END                                                      
   ETR.ID = CONO:T.ACCT:INAH.PROD:"!":INAH.WHSE                 
   TRAN.COST = 0-INAH.EXT.COST                                        
   IF TRAN.COST > 0 THEN                                        
      ETR.ID = ETR.ID:"!D*":INAH.SRC                             
   END ELSE                                                     
      ETR.ID = ETR.ID:"!C*":INAH.SRC                             
   END                                                          
   MATREADU ETR.REC FROM EOM.TRANS, ETR.ID ELSE MAT ETR.REC = ""
   ETR.AMT = ETR.AMT + TRAN.COST                                
   LOCATE IAHNO IN ETR.TRAN<1>,1 BY "AR" SETTING PTR ELSE NULL  
   ETR.TRAN = INSERT(ETR.TRAN,1,PTR,0,IAHNO)                    
   ETR.DATE = INSERT(ETR.DATE,1,PTR,0,INAH.DATE)                
   ETR.TAMT = INSERT(ETR.TAMT,1,PTR,0,TRAN.COST)                
   MATWRITE ETR.REC ON EOM.TRANS, ETR.ID                        
END
RETURN
91000 ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99000 
IF FR.NEXT.PER<1,POS> = "" THEN
   INBUFF<5> = "END"
   PROCWRITE INBUFF
END
99999 END
