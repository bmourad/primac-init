*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - ICS
* SOURCE      - ICSBP
* PROGRAM     - DATA.COLL.SHT.PRT
* DATE        - 02/26/97
* BY          - JULIANNE RIVERA, VERCOM SOFTWARE, INC
* DESCRIPTION - This report is used for physical inventory data
*                   collection.
* TASK 20826 JR CHANGE CATG.MAJ.LINE AND CATG.R.S.ID
* TASK 21604 JR INVENTORY REWRITE OF PHYSICAL INVENTORY
*T25740 epitka 03/05/2002 * Rev12
*T26090 ajibaly 03/28/2002 * Convert TO Rev12 REPORT.SCRN
*T26556 cmykleb 04/23/2002 * Inventory report changes.
*********************************************************************
*
*-----   INSERT EQUATES FROM CPYLIB
*
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>ICS.CPYLIB>PHY.INV
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*-----   INITIALIZATION
*
PAGE.NO=0
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
OPEN "","WAREHOUSE" TO WAREHOUSE ELSE
   ERRMSG = "WAREHOUSE FILE IS MISSING"
   GOTO 93000
END
OPEN "","PHY.INV" TO PHY.INV ELSE
   ERRMSG = "PHY.INV FILE IS MISSING"
   GOTO 93000
END
OPEN "","CONTROL" TO CONTROL ELSE
   ERRMSG = "CONTROL FILE IS MISSING"
   GOTO 93000
END
*
PROCREAD ID ELSE
   ERRMSG = " CANNOT PERFORM PROCREAD"
   GOTO 93000
END
CONO = ID<1>
REPRINT.FLAG = ID<9> ; * T26556
PROD = ID<10>
REPORT.NAME = ""
REPORT.NUM  = ID<2>
CONO.NAME   = ""
REPORT.DATE = DATE()
HD1 = ";HD2 = "
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUM,REPORT.DATE,HD1,HD2)
*
PRINTER ON
FIRST = 1
TODAY = DATE()
MAT PHYSI.REC = ""
LINE.CNT = 0
;*
;* Main Loop
;*
DONE=0
LOOP
   READNEXT ID ELSE DONE =1 
UNTIL DONE DO
   MATREADU PHYSI.REC FROM PHY.INV, ID ELSE
      MAT PHYSI.REC = ""
   END
   IF PHYSI.STATUS<1,1>='' OR REPRINT.FLAG='Y' THEN
      GOSUB PRINT.HEADING
      REC.CNT = DCOUNT(PHYSI.PRODUCT,VM)
      FOR LPTR = 1 TO REC.CNT
         GOSUB PRINT.LINE
      NEXT LPTR
      IF REPRINT.FLAG # "Y" THEN
         MATWRITE PHYSI.REC ON PHY.INV, ID
      END
      GOSUB PRINT.FOOTING
   END
REPEAT
GOTO 999999
*
************************************************************************
***** S U B R O U T I N E S ********************************************
************************************************************************
*
****************
PRINT.FOOTING: 
****************
*
HD99 = "_______________ ______________________________ ________ ________ ______ _______________ ______________ ________ _______________"
FOR XXX = 1 TO 3
   PRINT;PRINT HD99
NEXT XXX
RETURN
*
*************
PRINT.LINE: 
*************
*
IF LINE.CNT > 50 THEN
   GOSUB PRINT.HEADING
END
IF REPRINT.FLAG='Y' OR PHYSI.STATUS<1,LPTR>='' THEN
   IF PROD # "ALL" AND PHYSI.PRODUCT<1,LPTR> # PROD THEN
      NULL
   END ELSE
      PHYSI.STATUS<1,LPTR> = "PRINT"
      PHYSI.PRINT.DATE = TODAY
      DLINE = PHYSI.PRODUCT<1,LPTR>"L#15 "
      DLINE = DLINE:PHYSI.DESC<1,LPTR>"L#30 "
      DLINE = DLINE:PHYSI.COLOR<1,LPTR>"L#8 "
      DLINE = DLINE:OCONV(PHYSI.BSWT<1,LPTR>,"MD2Z")"R#8 "
      DLINE = DLINE:OCONV(PHYSI.MWT<1,LPTR>,"MD0Z")"R#6 "
      IF PHYSI.SERIAL<1,LPTR> # "" THEN
         DLINE = DLINE:PHYSI.SERIAL<1,LPTR>"L#15 "
      END ELSE
         DLINE = DLINE:PHYSI.RECP.NO<1,LPTR>"L#15 "
      END
      DLINE = DLINE:PHYSI.UNIT<1,LPTR>"L#3 "
      DLINE = DLINE:"______________ ________ _______________"
      PRINT
      PRINT DLINE
      LINE.CNT = LINE.CNT + 2
   END
END
RETURN
*
****************
PRINT.HEADING: 
****************
*
PAGE.NO = PAGE.NO + 1
WHSE.ID = CONO:PHYSI.WHSE
MATREAD WHSE.REC FROM WAREHOUSE, WHSE.ID ELSE MAT WHSE.REC = ""
HD0 = HD1:PAGE.NO
HD4="SEQUENCE ":ID[4,99] :SPACE(5)
HD4 := "WAREHOUSE : ":PHYSI.WHSE"L#4":" ":WHS.DESC"L#30"
HD4 = HD4:"   LOCATION : ":PHYSI.LOC"L#4"
HD5 = "ENTERED BY : _________________________  DATE : __________"
HD6 = "PRODUCT NUMBER            DESCRIPTION           COLOR"
HD6 = HD6:"     BAS WT  M/WGT SERIAL / RECP # UOM  QTY/WGT/SHT"
HD6 = HD6:"   DIAM       COMMENTS"
HD7 = "--------------- ------------------------------ --------"
HD7 = HD7:" -------- ------ --------------- --- --------------"
HD7 = HD7:" -------- ---------------"
PRINT CHAR(12)
PRINT HD0
PRINT HD2
PRINT
PRINT HD4
PRINT
PRINT HD5
PRINT
PRINT HD6
PRINT HD7
LINE.CNT = 9
RETURN
*
*-----   CALLS FOR SYSCOM
*
91000 *
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000 *
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000 *
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
999999 *
PRINTER OFF
END
