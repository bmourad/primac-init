*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.FULL.DESC.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 07/26/85
* DESCRIPTION -
* This program updates and maintains INV.FULL.DESC records
* in CONTROL files.
*T25978 adelgado 02/18/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>ICS.CPYLIB>INV.FULL.DESC
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE ERRMSG = "M.SCREENS FILE MISSING";GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = "END" THEN GOTO 99999
*
*---- INITIALIZATION
*     MAT EDIT.COM = ''
*     TYP = 0
*     CALL EDIT.SUB
  FILL = "#"
  MAT EDIT.COM.DRIVER = ''
  LINE.SPACE = 1
  PAGE.SIZE = 14
  BEGIN.PAGE = 6
  LINES = 0
  START.LINE = 0
  OLD.LINES = 0
  OPTION = ''
  UNKNOWN = STR('?',30)
  MAT IFD.REC = ""
*
*---- GET SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = "INV.FULL.DESC.MAINT"
  ECD.ACTION = 1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
  NEW.IFD = ""
  LN  =  1
  OLD.START.LINE  =  0
  LINES  =  0
  SCV.REC(1)<ECD.SCRN.NO,1> = DATE()
  ECD.NUM = 1;ECD.ACTION = 5;CALL SCRN.EDIT
  ECD.NUM = 2;ECD.ACTION = 4;CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
    CASE ECD.RET.VALUE # ''
      PAP.TYPE = ECD.RET.VALUE
      IFD.KEY = CONO:PAP.TYPE:"*":"INV.FULL.DESC"
      * T26126 v
      MATREADU IFD.REC FROM CONTROL,IFD.KEY LOCKED
        ERRMSG = 'MAJOR LINE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END ELSE
      * T26126 ^
        MAT IFD.REC = '';NEW.IFD = 'YES'
      END
  END CASE
  IF NEW.IFD = 'YES' THEN
    LOOP
      LN = LINES + 1
      OLD.LINES = LINES
      GOSUB 140
    WHILE LINES > OLD.LINES DO REPEAT
    LN = LINES
    LINES = COUNT(IFD.NO,VM) + (IFD.NO # '')
    GOSUB 160
  END ELSE
    LINES = COUNT(IFD.NO,VM) + (IFD.NO # '')
    GOSUB 160
  END
  GOSUB 1000
  GOSUB 2000
  GOTO 5
*
*---- ENTER SCREEN
*
140*
  GOSUB 160
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
  P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
  P_X  := AM:0 ; P_Y := AM:SLN ; P_VALUE := AM:LN "R#2"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
141*
  FILL = "#";X = 5;Y = SLN;TYP = 3;MAXL = 2;O.R = "O";MAXV = 10;MINV = 1
  DEFAULT = IFD.NO<1,LN>;HMSG = ""
  HMSG<1,1> = "1=INV.DESC;2=WIDTH;3=LENGTH;4=BASIS WEIGHT;5=M/WEIGHT;6=COLOR;7=FINISH"
  HMSG<1,2> = "8=BRIGHT;9=SHADE;10=PPI"
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = ''
      IF OPTION = "C" THEN GOTO 141
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 149
    CASE VALUE = "END"
      IF IFD.JUSTIFY<1,LN> = '' THEN
        IFD.NO = DELETE(IFD.NO,1,LN,0)
        P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END ELSE
        P_X  = 5 ; P_Y = SLN ; P_VALUE = IFD.NO<1,LN>"R#2" ; P_OPT = ""
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
        NUMBER = IFD.NO<1,LN>
        GOSUB 200
        P_X  = 10 ; P_Y = SLN ; P_VALUE = DESC "L#15" ; P_OPT = ""
        P_X  := AM:32 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.LEN<1,LN>"R#2"
        P_X  := AM:40 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.FIX<1,LN>"L#1"
        P_X  := AM:47 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.FILL<1,LN> "L#1"
        P_X  := AM:57 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.FILL.LEN<1,LN>"R#2"
        P_X  := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.SCALER<1,LN>"L#1"
        P_X  := AM:76 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.JUSTIFY<1,LN>"L#1"
        CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      GOTO 149
    CASE VALUE # ''
      LOCATE VALUE IN IFD.NO<1>,1 SETTING DD ELSE DD=0
      IF DD > 0 AND DD # LN THEN
        ERRMSG = "YOU HAVE ALREADY USED THIS NUMBER"
        GOSUB 91000;GOTO 141
      END
      IFD.NO<1,LN> = VALUE
      NUMBER = IFD.NO<1,LN>
      GOSUB 200
      P_X  = 10 ; P_Y = SLN ; P_VALUE = DESC "L#15" ; P_OPT = ""
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  END CASE
142*
  X = 32;Y = SLN;TYP = 3;MAXL = 2;O.R = "O";MAXV = 45;MINV = ''
  DEFAULT = IFD.LEN<1,LN>;HMSG = "ENTER LENGTH"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 141
  IF VALUE = '' THEN GOTO 142
  IFD.LEN<1,LN> = VALUE
143*
  X = 40;Y = SLN;TYP = 8;MAXL = 1;DEFAULT = IFD.FIX<1,LN>;O.R = "O"
  HMSG = "ENTER FIX (Y/N)"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 141
  IF VALUE = "" THEN GOTO 143
  IFD.FIX<1,LN> = VALUE
144*
  X = 47;Y = SLN;TYP = 1;MAXL = 1;O.R = "O";MAXV = '';MINV = ''
  DEFAULT = IFD.FILL<1,LN>;HMSG = "ENTER FILL"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 141
  IFD.FILL<1,LN> = VALUE
145*
  X = 57;Y = SLN;TYP = 3;MAXL = 2;O.R = "O"
  IF IFD.FILL.LEN<1,LN> = "" THEN
    DEFAULT = "1"
  END ELSE
    DEFAULT = IFD.FILL.LEN<1,LN>
  END
  HMSG = "ENTER FILL LENGTH"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 141
  IF VALUE = "" THEN GOTO 145
  IFD.FILL.LEN<1,LN> = VALUE
146*
  X = 68;Y = SLN;TYP = 3;MAXL = 1;O.R = "O";DEFAULT = IFD.SCALER<1,LN>
  MINV = 0;MAXV = 4
  HMSG = "ENTER SCALER (2,1,0)"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 141
  IFD.SCALER<1,LN> = VALUE
147*
  X = 76;Y = SLN;TYP = 1;MAXL = 1;O.R = "O"
  IF IFD.JUSTIFY<1,LN> = "" THEN
    DEFAULT = "L"
  END ELSE
    DEFAULT = IFD.JUSTIFY<1,LN>
  END
  HMSG = "ENTER JUSTIFY (L/R)"
  CALL EDIT.SUB
  IF VALUE = "END" THEN GOTO 141
  IF VALUE = "" THEN GOTO 147
  IF VALUE = "L" OR VALUE = "R" THEN
    IFD.JUSTIFY<1,LN> = VALUE
  END ELSE
    ERRMSG = 'INVALID INPUT, MUST ENTER "L" OR "R" ONLY'
    GOSUB 91000;GOTO 147
  END
149*
  LINES = COUNT(IFD.NO,VM) + (IFD.NO # '')
  RETURN
*
*---- SCROLL ROUTINE
*
160*
  START.LINE = 1 + INT((LN-1) / PAGE.SIZE) * PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN GOTO 169
  OLD.START.LINE = START.LINE
  LAST.LINE = START.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR L = START.LINE TO LAST.LINE UNTIL L > LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(L-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = L "R#2" ; P_OPT = ""
    P_X  := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.NO<1,L>"R#2"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    NUMBER = IFD.NO<1,L>
    GOSUB 200
    P_X  = 10 ; P_Y = SLN ; P_VALUE = DESC "L#15" ; P_OPT = ""
    P_X  := AM:32 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.LEN<1,L>"R#2"
    P_X  := AM:40 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.FIX<1,L>"L#1"
    P_X  := AM:47 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.FILL<1,L> "L#1"
    P_X  := AM:57 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.FILL.LEN<1,L>"R#2"
    P_X  := AM:68 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.SCALER<1,L>"L#1"
    P_X  := AM:76 ; P_Y := AM:SLN ; P_VALUE := AM:IFD.JUSTIFY<1,L>"L#1"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT = CNT + 1
  NEXT L 
  FOR M = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
169*
  RETURN
*
*---- GET LINE NUMBER
*
170*
  GOSUB 160
  ECD.NUM = 12
  IF LAST.LINE < LINES THEN
    ECD.MAXV = LAST.LINE
  END ELSE
    ECD.MAXV = LINES
  END
  SCV.REC(12)<ECD.SCRN.NO,1> = ''
  ECD.ACTION = 4;CALL SCRN.EDIT
  P_X  = 0 ; P_Y = 21 ; P_VALUE = "" ; P_OPT = "CL"
  CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  LNO = ECD.RET.VALUE
  IF LNO = '' OR LNO = "END" THEN LNO = 0
  RETURN
*---- GET DESCRIPTION
200*
  BEGIN CASE
    CASE NUMBER = "1"
      DESC = "INV. DESC"
    CASE NUMBER = "2"
      DESC = "WIDTH"
    CASE NUMBER = "3"
      DESC = "LENGTH"
    CASE NUMBER = "4"
      DESC = "BASIS WEIGHT"
    CASE NUMBER = "5"
      DESC = "M/WEIGHT"
    CASE NUMBER = "6"
      DESC = "COLOR"
    CASE NUMBER = "7"
      DESC = "FINISH"
    CASE NUMBER = "8"
      DESC = "BRIGHT"
    CASE NUMBER = "9"
      DESC = "SHADE"
    CASE NUMBER = "10"
      DESC = "PPI"
    CASE 1
      DESC = UNKNOWN
  END CASE
  RETURN
*
*---- OPTIONS FOR SCREEN
*
1000*
  MORE = 0
  LOOP
    SCV.REC(11)<ECD.SCRN.NO,1> = ''
    ECD.NUM = 11;ECD.ACTION = 4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = "END" OR OPTION = "E"
        MORE = 1
      CASE OPTION = "A"
        LINES = COUNT(IFD.NO,VM) + (IFD.NO # '')
        LOOP
          LN = LINES + 1
          OLD.LINES = LINES
          GOSUB 140
        WHILE LINES > OLD.LINES DO REPEAT
        LN = LINES
        GOSUB 160
      CASE OPTION = "S"
        LN = LN + PAGE.SIZE
        IF LN > LINES THEN LN = 1
        GOSUB 160
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB 160
      CASE OPTION = 'ST'
        LN = 1
        GOSUB 160
      CASE OPTION = 'SB'
        LN = LINES
        GOSUB 160
      * T25978 ^
      CASE OPTION = "C"
        GOSUB 170
        IF LNO THEN
          LN = LNO
          SLN = BEGIN.PAGE + LINE.SPACE * MOD(LN-1,PAGE.SIZE)
          GOSUB 141
        END
        GOSUB 160
      CASE OPTION = "D"
        GOSUB 170
        IF LNO THEN
          LN = LNO
          IFD.NO = DELETE(IFD.NO,1,LN,0)
          IFD.LEN = DELETE(IFD.LEN,1,LN,0)
          IFD.FIX = DELETE(IFD.FIX,1,LN,0)
          IFD.FILL = DELETE(IFD.FILL,1,LN,0)
          IFD.FILL.LEN = DELETE(IFD.FILL.LEN,1,LN,0)
          IFD.SCALER = DELETE(IFD.SCALER,1,LN,0)
          IFD.JUSTIFY = DELETE(IFD.JUSTIFY,1,LN,0)
          LINES = COUNT(IFD.NO,VM) + (IFD.NO # '')
          IF LN > 1 AND LN > LINES THEN LN = LN - 1
          IF LN < 1 THEN LN = 1
          OLD.START.LINE = 0
        END
        GOSUB 160
      CASE OPTION  =  "F"
        GOSUB 3000
        MORE = 1
      CASE 1
        ERRMSG = "INVALID OPTION";GOSUB 91000
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN
*
2000*
  MAT SCV.REC = ""
  ECD.ACTION = 6;CALL SCRN.EDIT
  RETURN
*
*---- UPDATE CONTROL FILE
*
3000*
  MATWRITE IFD.REC ON CONTROL,IFD.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000*
  ERR.TYPE = 2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000*
  ERR.TYPE = 3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
*      PRINT @(-1)
END
