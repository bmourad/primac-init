*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - PRICE.CHANGE.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 09/21/84
* DESCRIPTION -
* This program updates and maintains PRICE.CHANGE file.
* Modified to handle divisionalization.  JRS 09/07/95 - TASK 19403
*T23278 markt 10/20/1998 * Add check for divisional security
*T25740 edvard 10/24/2001 * Add validation for major line 
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>PRICE.CHANGE
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>ICS.CPYLIB>CATEGORY
*COPY>PMC.CPYLIB>DIVISION
*
*---- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE=1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE ERRMSG="ICS.SCREENS FILE MISSING";GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG="CONTROL FILE MISSING";GOTO 93000
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG="COMPANY FILE MISSING";GOTO 93000
  OPEN '','PRICE.CHANGE' TO PRICE.CHANGE ELSE ERRMSG="PRICE.CHANGE FILE MISSING";GOTO 93000
  OPEN '','WAREHOUSE' TO WAREHOUSE ELSE ERRMSG="WAREHOUSE FILE MISSING";GOTO 93000
  OPEN '','CATEGORY' TO CATEGORY ELSE ERRMSG="CATEGORY FILE MISSING";GOTO 93000
  OPEN '','DIVISION' TO DIVISION ELSE ERRMSG="DIVISION FILE MISSING";GOTO 93000
*
*---- GET COMPANY NUMBER
*
  CONO=''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO="END" THEN GOTO 99999
*
*---- INITIALIZATION
*
  MAT EDIT.COM=''
  MAT EDIT.COM.DRIVER=''
  SAVE.PC.COST=''
  DIV.OWNER=''
*
*---- GET DIVISION OWNING THIS CHANGE (COULD BE 'ALL')
*
4*  TASK 19403
  ECD.SCRN.CNT=1
  ECD.SCRN.NAME<1>="GET.DIVISION"
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO=1
  MAT SCV.REC = ""
  ECD.ACTION = 2;CALL SCRN.EDIT
  ECD.NUM=1;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN ECD.ACTION = 99 ; CALL SCRN.EDIT ; GOTO 99999
  IF ECD.RET.VALUE # '' THEN
    DIV.OWNER = ECD.RET.VALUE
    IF DIV.OWNER # "ALL" THEN
      MATREAD DIV.REC FROM DIVISION, CONO:DIV.OWNER ELSE
        ERRMSG="DIVISION NUMBER INVALID"; GOSUB 91000; GOTO 4
      END
    END
    DIV.CODE = DIV.OWNER; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
    CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
    IF ERRMSG # '' THEN
      GOSUB 91000; GOTO 4
    END
    * T26126
    MATREADU PC.REC FROM PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER LOCKED 
      ERRMSG = 'PRICE CHANGE record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 4 
    END ELSE NULL
    * T26126 ^
  END ELSE
    ERRMSG="ENTER DIVISION NUMBER '##', 'ALL', OR 'END'"; GOSUB 91000; GOTO 4
  END
  ECD.ACTION = 99 ; CALL SCRN.EDIT
*
*---- GET SCREEN
*
  ECD.SCRN.CNT=1
  ECD.SCRN.NAME<1>="PRICE.CHANGE.MAINT"
  ECD.ACTION=1;CALL SCRN.EDIT
  ECD.SCRN.NO=1
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
5*
  SCV.REC(15)<ECD.SCRN.NO,1>=DATE()
  ECD.NUM=15;ECD.ACTION=5;CALL SCRN.EDIT
  NEW.PC="NO"
  MATREADU PC.REC FROM PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER ELSE 
    MAT PC.REC='';NEW.PC="YES"
  END
  IF NEW.PC="YES" THEN
    FOR REQUEST=1 TO 7
      ON REQUEST GOSUB 10,20,30,40,50,60,70
      IF ECD.RET.VALUE="END" THEN
        IF REQUEST=1 THEN
          RELEASE PRICE.CHANGE,CONO:"PRICE.CHANGE"
          ECD.ACTION = 99 ; CALL SCRN.EDIT
          GOTO 4
        END ELSE
          RELEASE PRICE.CHANGE,CONO:"PRICE.CHANGE"
          GOSUB 400
          GOTO 5
        END
      END
    NEXT REQUEST
  END ELSE
    GOSUB 100
  END
  GOSUB 200
  ECD.ACTION = 99 ; CALL SCRN.EDIT
  GOTO 4
*
*---- ENTER WAREHOUSE ID
*
10*
  IF DIV.OWNER = "ALL" THEN PC.WAREHOUSE="ALL"; SCV.REC(1)<ECD.SCRN.NO>="ALL"
  ECD.NUM=1;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 15
  IF ECD.RET.VALUE # '' THEN
    IF DIV.OWNER = "ALL" AND ECD.RET.VALUE # "ALL" THEN
      ERRMSG="WAREHOUSE MUST BE 'ALL' WHEN DIVISION IS 'ALL'"
      GOSUB 91000; GOTO 10
    END
    PC.WAREHOUSE=ECD.RET.VALUE
    IF PC.WAREHOUSE="ALL" THEN
      WHS.DESC="ALL WAREHOUSES"
      WHS.DIV=DIV.OWNER
      PC.WHSE.DIV = DIV.OWNER
      IF WHS.DIV = "ALL" THEN DIV.DESC = "ALL DIVISIONS"
    END ELSE
      MATREAD WHSE.REC FROM WAREHOUSE,CONO:PC.WAREHOUSE ELSE
        ERRMSG="WAREHOUSE NUMBER INVALID";GOSUB 91000;GOTO 10
      END
      IF WHS.DIV # "" THEN
        IF WHS.DIV = DIV.OWNER OR DIV.OWNER = "ALL" THEN
          MATREAD DIV.REC FROM DIVISION, CONO:WHS.DIV ELSE
            ERRMSG="DIVISION NUMBER INVALID";GOSUB 91000;GOTO 10
          END
        END ELSE
          ERRMSG="WAREHOUSE IS OUT OF YOUR DIVISION";GOSUB 91000;GOTO 10
        END
      END ELSE
        ERRMSG="WAREHOUSE HAS NO DIVISION NUMBER";GOSUB 91000;GOTO 10
      END
      PC.WHSE.DIV = WHS.DIV
    END
    SCV.REC(2)<ECD.SCRN.NO>=WHS.DESC
    ECD.NUM=2;ECD.ACTION=5;CALL SCRN.EDIT
    SCV.REC(17)<ECD.SCRN.NO>=WHS.DIV
    ECD.NUM=17;ECD.ACTION=5;CALL SCRN.EDIT
    SCV.REC(18)<ECD.SCRN.NO>=DIV.DESC
    ECD.NUM=18;ECD.ACTION=5;CALL SCRN.EDIT
  END ELSE
    PC.WAREHOUSE=''
  END
15*
  RETURN
*
*---- ENTER PRODUCT TYPE
*
20*
  ECD.NUM=3;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # '' THEN PC.PROD.TYPE=ECD.RET.VALUE ELSE PC.PROD.TYPE=''
  RETURN
*
*---- ENTER MAJOR LINE
*
30*
  ECD.NUM=4;ECD.ACTION=4
  CALL SCRN.EDIT
  IF ECD.RET.VALUE # '' THEN PC.MLINE=ECD.RET.VALUE ELSE PC.MLINE=''
  RETURN
*
*---- ENTER PRODUCT LINE
*
40*  
  ECD.NUM=5;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 45
  IF ECD.RET.VALUE # '' THEN
    PC.PROD.LINE=ECD.RET.VALUE
    IF ECD.RET.VALUE="ALL" THEN
      CATG.DESC="ALL PRODUCT LINES"
    END ELSE
      MATREAD CATG.REC FROM CATEGORY,CONO:PC.PROD.LINE ELSE
        ERRMSG="PRODUCT LINE NUMBER INVALID";GOSUB 91000;GOTO 40
      END
    END
    SCV.REC(6)<ECD.SCRN.NO>=CATG.DESC
    ECD.NUM=6;ECD.ACTION=5;CALL SCRN.EDIT
  END
45*
  RETURN
*
*---- ENTER BASIS WEIGHT
*
50*
  ECD.NUM=7;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # '' THEN PC.BAS.WT=ECD.RET.VALUE ELSE PC.BAS.WT=''
  RETURN
*
*---- ENTER STANDARD COST
*
60*
  BURDEN.PRCNT=""
  IF PC.PROD.LINE # "ALL" THEN
    BURDEN=CATG.BURDEN.PRCNT * 100
    BURDEN.PRCNT=OCONV(BURDEN,"MD4")
  END
  ECD.NUM=8
  IF BURDEN.PRCNT # "" THEN ECD.DEFAULT=BURDEN.PRCNT
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 65
  IF ECD.RET.VALUE # '' THEN PC.COST<1,1>=ECD.RET.VALUE
  SAVE.PC.COST=PC.COST<1,1>
  IF PC.COST<1,1>=0 THEN
    P_X  = 31 ; P_Y = 11 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    PC.COST.ADJ<1,1>="";PC.COST.PREC<1,1>="";PC.BASIS=""
    GOTO 65
  END
  ECD.NUM=9
  IF BURDEN.PRCNT # "" THEN ECD.DEFAULT = "%"
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 65
  IF ECD.RET.VALUE # '' THEN PC.COST.ADJ<1,1>=ECD.RET.VALUE
  ECD.NUM=10;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 65
  IF ECD.RET.VALUE # '' THEN PC.COST.PREC<1,1>=ECD.RET.VALUE
  ECD.NUM=16;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 65
  IF ECD.RET.VALUE # "" THEN PC.BASIS=ECD.RET.VALUE
65*
  RETURN
*
*---- ENTER LIST PRICE
*
70*
  IF SAVE.PC.COST # 0 THEN 
    PC.COST<1,2>='';PC.COST.ADJ<1,2>='';PC.COST.PREC<1,2>=''
    P_X  = 22 ; P_Y = 13 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    GOTO 75
  END
  ECD.NUM=11;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 75
  IF ECD.RET.VALUE # '' THEN PC.COST<1,2>=ECD.RET.VALUE
  IF PC.COST<1,2>=0 THEN
    P_X  = 31 ; P_Y = 13 ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    PC.COST.ADJ<1,2>='';PC.COST.PREC<1,2>=''
    GOTO 75
  END
  ECD.NUM=12;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 75
  IF ECD.RET.VALUE # '' THEN PC.COST.ADJ<1,2>=ECD.RET.VALUE
  ECD.NUM=13;ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE="END" THEN GOTO 75
  IF ECD.RET.VALUE # '' THEN PC.COST.PREC<1,2>=ECD.RET.VALUE
75*
  RETURN
*
*---- GET VALUES FROM FILE
*
100*
  IF PC.WAREHOUSE="ALL" THEN
    WHS.DESC="ALL WAREHOUSES"
  END ELSE
    MATREAD WHSE.REC FROM WAREHOUSE,CONO:PC.WAREHOUSE ELSE
      ERRMSG="WAREHOUSE NUMBER INVALID";GOSUB 91000;WHS.DESC="????????????????????"
    END
  END
  IF PC.WHSE.DIV = DIV.OWNER THEN
    IF PC.WHSE.DIV = "ALL" THEN
      DIV.DESC = "ALL DIVISIONS"
    END ELSE
      IF PC.WHSE.DIV = '' THEN
        ERRMSG="NO VALID DIVISION NUMBER ON FILE";GOSUB 91000
        DIV.DESC="????????????????????"
      END ELSE
        MATREAD DIV.REC FROM DIVISION, CONO:PC.WHSE.DIV ELSE
          ERRMSG="DIVISION NUMBER INVALID";GOSUB 91000
          DIV.DESC="????????????????????"
        END
      END
    END
  END ELSE
    ERRMSG="WAREHOUSE IS OUT OF YOUR DIVISION";GOSUB 91000
  END
  IF PC.PROD.LINE="ALL" THEN
    CATG.DESC="ALL PRODUCT LINES"
  END ELSE
    MATREAD CATG.REC FROM CATEGORY,CONO:PC.PROD.LINE ELSE
      ERRMSG="PRODUCT LINE INVALID";GOSUB 91000;CATG.DESC="????????????????????"
    END
  END
  SCV.REC(1)<ECD.SCRN.NO>=PC.WAREHOUSE
  SCV.REC(2)<ECD.SCRN.NO>=WHS.DESC
  SCV.REC(3)<ECD.SCRN.NO>=PC.PROD.TYPE
  SCV.REC(4)<ECD.SCRN.NO>=PC.MLINE
  SCV.REC(5)<ECD.SCRN.NO>=PC.PROD.LINE
  SCV.REC(6)<ECD.SCRN.NO>=CATG.DESC
  SCV.REC(7)<ECD.SCRN.NO>=PC.BAS.WT
  SCV.REC(8)<ECD.SCRN.NO>=PC.COST<1,1>
  SCV.REC(9)<ECD.SCRN.NO>=PC.COST.ADJ<1,1>
  SCV.REC(10)<ECD.SCRN.NO>=PC.COST.PREC<1,1>
  SCV.REC(11)<ECD.SCRN.NO>=PC.COST<1,2>
  SCV.REC(12)<ECD.SCRN.NO>=PC.COST.ADJ<1,2>
  SCV.REC(13)<ECD.SCRN.NO>=PC.COST.PREC<1,2>
  SCV.REC(16)<ECD.SCRN.NO>=PC.BASIS
  SCV.REC(17)<ECD.SCRN.NO>=PC.WHSE.DIV
  SCV.REC(18)<ECD.SCRN.NO>=DIV.DESC
  ECD.ACTION=3;CALL SCRN.EDIT
  SAVE.PC.COST=PC.COST<1,1>
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE=0
  LOOP
    ECD.NUM=14;ECD.ACTION=4;CALL SCRN.EDIT
    OPTION=ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION="E" OR OPTION="END"
        RELEASE PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER
        MORE=1
      CASE NUM(OPTION) AND OPTION > 0
        ON OPTION GOSUB 10,20,30,40,50,60,70
        IF OPTION="6" THEN GOSUB 70
      CASE OPTION="F"
        IF PC.PRT.DATE='' THEN
          GOSUB 300;MORE=1
        END ELSE
          X=3;Y=23;TYP=18
          PMSG =  "PREVIOUS PRICE CHANGE REQUEST NOT COMPLETED, DO YOU WANT TO FILE (Y/N) ?"
          CALL EDIT.SUB
          ANSWER = VALUE
          IF ANSWER="Y" THEN
            PC.PRT.DATE=''
            GOSUB 300
          END ELSE 
            P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            ERRMSG="PRICE CHANGE NOT FILED"
            GOSUB 91000
          END
          MORE=1
        END
    END CASE
  WHILE MORE=0 DO REPEAT
  RETURN
*
*---- UPDATE FILE
*
300*
  MATWRITE PC.REC ON PRICE.CHANGE,CONO:"PRICE.CHANGE":DIV.OWNER
  RETURN
*
*---- CLEAR SCREEN
*
400*
  MAT SCV.REC = ""
  ECD.ACTION=6;CALL SCRN.EDIT
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
  ECD.ACTION=99 ; CALL SCRN.EDIT
END
