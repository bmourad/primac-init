* REVISION    - [08.0]
*T25740 epitka 01/17/2002 * REV12
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* T22841 rik - Print product id when reserved Qty message appears.
*T24815 alex 02/16/2000 * FIX PGRM TO UPDATE RESERVE QTY CORRECTLY IF
*                         RESV.QTYS # "".                            
  CUR.PRICE = 0
  STD.PRICE = 0
  ERRMSG = ""
  BEGIN CASE
    CASE RESV.QTYS # ""
      RSVD.QTYS = ""; QCNT = DCOUNT(FOS.RECP.NO,VM)
      FOR F = 1 TO QCNT
        LOCATE FOS.RECP.NO<1,F> IN IWH.RECP.NO<1> SETTING RPOS ELSE NULL
*        RSVD.QTYS<FOS.RECP.NO<1,F>+1> = FOS.FI.QTY<1,F>
        RSVD.QTYS<RPOS+1>=FOS.FI.QTY<1,F>
      NEXT F
      RESV.QTY = SUM(RESV.QTYS) - SUM(RSVD.QTYS)
      TMPCNT = DCOUNT(RESV.QTYS,AM)       
      QCNT = DCOUNT(RSVD.QTYS,AM)         
      IF TMPCNT GT QCNT THEN QCNT = TMPCNT
      FOR F = 1 TO QCNT
        FNO = F - 1
        RECPNO=IWH.RECP.NO<1,FNO>
        RQTY = RESV.QTYS<F> - RSVD.QTYS<F>
        BEGIN CASE
          CASE RQTY = 0
          CASE RQTY < 0
            LOCATE RECPNO IN FOS.RECP.NO<1>,1 SETTING FLOC THEN
              RQTY = 0 - RQTY
              IF RQTY < FOS.FI.QTY<1,FLOC> THEN
                FOS.FI.ORG<1,FLOC> = FOS.FI.ORG<1,FLOC> - RQTY
                FOS.FI.QTY<1,FLOC> = FOS.FI.QTY<1,FLOC> - RQTY
                STD.PRICE = STD.PRICE - INT(FOS.FI.AMT<1,FLOC> / 100 * RQTY / (INV.COST.WT/100))
              END ELSE
                RQTY = FOS.FI.QTY<1,FLOC>
                STD.PRICE = STD.PRICE - INT(FOS.FI.AMT<1,FLOC> / 100 * RQTY / (INV.COST.WT/100))
                FOS.RECP.NO = DELETE(FOS.RECP.NO,1,FLOC,0)
                FOS.FI.ORG = DELETE(FOS.FI.ORG,1,FLOC,0)
                FOS.FI.QTY = DELETE(FOS.FI.QTY,1,FLOC,0)
                FOS.FI.AMT = DELETE(FOS.FI.AMT,1,FLOC,0)
              END
              IF RECPNO > 0 THEN
                LOCATE RECPNO IN IWH.RECP.NO<1>,1 SETTING FI.REF THEN
                  IWH.RSV.FI<1,FI.REF> = IWH.RSV.FI<1,FI.REF> + RQTY
                  IWH.RESV = IWH.RESV - RQTY
                  CUR.PRICE = CUR.PRICE - INT(IWH.COST.FI<1,FI.REF> / 100 * RQTY / (INV.COST.WT/100))
                END
              END
            END
            RESV.QTY = RESV.QTY + RQTY
          CASE RQTY > 0
            LOCATE RECPNO IN FOS.RECP.NO<1>,1 SETTING FLOC ELSE
              FOS.RECP.NO<1,FLOC> = RECPNO
              FOS.FI.ORG<1,FLOC> = 0
              FOS.FI.QTY<1,FLOC> = 0
              BEGIN CASE
                CASE CATG.COST.TYPE = "SD"
                  FOS.FI.AMT<1,FLOC> = IWH.STD.COST
                CASE CATG.COST.TYPE = "AV"
                  FOS.FI.AMT<1,FLOC> = IWH.AVG.COST
                CASE CATG.COST.TYPE = "LT"
                  FOS.FI.AMT<1,FLOC> = IWH.LIST.COST
                CASE RECPNO < 1
                  FOS.FI.AMT<1,FLOC> = 0
                CASE CATG.COST.TYPE = "FI"
                  FOS.FI.AMT<1,FLOC> = IWH.COST.FI<1,RECPNO>
                CASE CATG.COST.TYPE = "LC"
                  FOS.FI.AMT<1,FLOC> = IWH.COST.FI<1,QCNT>
                CASE 1
                  FOS.FI.AMT<1,FLOC> = 0
              END CASE
            END
            IF RECPNO > 0 THEN
              LOCATE RECPNO IN IWH.RECP.NO<1>,1 SETTING FI.REF THEN
                IF RQTY < IWH.RSV.FI<1,FI.REF> THEN
                  IWH.RSV.FI<1,FI.REF> = IWH.RSV.FI<1,FI.REF> - RQTY
                  IWH.RESV = IWH.RESV + RQTY
                END ELSE
                  RQTY = IWH.RSV.FI<1,FI.REF>
                  IWH.RSV.FI<1,FI.REF> = 0
                  IWH.RESV = IWH.RESV + RQTY
                END
                CUR.PRICE = CUR.PRICE + INT(IWH.COST.FI<1,FI.REF> / 100 * RQTY / (INV.COST.WT/100))
              END
            END
            FOS.FI.ORG<1,FLOC> = FOS.FI.ORG<1,FLOC> + RQTY
            FOS.FI.QTY<1,FLOC> = FOS.FI.QTY<1,FLOC> + RQTY
            STD.PRICE = STD.PRICE + INT(FOS.FI.AMT<1,FLOC> / 100 * RQTY / (INV.COST.WT/100))
            IF FOS.FI.QTY<1,FLOC> = 0 THEN
              FOS.RECP.NO = DELETE(FOS.RECP.NO,1,FLOC,0)
              FOS.FI.ORG = DELETE(FOS.FI.ORG,1,FLOC,0)
              FOS.FI.QTY = DELETE(FOS.FI.QTY,1,FLOC,0)
              FOS.FI.AMT = DELETE(FOS.FI.AMT,1,FLOC,0)
            END
            RESV.QTY = RESV.QTY - RQTY
        END CASE
      NEXT F
      FOS.R.QTY = SUM(FOS.FI.QTY)
      IF RESV.QTY <> 0 THEN
        ERRMSG = "ERROR in actual reserving, QTY left = ":OCONV(RESV.QTY,"MD3"):" in Product ":PDNO
      END
    CASE CATG.TRACK.QOH # "Y" AND RESV.QTY > 0
      BEGIN CASE
        CASE CATG.COST.TYPE = "SD"
          CHECK.COST = IWH.STD.COST
        CASE CATG.COST.TYPE = "AV"
          CHECK.COST = IWH.AVG.COST
        CASE CATG.COST.TYPE = "LT"
          CHECK.COST = IWH.LIST.COST
        CASE 1
          CHECK.COST = 0
      END CASE
      CHECK.QTY = RESV.QTY
      FLOC = DCOUNT(FOS.RECP.NO,VM)
      BEGIN CASE
        CASE FLOC < 1
          FLOC = 1
          FOS.RECP.NO<1,FLOC> = 0
          FOS.FI.ORG<1,FLOC> = CHECK.QTY
          FOS.FI.QTY<1,FLOC> = CHECK.QTY
          FOS.FI.AMT<1,FLOC> = CHECK.COST
        CASE FOS.RECP.NO<1,FLOC> = 0 AND FOS.FI.AMT<1,FLOC> = CHECK.COST
          FOS.FI.ORG<1,FLOC> = FOS.FI.ORG<1,FLOC> + CHECK.QTY
          FOS.FI.QTY<1,FLOC> = FOS.FI.QTY<1,FLOC> + CHECK.QTY
        CASE 1
          FLOC = FLOC + 1
          FOS.RECP.NO<1,FLOC> = 0
          FOS.FI.ORG<1,FLOC> = CHECK.QTY
          FOS.FI.QTY<1,FLOC> = CHECK.QTY
          FOS.FI.AMT<1,FLOC> = CHECK.COST
      END CASE
      FOS.R.QTY = FOS.R.QTY + CHECK.QTY
      STD.PRICE = STD.PRICE + INT(CHECK.COST / 100 * CHECK.QTY / (INV.COST.WT/100))
      CUR.PRICE = 0
      RESV.QTY = 0
    CASE RESV.QTY > 0
      QCNT = DCOUNT(IWH.RSV.FI,VM)
      FOR FNO = 1 TO QCNT WHILE RESV.QTY <> 0
        IF IWH.RSV.FI<1,FNO> + 0 <> 0 THEN
          BEGIN CASE
            CASE CATG.COST.TYPE = "FI"
              CHECK.COST = IWH.COST.FI<1,FNO>
            CASE CATG.COST.TYPE = "LC"
              CHECK.COST = IWH.COST.FI<1,QCNT>
            CASE CATG.COST.TYPE = "SD"
              CHECK.COST = IWH.STD.COST
            CASE CATG.COST.TYPE = "AV"
              CHECK.COST = IWH.AVG.COST
            CASE CATG.COST.TYPE = "LT"
              CHECK.COST = IWH.LIST.COST
            CASE 1
              CHECK.COST = 0
          END CASE
          IF RESV.QTY > IWH.RSV.FI<1,FNO> THEN
            CHECK.QTY = IWH.RSV.FI<1,FNO>
          END ELSE
            CHECK.QTY = RESV.QTY
          END
          FLOC = DCOUNT(FOS.RECP.NO,VM)
          BEGIN CASE
            CASE FLOC < 1
              FLOC = 1
              FOS.RECP.NO<1,FLOC> = IWH.RECP.NO<1,FNO>
              FOS.FI.ORG<1,FLOC> = CHECK.QTY
              FOS.FI.QTY<1,FLOC> = CHECK.QTY
              FOS.FI.AMT<1,FLOC> = CHECK.COST
            CASE FOS.RECP.NO<1,FLOC> = IWH.RECP.NO<1,FNO> AND FOS.FI.AMT<1,FLOC> = CHECK.COST
              FOS.FI.ORG<1,FLOC> = FOS.FI.ORG<1,FLOC> + CHECK.QTY
              FOS.FI.QTY<1,FLOC> = FOS.FI.QTY<1,FLOC> + CHECK.QTY
            CASE 1
              FLOC = FLOC + 1
              FOS.RECP.NO<1,FLOC> = IWH.RECP.NO<1,FNO>
              FOS.FI.ORG<1,FLOC> = CHECK.QTY
              FOS.FI.QTY<1,FLOC> = CHECK.QTY
              FOS.FI.AMT<1,FLOC> = CHECK.COST
          END CASE
          FOS.R.QTY = FOS.R.QTY + CHECK.QTY
          IWH.RESV = IWH.RESV + CHECK.QTY
          IWH.RSV.FI<1,FNO> = IWH.RSV.FI<1,FNO> - CHECK.QTY
          STD.PRICE = STD.PRICE + INT(CHECK.COST / 100 * CHECK.QTY / (INV.COST.WT/100))
          CUR.PRICE = CUR.PRICE + INT(IWH.COST.FI<1,FNO> / 100 * CHECK.QTY / (INV.COST.WT/100))
          RESV.QTY = RESV.QTY - CHECK.QTY
        END
      NEXT FNO
      IF RESV.QTY <> 0 THEN
        ERRMSG = "RSV QTY IS MORE THAN AVAIL, QTY.LEFT = ":OCONV(RESV.QTY,"MD3")
      END
    CASE RESV.QTY < 0
      RESV.QTY = 0 - RESV.QTY
      QCNT = DCOUNT(FOS.RECP.NO,VM)
      FOR Q = QCNT TO 1 STEP -1 WHILE RESV.QTY <> 0
        RECPNO = FOS.RECP.NO<1,Q>
        CHECK.QTY = FOS.FI.QTY<1,Q>
        BEGIN CASE
          CASE CHECK.QTY > RESV.QTY
            CHECK.QTY = RESV.QTY
            STD.PRICE = STD.PRICE - INT(FOS.FI.AMT<1,Q> / 100 * CHECK.QTY / (INV.COST.WT/100))
            FOS.FI.ORG<1,Q> = FOS.FI.ORG<1,Q> - CHECK.QTY
            FOS.FI.QTY<1,Q> = FOS.FI.QTY<1,Q> - CHECK.QTY
          CASE FOS.FI.ORG<1,Q> <> CHECK.QTY
            STD.PRICE = STD.PRICE - INT(FOS.FI.AMT<1,Q> / 100 * CHECK.QTY / (INV.COST.WT/100))
            FOS.FI.ORG<1,Q> = FOS.FI.ORG<1,Q> - CHECK.QTY
            FOS.FI.QTY<1,Q> = 0
          CASE 1
            STD.PRICE = STD.PRICE - INT(FOS.FI.AMT<1,Q> / 100 * CHECK.QTY / (INV.COST.WT/100))
            FOS.RECP.NO = DELETE(FOS.RECP.NO,1,Q,0)
            FOS.FI.ORG = DELETE(FOS.FI.ORG,1,Q,0)
            FOS.FI.QTY = DELETE(FOS.FI.QTY,1,Q,0)
            FOS.FI.AMT = DELETE(FOS.FI.AMT,1,Q,0)
        END CASE
        FOS.R.QTY = FOS.R.QTY - CHECK.QTY
        IF RECPNO > 0 THEN
          LOCATE RECPNO IN IWH.RECP.NO<1>,1 SETTING FI.REF THEN
            IWH.RSV.FI<1,FI.REF> = IWH.RSV.FI<1,FI.REF> + CHECK.QTY
            IWH.RESV = IWH.RESV - CHECK.QTY
            CUR.PRICE = CUR.PRICE - INT(IWH.COST.FI<1,FI.REF> / 100 * CHECK.QTY / (INV.COST.WT/100))
          END
        END
        RESV.QTY = RESV.QTY - CHECK.QTY
      NEXT Q
      IF RESV.QTY <> 0 THEN
        ERRMSG = "ADJUST QTY IS MORE THAN RSV, QTY.LEFT = ":OCONV(RESV.QTY,"MD3")
      END
  END CASE
