*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - YTD.SHRINK.RPT
* DATE        - 12/12/85
* BY          - JIHAD YAMOUT , C.B.A
* DESCRIPTION - This report is used to eveluate inventory shrinkage.
*********************************************************************
***   INSERT EQUATE FROM CPYLIB
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
***   INITIALIZATION
      LINE.COUNT        = 99
      PAGE.NO           = 0
      TOT.QTY = 0
      TOT.SHK.QTY = 0
      TOT.COST = 0
      GRD.TOT = 0
      SP1 = SPACE(1)
***   SET UP THE ERRMSG ROUTINE FOR SYSCOM
           SYS.TYPE = 1
             CALL SYSCOM(MAT SYSCOM.REC)
***   OPEN THE FILE
   OPEN '','INV.WHSE' TO INV.WHSE ELSE
       ERRMSG = 'INV.WHSE FILE IS MISSING'
       GOTO 93000
   END
   OPEN '','INVENTORY' TO INVENTORY ELSE
       ERRMSG = 'INVENTORY FILE IS MISSING'
       GOTO 93000
   END
***   READ COMPANY NAME 
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
   CONO.NAME = ID<2>
   POS1 = 51 - INT(LEN(CONO.NAME)/2)
   POS2 = 26 + INT(LEN(CONO.NAME)/2)
***   PUT PRINTER ON
   PRINTER ON
***   READ AND PROCESS FIRST RECORD
100*
   READNEXT KEY ELSE GOTO 999999
   GOSUB 200
***   READ THE THREE FILE
   MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
      MAT IWH.REC = ''
      GOTO 100
   END
   MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
      MAT INV.REC = ''
   END
***   SET THE PRODUCT NUMBER
   PREV.PRO = PROD
***   SET THE HEADING LINES
   HH.LINE = "PRODUCT NUMBER                FULL DESCRIPTION                WHSE U/M  ON HAND  SHRINKAGE SHRINKAGE $ "
   DH.LINE = "--------------- --------------------------------------------- ---- --- --------- --------- ------------"
***   PROCESS THE FILE
   GOSUB 1000
***    READ AND PROCESS ALL THE FILES
   DATA = 0
LOOP
   READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
     GOSUB 200
***   READ THE THREE FILES
     MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
        MAT IWH.REC = ''
        GOTO 111
     END
     MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
        MAT INV.REC = ''
     END
***   PROCESS THE RECORD
     GOSUB 1000
111*
REPEAT
***   PRINT THE TOTAL FOR LAST PRODUCT
      GOSUB 25000
***   PRINT THE GRAND TOTAL
   IF LINE.COUNT + 2 > 56 THEN GOSUB 80000
   PRINT
   PRINT SPACE(91):"============"
   PRINT SPACE(79):"GRAND TOTAL ":OCONV(GRD.TOT, "MD2") "R#12"
***    END OF JOB
   GOTO 999999
***   SEPERATE THE KEY TO PRODUCT, WHSE 
200*
   PROD.KEY = FIELD(KEY,"!",1)
   PROD = PROD.KEY[4,99]
   WHSE = FIELD(KEY,"!",2)
RETURN
***   PROCESS THE RECORD
1000*
   BEGIN CASE
   CASE PROD # PREV.PRO
      GOSUB 25000
      PRINT
      LINE.COUNT = LINE.COUNT + 1
   END CASE
   IF INV.COST.WT *1 = 0 THEN INV.COST.WT = 100
   MAT INV.CNV.REC = ""
   BEGIN CASE
   CASE INV.UNIT<1,2> = "SHT" AND INV.UNIT<1,3> = "LBS"
      ICR.CNV = "MD0"; ICR.DV2 = 1
      ICR.MT1 = 1; ICR.DV1 = INV.M.WT
   CASE INV.UNIT<1,2> = "PC" AND INV.UNIT<1,3> = "MSI"
      ICR.CNV = "MD0"; ICR.DV2 = 1
      ICR.MT1 = 10; ICR.DV1 = INV.PAP.WIDTH/100
   CASE INV.UNIT<1,2> = "FT" AND INV.UNIT<1,3> = "MSI"
      ICR.CNV = "MD0"; ICR.DV2 = 12
      ICR.MT1 = 100; ICR.DV1 = INV.PAP.WIDTH/100
   CASE 1
      ICR.CNV = "MD2"; ICR.DV2 = 1
      ICR.MT1 = 1; ICR.DV1 = 10
   END CASE
   GOSUB 20000
   TOT.COST = TOT.COST + IWH.YTD.SHRNK.AMT
   TOT.QTY = TOT.QTY + ON.HAND
   TOT.SHK.QTY = TOT.SHK.QTY + SHK.QTY
RETURN
***  ROUTINE TO PRINT THE RECORD
20000*
   IF LINE.COUNT > 56 THEN GOSUB 80000
   LINE.COUNT = LINE.COUNT + 1
   IF INV.PAP.TYPE = '' OR INV.PAP.TYPE = 'REG' THEN INV.PAP.TYPE = 'REGULAR'
   H.LINE = PROD "L#15":SP1:INV.FULL.DESC "L#45":SP1
   H.LINE = H.LINE :WHSE "L#4":SP1:INV.UNIT<1,2> "L#3":SP1
   BEGIN CASE
   CASE IWH.ON.HAND = "" OR IWH.ON.HAND = "0"
      ON.HAND = 0
   CASE IWH.ON.HAND < 1
      ON.HAND = INT(((IWH.ON.HAND/ICR.DV1)*ICR.MT1)/ICR.DV2 - .5)
   CASE IWH.ON.HAND > 1
      ON.HAND = INT(((IWH.ON.HAND/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5)
   CASE 1
      ON.HAND = 0
   END CASE
   BEGIN CASE
   CASE IWH.YTD.SHRNK  = "" OR IWH.YTD.SHRNK = "0"
      SHK.QTY = 0
   CASE IWH.YTD.SHRNK < 1 
      SHK.QTY = INT(((IWH.YTD.SHRNK/ICR.DV1)*ICR.MT1)/ICR.DV2 -.5)
   CASE IWH.YTD.SHRNK > 1
      SHK.QTY = INT(((IWH.YTD.SHRNK/ICR.DV1)*ICR.MT1)/ICR.DV2 +.5)
   CASE 1
      SHK.QTY = 0
   END CASE
   H.LINE = H.LINE : OCONV(ON.HAND,ICR.CNV) "R#9":SP1
   H.LINE = H.LINE : OCONV(SHK.QTY,ICR.CNV) "R#9":SP1
   H.LINWE = H.LINE : OCONV(IWH.YTD.SHRNK.AMT, "MD2") "R#12"
   PRINT H.LINE
RETURN
***   ROUTINE TO PRINT THE TOTAL VALUES
25000*
   IF LINE.COUNT + 2 > 56 THEN GOSUB 80000
   LINE.COUNT = LINE.COUNT + 2
   T.LINE = ""
   T.LINE = T.LINE :SPACE(71):"--------- --------- ------------"
   PRINT T.LINE
   T.LINE = ''
   T.LINE = T.LINE:SPACE(71)
   T.LINE = T.LINE :OCONV(TOT.QTY, ICR.CNV) "R#9":SP1
   T.LINE = T.LINE :OCONV(TOT.SHK.QTY, ICR.CNV) "R#9":SP1
   T.LINE = T.LINE :OCONV(TOT.COST, "MD2") "R#12"
   PRINT T.LINE
   PRINT
***   CLEARING THE TOTAL VALUES
      PREV.PRO = PROD
      GRD.TOT = GRD.TOT + TOT.COST
      TOT.COST = 0
      TOT.QTY = 0
      TOT.SHK.QTY = 0
RETURN
***  ROUTINE TO PRINT HEADINGS
80000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   H.LINE = 'DATE : ' : OCONV(DATE(), 'D2/') "L#8"
   H.LINE = H.LINE : SPACE(POS1) : CONO.NAME "L#30"
   H.LINE = H.LINE : SPACE(POS2) : 'PAGE ' : PAGE.NO "R#3"
   PRINT H.LINE
   PRINT SPACE(51):"YEAR TO DATE SHRINKAGE REPORT"
   PRINT
   PRINT HH.LINE
   PRINT DH.LINE
   LINE.COUNT = 5
RETURN
***   CALLS FOR SYSCOM
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
999999*
   PRINTER OFF
   END
