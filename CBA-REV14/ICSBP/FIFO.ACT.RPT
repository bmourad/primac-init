*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - ICSBP
* PROGRAM     - FIFO.ACT.RPT
* DATE        - 04/22/85
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DESCRIPTION -
* This program creates a Monthly Stock Receipts Journal.
*
*T26190 ajibaly 03/08/2002 * CONVERT TO REV12 USING BUILD.IWH.FI SUB
*ENDDOC
*********************************************************************
***   INSERT EQUATE FROM CPYLIB
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
***   INITIALIZATION
LINE.COUNT = 99
POINTER = 1
PAGE.NO = 0
TOT.P.COST = 0
TOT.COST = 0
UNKNOWN = STR("?",45)
SP1 = SPACE(1)
SP12 = SPACE(12)
D3 = STR("-",3)
D4 = STR("-",4)
D8 = STR("-",8)
D9 = STR("-",9)
D10 = STR("-",10)
D15 = STR("-",15)
D45 = STR("-",45)
***   SET UP THE ERRMSG ROUTINE FOR SYSCOM
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
***   OPEN THE FILE
OPEN '','INV.WHSE' TO INV.WHSE ELSE
  ERRMSG = 'INV.WHSE FILE IS MISSING'
  GOTO 93000
END
OPEN '','INVENTORY' TO INVENTORY ELSE
  ERRMSG = 'INVENTORY FILE IS MISSING'
  GOTO 93000
END
***   READ COMPANY NAME 
PROCREAD ID ELSE
  ERRMSG = ' CANNOT PERFORM PROCREAD'
  GOTO 93000
END
CONO = ID<1>
CONO.NAME = ID<2>
SS.VEND = ID<7>
SS.S.DATE = ICONV(ID<8>, "D")
SS.E.DATE = ICONV(ID<9>, "D")
F.DATE = OCONV(ID<8>,"D2/")
T.DATE = OCONV(ID<9>,"D2/")
SS.PO = ID<10>
REPORT.NAME = "PERIOD STOCK RECEIPTS JOURNAL"
REPORT.NUM = "XXXX"
REPORT.DATE = DATE()
HD1 = "" ; HD2 = ""
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUM,REPORT.DATE,HD1,HD2)
*T26190 v
 OPEN.FLAG=1
 ERR.FLAG = ''
 PERIOD = ID<8>
SEL.PERIOD=ID<8>
 IF PERIOD = 'ALL' THEN PERIOD = ''
*T26190 ^
***   PUT PRINTER ON
PRINTER ON
***   SET THE HEADING LINES
HH.LINE = ""
HH.LINE = 'PRODUCT NUMBER' : SPACE(16)
HH.LINE = HH.LINE : 'FULL DESCRIPTION' : SPACE(16)
HH.LINE = HH.LINE : 'WHSE DATE REC PO NUMBER VENDOR # U/M  '
HH.LINE = HH.LINE : 'QUANTITY  UNIT COST    COST'
DH.LINE = ""
DH.LINE = DH.LINE :D15:SP1:D45:SP1:D4:SP1:D8:SP1:D9:SP1:D8:SP1:D3:SP1
DH.LINE = DH.LINE :D9:SP1:D10:SP1:D10
DT.LINE = ''
DT.LINE = DT.LINE : SPACE(120) : D10
*** READ AND PROCESS FIRST RECORD
10*
READNEXT KEY ELSE GOTO 999999
GOSUB 200
MATREAD IWH.REC FROM INV.WHSE , KEY ELSE
  MAT IWH.REC = "" ; GOTO 10
END
MATREAD INV.REC FROM INVENTORY, PROD.KEY ELSE
  MAT INV.REC = ""
  INV.FULL.DESC = UNKNOWN
END
CALL BUILD.IWH.FI(KEY,MAT IWH.REC,PERIOD,ERR.FLAG,ERRMSG,OPEN.FLAG) ; *T26190
PREV.PLINE = INV.LINE
PREV.MLINE = INV.M.LINE
D.CNT = COUNT(IWH.RECV.FI,VM) + (IWH.RECV.FI # "")
IF D.CNT = 0 THEN GOTO 10
FOR D = 1 TO D.CNT
  IF SS.VEND = "ALL" OR SS.VEND = IWH.VDR.FI<1,D> THEN
    IF SS.PO = "ALL" OR SS.PO = IWH.PO.NO.FI<1,D> THEN
      IF IWH.RECV.FI<1,D> >= SS.S.DATE AND IWH.RECV.FI<1,D> <= SS.E.DATE THEN
        GOSUB 1000
      END
    END
  END
NEXT D
***    READ AND PROCESS ALL THE FILES
DATA = 0
LOOP
  READNEXT KEY ELSE DATA = 1
WHILE DATA = 0 DO
  GOSUB 200
***   READ THE FILES
  MATREAD IWH.REC FROM INV.WHSE,KEY ELSE
    MAT IWH.REC = ''
    GOTO 111
  END
  MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
    MAT INV.REC = ''
    INV.FULL.DESC = UNKNOWN
  END
***   PROCESS THE RECORD
  CALL BUILD.IWH.FI(KEY,MAT IWH.REC,PERIOD,ERR.FLAG,ERRMSG,OPEN.FLAG)
  D.CNT = COUNT(IWH.RECV.FI,VM) + (IWH.RECV.FI # "")
  IF D.CNT = 0 THEN GOTO 111
  FOR D = 1 TO D.CNT
    IF SS.VEND = "ALL" OR SS.VEND = IWH.VDR.FI<1,D> THEN
      IF SS.PO = "ALL" OR SS.PO = IWH.PO.NO.FI<1,D> THEN
        IF IWH.RECV.FI<1,D> >= SS.S.DATE AND IWH.RECV.FI<1,D> <= SS.E.DATE THEN
          GOSUB 1000
        END
      END
    END
  NEXT D
111*
REPEAT
GOSUB 24000
***    END OF JOB
GOTO 999999
***   SEPERATE THE KEY TO PRODUCT, WHSE 
200*
PROD.KEY = FIELD(KEY,"!",1)
PROD = PROD.KEY[4,99]
WHSE = FIELD(KEY,"!",2)
RETURN
***   PROCESS THE RECORD
1000*
GOSUB 24000
GOSUB 60000
IF INV.COST.WT*1 = 0 THEN INV.COST.WT = 100
COST = 0
COST = INT((IWH.COST.FI<1,D>/10000) * ((IWH.ORG.FI<1,D>/10) / (INV.COST.WT/100)))
IF IWH.ORG.FI<1,D> > 0 THEN
  QTY=INT((((IWH.ORG.FI<1,D> / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
END ELSE
  QTY=INT((((IWH.ORG.FI<1,D> / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
END
TOT.COST = TOT.COST + COST
TOT.P.COST = TOT.P.COST + COST
DATE.REC = OCONV(IWH.RECV.FI<1,D>,"D2/")
GOSUB 20000
RETURN
***  ROUTINE TO PRINT THE RECORD
20000*
IF LINE.COUNT > 50 THEN GOSUB 80000
LINE.COUNT = LINE.COUNT + 1
H.LINE = ""
IF POINTER = 1 THEN
  POINTER = 0
  H.LINE = PROD "L#15" :SP1: INV.FULL.DESC "L#45"
  H.LINE = H.LINE :SP1: WHSE "L#4" :SP1
  H.LINE = H.LINE :DATE.REC "R#8" :SP1
  H.LINE = H.LINE :IWH.PO.NO.FI<1,D> "L#9" :SP1
  H.LINE = H.LINE :IWH.VDR.FI<1,D> "L#8" :SP1
  H.LINE = H.LINE :INV.UNIT<1,2> "L#3":SP1
  H.LINE = H.LINE :OCONV(QTY,ICR.CNV)"R#9":SP1
  H.LINE = H.LINE :OCONV(IWH.COST.FI<1,D>,"MD4")"R#10":SP1
  H.LINE = H.LINE :OCONV(COST,"MD2")"R#10"
END ELSE
  H.LINE = H.LINE :SPACE(67)
  H.LINE = H.LINE :DATE.REC "R#8" :SP1
  H.LINE = H.LINE :IWH.PO.NO.FI<1,D> "L#9" :SP1
  H.LINE = H.LINE :IWH.VDR.FI<1,D> "L#8" :SP1
  H.LINE = H.LINE :INV.UNIT<1,2> "L#3":SP1
  H.LINE = H.LINE :OCONV(QTY,ICR.CNV)"R#9":SP1
  H.LINE = H.LINE :OCONV(IWH.COST.FI<1,D>,"MD4")"R#10":SP1
  H.LINE = H.LINE :OCONV(COST,"MD2")"R#10"
END
PRINT H.LINE
RETURN
*** BREAK ON P.LINE AND PRINT TOTALS
24000*
IF PREV.PLINE # INV.LINE OR DATA = 1 THEN
  IF POINTER = 0 THEN
    POINTER = 1
    IF LINE.COUNT + 3 > 50 THEN GOSUB 80000
    LINE.COUNT = LINE.COUNT + 3
    H.LINE = ''
    H.LINE = H.LINE : SPACE(96) : 'SUB TOTAL  :'
    H.LINE = H.LINE :SP12: OCONV(TOT.P.COST, 'MD2') "R#10"
    PRINT DT.LINE
    PRINT H.LINE
    TOT.P.COST = 0
    PREV.PLINE = INV.LINE
    PREV.MLINE = INV.M.LINE
    LINE.COUNT = LINE.COUNT + 3
  END
  IF DATA = 0 THEN
    LINE.COUNT = 99
  END ELSE
    GOSUB 25000
  END
END
RETURN
***   ROUTINE TO PRINT THE TOTAL VALUES
25000*
IF LINE.COUNT + 3 > 50 THEN GOSUB 80000
LINE.COUNT = LINE.COUNT + 3
H.LINE = ''
H.LINE = H.LINE : SPACE(96) : 'GRAND TOTAL:'
H.LINE = H.LINE :SP12: OCONV(TOT.COST, 'MD2') "R#10"
PRINT DT.LINE
PRINT H.LINE
RETURN
***        CONVERT SHEET/WGHT/QTY
60000*
MAT INV.CNV.REC = ''
BEGIN CASE
  CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
    ICR.CNV = "MD0"
    ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
  CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
    ICR.CNV = "MD0"
    ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
  CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
    ICR.CNV = "MD0"
    ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
  CASE 1
    ICR.CNV = "MD2"
    ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
END CASE
RETURN
***  ROUTINE TO PRINT HEADINGS
80000*
PRINT CHAR(12):
PAGE.NO = PAGE.NO + 1
H.LINE = HD1 :PAGE.NO
PRINT H.LINE
H.LINE = HD2
PRINT H.LINE
H.LINE = SPACE(46)
H.LINE = H.LINE :"FOR PERIOD ":F.DATE:" THROUGH ":T.DATE
PRINT H.LINE
PRINT
H.LINE = "MLINE / PLINE : ":PREV.MLINE:" / ":PREV.PLINE
PRINT H.LINE
PRINT
PRINT HH.LINE
PRINT DH.LINE
LINE.COUNT = 8
POINTER = 1
RETURN
***   CALLS FOR SYSCOM
91000*
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000*
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000*
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
999999*
PRINTER OFF
END
