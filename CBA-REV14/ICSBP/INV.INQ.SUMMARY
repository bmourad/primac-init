   SUBROUTINE INV.INQ.SUMMARY(CONO, PROD, CURR.WHSE, CURR.LOC)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*********************************************************************
* REVISION    - [08.1B]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM     - INV.INQ.SUMMARY
* AUTHOR      - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE        - 01/13/92
* DESCRIPTION
* This program is used to display all Roll or Skid for information
* for a given product.
*T22243 renee 09/19/1997 * In GUI only, when scrolling through multiple
*                          pages, if the last page is only a few lines
*                          long, the previous page does not get cleared.
*********************************************************************
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>COLOR
*COPY>ICS.CPYLIB>FINISH
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   PAGE.SIZE = 12
   BEGIN.PAGE = 8
   LINE.SPACE = 1
   OLD.START.LINE = 0
*
*---- MAIN PROCESSING
*
   GOSUB 80000
   GOSUB 81000
   LINES = DCOUNT(XID,VM)
   LN = 1; GOSUB 50000
   LOOP
      ECD.NUM = 21; SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE ACTION = "E" OR ACTION = "" OR ACTION = "END"
            ACTION = ""
         CASE NUM(ACTION)
            LN = ACTION
            IF LN >= 1 AND LN <= LINES THEN
               ID = XID<1,LN>
               CURR.WHSE = FIELD(ID,"!",1)
               CURR.LOC  = FIELD(ID,"!",2)
            END
            ACTION = ""
         CASE ACTION[1,1] = "S"
            SFX = ACTION[2,1]
            BEGIN CASE
               CASE SFX = "" OR SFX = "F"
                  LN = LN + PAGE.SIZE
                  IF LN > LINES THEN LN = 1
               CASE SFX = "R"
                  LN = LN - PAGE.SIZE
                  IF LN < 1 THEN LN = 1
               CASE SFX = "T"
                  LN = 1
               CASE SFX = "B"
                  LN = LINES
                  IF LN = 0 THEN LN = 1
            END CASE
            GOSUB 50000
      END CASE
   WHILE ACTION # "" DO REPEAT
   GOTO 99999
*
*---- DISPLAY MULTI-LINE DATA
*
50000 *
   START.LINE = 1 + INT((LN-1)/PAGE.SIZE)*PAGE.SIZE
   IF START.LINE = OLD.START.LINE THEN GOTO 50090
   OLD.START.LINE = START.LINE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   IF LAST.LINE > LINES THEN LAST.LINE = LINES
   CNT = 1
   FOR N = START.LINE TO LAST.LINE
      GOSUB 82000
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 1 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
      P_X  := AM:6 ; P_Y := AM:SLN ; P_VALUE := AM:WHSE "L#4"
      P_X  := AM:12 ; P_Y := AM:SLN ; P_VALUE := AM:LOC "L#4"
      P_X  := AM:18 ; P_Y := AM:SLN ; P_VALUE := AM:RSID "L#14"
      P_X  := AM:34 ; P_Y := AM:SLN ; P_VALUE := AM:QTY "R#12"
      P_X  := AM:48 ; P_Y := AM:SLN ; P_VALUE := AM:DIA "R#5"
      P_X  := AM:55 ; P_Y := AM:SLN ; P_VALUE := AM:LST "R#8"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
* T22243 v Clear rest the rest of the page
*        P_X  = 0 ; P_Y = SLN ; P_VALUE = @(-4) ; P_OPT = ""
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
* T22243 ^
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
50090 RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
   SCV.REC(1)<ECD.SCRN.NO> = DATE()
   SCV.REC(2)<ECD.SCRN.NO> = PROD
   SCV.REC(3)<ECD.SCRN.NO> = INV.DESC
   BEGIN CASE
      CASE MOD(INV.PAP.WIDTH,10000) = 0
         WIDTH = INT(INV.PAP.WIDTH/10000)
      CASE MOD(INV.PAP.WIDTH,1000) = 0
         WIDTH = OCONV(INT(INV.PAP.WIDTH/1000),"MD1")
      CASE MOD(INV.PAP.WIDTH,100) = 0
         WIDTH = OCONV(INT(INV.PAP.WIDTH/100),"MD2")
      CASE MOD(INV.PAP.WIDTH,10) = 0
         WIDTH = OCONV(INT(INV.PAP.WIDTH/10),"MD3")
      CASE 1
         WIDTH = OCONV(INV.PAP.WIDTH,"MD4")
   END CASE
   BEGIN CASE
      CASE MOD(INV.PAP.LEN,10000) = 0
         LNGTH = INT(INV.PAP.LEN/10000)
      CASE MOD(INV.PAP.LEN,1000) = 0
         LNGTH = OCONV(INT(INV.PAP.LEN/1000),"MD1")
      CASE MOD(INV.PAP.LEN,100) = 0
         LNGTH = OCONV(INT(INV.PAP.LEN/100),"MD2")
      CASE MOD(INV.PAP.LEN,10) = 0
         LNGTH = OCONV(INT(INV.PAP.LEN/10),"MD3")
      CASE 1
         LNGTH = OCONV(INV.PAP.LEN,"MD4")
   END CASE
   BEGIN CASE
      CASE WIDTH = 0 AND LNGTH = 0
         SCV.REC(4)<ECD.SCRN.NO> = ""
      CASE LNGTH = 0
         SCV.REC(4)<ECD.SCRN.NO> = WIDTH:'"'
      CASE 1
         SCV.REC(4)<ECD.SCRN.NO> = WIDTH:'" x ':LNGTH:'"'
   END CASE
   SCV.REC(5)<ECD.SCRN.NO> = OCONV(INV.BAS.WT,"MD2")
   MATREAD COLOR.REC FROM COLOR, CONO:INV.COLOR ELSE
      MAT COLOR.REC = ""
   END
   IF COLOR.DESC = "" THEN
      SCV.REC(6)<ECD.SCRN.NO> = INV.COLOR
   END ELSE
      SCV.REC(6)<ECD.SCRN.NO> = COLOR.DESC
   END
   MATREAD FINISH.REC FROM FINISH, CONO:INV.PAP.FINSH ELSE
      MAT FINISH.REC = ""
   END
   IF FINISH.DESC = "" THEN
      SCV.REC(7)<ECD.SCRN.NO> = INV.PAP.FINSH
   END ELSE
      SCV.REC(7)<ECD.SCRN.NO> = FINISH.DESC
   END
   BEGIN CASE
      CASE INV.PAP.TYPE = "SHEET"
         SCV.REC(10)<ECD.SCRN.NO> = "Skid"
      CASE INV.PAP.TYPE = "ROLL"
         SCV.REC(10)<ECD.SCRN.NO> = "Roll"
   END CASE
   ECD.ACTION = 3; CALL SCRN.EDIT
   RETURN
*
*---- SORT MULTI-LINE DATA
*
81000 *
   XID = ""
   WCNT = DCOUNT(INV.WHSE.CODE,VM)
   FOR WPTR = 1 TO WCNT
      WHSE = INV.WHSE.CODE<1,WPTR>
      IWH.ID = PROD:"!":WHSE
      MATREAD IWH.REC FROM INV.WHSE, CONO:IWH.ID ELSE GOTO 81060
      LCNT = DCOUNT(IWH.LOC,VM)
      FOR LPTR = 1 TO LCNT
         LOC = IWH.LOC<1,LPTR>
         IWLO.ID = PROD:"!":WHSE:"!":LOC
         MATREAD IWLO.REC FROM INV.WHSE.LOC, CONO:IWLO.ID ELSE GOTO 81050
         RCNT = DCOUNT(IWLO.R.S.ID,VM)
         FOR RPTR = 1 TO RCNT
            RSID = IWLO.R.S.ID<1,RPTR>
            ID   = WHSE:"!":LOC:"!":RSID
            LOCATE ID IN XID<1>,1 BY "AL" SETTING P ELSE NULL
            XID  = INSERT(XID,1,P,0,ID)
         NEXT RPTR
81050 NEXT LPTR
81060 NEXT WPTR
   RETURN
*
*---- LOAD MULTI-LINE DATA
*
82000 *
   ID = XID<1,N>
   WHSE = FIELD(ID,"!",1)
   LOC  = FIELD(ID,"!",2)
   WLEN = LEN(WHSE) + LEN(LOC) + 2
   RSID = ID[WLEN+1,(LEN(ID)-WLEN)]
*      RSID = FIELD(ID,"!",3)
   IWLO.ID = PROD:"!":WHSE:"!":LOC
   MATREAD IWLO.REC FROM INV.WHSE.LOC, CONO:IWLO.ID ELSE
      MAT IWLO.REC = ""
   END
   LOCATE RSID IN IWLO.R.S.ID<1>,1 SETTING RPTR ELSE NULL
   QTY = INT(((IWLO.CURR.WT<1,RPTR> / ICR.DV1) * ICR.MT1) / ICR.DV2 + 0.5)
   QTY = OCONV(QTY,ICR.CNV1)
   IF IWLO.CURR.DIAM<1,RPTR>+0 = 0 THEN
      DIA = ""
   END ELSE
      DIA = OCONV(IWLO.CURR.DIAM<1,RPTR>,"MD2")
   END
   IF IWLO.LA.USED<1,RPTR> = "" THEN
      LST = ""
   END ELSE
      LST = OCONV(IWLO.LA.USED<1,RPTR>,"D2/")
   END
   RETURN
*
*---- END OF PROGRAM
*
99999 *
   IWH.ID = PROD:"!":CURR.WHSE
   MATREAD IWH.REC FROM INV.WHSE, CONO:IWH.ID ELSE
      MAT IWH.REC = ""
   END
   IWLO.ID = PROD:"!":CURR.WHSE:"!":CURR.LOC
   MATREAD IWLO.REC FROM INV.WHSE.LOC, CONO:IWLO.ID ELSE
      MAT IWLO.REC = ""
   END
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
