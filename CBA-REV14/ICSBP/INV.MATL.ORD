  SUBROUTINE INV.MATL.ORD(CONO,PROD.ID,PROD.DESC,INV.UNIT,INV.M.WT,INV.PAP.WIDTH)
*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.MATL.ORD
* AUTHOR      - KERRY WILSON, COMPUTER BUSINESS ASSOCIATES
* DATE        - 05/09/89
* TASK        - 12918
* DESCRIPTION - This program will perform an inquiry on the various 
*             - PO files, depending on the selection criteria entered
*             - by the operator.
*T25978 adelgado 02/18/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*COPY>PMC.CPYLIB>PO
*COPY>POS.CPYLIB>MISC.PO
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>SCREEN.COM
*
*---- SETUP ERRMSG ROUTINE
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN "PO" TO PO ELSE
    ERRMSG = 'CANNOT OPEN PO FILE'
    GOSUB 91000
    GOTO 99999
  END
  OPEN "MISC.PO" TO MISC.PO ELSE
    ERRMSG = 'CANNOT OPEN MISC.PO FILE'
    GOSUB 91000
    GOTO 99999
  END
  OPEN "JOB" TO JOB ELSE
    ERRMSG = 'CANNOT OPEN JOB FILE'
    GOSUB 91000
    GOTO 99999
  END
  OPEN "CUSTOMER" TO CUSTOMER ELSE
    ERRMSG = 'CANNOT OPEN CUSTOMER FILE'
    GOSUB 91000
    GOTO 99999
  END
*
*---- INITIALIZATION
*
  REF.NO = 0
  TOT.ALL = 0
  TOT.UNA = 0
  GR.TOT = 0
  SCREEN DEFINE;INV.MATL.ORD
  SCREEN FORMAT
  SCREEN COUNT;INV.MATL.ORD;12
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  BEGIN CASE
    CASE INV.UNIT<1,2> = "SHT" AND INV.UNIT<1,3> = "LBS"
      ICR.CNV = "MD0"; ICR.DV2 = 1
      ICR.DV1 = INV.M.WT; ICR.MT1 = 1
    CASE INV.UNIT<1,2> = "PC" AND INV.UNIT<1,3> = "MSI"
      ICR.CNV = "MD0"; ICR.DV2 = 1
      ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10
    CASE INV.UNIT<1,2> = "FT" AND INV.UNIT<1,3> = "MSI"
      ICR.CNV = "MD0"; ICR.DV2 = 12
      ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100
    CASE 1
      ICR.CNV = "MD2"; ICR.DV2 = 1
      ICR.DV1 = 10; ICR.MT1 = 1
  END CASE
*
*---- MAIN PROCESSING
*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  S$DATA(2)<S$SCR> = PROD.ID
  SCREEN DISPLAY;;2
  S$DATA(3)<S$SCR> = PROD.DESC
  SCREEN DISPLAY;;3
  SCREEN FIELD;;23
  SCREEN INPUT;;23
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION = 'R'
      SENTENCE = 'SSELECT PO BY-EXP PROD = "':PROD.ID:'" BY DUE.DATE'
    CASE OPTION = 'M'
      SENTENCE = 'SSELECT MISC.PO BY-EXP PROD = "':PROD.ID:'" BY DUE.DATE'
    CASE 1
      RETURN
  END CASE
  PERFORM SENTENCE CAPTURING XXX
  IF SYSTEM(11) THEN
    SELECT TO POLIST
  END ELSE
    POLIST = ""
  END
100 *
  READNEXT ID,MV.KEY FROM POLIST ELSE GOTO 200
  GOSUB 10000
  GOTO 100
200 *
  CURR.REF.NO = ''
  REF.NO = 1
  GOSUB 50000
  S$DATA(19)<S$SCR> = OCONV(INT(((TOT.ALL/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
  SCREEN DISPLAY;;19
  S$DATA(20)<S$SCR> = OCONV(INT(((TOT.UNA/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
  SCREEN DISPLAY;;20
  S$DATA(22)<S$SCR> = OCONV(INT(((GR.TOT/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
  SCREEN DISPLAY;;22
300 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21
  OPT = S$VALUE
  BEGIN CASE
    CASE OPT = 'S' OR OPT = 'SF'
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > S$CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPT = 'SB'
      REF.NO = S$CNT
      GOSUB 50000
    CASE OPT = 'ST'
      REF.NO = 1
      GOSUB 50000
    * T25978 ^
    CASE 1
      GOTO 99999
  END CASE
  GOTO 300
*
*---- LOAD SCREEN DATA
*
10000 *
  IF MV.KEY = '' THEN MV.KEY = 1
  BEGIN CASE
    CASE OPTION = 'R'
      MATREAD PO.REC FROM PO,ID ELSE RETURN
      IF PO.QTY.OPEN<1,MV.KEY> = '' OR PO.QTY.OPEN<1,MV.KEY> = 0 THEN RETURN
      REF.NO = REF.NO + 1
      S$DATA(18)<S$SCR,REF.NO> = ID[4,99]
      S$DATA(11)<S$SCR,REF.NO> = PO.DATE
      S$DATA(12)<S$SCR,REF.NO> = PO.DEL.DATE<1,MV.KEY>
      JB.ALL = PO.JB.OPEN<1,MV.KEY>
      JB.ALL = SUM(JB.ALL)
      IF PO.JOB.NO<1,MV.KEY> # '' THEN
        JOB.CNT = DCOUNT(PO.JOB.NO<1,MV.KEY>,SVM)
        FOR J = 1 TO JOB.CNT
          IF PO.JB.OPEN<1,MV.KEY,J> = '' OR PO.JB.OPEN<1,MV.KEY,J> = 0 THEN GOTO 10001
          IF J > 1 THEN
            S$DATA(18)<S$SCR,REF.NO> = ''
            S$DATA(11)<S$SCR,REF.NO> = ''
            S$DATA(12)<S$SCR,REF.NO> = ''
          END
          S$DATA(17)<S$SCR,REF.NO> = PO.JOB.NO<1,MV.KEY,J>
          MATREAD JOB.REC FROM JOB,CONO:PO.JOB.NO<1,MV.KEY,J> ELSE MAT JOB.REC = ''
          MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE MAT CUST.REC = ''
          S$DATA(13)<S$SCR,REF.NO> = CUST.NAME
          S$DATA(14)<S$SCR,REF.NO> = OCONV(INT(((PO.JB.OPEN<1,MV.KEY,J>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
*              S$DATA(15)<S$SCR,REF.NO> = ''
*              S$DATA(16)<S$SCR,REF.NO> = ''
          TOT.ALL = TOT.ALL + PO.JB.OPEN<1,MV.KEY,J>
          REF.NO = REF.NO + 1
10001 *
        NEXT J
        REF.NO = REF.NO - 1
        S$DATA(16)<S$SCR,REF.NO> = OCONV(INT(((PO.QTY.OPEN<1,MV.KEY>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
        NOT.AVAIL = PO.QTY.OPEN<1,MV.KEY> - SUM(PO.JB.OPEN<1,MV.KEY>)
        S$DATA(15)<S$SCR,REF.NO> = OCONV(INT(((NOT.AVAIL/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
        TOT.UNA = TOT.UNA + NOT.AVAIL
      END ELSE
        S$DATA(16)<S$SCR,REF.NO> = OCONV(INT(((PO.QTY.OPEN<1,MV.KEY>/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
        NOT.AVAIL = PO.QTY.OPEN<1,MV.KEY> - SUM(PO.JB.OPEN<1,MV.KEY>)
        S$DATA(15)<S$SCR,REF.NO> = OCONV(INT(((NOT.AVAIL/ICR.DV1)*ICR.MT1)/ICR.DV2 + .5),ICR.CNV)
        TOT.UNA = TOT.UNA + NOT.AVAIL
      END
      GR.TOT = GR.TOT + PO.QTY.OPEN<1,MV.KEY>
    CASE OPTION = 'M'
      MATREAD MPO.REC FROM MISC.PO,ID ELSE RETURN
      IF MPO.TOT.OPEN<1,MV.KEY> = '' OR MPO.TOT.OPEN<1,MV.KEY> = 0 THEN RETURN
      REF.NO = REF.NO + 1
      S$DATA(18)<S$SCR,REF.NO> = ID[4,99]
      S$DATA(11)<S$SCR,REF.NO> = MPO.DATE<1,MV.KEY>
      S$DATA(12)<S$SCR,REF.NO> = MPO.DUE.DATE<1,MV.KEY>
      S$DATA(17)<S$SCR,REF.NO> = ''
      S$DATA(13)<S$SCR,REF.NO> = ''
      S$DATA(14)<S$SCR,REF.NO> = ''
      S$DATA(15)<S$SCR,REF.NO> = OCONV(MPO.TOT.OPEN<1,MV.KEY>,"MD2")
      S$DATA(16)<S$SCR,REF.NO> = OCONV(MPO.TOT.OPEN<1,MV.KEY>,"MD2")
      TOT.UNA = TOT.UNA + MPO.TOT.OPEN<1,MV.KEY>
      GR.TOT = GR.TOT + MPO.TOT.OPEN<1,MV.KEY>
    CASE 1
      RETURN
  END CASE
  RETURN
*
*---- SCROLL DESCRIPTION
*
50000 *
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = COUNT(S$DATA(18),VM) + (S$DATA(18) # "")
  SCREEN MULTI;;C;10;18;11;12;17;13;14;15;16
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000 *
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
*
92000 *
  ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 *
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 *
  SCREEN CLEAR
*       PRINT @(-1)
  RETURN
END
