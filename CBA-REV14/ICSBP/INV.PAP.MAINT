*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INVENTORY
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.PAP.MAINT
* BY          - JIHAD YAMOUT , C.B.A
* DATE        - 10/11/83
* MODIFIED    - NLB              RWW
* DATE        - 10/13/88         07/05/89
* COMMENTS    - ADDED CUST XREF  CSF9259 CUST# CHG
* DESCRIPTION
* This program provides for maintenance of paper products.
*T22381 stefanie 01/09/1998 * Allow change or delete of customer number.
*C34760 diane 11/12/1999 * Fix check for inprcs on cust.
*T25092 alex 04/18/2000 * Fix prgm to not store invalid entries for
*                         paper/finish code.
*T25884 cm 06/18/2001 * Add duplicate function.
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*T28266 lross 09/22/2004 * Look at IWH.RECP.NO vs ON-HAND = 0.
*********************************************************************
*
**** INSERT FILES EQUATES
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>COLOR
*COPY>ICS.CPYLIB>FINISH
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*
**** SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG = 'INVENTORY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','WAREHOUSE' TO WAREHOUSE ELSE
    ERRMSG = 'WAREHOUSE FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','COLOR' TO COLOR ELSE
    ERRMSG = 'COLOR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','FINISH' TO FINISH ELSE
    ERRMSG = 'FINISH FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','FOH.TABLE' TO FOH.TABLE ELSE
    ERRMSG = 'FOH.TABLE FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.WHSE' TO INV.WHSE ELSE
    ERRMSG = 'INV.WHSE IS MISSING'
    GOTO 93000
  END
  OPEN '','CATEGORY' TO CATEGORY ELSE
    ERRMSG = 'CATEGORY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','VEND' TO VEND ELSE
    ERRMSG = 'VENDOR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','VENDOR.XREF' TO VENDOR.XREF ELSE
    ERRMSG = 'VENDOR.XREF FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'M.SCREENS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INVENTORY.XREF' TO INVENTORY.XREF ELSE
    ERRMSG = 'INVENTORY.XREF FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','PRODUCT.XREF' TO PRODUCT.XREF ELSE
    ERRMSG = 'PRODUCT.XREF FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CUSTOMER.XREF' TO CUSTOMER.XREF ELSE
    ERRMSG = 'CUSTOMER.XREF FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CUSTOMER' TO CUSTOMER ELSE
    ERRMSG = 'CUSTOMER FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','XREF.DATA' TO XREF.DATA ELSE
    ERRMSG = 'XREF.DATA FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.STATS' TO INV.STATS ELSE
    ERRMSG = 'INV.STATS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.WHSE.LOC' TO INV.WHSE.LOC ELSE
    ERRMSG = 'INV.WHSE.LOC FILE IS MISSING'
    GOTO 93000
  END
*
***** GET COMPANY NAME
*
  MAT COMP.REC = ''
  PW = 'ICSPROD.MAINT'
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
**** SET UP GEN.XREF.REC
*
*      MAT EDIT.COM = ''
*      TYP = 0; CALL EDIT.SUB; FILL = "#"
  MAT EDIT.COM.DRIVER = ''
  MAT GEN.XREF.REC = ''
  OPTION = ''
  GXR.CO = CONO
*
***** MAIN PROCESSING
*
  MAT EDIT.COM.DRIVER = ''
  ECD.SCRN.CNT = 4
  ECD.SCRN.NAME<1> = 'INV.PAP.MAINT'
  ECD.SCRN.NAME<2> = 'PROD.VEND.MAINT'
  ECD.SCRN.NAME<3> = 'WHSE.INV.REG.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
**** PRINT SCREEN
1*
  ECD.SCRN.NO = 1
  MAT INV.REC = ''
  MAT IWH.REC = '' ;*T28266
  MAT CATG.REC = ''
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
*
***** ENTER PROD NUMBER
*
5*
  QTY.OH = 0
  OPTION = ''
  OLD.NAME.XREF = ""
  OLD.NAME = ''
  NEW.REC = ""
  ECD.NUM = 5
*T25884 v
*  ECD.VALDAT.CODE = '4'
*  ECD.VALDAT.FILE = INVENTORY
*  ECD.PREFIX.ID = CONO
  ECD.ACTION = 6; CALL SCRN.EDIT
*T25884 ^
  ECD.ACTION=4;CALL SCRN.EDIT
  PROD.NUM = ECD.RET.VALUE
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
*T25884 v
*     CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
*        STATUS = '0'
*        FOR I = 1 TO INV.REC.SIZE
*           INV.REC(I) = ECD.VALDAT.ITEM<I>
*        NEXT I
*        BEGIN CASE
*           CASE INV.M.LINE = "FNGD"
*              IF CO.OPS = 'Y' THEN
*                 ERRMSG = "This product is a Finished Goods Product"
*                 GOSUB 91000; GOTO 5
*              END
*           CASE INV.PAP.TYPE = "REGULAR"
*              ERRMSG = "This product is not a Paper Product"
*              GOSUB 91000; GOTO 5
*        END CASE
*        OLD.NAME = INV.FULL.DESC
*        OLD.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
*        GOSUB 70000
*        GOSUB 1000
*        ECD.ACTION=3;CALL SCRN.EDIT
*        ECD.NUM=28; ECD.SCALER=ICR.CNV[3,1]; ECD.ACTION=5; CALL SCRN.EDIT
*        WH.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
*        IF WH.CNT > 0 THEN
*           FOR WW = 1 TO WH.CNT UNTIL QTY.OH > 0
*              MATREAD IWH.REC FROM INV.WHSE  , CONO:ECD.RET.VALUE:"!":INV.WHSE.CODE<1,WW> ELSE
*                 IWH.ON.HAND = 0
*              END
*              QTY.OH = QTY.OH + IWH.ON.HAND
*           NEXT WW
*        END
*     CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
*        STATUS = '1'
*        FOR S = 1 TO 23
*           ON S GOSUB 35,180,10,15,40,95,55,80,85,90,110,120,130,135,140,145,150,155,160,170,190,200,210
*           IF ECD.RET.VALUE = 'END' THEN GOTO 1
*        NEXT S
*T25884 ^
    CASE ECD.RET.VALUE = ''
      ECD.NUM = 11
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = '' OR ECD.RET.VALUE = 'END' THEN GOTO 5
      GXR.NAME = "INV.PAP"
      GXR.XREF = INVENTORY.XREF
      GXR.FILE = INVENTORY
      GXR.SRCH.ID = ECD.RET.VALUE
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION=2;CALL SCRN.EDIT
      IF GXR.ID = '' THEN
        SCV.REC(11)<1> = ''
        ECD.ACTION=3;CALL SCRN.EDIT
        GOTO 5
      END ELSE
        * T26126 v
        MATREADU INV.REC FROM INVENTORY, CONO:GXR.ID LOCKED
          ERRMSG = 'PRODUCT record is locked by user - ':GETUSERNAME(STATUS())
          GOSUB 91000;GOTO 5 
        END ELSE
        * T26126 ^
          ERRMSG = 'PRODUCT NUMBER ':GXR.ID:' IS MISSING'
          GOSUB 91000
          SCV.REC(11)<1> = ''
          GOTO 5
        END
        BEGIN CASE
          CASE INV.M.LINE = "FNGD"
            IF CO.OPS = 'Y' THEN
              ERRMSG = "This product is a Finished Goods Product"
              GOSUB 91000; GOTO 5
            END
          CASE INV.PAP.TYPE = "REGULAR"
            ERRMSG = "This product is not a Paper Product"
            GOSUB 91000; GOTO 5
        END CASE
        STATUS = '0'
        PROD.NUM = GXR.ID
        OLD.NAME = INV.FULL.DESC
        OLD.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
        SCV.REC(5)<1> = PROD.NUM
        GOSUB 70000
        GOSUB 1000
        ECD.ACTION=3;CALL SCRN.EDIT
      END
*T25884 v
    CASE 1
      PROD.NUM = ECD.RET.VALUE
      * T26126 v
      MATREADU INV.REC FROM INVENTORY, CONO:PROD.NUM LOCKED
        ERRMSG = 'PRODUCT record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END THEN
      * T26126 ^
        STATUS = '0'
        BEGIN CASE
          CASE INV.M.LINE = 'FNGD'
            IF CO.OPS = 'Y' THEN
              ERRMSG = 'This product is a Finished Goods Product'
              GOSUB 91000; GOTO 5
            END
          CASE INV.PAP.TYPE = 'REGULAR'
            ERRMSG = 'This product is not a Paper Product'
            GOSUB 91000; GOTO 5
        END CASE
        OLD.NAME = INV.FULL.DESC
        OLD.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
        GOSUB 70000
        GOSUB 1000
        ECD.ACTION = 3; CALL SCRN.EDIT
        ECD.NUM = 28 ; ECD.SCALER = ICR.CNV[3,1] ; ECD.ACTION = 5 ; CALL SCRN.EDIT
        WH.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
        IF WH.CNT > 0 THEN
          FOR WW = 1 TO WH.CNT UNTIL QTY.OH > 0
            MATREAD IWH.REC FROM INV.WHSE  , CONO:ECD.RET.VALUE:"!":INV.WHSE.CODE<1,WW> ELSE
              IWH.ON.HAND = 0
            END
            QTY.OH = QTY.OH + IWH.ON.HAND
          NEXT WW
        END
      END ELSE
        ECD.NUM = 47 ; SCV.REC(ECD.NUM)<1> = ''
        ECD.ACTION = 4 ; CALL SCRN.EDIT
        BEGIN CASE
          CASE ECD.RET.VALUE = 'D'
7 *
            ECD.NUM = 48 ; SCV.REC(ECD.NUM)<1> = ''
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE # 'END' THEN
              MATREAD INV.REC FROM INVENTORY, CONO:ECD.RET.VALUE THEN
                BEGIN CASE
                  CASE INV.M.LINE = 'FNGD'
                    IF CO.OPS = 'Y' THEN
                      ERRMSG = 'This product is a Finished Goods Product'
                      GOSUB 91000; GOTO 7
                    END
                  CASE INV.PAP.TYPE = 'REGULAR'
                    ERRMSG = 'This product is not a Paper Product'
                    GOSUB 91000; GOTO 7
                END CASE
                INV.SUBS = ''
                INV.LA.SALE = '' ; INV.LA.PUR = ''
                STATUS = '2'
                OLD.NAME = INV.FULL.DESC
                OLD.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
                SCV.REC(5)<1> = PROD.NUM
                GOSUB 70000
                GOSUB 1000
                ECD.ACTION = 3 ; CALL SCRN.EDIT
                INV.WHSE.CODE = ""
              END ELSE
                ERRMSG = 'Cannot locate product # ':ECD.RET.VALUE
                GOSUB 91000 ; GOTO 7
              END
            END
          CASE ECD.RET.VALUE = 'A'
            MAT INV.REC = ''
            STATUS = '1'
            FOR S = 1 TO 23
              ON S GOSUB 35,180,10,15,40,95,55,80,85,90,110,120,130,135,140,145,150,155,160,170,190,200,210
              IF ECD.RET.VALUE = 'END' THEN GOTO 1
            NEXT S
          CASE 1
            ECD.RET.VALUE = 'END'
        END CASE
        IF ECD.RET.VALUE = 'END' THEN
          RELEASE INVENTORY, CONO:PROD.NUM
          GOTO 5
        END
      END
*T25884 ^
  END CASE
  GOSUB 60000
  ECD.SCRN.NO = 1
  MAT INV.REC = ''
  MAT CATG.REC = ''
  MAT SCV.REC = ""
  ECD.ACTION=6;CALL SCRN.EDIT
  GOTO 5
*
*** ENTER SP.CODE
*
10*
  ECD.NUM = 6
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.SPEC = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER PROD LINE
*
15*
  GOSUB 80000
  IF QTY.FLAG THEN QTY.FLAG = "";GOTO 21
*T28266 v
* IF QTY.OH <= 0 THEN
  IF IWH.RECP.NO = '' THEN
    ECD.NUM = 7
    ECD.VALDAT.CODE = 2
    ECD.VALDAT.FILE = CATEGORY
    ECD.PREFIX.ID = CONO
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE = 'END' THEN GOTO 20
    INV.LINE = ECD.RET.VALUE
    FOR SAVE = 1 TO CATG.REC.SIZE
      CATG.REC(SAVE) = ECD.VALDAT.ITEM<SAVE>
    NEXT SAVE
    BEGIN CASE
      CASE CATG.MAJ.LINE = "FNGD"
        IF CO.OPS = 'Y' THEN
          ERRMSG = "Can't utilize a Finished Goods product line for a Paper product"
          GOSUB 91000 ; GOTO 15
        END
      CASE CATG.TYPE # "S" AND CATG.TYPE # "L" AND CATG.TYPE # "PC" AND CATG.TYPE # "RL"
        ERRMSG = "Can't utilize a Regular product line for a Paper product"
        GOSUB 91000 ; GOTO 15
    END CASE
*
**** DISPLAY MAJOR LINE
*
20*
    INV.M.LINE = CATG.MAJ.LINE
    ECD.NUM = 8
    SCV.REC(8)<ECD.SCRN.NO,1> = CATG.MAJ.LINE
    ECD.ACTION=5;CALL SCRN.EDIT
    IF OPTION # "" THEN
      GOSUB 40 ; GOSUB 95 ; GOSUB 55 ; GOSUB 90 ; GOSUB 140
    END
  END ELSE
    ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
    GOSUB 91000
  END
21*
  RETURN
*
***** ENTER DESCRIPTION
*
35*
  ECD.NUM = 11
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.DESC = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER UNIT BUY
*
40*
  BEGIN CASE
    CASE CATG.TYPE = "S"
      ECD.VALDAT = "SHT,LBS" ; ECD.HMSG = "SHT , LBS" ; ECD.DEFAULT = "SHT"
      INV.UNIT<1,2> = "SHT" ; INV.UNIT<1,3> = "LBS" ; INV.PAP.TYPE = "SHEET"
    CASE CATG.TYPE = "L"
      ECD.VALDAT = "LBS" ; ECD.HMSG = "LBS" ; ECD.DEFAULT = "LBS"
      INV.UNIT<1,2> = "LBS" ; INV.UNIT<1,3> = "LBS" ; INV.PAP.TYPE = "ROLL"
    CASE CATG.TYPE = "RL"
      ECD.VALDAT = "FT,MSI,PC" ; ECD.HMSG = "FT , MSI , PC" ; ECD.DEFAULT = "PC"
      INV.UNIT<1,2> = "PC" ; INV.UNIT<1,3> = "MSI" ; INV.PAP.TYPE = "LROLL"
    CASE CATG.TYPE = "PC"
      ECD.VALDAT = "FT,MSI" ; ECD.HMSG = "FT , MSI" ; ECD.DEFAULT = "FT"
      INV.UNIT<1,2> = "FT" ; INV.UNIT<1,3> = "MSI" ; INV.PAP.TYPE = "PCOAT"
    CASE 1
      INV.FACTOR = ""
      INV.AVG.DIA = ""
      INV.CORE.DIA = ""
      SCV.REC(44)<ECD.SCRN.NO,1> = ""
      SCV.REC(45)<ECD.SCRN.NO,1> = ""
      SCV.REC(46)<ECD.SCRN.NO,1> = ""
      ECD.NUM = 44; ECD.ACTION = 5;CALL SCRN.EDIT
      ECD.NUM = 45; ECD.ACTION = 5;CALL SCRN.EDIT
      ECD.NUM = 46; ECD.ACTION = 5;CALL SCRN.EDIT
  END CASE
  ECD.NUM = 12
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.UNIT<1,1> = ECD.RET.VALUE
  END
*
***** ENTER UNIT STOCK
*
45*
  ECD.NUM = 13
  SCV.REC(13)<ECD.SCRN.NO,1> = INV.UNIT<1,2>
  ECD.ACTION = 5 ; CALL SCRN.EDIT
*
***** ENTER UNIT PRICE
*
50*
  ECD.NUM = 14
  SCV.REC(14)<ECD.SCRN.NO,1> = INV.UNIT<1,3>
  ECD.ACTION = 5 ; CALL SCRN.EDIT
  GOSUB 70000
  RETURN
*
***** ENTER IF PAPER
*
55*
  ECD.NUM = 15
  SCV.REC(15)<ECD.SCRN.NO,1> = INV.PAP.TYPE
  ECD.ACTION=5;CALL SCRN.EDIT
  RETURN
*
**** ENTER BASIS WEIGHT
*
80*
  IF QTY.OH <= 0 THEN
    ECD.NUM = 20
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      IF NUM(ECD.RET.VALUE) THEN
        INV.BAS.WT = ICONV(ECD.RET.VALUE,"MD2")
      END ELSE
        INV.BAS.WT = ECD.RET.VALUE
      END
    END
  END ELSE
    ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
    GOSUB 91000
  END
  RETURN
*
**** ENTER COSTING WEIGHT
*
85*
  IF QTY.OH <= 0 THEN
    ECD.NUM = 21
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      INV.COST.WT = ECD.RET.VALUE
    END
  END ELSE
    ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
    GOSUB 91000
  END
  RETURN
*
**** ENTER M/WEIGHT
*
90*
  ECD.NUM = 22
  IF INV.PAP.TYPE = 'SHEET' THEN
    IF QTY.OH <= 0 THEN
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
*CSF 25812 v
        IF ECD.RET.VALUE+0 <= 0 THEN
          ERRMSG='This Paper Type must have M-weight greater than zero!'
          GOSUB 91000
          GOTO 90
        END
*CSF 25812 ^
        INV.M.WT = ECD.RET.VALUE
      END
    END ELSE
      ERRMSG = "THIS FIELD CANNOT BE CHANGED AFTER QTY HAS BEEN RECEIVED"
      GOSUB 91000
    END
  END ELSE
    INV.M.WT = 0
    SCV.REC(ECD.NUM)<1> = INV.M.WT; ECD.ACTION=5;CALL SCRN.EDIT
  END
  RETURN
*
**** ENTER STOCK BUY RATIO
*
95*
  INV.SBR = 1
  ECD.NUM = 23; SCV.REC(ECD.NUM)<1> = INV.SBR; ECD.ACTION=5;CALL SCRN.EDIT
  RETURN
*
**** ENTER IF TAXABLE
*
110*
  ECD.NUM = 26
  ECD.DEFAULT = "N"
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.TAXABLE = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER SUGG ORD QTY
*
120*
  ECD.NUM = 28
  ECD.SCALER = ICR.CNV[3,1]
  IF INV.SOQ # '' THEN
    SCV.REC(28)<ECD.SCRN.NO> = INV.SOQ
  END ELSE
    SCV.REC(28)<ECD.SCRN.NO> = ICONV(SCV.REC(28)<ECD.SCRN.NO>,'MD':ECD.SCALER)
  END
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.SOQ = ECD.RET.VALUE
  END
  RETURN
*
***** ENTER PAPER COLOR
*
130*
  ECD.NUM = 33
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    *T25092 v
    * INV.COLOR = ECD.RET.VALUE
    * MATREAD COLOR.REC FROM COLOR, CONO:INV.COLOR ELSE
    MATREAD COLOR.REC FROM COLOR, CONO:ECD.RET.VALUE ELSE
    *T25092 ^
      ERRMSG = "INVALID COLOR CODE";GOSUB 91000
      GOTO 130
    END
    INV.COLOR = ECD.RET.VALUE     ;*T25092
  END
  RETURN
*
***** ENTER PAPER FINISH
*
135*
  ECD.NUM = 34
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    *T25092 v
    * INV.PAP.FINSH = ECD.RET.VALUE
    * MATREAD FINISH.REC FROM FINISH, CONO:INV.PAP.FINSH ELSE
    MATREAD FINISH.REC FROM FINISH, CONO:ECD.RET.VALUE ELSE
    *T25092 ^
      ERRMSG = 'INVALID FINISH CODE'
      GOSUB 91000;GOTO 135
    END
    INV.PAP.FINSH = ECD.RET.VALUE     ;*T25092
  END
  RETURN
*
**** ENTER PAPER LENGTH
*
140*
  ECD.NUM = 35
  IF CATG.TYPE = "S" THEN
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      INV.PAP.LEN = ECD.RET.VALUE
    END
  END ELSE
    INV.PAP.LEN = 0
    SCV.REC(35)<ECD.SCRN.NO,1> = INV.PAP.LEN
    ECD.ACTION = 5 ; CALL SCRN.EDIT
  END
  RETURN
*
**** ENTER PAPER WIDTH
*
145*
  ECD.NUM = 36
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
*CSF 25812 v
* 12/04/97  I think all paper types should have a positive width (??)
*        IF INV.PAP.TYPE = 'LROLL' OR INV.PAP.TYPE = 'PCOAT' THEN
    IF ECD.RET.VALUE+0 <= 0 THEN
      ERRMSG='This Paper Type must have a positive Width!'
      GOSUB 91000
      GOTO 145
    END
*        END
*CSF 25812 ^
    INV.PAP.WIDTH = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER PAPER BRIGHTNESS
*
150 ECD.NUM = 40
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.BRIGHT = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER PAPER SHADE
*
155 ECD.NUM = 41
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.SHADE = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER PAPER PAGES PER INCH
*
160 ECD.NUM = 42
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.PPI = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER GRAIN DIRECTION
*
170 ECD.NUM = 43
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.GRAIN = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER CUSTOMER NUMBER
*
180*
*T22381 v      GOSUB 80000 ;*CSF9259
*              IF QTY.FLAG THEN QTY.FLAG = '';GOTO 189
  IF INV.CUST # '' THEN                                                     
    GOSUB 81000                                                             
    IF CUST.QTY.FLAG THEN CUST.QTY.FLAG = '' ; GOTO 189                     
  END                                                                       
*T22381 ^
  ECD.NUM=18; ECD.ACTION=4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 189
    CASE ECD.RET.VALUE = ""
*T22381 v
      SCV.REC(18)<1> = ''
      SCV.REC(19)<1> = ''
      ECD.ACTION = 3 ; CALL SCRN.EDIT
*T22381 ^
      ECD.NUM=19; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE="" OR ECD.RET.VALUE="END" THEN INV.CUST = "";GOTO 189
      GXR.NAME = "CUSTOMER"
      GXR.XREF = CUSTOMER.XREF
      GXR.FILE=CUSTOMER
      GXR.SRCH.ID=ECD.RET.VALUE
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION=2;CALL SCRN.EDIT
      IF GXR.ID='' THEN
        SCV.REC(18)<1> = ""
        ECD.ACTION=3; CALL SCRN.EDIT
        GOTO 180
      END ELSE
        MATREAD CUST.REC FROM CUSTOMER,CONO:GXR.ID ELSE
          ERRMSG = "CUSTOMER ":GXR.ID:" IS MISSING"
          GOSUB 91000
          GOTO 180
        END
        SCV.REC(18)<1>=GXR.ID
        SCV.REC(19)<1>=CUST.NAME
        ECD.ACTION=3; CALL SCRN.EDIT
        INV.CUST=GXR.ID
      END
    CASE 1
      MATREAD CUST.REC FROM CUSTOMER,CONO:ECD.RET.VALUE ELSE
        ERRMSG = "CUSTOMER ":ECD.RET.VALUE:" IS MISSING"
        GOSUB 91000
        GOTO 180
      END
      SCV.REC(18)<1> = ECD.RET.VALUE
      SCV.REC(19)<1> = CUST.NAME
      ECD.ACTION = 3; CALL SCRN.EDIT
      INV.CUST=ECD.RET.VALUE
  END CASE
189*
  RETURN
*
**** ENTER FACTOR
*
190*
*CSF 21824 (LMR 11/3/94)
*25892   IF CATG.R.S.ID # 'Y' THEN
  IF CATG.TRK.LVL='S' THEN
    BEGIN CASE
      CASE INV.PAP.TYPE = "ROLL"
        ECD.NUM = 44
        ECD.ACTION=4;CALL SCRN.EDIT
        IF ECD.RET.VALUE # 'END' THEN
          IF ECD.RET.VALUE = "" THEN
            INV.FACTOR = ""
          END ELSE
            INV.FACTOR = FIELD(ECD.RET.VALUE,".",1)*100000+FIELD(ECD.RET.VALUE,".",2)
          END
        END
      CASE INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
        ECD.PATRN = '0N,0N"."1N,0N"."2N'
        ECD.MINV = 0.10
        ECD.MAXV = 99.99
        ECD.HMSG = "Enter caliper in MILS"
        ECD.NUM = 44
        ECD.ACTION=4;CALL SCRN.EDIT
        IF ECD.RET.VALUE # 'END' THEN
          IF ECD.RET.VALUE = "" THEN
            INV.FACTOR = ""
          END ELSE
            INV.FACTOR = ECD.RET.VALUE * 100
            SCV.REC(44)<ECD.SCRN.NO> = OCONV(INV.FACTOR,"MD2")
            ECD.NUM=44; ECD.ACTION=5; CALL SCRN.EDIT
          END
        END
    END CASE
  END
  RETURN
*
**** ENTER AVG SIZE
*
200 *
*CSF 21824 (LMR 11/3/94)
*25892   IF CATG.R.S.ID # 'Y' THEN
  IF CATG.TRK.LVL='S' THEN
    BEGIN CASE
      CASE INV.PAP.TYPE = "ROLL"
      CASE INV.PAP.TYPE = "LROLL"
      CASE INV.PAP.TYPE = "PCOAT"
      CASE 1
        RETURN
    END CASE
    ECD.NUM = 45
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      INV.AVG.DIA = ECD.RET.VALUE
    END
  END
  RETURN
*
**** ENTER CORE SIZE
*
210 *
*CSF 21824 (LMR 11/3/94)
*25892   IF CATG.R.S.ID # 'Y' THEN
  IF CATG.TRK.LVL='S' THEN
    BEGIN CASE
      CASE INV.PAP.TYPE = "ROLL"
      CASE INV.PAP.TYPE = "LROLL"
      CASE INV.PAP.TYPE = "PCOAT"
      CASE 1
        RETURN
    END CASE
    ECD.NUM = 46
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      INV.CORE.DIA = ECD.RET.VALUE
    END
  END
  RETURN
*
***** GET ALL VALUES FROM FILE
*
1000*
  SCV.REC(6)<ECD.SCRN.NO> = INV.SPEC
  SCV.REC(7)<ECD.SCRN.NO> = INV.LINE
  MATREAD CATG.REC FROM CATEGORY , CONO:INV.LINE ELSE
    MAT CATG.REC = ''
  END
  SCV.REC(8)<ECD.SCRN.NO> = INV.M.LINE
  SCV.REC(11)<ECD.SCRN.NO> = INV.DESC
  SCV.REC(12)<ECD.SCRN.NO> = INV.UNIT<1,1>
  SCV.REC(13)<ECD.SCRN.NO> = INV.UNIT<1,2>
  SCV.REC(14)<ECD.SCRN.NO> = INV.UNIT<1,3>
  SCV.REC(15)<ECD.SCRN.NO,1> = INV.PAP.TYPE
  SCV.REC(18)<ECD.SCRN.NO> = INV.CUST
  IF INV.CUST # "" THEN
    MATREAD CUST.REC FROM CUSTOMER,CONO:INV.CUST ELSE
      CUST.NAME = "??????????????????????????????"
    END
    SCV.REC(19)<ECD.SCRN.NO> = CUST.NAME
  END
  SCV.REC(20)<ECD.SCRN.NO> = OCONV(INV.BAS.WT,"MD2")
  SCV.REC(21)<ECD.SCRN.NO> = INV.COST.WT
  SCV.REC(22)<ECD.SCRN.NO> = INV.M.WT
  SCV.REC(23)<ECD.SCRN.NO> = INV.SBR
  SCV.REC(26)<ECD.SCRN.NO> = INV.TAXABLE
  SCV.REC(28)<ECD.SCRN.NO> = OCONV(INV.SOQ,ICR.CNV)
  SCV.REC(33)<ECD.SCRN.NO> = INV.COLOR
  SCV.REC(34)<ECD.SCRN.NO> = INV.PAP.FINSH
  SCV.REC(35)<ECD.SCRN.NO> = INV.PAP.LEN
  SCV.REC(36)<ECD.SCRN.NO> = INV.PAP.WIDTH
  SCV.REC(40)<ECD.SCRN.NO> = INV.BRIGHT
  SCV.REC(41)<ECD.SCRN.NO> = INV.SHADE
  SCV.REC(42)<ECD.SCRN.NO> = INV.PPI
  SCV.REC(43)<ECD.SCRN.NO> = INV.GRAIN
  BEGIN CASE
    CASE INV.FACTOR = ""
      SCV.REC(44)<ECD.SCRN.NO> = ""
    CASE INV.PAP.TYPE = "ROLL"
      FLEN = LEN(INV.FACTOR)
      IF FLEN > 5 THEN
        SCV.REC(44)<ECD.SCRN.NO> = INV.FACTOR[1,FLEN-5]:".":INV.FACTOR[FLEN-4,5]
      END ELSE
        SCV.REC(44)<ECD.SCRN.NO> = "0.":STR("0",5-FLEN):INV.FACTOR
      END
    CASE INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
      SCV.REC(44)<ECD.SCRN.NO> = OCONV(INV.FACTOR,"MD2")
  END CASE
  SCV.REC(45)<ECD.SCRN.NO> = INV.AVG.DIA
  SCV.REC(46)<ECD.SCRN.NO> = INV.CORE.DIA
  RETURN
*
***** ENTER OPTIONS
*
60000*
  MORE = 1
  LOOP
    ECD.NUM = 38
    SCV.REC(ECD.NUM)<1> = ''
    ECD.ACTION=4;CALL SCRN.EDIT
IF @LOGNAME = 'cmykleb' THEN DEBUG
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE INVENTORY, CONO:PROD.NUM        ;* T26126
        MORE = 1
      CASE NUM(OPTION)
        ON OPTION GOSUB 35,180,10,15,40,95,55,80,85,90,110,120,130,135,140,145,150,155,160,170,190,200,210
        MORE = 0
      CASE OPTION = 'V'
        ECD.SCRN.NO = 2
        ECD.ACTION = 2; CALL SCRN.EDIT
        CALL VEND.PRD.MAINT(STATUS,CONO,PROD.NUM)
        ECD.SCRN.NO = 1
        ECD.ACTION=2;CALL SCRN.EDIT
        ECD.ACTION=3;CALL SCRN.EDIT
        ECD.SCALER = ICR.CNV[3,1]
        ECD.NUM=28;ECD.ACTION=5;CALL SCRN.EDIT
        MORE = 0
      CASE OPTION = 'W'
*            CALL INV.FULL.DESC.SUB(CONO,CONTROL)
*            MATWRITE INV.REC ON INVENTORY, CONO:PROD.NUM
*            CALL GEN.XREF.MAINT(GXR.CO,PROD.NUM,OLD.NAME,INV.FULL.DESC,INVENTORY.XREF,PREFIX)
*            NEW.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
*            NEW.REC = PROD.NUM : AM : INV.COLOR : AM : INV.PAP.LEN : AM : INV.PAP.WIDTH
*            CALL MULTI.XREF.MAINT(NEW.REC,OLD.NAME.XREF,NEW.NAME.XREF,PRODUCT.XREF)
        CALL WHSE.INV.REG.MAINT(STATUS,PROD.NUM,CONO)
        ECD.SCRN.NO = 1
        ECD.ACTION=2;CALL SCRN.EDIT
        ECD.ACTION=3;CALL SCRN.EDIT
        ECD.SCALER = ICR.CNV[3,1]
        ECD.NUM=28;ECD.ACTION=5;CALL SCRN.EDIT
        MORE = 0
      CASE OPTION = 'F'
        CALL INV.FULL.DESC.SUB(CONO,CONTROL)
        MATWRITE INV.REC ON INVENTORY, CONO:PROD.NUM
        CALL GEN.XREF.MAINT(GXR.CO,PROD.NUM,OLD.NAME,INV.FULL.DESC,INVENTORY.XREF,PREFIX)
        NEW.NAME.XREF = CONO:INV.LINE:INV.PAP.FINSH:INV.BAS.WT
        NEW.REC = PROD.NUM : AM : INV.COLOR : AM : INV.PAP.LEN : AM : INV.PAP.WIDTH
        CALL MULTI.XREF.MAINT(NEW.REC,OLD.NAME.XREF,NEW.NAME.XREF,PRODUCT.XREF)
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CONVERT
*
70000*
  MAT INV.CNV.REC = ''
  BEGIN CASE
    CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
      ICR.CNV = "MD0"
      ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
    CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
      ICR.CNV = "MD0"
      ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
    CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
      ICR.CNV = "MD0"
      ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
    CASE 1
      ICR.CNV = "MD2"
      ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
  END CASE
  RETURN
*
*** CHECK FOR QTY'S
*
80000*
  QTY.FLAG = ''  ;*CSF9259
  WHSE.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
  IF WHSE.CNT = 0 THEN RETURN
  FOR A = WHSE.CNT TO 1 STEP -1
    MATREAD IWH.REC FROM INV.WHSE, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE
      MAT IWH.REC = ""
    END
    MATREAD INV.STAT.REC FROM INV.STATS, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE MAT INV.STAT.REC = ""
    PO.QTY = SUM(ISTAT.PO.QTY)
    IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND ISTAT.JOB = "" THEN
      LOC.CNT = COUNT(IWH.LOC,VM) + (IWH.LOC # "")
      FOR DL = LOC.CNT TO 1 STEP -1
        MATREAD IWLO.REC FROM INV.WHSE.LOC,CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL> ELSE MAT IWLO.REC = ""
        IF IWLO.LOC.ON.HAND # "" AND IWLO.LOC.ON.HAND * 0 # 0 THEN QTY.FLAG = 1
      NEXT DL
    END ELSE
      QTY.FLAG = 1
    END
  NEXT A
  IF QTY.FLAG THEN
    ERRMSG = "QTY OR JOB(S) ATTACHED TO THIS ITEM, MAY NOT CHANGE THIS FIELD!!"
    GOSUB 91000
  END
  RETURN
*T22381 v
*
*** CHECK FOR CUSTOMER QTY'S
*
81000*
  CUST.QTY.FLAG = ''
  WHSE.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
  IF WHSE.CNT = 0 THEN RETURN
  FOR A = WHSE.CNT TO 1 STEP -1 UNTIL CUST.QTY.FLAG # ''
    MATREAD IWH.REC FROM INV.WHSE, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE
      MAT IWH.REC = ""
    END
    MATREAD INV.STAT.REC FROM INV.STATS, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE MAT INV.STAT.REC = ""
    PO.QTY = SUM(ISTAT.PO.QTY)
    IF PO.QTY # 0 AND PO.QTY # '' THEN CUST.QTY.FLAG = 1
    PJQ = 0
    PJQ.CNT = DCOUNT(ISTAT.PO,VM)
    FOR PJQ.POS = 1 TO PJQ.CNT
      PJQ += SUM(ISTAT.PO.JOB.QTY<1,PJQ.POS>)
    NEXT PJQ.POS
    IF PJQ # 0 AND PJQ # '' THEN CUST.QTY.FLAG = 1
    IF IWH.RESV # 0 AND IWH.RESV # '' THEN CUST.QTY.FLAG = 1
*C34760         IF IWH.INPRCS # 0 AND IWH.RESV # '' THEN CUST.QTY.FLAG = 1
    IF IWH.INPRCS # 0 AND IWH.INPRCS # '' THEN CUST.QTY.FLAG = 1   ;*C34760
  NEXT A
  IF CUST.QTY.FLAG THEN
    ERRMSG = "THERE IS A QTY ATTACHED TO THIS ITEM; YOU MAY NOT CHANGE THIS FIELD!!"
    GOSUB 91000
  END
  RETURN
*T22381 ^
*
***** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
