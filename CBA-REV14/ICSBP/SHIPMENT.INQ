   SUBROUTINE SHIPMENT.INQ(CONO,PROD.ID)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - SHIPMENT.INQ
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DATE        - 07/10/2000
* TASK #      - 25279
* DESCRIPTION - THIS SUBROUTINE DISPLAYS SHIPMENT HISTORY FOR ONE PRODUCT
* Cxxxxx  01/22/02 edvard  fixed the loop
*T27359 lhelms 04/09/2003 * QTY PROBLEM
*ENDDOC
*********************************************************************
*
*---- INSERT FILES EQUETES
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>OPS.CPYLIB>BOL
*COPY>OPS.CPYLIB>ORDER
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SHIP.TO
*COPY>PMC.CPYLIB>SHIP.VIA
*
*
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
   ESN = ECD.SCRN.NO
   FOR SCV = 1 TO SCV.REC.SIZE
      SCV.REC(SCV)<ECD.SCRN.NO>=""
   NEXT SCV
   ECD.ACTION=2;CALL SCRN.EDIT
   REF.CNT = 0
   REF.NO = ''
   CURR.REF.NO = ''
   LINE.COUNT = 5
   REF.CNT = ''
   SHIP.TOTAL = ''
*
*---- BUILD DATA ARRAY TO DISPLAY
*
   SETINDEX 'F24',PROD.ID ON BOL
1 *
   DONE=0
   LOOP
      READFWD TMP FROM BOL ELSE DONE=1
   UNTIL (DONE) DO
      LOCATE PROD.ID IN TMP<24> SETTING TPV ELSE EXIT
      MATREAD BOL.REC FROM BOL, @ID ELSE MAT BOL.REC = ''
      LOCATE @ID[4,10] IN SCV.REC(7)<ESN>,1 SETTING CHECKIT THEN CONTINUE
      IF BOL.STATUS = 'CANCEL' THEN CONTINUE
      MATREAD SPT.REC FROM SHIP.TO, CONO:BOL.CUST:'!':BOL.SHIP.TO ELSE
         MAT SPT.REC = ''
      END
      MATREAD SHIP.VIA.REC FROM SHIP.VIA, CONO:BOL.SHIP.VIA ELSE
         MAT SHIP.VIA.REC = ''
      END
      MATREAD ORD.REC FROM ORDER, CONO:BOL.ORDER<1,1> ELSE
         MAT ORD.REC = ''
      END
      IF ORD.CUST # "" THEN
         CUST.NO = ORD.CUST
      END ELSE
         CUST.NO = BOL.CUST
      END
      REF.CNT = REF.CNT + 1
*     SCV.REC(6)=INSERT(SCV.REC(6),ESN,1,0,BOL.CUST)
      SCV.REC(6)=INSERT(SCV.REC(6),ESN,1,0,CUST.NO)
      SCV.REC(7)=INSERT(SCV.REC(7),ESN,1,0,TRIM(@ID[4,10]))
      SCV.REC(8)=INSERT(SCV.REC(8),ESN,1,0,BOL.SHP.DATE)
      SCV.REC(9)=INSERT(SCV.REC(9),ESN,1,0,SPT.ADDR1)
      SCV.REC(11)=INSERT(SCV.REC(11),ESN,1,0,REF.CNT)
      SCV.REC(13)=INSERT(SCV.REC(13),ESN,1,0,BOL.ORDER<1,1>)
      PCNT = DCOUNT(BOL.PROD<1>,VM); PROD.TOTAL = 0 ; LOADED = 0
      FOR P = 1 TO PCNT
         LOC.CNT = 0 ;* T27359
         LOC.CNT = DCOUNT(BOL.QTY<1,P>,SVM)
         IF PROD.ID = BOL.PROD<1,P> THEN
            FOR L = 1 TO LOC.CNT
               PROD.TOTAL = PROD.TOTAL + SUM((BOL.QTY<1,P,L>)/1000)  ;* T27359
               SHIP.TOTAL = SHIP.TOTAL + SUM((BOL.QTY<1,P,L>)/1000)  ;* T27359
            NEXT L
            READ IWH.DATA FROM INV.WHSE, CONO:BOL.PROD<1,P>:'!':BOL.WHSE<1,P> THEN
               IF LOADED = 0 THEN
                  LOCATE BOL.FI.NO<1,P,1> IN IWH.DATA<56>,1 SETTING BFN THEN
                     SCV.REC(12)=INSERT(SCV.REC(12),ESN,1,0,IWH.DATA<38,BFN>)
                     LOADED = 1
                  END
               END
            END
         END
      NEXT P
      SCV.REC(14)=INSERT(SCV.REC(14),ESN,1,0,OCONV(PROD.TOTAL,'MD0,'))
      SCV.REC(15)=INSERT(SCV.REC(15),ESN,1,0,SPT.NAME)
      SCV.REC(16)=INSERT(SCV.REC(16),ESN,1,0,SHPV.DESC)
      SCV.REC(17)=INSERT(SCV.REC(17),ESN,1,0,BOL.INVOICE)
   REPEAT
2 *
   SCV.REC(1)<ESN> = PROD.ID
   ECD.NUM=1;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(2)<ESN> = INV.FULL.DESC
   ECD.NUM=2;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(3)<ESN> = 'ALL'
   ECD.NUM=3;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(21)<ESN> = OCONV(SHIP.TOTAL,'MD0,')
   ECD.NUM=21;ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(32)<ESN> = INT(REF.CNT / LINE.COUNT + .9)
   ECD.NUM=32;ECD.ACTION=5;CALL SCRN.EDIT
   IF REF.CNT GT 0 THEN GOSUB 1900
   LOOP
      ECD.NUM = 41; SCV.REC(ECD.NUM)<ESN> = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE ACTION = "" OR ACTION = "END"
            ACTION = "END"
         CASE ACTION = "S" AND REF.CNT GT 0
            REF.NO=CURR.REF.NO + LINE.COUNT
            IF REF.NO > REF.CNT THEN REF.NO=1
            GOSUB 1900
         CASE ACTION = "SF" AND REF.CNT GT 0
            REF.NO=CURR.REF.NO + LINE.COUNT
            IF REF.NO > REF.CNT THEN REF.NO=1
            GOSUB 1900
         CASE ACTION = "SR" AND REF.CNT GT 0
            REF.NO=CURR.REF.NO - LINE.COUNT
            IF REF.NO < 1 THEN REF.NO=1
            GOSUB 1900
         CASE ACTION = "ST"
            REF.NO = 1
            GOSUB 1900
         CASE ACTION = "SB"
            REF.NO = REF.CNT
            GOSUB 1900
         CASE NOT(NUM(ACTION))
            ERRMSG = "Invalid entry, please re-enter"
            GOSUB 91000
         CASE ACTION LT 1 OR ACTION GT REF.CNT
            ERRMSG = "Out of range, please re-enter"
            GOSUB 91000
         CASE 1
            LOCATE ACTION IN SCV.REC(11)<ESN>,1 SETTING BCNT THEN
               BOL.PRINT.NO = SCV.REC(7)<ESN,BCNT>
               CALL SHIPTO.BOL.PRINT.SUB(CONO,BOL.PRINT.NO,'P')
            END
      END CASE
   WHILE ACTION # "END" DO REPEAT
   GOTO 99999
*
*---- Display Multi Line fields
1900*
   START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)* LINE.COUNT
   IF START.REF.NO = CURR.REF.NO THEN RETURN
   CURR.REF.NO = START.REF.NO
   IF CURR.REF.NO LT LINE.COUNT THEN
      PG.NO = 1
   END ELSE
      PG.NO = INT(CURR.REF.NO / LINE.COUNT) + 1
   END
   ECD.NUM=11;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
*  ECD.NUM=12;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=13;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=14;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=15;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=16;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=17;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=6;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=7;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=8;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=9;ECD.SUB.NUM=START.REF.NO;ECD.ACTION=7;CALL SCRN.EDIT
   SCV.REC(31)<ESN> = PG.NO
   ECD.NUM=31;ECD.ACTION=5;CALL SCRN.EDIT
   RETURN
*
*---- Error message routine
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- End of job
*
99999*
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
