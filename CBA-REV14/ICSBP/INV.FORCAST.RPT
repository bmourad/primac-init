*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.FORCAST.RPT
* DATE        - 12/09/86
* BY          - CURTIS WILLIAMS , C.B.A
* DESCRIPTION - This Program list detail inventory requirements
*               forecasting report.
*********************************************************************
*
*---- INSERT EQUATE FROM CPYLIB
*COPY>JCS.CPYLIB>JOB
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>EDIT.COM.DRIVER
*
*---- SET UP FOR SYSCOM
*
   SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
*
*---- DIMENTION RECORD
   DIM DATE.REC(5)
   DIM BALANCE(6)
   DIM TOT.BALA(6)
*
*---- OPEN FILES
*
   OPEN '','JOB' TO JOB ELSE
      ERRMSG = 'JOB FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','INV.WHSE' TO INV.WHSE ELSE
      ERRMSG = 'INV.WHSE FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'INVENTORY FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','INV.STATS' TO INV.STATS ELSE
      ERRMSG = 'INV.STATS FILE IS MISSING'
      GOTO 93000
   END
   OPEN '', 'INV.JOB.STATS' TO INV.JOB.STATS ELSE
      ERRMSG = 'INV.JOB.STATS FILE IS MISSING'
      GOTO 93000
   END
   OPEN '','JCS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'JCS.SCREENS FILE IS MISSING'; GOTO 93000
*
*---- INITIALIZATION
*
   END.FLG = 0
   PRT.DESC = 0
   PRT.PROD = 0
   MAT EDIT.COM.DRIVER = ''
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'FORECAST.DATES'
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   MAT SCV.REC = ''
   MAT DATE.REC = ''
   MAT BALANCE = ''
   MAT TOT.BALA = ''
   ICR.CNV = ""
   LINE.COUNT = 99 ; PAGE.NO = 0
   SP1 = SPACE(1)
   D.LINE = "------------------------------- --- ---------- ---------- -------- ---------- ---------- ---------- ---------- ---------- ----------"
   T.LINE = "PRODUCT NUM / FULL DESCRIPTION  U/M  ON-HAND    JOB NUM   REQ DATE  PAST DUE   31 DAYS    61 DAYS    91 DAYS    121 DAYS   OVER 121"
   FT.LINE = "ON/AFTER   ON/AFTER   ON/AFTER   ON/AFTER     OVER"
   B.LINE = "========== ========== ========== ========== ========== =========="
*
*----PROCS VALUES
*
   PROCREAD ID ELSE
      ERRMSG = ' CANNOT PERFORM PROCREAD'
      GOTO 93000
   END
   CONO = ID<1>
   DATE.STATS = ID<4>
*T26493 v
*  CONO.NAME = ID<2>
*  REPORT.NAME = "INVENTORY REQUIREMENT FORECAST"
*  REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = ID<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26943 ^
   REPORT.DATE = DATE()
   HD1 = "" ; HD2 = ""
*
*---- GET HEADING
*
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
*----IF SPECIFIC DATE
*
   IF DATE.STATS = 1 THEN
      MORE = 0
      ECD.ACTION = 2; CALL SCRN.EDIT
      FOR I = 1 TO 5 UNTIL ECD.RET.VALUE = 'END'
         ON I GOSUB 7000,8000,9000,10000,11000
      NEXT I
      LOOP UNTIL MORE DO
         ECD.NUM = 6; ECD.ACTION = 4; CALL SCRN.EDIT
         REQUEST = ECD.RET.VALUE
         BEGIN CASE
            CASE NUM(REQUEST)
               ON REQUEST GOSUB 7000,8000,9000,10000,11000
            CASE REQUEST = 'E'
               END.FLG = 1
               MORE = 1
            CASE REQUEST = 'C'
               MORE = 1
         END CASE
      REPEAT
   END ELSE
      DATE.REC(1) = DATE()
      DATE.REC(2) = DATE.REC(1) + 30
      DATE.REC(3) = DATE.REC(1) + 60
      DATE.REC(4) = DATE.REC(1) + 90
      DATE.REC(5) = DATE.REC(1) + 120
   END
   IF END.FLG = 1 THEN GOTO 99990
*
*---- PRINTER ON
*
   PRINTER ON
*
*---- READ AND PROCESS FIRST RECORD
*
100*
   READNEXT KEY ELSE GOTO 99990
   GOSUB 200
   MATREAD IWH.REC FROM INV.WHSE,IWHSE.KEY ELSE GOTO 100
   MATREAD JOB.REC FROM JOB,CONO : JOB.NUM ELSE GOTO 100
   MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
      MAT INV.REC = ''
   END
   MATREAD INV.JS.REC FROM INV.JOB.STATS , KEY ELSE
      MAT INV.JS.REC = ""
   END
*---- SET THE WHSE PRODUCT
   PREV.WHSE = WHSE
   PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
   PREV.PROD = PROD
   PREV.DESC = INV.FULL.DESC
*---- PROCESS THE FILE
   GOSUB 2000
   IF BALA < 1 THEN GOTO 100
   PREV.CNV = ICR.CNV
*---- PRINT THE RECORD
   GOSUB 3000
*---- READ AND PROCESS ALL THE FILES
   DATA = 0
   LOOP
      READNEXT KEY ELSE DATA = 1
   WHILE DATA = 0 DO
      GOSUB 200
      MATREAD IWH.REC FROM INV.WHSE,IWHSE.KEY ELSE GOTO 111
      MATREAD JOB.REC FROM JOB,CONO : JOB.NUM ELSE GOTO 111
      MATREAD INV.REC FROM INVENTORY,PROD.KEY ELSE
         MAT INV.REC = ''
      END
      MATREAD INV.JS.REC FROM INV.JOB.STATS,KEY ELSE
         MAT INV.JS.REC = ""
      END
*---- PROCESS THEN FILE
      GOSUB 2000
      IF BALA < 1 THEN GOTO 111
*---- PRINT THE RECORD
      GOSUB 3000
      TOT.DESC = 0
111*
   REPEAT
   GOSUB 4000
*---- END OF JOB
   PRINTER OFF
   GOTO 99990
*---- SEPERATE THE KEY TO PRPDUCT , WHSE
200*
   PROD.KEY = FIELD(KEY,"!",1)
   PROD = PROD.KEY[4,99]
   WHSE    = FIELD(KEY,"!",2)
   JOB.NUM = FIELD(KEY,"!",3)
   IWHSE.KEY = PROD.KEY:"!":WHSE
   RETURN
*---- PRINT HEADINGS
1000*
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1:PAGE.NO
   PRINT HD2
   PRINT
   PRINT "WAREHOUSE : ":PREV.WHSE "L#8"
   PRINT "MLINE / PLINE : ":PREV.M.P.LINE
   PRINT
   LINE.COUNT = 6
   IF DATE.STATS = 1 THEN
      PRINT SPACE(79): FT.LINE
      T.LINE = T.LINE[1,68]:"CURRENT":SPACE(4)
      FOR I = 1 TO 5
         T.LINE = T.LINE: OCONV(DATE.REC(I),"D2/")"L#8":SPACE(3)
      NEXT I
      PRINT T.LINE
      LINE.COUNT = LINE.COUNT + 2
   END ELSE
      PRINT T.LINE
      LINE.COUNT = LINE.COUNT + 1
   END
   PRINT D.LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
*---- PROCESS THE RECORD
2000*
   PREV.CNV = ICR.CNV; MAT INV.CNV.REC = ''
   BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2"
         ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
   END CASE
   USED.QTY = 0; PTR = 1
   LOOP
      LOCATE PROD IN JOB.RESV.MATL<1>,PTR SETTING MFND ELSE MFND = 0
      BEGIN CASE
         CASE MFND = 0
            PTR = 0
         CASE WHSE = JOB.RESV.WHSE<1,MFND>
            PTR = 0
            USED.QTY = JOB.USED.QTY<1,MFND>
         CASE 1
            PTR = MFND + 1
      END CASE
   WHILE PTR DO REPEAT
   ON.HAND = INT((((IWH.ON.HAND/ICR.DV1) * ICR.MT1)/ICR.DV2)+.5)
   RESV.QTY = INT((((IJS.JOB.QTY/ICR.DV1) * ICR.MT1)/ICR.DV2)+.5)
   REQ.QTY = INT((((IJS.REQ.QTY/ICR.DV1) * ICR.MT1)/ICR.DV2)+.5)
   USED.QTY = INT((((USED.QTY/ICR.DV1) * ICR.MT1)/ICR.DV2)+.5)
   ALOC.QTY = INT((((IJS.JOB.ALOC/ICR.DV1) * ICR.MT1)/ICR.DV2)+.5)
   BALA = REQ.QTY - USED.QTY - RESV.QTY - ALOC.QTY
   IF BALA < 1 THEN
      ICR.CNV = PREV.CNV;  GOTO 2999
   END
   BEGIN CASE
      CASE IJS.REQ.DATE < DATE.REC(1)
         BALANCE(1) = BALA
      CASE IJS.REQ.DATE < DATE.REC(2)
         BALANCE(2) = BALA
      CASE IJS.REQ.DATE < DATE.REC(3)
         BALANCE(3) = BALA
      CASE IJS.REQ.DATE < DATE.REC(4)
         BALANCE(4) = BALA
      CASE IJS.REQ.DATE < DATE.REC(5)
         BALANCE(5) = BALA
      CASE 1
         BALANCE(6) = BALA
   END CASE
   BEGIN CASE
      CASE WHSE # PREV.WHSE
         PRT.PROD = 0
         GOSUB 4000
         PREV.WHSE = WHSE
         PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
         PREV.PROD = PROD
         PREV.DESC = INV.FULL.DESC
         LINE.COUNT = 99
      CASE INV.M.LINE:" / ":INV.LINE # PREV.M.P.LINE
         PRT.PROD = 0
         GOSUB 4000
         PREV.M.P.LINE = INV.M.LINE:" / ":INV.LINE
         PREV.PROD = PROD
         PREV.WHSE = WHSE
         PREV.DESC = INV.FULL.DESC
         LINE.COUNT = 99
      CASE PROD # PREV.PROD
         PRT.PROD = 0
         GOSUB 4000
         PREV.PROD = PROD
         PREV.DESC = INV.FULL.DESC
   END CASE
2999*
   RETURN
*----   PRINT THE RECORD
3000*
   IF LINE.COUNT > 53 THEN
      GOSUB 1000
   END
   LINE = ''
   IF PRT.PROD = 0 THEN
      LINE = PROD "L#31":SPACE(1)
      LINE = LINE: INV.UNIT<1,2>"L#3"
      LINE = LINE: SP1: OCONV(ON.HAND,ICR.CNV)"R#10"
      PRT.PROD = 1
   END ELSE
      IF PRT.DESC = 0 THEN
         LINE = INV.FULL.DESC "L#45" :SP1
         PRT.DESC = 1
         TOT.DESC = 1
      END ELSE
         LINE = SPACE(46)
      END
   END
   LINE = LINE: SP1: JOB.NUM "L#10"
   LINE = LINE: SP1: OCONV(IJS.REQ.DATE,"D2/") "L#8": SP1
   FOR I = 1 TO 6
      LINE = LINE: OCONV(BALANCE(I),ICR.CNV)"R#10":SP1
   NEXT I
   PRINT LINE'L#132'
   LINE.COUNT = LINE.COUNT + 1
   FOR I = 1 TO 6
      TOT.BALA(I) = TOT.BALA(I) + BALANCE(I)
      BALANCE(I) = ''
   NEXT I
   RETURN
*---- CHECK FOR END OF PAGE
4000*
   IF LINE.COUNT + 3 > 53 THEN GOSUB 1000
   LINE.COUNT = LINE.COUNT + 3
   LINE = ''
   B.LINE = "========== ========== ========== ========== ========== =========="
   IF PRT.DESC = 0 THEN
      B.LINE = PREV.DESC "L#67":B.LINE
   END ELSE
      B.LINE = SPACE(67):B.LINE
   END
   LINE = SPACE(67)
   FOR I = 1 TO 6
      LINE =  LINE:OCONV(TOT.BALA(I),PREV.CNV)"R#10":SP1
      TOT.BALA(I) = 0
   NEXT I
   PRINT B.LINE
   PRINT LINE
   PRINT
   PRT.DESC = 0
   RETURN
7000*
   ECD.NUM = 1; ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE = '' THEN GOTO 7000
   IF ECD.RET.VALUE = 'END' THEN RETURN
   DATE.REC(1) = ECD.RET.VALUE
   RETURN
8000*
   ECD.NUM = 2; ECD.ACTION = 4
   ECD.DEFAULT = DATE.REC(1) + 30; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN GOTO 7000
   DATE.REC(2) = ECD.RET.VALUE
   RETURN
9000*
   ECD.NUM = 3; ECD.ACTION = 4
   ECD.DEFAULT = DATE.REC(1) + 60; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN GOTO 7000
   DATE.REC(3) = ECD.RET.VALUE
   RETURN
10000*
   ECD.NUM = 4; ECD.ACTION = 4
   ECD.DEFAULT = DATE.REC(1) + 90; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN GOTO 7000
   DATE.REC(4) = ECD.RET.VALUE
   RETURN
11000*
   ECD.NUM = 7; ECD.ACTION = 4
   ECD.DEFAULT = DATE.REC(1) + 120; CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN GOTO 7000
   DATE.REC(5) = ECD.RET.VALUE
   RETURN
*----   CALLS FOR SYSCOM
91000*
   ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   PRINTER OFF
   ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
   GOTO 99999
*
99990 *
   ECD.ACTION = 99 ; CALL SCRN.EDIT
*
99999*
END
