*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM        - PRIMAC
* PROGRAM       - SPEC.PROD.DEL.ELIG
* BY            - DANIEL GRUENENFELDER, VERCOM SOFTWARE
* DATE          - 09/03/91
* DESCRIPTION   - This program prints a report showing products eligible for delete special product.
*T26259 adelgado 11/29/2001 * Add 'PRINTER OFF' statement.
*T26329 adelgado 01/29/2002 * Change naming of 'SPECIAL' to 'OBSOLETE'
*T29194 lross 06/09/2008 * Update with OPS module data.
*ENDDOC
*********************************************************************
**** INSERT FILES EQUATES
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>SPECIAL.PROD
*T29194 v
*COPY>ICS.CPYLIB>FNGD.STATS
*COPY>ICS.CPYLIB>FNGD.JOB.STATS
*COPY>PMC.CPYLIB>COMPANY
*T29194 ^
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
***
*
MAT IWH.REC = "" ; MAT IWLO.REC = "" ; MAT INV.REC = ""
MAT INV.STAT.REC = "" ; MAT INV.JS.REC = ""
LINE.COUNT = 99; PAGE.NO = 0
*
**** SETUP ERRMSG ROUTINE
*
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*** GET PROC VALUES
PROCREAD ID ELSE
   ERRMSG = ' CANNOT PERFORM PROCREAD'
   GOTO 93000
END
*
***** OPEN FILES
*
OPEN '','INVENTORY' TO INVENTORY ELSE
   ERRMSG = 'INVENTORY FILE IS MISSING'
   GOTO 93000
END
OPEN '','INV.STATS' TO INV.STATS ELSE
   ERRMSG = "INV.STATS FILE IS MISSING" ; GOTO 93000
END
OPEN '','INV.JOB.STATS' TO INV.JOB.STATS ELSE
   ERRMSG = "INV.JOB.STATS FILE IS MISSING" ; GOTO 93000
END
OPEN '','INV.WHSE' TO INV.WHSE ELSE
   ERRMSG = "INV.WHSE FILE IS MISSING" ; GOTO 93000
END
OPEN '','INV.WHSE.LOC' TO INV.WHSE.LOC ELSE
   ERRMSG = "INV.WHSE.LOC FILE IS MISSING" ; GOTO 93000
END
OPEN '','INVENTORY.XREF' TO INVENTORY.XREF ELSE
   ERRMSG = 'INVENTORY.XREF FILE IS MISSING'
   GOTO 93000
END
OPEN '','SPECIAL.PROD' TO SPECIAL.PROD ELSE
   ERRMSG = 'SPECIAL.PROD FILE IS MISSING'
   GOTO 93000
END
OPEN '','PREFIX' TO PREFIX ELSE
   ERRMSG = 'PREFIX FILE IS MISSING'
   GOTO 93000
END
*T29194 v
OPEN '','FNGD.STATS' TO FNGD.STATS ELSE
   ERRMSG = "FNGD.STATS FILE IS MISSING" ; GOTO 93000
END
OPEN '','FNGD.JOB.STATS' TO FNGD.JOB.STATS ELSE
   ERRMSG = "FNGD.JOB.STATS FILE IS MISSING" ; GOTO 93000
END
OPEN '','COMPANY' TO COMPANY ELSE
   ERRMSG = "COMPANY FILE IS MISSING" ; GOTO 93000
END
*T29194 ^
HD.LINE = "PRODUCT NUMBER               FULL DESCRIPTION"
T.LINE = "--------------- ---------------------------------------------"
CONO = ID<1>
*T26556 v
* CONO.NAME = ID<2>
* REPORT.NAME = "OBSOLETE.PRODUCT.DELETE.ELIGIBLE"
* REPORT.NUMBER = "XXXX"
CONO.NAME = ""
REPORT.NAME = ""
REPORT.NUMBER = ID<2>
*T26556 ^
REPORT.DATE = DATE()
HD1 = "" ; HD2 = ""
*** GET HEADING
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*T29194 v
MATREAD COMP.REC FROM COMPANY,CONO ELSE MAT COMP.REC = ''
*** PRINTER ON
PRINTER ON
*
***** MAIN PROCESS
DATA = 1
LOOP
   READNEXT KEY ELSE DATA = 0
   CONO = KEY[1,3]
   PROD.NO = KEY[4,99]
WHILE DATA = 1 DO
   MATREADU INV.REC FROM INVENTORY , KEY ELSE
      MAT INV.REC = '' ; GOTO 111
   END
   MAT SPD.REC = ""
   WHSE.CNT = COUNT(INV.WHSE.CODE, VM) + (INV.WHSE.CODE # "")
   FOR A = WHSE.CNT TO 1 STEP -1
      MATREADU IWH.REC FROM INV.WHSE, KEY:"!":INV.WHSE.CODE<1,A> ELSE
         MAT IWH.REC = ""
      END
      MATREADU INV.STAT.REC FROM INV.STATS , KEY:"!":INV.WHSE.CODE<1,A> ELSE
         MAT INV.STAT.REC = ''
      END
      PO.QTY = 0
*T29194 v
      DEL.TEST = 0
      IF INV.M.LINE = 'FNGD' AND CO.OPS = 'Y' THEN
         MATREAD FGS.REC FROM FNGD.STATS, KEY:"!":INV.WHSE.CODE<1,A> ELSE
            MAT FGS.REC = ''
         END
         PO.QTY = SUM(FGS.M.QTY)
         IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND FGS.ORDER = "" AND ISTAT.JOB = "" THEN DEL.TEST = 1
      END ELSE
         PO.CNT = DCOUNT(ISTAT.PO.QTY, VM)
         FOR XX = 1  TO PO.CNT
            PO.QTY = PO.QTY + ISTAT.PO.QTY<1,XX>
         NEXT XX
         IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND ISTAT.JOB = "" THEN DEL.TEST = 1
      END
*     IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND ISTAT.JOB = "" THEN
      IF DEL.TEST THEN
*T29194 ^
         LOC.CNT = DCOUNT(IWH.LOC, VM)
         FOR DL = LOC.CNT TO 1 STEP -1
            MATREADU IWLO.REC FROM INV.WHSE.LOC , KEY:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL> ELSE
               RELEASE INV.WHSE.LOC , KEY:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL>
               MAT IWLO.REC = ""
            END
            IF IWLO.LOC.ON.HAND + 0 = 0 THEN
               IWH.LOC = DELETE(IWH.LOC,1,DL,0)
            END
            RELEASE INV.WHSE.LOC , KEY:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL>
         NEXT DL
*T29194  INV.WHSE.CODE = DELETE(INV.WHSE.CODE,1,A,0)
         IF IWH.LOC = "" THEN INV.WHSE.CODE = DELETE(INV.WHSE.CODE,1,A,0)
      END
      RELEASE INV.WHSE , KEY:"!":INV.WHSE.CODE<1,A>
      RELEASE INV.STATS, KEY:"!":INV.WHSE.CODE<1,A>
   NEXT A
   IF INV.WHSE.CODE = '' THEN
*** key is eligible, print line here *
      IF LINE.COUNT > 53 THEN GOSUB 1000
      LINE.COUNT = LINE.COUNT + 1
      H.LINE = ''
      H.LINE = H.LINE :PROD.NO "L#15"
      H.LINE = H.LINE :" ": INV.FULL.DESC "L#45"
      PRINT H.LINE
   END ELSE
      RELEASE INVENTORY, KEY
   END
111 *
   RELEASE
REPEAT
PRINTER OFF     ;* T26259
PRINTER CLOSE   ;* T26259
GOTO 99999
*** PRINT HEADINGS
1000 *
PRINT CHAR(12)
PAGE.NO = PAGE.NO + 1
PRINT HD1:PAGE.NO
PRINT HD2
PRINT
PRINT HD.LINE
PRINT T.LINE
PRINT
LINE.COUNT = 6
RETURN
*
***** CALLS FOR SYSCOM
*
91000 *
ERR.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
92000 *
ERR.TYPE = 2
CALL SYSCOM(MAT SYSCOM.REC)
RETURN
93000 *
ERR.TYPE = 3
CALL SYSCOM(MAT SYSCOM.REC)
99999 *
END
