SUBROUTINE GET.SERIAL.ID(CONO,ISTK.SEQ,CONTROL,INV_SERIAL) 
************************************************************* 
* REVISION- [12.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM- PRIMAC
* SOURCE- ICSBP 
* PROGRAM - GET.SERIAL.ID 
*BY- EDVARD PITKA
* DATE- 11/14/01
* DESCRIPTION - This program gets the next available key for the
* INV_SERIAL file.
*ENDDOC 
************************************************************* 
* 
*---- FILE EQUATES
* 
*COPY>CPYLIB>CHAR 
OPEN '','INV_SERIAL_TEMP' TO INV_SERIAL_TEMP ELSE STOP "INV_SERIAL_TEMP FILE IS MISSING"
READU SERIAL.SEQ FROM CONTROL, CONO:"ISTK.SEQ" ELSE
  SERIAL.SEQ = 0
END 
FND=0
LOOP
  SERIAL.SEQ+=1
  IF LEN(SERIAL.SEQ) > 8 THEN SERIAL.SEQ = 1
  ISTK.ID=CONO:SERIAL.SEQ"R%8"
  IF RECORDLOCKED(INV_SERIAL_TEMP,ISTK.ID)=0 THEN
    DELETE INV_SERIAL_TEMP,ISTK.ID
  END
  READU CHECKREC FROM INV_SERIAL_TEMP,ISTK.ID LOCKED
    NULL
  END ELSE
    READU CHECKREC FROM INV_SERIAL,ISTK.ID THEN
      RELEASE INV_SERIAL, ISTK.ID 
      RELEASE INV_SERIAL_TEMP,ISTK.ID
    END ELSE 
      FND = 1
    END
  END
UNTIL (FND) DO
  RELEASE INV_SERIAL_TEMP,ISTK.ID
  RELEASE INV_SERIAL,ISTK.ID
REPEAT 
WRITE SERIAL.SEQ ON CONTROL, CONO:"ISTK.SEQ"
ISTK.SEQ=SERIAL.SEQ"R%8"
END
