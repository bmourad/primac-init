   SUBROUTINE RS.STOCK.REC.SUB(CONO,MAT DSR.REC,MAT PO.REC)
*********************************************************************
* REVISION    - [11.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - RS.STOCK.REC.SUB
* BY          - CHRIS MYKLEBUST, PRIMAC SYSTEMS
* DATE        - 08/19/2004
* DESCRIPTION - This screen will come up after a receipt is filed and
*               when the complete po line qty flag in Inventory Control in
*               Company Maintenance is set to a "Y".  This screen will
*               allow the user to cancel the remaining quantites on a po
*               line if desired.
*
* T27920 cmykleb 08/19/2004 * Allow the user to close a purchase order
*                             from inventory receipting.
*C44166 lross 12/16/2004 * Convert to SCRN.EDIT.
*T28422 lross 02/11/2005 * Qty display incorrect when DIFF.UM = Y.
*********************************************************************
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.MAIN  
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>PO
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>ICS.CPYLIB>DAILY_STOCK
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>TCC
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
*** INITIALIZATIONS
*
   ECD.SCRN.NO = 2
   ECD.ACTION = 2; CALL SCRN.EDIT
   ECD.ACTION = 3; CALL SCRN.EDIT
   LINE.COUNT = DCOUNT(DSR.PO.LINE<1>,VM)
   LINE.SPACE = 1
   ERRMSG = ""
   TOT.OPEN.PO=0
   OPT = ""
   DIM CAN.REC(10)
   MAT CAN.REC = ''
   EQU CAN.PO.LINE    TO CAN.REC(1)
   EQU CAN.PROD.ID    TO CAN.REC(2)
   EQU CAN.ON.ORDER   TO CAN.REC(3)
   EQU CAN.RCVD       TO CAN.REC(4)
   EQU CAN.OPEN       TO CAN.REC(5)
   EQU CAN.CANCEL     TO CAN.REC(6)
*
*** MAIN PROCESSING
*
100 *
*
   *** LOAD THE SCREEN INFORMATION ***
   PCNT = DCOUNT(DSR.PO.LINE<1>,VM)
   FRST.LN.TO.CAN=0
   FOR P = 1 TO PCNT
      PO.LN = DSR.PO.LINE<1,P>
      PROD.ID = DSR.PROD<1,P>
      CANCEL.FLAG = DSR.COMPLETE<1,P>
      CAN.PO.LINE<1,P> = PO.LN
      CAN.PROD.ID<1,P> = PROD.ID
      CAN.CANCEL<1,P> = CANCEL.FLAG
      TOT.RCVD = PO.TOT.RECEVED<1,PO.LN> + SUM(DSR.QTY<1,P>)
      TOT.OPEN = PO.TOT.ONORD<1,PO.LN> - TOT.RCVD - PO.TOT.CANCEL<1,PO.LN>
      IF TOT.OPEN < 0 THEN TOT.OPEN = 0
      IF NOT(FRST.LN.TO.CAN) AND TOT.OPEN # 0 THEN FRST.LN.TO.CAN = P
      IF TOT.OPEN = 0 AND CAN.CANCEL<1,P> = '' THEN CAN.CANCEL<1,P> = 'N'
      TOT.OPEN.PO += TOT.OPEN
      MATREAD INV.REC FROM INVENTORY, CONO:PROD.ID ELSE
         MAT INV.REC = ''
      END
*COPY>ICSBP>INV.UM.CNV
*T28422 v
      BEGIN CASE                                             
*        CASE PO.UNIT.FLG<1,PO.LN> = "SHT" AND INV.UNIT<1,3> = "LBS"       
         CASE DSR.UOM<1,PO.LN> = "SHT" AND INV.UNIT<1,3> = "LBS"       
            ICR.CNV<1,2> = "MD0";             ICR.DV2<1,2> = 1  
            ICR.DV1<1,2> = INV.M.WT;          ICR.MT1<1,2> = 1  
*        CASE PO.UNIT.FLG<1,PO.LN> = "PC" AND INV.UNIT<1,3> = "MSI"        
         CASE DSR.UOM<1,PO.LN> = "PC" AND INV.UNIT<1,3> = "MSI"        
            ICR.CNV<1,2> = "MD0";             ICR.DV2<1,2> = 1  
            ICR.DV1<1,2> = INV.PAP.WIDTH/100; ICR.MT1<1,2> = 10 
*        CASE PO.UNIT.FLG<1,PO.LN> = "FT" AND INV.UNIT<1,3> = "MSI"        
         CASE DSR.UOM<1,PO.LN> = "FT" AND INV.UNIT<1,3> = "MSI"        
            ICR.CNV<1,2> = "MD0";             ICR.DV2<1,2> = 12 
            ICR.DV1<1,2> = INV.PAP.WIDTH/100; ICR.MT1<1,2> = 100
         CASE 1                                                 
            ICR.CNV<1,2> = "MD2";             ICR.DV2<1,2> = 1  
            ICR.DV1<1,2> = 10;                ICR.MT1<1,2> = 1  
      END CASE                                               
*     IF PO.UNIT.FLG<1,PO.LN> # INV.UNIT<1,2> THEN
      IF DSR.UOM<1,PO.LN> # INV.UNIT<1,2> THEN
         DIFF.UM = "Y"
      END ELSE
         DIFF.UM = "N"
      END
      TOT.RCVD=0
      IF DIFF.UM # 'Y' THEN
         CAN.ON.ORDER<1,P> = OCONV(INT(PO.TOT.ONORD<1,PO.LN>/ICR.DV1<1,1> * ICR.MT1<1,1> / ICR.DV2<1,1> + .5),ICR.CNV<1,1>)
         FOR I = 1 TO DCOUNT(DSR.QTY<1,P>,SVM)
           CAN.RCVD<1,P> += OCONV(INT(DSR.QTY<1,P,I>/ICR.DV1<1,1> * ICR.MT1<1,1> / ICR.DV2<1,1> + .5),ICR.CNV<1,1>)
         NEXT I
         CAN.OPEN<1,P> = CAN.ON.ORDER<1,P> - CAN.RCVD<1,P>
         SCLR = ICR.CNV<1,1>[LEN(ICR.CNV<1,1>),1]
         IF SCLR > 0 THEN
           MPLR = '1' : STR('0',SCLR)
           CAN.RCVD<1,P> *= MPLR
           CAN.RCVD<1,P> = OCONV(CAN.RCVD<1,P>,ICR.CNV<1,1>)
           CAN.OPEN<1,P> *= MPLR
           CAN.OPEN<1,P> = OCONV(CAN.OPEN<1,P>,ICR.CNV<1,1>)
         END
*        CAN.OPEN<1,P> = OCONV(INT(TOT.OPEN/ICR.DV1<1,1> * ICR.MT1<1,1> / ICR.DV2<1,1> + .5),ICR.CNV<1,1>)
      END ELSE
         CAN.ON.ORDER<1,P> = OCONV(INT(PO.TOT.ONORD<1,PO.LN>/ICR.DV1<1,2> * ICR.MT1<1,2> / ICR.DV2<1,2> + .5),ICR.CNV<1,2>)
         FOR I = 1 TO DCOUNT(DSR.QTY<1,P>,SVM)
           CAN.RCVD<1,P> += OCONV(INT(DSR.QTY<1,P,I>/ICR.DV1<1,2> * ICR.MT1<1,2> / ICR.DV2<1,2> + .5),ICR.CNV<1,2>)
         NEXT I
         CAN.OPEN<1,P> = CAN.ON.ORDER<1,P> - CAN.RCVD<1,P>
         SCLR = ICR.CNV<1,2>[LEN(ICR.CNV<1,2>),1]
         IF SCLR > 0 THEN
           MPLR = '1' : STR('0',SCLR)
           CAN.RCVD<1,P> *= MPLR
           CAN.RCVD<1,P> = OCONV(CAN.RCVD<1,P>,ICR.CNV<1,2>)
           CAN.OPEN<1,P> *= MPLR
           CAN.OPEN<1,P> = OCONV(CAN.OPEN<1,P>,ICR.CNV<1,2>)
         END
*        CAN.OPEN<1,P> = OCONV(INT(TOT.OPEN/ICR.DV1<1,2> * ICR.MT1<1,2> / ICR.DV2<1,2> + .5),ICR.CNV<1,2>)
      END
*T28422 ^
   NEXT P
   *** LOAD THE SCREEN ***
   SCV.REC(2)<ECD.SCRN.NO> = DSR.PO
   ECD.NUM=2; ECD.ACTION=5; CALL SCRN.EDIT
   SCV.REC(5)<ECD.SCRN.NO> = CAN.PO.LINE
   SCV.REC(6)<ECD.SCRN.NO> = CAN.PROD.ID
   SCV.REC(7)<ECD.SCRN.NO> = CAN.ON.ORDER
   SCV.REC(8)<ECD.SCRN.NO> = CAN.RCVD
   SCV.REC(9)<ECD.SCRN.NO> = CAN.OPEN
   SCV.REC(10)<ECD.SCRN.NO> = CAN.CANCEL
   REF.NO = 1
   CURR.REF.NO = ''
   GOSUB 3000
*  IF DSR.COMPLETE<1,1> = '' THEN GOSUB 2000 ; * FIRST TIME ENTRY
   LOCATE '' IN CAN.CANCEL<1>,1 SETTING NFND THEN GOSUB 2000
*
*----- GET OPERATOR REPLY
*
500 *
   ECD.NUM=21
   ECD.ACTION=4; CALL SCRN.EDIT
   OPT = ECD.RET.VALUE
   BEGIN CASE
      CASE OPT = ''
         GOTO 99999
      CASE OPT = 'C'
         GOSUB 1000
      CASE OPT = 'S'
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > PCNT THEN REF.NO = 1
         GOSUB 3000
      CASE OPT = 'SR'
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 3000
      CASE OPT = 'ST'
         REF.NO = 1
         GOSUB 3000
      CASE OPT = 'SB'
         REF.NO = PCNT
         GOSUB 3000
         GOSUB 1000
      CASE 1
         ERRMSG = "Invalid Entry"
         GOSUB 91000
   END CASE
   GOTO 500
1000 *
   *** CHANGE A SINGLE LINE ***
   ECD.NUM=22
   ECD.ACTION=4; CALL SCRN.EDIT; IF ECD.RET.VALUE='END' THEN RETURN
   IF REF.NO # '' THEN
   REF.NO = ECD.RET.VALUE
   ECD.SUB.NUM = REF.NO
   ECD.NUM=10
   ECD.ACTION=4; CALL SCRN.EDIT; IF ECD.RET.VALUE#'END' THEN
     DSR.COMPLETE<1,REF.NO> = ECD.RET.VALUE
   END
   RETURN
2000 *
   *** CHANGE ALL LINES ***
*  FOR P = 1 TO PCNT
   FOR P = FRST.LN.TO.CAN TO PCNT
      REF.NO = P
*     SCV.REC(10)<ECD.SCRN.NO,REF.NO> = DSR.COMPLETE<1,REF.NO>
      SCV.REC(10)<ECD.SCRN.NO,REF.NO> = CAN.CANCEL<1,REF.NO>
      ECD.SUB.NUM = REF.NO
      ECD.NUM=10
      ECD.ACTION=4; CALL SCRN.EDIT; IF ECD.RET.VALUE#'END' THEN
        DSR.COMPLETE<1,REF.NO> = ECD.RET.VALUE
      END
   NEXT P
   GOSUB 3000
   RETURN
3000 *
   *** DISPLAY THE SERIAL PO LINES ***
   START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
   IF START.REF.NO = CURR.REF.NO THEN RETURN
   CURR.REF.NO = START.REF.NO
   LINE.COUNT = DCOUNT(SCV.REC(5)<ECD.SCRN.NO>,VM) 
   ECD.NUM=4; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM=5; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM=6; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM=7; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM=8; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM=9; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   ECD.NUM=10; ECD.SUB.NUM=START.REF.NO; ECD.ACTION=7; CALL SCRN.EDIT
   RETURN
91000 *
   ERR.TYPE=1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE=2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE=3; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
  ECD.ACTION=99; CALL SCRN.EDIT
   RETURN
END
