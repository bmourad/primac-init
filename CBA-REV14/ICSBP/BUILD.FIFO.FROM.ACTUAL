* Routine to build the Iventory FIFO Arrays from Actuals for FIFO Value Rpt
SUBROUTINE BUILD.FIFO.FROM.ACTUAL(MAT IWH.REC)
*
*COPY>CPYLIB>COM1
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - BUILD.FIFO.FROM.ACTUAL
* BY          - epitka; CBA
* DATE        - 06/20/2003
* DESCRIPTION -
*
* Revise receipt data for actual costing to FIFO for FIFO report on ACTUAL
* costed products. Uses receipt data input as built by BUILD.IWH.FI.
*ENDDOC
**********************************************
*
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*
*
WORK.ORG.QTY=IWH.ORG.FI
WORK.RSV.QTY=IWH.RSV.FI
WORK.CUR.QTY=IWH.QTY.FI
*
FCTR = DCOUNT(WORK.CUR.QTY,VM)
FOR BLK = 1 TO FCTR WHILE WORK.CUR.QTY<1,BLK>+0 = 0; NEXT BLK
FOR F = FCTR TO BLK STEP -1
  QLIMIT = WORK.ORG.QTY<1,F> - WORK.CUR.QTY<1,F>
  IF QLIMIT > 0 THEN
    FOR FF = F-1 TO BLK STEP -1 WHILE QLIMIT > 0
      AVAIL = WORK.CUR.QTY<1,FF>
      IF AVAIL > 0 THEN
        IF AVAIL >= QLIMIT THEN
          QTY = QLIMIT
          QLIMIT = 0
        END ELSE
          QTY = AVAIL
          QLIMIT -= AVAIL
        END
        WORK.CUR.QTY<1,F> += QTY
        WORK.RSV.QTY<1,F> += QTY
        WORK.CUR.QTY<1,FF> -= QTY
        WORK.RSV.QTY<1,FF> -= QTY
        IF WORK.RSV.QTY<1,FF> < 0 THEN WORK.RSV.QTY<1,FF> = 0
      END
    NEXT FF
  END
NEXT F
IWH.ORG.FI = WORK.ORG.QTY
IWH.RSV.FI = WORK.RSV.QTY
IWH.QTY.FI = WORK.CUR.QTY
*
RETURN
