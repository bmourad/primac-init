*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.CATG
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - CATEGORY.MAINT
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 10/25/83
* DESCRIPTION - This program create and maintain category code
*               information.
*T22125 renee 07/31/1997 * This screen needs to have a read-lock. Added
*                          DOMATREADU logic.
*T22125 renee 09/15/1997 * Change status check from NE "READ" to =
*                          "LOCKED".
*T24840 gil 02/16/2000 * Adding a error prompt to Costing Method. 
*T25740 edvard 06/15/2001 * Added tracking level(S/G). rev12
*T25740 edvard 09/19/2001 * Add option to actually cost serialy tracked
*                           items.
*T26126 adelgado 03/01/2002 * Implement the LOCKED clause for READU and
*                             remove DOMATREADU logic.
*T28086 lross 04/26/2004 * CATG.RSV.SERIAL valid only for COST.TYPE =
*                          'AC'.
*T27919 lross 02/07/2005 * Allow non-paper products to be tracked
*                          serially.
*T28570 lross 06/01/2005 * Correct check for active products.
*T28747 lross 12/14/2005 * Allow change of TRACKING LEVEL with Warning.
*T29032 cmyklebu 12/28/2006 * Move pgm from rev12 to rev14.
*ENDDOC
*********************************************************************
*
***** FILE INSERTS
*
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.STATS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SETUP SYSTEM ERRMSGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
***** OPEN FILES
*
   OPEN "","CATEGORY" TO CATEGORY ELSE
      ERRMSG = "Cannot locate the CATEGORY file"
      GOTO 93000
   END
   OPEN "","INVENTORY" TO INVENTORY ELSE
      ERRMSG = "Cannot locate the INVENTORY file"
      GOTO 93000
   END
   OPEN "","INV.WHSE" TO INV.WHSE ELSE
      ERRMSG = "Cannot locate the INV.WHSE file"
      GOTO 93000
   END
   OPEN "","INV.STATS" TO INV.STATS ELSE
      ERRMSG = "Cannot locate the INV.STATS file"
      GOTO 93000
   END
   OPEN "","ICS.SCREENS" TO M.SCREENS ELSE
      ERRMSG = "Cannot locate the ICS.SCREENS file"
      GOTO 93000
   END
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "Cannot locate the COMPANY file"
      GOTO 93000
   END
   OPEN "","COA" TO COA ELSE
      ERRMSG = "Cannot locate the COA file"
      GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "Cannot locate the CONTROL file"
      GOTO 93000
   END
   OPEN "","XREF.DATA" TO XREF.DATA ELSE
      ERRMSG = "Cannot locate the XREF.DATA file"
      GOTO 93000
   END
   OPEN "","PREFIX" TO PREFIX ELSE
      ERRMSG = "Cannot locate the PREFIX file"
      GOTO 93000
   END
*
***** GET COMPANY NAME
*
   CONO = ""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = "END" THEN GOTO 99999
   IN.ACCT.LEN = LEN(CO.ACCT.PIC)
*
***** MAIN PROCESSING
*
   MAT GEN.XREF.REC = ""
   GXR.CO = CONO
   MAT EDIT.COM.DRIVER = ""
   ECD.SCRN.CNT = 2
   ECD.SCRN.NAME = "CATEGORY.MAINT"
   ECD.SCRN.NAME<2> = "CATEGORY.SUB"
   ECD.ACTION = 1; CALL SCRN.EDIT
   ECD.SCRN.NO = 1; ESN = ECD.SCRN.NO
*
   ADDL.SCRNS = "B":AM:"D":AM:"S":AM:"P"
*
**** PRINT SCREEN
*
   ECD.ACTION = 2; CALL SCRN.EDIT
*
**** CLEAR SCREEN CONTANT
*
100*
   RELEASE              ;* T26126
   MAT SCV.REC = ""
   ECD.ACTION = 6; CALL SCRN.EDIT
*
**** ENTER PLNO
*
200*
   NEWREC = 0
   MAT CATG.REC = ""
   ECD.NUM = 1
   ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 99999
      CASE ECD.RET.VALUE = ""
         GXR.NAME = "CATEGORY"
         GXR.FILE = CATEGORY
         CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
         ECD.ACTION = 2; CALL SCRN.EDIT
         IF GXR.ID = "" THEN GOTO 200
         ECD.RET.VALUE = GXR.ID
         SCV.REC(ECD.NUM) = ECD.RET.VALUE
         ECD.ACTION = 5; CALL SCRN.EDIT
   END CASE
   PLNO = ECD.RET.VALUE
  * T26126 v
   MATREADU CATG.REC FROM CATEGORY, CONO:ECD.RET.VALUE LOCKED
      ERRMSG = 'PRODUCT LINE record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 91000;GOTO 200 
   END THEN
  * T26126 ^
      OLD.CATG.TYPE = CATG.TYPE
      SCV.REC(2)<ESN> = CATG.DESC
      SCV.REC(3)<ESN> = CATG.MAJ.LINE
      SCV.REC(4)<ESN> = CATG.TYPE
      GOSUB 1350
      SCV.REC(5)<ESN> = TYPE.DESC
      SCV.REC(6)<ESN> = CATG.TRACK.QOH
      SCV.REC(7)<ESN> = CATG.COST.TYPE
      SCV.REC(8)<ESN> = CATG.BARCODE
      SCV.REC(9)<ESN> = CATG.BURDEN.PRCNT
      SCV.REC(10)<ESN> = CATG.REQ.BOOK
      SCV.REC(11)<ESN> = CATG.PHY.INV.METH
      SCV.REC(12)<ESN> = CATG.PHY.INV.ALLOW
      SCV.REC(15)<ESN> = CATG.FNGD.TYPE
      SCV.REC(16)<ESN> = CATG.SALE.TYPE
      SCV.REC(20)<ESN> = CATG.ADDL.SCRNS
      SCV.REC(34)<ESN> = CATG.TRK.LVL
      SCV.REC(35)<ESN>=CATG.RSV.SERIAL
      ECD.ACTION = 3; CALL SCRN.EDIT
   END ELSE
      ECD.NUM = 32; SCV.REC(ECD.NUM) = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "Y" THEN GOTO 200
      OLD.CATG.TYPE = ""
      NEWREC = 1
      LIMIT = 15
      FOR OPTION = 1 TO LIMIT
         ON OPTION GOSUB 1100,1200,1300,1400,1550,1500,1600,1700,1800,1850,1855,1900,2000,2100,2200
         IF ECD.RET.VALUE = "END" THEN LIMIT = 0
      NEXT OPTION
      IF LIMIT = 0 THEN GOTO 100
      ECD.SCRN.NO = ESN + 1
      ECD.ACTION = 2; CALL SCRN.EDIT
      CALL CATEGORY.SUB(CONO,PLNO,NEWREC,IN.ACCT.LEN,CO.ACCT.MASK,CO.ACCT.MATCH)
      ECD.SCRN.NO = ESN
      ECD.ACTION = 2; CALL SCRN.EDIT
      IF NEWREC = "END" THEN GOTO 100
      ECD.ACTION = 3; CALL SCRN.EDIT
   END
   MORE = 1
   LOOP
      ECD.NUM = 30; SCV.REC(ECD.NUM)<ESN> = ""
      ECD.ACTION = 4; CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = "E" OR OPTION = "END"
            MORE = 0
         CASE OPTION =  "N"
            ECD.SCRN.NO = ESN + 1
            ECD.ACTION = 2; CALL SCRN.EDIT
            CALL CATEGORY.SUB(CONO,PLNO,NEWREC,IN.ACCT.LEN,CO.ACCT.MASK,CO.ACCT.MATCH)
            ECD.SCRN.NO = ESN
            ECD.ACTION = 2; CALL SCRN.EDIT
            ECD.ACTION = 3; CALL SCRN.EDIT
         CASE OPTION = "F"
            ERRMSG = ""
            IF OLD.CATG.TYPE # "" AND OLD.CATG.TYPE # CATG.TYPE THEN
               P_X  = 3 ; P_Y = 23 ; P_VALUE = "Please wait, verifying new Category type." ; P_OPT = ""
               CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               BEGIN CASE
                  CASE CATG.TYPE = "S"
                     NEW.PAP.TYPE = "SHEET"
                  CASE CATG.TYPE = "L"
                     NEW.PAP.TYPE = "ROLL"
                  CASE CATG.TYPE = "RL"
                     NEW.PAP.TYPE = "LROLL"
                  CASE CATG.TYPE = "PC"
                     NEW.PAP.TYPE = "PCOAT"
                  CASE 1
                     NEW.PAP.TYPE = "REGULAR"
               END CASE
               SELECT INVENTORY
               DATA = 1
               LOOP
                  READNEXT PDNO ELSE DATA = 0
               WHILE DATA DO
                  IF PDNO[1,3] = CONO THEN
                     MATREAD INV.REC FROM INVENTORY, PDNO THEN
                        IF INV.LINE = PLNO AND INV.PAP.TYPE # NEW.PAP.TYPE THEN
                           ERRMSG = "Product (":PDNO[4,99]:") has a different type # ":INV.PAP.TYPE
                           DATA = 0
                        END
                     END
                  END
               REPEAT
            END
            IF ERRMSG = "" THEN
               MATWRITE CATG.REC ON CATEGORY , CONO:PLNO
               MORE = 0
            END ELSE
               GOSUB 91000
            END
         CASE NOT(NUM(OPTION))
         CASE OPTION < 1 OR OPTION > 15
         CASE 1
            ON OPTION GOSUB 1100,1200,1300,1400,1550,1500,1600,1700,1800,1850,1855,1900,2000,2100,2200
      END CASE
   WHILE MORE DO REPEAT
   GOTO 100
*
***** ENTER CATG DESC
*
1100*
   ECD.NUM = 2
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      CATG.DESC = ECD.RET.VALUE
   END
   RETURN
*
**** ENTER CATG.MAJOR.LINE
*
1200*
   ECD.NUM = 3
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      CATG.MAJ.LINE = ECD.RET.VALUE
   END
   RETURN
*
*---- ENTER CATEGORY TYPE
*
1300*
   ECD.NUM = 4
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      IF OLD.CATG.TYPE # "" AND OLD.CATG.TYPE # ECD.RET.VALUE THEN
         ERRMSG = "Warning !! This change may affect existing Inventory records"
         GOSUB 91000
      END
      CATG.TYPE = ECD.RET.VALUE
      GOSUB 1350
      SCV.REC(5)<ESN> = TYPE.DESC
      ECD.NUM = 5; ECD.ACTION = 5; CALL SCRN.EDIT
   END
   RETURN
*
*---- GET TYPE DESCRIPTION
*
1350*
   TYPE.DESC = ""
   BEGIN CASE
      CASE CATG.TYPE = ""
         TYPE.DESC = ""
      CASE CATG.TYPE = "R"
         TYPE.DESC = "REGULAR"
      CASE CATG.TYPE = "L"
         TYPE.DESC = "ROLL"
      CASE CATG.TYPE = "S"
         TYPE.DESC = "SKID"
      CASE CATG.TYPE = "P"
         TYPE.DESC = "PLATE"
      CASE CATG.TYPE = "F"
         TYPE.DESC = "FILM"
      CASE CATG.TYPE = "I"
         TYPE.DESC = "INK"
      CASE CATG.TYPE = "O"
         TYPE.DESC = "OTHER"
      CASE CATG.TYPE = "RL"
         TYPE.DESC = "ROLL LABEL"
      CASE CATG.TYPE = "PC"
         TYPE.DESC = "PAPER COATER"
   END CASE
   RETURN
*
*---- ENTER CATEGORY TRACK QOH
*
1400*
   ECD.NUM = 6
   ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 1409
      CASE ECD.RET.VALUE = "N"
         IF CATG.COST.TYPE = "FI" OR CATG.COST.TYPE = "LC" OR CATG.COST.TYPE = "AC" THEN
*T28086 v   ERRMSG = "you should change costing method to other than FI or LC"
            ERRMSG = "you should change costing method to other than FI, LC or AC"
            GOSUB 91000; GOTO 1409
         END
         IF CATG.TRACK.QOH = "Y" THEN
            ERRMSG = "WARNING !!! YOU ARE NOT TRACKING QOH FOR THIS PRODUCT LINE ANY MORE"
            GOSUB 91000
         END
   END CASE
   CATG.TRACK.QOH = ECD.RET.VALUE
1409*
   ECD.NUM = 6; SCV.REC(ECD.NUM)<ESN> = CATG.TRACK.QOH
   ECD.ACTION = 5; CALL SCRN.EDIT
   RETURN
1500*
   IF CATG.MAJ.LINE='FNGD' THEN
      CATG.COST.TYPE='AC'
      SCV.REC(7)='AC' ; ECD.NUM=7 
      ECD.ACTION=5 ; CALL SCRN.EDIT
   END ELSE
      OLD.DEFAULT=CATG.COST.TYPE ;*T24840
      ECD.NUM = 7
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" THEN
         IF ECD.RET.VALUE='AC' THEN
            IF CATG.TRK.LVL#'S' THEN
               ERRMSG = "INVALID COSTING METHOD SPECIFIED FOR PRODUCT LINE"
               GOSUB 91000
               SCV.REC(7)=OLD.DEFAULT
               GOTO 1500
            END
         END
         IF CATG.TRACK.QOH = "N" THEN
            IF ECD.RET.VALUE = "FI" OR ECD.RET.VALUE = "LC" THEN
               ERRMSG = ECD.RET.VALUE : " is not valid when not tracking quantity on hand"
               GOSUB 91000; GOTO 1500
            END
         END
         CATG.COST.TYPE = ECD.RET.VALUE
*T27919  IF CATG.TRK.LVL='G' THEN
         IF CATG.COST.TYPE # 'AC' THEN
            CATG.RSV.SERIAL='N'
            SCV.REC(35)<ESN>='N';ECD.NUM=35;ECD.ACTION=5;CALL SCRN.EDIT
         END
      END
   END
   RETURN
*
1550*
   BEGIN CASE
      CASE CATG.MAJ.LINE='FNGD'
         CATG.TRK.LVL='R'
         SCV.REC(34)<ESN>="R"
         ECD.NUM=34;ECD.ACTION=5;CALL SCRN.EDIT
*T27919 CASE CATG.TYPE="L" OR CATG.TYPE="S" OR CATG.TYPE="RL" OR CATG.TYPE="PC" 
      CASE 1
         ECD.NUM=34
         ECD.ACTION=4 ; CALL SCRN.EDIT
         IF ECD.RET.VALUE # "END" THEN
            IF ECD.RET.VALUE = 'R' THEN
               ERRMSG = 'Invalid option, Receipt Tracking is for FNGD use only'
               GOSUB 91000 ; ERRMSG = '' ; GOTO 1550
            END ELSE
               IF ECD.RET.VALUE # CATG.TRK.LVL THEN
                  IF NEWREC = 0 THEN GOSUB CHECK.FOR.ACTIVITY
*T28747 v
*                 IF ERRMSG # '' THEN ERRMSG = '' ; GOTO 1550
                  IF ERRMSG # '' THEN
                    PMSG = "WARNING !!! Product Line has Activity "
                    PMSG := "- Continue? Y/N "
                    X=0; Y=21; TYP=8; O.R='R'; CALL EDIT.SUB
                    P_X=0; P_Y=21; P_VALUE=''; P_OPT='CL'
                    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
                    IF VALUE# 'Y' THEN GOTO 1550
                  END
*T28747 ^
                  CATG.TRK.LVL = ECD.RET.VALUE ; * P747
               END ELSE
                  CATG.TRK.LVL=ECD.RET.VALUE
               END
            END
         END
*T27919 v
*     CASE 1
*        CATG.TRK.LVL = "G"
*        SCV.REC(34)<ESN> = "G"
*        ECD.NUM=34;ECD.ACTION=5; CALL SCRN.EDIT
*        CATG.RSV.SERIAL=''
*        SCV.REC(35)<ESN>=''
*        ECD.NUM=35;ECD.ACTION=5;CALL SCRN.EDIT
   END CASE
   RETURN
1600*
   IF CATG.TRK.LVL = "S" THEN
      ECD.NUM = 8
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" THEN
         CATG.BARCODE=ECD.RET.VALUE
      END
   END ELSE
      CATG.BARCODE ="N"
      SCV.REC(8)<ESN> = "N"
      ECD.NUM=8;ECD.ACTION=5;CALL SCRN.EDIT
   END
   RETURN
*
***** ENTER BURDEN PERCENT
*
1700*
   ECD.NUM = 9
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      CATG.BURDEN.PRCNT = ECD.RET.VALUE
   END
   RETURN
1800*
   ECD.NUM = 10
   ECD.ACTION = 4; CALL SCRN.EDIT
   IF ECD.RET.VALUE # "END" THEN
      CATG.REQ.BOOK = ECD.RET.VALUE
   END
   RETURN
1900*
   ECD.NUM = 15
   IF CATG.MAJ.LINE = "FNGD" THEN
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" THEN
         CATG.FNGD.TYPE = ECD.RET.VALUE
      END
   END ELSE
      CATG.FNGD.TYPE = ""; SCV.REC(ECD.NUM)<ESN> = CATG.FNGD.TYPE
      ECD.ACTION = 5; CALL SCRN.EDIT
   END
   RETURN
2000*
   ECD.NUM = 16
   IF CATG.MAJ.LINE = "FNGD" THEN
      ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # "END" THEN
         CATG.SALE.TYPE = ECD.RET.VALUE
      END
   END ELSE
      CATG.SALE.TYPE = ""; SCV.REC(ECD.NUM)<ESN> = CATG.SALE.TYPE
      ECD.ACTION = 5; CALL SCRN.EDIT
   END
   RETURN
1850 *
   ECD.NUM = 11
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      CATG.PHY.INV.METH = ECD.RET.VALUE
   END
   RETURN
1855 *
   ECD.NUM=12
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      CATG.PHY.INV.ALLOW = ECD.RET.VALUE
   END
   RETURN
2100*
   IF CATG.MAJ.LINE # "FNGD" THEN
      CATG.SALE.TYPE = ""
      ECD.NUM = 20; SCV.REC(ECD.NUM)<ESN> = CATG.SALE.TYPE
      ECD.ACTION = 5; CALL SCRN.EDIT
      GOTO 2109
   END
2101*
   ECD.NUM = 20
   ECD.ACTION = 4; CALL SCRN.EDIT
   BEGIN CASE
      CASE ECD.RET.VALUE = "END"
         GOTO 2109
      CASE ECD.RET.VALUE = ""
         CATG.ADDL.SCRNS = ECD.RET.VALUE
         GOTO 2109
   END CASE
   LIMIT = COUNT(ECD.RET.VALUE,",") + 1
   FOR P = 1 TO LIMIT
      VAL = FIELD(ECD.RET.VALUE,",",P)
      LOCATE VAL IN ADDL.SCRNS,1 SETTING FND ELSE
         LIMIT = 0
      END
   NEXT P
   IF NOT(LIMIT) THEN
      ERRMSG = "Invalid additional screen type # ":VAL
      GOSUB 91000; GOTO 2101
   END
   CATG.ADDL.SCRNS = ECD.RET.VALUE
2200*
*T28086 v
*  IF CATG.TRK.LVL='S' THEN
   IF CATG.COST.TYPE = 'AC' THEN
      ECD.NUM=35 ; ECD.ACTION=4 ; CALL SCRN.EDIT
      IF ECD.RET.VALUE#'END' THEN
         CATG.RSV.SERIAL=ECD.RET.VALUE
      END
*  END
   END ELSE
      CATG.RSV.SERIAL='N'
      ECD.NUM=35; SCV.REC(35)<ESN>='N'; ECD.ACTION=5; CALL SCRN.EDIT
   END
*T28086 ^
2109*
   RETURN
*
CHECK.FOR.ACTIVITY: 
*
   P_X  = 3 ; P_Y = 23 ; P_VALUE = "Please wait, verifying product line activity." ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   ERRMSG = ''
   CMD = 'SSELECT INV.WHSE WITH P.LINE = "':PLNO:'" AND WITH CONO = "':CONO:'"'
   PERFORM CMD CAPTURING MSG
   LOOP.DONE = 0
   LOOP
      READNEXT P.ID ELSE LOOP.DONE = 1
   UNTIL LOOP.DONE DO
      MATREAD IWH.REC FROM INV.WHSE, P.ID THEN
*T28570 v Since IWH.REC can exist without INV.STAT.REC move logic here.
   BEGIN CASE
      CASE SUM(IWH.ON.HAND) # 0 ; ERRMSG = 1 ; LOOP.DONE = 1
      CASE SUM(IWH.RESV) # 0 ; ERRMSG = 1 ; LOOP.DONE = 1
   END CASE
*T28570 ^
         MATREAD INV.STAT.REC FROM INV.STATS, P.ID ELSE MAT INV.STAT.REC = ''
         BEGIN CASE
            CASE ISTAT.JOB # '' ; ERRMSG = 1 ; LOOP.DONE = 1
            CASE ISTAT.PO # '' ; ERRMSG = 1 ; LOOP.DONE = 1
* T28570 v
*           CASE SUM(IWH.ON.HAND) # 0 ; ERRMSG = 1 ; LOOP.DONE = 1
*           CASE SUM(IWH.RESV) # 0 ; ERRMSG = 1 ; LOOP.DONE = 1
* T28570 ^
         END CASE
      END
   REPEAT
*T28747 v Bypass
   P_X=0; P_Y=23; P_VALUE=''; P_OPT='CL'
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   RETURN
   IF ERRMSG # '' THEN
      ERRMSG = 'Cannot change, active products exist for this product line'
      GOSUB 91000
   END
   P_X = 3 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  *
*T28747   RETURN
*
***** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999 END
