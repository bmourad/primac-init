   SUBROUTINE INV.UNPOSTED.MATL(CONO,PROD,WHSE,UNPARRAY)
*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INV.INQ
*********************************************************************
* REVISION    - [14.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.UNPOSTED.MATL
* BY          - L. ROSS
* DATE        - 07/23/2007
* DESCRIPTION -
* Displays the unposted materials for PROD/WHSE
*ENDDOC
*********************************************************************
*
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>ICS.CPYLIB>INV.CNV
*COPY>JCS.CPYLIB>JOB
*COPY>JCS.CPYLIB>DAILY.MATL
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
   DEFFUN CALC.STK.QTY(COST.QTY,MAT INV.CNV.REC,ROND,LN)
*
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
   PAGE.SIZE = 5
   S.CUST.NAME = ''
   S.UNPOST.ID = ''
   S.UNPOST.CUST = ''
   S.UNPOST.DATE = ''
   S.UNPOST.QTY = ''
   S.UNPOST.SERIAL = ''
   UNKNOWN = STR('?',30)
   START = 1
   ESN = ECD.SCRN.NO
*
   GOSUB GET.DATA
   GOSUB BUILD.SCREEN
   ECD.ACTION = 3; CALL SCRN.EDIT
   ;*
   ;* Main Prompt
   ;*
   LOOP
      ECD.NUM = 7; SCV.REC(ECD.NUM)<ESN> = ''
      ECD.ACTION = 4; CALL SCRN.EDIT
      ACTION = ECD.RET.VALUE
      BEGIN CASE
         CASE ACTION = '' OR ACTION = 'END'
            ACTION = ''
         CASE ACTION[1,1] = 'S'
            GOSUB SCROLL
      END CASE
   WHILE ACTION # '' DO REPEAT
   GOTO 99999
*
*************************************************************************
**** S U B R O U T I N E S **********************************************
*************************************************************************
*
***************
GET.DATA: 
****************
*
   LN.CNT = DCOUNT(UNPARRAY<1>,VM)
   TOT.UNPOST=0
   II=0
   FOR I = 1 TO LN.CNT
      IF UNPARRAY<5,I> = WHSE THEN
        II += 1
        S.UNPOST.ID<1,II> = UNPARRAY<1,I>
        CUST.ID = CONO:UNPARRAY<2,I>
        MATREAD CUST.REC FROM CUSTOMER,CUST.ID ELSE CUST.NAME = UNKNOWN
        S.CUST.NAME<1,II> = CUST.NAME
        S.UNPOST.CUST<1,II> = UNPARRAY<2,I>
        S.UNPOST.DATE<1,II> = UNPARRAY<3,I>
        S.UNPOST.QTY<1,II> = UNPARRAY<4,I>
        S.UNPOST.SERIAL<1,II> = UNPARRAY<7,I>
        TOT.UNPOST +=S.UNPOST.QTY<1,II>
      END
   NEXT I
   RETURN
*
***************
BUILD.SCREEN: 
***************
*
   LN.CNT = DCOUNT(S.UNPOST.ID,VM)
   FOR L = 1 TO LN.CNT
      SCV.REC(12)<ESN,L> = S.UNPOST.ID<1,L>
      SCV.REC(13)<ESN,L> = S.UNPOST.CUST<1,L>
      SCV.REC(18)<ESN,L> = S.CUST.NAME<1,L>
      SCV.REC(11)<ESN,L> = L
      SCV.REC(14)<ESN,L> = OCONV(S.UNPOST.DATE<1,L>,"D2/")
      SCV.REC(15)<ESN,L> = S.UNPOST.SERIAL<1,L>
      TMP=CALC.STK.QTY(S.UNPOST.QTY<1,L>,MAT INV.CNV.REC,'','')
      SCV.REC(16)<ESN,L>=OCONV(TMP,ICR.CNV1)
   NEXT L
   TMP=CALC.STK.QTY(TOT.UNPOST,MAT INV.CNV.REC,'','')
   SCV.REC(5)<ESN>=OCONV(TMP,ICR.CNV1)
   RETURN
*
*************
SCROLL: 
*************
*
   BEGIN CASE
      CASE ACTION = "S" OR ACTION = "SF"
         START += PAGE.SIZE
         IF START > LN.CNT THEN START = 1
      CASE ACTION = "SR"
         START -= PAGE.SIZE
         IF START<1 THEN START=1
      CASE ACTION = "ST"
         START=1
      CASE ACTION = "SB"
         START = LN.CNT
   END CASE
   START = 1 + INT((START-1)/PAGE.SIZE) * PAGE.SIZE
   ECD.NUM=11;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=12;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=13;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=14;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=15;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   ECD.NUM=16;ECD.SUB.NUM=START;ECD.ACTION=7;CALL SCRN.EDIT
   RETURN
*
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
99999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
END
