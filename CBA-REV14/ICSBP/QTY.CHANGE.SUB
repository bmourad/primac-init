   SUBROUTINE QTY.CHANGE.SUB(CONO,PO.CODE,MAT IWH.REC,MAT INV.REC,MAT PO.REC,DEPL.METHOD,RECP.NO,DATE,PERIOD,DIFF.QTY,PRICE,TYPE,ERRMSG,RET.ARR)
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - QTY.CHANGE.SUB
* BY          - edvard; CBA
* DATE        - 09/20/2001
* DESCRIPTION -
* CALL ICS.IWH.SUB SUBROUTINE WITH ACTION 1 BEFORE CALLING THIS SUB.
*
*T25740 edvard 09/20/2001 * REV12 THIS PROGRAM COMBINES
*                           PHY.FIFO.RECV.SUB AND FIFO.RECV.SUB FROM
*                           REV11 AND ADDS ALSO ACTUAL COSTING AND IS
*                           CALLED AS EXTERNAL SUBROUTINE RATHER THAN
*                           BEING AN INCLUDE
*                           ADDED CHECK OF QTY POSTED TO A FUTURE PERIOD
*T27801 lross 12/05/2003 * Error in postive FNGD adjusts. Also
*                          incomplete code for reduction of raw matl
*                          using 'AC' depl method.
*ENDDOC
**********************************************
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INVENTORY
*COPY>PMC.CPYLIB>PO
*COPY>CPYLIB>CHAR
*
   RET.ARR = ""
   IF DATE='' THEN DATE=DATE()
*
   ERRMSG = ''
   TOT.COST = 0
   TRANS.COST = ""
   TRANS.UN.PRICE = ""
   TRANS.QTY = ""
*
   BEGIN CASE
      CASE DIFF.QTY > 0
         IF TYPE='R' THEN
            PTR=DCOUNT(IWH.RECP.NO,VM)+1
         END ELSE
            PTR = 1
         END
         LOOP
*T27801 v
            IF INV.M.LINE # 'FNGD' OR TYPE = 'R' OR RECP.NO = '' THEN
               LOCATE DATE IN IWH.RECV.FI<1>,PTR SETTING FND ELSE
                  IWH.RECV.FI<1,FND> = DATE
                  IWH.PO.NO.FI<1,FND> = PO.CODE
                  IWH.VDR.FI<1,FND> = PO.VEND.NO
                  IWH.QTY.FI<1,FND> = 0
                  IWH.ORG.FI<1,FND> = 0
                  IWH.RSV.FI<1,FND> = 0
                  IWH.COST.FI<1,FND> = PRICE
               END
            END ELSE
               LOCATE RECP.NO IN IWH.RECP.NO<1>,PTR SETTING FND ELSE
                  ERRMSG = 'Cannot locate Receipt ':RECP.NO:' in INV.WHSE record.'
                  PTR = 0
                  EXIT
               END
            END
*T27801 ^
            BEGIN CASE
               CASE PRICE # IWH.COST.FI<1,FND>
                  PTR = FND + 1
               CASE PO.VEND.NO # IWH.VDR.FI<1,FND>
                  PTR = FND + 1
               CASE PO.CODE # IWH.PO.NO.FI<1,FND>
                  PTR = FND + 1
               CASE 1
                  PTR = 0
            END CASE
         WHILE PTR DO REPEAT
         IF ERRMSG = '' THEN ;*T27801
           IWH.QTY.FI<1,FND> = IWH.QTY.FI<1,FND> + DIFF.QTY
           IWH.ORG.FI<1,FND> = IWH.ORG.FI<1,FND> + DIFF.QTY
           IWH.RSV.FI<1,FND> = IWH.RSV.FI<1,FND> + DIFF.QTY
           TOT.COST = (IWH.COST.FI<1,FND>/10000)*(DIFF.QTY/10)
           TOT.COST = TOT.COST/(INV.COST.WT/100)
           TOT.COST = INT(TOT.COST+.5)
           TP = DCOUNT(TRANS.COST,VM)+1
           TMP = ((IWH.COST.FI<1,FND>/10000)*(DIFF.QTY/10))
           TMP = TMP/(INV.COST.WT/100)
           TRANS.COST<1,TP>  =  INT(TMP+.5)
           TRANS.UN.PRICE<1,TP> = IWH.COST.FI<1,FND>
           TRANS.QTY<1,TP> = DIFF.QTY
           RET.ARR<1>=TOT.COST
           RET.ARR<2>=TRANS.COST
           RET.ARR<3>=TRANS.UN.PRICE
           RET.ARR<4>=TRANS.QTY
         END
*T27801 ^
      CASE DIFF.QTY < 0
         IF IWH.ON.HAND + DIFF.QTY < IWH.RESV THEN
            ERRMSG = 'NOT ENOUGH NET AVAILABLE QTY'
         END ELSE
            BEGIN CASE
               CASE DEPL.METHOD = "FI"
                  SUB.QTY = 0-DIFF.QTY
                  QCNT = DCOUNT(IWH.QTY.FI,VM)
                  FOR Q = 1 TO QCNT WHILE SUB.QTY <> 0
                     IF PERIOD>=IWH.RECP.PERIOD<1,Q> THEN
                        BEGIN CASE
                           CASE IWH.RSV.FI<1,Q> = 0
                           CASE SUB.QTY > IWH.RSV.FI<1,Q>
                              TP = DCOUNT(TRANS.COST,VM)+1
                              TOT.COST -=(IWH.COST.FI<1,Q>/10000)*((IWH.RSV.FI<1,Q>/10)/(INV.COST.WT/100))
                              TMP = 0-INT((IWH.COST.FI<1,Q>/10000)*((IWH.RSV.FI<1,Q>/10)/(INV.COST.WT/100))+.5)
                              TRANS.COST<1,TP> = TMP
                              TRANS.UN.PRICE<1,TP> = IWH.COST.FI<1,Q>
                              TRANS.QTY<1,TP> = 0-IWH.RSV.FI<1,Q>
                              SUB.QTY = SUB.QTY-IWH.RSV.FI<1,Q>
                              IWH.QTY.FI<1,Q> = IWH.QTY.FI<1,Q>-IWH.RSV.FI<1,Q>
                              IWH.RSV.FI<1,Q> = 0
                           CASE 1
                              TP = DCOUNT(TRANS.COST,VM)+1
                              TOT.COST -=(IWH.COST.FI<1,Q>/10000)*((SUB.QTY/10)/(INV.COST.WT/100))
                              TMP = 0-INT((IWH.COST.FI<1,Q>/10000)*((SUB.QTY/10)/(INV.COST.WT/100))+.5)
                              TRANS.COST<1,TP> = TMP
                              TRANS.UN.PRICE<1,TP> = IWH.COST.FI<1,Q>
                              TRANS.QTY<1,TP> = 0-SUB.QTY
                              IWH.RSV.FI<1,Q> = IWH.RSV.FI<1,Q>-SUB.QTY
                              IWH.QTY.FI<1,Q> = IWH.QTY.FI<1,Q>-SUB.QTY
                              SUB.QTY = 0
                        END CASE
                     END
                  NEXT Q
                  IF SUB.QTY <> 0 THEN
                     ERRMSG = 'NOT ENOUGH NET AVAILABLE FI QTY. QTY IS EITHER RESERVED OR POSTED TO A FUTURE PERIOD.'
                  END ELSE
*T27653 v            TOT.COST=INT(TOT.COST+.5)
                     TOT.COST=INT(TOT.COST-.5)
                     RET.ARR<1>=TOT.COST     
                     RET.ARR<2>=TRANS.COST   
                     RET.ARR<3>=TRANS.UN.PRICE
                     RET.ARR<4>=TRANS.QTY     
                  END
               CASE DEPL.METHOD='AC'
                  LOCATE RECP.NO IN IWH.RECP.NO<1> SETTING RPOS THEN
                     IF PERIOD<IWH.RECP.PERIOD<1,RPOS> THEN
                        ERRMSG='QTY. IS POSTED TO A FUTURE PERIOD.'
                     END ELSE
                        IF ABS(DIFF.QTY)>IWH.RSV.FI<1,RPOS> THEN
                           ERRMSG='NOT ENOUGH AVAILABLE QTY'
                        END ELSE
*T27653 v                  TOT.COST=IWH.COST.FI<1,RPOS>
                           TOT.COST = INT((IWH.COST.FI<1,RPOS>/10000)*((DIFF.QTY/10)/(INV.COST.WT/100)) - .5)
*T27801 v
                           IWH.RSV.FI<1,RPOS> += DIFF.QTY
                           IWH.QTY.FI<1,RPOS> += DIFF.QTY
                           RET.ARR<1>=TOT.COST     
                           RET.ARR<2>=TOT.COST   
                           RET.ARR<3>=IWH.COST.FI<1,RPOS>
                           RET.ARR<4>=DIFF.QTY     
*T27801 ^
                        END
                     END
                  END ELSE
                     ERRMSG='CANNOT LOCATE AVAILABLE QTY. FOR ': RECP.NO
                  END
            END CASE
         END
   END CASE
   RETURN
