*COPY>CPYLIB>COM1
*************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - COLOR.CODE.MAINT
* BY          - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 10/31/84
* DESCRIPTION -
* This program will be used for Color Code Maintenance.
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>ICS.CPYLIB>COLOR
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>CHAR
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
  OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE ERRMSG = 'ICS.SCREENS FILE MISSING';GOTO 93000
  OPEN '','COLOR' TO COLOR ELSE ERRMSG = 'COLOR FILE MISSING';GOTO 93000
*
*---- INITIALIZATION
*
  MAT EDIT.COM = ''
  MAT EDIT.COM.DRIVER = ''
  ERRMSG = ''
*
*---- GET COMPANY NUMBER
*
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
*---- PRINT SCREEN
*
  ECD.SCRN.CNT = 1
  ECD.SCRN.NAME<1> = 'COLOR.CODE.MAINT'
  ECD.ACTION = 1;CALL SCRN.EDIT
  ECD.SCRN.NO = 1
  MAT SCV.REC = ""
  ECD.ACTION = 2;CALL SCRN.EDIT
*
*---- MAIN PROCESSING
*
10*
  MAT COLOR.REC = ""
  SCV.REC(5)<ECD.SCRN.NO,1> = DATE()
  ECD.NUM = 5;ECD.ACTION = 5;CALL SCRN.EDIT
  ECD.NUM = 1;ECD.ACTION = 4;CALL SCRN.EDIT
  COLOR.CD.KEY = ECD.RET.VALUE
  BEGIN CASE
    CASE COLOR.CD.KEY = 'END'
      GOTO 99999
    CASE COLOR.CD.KEY # ''
      NEW.COLOR = 'NO'
      * T26126 v
      MATREADU COLOR.REC FROM COLOR, CONO:COLOR.CD.KEY LOCKED
        ERRMSG = 'COLOR CODE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 10 
      END ELSE
      * T26126 ^
        NEW.COLOR = 'YES';MAT COLOR.REC = ''
      END
  END CASE
  IF NEW.COLOR = 'YES' THEN
    GOSUB 20
    IF ECD.RET.VALUE = 'END' THEN
      GOSUB 300;GOTO 10
    END
  END ELSE
    SCV.REC(2)<ECD.SCRN.NO,1> = COLOR.DESC
    ECD.NUM = 2;ECD.ACTION = 5;CALL SCRN.EDIT
  END
  GOSUB 200
  GOSUB 300
  GOTO 10
*
*---- ENTER COLOR CODE DESCRIPTION
*
20*
  ECD.NUM = 2;ECD.ACTION = 4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    COLOR.DESC = ECD.RET.VALUE
  END
  RETURN
*
*---- ENTER OPTIONS
*
200*
  MORE = 1
  LOOP
    ECD.NUM = 4;ECD.ACTION = 4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        MORE = 1
      CASE OPTION = '1'
        GOSUB 20
        MORE = 0
      CASE OPTION = 'F'
        GOSUB 400
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*---- CLEAR SCREEN & RE-INITIALIZE
*
300*
  ECD.ACTION = 6;CALL SCRN.EDIT
  MAT SCV.REC = ""
  RETURN
*
*---- UPDATE COLOR FILE
*
400*
  MATWRITE COLOR.REC ON COLOR , CONO:COLOR.CD.KEY
  RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
  ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000*
  ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999*
*       PRINT @(-1)
END
