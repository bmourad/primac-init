*********************************************************************
*
*  PROGRAM - TT8.UTIL
*
*  AUTHOR  - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
*  DATE    - 09/17/91
*
*  DESCRIPTION
*
*  This program is used to interface to the TT8 handheld barcode
*  reader. The following functions may be performed.
*
*   1.   Initialize TT8
*   2.   Download control program to TT8
*   3.   Upload collected data from TT8
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>TT8.TRAN
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
   EQU CR  TO CHAR(13)
   EQU ESC TO CHAR(27)
*
*---- PRE-INITIALIZATION
*
   PROCREAD PARAM ELSE PARAM = ""
   ACTION = PARAM<1>
   IF NOT(NUM(ACTION)) THEN ACTION =  ""
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","TT8.TRAN" TO TT8.TRAN ELSE
      ERRMSG = "CANNOT OPEN TT8.TRAN FILE"
      GOSUB 90000
      STOP
   END
*
*---- INITIALIZATION
*
   TYP = 0
   BREAK OFF
   CALL EDIT.SUB1(MAT EDIT.COM)
   CONO = ""
   CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
   TRACE = 0                                    ;*** T R A C E ***
   IF TRACE THEN
      WRITE "" ON TT8.TRAN, CONO:"TRACE"
   END
   TMSG = ""
*
*---- MAIN PROCESSING
*
100 *
   GOSUB 70000                         ;* DISPLAY SCREEN HEAD
   IF ACTION = "" THEN
      GOSUB 80000                      ;* DISPLAY MENU
      X=0; Y=21; MAXL=3
      PMSG="Enter selection:"
      MINV=1; MAXV=3
      CALL EDIT.SUB1(MAT EDIT.COM)
      OPT = VALUE
   END ELSE
      OPT = ACTION
   END
   BEGIN CASE
      CASE OPT = "END"
         GOTO 99999
      CASE NUM(OPT)
         ON OPT GOSUB 1000, 2000, 3000
   END CASE
   IF ACTION = "" THEN GOTO 100 ELSE GOTO 99999
*
*-------------------------------------------------------------------*
*                                                                   *
*           A P P L I C A T I O N   S U B R O U T I N E S           *
*                                                                   *
*-------------------------------------------------------------------*
*
*---- INITIALIZE TT8
*
1000 *
   GOSUB 80100
   TYP = 8; X=0; Y=21
   PMSG = " Destroy all TT8 memory contents! Continue (Y/N):"
   CALL EDIT.SUB1(MAT EDIT.COM)
   IF VALUE # "Y" THEN RETURN
   GOSUB 20100                         ;* INITIATE TT8 COMM
   IF COMM.OPEN THEN
      PRINT "MTOP=8192"
      GOSUB 20200                      ;* GET TT8 RESPONSE
      PRINT "FORMAT 1"
      GOSUB 20200                      ;* GET TT8 RESPONSE
      PRINT "CREATE DFRE/70,70"
      GOSUB 20200                      ;* GET TT8 RESPONSE
      TMSG = "TT8 INITIALIZATION COMPLETED."
      GOSUB 20900                      ;* TERMINATE TT8 COMM
   END
   RETURN
*
*---- DOWNLOAD PROGRAM FILE
*
2000 *
   GOSUB 80200
   TYP = 8; X=0; Y=21
   PMSG = " Continue (Y/N):"
   CALL EDIT.SUB1(MAT EDIT.COM)
   IF VALUE # "Y" THEN RETURN
   FILENAME = "TT8BC"
   GOSUB 10100                         ;* GET FILE NAME
   IF FILENAME = "" THEN RETURN
   ITEMNAME = "TT8.BC"
   GOSUB 10200                         ;* GET ITEM NAME
   IF ITEMNAME = "" THEN RETURN
   LCNT = DCOUNT(PROG, AM)
   IF LCNT = 0 THEN RETURN
   GOSUB 20100                         ;* INITIATE TT8 COMM
   IF COMM.OPEN THEN
      PRINT "NEW"
      GOSUB 20200                      ;* GET TT8 RESPONSE
      PRINT 'DATE="':OCONV(DATE(),"D2/"):'"'
      GOSUB 20200                      ;* GET TT8 RESPONSE
      PRINT 'TIME="':OCONV(TIME(),"MTS"):'"'
      GOSUB 20200                      ;* GET TT8 RESPONSE
      FOR LPTR = 1 TO LCNT
         PROGLINE = PROG<LPTR>
         GOSUB 20500                   ;* XMIT PROGRAM STATEMENT
         GOSUB 20400                   ;* AWAIT TT8 PROMPT
      NEXT LPTR
      PRINT "PRINT PLEN"
      GOSUB 20200                      ;* GET TT8 RESPONSE
      TMSG = "PROGRAM LOAD COMPLETED (":RESP:" BYTES)."
      GOSUB 20900
   END
   RETURN
*
*---- UPLOAD DATA FILE FROM RAM DISK
*
3000 *
   GOSUB 80300
   TYP = 8; X=0; Y=21
   PMSG = " Continue (Y/N):"
   CALL EDIT.SUB1(MAT EDIT.COM)
   IF VALUE # "Y" THEN RETURN
   FILENAME = "TT8.TRAN"
   GOSUB 10100                         ;* GET FILE NAME
   IF FILENAME = "" THEN RETURN
   READU TT8PAR FROM F1, CONO:"CONTROL" ELSE TT8PAR = ""
   LASTREC = TT8PAR<2>
   RAMDISK = 1
   GOSUB 10300                         ;* GET RAM DISK FILE #
   IF RAMDISK = "" THEN RETURN
   GOSUB 20100                         ;* INITIATE TT8 COMM
   IF COMM.OPEN THEN
      RECCNT = 0
      PRINT "GOTO 9000"
      GOSUB 20300                      ;* GET TT8 ECHO
      TDONE = 0
      LOOP
         LOOP
            INPUT TT8REC:
         WHILE TT8REC = "" DO
         REPEAT
         BEGIN CASE
            CASE TT8REC = "END"
               TDONE = 1
            CASE INDEX(TT8REC,"\",1) = 0
               TDONE = 1
         END CASE
      UNTIL TDONE DO
         RECCNT = RECCNT + 1
         TCNT = DCOUNT(TT8REC,"\")
         DATAREC = TRIM(FIELD(TT8REC,"\",1))
         FOR TPTR = 2 TO TCNT
            DATAREC<TPTR> = TRIM(FIELD(TT8REC,"\",TPTR))
         NEXT TPTR
         MAT TT8.REC = ""
         TT8.TTYPE = DATAREC<1>
         BEGIN CASE
            CASE TT8.TTYPE = "TS"
               TDATE       = ICONV(DATAREC<2>,"D")
               TTIME       = ICONV(DATAREC<3>,"MTS")
               TT8.TDATE   = TDATE
               TT8.TTIME   = TTIME
            CASE TT8.TTYPE = "M"
               XTIME       = TTIME + DATAREC<2>
               TT8.TDATE   = TDATE + INT(XTIME/86400)
               TT8.TTIME   = MOD(XTIME,86400)
               TT8.MAN.ID  = DATAREC<3>
               IF DATAREC<4> # "" THEN
                  TT8.MAN.TOT = INT(DATAREC<4>*100+0.5)
               END
            CASE TT8.TTYPE = "R"
               XTIME       = TTIME + DATAREC<2>
               TT8.TDATE   = TDATE + INT(XTIME/86400)
               TT8.TTIME   = MOD(XTIME,86400)
               TT8.RS.ID   = DATAREC<3>
               TT8.MILL.ID = DATAREC<4>
               TT8.QTY     = INT(DATAREC<5>*100+0.5)
               TT8.WHSE    = DATAREC<6>
               TT8.LOC     = DATAREC<7>
            CASE TT8.TTYPE = "T"
               XTIME       = TTIME + DATAREC<2>
               TT8.TDATE   = TDATE + INT(XTIME/86400)
               TT8.TTIME   = MOD(XTIME,86400)
               TT8.RS.ID   = DATAREC<3>
               TT8.WHSE    = DATAREC<4>
               TT8.LOC     = DATAREC<5>
         END CASE
         LASTREC = MOD(LASTREC,999) + 1
         MATWRITE TT8.REC ON F1, CONO:LASTREC
*           PRINT RECCNT"R#4"
*           GOSUB 20300                   ;* GET TT8 ECHO
      REPEAT
      GOSUB 20400                      ;* AWAIT TT8 PROMPT
      BEGIN CASE
         CASE RECCNT = 0 AND TT8REC = "END"
            RELEASE F1, CONO:"CONTROL"
            TMSG = "NO TRANSACTIONS RECEIVED."
         CASE TT8REC = "END"
            TT8PAR<2> = LASTREC
            WRITE TT8PAR ON F1, CONO:"CONTROL"
            PRINT "ZERO 1"
            GOSUB 20200                   ;* GET TT8 RESPONSE
            TMSG = RECCNT:" TRANSACTIONS RECEIVED."
         CASE 1
            RELEASE F1, CONO:"CONTROL"
            TMSG = "UPLOAD ABORTED."
      END CASE
      GOSUB 20900
   END
   RETURN
*
*-------------------------------------------------------------------*
*                                                                   *
*               G E N E R I C   S U B R O U T I N E S               *
*                                                                   *
*-------------------------------------------------------------------*
*
*---- GET HOST FILE NAME
*
10100 *
   IF FILENAME # "" THEN
      OPEN "",FILENAME TO F1 ELSE FILENAME = ""
      IF FILENAME # "" THEN RETURN
   END
   FILENAME = ""
   X=0; Y=21; MAXL=15
   PMSG = "File to be transferred:"
   CALL EDIT.SUB1(MAT EDIT.COM)
   IF VALUE = "END" THEN RETURN
   FILENAME = VALUE
   OPEN "",FILENAME TO F1 ELSE
      ERRMSG = "Invalid File. Try again! "
      GOSUB 90000
      GOTO 10100
   END
   RETURN
*
*---- GET PROGRAM NAME
*
10200 *
   IF ITEMNAME # "" THEN
      READ PROG FROM F1, ITEMNAME ELSE ITEMNAME = ""
      IF ITEMNAME # "" THEN RETURN
   END
   ITEMNAME = ""
   X=0; Y=21; MAXL=15
   PMSG = "Program to be transferred:"
   CALL EDIT.SUB1(MAT EDIT.COM)
   IF VALUE = "END" THEN RETURN
   ITEMNAME = VALUE
   READ PROG FROM F1, ITEMNAME ELSE
      ERRMSG = "Invalid Program. Try again! "
      GOSUB 90000
      GOTO 10200
   END
   RETURN
*
*---- GET RAM DISK FILE NUMBER
*
10300 *
   IF RAMDISK # "" THEN RETURN
   RAMDISK = ""
   X=0; Y=21; MAXL=3
   PMSG = "Ram Disk file number:"
   CALL EDIT.SUB1(MAT EDIT.COM)
   IF VALUE = "END" THEN RETURN
   RAMDISK = VALUE
   RETURN
*
*---- INITIATE TT8 COMMUNICATIONS
*
20100 *
   COMM.OPEN = 0
   PRINT @(0,21):@(-4):" Turn TT8 power on!  Switch to TT8.... ":
   PRINT @(0,23):
   SLEEP 10
   PRINT CHAR(18)
   RESP = ""
   ECHO OFF
   DTIME = 1                            ;* TIMEOUT AFTER 1 SECOND
   LOOP
      GOSUB 50000
      IF NOT(TIMEOUT) THEN
         INPUT C,1:
         RESP = RESP : C
      END
   UNTIL TIMEOUT DO
   REPEAT
   IF RESP[LEN(RESP),1] = ">" THEN
      COMM.OPEN = 1
   END ELSE
      TMSG = " TransTerm 8 is not responding!"
      GOSUB 20900
   END
   RETURN
*
*---- GET TT8 RESPONSE FROM INQUIRY
*
20200 *
   GOSUB 20300                         ;* GET TT8 ECHO
   RESP = ""
   DTIME = 5                           ;* TIMEOUT AFTER 5 SECONDS
   LOOP
      GOSUB 50000
      IF NOT(TIMEOUT) THEN
         INPUT C,1:
         BEGIN CASE
            CASE C = ""
            CASE C = CR
            CASE C = LF
            CASE C = ">"
            CASE 1
               RESP = RESP : C
         END CASE
      END
   UNTIL C = ">" OR TIMEOUT DO
   REPEAT
   RESP = TRIM(RESP)
   IF TRACE THEN TRDATA="RESP=":RESP;GOSUB 89000
   RETURN
*
*---- GET ECHO FROM TT8
*
20300 *
   TT8.ECHO = ""
   TIMEOUT = 0
   DTIME = 5                            ;* TIMEOUT AFTER 5 SECONDS
   LOOP
      GOSUB 50000
      IF NOT(TIMEOUT) THEN
         INPUT TT8.ECHO:
      END
   WHILE TT8.ECHO = "" AND NOT(TIMEOUT) DO
   REPEAT
   IF TRACE THEN TRDATA="ECHO=":TT8.ECHO;GOSUB 89000
   RETURN
*
*---- AWAIT TT8 PROMPT CHARACTER
*
20400 *
   DTIME = 1                            ;* TIMEOUT AFTER 1 SECOND
   LOOP
      GOSUB 50000
      IF NOT(TIMEOUT) THEN
         INPUT TT8IN,1:
      END
   UNTIL TT8IN = ">" OR TIMEOUT DO
   REPEAT
   RETURN
*
*---- TRANSMIT STATEMENT TO TT8 & COLLECT ECHO
*
20500 *
   IF TRIM(FIELD(PROGLINE," ",2))[1,3] = "REM" THEN
      PROGLINE = TRIM(FIELD(PROGLINE," ",1)):" REM"
   END
   SLEN = 20                           ;* SEGMENT LENGTH
   CCNT = LEN(PROGLINE)                ;* PROGRAM LINE LENGTH
   SCNT = INT(CCNT/SLEN)               ;* SEGMENT COUNT
   RCNT = MOD(CCNT,SLEN)               ;* REMAINDER LENGTH
   FOR SPTR = 1 TO SCNT
      LINESEG = PROGLINE[1+(SPTR-1)*SLEN,SLEN]
      PRINT LINESEG:
      DTIME = 1                          ;* TIMEOUT AFTER 1 SECOND
      LOOP
         GOSUB 50000
         IF TIMEOUT THEN
            TT8IN = ""
         END ELSE
            INPUT TT8IN,SLEN:
         END
      UNTIL TIMEOUT DO
      REPEAT
   NEXT CPTR
   IF RCNT > 0 THEN
      LINESEG = PROGLINE[1+SCNT*SLEN,RCNT]
      PRINT LINESEG:
      DTIME = 1                          ;* TIMEOUT AFTER 1 SECOND
      LOOP
         GOSUB 50000
         IF TIMEOUT THEN
            TT8IN = ""
         END ELSE
            INPUT TT8IN,RCNT:
         END
      UNTIL TIMEOUT DO
      REPEAT
   END
   PRINT
   RETURN
*
*---- TERMINATE TT8 COMMUNICATIONS
*
20900 *
   PRINT "OFF"
   GOSUB 20300
   PRINT
   IF TMSG # "" THEN
      PRINT TMSG:
      PRINT
      TMSG = ""
   END
   PRINT " Switch to Terminal. Press <RETURN> to continue.... ":
   DTIME = 1                        ;* TIMEOUT AFTER 1 SECOND
   LOOP
      GOSUB 50000
      IF TIMEOUT THEN
         REPLY = ""
      END ELSE
         INPUT REPLY,1:
      END
   UNTIL TIMEOUT DO
   REPEAT
   INPUT REPLY:
   ECHO ON
   RETURN
*
*---- CHECK INPUT PRESENT
*
50000 *
*
* DTIME = NUMBER OF SECONDS TO WAIT
*
   STIME = TIME()
   LOOP
      INPUT DATA.PRESENT,-1
      ETIME = TIME() - STIME
      IF ETIME < 0 THEN ETIME = ETIME + 86400
   UNTIL DATA.PRESENT OR ETIME > DTIME DO
   REPEAT
   TIMEOUT = NOT(DATA.PRESENT)
   RETURN
*
*---- DISPLAY SCREEN
*
70000 *
   PRINT @(-1):
   PRINT @(25,0):"TransTerm 8 Utility Program":
   PRINT @(67,0):"Version 1.20":
   PRINT @(0,1):STR("-",80):
   PRINT @(0,20):STR("-",80):
   PRINT @(0,22):STR("-",80):
   RETURN
*
*---- DISPLAY MAIN MENU
*
80000 *
   PRINT @(34,5)  :"Main Menu":
   PRINT @(25,8)  :"1 - Initialize TT8":
   PRINT @(25,10) :"2 - Download program to TT8":
   PRINT @(25,12) :"3 - Upload data from TT8":
   RETURN
*
*---- DISPLAY OPTION 1 DESCRIPTION
*
80100 *
   GOSUB 80900
   PRINT @(20,8)  :"This function will completely initialize"
   PRINT @(20,9)  :"the TT8 Barcode reader."
   PRINT @(20,11) :"Any data stored in the TT8 will be cleared."
   PRINT @(20,13) :"The program must be downloaded to the TT8"
   PRINT @(20,14) :"before operation may be resumed."
   RETURN
*
*---- DISPLAY OPTION 2 DESCRIPTION
*
80200 *
   GOSUB 80900
   PRINT @(20,8)  :"This function will download the TT8 control"
   PRINT @(20,9)  :"program into the TT8 Barcode reader."
   PRINT @(20,11) :"Any data stored in the TT8 will remain"
   PRINT @(20,12) :"intact."
   RETURN
*
*---- DISPLAY OPTION 3 DESCRIPTION
*
80300 *
   GOSUB 80900
   PRINT @(20,8)  :"This function will retrieve data stored"
   PRINT @(20,9)  :"within the TT8 Barcode reader."
   PRINT @(20,11) :"Upon successful completion, retrieved data"
   PRINT @(20,12) :"will be cleared."
   RETURN
*
*---- CLEAR MENU
*
80900 *
   FOR ROW = 3 TO 19
      PRINT @(0,ROW):@(-4):
   NEXT ROW
   RETURN
*
*---- TRACE ROUTINE
*
89000 *
   READU TRREC FROM TT8.TRAN, CONO:"TRACE" ELSE TRREC = ""
   IF TRDATA = "" THEN
      TRREC<-1> = "NULL"
   END ELSE
      TRREC<-1> = TRDATA
   END
   WRITE TRREC ON TT8.TRAN, CONO:"TRACE"
   RETURN
*
*---- ERROR ROUTINE
*
90000 *
   PRINT @(0,23):@(-4):ERRMSG:
   INPUT REPLY,1:
   PRINT @(0,23):@(-4):
   RETURN
*
*---- END OF PROGRAM
*
99999 *
   PRINT @(-1):
   STOP
END
