*COPY>CPYLIB>COM1
*COPY>ICS.CPYLIB>COM.INVENTORY
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.REG.MAINT
* BY          - JIHAD YAMOUT, C.B.A
* DATE        - 10/11/83
* MODIFIED    - NATALIE BLAKE
* DATE MOD    - 10/17/88
* COMMENTS    - ADDED CUST XREF
* DESCRIPTION
*           This program provides the maintenance facility for
*           regular inventory.
* MOD         - CSF 12279 INSTALL CSF 9259 CHANGES RWW 3.22.90
*T20234 rick 08/07/1996 * Found mismatch in rev10a and 10b in this pgm.
*T22381 stefanie 01/09/1998 * Allow change or delete of customer number.
*T25884 cm 06/19/2001 * Add duplicate function.
*T26126 adelgado 02/25/2002 * Implement the LOCKED clause for READU.
*T28266 lross 09/22/2004 * Look at IWH.RECP.NO vs IWH.ON.HAND.
*********************************************************************
*
**** INSERT FILE EQUATES
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.HIST
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>COLOR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>ICS.CPYLIB>INV.STATS
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>INV.WHSE
*
**** SETUP FOR SYSTEM ERRMSGS
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
**** OPEN FILES
*
  OPEN '','INVENTORY' TO INVENTORY ELSE
    ERRMSG = 'INVENTORY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','FOH.TABLE' TO FOH.TABLE ELSE
    ERRMSG = 'FOH.TABLE FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','WAREHOUSE' TO WAREHOUSE ELSE
    ERRMSG = 'WAREHOUSE FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.WHSE' TO INV.WHSE ELSE
    ERRMSG = 'INV.WHSE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.HIST' TO INV.HIST ELSE
    ERRMSG = 'INV.HIST IS MISSING'
    GOTO 93000
  END
  OPEN '','CATEGORY' TO CATEGORY ELSE
    ERRMSG = 'CATEGORY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','COMPANY' TO COMPANY ELSE
    ERRMSG = 'COMPANY FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CUSTOMER' TO CUSTOMER ELSE
    ERRMSG = 'CUSTOMER FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','VEND' TO VEND ELSE
    ERRMSG = 'VENDOR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','VENDOR.XREF' TO VENDOR.XREF ELSE
    ERRMSG = 'VENDOR.XREFOR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','COLOR' TO COLOR ELSE
    ERRMSG='COLOR FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','ICS.SCREENS' TO M.SCREENS ELSE
    ERRMSG = 'M.SCREENS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CONTROL' TO CONTROL ELSE
    ERRMSG = 'CONTROL FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INVENTORY.XREF' TO INVENTORY.XREF ELSE
    ERRMSG = 'INVENTORY.XREF FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','CUSTOMER.XREF' TO CUSTOMER.XREF ELSE
    ERRMSG = 'CUSTOMER.XREF FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','XREF.DATA' TO XREF.DATA ELSE
    ERRMSG = 'XREF.DATA FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','PREFIX' TO PREFIX ELSE
    ERRMSG = 'PREFIX FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.STATS' TO INV.STATS ELSE
    ERRMSG = 'INV.STATS FILE IS MISSING'
    GOTO 93000
  END
  OPEN '','INV.WHSE.LOC' TO INV.WHSE.LOC ELSE
    ERRMSG = 'INV.WHSE.LOC FILE IS MISSING'
    GOTO 93000
  END
*
**** GET COMPANY NAME
*
  MAT COMP.REC = ''
  CONO = ''
  CALL GET.CONO(CONO,MAT COMP.REC)
  IF CONO = 'END' THEN GOTO 99999
*
**** SET UP GEN.XREF.REC
*
*      MAT EDIT.COM = ''
*      TYP = 0; CALL EDIT.SUB; FILL = "#"
  MAT EDIT.COM.DRIVER = ''
  MAT GEN.XREF.REC = ''
  OPTION = ''
  GXR.CO = CONO
*
**** MAIN PROCESSING
*
  MAT EDIT.COM.DRIVER = ''
  ECD.SCRN.CNT = 3
  ECD.SCRN.NAME<1> = 'INV.REG.MAINT'
  ECD.SCRN.NAME<2> = 'PROD.VEND.MAINT'
  ECD.SCRN.NAME<3> = 'WHSE.INV.REG.MAINT'
  ECD.ACTION=1;CALL SCRN.EDIT
**** PRINT SCREEN
1*
  ECD.SCRN.NO = 1
  MAT INV.REC = ''
  MAT IWH.REC = '' ;*T28266
  MAT CATG.REC = ""
  MAT SCV.REC = ""
  ECD.ACTION=2;CALL SCRN.EDIT
*
**** ENTER PROD NUMBER
*
5*
  ECD.NUM = 5
*T25884 v
*  ECD.VALDAT.CODE = '4'
*  ECD.VALDAT.FILE = INVENTORY
*  ECD.PREFIX.ID = CONO
  ECD.ACTION = 6 ; CALL SCRN.EDIT
*T25884 ^
  ECD.ACTION=4;CALL SCRN.EDIT
  PROD.NUM = ECD.RET.VALUE
  OLD.NAME = ''
  BEGIN CASE
    CASE ECD.RET.VALUE = 'END'
      GOTO 99999
*T25884 v
*     CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
*        STATUS = '0'
*        FOR I = 1 TO INV.REC.SIZE
*           INV.REC(I) = ECD.VALDAT.ITEM<I>
*        NEXT I
*        BEGIN CASE
*           CASE INV.M.LINE = "FNGD"
*              IF CO.OPS = 'Y' THEN
*                 ERRMSG = "This product is a Finished Goods Product"
*                 GOSUB 91000; GOTO 5
*              END
*           CASE INV.PAP.TYPE = "ROLL" OR INV.PAP.TYPE = "SKID" OR INV.PAP.TYPE = "SHEET" OR INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
*              ERRMSG = "This product is a Paper Product"
*              GOSUB 91000; GOTO 5
*        END CASE
*        OLD.NAME = INV.FULL.DESC
*        GOSUB 1000
*        ECD.ACTION=3;CALL SCRN.EDIT
*     CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
*        STATUS = '1'
*        INV.PAP.TYPE = 'REGULAR'
*        INV.COST.WT = 100
*        FOR S = 1 TO 13 UNTIL ECD.RET.VALUE = "END"
*           ON S GOSUB 35,180,10,15,40,70,80,85,90,95,97,100,110
*        NEXT S
*        IF ECD.RET.VALUE = 'END' THEN GOTO 1
*T25884 ^
    CASE ECD.RET.VALUE = ''
      ECD.NUM = 11
      ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE = '' OR ECD.RET.VALUE = 'END' THEN GOTO 5
      GXR.NAME = "INV.REG"
      GXR.XREF = INVENTORY.XREF
      GXR.FILE = INVENTORY
      GXR.SRCH.ID = ECD.RET.VALUE
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION=2;CALL SCRN.EDIT
      IF GXR.ID = '' THEN
        SCV.REC(11)<1> = ''
        ECD.ACTION=3;CALL SCRN.EDIT
        GOTO 5
      END ELSE
        * T26126 v
        MATREADU INV.REC FROM INVENTORY, CONO:GXR.ID LOCKED
          ERRMSG = 'PRODUCT record is locked by user - ':GETUSERNAME(STATUS())
          GOSUB 91000;GOTO 5 
        END ELSE
        * T26126 ^
          ERRMSG = 'PRODUCT NUMBER ':GXR.ID:' IS MISSING'
          GOSUB 91000
          SCV.REC(11)<1> = ''
          GOTO 5
        END
        BEGIN CASE
          CASE INV.M.LINE = "FNGD"
            IF CO.OPS = 'Y' THEN
              ERRMSG = "This product is a Finished Goods Product"
              GOSUB 91000; GOTO 5
            END
          CASE INV.PAP.TYPE = "ROLL" OR INV.PAP.TYPE = "SKID" OR INV.PAP.TYPE = "SHEET" OR INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
            ERRMSG = "This product is a Paper Product"
            GOSUB 91000; GOTO 5
        END CASE
        STATUS = '0'
        PROD.NUM = GXR.ID
        OLD.NAME = INV.FULL.DESC
        SCV.REC(5)<1> = PROD.NUM
        GOSUB 1000
        ECD.ACTION=3;CALL SCRN.EDIT
      END
*T25884 v
    CASE 1
      PROD.NUM = ECD.RET.VALUE
      * T26126 V
      MATREADU INV.REC FROM INVENTORY, CONO:PROD.NUM LOCKED
        ERRMSG = 'PRODUCT record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 91000;GOTO 5 
      END THEN
     * T26126 ^
        STATUS = '0'
        BEGIN CASE
          CASE INV.M.LINE = 'FNGD'
            IF CO.OPS = 'Y' THEN
              ERRMSG = 'This product is a Finished Goods Product'
              GOSUB 91000; GOTO 5
            END
          CASE INV.PAP.TYPE = "ROLL" OR INV.PAP.TYPE = "SKID" OR INV.PAP.TYPE = "SHEET" OR INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
            ERRMSG = 'This product is a Paper Product'
            GOSUB 91000; GOTO 5
        END CASE
        OLD.NAME = INV.FULL.DESC
        GOSUB 1000
        ECD.ACTION = 3; CALL SCRN.EDIT
      END ELSE
        ECD.NUM = 34 ; SCV.REC(ECD.NUM)<1> = ''
        ECD.ACTION = 4 ; CALL SCRN.EDIT
        BEGIN CASE
          CASE ECD.RET.VALUE = 'D'
7 *
            ECD.NUM = 35 ; SCV.REC(ECD.NUM)<1> = ''
            ECD.ACTION = 4; CALL SCRN.EDIT
            IF ECD.RET.VALUE # 'END' THEN
              MATREAD INV.REC FROM INVENTORY, CONO:ECD.RET.VALUE THEN
                BEGIN CASE
                  CASE INV.M.LINE = 'FNGD'
                    IF CO.OPS = 'Y' THEN
                      ERRMSG = 'This product is a Finished Goods Product'
                      GOSUB 91000; GOTO 7
                    END
                  CASE INV.PAP.TYPE = "ROLL" OR INV.PAP.TYPE = "SKID" OR INV.PAP.TYPE = "SHEET" OR INV.PAP.TYPE = "LROLL" OR INV.PAP.TYPE = "PCOAT"
                    ERRMSG = 'This product is a Paper Product'
                    GOSUB 91000; GOTO 7
                END CASE
                INV.SUBS = ''
                INV.LA.SALE = '' ; INV.LA.PUR = ''
                STATUS = '2'
                OLD.NAME = INV.FULL.DESC
                SCV.REC(5)<1> = PROD.NUM
                GOSUB 1000
                ECD.ACTION = 3 ; CALL SCRN.EDIT
                INV.WHSE.CODE = ""
              END ELSE
                ERRMSG = 'Cannot locate product # ':ECD.RET.VALUE
                GOSUB 91000 ; GOTO 7
              END
            END
          CASE ECD.RET.VALUE = 'A'
            MAT INV.REC = ''
            STATUS = '1'
            INV.PAP.TYPE = 'REGULAR' 
            INV.COST.WT = 100        
            FOR S = 1 TO 23
              ON S GOSUB 35,180,10,15,40,70,80,85,90,95,97,100,110
              IF ECD.RET.VALUE = 'END' THEN GOTO 1
            NEXT S
          CASE 1
            ECD.RET.VALUE = 'END'
        END CASE
        IF ECD.RET.VALUE = 'END' THEN
          RELEASE INVENTORY, CONO:PROD.NUM
          GOTO 5
        END
      END
*T25884 ^
  END CASE
  GOSUB 60000
  OPTION = ''
  ECD.SCRN.NO = 1
  MAT INV.REC = ''
  MAT CATG.REC = ""
  MAT SCV.REC = ""
  ECD.ACTION=6;CALL SCRN.EDIT
  GOTO 5
*
**** ENTER SPECIAL CODE
*
10*
  ECD.NUM = 6
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.SPEC = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER PROD LINE
*
15*
  GOSUB 80000
  IF QTY.FLAG THEN QTY.FLAG = '';GOTO 21
  ECD.NUM = 7
  ECD.VALDAT.CODE = 2
  ECD.VALDAT.FILE = CATEGORY
  ECD.PREFIX.ID = CONO
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE = 'END' THEN GOTO 20
  INV.LINE = ECD.RET.VALUE
  FOR SAVE = 1 TO CATG.REC.SIZE
    CATG.REC(SAVE) = ECD.VALDAT.ITEM<SAVE>
  NEXT SAVE
  BEGIN CASE
    CASE CATG.MAJ.LINE = "FNGD"
      IF CO.OPS = 'Y' THEN
        ERRMSG = "Can't utilize a Finished Goods product line for a Regular product"
        GOSUB 91000; GOTO 15
      END
    CASE CATG.TYPE # "P" AND CATG.TYPE # "I" AND CATG.TYPE # "F" AND CATG.TYPE # "O"
      ERRMSG = "Can't utilize a Paper product line for a Regular product"
      GOSUB 91000; GOTO 15
  END CASE
*
**** SAVE MAJOR LINE
*
20*
  INV.M.LINE = CATG.MAJ.LINE
  ECD.NUM = 8
  SCV.REC(8)<ECD.SCRN.NO,1> = CATG.MAJ.LINE
  ECD.ACTION=5;CALL SCRN.EDIT
  IF OPTION # "" THEN GOSUB 40
21*
  RETURN
*
**** ENTER DESCRIPTION
*
35*
  ECD.NUM = 11
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.DESC = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER UNIT BUY
*
40*
  BEGIN CASE
    CASE CATG.TYPE = "P"
      U.TYPE = "EA"
    CASE CATG.TYPE = "I"
      U.TYPE = "LBS"
    CASE 1
      U.TYPE = ""
  END CASE
  ECD.NUM = 12
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.UNIT<1,1> = ECD.RET.VALUE
  END
*
**** ENTER UNIT STOCK
*
45*
  ECD.NUM = 13
  IF CATG.TYPE # "P" THEN
    ECD.ACTION = 4 ; CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      IF CATG.TYPE = "I" THEN
        BEGIN CASE
          CASE ECD.RET.VALUE = "SHT"
            ERRMSG = "CAN'T ENTER PAPER UNIT FOR REGULAR PRODUCT" ; GOSUB 91000 ; GOTO 45
          CASE ECD.RET.VALUE = "PC"
            ERRMSG = "CAN'T ENTER PAPER UNIT FOR REGULAR PRODUCT" ; GOSUB 91000 ; GOTO 45
          CASE ECD.RET.VALUE = "MSI"
            ERRMSG = "CAN'T ENTER PAPER UNIT FOR REGULAR PRODUCT" ; GOSUB 91000 ; GOTO 45
          CASE ECD.RET.VALUE = "FT"
            ERRMSG = "CAN'T ENTER PAPER UNIT FOR REGULAR PRODUCT" ; GOSUB 91000 ; GOTO 45
        END CASE
      END
      INV.UNIT<1,2> = ECD.RET.VALUE
    END
  END ELSE
    INV.UNIT<1,2> = U.TYPE
    ECD.ACTION=5;SCV.REC(13)<ECD.SCRN.NO,1> = INV.UNIT<1,2> ; CALL SCRN.EDIT
  END
*
**** ENTER UNIT PRICE
*
50*
  INV.UNIT<1,3> = INV.UNIT<1,2>
  ECD.NUM = 14
  ECD.ACTION=5;SCV.REC(14)<ECD.SCRN.NO,1> = INV.UNIT<1,3> ; CALL SCRN.EDIT
  RETURN
*
**** ENTER STOCK/BUY RATIO
*
70*
  ECD.NUM = 18
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.SBR = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER WEIGHT
*
80*
  ECD.NUM = 20
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.WEIGHT = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER VOLUME
*
85*
  ECD.NUM = 21
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.VOLUME = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER COLOR
*
90*
  ECD.NUM = 22
  ECD.VALDAT.FILE = COLOR
  ECD.PREFIX.ID = CONO
  ECD.VALDAT.CODE = "2"
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.COLOR = ECD.RET.VALUE
*       READ DESC FROM COLOR,CONO:INV.COLOR ELSE
*          ERRMSG="INVALID COLOR CODE";GOSUB 91000
*          GOTO 95
*       END
  END
  RETURN
*
**** ENTER LENGTH
*
95*
  IF CATG.TYPE # "I" THEN
    ECD.NUM = 23
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      INV.PAP.LEN = ECD.RET.VALUE
    END
  END
  RETURN
*
**** ENTER WIDTH
*
97*
  IF CATG.TYPE # "I" THEN
    ECD.NUM = 33
    ECD.ACTION=4;CALL SCRN.EDIT
    IF ECD.RET.VALUE # 'END' THEN
      INV.PAP.WIDTH = ECD.RET.VALUE
    END
  END
  RETURN
*
**** ENTER IF TAXABLE
*
100*
  ECD.NUM = 24
  ECD.DEFAULT = 'N'
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.TAXABLE = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER SUGG ORD QTY
*
110*
  ECD.NUM = 26
  ECD.ACTION=4;CALL SCRN.EDIT
  IF ECD.RET.VALUE # 'END' THEN
    INV.SOQ = ECD.RET.VALUE
  END
  RETURN
*
**** ENTER CUSTOMER NUMBER
*
180*
*T22381 v      GOSUB 80000 ;*CSF9259
*      IF QTY.FLAG THEN QTY.FLAG = '';GOTO 189
  IF INV.CUST # '' THEN                                                     
    GOSUB 81000                                                             
    IF CUST.QTY.FLAG THEN CUST.QTY.FLAG = '' ; GOTO 189                     
  END                                                                       
*T22381 ^
  ECD.NUM=3; ECD.ACTION=4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END"
      GOTO 189
    CASE ECD.RET.VALUE = ""
*T22381 v
      SCV.REC(3)<1> = ''
      SCV.REC(4)<1> = ''
      ECD.ACTION = 3 ; CALL SCRN.EDIT
*T22381 ^
      ECD.NUM=4; ECD.ACTION=4; CALL SCRN.EDIT
      IF ECD.RET.VALUE="" OR ECD.RET.VALUE="END" THEN INV.CUST = "";GOTO 189
      GXR.NAME = "CUSTOMER"
      GXR.XREF = CUSTOMER.XREF
      GXR.FILE=CUSTOMER
      GXR.SRCH.ID=ECD.RET.VALUE
      CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
      ECD.ACTION=2;CALL SCRN.EDIT
      IF GXR.ID='' THEN
        SCV.REC(3)<1> = ""
        ECD.ACTION=3; CALL SCRN.EDIT
        GOTO 180
      END ELSE
        MATREAD CUST.REC FROM CUSTOMER,CONO:GXR.ID ELSE
          ERRMSG = "CUSTOMER ":GXR.ID:" IS MISSING"
          GOSUB 91000
          GOTO 180
        END
        SCV.REC(3)<1>=GXR.ID
        SCV.REC(4)<1>=CUST.NAME
        ECD.ACTION=3; CALL SCRN.EDIT
        INV.CUST=GXR.ID
      END
    CASE 1
      MATREAD CUST.REC FROM CUSTOMER,CONO:ECD.RET.VALUE ELSE
        ERRMSG = "CUSTOMER ":GXR.ID:" IS MISSING"
        GOSUB 91000
        GOTO 180
      END
      SCV.REC(3)<1> = ECD.RET.VALUE
      SCV.REC(4)<1> = CUST.NAME
      ECD.ACTION = 3; CALL SCRN.EDIT
      INV.CUST=ECD.RET.VALUE
  END CASE
189*
  RETURN
*
**** GET ALL VALUES FROM FILE
*
1000*
  SCV.REC(3)<ECD.SCRN.NO>= INV.CUST
  IF INV.CUST # "" THEN
    MATREAD CUST.REC FROM CUSTOMER,CONO:INV.CUST ELSE
      CUST.NAME = "??????????????????????????????"
    END
    SCV.REC(4)<ECD.SCRN.NO> = CUST.NAME
  END
  SCV.REC(6)<ECD.SCRN.NO,1> = INV.SPEC
  SCV.REC(7)<ECD.SCRN.NO,1> = INV.LINE
  MATREAD CATG.REC FROM CATEGORY , CONO:INV.LINE ELSE
    MAT CATG.REC = ''
  END
  SCV.REC(8)<ECD.SCRN.NO,1> = INV.M.LINE
  SCV.REC(11)<ECD.SCRN.NO,1> = INV.DESC
  SCV.REC(12)<ECD.SCRN.NO,1> = INV.UNIT<1,1>
  SCV.REC(13)<ECD.SCRN.NO,1> = INV.UNIT<1,2>
  SCV.REC(14)<ECD.SCRN.NO,1> = INV.UNIT<1,3>
  SCV.REC(18)<ECD.SCRN.NO,1> = INV.SBR
  SCV.REC(20)<ECD.SCRN.NO,1> = INV.WEIGHT
  SCV.REC(21)<ECD.SCRN.NO,1> = INV.VOLUME
  SCV.REC(22)<ECD.SCRN.NO,1> = INV.COLOR
  SCV.REC(23)<ECD.SCRN.NO,1> = INV.PAP.LEN
  SCV.REC(24)<ECD.SCRN.NO,1> = INV.TAXABLE
  SCV.REC(26)<ECD.SCRN.NO,1> = INV.SOQ
  SCV.REC(33)<ECD.SCRN.NO,1> = INV.PAP.WIDTH
  RETURN
*
**** ENTER OPTIONS
*
60000*
  MORE = 1
  LOOP
    ECD.NUM = 32
    SCV.REC(ECD.NUM)<1> = ''
    ECD.ACTION=4;CALL SCRN.EDIT
    OPTION = ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION = 'END' OR OPTION = 'E'
        RELEASE INVENTORY, CONO:PROD.NUM        ;* T26126
        MORE = 1
      CASE NUM(OPTION)
        ON OPTION GOSUB 35,180,10,15,40,70,80,85,90,95,97,100,110
        MORE = 0
      CASE OPTION = 'V'
        ECD.SCRN.NO = 2
        ECD.ACTION = 2; CALL SCRN.EDIT
        CALL VEND.PRD.MAINT(STATUS,CONO,PROD.NUM)
        ECD.SCRN.NO = 1
        ECD.ACTION=2;CALL SCRN.EDIT
        ECD.ACTION=3;CALL SCRN.EDIT
        MORE = 0
      CASE OPTION = 'W'
*T20234 v
*            CALL INV.FULL.DESC.SUB(CONO,CONTROL)
*            MATWRITE INV.REC ON INVENTORY, CONO:PROD.NUM
*            MATREAD IH.REC FROM INV.HIST, CONO:PROD.NUM ELSE
*               MAT IH.REC = ''
*               MATWRITE IH.REC ON INV.HIST, CONO:PROD.NUM
*            END
*            CALL GEN.XREF.MAINT(GXR.CO,PROD.NUM,OLD.NAME,INV.FULL.DESC,INVENTORY.XREF,PREFIX)
*T20234 ^
        CALL WHSE.INV.REG.MAINT(STATUS,PROD.NUM,CONO)
        ECD.SCRN.NO = 1
        ECD.ACTION=2;CALL SCRN.EDIT
        ECD.ACTION=3;CALL SCRN.EDIT
        MORE = 0
      CASE OPTION = 'F'
        CALL INV.FULL.DESC.SUB(CONO,CONTROL)
        MATWRITE INV.REC ON INVENTORY, CONO:PROD.NUM
        MATREAD IH.REC FROM INV.HIST, CONO:PROD.NUM ELSE
          MAT IH.REC = ''
          MATWRITE IH.REC ON INV.HIST, CONO:PROD.NUM
        END
        CALL GEN.XREF.MAINT(GXR.CO,PROD.NUM,OLD.NAME,INV.FULL.DESC,INVENTORY.XREF,PREFIX)
        MORE = 1
    END CASE
  WHILE MORE = 0 DO REPEAT
  RETURN
*
*** CHECK FOR QTY'S
*
80000*
  QTY.FLAG = ''  ;*CSF9259
  WHSE.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
  IF WHSE.CNT = 0 THEN RETURN
  FOR A = WHSE.CNT TO 1 STEP -1
    MATREAD IWH.REC FROM INV.WHSE, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE
      MAT IWH.REC = ""
    END
    MATREAD INV.STAT.REC FROM INV.STATS, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE MAT INV.STAT.REC = ""
    PO.QTY = SUM(ISTAT.PO.QTY)
*T28266 v
*   IF PO.QTY * 1 = 0 AND IWH.ON.HAND * 1 = 0 AND ISTAT.JOB = "" THEN
    IF PO.QTY * 1 = 0 AND IWH.RECP.NO = '' AND ISTAT.JOB = "" THEN
      LOC.CNT = COUNT(IWH.LOC,VM) + (IWH.LOC # "")
      FOR DL = LOC.CNT TO 1 STEP -1
        MATREAD IWLO.REC FROM INV.WHSE.LOC,CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A>:"!":IWH.LOC<1,DL> ELSE MAT IWLO.REC = ""
        IF IWLO.LOC.ON.HAND # "" AND IWLO.LOC.ON.HAND * 0 # 0 THEN QTY.FLAG = 1
      NEXT DL
    END ELSE
      QTY.FLAG = 1
    END
  NEXT A
  IF QTY.FLAG THEN
    ERRMSG = "QTY OR JOB(S) ATTACHED TO THIS ITEM, MAY NOT CHANGE THIS FIELD!!"
    GOSUB 91000
  END
  RETURN
*T22381 v                                                                       
*                                                                               
*** CHECK FOR CUSTOMER QTY'S                                                    
*                                                                               
81000*
  CUST.QTY.FLAG = ''
  WHSE.CNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
  IF WHSE.CNT = 0 THEN RETURN
  FOR A = WHSE.CNT TO 1 STEP -1 UNTIL CUST.QTY.FLAG # ''
    MATREAD IWH.REC FROM INV.WHSE, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE
      MAT IWH.REC = ""                                                    
    END
    MATREAD INV.STAT.REC FROM INV.STATS, CONO:PROD.NUM:"!":INV.WHSE.CODE<1,A> ELSE MAT INV.STAT.REC = ""
    PO.QTY = SUM(ISTAT.PO.QTY)
    IF PO.QTY # 0 AND PO.QTY # '' THEN CUST.QTY.FLAG = 1
    PJQ = 0
    PJQ.CNT = DCOUNT(ISTAT.PO,VM)
    FOR PJQ.POS = 1 TO PJQ.CNT
      PJQ += SUM(ISTAT.PO.JOB.QTY<1,PJQ.POS>)
    NEXT PJQ.POS
    IF PJQ # 0 AND PJQ # '' THEN CUST.QTY.FLAG = 1
    IF IWH.RESV # 0 AND IWH.RESV # '' THEN CUST.QTY.FLAG = 1
    IF IWH.INPRCS # 0 AND IWH.RESV # '' THEN CUST.QTY.FLAG = 1
  NEXT A
  IF CUST.QTY.FLAG THEN
    ERRMSG = "THERE IS A QTY ATTACHED TO THIS ITEM; YOU MAY NOT CHANGE THIS FIELD!!"
    GOSUB 91000
  END
  RETURN
*T22381 ^
*
**** CALLS FOR SYSCOM
*
91000 ERR.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
92000 ERR.TYPE = 2
  CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 ERR.TYPE = 3
  CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1)
END
