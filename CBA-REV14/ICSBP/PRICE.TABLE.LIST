*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - PRICE.TABLE.LIST
* BY
* DATE        - 11/26/91
* DESCRIPTION -
*T26090 ajibaly 03/22/2002 * CONVERT TO R12 REPORT.SCRN
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>PRICE.TABLE
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SET UP FOR SYSCOM
SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
PROCREAD BUFFER ELSE
  ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC"
  GOTO 93000
END
CONO = BUFFER<1>
CONO.NAME = ""
*T26090 REPORT.NAME = "PRICE TABLE LISTING"
REPORT.NAME = ''
REPORT.NUM = BUFFER<2>
REPORT.DATE = ""
HD1 = " ; HD2 = "
CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUM,REPORT.DATE,HD1,HD2)
*
*---- OPEN FILES
*
OPEN '','PRICE.TABLE' TO PRICE.TABLE ELSE ERRMSG = "PRICE.TABLE FILE IS MISSING"; GOTO 93000
OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = "COMPANY IS MISSING" ; GOTO 93000
*
*---- INITIALIZE
*
SHD2 = "From Quantity   To Quantity     Price"
SHD3 = "-------------  -------------  ----------"
PAGE.NO = 0
LCNT = 99
*
*---- MAIN PROCESSING
*
PRINTER ON
DATA = 1
LOOP
  READNEXT PGT.ID ELSE DATA = 0
WHILE DATA DO
  MATREAD PGT.REC FROM PRICE.TABLE, PGT.ID THEN
    SHD1 = "Code : ":PGT.ID[4,6]"L#6  ":"Desc : ":PGT.DESC"L#20"
    GOSUB 1000
    QCNT = DCOUNT(PGT.QTY,VM)
    FOR Q = 1 TO QCNT
      IF LCNT > 55 THEN GOSUB 1000
      IF Q = 1 THEN
        START.QTY = 1
      END ELSE
        START.QTY = PGT.QTY<1,Q-1> + 1
      END
      PLINE = OCONV(START.QTY,"MD0,")"R#13  ":OCONV(PGT.QTY<1,Q>,"MD0,")"R#13  "
      PLINE = PLINE:OCONV(PGT.PRICE<1,Q>,"MD4")"R#10"
      PRINT PLINE
      LCNT = LCNT + 1
    NEXT Q
  END
REPEAT
GOTO 99999
*
*---- PRINT HEADINGS
*
1000 *
PRINT CHAR(12)
PAGE.NO = PAGE.NO + 1
PRINT HD1:PAGE.NO
PRINT HD2
PRINT
PRINT SHD1
PRINT
PRINT SHD2
PRINT SHD3
LCNT = 7
RETURN
*
93000 *
ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
PRINTER CLOSE
PRINTER OFF
END
