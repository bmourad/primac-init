*COPY>CPYLIB>COM1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - ICSBP
* PROGRAM     - INV.WHSE.LIST
* BY          - NATALIE BLAKE, C.B.A
* DATE        - 05/24/88
* DESCRIPTION - Inventory Warehouse Listing
*T26090 ajibaly 03/21/2002 * CONVERT TO REV12 / RPT.SCRN
*********************************************************************
*
*--- INSERT FILE EQUATES
*
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*--- OPEN FILES
*
      OPEN "","INVENTORY" TO INVENTORY ELSE
         ERRMSG = "INVENTORY FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
      END
      OPEN "","INV.WHSE" TO INV.WHSE ELSE
         ERRMSG = "INV.WHSE FILE IS MISSING" ; GOSUB 90000 ; GOTO 99999
      END
*
*--- READ PROC VALUES
      PROCREAD ID ELSE
         ERRMSG = "THIS PROGRAM MUST RUN FROM A PROC." ; GOSUB 90000 ; GOTO 99999
      END
*T26090v
*     CONO = ID<1> ; CONO.NAME = ID<2>
*
*--- GET THE FIRST TWO LINES OF HEADING
*
*      REPORT.NAME = "INVENTORY WAREHOUSE LISTING"
      CONO = ID<1> ; CONO.NAME = ''
      REPORT.NAME = ''
*T26090^
      REPORT.DATE = DATE()
      HD1 = "" ; HD2 = ""
      REPORT.NUMBER = ID<2>
      CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,REPORT.DATE,HD1,HD2)
*
*--- INITIALIZATION
*
      B.LINE = ""
      LINE.COUNT = 99
      PAGE.NO = 0
      S.HD1 = "PRODUCT NUMBER           DESCRIPTION           WHSE  STD-COST   AVG-COST   LIST-COST  FFOH % MARKUP  MIN QTY   MAX QTY  LOC"
      S.UN1 = "--------------- ------------------------------ ---- ---------- ---------- ---------- ------- ------ --------- --------- ----"
      S.HD2 = "MLINE / PLINE : "
*
*---- MAIN PROCESSING
*
      PRINTER ON
      DATA = 1
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA = 1 DO
         MATREAD IWH.REC FROM INV.WHSE,ID ELSE GOTO 111
         PRODUCT = FIELD(ID,"!",1)
         PRODUCT = PRODUCT[4,99]
         PRODUCT = TRIM(PRODUCT)
         WHSE = FIELD(ID,"!",2)
         MATREAD INV.REC FROM INVENTORY,CONO:PRODUCT ELSE MAT INV.REC=""
         NEXT.B.LINE = INV.M.LINE:" / ": INV.LINE
         GOSUB 300
         GOSUB 100
         B.LINE = NEXT.B.LINE
111   REPEAT
      GOTO 99999
*
*--- PRINT INVENTORY INFORMATION
*
100 *
      IF ICR.DV1 + 0 # 0 THEN
         IF IWH.MIN.QTY >= 0 THEN
            IWH.MIN.QTY=INT((((IWH.MIN.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
         END ELSE
            IWH.MIN.QTY=INT((((IWH.MIN.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
         END
         IF IWH.MAX.QTY >= 0 THEN
            IWH.MAX.QTY=INT((((IWH.MAX.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) + .5)
         END ELSE
            IWH.MAX.QTY=INT((((IWH.MAX.QTY / ICR.DV1) * ICR.MT1) / ICR.DV2) - .5)
         END
      END ELSE
         IWH.MIN.QTY = 0; IWH.MAX.QTY = 0
      END
      IF NEXT.B.LINE # B.LINE AND B.LINE # "" THEN
         GOSUB 200
      END
      IF LINE.COUNT > 60 THEN
         GOSUB 200
      END
      PRINT PRODUCT "L#15 ":
      PRINT INV.DESC "L#30 ":
      PRINT WHSE "R#4 ":
      PRINT OCONV(IWH.STD.COST,"MD4") "R#10 ":
      PRINT OCONV(IWH.AVG.COST,"MD4") "R#10 ":
      PRINT OCONV(IWH.LIST.COST,"MD4") "R#10 ":
      PRINT OCONV(IWH.FOH.PCT,"MD2") "R#7 ":
      PRINT OCONV(IWH.MARKUP,"MD2") "R#6 ":
      PRINT OCONV(IWH.MIN.QTY,ICR.CNV) "R#9 ":
      PRINT OCONV(IWH.MAX.QTY,ICR.CNV) "R#9 ":
      PRINT IWH.LOC<1,1> "L#4"
      CNT = DCOUNT(IWH.LOC,VM)
      FOR I = 2 TO CNT
         PRINT SPACE(120):IWH.LOC<1,I> "L#4"
         LINE.COUNT = LINE.COUNT + 1
         IF LINE.COUNT > 60 THEN
            GOSUB 200
         END
      NEXT I
      LINE.COUNT = LINE.COUNT + 1
      RETURN
*
*--- PRINT HEADING
*
200 *
      PRINT CHAR(12):
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PRINT S.HD2:NEXT.B.LINE
      PRINT
      PRINT S.HD1
      PRINT S.UN1
      PRINT
      LINE.COUNT = 8
      RETURN
***        CONVERT SHEET/WGHT/QTY
300 *
      BEGIN CASE
      CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE INV.UNIT<1,2> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2"
         ICR.DV1 = 10; ICR.MT1 = 1; ICR.DV2 = 1
      END CASE
      RETURN
*
*--- PRINT ERRMSG 
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY
*       PRINT @(0,23):CL
*       RETURN
*
*--- END OF REPORT
*
99999 *
      PRINTER OFF
      PRINTER CLOSE
      END
