********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - WEEK.UPDATE
* BY          - WALID YAMOUT , C.B.A
* DATE        - 06/11/85
* MODIFIED    - 03/17/88 BY KERRY WILSON
* DESCRIPTION -
* MOD         - TASK 15719 RWW 1.10.90 MEDICARE FICA (15723)
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQU
*
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PRS.CPYLIB>PAYREG
*COPY>PRS.CPYLIB>DEDREG
*COPY>PRS.CPYLIB>PETTY.CASH
*COPY>PRS.CPYLIB>MDS
*COPY>PRS.CPYLIB>QHR
*COPY>PRS.CPYLIB>MPAYREG
*COPY>PRS.CPYLIB>HEADER
*COPY>PRS.CPYLIB>EMPLOYEE.INVEST.CONTR
*COPY>PRS.CPYLIB>401K
*COPY>CPYLIB>CHAR
      PRECISION 6
      PROMPT ""
      WKHRS = 0
*1* V(7),S(7),AC(15)
      DIM BAD(500)
      DIM MUN(20)
      EQUATE MUN1 TO MUN(1),MUN2 TO MUN(2),MUN13 TO MUN(13),MUN14 TO MUN(14),MUN15 TO MUN(15),MUN16 TO MUN(16)
      MAT PAY.REC = ""; MAT DED.REC = ""; MAT EMP.REC = ""; MAT PTHR.REC = ""; MAT BAD = ""
      MAT QTD.REC = ""; MAT MUN = ""; MAT MPR.REC = ""
      PROCREAD X ELSE PRINT "MUST COME FROM PROC  -  HIT RETURN":;INPUT X;STOP
      CONO=X<1>
      BATCH.NO=X<4>
      OPEN "","DEDREG" TO DEDREG ELSE PRINT "CAN'T OPEN DEDREG":;INPUT R;STOP
      OPEN "","PAYREG" TO PAYREG ELSE PRINT "CAN'T OPEN PAYREG":;INPUT R;STOP
      OPEN "","PAYTYPE" TO PAYTYPE ELSE PRINT "CAN'T OPEN PAYTYPE":;INPUT R;STOP
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE":;INPUT R;STOP
      OPEN "","QHR" TO QHR ELSE PRINT "CAN'T OPEN QHR":;INPUT R;STOP
      OPEN "DICT","QHR" TO DQHR ELSE PRINT "CAN'T OPEN DQHR":;INPUT R;STOP
      OPEN "","MDS" TO MDS ELSE PRINT "CAN'T OPEN MDS":;INPUT R;STOP
      OPEN "","QTD-YTD" TO QTD.YTD ELSE PRINT "CAN'T OPEN QTD-YTD":;INPUT R;STOP
      OPEN "","MPAYREG" TO MPAYREG ELSE PRINT "CAN'T OPEN MPAYREG":;INPUT R;STOP
      OPEN "","UNION" TO UNION ELSE PRINT "CAN'T OPEN UNION":;INPUT R;STOP;*1
      OPEN "","MTH-UNION" TO MTH.UNION ELSE PRINT "CAN'T OPEN MTH-UNION":;INPUT R;STOP;*1
      OPEN "DICT","MTH-UNION" TO D.MTH.UNION ELSE PRINT "CAN'T OPEN DICT MTH-UNION":;INPUT R;STOP
      OPEN "","COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY FILE":;INPUT R;STOP
      OPEN "","CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL FILE":;INPUT R;STOP
      OPEN "","PETTY.CASH" TO PETTY.CASH ELSE PRINT "CAN'T OPEN PETTY.CASH FILE":;INPUT R;STOP
      OPEN "","EMPLOYEE.INVEST.CONTR" TO EMPLOYEE.INVEST.CONTR ELSE PRINT "CAN'T OPEN EMPLOYEE.INVEST.CONTR FILE":;INPUT R;STOP
      MATREAD COMP.REC FROM COMPANY, CONO ELSE PRINT "COMPANY ":CONO:" NOT ON FILE":; INPUT R; STOP
      MATREAD DC.REC FROM CONTROL, CONO:"401K" ELSE PRINT "401K RECORD NOT ON FILE FOR COMPANY ":CONO; INPUT R; STOP
      MATREAD PTHR.REC FROM PAYTYPE, CONO:"HEADER" ELSE
         PRINT "PAYTYPE ":CONO:"HEADER IS MISSING":
         INPUT R; STOP
      END
      READU PETTY.CODE FROM CONTROL, CONO:"PETTY.CASH.CODE" ELSE
         PRINT "CONTROL ":CONO:"PETTY.CASH.CODE IS MISSING":
         INPUT R; STOP
      END
      IF CO.QTR.CK.DATE = "Y" THEN PTHR.END.DATE=PTHR.CHK.DATE
      MONTH=OCONV(PTHR.END.DATE,'D2/')[1,2]
      IF MONTH = 1 OR MONTH = 2 OR MONTH = 3 THEN QTR = 1
      IF MONTH = 4 OR MONTH = 5 OR MONTH = 6 THEN QTR = 2
      IF MONTH = 7 OR MONTH = 8 OR MONTH = 9 THEN QTR = 3
      IF MONTH = 10 OR MONTH = 11 OR MONTH = 12 THEN QTR = 4
      PER.END=PTHR.END.DATE      ;*WEEK ENDING DATE USED AS KEY TO WEEKACT FILE
      BADS=0
      PRINT@(5,18):"NOW UPDATING EMPLOYEE NUMBER:":
*****
100*
      READNEXT ID ELSE GOTO 500
      IF ID[1,3] # CONO THEN GOTO 100
      MAT DED.REC = ""; MAT PAY.REC = ""; MAT EMP.REC = ""
      UNIONDUES=0
      SECONDKEY=ID[8,2]
      EMP.NO = ID[4,4]
      MATREAD DED.REC FROM DEDREG,ID ELSE BADS=BADS+1;BAD(BADS)=EMP.NO:" ":SECONDKEY:"   NOT IN DEDREG FILE";GOTO 100
      MATREAD PAY.REC FROM PAYREG,ID ELSE BADS=BADS+1;BAD(BADS)=EMP.NO:" ":SECONDKEY:"   NOT IN PAYREG FILE";GOTO 100
      PRINT@(35,18):EMP.NO"R#4":
      TOTDED=DED.BONDS
      MSC.CNT = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
      FOR Y = MSC.CNT TO 1 STEP - 1
         IF DED.MISC.AMT<1,Y> <> 0 THEN
            TOTDED = TOTDED + DED.MISC.AMT<1,Y>
         END ELSE
            DED.MISC.AMT = DELETE(DED.MISC.AMT,1,Y,0)
            DED.MISC.CODE = DELETE(DED.MISC.CODE,1,Y,0)
         END
      NEXT Y
      MSC.CNT = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
*****
*   EMPLOYEE FILE UPDATE
200*
      EID = CONO:EMP.NO
      MATREADU EMP.REC FROM EMPLOYEE,EID ELSE RELEASE EMPLOYEE,EID;BADS=BADS+1;BAD(BADS)=EMP.NO:"   NOT IN EMPLOYEE FILE";GOTO 100
      QID = CONO:EMP.NO:EMP.DEPT
      IF EMP.BONDS<1,3>#"" THEN
         EMP.BONDS<1,2>=EMP.BONDS<1,2>+DED.BONDS
      END
      MATREADU EICR.REC FROM EMPLOYEE.INVEST.CONTR, EID ELSE MAT EICR.REC = ""
      EICR.PCT.CNT<1,MONTH> = EICR.PCT.CNT<1,MONTH>+1
      PCT.CNT = EICR.PCT.CNT<1,MONTH>
      EICR.GROSS<1,MONTH> = EICR.GROSS<1,MONTH>+PAY.GROSS
      LOCATE DC.BM IN DED.MISC.CODE<1>,1 SETTING MFND THEN
         LOCATE DC.BM IN EMP.MSC.CODE<1>,1 SETTING FND THEN
            EICR.BTM.PCT<1,MONTH,PCT.CNT> = EMP.MSC.AMT<1,FND>
         END
      END
      LOCATE DC.AM IN DED.MISC.CODE<1>,1 SETTING MFND THEN
         LOCATE DC.AM IN EMP.MSC.CODE<1>,1 SETTING FND THEN
            EICR.ATM.PCT<1,MONTH,PCT.CNT> = EMP.MSC.AMT<1,FND>
         END
      END
      MATWRITE EICR.REC ON EMPLOYEE.INVEST.CONTR, EID
      MAT EICR.REC = ''
      FOR Y = 1 TO MSC.CNT
         LOCATE DED.MISC.CODE<1,Y> IN EMP.MSC.CODE<1>,1 SETTING FND ELSE FND = 0
         IF FND THEN
            IF DED.MISC.CODE<1,Y>[1,1] = "U" THEN UNIONDUES = UNIONDUES + DED.MISC.AMT<1,Y>
            IF EMP.MSC.DED<1,FND> # "N" AND EMP.MSC.DED<1,FND> # "" AND EMP.MSC.DED<1,FND> # 0 THEN
               EMP.MSC.DED<1,FND> = EMP.MSC.DED<1,FND> - DED.MISC.AMT<1,Y>
            END
*****
*  EMPLOYEE.INVEST.CONTR FILE
*
            CNT = DCOUNT(EMP.INVEST<1,FND>,SVM)
            BEGIN CASE
            CASE DED.MISC.CODE<1,Y> = DC.BM
            CASE DED.MISC.CODE<1,Y> = DC.BU
            CASE DED.MISC.CODE<1,Y> = DC.AM
            CASE DED.MISC.CODE<1,Y> = DC.AU
            CASE 1
               CNT = 0
            END CASE
            IF CNT THEN
               TOT.CONT = 0
               HIGH.PCT = EMP.INVEST.PCT<1,FND,1>
               HIGH.LN = 1
               FOR I = 1 TO CNT
                  AMT = INT(DED.MISC.AMT<1,Y> * EMP.INVEST.PCT<1,FND,I>/100)
                  IF EMP.INVEST.PCT<1,FND,I> > HIGH.PCT THEN
                     HIGH.LN = I
                     HIGH.PCT = EMP.INVEST.PCT<1,FND,I>
                  END
                  IF AMT = 0 THEN GOTO 205
                  MATREADU EICR.REC FROM EMPLOYEE.INVEST.CONTR, EID:"!":EMP.MSC.CODE<1,FND>:"!":EMP.INVEST<1,FND,I> ELSE MAT EICR.REC = ""
                  TOT.CONT = TOT.CONT + AMT
                  BEGIN CASE
                  CASE DED.MISC.CODE<1,Y> = DC.BM
                     EICR.BTM<1,MONTH,PCT.CNT> = AMT
                     EICR.BTM.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  CASE DED.MISC.CODE<1,Y> = DC.BU
                     EICR.BTU<1,MONTH,PCT.CNT> = AMT
                     EICR.BTU.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  CASE DED.MISC.CODE<1,Y> = DC.AM
                     EICR.ATM<1,MONTH,PCT.CNT> = AMT
                     EICR.ATM.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  CASE 1
                     EICR.ATU<1,MONTH,PCT.CNT> = AMT
                     EICR.ATU.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  END CASE
                  MATWRITE EICR.REC ON EMPLOYEE.INVEST.CONTR, EID:"!":EMP.MSC.CODE<1,FND>:"!":EMP.INVEST<1,FND,I>
205            NEXT I
               IF TOT.CONT <> DED.MISC.AMT<1,Y> THEN
                  AMT = DED.MISC.AMT<1,Y> - TOT.CONT
                  MATREADU EICR.REC FROM EMPLOYEE.INVEST.CONTR, EID:"!":EMP.MSC.CODE<1,FND>:"!":EMP.INVEST<1,FND,HIGH.LN> ELSE MAT EICR.REC = ""
                  BEGIN CASE
                  CASE DED.MISC.CODE<1,Y> = DC.BM
                     EICR.BTM<1,MONTH,PCT.CNT> = EICR.BTM<1,MONTH,PCT.CNT> + AMT
                     EICR.BTM.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  CASE DED.MISC.CODE<1,Y> = DC.BU
                     EICR.BTU<1,MONTH,PCT.CNT> = EICR.BTU<1,MONTH,PCT.CNT> + AMT
                     EICR.BTU.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  CASE DED.MISC.CODE<1,Y> = DC.AM
                     EICR.ATM<1,MONTH,PCT.CNT> = EICR.ATM<1,MONTH,PCT.CNT> + AMT
                     EICR.ATM.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  CASE 1
                     EICR.ATU<1,MONTH,PCT.CNT> = EICR.ATU<1,MONTH,PCT.CNT> + AMT
                     EICR.ATU.PCT<1,MONTH,PCT.CNT>=EMP.MSC.AMT<1,FND>
                  END CASE
                  MATWRITE EICR.REC ON EMPLOYEE.INVEST.CONTR, EID:"!":EMP.MSC.CODE<1,FND>:"!":EMP.INVEST<1,FND,HIGH.LN>
               END
            END
         END
      NEXT Y
      MATWRITE EMP.REC ON EMPLOYEE,EID
*******************************
*   QTD-YTD FILE
*******************************
      THR = 0; SICK = 0
      MATREADU QTD.REC FROM QTD.YTD, EID ELSE MAT QTD.REC = ""
      FOR Y=1 TO PAY.RECORDS
         QTD.REG.HRS<1,QTR>= QTD.REG.HRS<1,QTR>+ PAY.HOURS<1,Y,1>
         QTD.OT.HRS<1,QTR>= QTD.OT.HRS<1,QTR>+ PAY.HOURS<1,Y,2>
         QTD.DT.HRS<1,QTR>= QTD.DT.HRS<1,QTR>+ PAY.HOURS<1,Y,3>
         QTD.SCK.HRS<1,QTR>= QTD.SCK.HRS<1,QTR>+ PAY.HOURS<1,Y,4>
         QTD.HOL.HRS<1,QTR>= QTD.HOL.HRS<1,QTR>+ PAY.HOURS<1,Y,5>
         QTD.VAC.HRS<1,QTR>= QTD.VAC.HRS<1,QTR>+ PAY.HOURS<1,Y,6>
         QTD.MSC.HRS<1,QTR>= QTD.MSC.HRS<1,QTR>+ PAY.HOURS<1,Y,7>
         QTD.REGN.HRS<1,QTR>= QTD.REGN.HRS<1,QTR>+ PAY.HOURS<1,Y,8>
         QTD.OTN.HRS<1,QTR>= QTD.OTN.HRS<1,QTR>+ PAY.HOURS<1,Y,9>
         QTD.10.HRS<1,QTR>= QTD.10.HRS<1,QTR>+ PAY.HOURS<1,Y,10>
         QTD.11.HRS<1,QTR>= QTD.11.HRS<1,QTR>+ PAY.HOURS<1,Y,11>
         QTD.REG.ERN<1,QTR>= QTD.REG.ERN<1,QTR>+ PAY.EARNINGS<1,Y,1>
         QTD.OT.ERN<1,QTR>= QTD.OT.ERN<1,QTR>+ PAY.EARNINGS<1,Y,2>
         QTD.DT.ERN<1,QTR>= QTD.DT.ERN<1,QTR>+ PAY.EARNINGS<1,Y,3>
         QTD.SCK.ERN<1,QTR>= QTD.SCK.ERN<1,QTR>+ PAY.EARNINGS<1,Y,4>
         QTD.HOL.ERN<1,QTR>= QTD.HOL.ERN<1,QTR>+ PAY.EARNINGS<1,Y,5>
         QTD.VAC.ERN<1,QTR>= QTD.VAC.ERN<1,QTR>+ PAY.EARNINGS<1,Y,6>
         QTD.MSC.ERN<1,QTR>= QTD.MSC.ERN<1,QTR>+ PAY.EARNINGS<1,Y,7>
         QTD.REGN.ERN<1,QTR>= QTD.REGN.ERN<1,QTR>+ PAY.EARNINGS<1,Y,8>
         QTD.OTN.ERN<1,QTR>= QTD.OTN.ERN<1,QTR>+ PAY.EARNINGS<1,Y,9>
         QTD.10.ERN<1,QTR>= QTD.10.ERN<1,QTR>+ PAY.EARNINGS<1,Y,10>
         QTD.11.ERN<1,QTR>= QTD.11.ERN<1,QTR>+ PAY.EARNINGS<1,Y,11>
         QTD.OTH.INC<1,QTR>= QTD.OTH.INC<1,QTR>+ PAY.OTH.INC<1,Y>
         FOR YY = 1 TO 11
            THR = THR + PAY.HOURS<1,Y,YY>
         NEXT YY
         SICK = PAY.EARNINGS<1,Y,4> + SICK
      NEXT Y
      QTD.GROSS<1,QTR> = QTD.GROSS<1,QTR> + PAY.GROSS - PAY.NON.TAXED
      QTD.NON.TAX<1,QTR> = QTD.NON.TAX<1,QTR> + PAY.NON.TAXED
      QTD.SCK.PAY<1,QTR> = QTD.SCK.PAY<1,QTR> + SICK
      IF EMP.SK.FICA = "N" THEN QTD.SK.NO.FI<1,QTR> = QTD.SK.NO.FI<1,QTR> + SICK
      QTD.FWHT<1,QTR> = QTD.FWHT<1,QTR> + DED.FWT
      QTD.FICA<1,QTR> = QTD.FICA<1,QTR> + DED.FICA
      QTD.LVL1.FICA<1,QTR> = QTD.LVL1.FICA<1,QTR> + DED.LVL1.FICA
      QTD.LVL2.FICA<1,QTR> = QTD.LVL2.FICA<1,QTR> + DED.LVL2.FICA
      QTD.SUI<1,QTR> = QTD.SUI<1,QTR> + DED.SUI
      QTD.SWHT<1,QTR> = QTD.SWHT<1,QTR> + DED.STATE
      QTD.CWHT<1,QTR> = QTD.CWHT<1,QTR> + DED.CITY
      QTD.EIC<1,QTR> = QTD.EIC<1,QTR> + DED.EIC ;*EIC AMOUNT
      QTD.BONDS<1,QTR> = QTD.BONDS<1,QTR> + DED.BONDS
      MSC.CNT = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
      FOR II = 1 TO MSC.CNT
         IF DED.MISC.CODE<1,II> = PETTY.CODE THEN
            MAT PTY.REC = ""
            MATREADU PTY.REC FROM PETTY.CASH, EID ELSE
               RELEASE PETTY.CASH, EID
               GOTO 209
            END
            PTY.BALANCE = PTY.BALANCE - DED.MISC.AMT<1,II>
            PTY.DEDUCT = ""
            IF PTY.BALANCE = 0 THEN
               DELETE PETTY.CASH, EID
               RELEASE PETTY.CASH, EID
            END ELSE
               MATWRITE PTY.REC ON PETTY.CASH, EID
            END
         END
209*
         LOCATE DED.MISC.CODE<1,II> IN QTD.DED<1>,1 SETTING FOND ELSE NULL
         QTD.DED<1,FOND> = DED.MISC.CODE<1,II>
         QTD.AMNT<1,FOND,QTR> = QTD.AMNT<1,FOND,QTR> + DED.MISC.AMT<1,II>
      NEXT II
      MATWRITE QTD.REC ON QTD.YTD,EID
      IF EMP.UNION="" THEN GOTO 215
*******************************
*   MONTHLY UNION FILE
*******************************
      MATREADU MUN FROM MTH.UNION,EID ELSE MAT MUN=""; MUN1=EMP.NO;MUN2=EMP.SOC.SEC
      IF EMP.UNION="" THEN GOTO 215
      FOR X=1 TO PAY.RECORDS
         MUN(2+PTHR.DED.WEEK)=MUN(2+PTHR.DED.WEEK)+PAY.HOURS<1,X,1>;* REG HRS
         MUN13=MUN13+PAY.HOURS<1,X,2>+PAY.HOURS<1,X,3> ;*OVERTIME AND DOUBLETIME HOURS
      NEXT X
      MUN14=MUN14+UNIONDUES
      IF SECONDKEY # 1 THEN
         MUN15=MUN15
         MUN16=MUN16
         GOTO 99
      END
      MUN16=EMP.UNION
99    MATWRITE MUN ON MTH.UNION,EID
      WRITEV PTHR.END.DATE ON D.MTH.UNION,ID[1,3]:"WEEKS",PTHR.DED.WEEK
***********************
215* MPAYREG FILE UPDATE FOR MONTHLY O/T D/T REPORT
*******************************
      FOR X=1 TO PAY.RECORDS
         IF PAY.DEPT<1,X> = "" THEN GOTO 216
         IF PAY.DIV<1,X> = "" THEN GOTO 216
         IF PAY.COST.CNTR<1,X> = "" THEN GOTO 216
         MPKEY = CONO : EMP.NO : PAY.DIV<1,X> : PAY.DEPT<1,X> :"!": PAY.COST.CNTR<1,X>
         MATREADU MPR.REC FROM MPAYREG,MPKEY ELSE MAT MPR.REC = ""
         FOR Y=1 TO 11
            MPR.GROSS=MPR.GROSS+PAY.EARNINGS<1,X,Y>;* EARNINGS
         NEXT Y
*  MPR.GROSS = MPR.GROSS + PAY.OTH.INC<1,X>
         MPR.REG.HRS = MPR.REG.HRS + PAY.HOURS<1,X,1>
         MPR.REG.ERN = MPR.REG.ERN + PAY.EARNINGS<1,X,1>
         MPR.REG.HRS = MPR.REG.HRS + PAY.HOURS<1,X,8>
         MPR.REG.ERN = MPR.REG.ERN + PAY.EARNINGS<1,X,8>
         MPR.REG.HRS = MPR.REG.HRS + PAY.HOURS<1,X,10>
         MPR.REG.ERN = MPR.REG.ERN + PAY.EARNINGS<1,X,10>
         MPR.OT.HRS = MPR.OT.HRS + PAY.HOURS<1,X,2>
         MPR.OT.ERN = MPR.OT.ERN + PAY.EARNINGS<1,X,2>
         MPR.OT.HRS = MPR.OT.HRS + PAY.HOURS<1,X,9>
         MPR.OT.ERN = MPR.OT.ERN + PAY.EARNINGS<1,X,9>
         MPR.OT.HRS = MPR.OT.HRS + PAY.HOURS<1,X,11>
         MPR.OT.ERN = MPR.OT.ERN + PAY.EARNINGS<1,X,11>
         MPR.DT.HRS = MPR.DT.HRS + PAY.HOURS<1,X,3>
         MPR.DT.ERN = MPR.DT.ERN + PAY.EARNINGS<1,X,3>
         MATWRITE MPR.REC ON MPAYREG,MPKEY
216   NEXT X
*******************************
218*   MONTHLY DISTRIBUTION SUMMARY FILE
*********************
      BLK = 1
      FOR II = 1 TO PAY.RECORDS
         MID = CONO : EMP.NO : PAY.DIV<1,II> : PAY.DEPT<1,II> :"!": PAY.COST.CNTR<1,II>
         MATREADU MDS.REC FROM MDS, MID ELSE MAT MDS.REC = ""
         IF BLK AND EMP.DIV = PAY.DIV<1,II> AND EMP.DEPT = PAY.DEPT<1,II> AND EMP.COST.CNTR = PAY.COST.CNTR<1,II> THEN
            MDS.FWHT = MDS.FWHT + DED.FWT
            MDS.FICA = MDS.FICA + DED.FICA
            MDS.LVL1.FICA = MDS.LVL1.FICA + DED.LVL1.FICA
            MDS.LVL2.FICA = MDS.LVL2.FICA + DED.LVL2.FICA
            MDS.DIAB = MDS.DIAB + DED.SUI
            MDS.SWHT = MDS.SWHT + DED.STATE
            MDS.CWHT = MDS.CWHT + DED.CITY
            MDS.BONDS = MDS.BONDS + DED.BONDS
            MDS.EIC = MDS.EIC + DED.EIC
            MSC.CNT = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
            FOR Y = 1 TO MSC.CNT
               LOCATE DED.MISC.CODE<1,Y> IN MDS.DED.CODE<1>,1 SETTING FND ELSE NULL
               MDS.DED.CODE<1,FND> = DED.MISC.CODE<1,Y>
               MDS.DED.AMT<1,FND> = MDS.DED.AMT<1,FND> + DED.MISC.AMT<1,Y>
            NEXT Y
            BLK = 0
         END
         IF PAY.OTH.INC<1,II>+0 # 0 THEN
            LOCATE PAY.OTH.CODE<1,II> IN MDS.OINC.CODE<1>,1 SETTING FND ELSE NULL
            MDS.OINC.CODE<1,FND> = PAY.OTH.CODE<1,II>
            MDS.OINC.AMT<1,FND> = MDS.OINC.AMT<1,FND> + PAY.OTH.INC<1,II>
*   MDS.GROSS = MDS.GROSS + PAY.OTH.INC<1,II>
            MDS.TOT.OINC = MDS.TOT.OINC + PAY.OTH.INC<1,II>
         END
         FOR Y = 1 TO 11
            MDS.EARN<1,Y> = MDS.EARN<1,Y> + PAY.EARNINGS<1,II,Y>
            MDS.GROSS = MDS.GROSS + PAY.EARNINGS<1,II,Y>
         NEXT Y
         MATWRITE MDS.REC ON MDS,MID
      NEXT II
      IF BLK THEN
         MID = CONO : EMP.NO : EMP.DIV : EMP.DEPT :"!": EMP.COST.CNTR
         MATREADU MDS.REC FROM MDS, MID ELSE MAT MDS.REC = ""
         MDS.FWHT = MDS.FWHT + DED.FWT
         MDS.FICA = MDS.FICA + DED.FICA
         MDS.LVL1.FICA = MDS.LVL1.FICA + DED.LVL1.FICA
         MDS.LVL2.FICA = MDS.LVL2.FICA + DED.LVL2.FICA
         MDS.DIAB = MDS.DIAB + DED.SUI
         MDS.SWHT = MDS.SWHT + DED.STATE
         MDS.CWHT = MDS.CWHT + DED.CITY
         MDS.BONDS = MDS.BONDS + DED.BONDS
         MDS.EIC = MDS.EIC + DED.EIC
         MSC.CNT = COUNT(DED.MISC.CODE,VM) + (DED.MISC.CODE # "")
         FOR Y = 1 TO MSC.CNT
            LOCATE DED.MISC.CODE<1,Y> IN MDS.DED.CODE<1>,1 SETTING FND ELSE NULL
            MDS.DED.CODE<1,FND> = DED.MISC.CODE<1,Y>
            MDS.DED.AMT<1,FND> = MDS.DED.AMT<1,FND> + DED.MISC.AMT<1,Y>
         NEXT Y
         MATWRITE MDS.REC ON MDS,MID
      END
******************
*   QUARTERLY HISTORY FILE UPDATE
******************
      READV COUNT FROM DQHR,CONO:"COUNTER",1 ELSE COUNT=0
      COUNT=COUNT+1
      WRITE COUNT ON DQHR,CONO:"COUNTER"
      MAT QHR.REC = ""
      QHR.CONO = CONO
      QHR.EMP = EMP.NO
      QHR.PERIOD = PTHR.PAY.PER
      QHR.GROSS = PAY.GROSS
      QHR.FWHT = DED.FWT
      QHR.FICA = DED.FICA
      QHR.LVL1.FICA = DED.LVL1.FICA
      QHR.LVL2.FICA = DED.LVL2.FICA
      QHR.SUI = DED.SUI
      QHR.STATE = DED.STATE
      QHR.CITY = DED.CITY
      QHR.CHECK = PAY.CHECK.NO
      QHR.MANUAL = PAY.MANUAL
      FOR Y = 1 TO PAY.RECORDS
         QHR.REG.HRS = QHR.REG.HRS+PAY.HOURS<1,Y,1>;*REG HOURS
         QHR.REG.HRS = QHR.REG.HRS+PAY.HOURS<1,Y,8>;*REG NON HOURS
         QHR.REG.HRS = QHR.REG.HRS+PAY.HOURS<1,Y,10>;*REG SUP HOURS
         QHR.OTDT.HRS=QHR.OTDT.HRS+PAY.HOURS<1,Y,2>+PAY.HOURS<1,Y,3>;*OT HOURS + DBL TIME
         QHR.OTDT.HRS = QHR.OTDT.HRS + PAY.HOURS<1,Y,9>;* OT NON HOURS
         QHR.OTDT.HRS = QHR.OTDT.HRS + PAY.HOURS<1,Y,11>;* OT SUP HOURS
         QHR.MSC.HRS = QHR.MSC.HRS+PAY.HOURS<1,Y,4>+PAY.HOURS<1,Y,5>+PAY.HOURS<1,Y,6>+PAY.HOURS<1,Y,7>
         FOR YY = 1 TO 11
            QHR.EARN = QHR.EARN + PAY.EARNINGS<1,Y,YY>
         NEXT YY
      NEXT Y
      QHR.EIC = DED.EIC
      QHR.PER.END = PER.END
      QHR.MISC.CODE = DED.MISC.CODE
      QHR.MISC.AMT = DED.MISC.AMT
      IF DED.BONDS # "" THEN
         QHR.MISC.CODE = INSERT(QHR.MISC.CODE,1,1,0,"BD")
         QHR.MISC.AMT = INSERT(QHR.MISC.AMT,1,1,0,DED.BONDS)
      END
      MATWRITE QHR.REC ON QHR, QID:PTHR.PAY.PER:COUNT
      PAY.STATUS = "UPDATE"
      MATWRITE PAY.REC ON PAYREG, ID
      GO 100
*****
500* WRITE WARNING ON NONUPDATES
*****
      IF BADS=0 THEN STOP ELSE
         DOWN=CHAR(10)
         MSG="*** WARNING***":DOWN:BADS:" EMPLOYEE RECORDS WERE NOT UPDATED."
*   MSG=MSG:DOWN:"A LISTING OF THOSE EMPLOYEE NUMBERS AND WHY THEY WERE NOT UPDATED IS BEING SENT TO THE LINE PRINTER."
*   MSG=MSG:DOWN:"*** PLEASE INVESTIGATE !!! ***"
         MSG1="A LISTING OF THOSE EMPLOYEE NUMBERS AND WHY THEY WERE NOT UPDATED IS BEING SENT TO THE PRINTER."
         PRINTER ON
         HEAD1="'LLT'":"          DISTRIBUTION UPDATE ERROR REPORT":"                         PAGE 'PPLL'"
         HEADING HEAD1
         PRINT MSG
         PRINT;PRINT "EMPLOYEE NO"
         FOR Y=1 TO BADS
            PRINT BAD(Y)
         NEXT Y
         PRINT CS
         PRINTER OFF
*   PRINT CS:@(0,5):MSG:;INPUT R
         PRINT CS:@(0,5):MSG
         PRINT @(0,7):MSG1:;INPUT R
      END
************
      END
