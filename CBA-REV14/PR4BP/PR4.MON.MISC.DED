*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PRSBP
* PROGRAM     - MON.MISC.DED
* BY          - WALID YAMOUT , C.B.A
* DATE        - 06/11/85
* DESCRIPTION -
*ENDDOC
*********************************************************************
*
*--- INSERT FILES EQUETES
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>FISCAL
*COPY>PRS.CPYLIB>MDS
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>CPYLIB>CHAR
********
      PROMPT ""
      OPEN "","COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN COMPANY":;INPUT REPLY,1:;STOP
      OPEN "","EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE":;INPUT REPLY,1:;STOP
      OPEN "","CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL":;INPUT REPLY,1:;STOP
      OPEN "","DEPARTMENT" TO DEPARTMENT ELSE PRINT "CAN'T OPEN DEPARTMENT":;INPUT REPLY,1:;STOP
      OPEN "","MISCDED" TO MISCDED ELSE PRINT "CAN'T OPEN MISCDED":;INPUT REPLY,1:;STOP
      OPEN "","MDS" TO MDS ELSE PRINT "CAN'T OPEN MDS":;INPUT REPLY,1:;STOP
      OPEN "","QTD-YTD" TO QTD.YTD ELSE PRINT "CAN'T OPEN QTD-YTD":;INPUT REPLY,1:;STOP
      PROCREAD X ELSE PRINT "MUST COME FROM PROC  -  HIT RETURN":;INPUT REPLY,1:;STOP
      CONO=X<1>
      MATREAD COMP.REC FROM COMPANY, CONO ELSE PRINT "COMPANY IS NOT ON FILE":; INPUT REPLY,1:; STOP
      MATREAD FISCAL.REC FROM CONTROL, CONO:"PRFISCAL" ELSE PRINT "CONTROL PRFISCAL IS NOT ON FILE":; INPUT REPLY,1:; STOP
      MON = FR.CURR.PER[5,2] : "/" : FR.CURR.PER[1,4]
*****
** HEADING ROUTINE
      SP1 = SPACE(1)
      SP3 = SPACE(3)
      POS1 = 51 - INT(LEN(CO.NAME)/2)
      POS2 = 26 + INT(LEN(CO.NAME)/2)
      HEAD1 = "DATE - " : OCONV(DATE(), "D2/") "L#8"
      HEAD1 = HEAD1 : SPACE(POS1) : CO.NAME "L#30" : SPACE(POS2) : "PAGE - "
      HEAD = SPACE(27):"M O N T H L Y   M I S C E L L A N E O U S   D E D U C T I O N   R E G I S T E R'L'"
      HEAD = HEAD:SPACE(52):"F O R   P E R I O D   ":MON:"'LL'"
      HEAD3 = "EMP#   EMPLOYEE NAME                   SOC SEC #    DEPT     DED M-T-D    DED Y-T-D    EMP %"
      HEAD4 = "----   ------------------------------ -----------   -----   ----------   ----------   ------"
      PRINTER ON
*****
      FIRST = 0
      LINE.COUNT = 99
      PAGE.NO = 0
      TOT.AMT = 0
      TOT.DEPT = 0; TOT.DEPT.QTD = 0; TOT.DEPT.YTD = 0
      TOTAL = 0; TOTAL.QTD = 0; TOTAL.YTD = 0
300*
      READNEXT ID,CD ELSE
         IF FIRST THEN
            GOSUB 2000
            GOSUB 2500
            GOSUB 3000
         END
         GOTO 999999
      END
      EMP.NO = ID[4,4]
      IF CONO#ID[1,3] THEN GO 300
      MATREAD MDS.REC FROM MDS, ID ELSE GOTO 300
      MATREAD EMP.REC FROM EMPLOYEE, CONO:EMP.NO ELSE
         MAT EMP.REC = ""; EMP.LAST.NAME = "NOT ON FILE"
      END
      IF FIRST = 0 THEN
         HOLD.EMP = EMP.NO
         HOLD.DED = MDS.DED.CODE<1,CD>
         HOLD.DP = EMP.DEPT
         NAME = EMP.LAST.NAME :", ": EMP.FRST.NAME
         HOLD.SOC.SEC = EMP.SOC.SEC
         HOLD.MSC.CODE = EMP.MSC.CODE
         HOLD.MSC.AMT = EMP.MSC.AMT
         GOSUB 8000
         FIRST = 1
      END
      BEGIN CASE
      CASE HOLD.DED # MDS.DED.CODE<1,CD>
         GOSUB 2000
         GOSUB 2500
         GOSUB 3000
         GOSUB 8000
      CASE HOLD.DP # EMP.DEPT
         GOSUB 2000
         GOSUB 2500
      CASE HOLD.EMP # EMP.NO
         GOSUB 2000
      END CASE
      TOT.AMT = TOT.AMT + MDS.DED.AMT<1,CD>
      GOTO 300
******
2000*
      IF LINE.COUNT > 53 THEN GOSUB 8100
      MATREAD QTD.REC FROM QTD.YTD, CONO:HOLD.EMP ELSE MAT QTD.REC =0
      LOCATE HOLD.DED IN QTD.DED<1> SETTING FND ELSE FND = 0
      IF FND THEN YTD.DED =SUM(QTD.AMNT<1,FND>) ELSE YTD.DED=0
      IF YTD.DED = '' THEN YTD.DED = 0
      IF MDED.TYPE = '%' THEN
         LOCATE HOLD.DED IN HOLD.MSC.CODE<1> SETTING FOUND ELSE FOUND = 0
         IF FOUND THEN HOLD.PCT = HOLD.MSC.AMT<1,FOUND> ELSE HOLD.PCT = ''
      END ELSE
         HOLD.PCT = ''
      END
      PRINT HOLD.EMP "R#4" :SP3: NAME "L#30":SP1:HOLD.SOC.SEC"R#11":SP3: HOLD.DP "L#5" :SP3: OCONV(TOT.AMT,"MD2")"R#10":SP3:OCONV(YTD.DED,"MD2")"R#10":SP3:OCONV(HOLD.PCT,"MD2")"R#6"
      LINE.COUNT = LINE.COUNT + 1
      TOT.DEPT = TOT.DEPT + TOT.AMT
      TOT.DEPT.YTD = TOT.DEPT.YTD + YTD.DED
      NAME = EMP.LAST.NAME : ", " : EMP.FRST.NAME
      HOLD.EMP = EMP.NO
      HOLD.SOC.SEC = EMP.SOC.SEC
      HOLD.MSC.CODE = EMP.MSC.CODE
      HOLD.MSC.AMT = EMP.MSC.AMT
      TOT.AMT = 0
      RETURN
2500*
      MATREAD DEPT.REC FROM DEPARTMENT, CONO:HOLD.DP ELSE
         MAT DEPT.REC = ""; DEPT.DESC = "NOT ON FILE"
      END
      IF LINE.COUNT > 53 THEN GOSUB 8100
      PRINT SPACE(60):"----------":SP3:"----------"
      TEMP.NAME = "DEPARTMENT - ": DEPT.DESC
      PRINT TEMP.NAME "R#57" :" - ": OCONV(TOT.DEPT,"MD2")"R#10":SP3:OCONV(TOT.DEPT.YTD,"MD2")"R#10"
      PRINT
      LINE.COUNT = LINE.COUNT + 3
      TOTAL = TOTAL + TOT.DEPT
      TOTAL.YTD = TOTAL.YTD + TOT.DEPT.YTD
      TOT.DEPT = 0; TOT.DEPT.YTD = 0
      HOLD.DP = EMP.DEPT
      RETURN
3000*
      PRINT SPACE(60):"----------":SP3:"----------"
      PRINT SPACE(42):"DEDUCTION TOTAL - ": OCONV(TOTAL,"MD2")"R#10":SP3:OCONV(TOTAL.YTD,"MD2")"R#10"
      TOTAL = 0; TOTAL.YTD = 0
      HOLD.DED = MDS.DED.CODE<1,CD>
      RETURN
8000*
      MATREAD MDED.REC FROM MISCDED, CONO:HOLD.DED ELSE
         MAT MDED.REC = ""; MDED.SCRN.DESC = "NOT ON FILE"
      END
      HEAD2 = "DEDUCTION # " : HOLD.DED "L#2" :" - ": MDED.SCRN.DESC "L#20" :SP3: "G/L " : MDED.ACCT CO.ACCT.MASK :"'L'"
8100*
      PAGE.NO = PAGE.NO + 1
      HEADING HEAD1 : PAGE.NO "R#3" :"'L'":HEAD:HEAD2
      PRINT HEAD3
      PRINT HEAD4
      PRINT
      LINE.COUNT = 10
      RETURN
999999*
      PRINTER OFF
      END
