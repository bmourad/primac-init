*COPY>CPYLIB>COM1
*COPY>PRS.CPYLIB>COM.PRS.FILE.VARS
*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - PRSBP
* PROGRAM     - MISCDED.MAINT
* BY          - CAROL SAGE, CBA
* DATE        - 03/16/88
* DESCRIPTION - This program maintains the MISCDED file.
* MOD         - RWW 8.22.90, TASK 15113 add 401k calc flag
* MOD 11/23/93 (LMR) THE ABOVE WAS A VIKING MOD - NOT BASE SYSTEM (REMOVED)
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COA
*COPY>PRS.CPYLIB>PRS.FILE.VARS
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>INVEST.FUND.CODE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN 'CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE IS MISSING';GOTO 93000
      OPEN 'COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE IS MISSING';GOTO 93000
      OPEN 'PRS.SCREENS' TO M.SCREENS ELSE ERRMSG='PRS.SCREENS FILE IS MISSING';GOTO 93000
      OPEN 'PR4.SCREENS' TO SECOND.SCREENS ELSE ERRMSG='PR4.SCREENS FILE IS MISSING';GOTO 93000
      OPEN 'MISCDED' TO MISCDED ELSE ERRMSG='MISCDED FILE IS MISSING';GOTO 93000
      OPEN 'COA' TO COA ELSE ERRMSG='COA FILE IS MISSING'; GOTO 93000
      OPEN 'INVEST.FUND.CODE' TO INVEST.FUND.CODE ELSE ERRMSG='INVEST.FUND.CODE FILE IS MISSING'; GOTO 93000
*
*---- GET COMPANY NUMBER
*
      CONO='';CALL GET.CONO(CONO,MAT COMP.REC)
      IF CONO='END' THEN GOTO 99999
      IN.ACCT.LEN=LEN(CO.ACCT.PIC)
      READ DC.REC FROM CONTROL, CONO : "401K" ELSE DC.REC=''
      K401 = ""
*
*---- INITIALIZATION
*
      MAT EDIT.COM.DRIVER=""
      ECD.SCRN.CNT=2
      ECD.SCRN.NAME<1>='MISCDED.MAINT'
      ECD.SCRN.FLAG<1>=2
      ECD.SCRN.NAME<2>='MISCDED.MAINT2'
      ECD.ACTION=1;CALL SCRN.EDIT
      ECD.SCRN.NO=1
*
*---- MAIN PROCESSING
100 *
      MAT SCV.REC=""
      MAT MDED.REC=""
      ECD.ACTION=2;CALL SCRN.EDIT
      ECD.VALDAT.CODE='4'
      ECD.PREFIX.ID=CONO
      ECD.VALDAT.FILE=MISCDED
      ECD.NUM=1;ECD.ACTION=4;CALL SCRN.EDIT
      K401 = 1
      LOCATE ECD.RET.VALUE IN DC.REC,1 SETTING XX ELSE K401 = 0
*
      BEGIN CASE
      CASE ECD.RET.VALUE='END'
         GOTO 99999
      CASE ECD.RET.VALUE='B' OR ECD.RET.VALUE='TM' OR ECD.RET.VALUE='EI' OR ECD.RET.VALUE='CT' OR ECD.RET.VALUE='BD'
         GOTO 199
      CASE ECD.RET.VALUE # "" AND ECD.VALDAT.ITEM=''
         DED.NO=ECD.RET.VALUE
         FOR X=1 TO 7
*           ON X GOSUB 1000,2000,3000,4000,5000,6000,7000
            ON X GOSUB 1000,2000,3000,4000,5000,6000
            IF ECD.RET.VALUE='END' THEN GOTO 100
         NEXT X
         IF MDED.CONT.PLAN = 'Y' THEN
            NEW = 1
            ECD.SCRN.NO = 2; ECD.ACTION = 2; CALL SCRN.EDIT
            CALL PR4.MISCDED.MAINT2(CONO,NEW,DED.NO,MAT MDED.REC)
            ECD.SCRN.NO = 1; ECD.ACTION = 2; CALL SCRN.EDIT
            ECD.ACTION = 3; CALL SCRN.EDIT
         END
      CASE ECD.RET.VALUE#''
         DED.NO=ECD.RET.VALUE
         FOR I=1 TO MDED.REC.SIZE
            MDED.REC(I)=ECD.VALDAT.ITEM<I>
         NEXT I
         GOSUB 10000
      CASE 1
199 *
         ERRMSG=BEL:"** INVALID DEDUCTION CODE **"
         GOSUB 91000;GOTO 100
      END CASE
*
*---- ENTER OPTIONS
500 *
      IF MDED.CONT.PLAN = 'Y' THEN
         ECD.NUM=22;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=''
      END ELSE
         ECD.NUM=21;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=''
      END
      ECD.ACTION=4;CALL SCRN.EDIT
      OPTION=ECD.RET.VALUE
      BEGIN CASE
      CASE OPTION='E' OR OPTION='END'
         GOTO 100
      CASE NUM(OPTION)
*        ON OPTION GOSUB 1000,2000,3000,4000,5000,6000,7000
         ON OPTION GOSUB 1000,2000,3000,4000,5000,6000
      CASE OPTION='N'
         NEW = 0
         ECD.SCRN.NO = 2; ECD.ACTION = 2; CALL SCRN.EDIT
         CALL PR4.MISCDED.MAINT2(CONO,NEW,DED.NO,MAT MDED.REC)
         ECD.SCRN.NO = 1; ECD.ACTION = 2; CALL SCRN.EDIT
         ECD.ACTION = 3; CALL SCRN.EDIT
      CASE OPTION='D'
         DELETE MISCDED,CONO:DED.NO
         GOTO 100
      CASE OPTION='F'
         MATWRITE MDED.REC ON MISCDED,CONO:DED.NO
         GOTO 100
      END CASE
      GOTO 500
*
*---- SCREEN DESCRIPTION
1000 *
      ECD.NUM=2;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE#'END' THEN MDED.SCRN.DESC=ECD.RET.VALUE
      RETURN
*
*---- HEADING DESCRIPTION
2000 *
      ECD.NUM=3;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE#'END' THEN MDED.HEAD.DESC=ECD.RET.VALUE
      RETURN
*
*---- ACCOUNT#
3000 *
      ECD.NUM=4
      SCV.REC(ECD.NUM)<ECD.SCRN.NO>='';ECD.ACTION=5;CALL SCRN.EDIT  
      ECD.MAXL=IN.ACCT.LEN  
      ECD.VALDAT.CODE='2';ECD.VALDAT.FILE=COA;ECD.PREFIX.ID=CONO  
      SCV.REC(ECD.NUM)<ECD.SCRN.NO>=MDED.ACCT  
      ECD.PATRN=CO.ACCT.MATCH  
      ECD.ACTION=4;CALL SCRN.EDIT  
      IF ECD.RET.VALUE # 'END' THEN  
         MDED.ACCT=ECD.RET.VALUE
         FOR L=1 TO COA.REC.SIZE;COA.REC(L)=ECD.VALDAT.ITEM<L>;NEXT L
         SCV.REC(ECD.NUM)<ECD.SCRN.NO>=MDED.ACCT CO.ACCT.MASK;ECD.ACTION=5;CALL SCRN.EDIT
         ECD.NUM=5;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=COA.DESC;ECD.ACTION=5;CALL SCRN.EDIT
      END  
      RETURN  
*
*---- TYPE ($/%/H)
4000 *
      ECD.NUM=6;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE#'END' THEN MDED.TYPE=ECD.RET.VALUE
      IF MDED.TYPE # "%" AND MDED.CONT.PLAN = "Y" THEN GOSUB 6000
      RETURN
*
*---- TAXABLE (B/A)
5000 *
      IF MDED.USAGE.FLAG = "Y" THEN
         ERRMSG=BEL:"** CANNOT CHANGE TAX CRITERIA ONCE CODE EXISTS IN EMPLOYEE RECORD **"
         GOSUB 91000
         GOTO 5999
      END
      ECD.NUM=7;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 5999
      MDED.TAXABLE=ECD.RET.VALUE
*
      IF MDED.TAXABLE='A' THEN
         MDED.NTXFED=''
         MDED.NTXFICA=''
         MDED.NTXSTATE=''
         MDED.NTXCITY=''
         ECD.NUM=8;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=""
         ECD.ACTION=5;CALL SCRN.EDIT
         ECD.NUM=9;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=""
         ECD.ACTION=5;CALL SCRN.EDIT
         ECD.NUM=10;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=""
         ECD.ACTION=5;CALL SCRN.EDIT
         ECD.NUM=11;SCV.REC(ECD.NUM)<ECD.SCRN.NO>=""
         ECD.ACTION=5;CALL SCRN.EDIT
         GOTO 5999
      END
*
      ECD.NUM=8;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 5999
      MDED.NTXFED=ECD.RET.VALUE
*
      ECD.NUM=9;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 5999
      MDED.NTXFICA=ECD.RET.VALUE
*
      ECD.NUM=10;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 5999
      MDED.NTXSTATE=ECD.RET.VALUE
*
      ECD.NUM=11;ECD.ACTION=4;CALL SCRN.EDIT
      IF ECD.RET.VALUE='END' THEN GOTO 5999
      MDED.NTXCITY=ECD.RET.VALUE
5999 *
      RETURN
*
*---- DC PLAN
6000 *
      ECD.NUM=12
      BEGIN CASE
      CASE MDED.TYPE # "%"
         MDED.CONT.PLAN = 'N'
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = MDED.CONT.PLAN
         ECD.ACTION = 5
         CALL SCRN.EDIT
      CASE 1
         ECD.ACTION=4;CALL SCRN.EDIT
         IF ECD.RET.VALUE # 'END' THEN
            IF ECD.RET.VALUE = 'Y' THEN MDED.CONT.PLAN = ECD.RET.VALUE ELSE MDED.CONT.PLAN = 'N'
         END
      END CASE
      IF MDED.CONT.PLAN = "N" THEN
         MDED.BT.MAX = ""
         MDED.VEST.MON = ""
         MDED.DED.MAX = ""
         MDED.INVEST = ""
      END
      RETURN
*
* DEDUCT FROM GROSS BEFORE 401K CALC??
*
7000 *
*
      ECD.NUM=23
      IF MDED.CONT.PLAN = 'Y' OR K401 THEN 
         MDED.401K.CALC = "N"
         SCV.REC(ECD.NUM)<ECD.SCRN.NO> = "N"
         ECD.ACTION = 5
         CALL SCRN.EDIT
         RETURN
      END
      ECD.ACTION = 4 ; CALL SCRN.EDIT
      MDED.401K.CALC = ECD.RET.VALUE
      SCV.REC(ECD.NUM)<ECD.SCRN.NO>=ECD.RET.VALUE
      ECD.ACTION = 5
      CALL SCRN.EDIT
*
      RETURN
*
*---- LOAD SCV.REC
10000 *
      MATREAD COA.REC FROM COA,CONO:MDED.ACCT ELSE COA.DESC=''
      SCV.REC(1)<ECD.SCRN.NO>=DED.NO
      SCV.REC(2)<ECD.SCRN.NO>=MDED.SCRN.DESC
      SCV.REC(3)<ECD.SCRN.NO>=MDED.HEAD.DESC
      SCV.REC(4)<ECD.SCRN.NO>=MDED.ACCT CO.ACCT.MASK
      SCV.REC(5)<ECD.SCRN.NO>=COA.DESC
      SCV.REC(6)<ECD.SCRN.NO>=MDED.TYPE
      SCV.REC(7)<ECD.SCRN.NO>=MDED.TAXABLE
      SCV.REC(8)<ECD.SCRN.NO>=MDED.NTXFED
      SCV.REC(9)<ECD.SCRN.NO>=MDED.NTXFICA
      SCV.REC(10)<ECD.SCRN.NO>=MDED.NTXSTATE
      SCV.REC(11)<ECD.SCRN.NO>=MDED.NTXCITY
      SCV.REC(12)<ECD.SCRN.NO>=MDED.CONT.PLAN
      SCV.REC(23)<ECD.SCRN.NO>=MDED.401K.CALC
      ECD.ACTION=3;CALL SCRN.EDIT
      RETURN
*
*---- CALLS FOR SYSCOM
91000 *
      ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 *
      ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999 *
      PRINT CS:
      END
