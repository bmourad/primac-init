********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - POSBP
* PROGRAM     - MPO.PRINT
* BY          - JIHAD YAMOUT ,C.B.A
* DATE        - 12/04/84
* DESCRIPTION - PRINT MISC PO .
* MOD         - 2.23.90 GG  ENLARGE UNIT COST FIELD.
* MOD         - RWW 8.20.90 CSF 13971 - ENSURE MD2 IF OVER 14MILL OVERFLOW
* MOD         - LMR 12/6/93 - THIS VERSION PRINTS 80 COLS WIDE.
*               THE 82 COL WIDE VERSION IS IN POSBP.
* TASK
*    19542  LLH 08/30/95 - PRINT DUE DATE FOR EACH LINE ITEM
*
*T22081 stefanie 08/11/1997 * Check to see if using laser printer and
*                             use laser settings if they are.
*T22579 lanny 02/09/1998 * Correct align on 1st PO after MASK & multi
*                          MASK form advance.
*T22743 rik 04/14/1998 * Only 1st PO was printing in correct allignment.
*T25941 ajibaly 06/28/2002 * Allow emailing of POs to vendor
*T111809 UniPrice Changes By Naresh on 11/18/09
********************************************************************
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INVENTORY
*COPY>POS.CPYLIB>MISC.PO
*COPY>ICS.CPYLIB>WAREHOUSE
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>PMC_REPORTS ;* T25941
*COPY>PMC.CPYLIB>SECURITY ;* T25941
   ;*
   ;* INITIALIZATION
   ;*
   MODEFLAG="A"
   PROMPT""
   ENDFLG=0
   MPO.KEYS = ""
   ;*
   ;* T25941 v
   ;* List to contain print/mailing info:
   ;* value loc 1 specifies print/mail/or both
   ;* value loc 2 specifies email addresses (comma seperated)
   ;* value loc 3 specifies background form image to use
   PM.LIST = ""
   USERNO = "USERNO"
   CALL SYSVARS.SUB(USERNO)
   LOGNAME = "LOGNAME"
   CALL SYSVARS.SUB(LOGNAME)
   RESTORE.PRINTER.CMD = "SETPTR ":GETPTR(0):",BRIEF"
   NEW.PRINTER.CMD     = "SETPTR ,,,,,3,BRIEF,NOMESSAGE,NOHEAD,BANNER "
   ;* T25941 ^
   ;*
   ;* SET UP FOR SYSCOM
   ;*
   SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   ;*
   ;* OPEN FILES
   ;*
   OPEN "","INVENTORY" TO INVENTORY ELSE
      ERRMSG = " INVENTORY FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","VEND" TO VEND ELSE
      ERRMSG = "VEND FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","MISC.PO" TO MISC.PO ELSE
      ERRMSG = "MISC.PO FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","WAREHOUSE" TO WAREHOUSE ELSE
      ERRMSG = "WAREHOUSE FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "COMPANY FILE IS MISSING" ; GOTO 93000
   END
   ;*
   ;* T25941 v
   HOLD.FILE.NAME = "_HOLD_"
   OPEN "",HOLD.FILE.NAME TO HOLD.FILE ELSE
      ERRMSG = HOLD.FILE.NAME:" FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","PMC_REPORTS" TO PMC_REPORTS ELSE
      ERRMSG = "PMC_REPORTS FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","SECURITY" TO SECURITY ELSE
      ERRMSG = "SECURITY FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING" ; GOTO 93000
   END
   ;*
   ;* Read the buffer and get the report number
   ;*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST BE RUN FROM A PROC"
      GOSUB 91000; RETURN
   END
   RPT.NO = BUFFER<2>
   MAIL.FLAG = BUFFER<8>
   MATREAD PRPT.REC FROM PMC_REPORTS, RPT.NO ELSE
      ERRMSG = "Cannot read the PMC_REPORTS record for this report. "
      ERRMSG:= "Make sure that Report Screen flag is 'R'."
      GOSUB 91000; RETURN
   END
   ;* T25941 ^
   ;*
   ;* GET CONO
   ;*
   READNEXT PON,PON2 ELSE GOTO 900
   CONO=PON[1,3]
   MATREAD COMP.REC FROM COMPANY , CONO ELSE
      ERRMSG = "COMPANY # (":CONO:") IS MISSING" ; GOTO 93000
   END
   ;*
   ;* T25941 v
   ;* Read the security record and get the user's email
   ;*
   USER.EMAIL.ADDR = ''
   MATREAD SEC.REC FROM SECURITY, CONO:UPCASE(LOGNAME) THEN
      IF SEC.EMAIL # '' THEN USER.EMAIL.ADDR = ',':SEC.EMAIL
   END
   ;* T25941 ^
   ;*
   ;* T22081 v
   FIRST.TIME = ''
   LASER.FLAG = ''
   IF OCONV(CONO:OCONV(@LOGNAME,'MCU'):'!LASER','TSECURITY;X;0;0') # '' THEN
      LASER.FLAG = 1
   END
   ;* T22081 ^
   ALIGN.PRT=0
   FOR EE = 3 TO 20
      PRINT @(0,EE):CL:
   NEXT EE
*
20*
*
   ;* T22081 v
   IF LASER.FLAG = '' AND MAIL.FLAG = '' THEN
      ;* T22081 ^    
      PRINT @(32,6):"   ":
      PRINT @(0,4): "LOAD PURCHASE ORDER FORMS IN PRINTER AND ALIGN BY EYE."
      PRINT @(0,6): "PRINT MASK OR ORDERS (M/O/END) ?":
      INPUT RESPONSE
      ;* T22081 v
   END ELSE
      RESPONSE = "O"
   END
   ;* T22081 ^
   IF RESPONSE = "END" THEN GOTO 99999
   IF RESPONSE # "O" AND RESPONSE # "M" THEN
      PRINT
      GOTO 20
   END
   IF RESPONSE # "O" THEN
      PRINTER ON
      ;*T22579 v
      IF NOT(ALIGN.PRT) THEN
         PRINT CHAR(12)
      END ELSE
         PRINT
      END
      ALIGN.PRT=1
      ;*T22579 ^
      PRINT "X"
      PRINT
      PRINT
      PRINT SPACE(71):"PAGE XXXX"
      PRINT SPACE(21):"***XXXXXXX: XXXX XX X XXXXXXXX XXXXX XXXXXXX."
      PRINT SPACE(33):"XX XXX XXXXXXXXX XXX XXXXX!!!"
      PRINT
      PRINT
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT
      PRINT
      PRINT
      PRINT "MM/DD/YY  99999999  XXXXXXXXXXXXXXX"
      PRINT;PRINT
      FOR X=1 TO 5
         PRINT "9,999,999  XXXXXXXXXXXXXXX":SPACE(32):"9999999.99 99999999.99"
         PRINT
         PRINT SPACE(11):STR("X",41)
         PRINT SPACE(11):STR("X",41)
         PRINT SPACE(11):STR("X",41)
         PRINT
      NEXT X
      PRINT;PRINT;PRINT;PRINT
      PRINT SPACE(17):STR("X",42):SPACE(11):"9999999.99"
      PRINTER CLOSE
      PRINTER OFF
      GOTO 20
   END
   ;*
   ;*** BEGIN PRINT ORDER ***
   ;*
   PRINTER ON
   GOTO 101
100 *
   READNEXT PON,PON2 ELSE GOTO 900
101 *
   IF ENDFLG THEN GOTO 900
   OLDPO=PON[4,8]
   MATREAD MPO.REC FROM MISC.PO , PON ELSE GOTO 100
   ;*
   ;* T25941 v
   ;* Save each PO in a new file
   PRINTER CLOSE
   UDTEXECUTE NEW.PRINTER.CMD:"PO-":USERNO:"-":PON
   PO.PRINT.MAIL.ITEM = MPO.PRT.FLG<1,1>:SVM:MPO.PRT.FLG<1,2>
   IF MAIL.FLAG # '' THEN
     PO.PRINT.MAIL.ITEM = 'N' : SVM : 'Y'
   END
   ;*
   ;* Get the background form image for this PO
   ;*
   OCCUR.IDX = 1
   LOOP
      FIND CONO IN PRPT.FORM.CONO,OCCUR.IDX SETTING F,V THEN
         ;* If an image is found for the CONO/DIV then stop looking
         IF PRPT.FORM.DIV<1,V> = MPO.DIV.OWNER THEN
            PO.PRINT.MAIL.ITEM<1,2> = PRPT.FORM.PATH<1,V>
            EXIT
         END
         ;* Otherwise look for the next combination
         OCCUR.IDX = OCCUR.IDX + 1
      END ELSE
         ;* If there are no more occurences of the CONO then exit
         ;* and specify no background
         PO.PRINT.MAIL.ITEM<1,2> = ''
         OCCUR.IDX = 0
      END
   UNTIL OCCUR.IDX = 0 DO REPEAT
   ;*
   ;* Get the company defined carbon copy emails
   ;*
   ADD.EMAIL.ADDR = USER.EMAIL.ADDR
   FCCE.ID = CONO:"_FORMS_CC_EMAILS_":MPO.DIV.OWNER:"PO"
   READ FCCE.ADDRESSES FROM CONTROL, FCCE.ID THEN
      FOR FCCE.IDX = 1 TO DCOUNT(FCCE.ADDRESSES,@VM)
         ADD.EMAIL.ADDR:= ',':FCCE.ADDRESSES<1,FCCE.IDX>
      NEXT FCCE.IDX
   END
   ;* T25941 ^
   MPO.KEYS<-1> = PON
   ITEMS="";Z=0;ZZ=0
140 *
   MATREAD VEND.REC FROM VEND,CONO:MPO.VEND.NO ELSE
      MAT VEND.REC = ""
      VEND.PO.DESC= "NOT ON FILE"
   END
   ;* T25941 v
   ;* Add the vendor email to value loc 3, and add the record to the list
   IF VEND.EMAIL.ADDR # '' THEN
      PO.PRINT.MAIL.ITEM<1,3> = VEND.EMAIL.ADDR:ADD.EMAIL.ADDR
   END ELSE
      PO.PRINT.MAIL.ITEM<1,3> = ''
   END
   PM.LIST<-1> = PO.PRINT.MAIL.ITEM
   ;* T25941 ^
141 *
   IF MODEFLAG#"I" THEN
      Z=Z+1
      ITEMS=REPLACE(ITEMS,1,-1,0,PON2)
      READNEXT PON,PON2 ELSE ENDFLG=1;GOTO 142
      IF PON[4,8]=OLDPO THEN GOTO 141
   END
142 *
   PGNO=0
   LNNO=0
   TOT=0
   ZZ=1
   N=EXTRACT(ITEMS,1,ZZ,0)
   SAVEWHSE=MPO.SHIP.WHSE
   GOSUB 310
   ZZ=1
   N=EXTRACT(ITEMS,1,ZZ,0)
   LOOP PN=FIELD(MPO.PROD.DESC,VM,N) UNTIL COL2()=0 OR MPO.PROD.DESC="" OR N="" DO
      IF LNNO>30 OR SAVEWHSE#MPO.SHIP.WHSE THEN
         FOR X= LNNO TO 33; PRINT ; NEXT X
         GOSUB 300
      END
      DESC=MPO.PROD.DESC<1,N>; WT=0;PK=1
      PRINT OCONV(MPO.TOT.ONORD<1,N>,'MD2')"R#10":
*T111809 v
*      PRINT "  ":MPO.PROD.NUM<1,N> "L#16":SPACE(28):
      PRINT "  ":MPO.PROD.NUM<1,N> "L#16":SPACE(26):
*T111809 ^
*
      IF MPO.DISCOUNT<1,N>+0#0 THEN
         NET=OCONV(MPO.GROS.PRICE<1,N>,"MD4")*OCONV(MPO.DISCOUNT<1,N>,"MD4")
         NET=OCONV(MPO.GROS.PRICE<1,N>,"MD4")-NET
      END ELSE NET=OCONV(MPO.GROS.PRICE<1,N>,"MD4")
      NET=ICONV(NET,"MD4")
      PRINT OCONV(NET,"MD4") "R#13":
      EXT=OCONV(NET,"MD4")*OCONV(MPO.TOT.ONORD<1,N>,'MD2')
      EXT=ICONV(EXT,"MD2")
*T111809 v
*      PRINT OCONV(EXT,"MD2") "R#11"
      PRINT OCONV(EXT,"MD2") "R#13"
*T111809 ^
      TOT = TOT+EXT
*
      PRINT MPO.UNIT.MAG<1,N> "R#10 ":MPO.PROD.DESC<1,N> "L#45"
      PRINT SPACE(11):OCONV(MPO.DEL.DATE<1,N>,"D2/")"L#8"
      PRINT
      LNNO=LNNO+4
      CNT = DCOUNT(MPO.ITEM.COMM<1,N>,SVM)
      IF CNT > 0 THEN
         FOR XD = 1 TO CNT
            IF LNNO > 30 THEN
               FOR X = LNNO + 1 TO 33
                  PRINT
               NEXT X
               PRINT
               GOSUB 300
            END
            PRINT SPACE(11):MPO.ITEM.COMM<1,N,XD>"L#45"
            LNNO = LNNO + 1
         NEXT XD
         PRINT
         LNNO = LNNO + 1
      END
      ZZ=ZZ+1;N=EXTRACT(ITEMS,1,ZZ,0)
   REPEAT
   FOR X= LNNO TO 32; PRINT ; NEXT X
   PRINT
   PRINT SPACE(18):MPO.INTRAL.INT "L#42":
   TOT = FIELD(TOT,'.',1)
*
*T111809 v
*   PRINT SPACE(9):OCONV(TOT,"MD2") "R#11"
    PRINT SPACE(7):OCONV(TOT,"MD2") "R#13"
*T111809 ^
   IF MODEFLAG="I" THEN GOTO 900
   GOTO 101
300 *
   PRINT SPACE(18):MPO.INTRAL.INT "L#42":SPACE(11):"CONTINUED"
   LNNO=0
310 *
   PGNO=PGNO+1
   ;* T22081 v
   ;*      PRINT CHAR(12)
   IF FIRST.TIME # '' THEN 
      PRINT CHAR(12)
   END ELSE
      ;*T22579 PRINT
      ;*T22743        IF ALIGN.PRT THEN PRINT  ;*T22579
      PRINT ;*T22743
   END
   ALIGN.PRT=0  ;*T22579
   ;* T22081 ^
   ;*
   ;* T25941 v
   ;* MPO.PRT.FLG is now multivalued (PRINT-FLAG:@VM:MAIL-FLAG)
   ;* If either flag is a copy then both are considered copies
   ;* IF MPO.PRT.FLG = "C" THEN
   IF MPO.PRT.FLG<1,1> = "C" OR MPO.PRT.FLG<1,2> = "C" THEN
   ;* T25941 ^
      PRINT
      PRINT SPACE(21):"***CAUTION: THIS IS A PURCHASE ORDER REPRINT."
      PRINT SPACE(29):"DO NOT DUPLICATE THE ORDER!!!"
   END ELSE
      PRINT;PRINT;PRINT
   END
   PRINT SPACE(71):"PAGE ":PGNO "L#4"
   PRINT
   PRINT
   PRINT
   PRINT
   PRINT SPACE(5):VEND.PO.DESC "L#40":"  ":MPO.SHIP.NAME "L#33"
   PRINT SPACE(5):VEND.PO.ADDR1 "L#40":"  ":MPO.SHIP.ADD1 "L#33"
   PRINT SPACE(5):VEND.PO.ADDR2 "L#40":"  ":MPO.SHIP.ADD2 "L#33"
   ADDZIP=VEND.PO.CT.ST:" ":VEND.PO.ZIP
   PRINT SPACE(5):ADDZIP "L#42":MPO.SHIP.ADD3 "L#33"
   PRINT SPACE(5):SPACE(42):MPO.SHIP.ADD4 "L#33"
   PRINT
   PRINT
   ;*T22081 v      PRINT
   IF LASER.FLAG = '' THEN 
      PRINT
      PRINT OCONV(MPO.DATE,"D2/") "L#11":OLDPO "L#10":
      PRINT MPO.WRIT.BY "L#19":MPO.VEND.NO "L#8"
   END ELSE
      PRINT OCONV(MPO.DATE,"D2/") "L#10":OLDPO "L#10":
      PRINT MPO.WRIT.BY "L#20":MPO.VEND.NO "L#8"
   END
   ;* T22081 ^
   PRINT;PRINT
   SAVEWHSE=MPO.SHIP.WHSE
   ;* T22081 v
   FIRST.TIME = 1
   ;* T22081 ^
   RETURN
900 *
   PRINTER CLOSE
   PRINTER OFF
   ;* T25941 v
   ;* Restore original printer settings, and output saved reports
   UDTEXECUTE RESTORE.PRINTER.CMD
   CALL DOC.PRINT.MAIL(HOLD.FILE,MPO.KEYS,PM.LIST,MAT COMP.REC,"MPO")
   IF MAIL.FLAG # '' THEN GOTO 1100
   ;* T25941 ^
   PRINT @(-1)
   PRINT @(4,2):"PRINTING COMPLETE."
901 *
   PRINT @(9,6):"HAVE PURCHASE ORDERS PRINTED CORRECTLY ? (Y/N)":
   INPUT OPTION:
   IF OPTION = "Y" THEN GOTO 1100
   IF OPTION = "N" THEN GOTO 1200
   PRINT ;GOTO 901
1100 *
   MPO.CNT = DCOUNT(MPO.KEYS,AM)
   FOR MPO.PTR=1 TO MPO.CNT
      MPO.KEY=MPO.KEYS<MPO.PTR>
      MATREADU MPO.REC FROM MISC.PO,MPO.KEY ELSE
         RELEASE MISC.PO,MPO.KEY
         GOTO 1199
      END
      ;* T25941 v
      ;* MPO.PRT.FLG="P"
      ;* Use multivalued flag for mail and print
      IF MPO.PRT.FLG<1,1> = 'Y' OR MPO.PRT.FLG<1,1> = 'C' THEN
         MPO.PRT.FLG<1,1> = 'P'
      END
      IF MPO.PRT.FLG<1,2> = 'Y' OR MPO.PRT.FLG<1,2> = 'C' THEN
         MPO.PRT.FLG<1,2> = 'M'
      END
      ;* T25941 ^
      MATWRITE MPO.REC ON MISC.PO,MPO.KEY
1199 *
   NEXT MPO.PTR
1200 *
   GOTO 99999
   RETURN
*
***** CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
* T22081 v
   IF LASER.FLAG # '' THEN
      OPEN '',"SECURITY" TO SECURITY THEN
         READ TEMP.REC FROM SECURITY, CONO:OCONV(@LOGNAME,'MCU'):'!LASER' THEN
            DELETE SECURITY, CONO:OCONV(@LOGNAME,'MCU'):'!LASER'
         END
      END
   END
* T22081 ^
END
