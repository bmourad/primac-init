*COPY>CPYLIB>COM1
************************************************************************
* REVISION    - [1.0]
* Copyright 2001 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - PORBP
* PROGRAM     - REQ.INQ.DRIVER
* BY          - Edwin Lehr
* DATE        - 04/25/01
* DESCRIPTION -  A driver program to write selected records to a 
*                temporary file.
*
*ENDDOC
************************************************************************
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>PMC.CPYLIB>PO
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>POS.CPYLIB>MISC.PO
*COPY>POS.CPYLIB>REQ.INQ.TEMP
*
***** SETUP FOR SYSTEM ERRMSG
SYS.TYPE = 1
CALL SYSCOM(MAT SYSCOM.REC)
*
PROCREAD PROC.DATA ELSE
  ERRMSG = "MUST BE RUN FROM A PROC" ; GOTO 93000
END
FILE.TYPE = PROC.DATA<9>
*
*----- OPEN FILES
*
OPEN '','REQ.INQ.TEMP' TO REQ.INQ.TEMP ELSE
  ERRMSG = 'REQ.INQ.TEMP IS MISSING' ; GOTO 93000
END
*
*
BEGIN CASE
  CASE FILE.TYPE = 'R'
    OPEN '','REG.REQ' TO REG.REQ ELSE
      ERRMSG = 'REG.REQ FILE IS MISSING' ; GOTO 93000
    END
    LOOP
      READNEXT RECID ELSE EXIT
      MATREAD PO.REC FROM REG.REQ, RECID ELSE MAT PO.REC = ""
      RIT.WRIT.BY = PO.WRIT.BY
      RIT.APPROVER = PO.APPROVER
      RIT.DATE = PO.DATE
      LAST.POS = DCOUNT(PO.STATUS,VM)
      RIT.STATUS = PO.STATUS<1,LAST.POS>
      RIT.VEND.NO = PO.VEND.NO
      MATWRITE RIT.REC ON REQ.INQ.TEMP, RECID:FILE.TYPE
    REPEAT
  CASE FILE.TYPE = 'O'
    OPEN '','OUT.REQ' TO OUT.REQ ELSE
      ERRMSG = 'OUT.REQ FILE IS MISSING' ; GOTO 93000
    END
    LOOP
      READNEXT RECID ELSE EXIT
      MATREAD OPO.REC FROM OUT.REQ, RECID ELSE MAT OPO.REC = ""
      RIT.WRIT.BY = OPO.ORD.BY
      RIT.APPROVER = OPO.APPROVER
      RIT.DATE = OPO.DATE
      LAST.POS = DCOUNT(OPO.STAT,VM)
      RIT.STATUS = OPO.STAT<1,LAST.POS>
      RIT.VEND.NO = OPO.VEND.NO
      MATWRITE RIT.REC ON REQ.INQ.TEMP, RECID:FILE.TYPE
    REPEAT
  CASE FILE.TYPE = 'M'
    OPEN '','MISC.REQ' TO MISC.REQ ELSE
      ERRMSG = 'MISC.REQ FILE IS MISSING' ; GOTO 93000
    END
    LOOP
      READNEXT RECID ELSE EXIT
      MATREAD MPO.REC FROM MISC.REQ, RECID ELSE MAT MPO.REC = ""
      RIT.WRIT.BY = MPO.WRIT.BY
      RIT.APPROVER = MPO.APPROVER
      RIT.DATE = MPO.DATE
      LAST.POS = DCOUNT(MPO.STATUS,VM)
      RIT.STATUS = MPO.STATUS<1,LAST.POS>
      RIT.VEND.NO = MPO.VEND.NO
      MATWRITE RIT.REC ON REQ.INQ.TEMP, RECID:FILE.TYPE
    REPEAT
END CASE
*
GOTO 99999:
*
*************************************************************************
*************************************************************************
*
*---- CALLS FOR SYSCOM
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE=2;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
*
99999 *
END
