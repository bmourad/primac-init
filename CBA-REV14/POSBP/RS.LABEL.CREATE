*********************************************************************
* REVISION - [08.2B]
* PROGRAM  - RS.LABEL.CREATE
* AUTHOR   - W.R. MCKENZIE, COMPUTER BUSINESS ASSOCIATES
* DATE     - 06/18/90
* DESCRIPTION
* This program is used to transfer ROLL.SKID.INFO file data to the
* P/C for subsequent label production by LabelRight.
*T19259 rick 06/07/1995 * Bug with when a file cannot be opened. The pgm
*                         attempts to create the file but fails to open
*                         the file after it creates it
*T26311 adelgado 11/29/2001 * When the user gets 'NO ITEMS SELECTED',
*                             don't let the prgm do a fatal error.
*T26556 adelgado 04/22/2002 * ICS Rewrite.
*ENDDOC
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>PMC.CPYLIB>PO
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>COLOR
*COPY>ICS.CPYLIB>FINISH
*COPY>PMC.CPYLIB>VEND
*COPY>POS.CPYLIB>RS.LABEL
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*-----PROCREAD
*
  PROCREAD BUFFER ELSE
    ERRMSG = "MUST RUN FROM A PROC"
    GOTO 93000
  END
  CONO = BUFFER<1>
  LABEL.NAME = BUFFER<3>
  LABEL.FILE.NAME = LABEL.NAME:".TXT"
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOTO 93000
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOTO 93000
  END
  OPEN "","INV_SERIAL" TO INV_SERIAL ELSE
    ERRMSG = "CANNOT OPEN INV_SERIAL FILE"
    GOTO 93000
  END
  OPEN "","PO" TO PO ELSE
    ERRMSG = "CANNOT OPEN PO FILE"
    GOTO 93000
  END
  OPEN "","INVENTORY" TO INVENTORY ELSE
    ERRMSG = "CANNOT OPEN INVENTORY FILE"
    GOTO 93000
  END
  OPEN "","COLOR" TO COLOR ELSE
    ERRMSG = "CANNOT OPEN COLOR FILE"
    GOTO 93000
  END
  OPEN "","FINISH" TO FINISH ELSE
    ERRMSG = "CANNOT OPEN FINISH FILE"
    GOTO 93000
  END
  OPEN "","VEND" TO VEND ELSE
    ERRMSG = "CANNOT OPEN VEND FILE"
    GOTO 93000
  END
  OPEN "","RS.LABEL" TO RS.LABEL ELSE
    ERRMSG = "CANNOT OPEN RS.LABEL FILE"
    GOTO 93000
  END
*
*---- INITIALIZATION
*
  IF CONO = "" OR LEN(CONO) # 2 OR NOT(NUM(CONO)) THEN
    CONO = ""
    CALL GET.CONO1(CONO, MAT COMP.REC, COMPANY, CONTROL)
    IF CONO = "END" THEN GOTO 93000
  END
  TODAY = DATE()
*
  MATREAD RS.LABEL.REC FROM RS.LABEL, CONO:LABEL.NAME ELSE
    ERRMSG = "CANNOT LOCATE RS.LABEL RECORD ":LABEL.NAME
    GOTO 93000
  END
  ICNT = DCOUNT(RSL.ITEM,VM)
  OPEN "",LABEL.FILE.NAME TO LABEL.FILE ELSE
    IF LABEL.NAME = "LABEL" THEN
      ERRMSG = "CANNOT OPEN FILE ":LABEL.FILE.NAME
      GOTO 93000
    END
    SENTENCE = 'CREATE-FILE ':LABEL.FILE.NAME:' 1 97'
    PERFORM SENTENCE CAPTURING ERRMSG
*        IF ERRMSG<1,1>[1,3] # "417" THEN GOTO 93000
*T19259 v
    OPEN "",LABEL.FILE.NAME TO LABEL.FILE ELSE
      ERRMSG="CANNOT CREATE FILE ":LABEL.FILE.NAME
      GOTO 93000
    END
*T19259 ^
    OPEN "DICT",LABEL.FILE.NAME TO DLABEL.FILE ELSE
      ERRMSG = "CANNOT CREATE FILE ":LABEL.FILE.NAME
      GOTO 93000
    END
    OPEN "DICT","LABEL.TXT" TO DLABEL.TXT ELSE
      ERRMSG = "CANNOT OPEN FILE DICT LABEL.TXT"
      GOTO 93000
    END
    READ DREC FROM DLABEL.TXT, "PO.NO" THEN
      WRITE DREC ON DLABEL.FILE, "PO.NO"
    END
    READ DREC FROM DLABEL.TXT, "PO.LINE.NO" THEN
      WRITE DREC ON DLABEL.FILE, "PO.LINE.NO"
    END
    READ DREC FROM DLABEL.TXT, "ROLL.ID" THEN
      WRITE DREC ON DLABEL.FILE, "ROLL.ID"
    END
  END
  RCOUNT = 0
  ZZZS = STR('Z',8)  ;*C36992
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  PREV.PO = "!@#$%"
  PREV.LN = "!@#$%"
  PREV.IN = "!@#$%"
110 *
  READNEXT ID ELSE
    IF RCOUNT = 0 THEN
      ERRMSG = "NO ITEMS SELECTED"
      * T26311 v
      * GOTO 93000
      GOSUB 90000
      BUFFER = 'END'
      PROCWRITE BUFFER
      * T26311 ^
    END
*C36992 v
*C38285 v May have been req'd for earlier version of SBCLIENT
*        IF BUFFER<8> = 'S' THEN
*          LAB.ID = ZZZS:'!':ZZZS[1,4]:'!':ZZZS
*          WRITE '' ON LABEL.FILE,LAB.ID
*        END
*C38285 ^
*C36992 ^
    GOTO 99999
  END
  RCOUNT = RCOUNT + 1
  MATREADU ISTK.REC FROM INV_SERIAL, ID ELSE GOTO 100
  IF ISTK.PO.NO # PREV.PO THEN
    MATREAD PO.REC FROM PO, CONO:ISTK.PO.NO ELSE MAT PO.REC = "???"
    MATREAD VEND.REC FROM VEND, CONO:PO.VEND.NO ELSE MAT VEND.REC = "???"
  END
  IF ISTK.PROD # PREV.IN THEN
    MATREAD INV.REC FROM INVENTORY, CONO:ISTK.PROD ELSE MAT INV.REC = "???"
    MATREAD COLOR.REC FROM COLOR, CONO:INV.COLOR ELSE MAT COLOR.REC = "???"
    MATREAD FINISH.REC FROM FINISH, CONO:INV.PAP.FINSH ELSE MAT FINISH.REC = "???"
*** CSF 25117
    IF PO.VEND.NO = '???' AND INV.VENDOR<1,1> # '' THEN ;* SLIT.TRANS
      PO.VEND.NO = INV.VENDOR<1,1>
      MATREAD VEND.REC FROM VEND, CONO:PO.VEND.NO ELSE MAT VEND.REC = "???"
    END
***
    LOCATE PO.VEND.NO IN INV.VENDOR<1>,1 SETTING PART.PTR ELSE PART.PTR = 0
  END
  LAB.REC = ""
  FOR IPTR = 1 TO ICNT
    FDATA = ""
    FCNT = DCOUNT(RSL.ITEM<1,IPTR>,";")
    FOR FPTR = 1 TO FCNT
      FLD = FIELD(RSL.ITEM<1,IPTR>,";",FPTR)
      GOSUB 1000
      FDATA = FDATA : FLD
    NEXT FPTR
    IF IPTR = 1 THEN
      LAB.REC = '"':FDATA:'"'
    END ELSE
      LAB.REC = LAB.REC:',"':FDATA:'"'
    END
  NEXT IPTR
  LAB.ID = ISTK.PO.NO:"!":ISTK.PO.LINE:"!":ID
  WRITE LAB.REC ON LABEL.FILE,LAB.ID
  ISTK.PRINT.DATE = TODAY
  MATWRITE ISTK.REC ON INV_SERIAL, ID
  PREV.PO = ISTK.PO.NO
  PREV.LN = ISTK.PO.LINE
  PREV.IN = ISTK.PROD
  GOTO 110
*
*---- CREATE LABEL DATA
*
1000 *
  IF NUM(FLD) THEN
    BEGIN CASE
      CASE FLD = 1
        FLD = ID[4,99]
      CASE FLD = 2
        FLD = ISTK.PROD
      CASE FLD = 3
        FLD = INV.DESC
      CASE FLD = 4
        IF NOT(NUM(INV.PAP.WIDTH)) THEN
          FLD = INV.PAP.WIDTH
        END ELSE
          WIDTH = OCONV(INV.PAP.WIDTH,"MD4")
          LWIDTH = LEN(WIDTH)
          BEGIN CASE
            CASE WIDTH[LWIDTH-4,5] = ".0000"
              FLD = WIDTH[1,LWIDTH-5]
            CASE WIDTH[LWIDTH-2,3] = "000"
              FLD = WIDTH[1,LWIDTH-3]
            CASE WIDTH[LWIDTH-1,2] = "00"
              FLD = WIDTH[1,LWIDTH-2]
            CASE WIDTH[LWIDTH,1] = "0"
              FLD = WIDTH[1,LWIDTH-1]
            CASE 1
              FLD = WIDTH
          END CASE
        END
        IF NUM(INV.PAP.LEN) AND INV.PAP.LEN # "" AND INV.PAP.LEN # "0" THEN
          LNGTH = OCONV(INV.PAP.LEN,"MD4")
          LLNGTH = LEN(LNGTH)
          BEGIN CASE
            CASE LNGTH[LLNGTH-4,5] = ".0000"
              FLD = FLD:" x ":LNGTH[1,LLNGTH-5]
            CASE LNGTH[LLNGTH-2,3] = "000"
              FLD = FLD:" x ":LNGTH[1,LLNGTH-3]
            CASE LNGTH[LLNGTH-1,2] = "00"
              FLD = FLD:" x ":LNGTH[1,LLNGTH-2]
            CASE LNGTH[LLNGTH,1] = "0"
              FLD = FLD:" x ":LNGTH[1,LLNGTH-1]
            CASE 1
              FLD = LNGTH
          END CASE
        END
      CASE FLD = 5
        IF NOT(NUM(INV.BAS.WT)) THEN
          FLD = INV.BAS.WT
        END ELSE
          BW = OCONV(INV.BAS.WT,"MD2")
          LBW = LEN(BW)
          BEGIN CASE
            CASE BW[LBW-2,3] = ".00"
              FLD = BW[1,LBW-3]
            CASE BW[LBW,1] = "0"
              FLD = BW[1,LBW-1]
            CASE 1
              FLD = BW
          END CASE
        END
      CASE FLD = 6
        FLD = COLOR.DESC
      CASE FLD = 7
        FLD = FINISH.DESC
      CASE FLD = 8
        FLD = INV.M.LINE
      CASE FLD = 9
        FLD = INV.LINE
      CASE FLD = 10
        FLD = PO.VEND.NO
      CASE FLD = 11
        FLD = VEND.PO.DESC
      CASE FLD = 12
        IF PART.PTR THEN
          FLD = INV.PART.NO<1,PART.PTR>
        END ELSE
          FLD = ""
        END
      CASE FLD = 13
        FLD = ISTK.PO.NO
      CASE 1
        FLD = "???"
    END CASE
  END ELSE
    LFLD = LEN(FLD)
    FLD = FLD[2,LFLD-2]
  END
*
  QN = 1
  LOOP
    QPTR = INDEX(FLD,'"',QN)
  UNTIL QPTR = 0 DO
    FLD = FLD[1,QPTR]:'"':FLD[QPTR+1,999]
    QN = QN + 2
  REPEAT
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC); RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
  PROCWRITE "END"
*
*---- END OF PROGRAM
*
99999 *
END
