********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - POSBP
* PROGRAM     - OPEN.PO.PRT.1
* BY          - JIHAD YAMOUT ,C.B.A
* DATE        - 04/09/87
* DESCRIPTION - LIST ALL OPEN PO'S BY VENDOR
* TASK 20080 JR ADD DIVISION TO LISTING
*T25001 renee 04/24/2000 * Use line item due date instead of PO due date
*T25850 edwin 07/11/2001 * Add select criteria to report.
*T25901 cmykleb 03/08/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T26871 thompson 10/07/2002 * Fix report problems.
*T111909 Uprice changes by Naresh on 11/19/09
********************************************************************
*
*COPY>PMC.CPYLIB>PO
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.CNV
*COPY>PMC.CPYLIB>VEND
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
   LINE.COUNT = 99
   PAGE.NO = 0
   PREV.VEND = ''
   PREV.VEND.NAME = ''
   TOT.AMT = 0
*T111909 v
*   HD1 = "                                                                                                   ORDERED       OPEN"
*   HD2 = "PO NUM   VEND NO  VEND NAME      PRODUCT NUMBER       PRODUCT DESCRIPTION           DUE DATE U/M   QUANTITY    QUANTITY  OPEN AMOUNT" 
*   HD3 = "-------- -------- -------------- --------------- ---------------------------------- -------- --- ----------- ----------- -----------"
   HD1 = "                                                                                                ORDERED       OPEN"
   HD2 = "PO NUM   VEND NO  VEND NAME      PRODUCT NUMBER       PRODUCT DESCRIPTION        DUE DATE U/M   QUANTITY    QUANTITY    OPEN AMOUNT" 
   HD3 = "-------- -------- -------------- --------------- ------------------------------- -------- --- ----------- ----------- --------------"
*T111909 ^
**** SET UP FOR SYSCOM
   SYS.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
**** READ COMPANY NAME FROM PROC
   PROCREAD ID ELSE
      ERRMSG = "MUST RUN FROM A PROC"
      GOTO 93000
   END
*T26493 v
*  CONO = ID<1> ; CONO.NAME = ID<2>
   CONO = ID<1> ; REPORT.NUMBER = ID<2>
   CONO.NAME = ""
*T26493 ^
   DIV.OWNER = ID<3> ; *T20080
*T25901 v
   REPORT.NAME = ""
   HEAD1 = "" ; HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HEAD1,HEAD2)
*T25901 ^
*T25850 v
   START.DELIVER.DATE.RANGE = ICONV(ID<4>,'D2')
   END.DELIVER.DATE.RANGE = ICONV(ID<5>,'D2')
*T25850 ^
   POS = 50 - INT(LEN(CONO.NAME) /2)
   POS2 = 70 - POS
**** OPEN FILES
   OPEN '','PO' TO PO ELSE
      ERRMSG = 'PO FILE IS MISSING'
      GOSUB 93000
   END
   OPEN '','INVENTORY' TO INVENTORY ELSE
      ERRMSG = 'INVENTORY FILE IS MISSING'
      GOSUB 93000
   END
   OPEN '','VEND' TO VEND ELSE
      ERRMSG = 'VEND FILE IS MISSING'
      GOSUB 93000
   END
*
***** MAIN PROCESS
*
   PRINTER ON
**** READ FIRST RECORED
10*
   READNEXT PO.CODE ELSE
      ERRMSG = "NO ITEM SELECTED"
*T19838 v
      PRINTER OFF
      GOSUB 91000
      GOTO 99999
*T19838 ^
   END
   MATREAD PO.REC FROM PO , PO.CODE ELSE
      MAT PO.REC = ''
      GOTO 10
   END
   MATREAD VEND.REC FROM VEND, CONO:PO.VEND.NO ELSE
      VEND.DESC = "??????????"
   END
   PREV.VEND = PO.VEND.NO ; PREV.VEND.NAME = VEND.DESC
   GOSUB 200
   DATA = 0
   LOOP
      READNEXT PO.CODE ELSE DATA = 1
   WHILE DATA = 0 DO
      MATREAD PO.REC FROM PO , PO.CODE ELSE
         MAT PO.REC = ''
         GOTO 111
      END
      MATREAD VEND.REC FROM VEND , CONO: PO.VEND.NO ELSE
         VEND.DESC = "??????????"
      END
      IF PO.VEND.NO # PREV.VEND THEN
         GOSUB 400
         PREV.VEND = PO.VEND.NO ; PREV.VEND.NAME = VEND.DESC
         TOT.AMT = 0
      END
      GOSUB 200
111 REPEAT
   GOSUB 400
   GOTO 99999
***** PRINT PO LINES
200*
   QCNT=COUNT(PO.PROD.NUM,VM)+(PO.PROD.NUM # '')
   FLG = 0
   FOR MV.NUM = 1 TO QCNT
*T26871      IF START.DELIVER.DATE.RANGE THEN        ; * T25850 v
*T26871         IF PO.DEL.DATE<1,MV.NUM> < START.DELIVER.DATE.RANGE OR PO.DEL.DATE<1,MV.NUM> > END.DELIVER.DATE.RANGE THEN CONTINUE
*T26871      END                                     ; * T25850 ^
      IF PO.QTY.OPEN<1,MV.NUM> + 0 > 0 THEN
         FLG = FLG + 1
         MATREAD INV.REC FROM INVENTORY ,CONO:PO.PROD.NUM<1,MV.NUM> ELSE
            INV.DESC = "??????????????????????????"
         END
         GOSUB 300
         GOSUB 1000
      END
   NEXT MV.NUM
   RETURN
***** CONVERT QUANTITY
300*
   IF INV.SBR + 0 = 0 THEN INV.SBR = 1
   MAT INV.CNV.REC = ''
   BEGIN CASE
      CASE PO.UNIT.FLG<1,MV.NUM> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1
      CASE PO.UNIT.FLG<1,MV.NUM> = 'PC' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 10; ICR.DV2 = 1
      CASE PO.UNIT.FLG<1,MV.NUM> = 'FT' AND INV.UNIT<1,3> = 'MSI'
         ICR.CNV = "MD0"
         ICR.DV1 = INV.PAP.WIDTH/100; ICR.MT1 = 100; ICR.DV2 = 12
      CASE 1
         ICR.CNV = "MD2"
         ICR.DV1 = INV.SBR; ICR.MT1 = 1; ICR.DV2 = 1
   END CASE
   IF PO.UNIT.FLG<1,MV.NUM> # INV.UNIT<1,2> THEN DIFF.UM = "Y" ELSE DIFF.UM = "N"
   RETURN
****  PRINT TOTALS
400*
   PRINT
   LINE.COUNT = LINE.COUNT + 1
   IF TOT.AMT # 0 THEN
      T.LINE = ''
*T111909 v
*      T.LINE = SPACE(107):"VENDOR TOTAL ":OCONV(TOT.AMT, "MD2") "R#12"
      T.LINE = SPACE(105):"VENDOR TOTAL ":OCONV(TOT.AMT, "MD2") "R#14"
*T111909 ^
      PRINT T.LINE
      PRINT
      LINE.COUNT  = LINE.COUNT + 2
   END
   RETURN
***** PRINT LINE
1000*
   IF LINE.COUNT > 45 THEN
      LINE.COUNT = 0
      GOSUB 9000
   END
   D.LINE = ''
   IF FLG = 1 THEN
      PRINT
      LINE.COUNT = LINE.COUNT + 1
      D.LINE = D.LINE :PO.CODE[4,LEN(PO.CODE)] "L#8":' ':PO.VEND.NO "L#8":' ':VEND.DESC "L#14":' '
   END ELSE
      D.LINE = SPACE(33)
   END
*T111909 v
*   D.LINE = D.LINE :PO.PROD.NUM<1,MV.NUM> "L#15":' ':INV.DESC "L#34":' '
   D.LINE = D.LINE :PO.PROD.NUM<1,MV.NUM> "L#15":' ':INV.DESC "L#31":' '
*T111909 ^
* T25001 v Use line item due date instead of PO due date
*  D.LINE = D.LINE :OCONV(PO.DUE.DATE, "D2/") "L#8":' '
   D.LINE = D.LINE :OCONV(PO.DEL.DATE<1,MV.NUM>, "D2/") "L#8":' '
* T25001 ^
   D.LINE = D.LINE :PO.UNIT.FLG<1,MV.NUM> "L#3":' '
   IF DIFF.UM = "Y" THEN
      IF ICR.CNV = "MD0" THEN
         TEMP = INT(((PO.TOT.ONORD<1,MV.NUM> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5)
         TEMP1 = INT(((PO.QTY.OPEN<1,MV.NUM> / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5)
      END ELSE
         TEMP = INT((PO.TOT.ONORD<1,MV.NUM> / 10) / ICR.DV1)
         TEMP1 = INT((PO.QTY.OPEN<1,MV.NUM> / 10) / ICR.DV1)
      END
   END ELSE
      IF ICR.CNV = "MD0" THEN
         TEMP = INT(((PO.TOT.ONORD<1,MV.NUM> / ICR.DV1) * ICR.MT1)/ICR.DV2 +.5)
         TEMP1 = INT(((PO.QTY.OPEN<1,MV.NUM> / ICR.DV1) * ICR.MT1)/ICR.DV2 +.5)
      END ELSE
         TEMP = INT(PO.TOT.ONORD<1,MV.NUM> / 10)
         TEMP1 = INT(PO.QTY.OPEN<1,MV.NUM> / 10)
      END
   END
   D.LINE = D.LINE:OCONV(TEMP, ICR.CNV) "R#11":' '
   D.LINE = D.LINE:OCONV(TEMP1, ICR.CNV) "R#11":' '
   DESC.PRICE = INT((PO.GROS.PRICE<1,MV.NUM> * (1- (PO.DISCOUNT<1,MV.NUM>/10000))) / INV.SBR +.5)
   IF INV.COST.WT * 1 = '0' THEN INV.COST.WT = 100
   OPEN.PRICE = INT((DESC.PRICE / 10000) * ((PO.QTY.OPEN<1,MV.NUM>/10)/(INV.COST.WT / 100)))
*T111909 v
*   D.LINE = D.LINE: OCONV(OPEN.PRICE, "MD2") "R#11"
   D.LINE = D.LINE: OCONV(OPEN.PRICE, "MD2") "R#14"
*T111909 ^
   TOT.AMT = TOT.AMT + OPEN.PRICE
   PRINT D.LINE
   LINE.COUNT = LINE.COUNT + 1
   RETURN
**** PRINT HEADING
9000*
   PRINT CHAR(12):
   PAGE.NO = PAGE.NO + 1
*T25901*v
*  H.LINE = ''
*  H.LINE = H.LINE : "DATE : " :OCONV(DATE(),"D2/") "L#8"
*  H.LINE = H.LINE : SPACE(POS) : CONO.NAME "L#30"
*  H.LINE = H.LINE : SPACE(POS2) : 'PAGE ' : PAGE.NO "R#3"
*  PRINT H.LINE
*  H.LINE = ''
*  H.LINE = H.LINE : SPACE(52) : 'OPEN PURCHASE ORDER REPORT'
*  PRINT H.LINE
*  H.LINE = ''
*  H.LINE = H.LINE:"DIVISION : ":DIV.OWNER"L#3"
*  H.LINE = H.LINE : SPACE(38) : '        BY VENDOR       '
*  PRINT H.LINE
   PRINT HEAD1:PAGE.NO
   PRINT HEAD2:
   H.LINE = "DIVISION : ":DIV.OWNER"L#3"
   IF START.DELIVER.DATE.RANGE THEN
      H.LINE = H.LINE : SPACE(49):OCONV(START.DELIVER.DATE.RANGE,'D2/')
      H.LINE := ' THRU ':OCONV(END.DELIVER.DATE.RANGE,'D2/')
   END
   PRINT H.LINE
*T25901 ^
   PRINT
   PRINT HD1
   PRINT HD2
   PRINT HD3
   LINE.COUNT = LINE.COUNT + 7
   RETURN
***** CALLS FOR SYSCOM
91000 ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999 PRINTER OFF
END
