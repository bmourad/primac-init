********************************************************************
* REVISION    - [10.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - POSBP
* PROGRAM     - OUTSIDE.PO.PRINT
* BY          - CINDY STRAIT
* DATE        - 10/30/94
* DESCRIPTION - PRINT OUTSIDE PO .
* TASK
*     19542 LLH 08/30/95 - PRINT DUE DATE FOR EACH LINE ITEM
*
*T21153 lanny 10/31/1996 * Widen qty column to allow 10 pos.
*T22081 stefanie 08/07/1997 * Add check to see if using laser printer.
*                             If so, use laser settings.
*T22579 lanny 02/09/1998 * ALIGNMENT ON 1ST PO AFTER MASK PRINTS 1 LINE
*                          TOO HIGH.
*T22743 rik 04/14/1998 * ALIGNMENT WRONG AFTER 1ST PO.
*C33495 diane 06/09/1999 * PRINTING TOO LOW ON LASER
*C34069 markt 08/16/1999 * Order info line prints one line too high
*T23319 alex 04/11/2000 * Use OPO.EST.COST to print amounts; comment out
*                         calculation formula.
*T25291 edvard 06/06/00  * Convert VM to " " in JOB.DESC to print 
*                    
*T25941 ajibaly 06/28/2002 * Allow emailing of POs to vendor
*T111709 UnitPrice Chanes by Naresh on 11/17/09
********************************************************************
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>POS.CPYLIB>OUTSIDE.PO
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>POS.CPYLIB>SPECIAL.INST
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>PMC.CPYLIB>PMC_REPORTS ;* T25941
*COPY>PMC.CPYLIB>SECURITY ;* T25941
   ;*
   ;* INITIALIZATION
   ;*
   MODEFLAG="A"
   PROMPT""
   ENDFLG=0
   OPO.KEYS = ''
   ;*
   ;* T25941 v
   ;* List to contain print/mailing info:
   ;* value loc 1 specifies print/mail/or both
   ;* value loc 2 specifies email addresses (comma seperated)
   ;* value loc 3 specifies background form image to use
   PM.LIST = ""
   USERNO = "USERNO"
   CALL SYSVARS.SUB(USERNO)
   LOGNAME = "LOGNAME"
   CALL SYSVARS.SUB(LOGNAME)
   RESTORE.PRINTER.CMD = "SETPTR ":GETPTR(0):",BRIEF"
   NEW.PRINTER.CMD     = "SETPTR ,,,,,3,BRIEF,NOMESSAGE,NOHEAD,BANNER "
   ;* T25941 ^
   ;*
   ;* SET UP FOR SYSCOM
   ;*
   SYS.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   ;*
   ;* OPEN FILES
   ;*
   OPEN "","JOB" TO JOB ELSE
      ERRMSG = " JOB FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","VEND" TO VEND ELSE
      ERRMSG = "VEND FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","OUTSIDE.PO" TO OUTSIDE.PO ELSE
      ERRMSG = "OUTSIDE.PO FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","CATEGORY.OSP" TO CATEGORY.OSP ELSE
      ERRMSG = "CATEGORY.OSP FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "COMPANY FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CONTROL FILE IS MISSING" ; GOTO 93000
   END
   ;*
   ;* T25941 v
   HOLD.FILE.NAME = "_HOLD_"
   OPEN "",HOLD.FILE.NAME TO HOLD.FILE ELSE
      ERRMSG = HOLD.FILE.NAME:" FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","PMC_REPORTS" TO PMC_REPORTS ELSE
      ERRMSG = "PMC_REPORTS FILE IS MISSING" ; GOTO 93000
   END
   OPEN "","SECURITY" TO SECURITY ELSE
      ERRMSG = "SECURITY FILE IS MISSING" ; GOTO 93000
   END
   ;*
   ;* Read the buffer and get the report number
   ;*
   PROCREAD BUFFER ELSE
      ERRMSG = "MUST BE RUN FROM A PROC"
      GOSUB 91000; RETURN
   END
   RPT.NO = BUFFER<2>
   MAIL.FLAG = BUFFER<8>
   MATREAD PRPT.REC FROM PMC_REPORTS, RPT.NO ELSE
      ERRMSG = "Cannot read the PMC_REPORTS record for this report. "
      ERRMSG:= "Make sure that Report Screen flag is 'R'."
      GOSUB 91000; RETURN
   END
   ;* T25941 ^
   ;*
   ;* GET CONO
   ;*
   READNEXT PON ELSE GOTO 900
   CONO=PON[1,3]
   MATREAD COMP.REC FROM COMPANY , CONO ELSE
      ERRMSG = "COMPANY # (":CONO:") IS MISSING" ; GOTO 93000
   END
   OPO.KEYS<-1> = PON
   ;*
   ;* T25941 v
   ;* Read the security record and get the user's email
   ;*
   USER.EMAIL.ADDR = ''
   MATREAD SEC.REC FROM SECURITY, CONO:UPCASE(LOGNAME) THEN
      IF SEC.EMAIL # '' THEN USER.EMAIL.ADDR = ',':SEC.EMAIL
   END
   ;* T25941 ^
   ;*
   ;*
   ;* READ SPECIAL INST
   ;*
   MATREAD SI.REC FROM CONTROL , CONO:"SPEC.INST" ELSE
      MAT SI.REC = ''
   END
   SPI.CNT = DCOUNT(SI.INST, VM)
   ;*
   ;* T22081 v
   ;*
   FIRST.TIME = ''
   LASER.FLAG = ''
   ALIGN.PRT=''
   IF OCONV(CONO:OCONV(@LOGNAME,'MCU'):'!LASER','TSECURITY;X;0;0') # '' THEN
      LASER.FLAG = 1
   END
   ;*
   ;* T22081 ^
   ;*
20*
   ;* T22081 v
   IF LASER.FLAG = '' AND MAIL.FLAG = '' THEN 
      CRT @(-1)
      CRT @(0,4): "LOAD PURCHASE ORDER FORMS IN PRINTER AND ALIGN BY EYE."
      CRT @(0,6): "PRINT MASK OR ORDERS (M/O/END) ?":
      INPUT RESPONSE
   END ELSE
      RESPONSE = "O"
   END
   ;*
   ;* T22081 ^
   IF RESPONSE = "END" THEN GOTO 99999
   IF RESPONSE # "O" AND RESPONSE # "M" THEN
      PRINT
      GOTO 20
   END
   IF RESPONSE # "O" THEN
      PRINTER ON
      ;*T22579 v
      IF ALIGN.PRT THEN
         PRINT
      END ELSE
         ALIGN.PRT=1
         PRINT CHAR(12)
      END
      ;*T22579 ^
      PRINT "X"
      PRINT
      PRINT
      PRINT SPACE(71):"PAGE XXXX"
      PRINT SPACE(21):"***XXXXXXX: XXXX XX X XXXXXXXX XXXXX XXXXXXX."
      PRINT SPACE(33):"XX XXX XXXXXXXXX XXX XXXXX!!!"
      PRINT
      PRINT
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT SPACE(5):STR("X",40):SPACE(2):STR("X",33)
      PRINT
      PRINT
      PRINT
      PRINT "MM/DD/YY   99999999  XXXXXXXXXXXXXXX"
      PRINT;PRINT
      FOR X=1 TO 5
         PRINT "9999999.99 XXXXXXXXXXXXXXX":SPACE(32):"9999999.99 99999999.99"
         PRINT
         PRINT SPACE(11):STR("X",41)
         PRINT SPACE(11):STR("X",41)
         PRINT SPACE(11):STR("X",41)
         PRINT
      NEXT X
      PRINT;PRINT;PRINT;PRINT
*T111709 v
*      PRINT SPACE(17):STR("X",42):SPACE(11):"9999999.99"
      PRINT SPACE(17):STR("X",42):SPACE(8):"9999999999.99"
*T111709 ^
      PRINTER CLOSE
      PRINTER OFF
      GOTO 20
   END
   ;*
   ;* BEGIN PRINT ORDER **
   ;*
   PRINTER ON
   GOTO 101
100 *
   READNEXT PON ELSE GOTO 900
   OPO.KEYS<-1> = PON
101 *
   IF ENDFLG THEN GOTO 900
   OLDPO=PON[4,8]
   MATREAD OPO.REC FROM OUTSIDE.PO , PON ELSE GOTO 100
   ;*
   ;* T25941 v
   ;* Save each PO in a new file
   PRINTER CLOSE
UDTEXECUTE NEW.PRINTER.CMD:"PO-":USERNO:"-":PON CAPTURING MESG
   PO.PRINT.MAIL.ITEM = OPO.PRT.FLG<1,1>:SVM:OPO.PRT.FLG<1,2>
   IF MAIL.FLAG # '' THEN
     PO.PRINT.MAIL.ITEM = 'N' : SVM : 'Y'
   END
   ;*
   ;* Get the background form image for this PO
   ;*
   OCCUR.IDX = 1
   LOOP
      FIND CONO IN PRPT.FORM.CONO,OCCUR.IDX SETTING F,V THEN
         ;* If an image is found for the CONO/DIV then stop looking
         IF PRPT.FORM.DIV<1,V> = OPO.DIV.OWNER THEN
            PO.PRINT.MAIL.ITEM<1,2> = PRPT.FORM.PATH<1,V>
            EXIT
         END
         ;* Otherwise look for the next combination
         OCCUR.IDX = OCCUR.IDX + 1
      END ELSE
         ;* If there are no more occurences of the CONO then exit
         ;* and specify no background
         PO.PRINT.MAIL.ITEM<1,2> = ''
         OCCUR.IDX = 0
      END
   UNTIL OCCUR.IDX = 0 DO REPEAT
   ;*
   ;* Get the company defined carbon copy emails
   ;*
   ADD.EMAIL.ADDR = USER.EMAIL.ADDR
   FCCE.ID = CONO:"_FORMS_CC_EMAILS_":OPO.DIV.OWNER:"PO"
   READ FCCE.ADDRESSES FROM CONTROL, FCCE.ID THEN
      FOR FCCE.IDX = 1 TO DCOUNT(FCCE.ADDRESSES,@VM)
         ADD.EMAIL.ADDR:= ',':FCCE.ADDRESSES<1,FCCE.IDX>
      NEXT FCCE.IDX
   END
   ;* T25941 ^
140 *
   MATREAD VEND.REC FROM VEND,CONO:OPO.VEND.NO ELSE
      MAT VEND.REC = ""
      VEND.PO.DESC= "NOT ON FILE"
   END
   ;* T25941 v
   ;* Add the vendor email to value loc 3, and add the record to the list
   IF VEND.EMAIL.ADDR # '' THEN
      PO.PRINT.MAIL.ITEM<1,3> = VEND.EMAIL.ADDR:ADD.EMAIL.ADDR
   END ELSE
      PO.PRINT.MAIL.ITEM<1,3> = ''
   END
   PM.LIST<-1> = PO.PRINT.MAIL.ITEM
   ;* T25941 ^
142 *
   PGNO=0
   LNNO=0
   TOT=0
   GOSUB 310
   JOB.CNT = DCOUNT(OPO.JOB.NO,VM)
   FOR N = 1 TO JOB.CNT
      IF LNNO > 30 THEN
         FOR X= LNNO TO 33; PRINT ; NEXT X
         GOSUB 300
      END
      MATREAD JOB.REC FROM JOB,CONO:OPO.JOB.NO<1,N> ELSE GOTO  150
      READV CATG.DESC FROM CATEGORY.OSP,CONO:OPO.PROD.LINE<1,N>,1 ELSE
         CATG.DESC = 'UNKNOWN'
      END
      PRINT OCONV(OPO.QTY<1,N>,'MD2')"R#10":
      PRINT " ":"JOB:":OPO.JOB.NO<1,N> "L#9":
      PRINT OPO.PROD.LINE<1,N> "L#7" :CATG.DESC "L#25":
      IF OPO.DISCOUNT<1,N>+0#0 THEN
         NET = (OPO.U.PRICE<1,N>/10000)*(1-(OPO.DISCOUNT<1,N>/10000))
      END ELSE NET=OCONV(OPO.U.PRICE<1,N>,"MD4")
      NET=ICONV(NET,"MD4")
*T111709 v
*      PRINT OCONV(NET,"MD4") "R#12":
      PRINT OCONV(NET,"MD4") "R#11":
*T111709 ^
      EXT = OPO.EST.COST<1,N> - OPO.CANCEL.COST<1,N>
*T111709 v
*      PRINT OCONV(EXT,"MD2") "R#11"
      PRINT OCONV(EXT,"MD2") "R#13"
*T111709 ^
      TOT = TOT+EXT
*
      PRINT OPO.UOM<1,N>"R#9":
      PRINT " " "L#2":CONVERT(VM," ",JOB.DESC) "L#45"
      PRINT SPACE(11):OCONV(OPO.EXP.DATE<1,N>,"D2/")"L#8"
      PRINT
      LNNO=LNNO+4
      CNT = DCOUNT(OPO.ITEM.COMM<1,N>,SVM)
      IF CNT > 0 THEN
         FOR XD = 1 TO CNT
            IF LNNO > 30 THEN
               FOR X = LNNO + 1 TO 33
                  PRINT
               NEXT X
               PRINT
               GOSUB 300
            END
            PRINT SPACE(11):OPO.ITEM.COMM<1,N,XD>"L#45"
            LNNO = LNNO + 1
         NEXT XD
         PRINT
         LNNO = LNNO + 1
      END
150   
   NEXT N
   INT.CNT = DCOUNT(OPO.INTRAL.INT,VM)
   JOBSPEC.CNT = DCOUNT(OPO.JOB.SPEC,VM)
   SI.CNT = 0
   FOR C = 1 TO JOBSPEC.CNT
      IF OPO.JOB.SPEC<1,C> # '' THEN
         SI.CNT = SI.CNT + 1
      END
   NEXT C
   IF LNNO + INT.CNT + SI.CNT > 33 THEN 
      FOR X= LNNO TO 33; PRINT ; NEXT X
      GOSUB 300
   END
   FOR XC = 1 TO INT.CNT
      PRINT SPACE(11):OPO.INTRAL.INT<1,XC>"L#45"
      LNNO = LNNO + 1
   NEXT XC
   PRINT
   PRINT SPACE(11): "**SPECIAL INSTRUCTIONS**"
   LNNO = LNNO + 2
   FOR XS = 1 TO JOBSPEC.CNT
      IF OPO.JOB.SPEC<1,XS> # '' THEN
         IF SI.TYP<1,XS> = '6' THEN 
            PRINT SPACE(11):SI.INST<1,XS>:" ":OCONV(OPO.JOB.SPEC<1,XS>,"D2/")
         END ELSE
            IF SI.TYP<1,XS> = '4' THEN
               PRINT SPACE(11):SI.INST<1,XS>:" ":OCONV(OPO.JOB.SPEC<1,XS>,"MD2")
            END ELSE
               PRINT SPACE(11):SI.INST<1,XS>:" ":OPO.JOB.SPEC<1,XS>
            END
         END
         LNNO = LNNO + 1
      END
   NEXT XS
   TOT = FIELD(TOT,'.',1)
*
   IF LNNO < 33 THEN
      FOR X= LNNO TO 33; PRINT ; NEXT X
   END
*
*T111709 v
*   PRINT SPACE(30):"SEE P.O. DETAIL" "L#39":OCONV(TOT,"MD2") "R#11"
   PRINT SPACE(30):"SEE P.O. DETAIL" "L#37":OCONV(TOT,"MD2") "R#13"
*T111709 ^
   GOTO 100
300 PRINT SPACE(30):"SEE P.O. DETAIL" "L#41":"CONTINUED"
   LNNO=0
310 PGNO=PGNO+1
   IF FIRST.TIME THEN
      PRINT CHAR(12)
   END ELSE
      PRINT  ;*T22743
   END
   ;* T25941 v
   ;* OPO.PRT.FLG is now multivalued (PRINT-FLAG:@VM:MAIL-FLAG)
   ;* If either flag is a copy then both are considered copies
   ;* IF OPO.PRT.FLG = "C" THEN
   IF OPO.PRT.FLG<1,1> = "C" OR OPO.PRT.FLG<1,2> = "C" THEN
   ;* T25941 ^
      PRINT
      PRINT SPACE(19):"***CAUTION: THIS IS A PURCHASE ORDER REPRINT."
      PRINT SPACE(25):"DO NOT DUPLICATE THE ORDER!!!"
   END ELSE
      PRINT;PRINT;PRINT
   END
   PRINT SPACE(71):"PAGE ":PGNO "L#4"
   IF LASER.FLAG = '' THEN PRINT
   PRINT
   PRINT
   PRINT
   PRINT SPACE(5):VEND.PO.DESC "L#40":"  ":OPO.SHIP.NAME "L#33"
   PRINT SPACE(5):VEND.PO.ADDR1 "L#40":"  ":OPO.SHIP.ADDR1 "L#33"
   PRINT SPACE(5):VEND.PO.ADDR2 "L#40":"  ":OPO.SHIP.ADDR2 "L#33"
   ADDZIP=VEND.PO.CT.ST:" ":VEND.PO.ZIP
   PRINT SPACE(5):ADDZIP "L#42":OPO.SHIP.ADDR3 "L#33"
   PRINT SPACE(5):SPACE(42):OPO.SHIP.ADDR4 "L#33"
   PRINT
   PRINT
   PRINT
   IF LASER.FLAG = '' THEN
      PRINT OCONV(OPO.DATE,"D2/") "L#11":OLDPO "L#10":
      PRINT OPO.ORD.BY "L#19": OPO.VEND.NO "L#11": "SEE BELOW" "R#20"
   END ELSE
      PRINT OCONV(OPO.DATE,"D2/") "L#10":OLDPO "L#10":
      PRINT OPO.ORD.BY "L#19":SPACE(1): OPO.VEND.NO "L#11": "SEE BELOW" "R#20"
   END
   PRINT
   PRINT
   FIRST.TIME = 1
   RETURN
*
900 *
*
   PRINTER CLOSE
   PRINTER OFF
   ;* T25941 v
   ;* Restore original printer settings, and output saved reports
   UDTEXECUTE RESTORE.PRINTER.CMD
   CALL DOC.PRINT.MAIL(HOLD.FILE,OPO.KEYS,PM.LIST,MAT COMP.REC,"OPO")
*   IF MAIL.FLAG # '' THEN RETURN
   IF  MAIL.FLAG # "" THEN GOTO 1100
   ;* T25941 ^
   CRT @(-1)
   CRT @(4,2):"PRINTING COMPLETE."
901 *
   CRT @(9,6):"HAVE PURCHASE ORDERS PRINTED CORRECTLY ? (Y/N)":
   INPUT OPTION:
   IF OPTION = "Y" THEN GOTO 1100
   IF OPTION = "N" THEN GOTO 1200
   PRINT ;GOTO 901
*
1100 *
*
   OPO.CNT = DCOUNT(OPO.KEYS,AM)
   FOR OPO.PTR=1 TO OPO.CNT
      OPO.KEY=OPO.KEYS<OPO.PTR>
      MATREADU OPO.REC FROM OUTSIDE.PO,OPO.KEY ELSE
         RELEASE OUTSIDE.PO,OPO.KEY
         GOTO 1199
      END
      ;* T25941 v
      ;* OPO.PRT.FLG="P"
      ;* Use multivalued flag for mail and print
      IF OPO.PRT.FLG<1,1> = 'Y' OR OPO.PRT.FLG<1,1> = 'C' THEN
         OPO.PRT.FLG<1,1> = 'P'
      END
      IF OPO.PRT.FLG<1,2> = 'Y' OR OPO.PRT.FLG<1,2> = 'C' THEN
         OPO.PRT.FLG<1,2> = 'M'
      END
      ;* T25941 ^
      MATWRITE OPO.REC ON OUTSIDE.PO,OPO.KEY
1199  
   NEXT OPO.PTR
1200 *
   GOTO 99999
   RETURN
*
***** CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE = 1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE = 2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE = 3 ; CALL SYSCOM(MAT SYSCOM.REC)
99999 *
* T22081 v
   IF LASER.FLAG # '' THEN
      OPEN '',"SECURITY" TO SECURITY THEN
         READ TEMP.REC FROM SECURITY, CONO:OCONV(@LOGNAME,'MCU'):'!LASER' THEN
            DELETE SECURITY, CONO:OCONV(@LOGNAME,'MCU'):'!LASER'
         END
      END
   END
* T22081 ^
END
