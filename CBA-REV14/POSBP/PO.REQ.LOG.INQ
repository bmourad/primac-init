  SUBROUTINE PO.REQ.LOG.INQ(STATUS, ST.DATE, USER)
*COPY>CPYLIB>COM1
*
*
**********************************************
* REVISION    - [XX.X]
* Copyright 1995 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - POSBP
* PROGRAM     - PO.REQ.LOG.INQ
* BY          - edvard; CBA
* DATE        - 08/18/1999
* DESCRIPTION -
*
*T24231 edvard 08/18/1999 * Log display
*T24232 edvard 09/28/1999 * Just to go in LITHOBP
*T25978 adelgado 02/07/2002 * Add the use of prompts (S,SR,SB,ST).
*ENDDOC
**********************************************
*
*COPY>PMC.CPYLIB>PO
*COPY>PMC.CPYLIB>COMP.OPS
*COPY>PMC.CPYLIB>COMPANY
*COPY>POS.CPYLIB>APP.REQ
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZE
  PAGE.SIZE = 15
  BEGIN.PAGE = 5
  LINE.SPACE = 1
  START.LINE = 1
  LN = 1
*
*
*---- GET SCREEN
*
  ESN = ECD.SCRN.NO
*
  GOSUB GET.DATA
*
  GOSUB PRINT.ALL.DATA
*
  GOSUB MAIN.OPT.PROMPT
  GOTO 99999
*****************
MAIN.OPT.PROMPT: 
*****************
*
  DONE=0
  LOOP
    ECD.NUM = 21
    SCV.REC(ECD.NUM)<ESN,1>=''
    GOSUB ACTION.4
    OPTION=ECD.RET.VALUE
    BEGIN CASE
      CASE OPTION="E" OR OPTION="END"
        DONE=1
      CASE OPTION='S'
        LN=LN + PAGE.SIZE
        IF LN > LINES THEN LN=1
        GOSUB SHOW.LINES
      * T25978 v
      CASE OPTION = 'SR'
        LN -= PAGE.SIZE
        IF LN < 1 THEN LN = 1
        GOSUB SHOW.LINES
      CASE OPTION = 'ST'
        LN = 1
        GOSUB SHOW.LINES
      CASE OPTION = 'SB'
        LN = LINES
        GOSUB SHOW.LINES
      * T25978 ^
    END CASE
  UNTIL DONE DO REPEAT
  RETURN
*
*
***********
SHOW.LINES: 
***********
*
  START.LINE=1 + INT((LN-1) / PAGE.SIZE) * PAGE.SIZE
  IF START.LINE=OLD.START.LINE THEN GOTO 5047
  OLD.START.LINE=START.LINE
  LAST.LINE=START.LINE + PAGE.SIZE - 1
  CNT=1
  FOR L=START.LINE TO LAST.LINE UNTIL L > LINES
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(L-1,PAGE.SIZE)
    P_X  = 5 ; P_Y = SLN ; P_VALUE = STATUS<1,L>"L#10" ; P_OPT = ""
    P_X  := AM:17 ; P_Y := AM:SLN
    P_VALUE :=AM:OCONV(ST.DATE<1,L>,"D2/")
    P_X :=AM:27 ; P_Y :=AM:SLN 
    P_VALUE :=AM:USER<1,L>
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    CNT=CNT + 1
  NEXT L
  FOR M=CNT TO PAGE.SIZE
    SLN=BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
    P_X  = 5 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
    CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  NEXT M
5047 *
  RETURN
*
*
********************
PRINT.ALL.DATA: 
********************
*
  GOSUB ACTION.3
*
*print divisions
*
  LINES=DCOUNT(STATUS<1>,VM)
  OLD.START.LINE = 0
  GOSUB SHOW.LINES
  RETURN
*
*
*
**************
CLEAR.SCREEN: 
**************
*
  MAT SCV.REC = ""
  ECD.ACTION=6;CALL SCRN.EDIT
  RETURN
****************
GET.DATA: 
****************
*
  SCV.REC(1)<ESN>=STATUS
  SCV.REC(2)<ESN>=ST.DATE
  SCV.REC(3) = USER
  RETURN
*
**
*************
ACTION.1: 
*************
  ECD.ACTION = 1 ; CALL SCRN.EDIT ; RETURN
*************
ACTION.2: 
*************
  ECD.ACTION = 2 ; CALL SCRN.EDIT ; RETURN
*************
ACTION.3: 
*************
  ECD.ACTION = 3 ; CALL SCRN.EDIT ; RETURN
*************
ACTION.4: 
*************
  ECD.ACTION = 4 ; CALL SCRN.EDIT ; RETURN
*************
ACTION.5: 
*************
  ECD.ACTION = 5 ; CALL SCRN.EDIT ; RETURN
**
*---- CALLS FOR SYSCOM
91000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SYSCOM(MAT SYSCOM.REC)
99999*
  ECD.ACTION = 99 ; CALL SCRN.EDIT
END
