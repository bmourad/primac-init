*COPY>CPYLIB>COM1
*******************************************************
* REVISION    - [08.0]
* COPYRIGHT   - 1982 by C.B.A   (Vercom Software, Inc.)
* PROGRAM     - ACCRUED.LIAB.MAINT
* AUTHOR      -
* DATE        - 09/09/94
* DESCRIPTION -
* TASK 18746/17919 RKB 1/25/95
*T23278 markt 10/21/1998 * Add check for divisional security
*******************************************************
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>SECURITY
*COPY>POS.CPYLIB>ACCRUED.LIAB.HIST
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*
*-- OPEN FILES
*
   SYS.TYPE=1
   CALL SYSCOM(MAT SYSCOM.REC)
   OPEN "","ACCRUED.LIAB.HIST" TO ACCRUED.LIAB.HIST ELSE ERRMSG="CANNOT OPEN ACCRUED.LIAB.HISTFILE";GOTO 93000
   OPEN "","POS.SCREENS" TO M.SCREENS ELSE ERRMSG="CANNOT OPEN POS.SCREENS";GOTO 93000
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG="CANNOT OPEN COMPANY";GOTO 93000
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG="CANNOT OPEN CONTROL";GOTO 93000
   OPEN "","SECURITY" TO SECURITY ELSE ERRMSG="CANNOT OPEN SECURITY";GOTO 93000
*
*-- INIT
*
   CONO="";MAT COMP.REC=''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO='END' THEN GOTO 99999
   PORT.NO = "TTY"
   CALL SYSVARS.SUB(PORT.NO)
   READV OPER.ID FROM SECURITY, "R.":PORT.NO,2 ELSE OPER.ID = "???"
   MATREAD SEC.REC FROM SECURITY, CONO:OPER.ID ELSE SEC.NAME = "???"
   MAT EDIT.COM.DRIVER = ''
* MAT EDIT.COM = ''
* TYP = 0; CALL EDIT.SUB
   FILL = "#"
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'ACCRUED.LIAB.MAINT'
   ECD.ACTION=1;CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   BEGIN.PAGE=8; PAGE.SIZE=10; LINE.SPACE=1
   LN=1; OLD.START.LINE=0
   TODAY=DATE()
*
*-- MAIN PROCESS
*
100 *
   RELEASE
   LCNT = 0
   TMP.IDS = ""
   TMP.DATE = ""
   TMP.SRC = ""
   TMP.MON = ""
   TMP.ACCT = ""
   TMP.AMT = ""
   TMP.DVDPCC=''
   MAT SCV.REC = ""
   ECD.ACTION=6;CALL SCRN.EDIT
*
*-- ENTER PO TYPE
*
   ECD.NUM = 5
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' OR ECD.RET.VALUE = 'E' THEN GOTO 99999
   PO.TYPE = ECD.RET.VALUE
*
*-- ENTER PO NUMBER
*
120 *
   ECD.NUM = 6
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN GOTO 100
   PO.NUM = ECD.RET.VALUE
*
*-- ENTER PROD/JOB
*
130 *
   ECD.NUM = 7
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE = 'END' THEN GOTO 100
   PROD = ECD.RET.VALUE
*
*-- PROCESS RECORD
*
   ALH.ID = PO.TYPE:"*":PO.NUM:"*":PROD
   SELECTINDEX "ALH_REF",ALH.ID FROM ACCRUED.LIAB.HIST
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      MATREAD ALH.REC FROM ACCRUED.LIAB.HIST, ID THEN
*T23278 v
         DIV.CODE = ALH.DV.DP.CC[1,2]; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
         CALL CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
         IF ERRMSG # '' THEN
            GOSUB 91000; GOTO 100
         END
*T23278 ^
         LCNT = LCNT + 1
         TMP.IDS<LCNT> = ID
         TMP.DATE<LCNT> = ALH.DATE
         TMP.SRC<LCNT> = ALH.SRC
         TMP.MON<LCNT> = ALH.MON
         TMP.ACCT<LCNT> = ALH.ACCT
         TMP.AMT<LCNT> = ALH.AMT
         TMP.DVDPCC<LCNT> = ALH.DV.DP.CC
         TMP.CLOSED = ALH.CLOSED
         TMP.USER = ALH.USER
         TMP.COMMENT = ALH.COMMENT
      END
   REPEAT
   LINES = DCOUNT(TMP.IDS,AM)
   IF NOT(LINES) THEN
      ERRMSG = "No transactions exist"
      GOSUB 91000
      GOTO 100
   END
   SCV.REC(1) = TMP.CLOSED
   SCV.REC(3) = TMP.USER
   SCV.REC(4) = TMP.COMMENT
   ECD.ACTION=3; CALL SCRN.EDIT
   OLD.START.LINE=0; GOSUB 9000
*
*-- OPTIONS
*
   MORE = 1
   LOOP
      IF TMP.CLOSED = "" THEN
         ECD.NUM = 9
      END ELSE
         ECD.NUM = 10
      END
      ECD.ACTION = 4
      CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = 'E' OR OPTION = 'END'
            MORE = 0
         CASE OPTION = 'S' OR OPTION = 'SF'
            LN=LN+PAGE.SIZE-1
            LN=(PAGE.SIZE * INT(LN/PAGE.SIZE)) + 1
            IF LN > LINES THEN LN = 1
            GOSUB 9000
         CASE OPTION = "SR"
            LN = OLD.START.LINE - PAGE.SIZE
            IF LN < 1 THEN LN = 1
            GOSUB 9000
         CASE OPTION = "ST"
            LN = 1
            GOSUB 9000
         CASE OPTION = "SB"
            LN = LINES
            IF LN < 1 THEN LN = 1
            GOSUB 9000
         CASE OPTION = "C"
            FOR I = 1 TO LINES
               MATREADU ALH.REC FROM ACCRUED.LIAB.HIST, TMP.IDS<I> THEN
                  ALH.CLOSED = TODAY
                  ALH.USER = SEC.NAME
                  ALH.COMMENT = TMP.COMMENT
                  MATWRITE ALH.REC ON ACCRUED.LIAB.HIST, TMP.IDS<I>
               END ELSE
                  RELEASE ACCRUED.LIAB.HIST, TMP.IDS<I>
               END
            NEXT I
            MORE = 0
         CASE NUM(OPTION)
            GOSUB 1000
      END CASE
   WHILE MORE DO
   REPEAT
   GOTO 100
*
*-- ENTER COMMENT
*
1000*
   ECD.NUM = 4
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN
      TMP.COMMENT = ECD.RET.VALUE
   END
   RETURN
*
*-- SCROLL ROUTINE
*
9000 *
   START.LINE = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE
   IF START.LINE = OLD.START.LINE THEN RETURN
   OLD.START.LINE = START.LINE
   LAST.LINE = START.LINE + PAGE.SIZE - 1
   CNT = 1
   FOR N = START.LINE TO LAST.LINE UNTIL N > LINES
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = "CL"
      P_X  := AM:5 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(TMP.DATE<N>,"D2/")"L#8"
      P_X  := AM:15 ; P_Y := AM:SLN ; P_VALUE := AM:TMP.SRC<N>"L#3"
      P_X  := AM:20 ; P_Y := AM:SLN ; P_VALUE := AM:TMP.MON<N>"L#6"
      P_X  := AM:28 ; P_Y := AM:SLN ; P_VALUE := AM:TMP.ACCT<N>"L#14"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      DV=TMP.DVDPCC<N>[1,2]
      DP=TMP.DVDPCC<N>[3,2]
      CC=TMP.DVDPCC<N>[5,3]
      P_X  = 43 ; P_Y = SLN ; P_VALUE = DV"R#2" ; P_OPT = ""
      P_X  := AM:46 ; P_Y := AM:SLN ; P_VALUE := AM:DP"R#2"
      P_X  := AM:49 ; P_Y := AM:SLN ; P_VALUE := AM:CC"R#3"
      P_X  := AM:53 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(TMP.AMT<N>,"MD2,C")"R#14"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M-1,PAGE.SIZE)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M
   P_X  = 53 ; P_Y = 19 ; P_VALUE = OCONV(SUM(TMP.AMT),"MD2,C")"R#14" ; P_OPT = ""
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   RETURN
*
*-- CALLS FOR SYSCOM
*
91000 *
   ERR.TYPE=1 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE=2 ; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE=3 ; CALL SYSCOM(MAT SYSCOM.REC)
*
*-- END OF JOB
*
99999 *
   ECD.ACTION=99;CALL SCRN.EDIT
   RELEASE
END
