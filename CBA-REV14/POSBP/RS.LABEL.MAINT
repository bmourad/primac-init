*********************************************************************
* REVISION - [08.2B]
* PROGRAM  - RS.LABEL.MAINT
* AUTHOR   - W.R. MCKENZIE, COMPUTER BUSINESS ASSOCIATES
* DATE     - 06/18/90
* DESCRIPTION
* This program is used to define the data elements to be loaded to
* the P/C for label production.
*T26126 adelgado 02/26/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>POS.CPYLIB>RS.LABEL
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","RS.LABEL" TO RS.LABEL ELSE
    ERRMSG = "CANNOT OPEN RS.LABEL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","POS.SCREENS" TO POS.SCREENS ELSE
    ERRMSG = "CANNOT OPEN POS.SCREENS FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;RS.LABEL.MAINT;POS.SCREENS
  SCREEN FORMAT
  SCREEN COUNT;;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  LINE.CNT = 0
  REF.NO = ""
  CURR.REF.NO = ""
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  SCREEN CLEAR
110 *
  GOSUB 85000
  SCREEN FIELD;;2
  SCREEN INPUT;;2;GOTO 99999
  KEY = S$VALUE
  NEW.REC = 0
  * T26126 v
  MATREADU RS.LABEL.REC FROM RS.LABEL, CONO:KEY LOCKED
    ERRMSG = 'LABEL record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 110 
  END ELSE
  * T26126 ^
    MAT RS.LABEL.REC = ""
    NEW.REC = 1
  END
  IF NEW.REC THEN
    LINE.CNT = 0
    OPT = "A"
    GOTO 510
  END ELSE
    GOSUB 80000
    GOSUB 81000
    LINE.CNT = DCOUNT(RSL.ITEM,VM)
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 50000
  END
*
*---- GET OPERATOR REPLY
*
500 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      GOTO 100
    CASE OPT = "A" AND LINE.CNT < 99
      MODE = "A"
      DONE = 0
      FOR REF.NO = LINE.CNT+1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 700
        END ELSE
          LINE.CNT = LINE.CNT + 1
        END
      NEXT REF.NO
      REF.NO = LINE.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND LINE.CNT > 0
      MODE = "C"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10000
        IF S$VALUE = "END" THEN
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "D" AND LINE.CNT > 0
      MODE = "D"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        LINE.CNT = LINE.CNT - 1
        IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "I" AND LINE.CNT > 0
      MODE = "I"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 800
        LINE.CNT = LINE.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          GOSUB 700
          LINE.CNT = LINE.CNT - 1
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "S" OR OPT = "SF"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "SR"
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "ST"
      REF.NO = 1
      GOSUB 50000
    CASE OPT = "SB"
      REF.NO = LINE.CNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "F"
      IF RSL.ITEM = "" THEN
        DELETE RS.LABEL, CONO:KEY
      END ELSE
        MATWRITE RS.LABEL.REC ON RS.LABEL,CONO:KEY
      END
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET LINE NUMBER
*
600 *
  SCREEN FIELD;;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
  SCREEN INPUT;;22
  RETURN
*
*---- DELETE MULTI-LINE DATA
*
700 *
  RSL.ITEM = DELETE(RSL.ITEM,1,REF.NO,0)
  GOSUB 81000
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
800 *
  RSL.ITEM = INSERT(RSL.ITEM,1,REF.NO,0,"")
  GOSUB 81000
  RETURN
*
*---- GET MULTI-LINE DATA
*
10000 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;11
10100 *
  S$DATA(12)<S$SCR,REF.NO> = RSL.ITEM<1,REF.NO>
  S$VAL = REF.NO
  SCREEN FIELD;;12
  SCREEN INPUT;;12;GOTO 19950
  FCNT = DCOUNT(S$VALUE,";")
  FOR FPTR = 1 TO FCNT
    FLD = FIELD(S$VALUE,";",FPTR)
    LFLD = LEN(FLD)
    BEGIN CASE
      CASE NUM(FLD)
        IF FLD < 1 OR FLD > MAXITEM THEN
          ERRMSG = "Invalid item number at position ":FPTR:". Try again! "
          GOSUB 90000
          GOTO 10100
        END
      CASE COUNT(FLD,'"')=2 AND FLD[1,1]='"' AND FLD[LFLD,1]='"'
      CASE COUNT(FLD,"'")=2 AND FLD[1,1]="'" AND FLD[LFLD,1]="'"
      CASE 1
        ERRMSG = "Invalid constant at position ":FPTR:". Try again! "
        GOSUB 90000
        GOTO 10100
    END CASE
  NEXT FPTR
  TEMP.MF1 = S$VALUE
19900 *
  RSL.ITEM<1,REF.NO> = TEMP.MF1
  RETURN
19950 *
  GOSUB 81000
  RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
  SCREEN MULTI;;S;11;12
  GOSUB 85000  ;* DT
  RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
80050 *
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- DISPLAY OPTIONS
*
85000 *
  VALITM = ""
  VALITM<1,1>  = " 1- Roll/Skid Number"
  VALITM<1,2>  = " 2- Inventory Number"
  VALITM<1,3>  = " 3- Inventory Description"
  VALITM<1,4>  = " 4- Size"
  VALITM<1,5>  = " 5- Basis Weight"
  VALITM<1,6>  = " 6- Color"
  VALITM<1,7>  = " 7- Finish"
  VALITM<1,8>  = " 8- Major Line"
  VALITM<1,9>  = " 9- Product Line"
  VALITM<1,10> = "10- Vendor ID"
  VALITM<1,11> = "11- Vendor Name"
  VALITM<1,12> = "12- Vendor Item ID"
  VALITM<1,13> = "13- P/O Number"
  MAXITEM = DCOUNT(VALITM,VM)
85050 *
  S$DATA(20)<S$SCR> = VALITM
  S$VAL = 1
  S$CNT = MAXITEM
  SCREEN MULTI;;C;20
  RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
  S$DATA(12)<S$SCR> = RSL.ITEM
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*---- END OF PROGRAM
*
99999 *
  SCREEN CLEAR;;D
*       PRINT @(-1)
END
