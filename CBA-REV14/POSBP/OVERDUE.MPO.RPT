*COPY>CPYLIB>COM1
***************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - POSBP
* PROGRAM     - OVERDUE.MPO.RPT
* AUTHOR      - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE        - 08/27/84
* DESCRIPTION -
* This program produces the Overdue Miscellaneous Purchase
* Order Report sorted by Miscellaneous Purchase Order Number.
*T19444 rick 07/28/1995 * DUE DATE FIELD PRINTING PO.DUE.DATE RATHER
*                         THAN PRINTING PO.DEL.DATE. PO.DEL.DATE IS A
*                         MULTI-VALUED FIELD AND SHOULD PRINT RATHER
*                         THAN THE PO.DATE
* TASK 20080 JR ADD DIVISION TO LISTING
* T22398 renee 02/04/1998 * page # wrapping to next line
*T25880 lanny 06/13/2001 * Adds line-cnt of PO without regard to whether
*                          PO will print. Also, prints total line
*                          regardless of same condition.
*T25901 cmykleb 03/13/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T26871 thompson 10/07/2002 * FIX REPORT PROBLEMS
*ENDDOC
***************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>POS.CPYLIB>MISC.PO
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
* TASK 20080
**** READ COMPANY NAME FROM PROC
   PROCREAD ID ELSE
      ERRMSG = "CANNOT PERFORM PROCREAD"
      GOTO 93000
   END
*T26493 v
*  CONO = ID<1> ; CONO.NAME = ID<2>
   CONO = ID<1> ; REPORT.NUMBER = ID<2>
   CONO.NAME = ""
* T26871   START.DATE = ID<4>
* T26871   END.DATE = ID<5>
*T26493 ^
   TODAY = DATE()
   DIV.OWNER = ID<3>
* TASK 20080
*
*---- OPEN FILES
*
   OPEN "","MISC.PO" TO MISC.PO ELSE ERRMSG = "MISC.PO FILE MISSING";GOTO 93000
   OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "COMPANY FILE MISSING";GOTO 93000
   OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CONTROL FILE MISSING";GOTO 93000
*
*---- INITIALIZATION
*
   PG.NO = 0
   FIRST.FLAG = 1
   LINE.CNT = 0
   ERRMSG = ""
   RPT.NO = "XXXX"
   BY.SORT = "PO NUMBER"
   PREVIOUS.MPO.NUM = ""
   DISC.PRICE = ""
   DAYS.OVD = 0
   TOT.VALUE.OPEN = 0
   GRAND.TOT.VALUE.OPEN = 0
   ORDERED = 0
   RECVD = 0
   CANCEL = 0
   OPEN = 0
   VALUE.OPEN = 0
   MAT MPO.REC = ""
   PRINTER ON
*
*---- GET COMPANY NUMBER
*
*T20080  CONO = ""
*T20080  CALL GET.CONO(CONO,MAT COMP.REC)
*T20080  IF CONO = "END" THEN GOTO 99999
*
*---- GET REPORT HEADING
*
*T26493 v
*  REPORT.NAME = "OVERDUE MISCELLANEOUS PURCHASE ORDER REPORT"
*  REPORT.NUMBER = "XXXX"
   REPORT.NAME = ""
*T26493 ^
   HLINE1 = "" ; HLINE2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HLINE1,HLINE2)
*
*---- MAIN PROCESSING
*
10*
*T25880 IF FIRST.FLAG = 1 THEN FIRST.FLAG = 0; GOSUB 20
   READNEXT MPO.KEY ELSE GOTO 15
   MATREAD MPO.REC FROM MISC.PO,MPO.KEY ELSE GOTO 10
   OPEN.FLG = 0
   OPEN.CNT = COUNT(MPO.PROD.DESC,VM) + (MPO.PROD.DESC # "")
*CSF 23459 v
   FOR V = 1 TO OPEN.CNT UNTIL OPEN.FLG
      IF TODAY - MPO.DEL.DATE<1,V> > 0 THEN OPEN.FLG = 1
   NEXT V
   IF NOT(OPEN.FLG) THEN GOTO 10
   OPEN.FLG = 0
*CSF 23459 ^
*T25880 NEW.LINE.CNT = LINE.CNT + OPEN.CNT
*T25880 IF NEW.LINE.CNT = 50 OR NEW.LINE.CNT > 50 THEN GOSUB 20
   V = 0
   FOR V = 1 TO OPEN.CNT
*T25880 IF MPO.TOT.OPEN<1,V> > 0 THEN
      IF MPO.TOT.OPEN<1,V> > 0 AND TODAY - MPO.DEL.DATE<1,V> > 0 THEN
         PROD.NUM = MPO.PROD.NUM<1,V>
         PROD.DESC = MPO.PROD.DESC<1,V>
         GROS.PRICE = MPO.GROS.PRICE<1,V>
         DISCOUNT = MPO.DISCOUNT<1,V>
         TOT.ONORD = MPO.TOT.ONORD<1,V>
         TOT.OPEN = MPO.TOT.OPEN<1,V>
         TOT.RECEVED = MPO.TOT.RECEVED<1,V>
         TOT.CANCEL = MPO.TOT.CANCEL<1,V>
         GOSUB 30
         OPEN.FLG = 1
      END
   NEXT V
   IF OPEN.FLG = 1 THEN OPEN.FLG = 0; GOSUB 50
   GOTO 10
15*
   GOSUB 60
   GOTO 99999
*
*---- PRINT THE HEADING
*
20*
   PRINT CHAR(12):
   LINE.CNT = 0
   NEW.LINE.CNT = 0
   PG.NO = PG.NO + 1
*T25901 v
*  H.LINE = "";HLINE1 = "";HLINE2 = "";HLINE3 = ""
*  HLINE1 = "REPORT NO: ":RPT.NO"L#4":SPACE(26)
*  REMAIN = 50 - LEN(CONO.NAME)
*  HALF = INT(REMAIN / 2)
*  K = SPACE(HALF)
*  HLINE2 = K:CONO.NAME:K
*  HLINE3 = SPACE(31):"PAGE: ":PG.NO ; * T22398 FROM (33) TO (31)
*  H.LINE = HLINE1:HLINE2:HLINE3
*  PRINT H.LINE
*  H.LINE = ""
*  H.LINE = H.LINE:"DATE   : ":OCONV(DATE(),"D2/"):SPACE(27):"OVERDUE MISCELLANEOUS PURCHASE ORDER REPORT"
*  PRINT H.LINE
*  H.LINE = ""
*  H.LINE = H.LINE:"DIVISION : ":DIV.OWNER"L#3"
*  H.LINE = H.LINE:SPACE(55):"BY ":BY.SORT
*  PRINT H.LINE
   HLINE3 = "DIVISION : ":DIV.OWNER"L#3"
* T26871   HLINE3 = HLINE3 : SPACE(50) : START.DATE
* T26871   HLINE3 = HLINE3 : " THRU " : END.DATE
   PRINT HLINE1:PG.NO"R#3"
   PRINT HLINE2
   PRINT HLINE3
*T25901 ^
   PRINT
   H.LINE = ""
   H.LINE = H.LINE:"   PO    VENDOR      PRODUCT               PRODUCT             PO       DUE    ORDERED    RECVD     CANCEL     OPEN   DAYS   VALUE"
   PRINT H.LINE
   H.LINE = ""
   H.LINE = H.LINE:" NUMBER  NUMBER      NUMBER              DESCRIPTION          DATE     DATE":SPACE(43):"OVD    OPEN"
   PRINT H.LINE
   H.LINE = ""
   H.LINE = H.LINE:"-------- -------- --------------- ------------------------- -------- -------- --------- --------- --------- --------- --- ----------"
   PRINT H.LINE
   LINE.CNT = LINE.CNT + 7
   RETURN
*
*---- PRINT THE VALUES
*
30*
*    NEW.LINE.CNT = LINE.CNT + OPEN.CNT
*    IF NEW.LINE.CNT = 50 OR NEW.LINE.CNT > 50 THEN GOSUB 20
   IF FIRST.FLAG THEN FIRST.FLAG = 0; GOSUB 20 ;*T25880
   IF LINE.CNT > 55 THEN GOSUB 20 ;*T25880
   GOSUB 40
   P.LINE = ""
   IF MPO.NUM # PREVIOUS.MPO.NUM THEN
      P.LINE = P.LINE:MPO.NUM"L#8":" ":MPO.VEND.NO"L#8":" ":PROD.NUM"L#15":" ":PROD.DESC"L#25":" ":OCONV(MPO.DATE,"D2/"):" "
*19444 On next line chgd MPO.DUE.DATE TO MPO.DEL.DATE<1,V>
      P.LINE = P.LINE:OCONV(MPO.DEL.DATE<1,V>,"D2/")"L#8":" ":OCONV(ORDERED,"MD2")"R#9":" ":OCONV(RECVD,"MD2")"R#9":" ":OCONV(CANCEL,"MD2")"R#9":" ":OCONV(OPEN,"MD2")"R#9":" ":DAYS.OVD"L#3":" ":OCONV(VALUE.OPEN,"MD2")"R#10"
      PRINT P.LINE
      PREVIOUS.MPO.NUM = MPO.NUM
   END ELSE
*19444 On next line chgd MPO.DUE.DATE TO MPO.DEL.DATE<1,V>
      P.LINE = P.LINE:SPACE(18):PROD.NUM"L#15":" ":PROD.DESC"L#25":" ":OCONV(MPO.DATE,"D2/")"L#8":" ":OCONV(MPO.DEL.DATE<1,V>,"D2/")"L#8":" "
      P.LINE = P.LINE:OCONV(ORDERED,"MD2")"R#9":" ":OCONV(RECVD,"MD2")"R#9":" ":OCONV(CANCEL,"MD2")"R#9":" ":OCONV(OPEN,"MD2")"R#9":" ":DAYS.OVD"L#3":" ":OCONV(VALUE.OPEN,"MD2")"R#10"
      PRINT P.LINE
   END
   LINE.CNT = LINE.CNT + 1
   DAYS.OVD = 0;ORDERED = 0;RECVD = 0;CANCEL = 0;OPEN = 0;VALUE.OPEN = 0
   DISC.PRICE = ""
   RETURN
*
*---- GET FIELD VALUES, CALCULATE AND ACCUMULATE
*
40*
   MPO.NUM = MPO.KEY[4,8]
*T25880 v DAYS.OVD = DATE() - MPO.DUE.DATE
   IF MPO.DEL.DATE<1,V> = '' THEN MPO.DEL.DATE<1,V> = MPO.DUE.DATE
   DAYS.OVD = DATE() - MPO.DEL.DATE<1,V>
   IF DISCOUNT > 0 THEN
      DISC.PRICE = GROS.PRICE - (INT(((GROS.PRICE * DISCOUNT) / 10000)))
   END ELSE
      DISC.PRICE = GROS.PRICE
   END
   ORDERED = TOT.ONORD
   RECVD = TOT.RECEVED
   CANCEL = TOT.CANCEL
   OPEN = TOT.OPEN
*    VALUE.OPEN = INT((DISC.PRICE / 100) * (OPEN / 100))
   VALUE.OPEN = INT((DISC.PRICE * OPEN) / 10000 + .5)
   TOT.VALUE.OPEN = TOT.VALUE.OPEN + VALUE.OPEN
   GRAND.TOT.VALUE.OPEN = GRAND.TOT.VALUE.OPEN + VALUE.OPEN
   RETURN
*
*---- PRINT TOTALS
*
50*
   T.LINE = ""
   T.LINE = T.LINE:SPACE(122):"----------"
   PRINT T.LINE
   T.LINE = "P/O TOTAL""R#121":SPACE(1)
   T.LINE = T.LINE:OCONV(TOT.VALUE.OPEN,"MD2")"R#10"
   PRINT T.LINE
   TOT.VALUE.OPEN = 0
   PRINT
   LINE.CNT = LINE.CNT + 3
   RETURN
*
*---- PRINT GRAND TOTAL
*
60*
   PRINT
   GT.LINE = ""
   GT.LINE = GT.LINE:SPACE(122):"----------"
   PRINT GT.LINE
   GT.LINE = "GRAND TOTAL""R#121":SPACE(1)
   GT.LINE = GT.LINE:OCONV(GRAND.TOT.VALUE.OPEN,"MD2")"R#10"
   PRINT GT.LINE
   PRINTER OFF
   RETURN
*
*---- CALLS FOR SYSCOM
*
91000*
   ERR.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000*
   ERR.TYPE = 2
   CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000*
   ERR.TYPE = 3
   CALL SYSCOM(MAT SYSCOM.REC)
99999*
   PRINT @(-1):
END
