   SUBROUTINE MISC.PO.IN.REC.MAINT(CONO,MPO.CODE,MISC.REC)
*COPY>CPYLIB>COM1
*COPY>POS.CPYLIB>COM.MPO
*COPY>POS.CPYLIB>COM.PO.INTRF
**************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - POSBP
* PROGRAM     - MISC.PO.IN.MAINT
* BY          - GRAHAM JARVIS, PRISYS PTY LTD
* DATE        - 09 FEB 90
* DESCRIPTION - THIS PROGRAM TAKE INPUT FOR LINE ITEMS ON MISCELLANEOUS ORDER.
* MOD CLW 10/01/94 Task 17919 - Put in to standard
*T25463 lanny 10/16/2000 * Misc Po Maint & Receipts both call
*                          MISC.VEND.STAT.UPDATE where the on-order &
*                          Receipt data is updated. This causes an
*                          overwrite to receipt data if price changes
*                          from one receipt to another.
*T25978 adelgado 02/07/2002 * Add the use of prompts (S,SR,SB,ST).
****     NOTE! If this program is loaded to a user site, make sure to ****
****  !!       load MISC.PO.MAINT, MISC.PO.IN.MAINT, MISC.PO.REC.MAINT ***
****  !!       MISC.PO.IN.REC.MAINT & MISC.VEND.STAT.UPDATE since all ****
****  !!       have an updated argument list and CPYLIB change.       ****
*T26186 lhelms 05/02/2002 * REV12 UPGRADE DEFAULT DATE RECV FROM
*                           MPO.DATE.RECV
**************************************************************************
**************************************************************
*COPY>POS.CPYLIB>MISC.PO
*COPY>ICS.CPYLIB>CATEGORY.MISC
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>PMC.CPYLIB>VEND
*COPY>APS.CPYLIB>VEND.PROD.STATS ;*T25463
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>APS.CPYLIB>APS.FILE.VARS
*COPY>CPYLIB>CHAR
*
* Setup ERRMSG routine
*
   SYS.TYPE = 1
   CALL SYSCOM(MAT SYSCOM.REC)
*
* Initialization
*
OPEN '','CONTROL' TO CONTROL ELSE RETURN
   BEGIN.PAGE.1 = 7
   PAGE.SIZE.1 = 1
   LINE.SPACE = 6
   BEGIN.PAGE.2 = 16
   PAGE.SIZE.2 = 4
   LINE.SPACE.2 = 1
   FILL = '#'
   ECD.SCRN.NO = 2
   ECD.ACTION=2;CALL SCRN.EDIT
   LINES.1 = 0 ; S.LINES.1 = 0
   OLD.START.LINE.1 = 0
   OLD.START.LINE.2 = 0
   TODAY = OCONV(DATE(),"D2/")
*
* Main processing
*
GFR = ""
GFR<1>="IN MISC.IN.REC "; WRITE GFR ON CONTROL,"111609"
   SCV.REC(5)<ECD.SCRN.NO,1> = MPO.VEND.NO
   ECD.NUM = 5
   ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(6)<ECD.SCRN.NO,1> = VEND.DESC
   ECD.NUM = 6
   ECD.ACTION=5;CALL SCRN.EDIT
   SCV.REC(7)<ECD.SCRN.NO,1> = MPO.CODE
   ECD.NUM = 7
   ECD.ACTION=5; CALL SCRN.EDIT
   LINES.1 = COUNT(MPO.PROD.DESC,VM) + (MPO.PROD.DESC # "")
   S.LINES.1 = MPO.SEQ.NO<1,LINES.1>
   LN1 = 1
   GOSUB 10000
   GOSUB 9000
   GOTO 99999
*
* Calculate quantities received
*
GFR<-1>="IN MISC.IN.REC  1000 "; WRITE GFR ON CONTROL,"111609"
1000 *
   IF MPO.TOT.RECEVED<1,LN1>='' THEN
      MPO.TOT.RECEVED<1,LN1>=0
   END 
   P_X  = 53 ; P_Y = SLN+1 ; P_VALUE = "Rec'd " ; P_OPT = ""
   P_X  := AM:59 ; P_Y := AM:SLN+1 ; P_VALUE := AM:OCONV(MPO.TOT.RECEVED<1,N>,"MD2") "R#8"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*
* Display open quantities
*
1010*
   IF MPO.SEQ.NO<1,LN1> = "" THEN
      S.LINES.1 = S.LINES.1 + 1
      MPO.SEQ.NO<1,LN1> = S.LINES.1
   END
   LINES.1 = COUNT(MPO.PROD.DESC,VM) + (MPO.PROD.DESC # '')
   RETURN
*
* Enter option
*
9000*
   MORE = 1
   LOOP
      ECD.NUM = 31
      SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
      ECD.ACTION = 4
      CALL SCRN.EDIT
      OPTION = ECD.RET.VALUE
      BEGIN CASE
         CASE OPTION = 'E' OR OPTION = 'END'
            MORE = 0
         CASE OPTION = 'R'
            GOSUB 60000
            IF LNO THEN
               LN1 = LNO
               SLN = BEGIN.PAGE.1 + LINE.SPACE * REM(LN1-1,PAGE.SIZE.1)
               P_X  = 4 ; P_Y = SLN ; P_VALUE = "*" ; P_OPT = ""
               CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
               LINES.2 = COUNT(MISC.REC<LN1>,VM) + (MISC.REC<LN1> # "")
               LN2 = 1
               OLD.START.LINE.2 = 0
               GOSUB 20000
               P_X  = 4 ; P_Y = SLN ; P_VALUE = " " ; P_OPT = ""
               CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            END
         CASE OPTION = 'S'
            LN1 = LN1 + PAGE.SIZE.1
            IF LN1 > LINES.1 THEN LN1 = 1
            GOSUB 10000
      * T25978 v
         CASE OPTION = 'SR'
            LN1 -= PAGE.SIZE.1
            IF LN1 < 1 THEN LN1 = 1
            GOSUB 10000
         CASE OPTION = 'ST'
            LN1 = 1
            GOSUB 10000
         CASE OPTION = 'SB'
            LN1 = LINES.1
            GOSUB 10000
      * T25978 ^
      END CASE
   WHILE MORE = 1 DO REPEAT
   RETURN
*
* Scroll routine for product
*
10000*
   START.LINE.1 = 1 + INT((LN1-1)/PAGE.SIZE.1) * PAGE.SIZE.1
   IF START.LINE.1 = OLD.START.LINE.1 THEN GOTO 10001
   OLD.START.LINE.1 = START.LINE.1
   LAST.LINE.1 = START.LINE.1 + PAGE.SIZE.1 - 1
   CNT = 1
   FOR N = START.LINE.1 TO LAST.LINE.1 UNTIL N > LINES.1
      SLN = BEGIN.PAGE.1 + LINE.SPACE * REM(N-1,PAGE.SIZE.1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = N "R#3" ; P_OPT = ""
      P_X  := AM:6 ; P_Y := AM:SLN ; P_VALUE := AM:MPO.PROD.NUM<1,N> "L#15"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      FOR D.LINE = 1 TO 5
         P_X  = 6 ; P_Y = SLN+D.LINE ; P_VALUE = MPO.PROD.DESC<1,N,D.LINE> "L#30" ; P_OPT = ""
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT D.LINE
      P_X  = 22 ; P_Y = SLN ; P_VALUE = OCONV(MPO.DEL.DATE<1,N>, "D2/") "R#8" ; P_OPT = ""
      P_X  := AM:31 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MPO.GROS.PRICE<1,N> , "MD4") "R#12"
      P_X  := AM:40 ; P_Y := AM:SLN+1 ; P_VALUE := AM:OCONV(MPO.DISCOUNT<1,N> , "MD2") "R#5"
      P_X  := AM:46 ; P_Y := AM:SLN ; P_VALUE := AM:MPO.UNIT.MAG<1,N> "L#3"
      P_X  := AM:50 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MPO.TOT.ONORD<1,N> , "MD2") "R#8"
      P_X  := AM:59 ; P_Y := AM:SLN ; P_VALUE := AM:OCONV(MPO.TOT.CANCEL<1,N>,"MD2") "R#8"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      ACT.PRICE = INT(MPO.GROS.PRICE<1,N> * (1 - (MPO.DISCOUNT<1,N>/10000)))
      LINE.VAL = MPO.TOT.ONORD<1,N>/100 * ACT.PRICE / 10000
      P_X  = 68 ; P_Y = SLN ; P_VALUE = LINE.VAL "R2#12" ; P_OPT = ""
      P_X  := AM:53 ; P_Y := AM:SLN+1 ; P_VALUE := AM:"Rec'd "
      P_X  := AM:59 ; P_Y := AM:SLN+1 ; P_VALUE := AM:OCONV(MPO.TOT.RECEVED<1,N>,"MD2") "R#8"
      P_X  := AM:53 ; P_Y := AM:SLN+2 ; P_VALUE := AM:"Open "
      P_X  := AM:59 ; P_Y := AM:SLN+2 ; P_VALUE := AM:OCONV(MPO.TOT.OPEN<1,N>,"MD2") "R#8"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N
   FOR M = CNT TO PAGE.SIZE.1
      SLN = BEGIN.PAGE.1 + LINE.SPACE * REM(M-1,PAGE.SIZE.1)
      P_X  = 0 ; P_Y = SLN ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      FOR D.LINE = 1 TO 5
         P_X  = 0 ; P_Y = SLN+D.LINE ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      NEXT D.LINE
   NEXT M
   FOR I = 1 TO PAGE.SIZE.2
      P_X  = 0 ; P_Y = BEGIN.PAGE.2 + I - 1 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT I
10001*
   RETURN
*
* Input receipt details
*
20000 *
   GOSUB 30000
   ECD.NUM = 32
   SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
   ECD.ACTION = 4
   CALL SCRN.EDIT
   ACTION = ECD.RET.VALUE
   BEGIN CASE
      CASE ACTION = "E" OR ACTION = "END"
         RETURN
      CASE ACTION = "A"
         NUM.RECS = COUNT(MISC.REC<LN1>,VM) + (MISC.REC<LN1> # "")
         LN2 = NUM.RECS + 1
         GOSUB 30000
         GOSUB 50000
      CASE ACTION = "C"
         GOSUB 70000
         IF LN.NO THEN
            LN2 = LN.NO
            GOSUB 30000
            SLN2 = BEGIN.PAGE.2 + LINE.SPACE.2 * REM(LN2-1,PAGE.SIZE.2)
            P_X  = 0 ; P_Y = SLN2 ; P_VALUE = "" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            GOSUB 50010
         END
      CASE ACTION = "D"
         GOSUB 70000
         IF LN.NO THEN
            LN2 = LN.NO
            MPO.TOT.RECEVED<1,LN1> = MPO.TOT.RECEVED<1,LN1> - MISC.REC<LN1,LN2,2>
            MISC.REC = DELETE(MISC.REC,LN1,LN2,0)
            LINES.2 = COUNT(MISC.REC<LN1>,VM) + (MISC.REC<LN1> # "")
            MPO.TOT.OPEN<1,LN1> = MPO.TOT.ONORD<1,LN1> - MPO.TOT.CANCEL<1,LN1> - MPO.TOT.RECEVED<1,LN1>
            IF MPO.TOT.OPEN<1,LN1> < 0 THEN MPO.TOT.OPEN<1,LN1> = 0
            P_X  = 59 ; P_Y = SLN+1 ; P_VALUE = OCONV(MPO.TOT.RECEVED<1,LN1>,"MD2") "R#8" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            P_X  := AM:59 ; P_Y := AM:SLN+2 ; P_VALUE := AM:OCONV(MPO.TOT.OPEN<1,LN1>,"MD2") "R#8" ; P_OPT = "CL"
            CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
            IF LN2 > 1 AND LN2 > LINES.2 THEN LN2 = LN2 - 1
            IF LN2 < 1 THEN LN2 = 1
            OLD.START.LINE.2 = 0
         END
      CASE ACTION = "S"
         LN2 = LN2 + PAGE.SIZE.2
         NUM.RECS = COUNT(MISC.REC<LN1>,VM) + (MISC.REC<LN1> # "")
         IF LN2 > NUM.RECS THEN LN2 = 1
    * T25978 v
      CASE ACTION = 'SR'
         LN2 -= PAGE.SIZE.2
         IF LN2 < 1 THEN LN2 = 1
      CASE ACTION = 'ST'
         LN2 = 1
      CASE ACTION = 'SB'
         LN2 = COUNT(MISC.REC<LN1>,VM) + (MISC.REC<LN1> # "")
    * T25978 ^
   END CASE
   GOTO 20000
*
* Scroll details for receipts entered in this update ONLY
*
30000*
   START.LINE.2 = 1 + INT((LN2-1)/PAGE.SIZE.2) * PAGE.SIZE.2
   IF START.LINE.2 = OLD.START.LINE.2 THEN GOTO 30099
   OLD.START.LINE.2 = START.LINE.2
   LAST.LINE.2 = START.LINE.2 + PAGE.SIZE.2 - 1
   CNT = 1
   FOR N2 = START.LINE.2 TO LAST.LINE.2 UNTIL N2 > LINES.2
      SLN2 = BEGIN.PAGE.2 + LINE.SPACE.2 * REM(N2-1,PAGE.SIZE.2)
      P_X  = 23 ; P_Y = SLN2 ; P_VALUE = N2 "R#3" ; P_OPT = ""
      P_X  := AM:31 ; P_Y := AM:SLN2 ; P_VALUE := AM:OCONV(MISC.REC<LN1,N2,1> , "D2/")
      P_X  := AM:45 ; P_Y := AM:SLN2 ; P_VALUE := AM:OCONV(MISC.REC<LN1,N2,2> , "MD2") "R#8"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      CNT = CNT + 1
   NEXT N2
   FOR M2 = CNT TO PAGE.SIZE.2
      SLN2 = BEGIN.PAGE.2 + LINE.SPACE.2 * REM(M2-1,PAGE.SIZE.2)
      P_X  = 0 ; P_Y = SLN2 ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   NEXT M2
30099*
   RETURN
*
* Enter receipt details
*
50000*
   GOSUB 30000
   SLN2 = BEGIN.PAGE.2 + LINE.SPACE.2 * REM(LN2-1,PAGE.SIZE.2)
*
* Enter receipt date
*
50010*
   P_X  = 23 ; P_Y = SLN2 ; P_VALUE = LN2 "R#3" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   X = 31 ; Y = SLN2 ; TYP = 6 ; O.R = "O" ; MAXL = 8
   IF ACTION = "A" THEN
* T 26286
      DEFAULT = OCONV(MPO.DATE.RECVD,"D2/")
   END ELSE
      DEFAULT = OCONV(MISC.REC<LN1,LN2,1>,"D2/")
   END
   CALL EDIT.SUB
   IF VALUE = "END" THEN
      IF ACTION = "A" THEN
         P_X  = 0 ; P_Y = SLN2 ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      GOTO 50099
   END
   SAVE.DATE = VALUE
*
* Enter receipt quantity
*
   X = 45 ; Y = SLN2 ; TYP = 4 ; SCALER = 2
   MINV = -999999 ; MAXV = 999999 ; MAXL = 8
   IF ACTION = "A" THEN O.R = "R" ELSE
      O.R = "O"
      DEFAULT = OCONV(MISC.REC<LN1,LN2,2>,"MD2")
   END
   OLD.QTY = MISC.REC<LN1,LN2,2> + 0
   CALL EDIT.SUB
   IF VALUE = "END" THEN
      IF ACTION = "A" THEN
         P_X  = 0 ; P_Y = SLN2 ; P_VALUE = "" ; P_OPT = "CL"
         CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      END
      GOTO 50099
   END
   MISC.REC<LN1,LN2,1> = SAVE.DATE
   MISC.REC<LN1,LN2,2> = VALUE
   MPO.TOT.RECEVED<1,LN1> = MPO.TOT.RECEVED<1,LN1> - OLD.QTY + MISC.REC<LN1,LN2,2>
   IF MPO.TOT.OPEN<1,LN1> < MISC.REC<LN1,LN2,2> - OLD.QTY THEN
      ERRMSG = 'WARNING - MORE RECEIVED THAN OPEN'
      GOSUB 91000
      MPO.TOT.OPEN<1,LN1> = 0
   END ELSE
      IF MPO.TOT.ONORD<1,LN1> <= MPO.TOT.RECEVED<1,LN1> THEN
         MPO.TOT.OPEN<1,LN1> = 0
      END ELSE
         MPO.TOT.OPEN<1,LN1> = MPO.TOT.OPEN<1,LN1> + OLD.QTY - MISC.REC<LN1,LN2,2>
      END
   END
   P_X  = 59 ; P_Y = SLN+1 ; P_VALUE = OCONV(MPO.TOT.RECEVED<1,LN1>,"MD2") "R#8" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
   P_X  = 59 ; P_Y = SLN+2 ; P_VALUE = OCONV(MPO.TOT.OPEN<1,LN1>,"MD2") "R#8" ; P_OPT = "CL"
   CALL VSI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*T25463 v
   IF MISC.REC<LN1,LN2,2> < 0 THEN
      VPDS.KEY=CONO:MPO.VEND.NO:"!M!":MPO.CODE:"!":MPO.SEQ.NO<1,LN1>:"!":MPO.SHIP.WHSE
      MATREAD VPDS.REC FROM VEND.PROD.STATS,VPDS.KEY ELSE MAT VPDS.REC = ''
      PTR = 1
      LOOP
         LOCATE MISC.REC<LN1,LN2,1> IN VPDS.REC.DATE<1>,PTR SETTING DFND ELSE DFND = 0
         BEGIN CASE
            CASE DFND = 0
               PTR = 0
            CASE VPDS.REC.QTY<1,DFND> > 0
               MISC.REC<LN1,LN2,3> = VPDS.REC.UN.COST<1,DFND>
               PTR = 0
            CASE 1
               PTR = DFND + 1
         END CASE
      WHILE PTR DO REPEAT
   END
   IF MISC.REC<LN1,LN2,3>+0 = 0 THEN
      MISC.REC<LN1,LN2,3> = INT((MPO.GROS.PRICE<1,LN1> * (MPO.DISCOUNT<1,LN1>/10000)))
      MISC.REC<LN1,LN2,3> = INT(MPO.GROS.PRICE<1,LN1> - MISC.REC<LN1,LN2,3>)
   END
*T25463 ^
   IF ACTION = 'A' THEN
      LINES.2 = LINES.2 + 1
      LN2 = LN2 + 1
      GOTO 50000
   END
50099*
   RETURN
*
* Enter misc. po line number
*
60000*
   GOSUB 10000
   ECD.NUM = 33
   SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
   ECD.MINV = OLD.START.LINE.1
   IF LAST.LINE.1 < LINES.1 THEN
      ECD.MAXV = LAST.LINE.1
   END ELSE
      ECD.MAXV = LINES.1
   END
   ECD.ACTION = 4
   CALL SCRN.EDIT
   LNO = ECD.RET.VALUE
   IF LNO = '' OR LNO = 'END' THEN LNO = 0
   RETURN
*
* Enter receipt line number
*
70000*
   GOSUB 30000
   ECD.NUM = 33
   SCV.REC(ECD.NUM)<ECD.SCRN.NO> = ""
   ECD.MINV = OLD.START.LINE.2
   IF LAST.LINE.2 < LINES.2 THEN
      ECD.MAXV = LAST.LINE.2
   END ELSE
      ECD.MAXV = LINES.2
   END
   ECD.ACTION = 4
   CALL SCRN.EDIT
   LN.NO = ECD.RET.VALUE
   IF LN.NO = '' OR LN.NO = 'END' THEN LN.NO = 0
   RETURN
*
* Calls for SYSCOM
*
91000 ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2; CALL SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * PRINT * @(-1) * :
   ECD.ACTION=99;CALL SCRN.EDIT
   RETURN
   END

