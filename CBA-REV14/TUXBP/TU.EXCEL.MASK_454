SUBROUTINE TU.EXCEL.MASK_454(CONV, MASK)
 INCLUDE TUBP USER.INCLUDE.H
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
***************************************************************************
*-* TU.EXCEL.MASK(Conv, Mask)
*-*
*-*       This function will convert a pick OCONV code into a EXCEL mask
*-*
*-* [P] Conv          This is the pick conversion code
*-*
*-* [R] Mask          This is the EXCEL formatting mask
***************************************************************************
*
 INCLUDE TUBP SPECIAL.H
 INCLUDE TUBP TU.API.H
*
EQU TRUE TO 1
EQU FALSE TO 0
*
IF TU.TRACE THEN
OPEN '','TUSTATE' TO TUSTATE THEN
WRITEV ('TU.EXCEL.MASK':VM:CONV) ON TUSTATE,('TRACE.':PORTNO),-1
END
END
*
MASK = ''
IF CONV[1, 2] = 'MR' OR CONV[1, 2] = 'ML' OR CONV[1, 2] = 'MD' THEN
DECIMALS = 0; SCALE = 1; THOUSANDS = 0; DOLLAR = 0; NEGATIVE = 0; ZERO = 0
P = 3
IF CONV[P, 1] MATCHES '1N' THEN
DECIMALS = CONV[P, 1]
P = P + 1
IF CONV[P, 1] MATCHES '1N' THEN
SCALE = PWR(10, CONV[P, 1])
P = P + 1
END
L = LEN(CONV)
FOR J = P TO L
C = CONV[J, 1]
BEGIN CASE
CASE C = COMMA; THOUSANDS = 1
CASE C = '$'; DOLLAR = 1
CASE C = '-'; NEGATIVE = 1
CASE C = 'Z'; ZERO = 1
CASE C = '#' OR C = '*'
WIDTH = ''
10 *
IF CONV[J + 1, 1] MATCHES '1N' THEN
J = J + 1
WIDTH = WIDTH:CONV[J , 1]
GOTO 10
END
IF WIDTH THEN
MASK = STR('#', WIDTH)
END ELSE
MASK = C
20 *
IF CONV[J + 1, 1] = C THEN
J = J + 1
MASK = MASK:CONV[J, 1]
GOTO 20
END
END
CASE C = '%'
WIDTH = ''
15 *
IF CONV[J + 1, 1] MATCHES '1N' THEN
J = J + 1
WIDTH = WIDTH:CONV[J , 1]
GOTO 15
END
IF WIDTH THEN
MASK = STR('0', WIDTH)
END ELSE
25 *
IF CONV[J + 1, 1] = C THEN
J = J + 1
MASK = MASK:CONV[J, 1]
GOTO 25
END
END
END CASE
NEXT J
IF MASK = '' THEN MASK = STR('#', 12)
L = LEN(MASK)
IF NOT(ZERO) THEN
MASK = MASK[1, L - 1]:'0'
END
IF THOUSANDS THEN
FOR J = L TO 1 STEP -3
IF J # L THEN
MASK = MASK[1, J]:COMMA:MASK[J + 1, 9999]
END
NEXT J
END
IF DOLLAR THEN
MASK = '$':MASK
END
IF DECIMALS THEN
MASK = MASK:'.':STR('0', DECIMALS)
END
IF NEGATIVE THEN
MASK = MASK:');(':MASK:')'
END
CONV = CONV[1,2]:DECIMALS
END
END
*
IF CONV[1,2] = 'MT' THEN
IF INDEX(CONV, 'S', 1) THEN
MASK = 'h:mm:ss'
END ELSE
MASK = 'h:mm'
END
IF INDEX(CONV, 'H', 1) THEN
MASK = MASK:' AM/PM'
END
END
*
IF CONV[1,1] = 'D' THEN
YEARLEN = 2; SEPARATOR = ' '; INTDATE = ""
CALL TU.CLIENT.GETEND_454("SBODATE", INTDATE)
IF INDEX(INTDATE, "INTERNATIONAL", 1) THEN
INTDATE = TRUE
END ELSE
INTDATE = FALSE
END
P = 2
IF CONV[P, 1] MATCHES '1N' THEN
YEARLEN = CONV[P, 1]
P = P + 1
IF CONV[P, 1] # '' THEN
SEPARATOR = CONV[P, 1]
P = P + 1
END
IF SEPARATOR = ' ' THEN
MASK = 'd mmm ':STR('y', YEARLEN)
END ELSE
IF INTDATE THEN
MASK = 'd':SEPARATOR:'m':SEPARATOR:STR('y', YEARLEN)
END ELSE
MASK = 'm':SEPARATOR:'d':SEPARATOR:STR('y', YEARLEN)
END
END
CONV = 'D':YEARLEN
END
END
RETURN
END
