SUBROUTINE RPC.FILEINFO.UV(ARRAY, VALUE, ERROR)
 INCLUDE TUBP USER.INCLUDE.H
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
*
* Description : Stored Procedure
*
 INCLUDE TUBP SPECIAL.H
*
*
PERM = "---":AM:"--x":AM:"-w-":AM:"-wx":AM:"r--":AM:"r-x":AM:"rw-":AM:"rwx"
VALUE = ""
ERROR = 0
*
FILE1 = ARRAY<1>
OPEN FILE1 TO FN1 ELSE
VALUE = 'Illegal File ':FILE1
ERROR = 1
RETURN
END
OPEN '','VOC' TO MD ELSE
OPEN '','MD' TO MD ELSE
VALUE = 'Cannot locate MD or VOC'
ERROR = 1
RETURN
END
END
IF FILE1[1,5] = 'DICT ' THEN
FILE1 = FILE1[6,999]
END
READ DPTR FROM MD,FILE1 ELSE DPTR = '?'
STATS = ''
STATUS STATS FROM FN1 ELSE
VALUE = 'Unable To Retreive File Stats'
ERROR = 1
RETURN
END
VALUE<-1> = "File Definition - ":DPTR<1>
VALUE<-1> = "File Type - ":STATS<21>
VALUE<-1> = "File Path - ":STATS<20>
VALUE<-1> = "File Size - ":STATS<6>
VALUE<-1> = "Modulo - ":STATS<22>
VALUE<-1> = "Seperation - ":STATS<23>
*
PERMS = OCONV(STATS<5>, "MO")
P = PERM<PERMS[4,1]+1>
P = P:PERM<PERMS[5,1]+1>
P = P:PERM<PERMS[6,1]+1>
P = PERM<8>
VALUE<-1> = "Permissions - ":P
VALUE<-1> = "User ID Of Owner - ":STATS<8>
VALUE<-1> = "Group ID Of Owner - ":STATS<9>
VALUE<-1> = "Time/Date Of Last Access - ":OCONV(STATS<13>, "MTS"):" ":OCONV(STATS<14>, "D4")
VALUE<-1> = "Time/Date Of Last Modification - ":OCONV(STATS<15>, "MTS"):" ":OCONV(STATS<16>, "D4")
VALUE<-1> = "Time/Date Of Last Status Change - ":OCONV(STATS<17>, "MTS"):" ":OCONV(STATS<18>, "D4")
VALUE<-1> = "Hard Link Count - ":STATS<7>
VALUE<-1> = "Inode Number - ":STATS<10>
VALUE<-1> = "Inode Device Number - ":STATS<11>
VALUE<-1> = "Character/Block Device Number - ":STATS<12>
RETURN
END
