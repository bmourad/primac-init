SUBROUTINE TU.SCRIPT.CREATE(HANDLE, I.TIMEOUT, USESAFESUBSET, RESERVED, OPTIONS, STATUS)
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
***************************************************************************
*-* Tu.Script.Create(Handle, Timeout, UseSafeSubSet, Reserved, Options,
*-*                  Status)
*-*
*-* This function will create an instance of the SBClientScriptControl
*-* object. If the object has been previously created then the handle to
*-* the previous instance is returned. Otherwise the handle to the created
*-* instance is returned.
*-*
*-* [R] Handle          Handle to the SBClientScriptControl object.
*-*
*-* [P] Timeout         Length of time in milliseconds that a Visual
*-*                     Basic script can execute before being considered
*-*                     hung. Default value is 10000.
*-*
*-* [P] UseSafeSubSet   Boolean value which tells the scritping engine to
*-*                     execute in safe mode. If set this will disallow
*-*                     potential harmful operations. Default value is True.
*-*
*-* [P] Resrved         Argument reserved for future use.
*-*
*-* [P] Options         This is an array which indicates minor options of
*-*                     the API. Possible values:-
*-*
*-*                       I, Don't perform a TU.INIT
*-*
*-* [R] Status          A zero status indicates a successful call. A non
*-*                     zero status indicates a failure.
*-*
***************************************************************************
*
 INCLUDE TUBP TU.API.H
 INCLUDE TUBP SPECIAL.H
 INCLUDE TUBP USER.INCLUDE.H
 INCLUDE TUBP TU.ERRORCODES.H
 INCLUDE TUBP ROC.H
 INCLUDE TUBP OBJECT.H
 INCLUDE TUBP SCRIPT.H
*
*-* Init
*
DEBUGFLAG = FALSE; STATUS = FALSE; HROC = 0
IF NUM(USESAFESUBSET) AND USESAFESUBSET > 0 THEN
BSAFE = TRUE
END ELSE
BSAFE = FALSE
END
IF NUM(I.TIMEOUT) AND I.TIMEOUT < 1 THEN
I.TIMEOUT = 10000
END
*
IF DEBUGFLAG THEN CRT TIMEDATE():": Calling Tu.Init"
CALL TU.INIT(STATUS)
IF STATUS THEN RETURN
*
IF DEBUGFLAG THEN CRT TIMEDATE():": Getting handle to ROC"
CALL ROC.GETHANDLE("this", HROC, STATUS)
IF STATUS AND NOT(HROC) THEN RETURN
*
*-* Create SBClientScriptControl
*
IF DEBUGFLAG THEN CRT TIMEDATE():": Getting handle to session class"
ATTR = SESSIONMASTER; HSESS = 0
CALL ROC.GET(HROC, ATTR, HSESS, STATUS)
IF STATUS AND NOT(HSESS) THEN RETURN
*
IF DEBUGFLAG THEN CRT TIMEDATE():": Getting handle to Script Engine"
ATTR = VBSCRIPT; HVB = 0
CALL ROC.GET(HSESS, ATTR, HVB, STATUS)
IF STATUS THEN RETURN
*
IF NOT(HVB) THEN
IF DEBUGFLAG THEN CRT TIMEDATE():": Creating Script Engine"
ATTR = PARENT:AM:DRAWABLE ; VALS = HSESS:AM:FALSE; HVB = 0
CALL ROC.CREATE(SCRIPT.ENGINE, ATTR, VALS, HVB, STATUS)
IF STATUS AND NOT(HVB) THEN RETURN
ATTR = VBSCRIPT; VALS = HVB
CALL ROC.SET(HSESS, ATTR, VALS, STATUS)
IF STATUS THEN RETURN
*
*-* Set properties, execute RunCreate method
*
IF DEBUGFLAG THEN CRT TIMEDATE():": Setting properties"
HANDLE = HVB
ATTR = P.TIMEOUT:ROC.US:P.USESAFESUBSET ; VALS = I.TIMEOUT:ROC.US:BSAFE
CALL ROC.SET(HANDLE, ATTR, VALS, STATUS)
IF STATUS THEN RETURN
*
PATH = ""
CALL TU.CLIENT.GETENV("SBTUPATH", PATH)
ATTR = M.RUNCREATE ; VALS = ''
CALL ROC.GET(HANDLE, ATTR, VALS, STATUS)
IF STATUS THEN RETURN
*
IF DEBUGFLAG THEN CRT TIMEDATE():": Reseting Script Engine"
CALL TU.SCRIPT.RESET(HVB, "", "", STATUS)
IF STATUS THEN RETURN
*
ERROR = ""
CALL TU.SCRIPT.LAST.ERROR(HANDLE, ERROR, "", "", STATUS)
IF STATUS THEN RETURN
IF ERROR # "" THEN STATUS = TRUE
END
*
HANDLE = HVB
*
RETURN
***************************************************************************
*   the End
***************************************************************************
END
