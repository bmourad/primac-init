SUBROUTINE TU.TO.WORD.BOOKMARK(HOSTFILENAME, FIELDLIST, SELECTION, OPTIONS, DOCUMENTNAME, STATUS)
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
***************************************************************************
*-* TU.TO.WORD.BOOKMARK(Pickfilename, Fieldlist, Selection, Options, Documentname,
*-* Status)
*-*
*-*       This function will download formatted pick data into a WINWORD document
*-* at a WINWORD bookmark using DDE. The bookmark names are the HEADING fields of the pick
*-* dictionary definitions.
*-*
*-* [P] Pickfilename   The name of the pick file to be downloaded.
*-*
*-* [P] Fieldlist      List of dictionary definitions of fields to be
*-* downloaded.
*-*
*-* [P] Selection      Selection criteria to locate items to be downloaded.
*-*
*-* [P] Options        This is an array of characters to indicate minor
*-*                    details of the transfer. Possible values INCLUDE :-
*-*
*-*                           I, Do OEM to ANSI conversion
*-*                           P, Print Document
*-*                           R, Restore Application
*-*                           S, Save Document
*-*                           X, Close Word
*-*
*-* [P] Documentname   This the name of the PC file to store the data.
*-*
*-* [R] Status         This is an indication of the success or failure of
*-*                    the subroutine. A successful call will return with
*-*                    zero Status and failure will return with a non-zero
*-*                    value.
***************************************************************************
*
 INCLUDE TUBP SPECIAL.H
 INCLUDE TUBP TU.API.H
 INCLUDE TUBP HEADER.H
 INCLUDE TUBP TU.ERRORCODES.H
 INCLUDE TUBP USER.INCLUDE.H
*
IF TU.TRACE THEN
OPEN '','TUSTATE' TO TUSTATE THEN
WRITEV ('TO.WORD.BOOKMARK':VM:HOSTFILENAME:VM:FIELDLIST:VM:SELECTION:VM:OPTIONS:VM:DOCUMENTNAME) ON TUSTATE,('TRACE.':PORTNO),-1
END
END
*
DESCRIPTION = 'WINWORD'
CALL TU.BUILD.XFER.DATA(HOSTFILENAME, FIELDLIST, SELECTION, DESCRIPTION, STATUS)
IF STATUS THEN RETURN
*
OPEN '','TUXFER.DATA.':PORTNO TO TUXFER.DATA ELSE
STATUS = FTE.TUXFER.DATA.FILE.OPEN.ERROR
RETURN
END
*
READ HEADER FROM TUXFER.DATA,'XFER.HEADER' ELSE
STATUS = FTE.ITEM.READ.ERROR
RETURN
END
*
BOOKMARKS = HEADER<HED.HEADING>; BOOKDATA = ""
NO.BOOKS = COUNT(BOOKMARKS<1>, VM) + (BOOKMARKS # "")
*
READ ITEM FROM TUXFER.DATA, "XFER.DATA.1" ELSE
STATUS = FTE.ITEM.READ.ERROR
RETURN
END
*
FOR I = 1 TO NO.BOOKS
VALUE = ITEM<1,I>
IF HEADER<HED.OCONV, I> # '' THEN
VALUE = OCONV(ICONV(VALUE, HEADER<HED.OCONV, I>), HEADER<HED.OCONV, I>)
END
BOOKDATA<1, I> = VALUE
NEXT I
*
CALL TU.TO.WORD.BOOKMARK.OLE(BOOKMARKS, BOOKDATA, OPTIONS, DOCUMENTNAME, STATUS)
*
RETURN
***************************************************************************
*  the End
***************************************************************************
END
