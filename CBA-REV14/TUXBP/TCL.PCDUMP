 INCLUDE TUBP USER.INCLUDE.H
*
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
*
***************************************************************************
*
 INCLUDE TUBP TU.API.H
*
 INCLUDE TUBP SPECIAL.H
*
EQU EXCLUDE.WORDS TO ',BREAK-ON,BREAK.ON,BREAK.SUP,TOTAL,AVERAGE,PERCENTAGE,'; *JM 18/2/95: INCLUDE BREAK.SUP
*
 INCLUDE TUBP CASE.INVERT.OFF.H
*
10 *
PROMPT ''
DEBUGGING = 0
STATUS = 0
FIELDLIST = ''
SELECTION = ''
OPTIONS = 'BH'
 INCLUDE TUBP GET.COMMAND.LINE.H
*
FC = 1
IF TCL = '' OR (COUNT(TCL, ' ') = 0 AND COUNT(TCL, AM) = 0) THEN
CRT 'pcdump>':
INPUT TCL
END
QUOTES = '"':"'":"\"
CALL TU.PARSE.TCL(TCL, QUOTES)
WC = COUNT(TCL, AM) + (TCL # '')
HOSTFILENAME = TCL<FC>
IF HOSTFILENAME = 'DICT' THEN
FC = FC + 1
HOSTFILENAME = HOSTFILENAME:' ':TCL<FC>
END
*
SQLATOR.FILE = 0
OPEN '','VOC' TO F.VOC ELSE CRT "Unable to open VOC file"; GOTO 999; !*UDT*!
READ DFILE FROM F.VOC, HOSTFILENAME ELSE DFILE = ""; !*UDT*!
IF DFILE<1> = "FX" THEN SQLATOR.FILE = 1; !*UDT*!
*
LOOP
FC = FC + 1
WHILE FC <= WC DO
WORD = TCL<FC>
BEGIN CASE
CASE WORD = 'BY' OR WORD = 'BY-EXP' OR WORD = 'BY.EXP' OR WORD = 'OR' OR WORD = 'AND'
FC = FC + 1
SELECTION = SELECTION:' ':WORD:' ':TCL<FC>
CASE INDEX(EXCLUDE.WORDS,',':WORD:',',1); *JM 18/2/95: INCLUDE BREAK.SUP
FC = FC + 1
FIELDLIST = FIELDLIST:' ':WORD:' ':TCL<FC>
CASE WORD = 'IF' OR WORD = 'WITH'
SELECTION = SELECTION:' ':WORD:' ':TCL<FC + 1>:' ':TCL<FC + 2>:' ':TCL<FC + 3>
IF TCL<FC + 1> = 'EVERY' THEN
SELECTION = SELECTION:' ':TCL<FC + 4>
FC = FC + 1
END
FC = FC + 3
CASE WORD[1, 1] = "'" OR WORD = "*"
SELECTION = SELECTION:' ':WORD
CASE WORD[LEN(WORD) - 4, 5] = '-SUPP' OR WORD[LEN(WORD) - 3, 4] = '.SUP'
FIELDLIST = FIELDLIST:' ':WORD
CASE WORD[1,1] = '('
L = LEN(WORD)
FOR I = 1 TO L
IF WORD[I, 1] MATCHES '1N' THEN
WORD = WORD[1, I - 1]:WORD[I + 1, L]
END
IF WORD[I, 1] = 'Y' THEN
DEBUGGING = 1
WORD = WORD[1, I - 1]:WORD[I + 1, L]
END
NEXT I
OPTIONS = OPTIONS:WORD
CASE 1
SELECTION = SELECTION:" '":WORD:"'"
END CASE
REPEAT
IF FIELDLIST[1, 1] = ' ' THEN FIELDLIST = FIELDLIST[2, 9999]
IF SELECTION[1, 1] = ' ' THEN SELECTION = SELECTION[2, 9999]
IF OPTIONS[1, 1] = '(' THEN OPTIONS=OPTIONS[2, 9999]
IF OPTIONS[1, LEN(OPTIONS)] = ')' THEN OPTIONS=OPTIONS[1, LEN(OPTIONS) - 1]
OPEN '',HOSTFILENAME TO FILE ELSE
CRT HOSTFILENAME:' is not a file'
GOTO 999
END
CRT 'TO:(':
INPUT PCFILENAME
IF DEBUGGING THEN
CRT 'tcl=':TCL
CRT 'fil=':HOSTFILENAME
CRT 'sel=':SELECTION
CRT 'opt=':OPTIONS
END
IF PCFILENAME = '' THEN GOTO 999
DESCRIPTION = 'PCDUMP from TCL'
20 *
IF SELECTION # '' THEN
IF SELECTION = "*" THEN SELECTION = ''
IF SQLATOR.FILE THEN
IF INDEX(SELECTION, "USING DICT", 1) THEN
CRT "SQLator file used. USING DICT syntax not supported."
GOTO 999
END
UDTEXECUTE 'SSELECT ':HOSTFILENAME:' ':SELECTION CAPTURING OUTPUT; !*UDT*!
END ELSE
EXECUTE 'SSELECT ':HOSTFILENAME:' ':SELECTION CAPTURING OUTPUT; !*-MRX*!
END
!@!PERFORM 'SSELECT ':HOSTFILENAME:' ':SELECTION RTNLIST CAPTURING OUTPUT ; !*MRX*!
END
IDLIST = ''
30 *
READNEXT ID ELSE GOTO 40
IDLIST<-1> = ID
GOTO 30
40 *
IF IDLIST = '' AND SELECTION = '' THEN SELECTION = '*'; GOTO 20
IF IDLIST = '' THEN
CRT 'nothing to PCDUMP'
GOTO 999
END
AMC = DCOUNT(IDLIST, AM)
FOR I = 1 TO AMC
ID = IDLIST<I>
READ ITEM FROM FILE,ID ELSE
IDLIST = DELETE(IDLIST,I)
AMC = AMC - 1
ITEM = ''
GOTO 50
END
IF NOT(INDEX(OPTIONS, 'I', 1)) THEN
CRT I "R#4 ":ID
END
CALL TU.DOWNLOAD(ITEM, PCFILENAME:'.':(I "R%%%"), OPTIONS, DESCRIPTION, STATUS)
IF STATUS THEN
CRT 'PCDUMP failed, Status = ':STATUS
END
50 *
NEXT I
CALL TU.DOWNLOAD(IDLIST, PCFILENAME:'.000', OPTIONS, DESCRIPTION, STATUS)
CRT AMC:' items PCDUMPed'
999 *
IF PTERM THEN EXECUTE "PTERM CASE INVERT"; * universe
END
