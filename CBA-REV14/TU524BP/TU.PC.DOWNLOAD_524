SUBROUTINE TU.PC.DOWNLOAD_524(HOSTFILENAME, FIELDLIST, SELECTION, PCFILENAME, SEPARATORS, OPTIONS, DESCRIPTION, STATUS)
*
** SBClient Host 3GL API
** Copyright (C) Ardent Software Inc. 1998
** Copyright (C) UniData, Inc. 1996, 1997
** Copyright (C) System Builder Corporation. 1995
**
**      This software is the proprietary property and contains
**      trade secrets of Ardent Software, Inc. Any unauthorized use,
**      disclosure or duplication is strictly prohibited.
**      All rights reserved.
*
*
***************************************************************************
*-* TU.PC.DOWNLOAD(Pickfilename, Fieldlist, Selection, Dosfilename,
*-* Separators, Options, Description, Status)
*-*
*-*  This subroutine selects and downloads pick items to PC.
*-*
*-* [P] Pickfilename The name of the pick file to be downloaded.
*-*
*-* [P] Fieldlist List of dictionary definitions of fields to be
*-* downloaded.
*-*
*-* [P] Selection Selection criteria to locate items to be downloaded.
*-*
*-* [P] Dosfilename This the name of the PC file to store the data.
*-*
*-* [P] Separators Flag indicating what field separators are to be used in
*-* the PC file.
*-* -1 New file transfer type  - NO translation from dict items
*-*  0 No delimiters, fixed length based on dictionary
*-*  1 Strings in quotes, commas between data
*-*  2 Tabs between fields
*-*  3 Spaces between fields
*-*
*-* [P] Options This is an array of characters to indicate minor details
*-* of the transfer. Possible values INCLUDE :-
*-*
*-*  A Append to existing file
*-*  B Data is in binary
*-*  D Debug mode - create TUSTATE records
*-*  H Host initiated transfer
*-*  L Local transfer - simple protocol
*-*  O Overwrite the dosfile if it exists
*-*  R Non resilient line - complex protcol
*-*  T Text - no protocol
*-*  V Strip VMs and SVMs
*-*  X Hex output
*-*  Z No status window
*-*  0 Add Duplicate Data for Correlative
*-*  1 Insert Column Headers into Data
*-*  2 Insert Blank Line Before Data
*
*-*
*-* [P] Description This is a description of the data being transferred and
*-* will be displayed in the dialog box by Termulator.
*-* [R] Status This is the result of the transfer. Possible return
*-* values are :-
*-*  0 file has been transfered successfully
*-*  1 user abort
*-*  2 unable to open PC file
*-*  3 PC read error
*-*  4 communications error
*-*  5 retry limit exceeded
*-*  6 PC write error
*-*  7 File transfer not supported
*-*  10 unknown error
***************************************************************************
*
 INCLUDE TU524BP USER.INCLUDE.H
 INCLUDE TU524BP SPECIAL.H
 INCLUDE TU524BP TU.API.H
 INCLUDE TU524BP HEADER.H
 INCLUDE TU524BP CASE.INVERT.OFF.H
 INCLUDE TU524BP ROC.MAIN.H
 INCLUDE TU524BP TU.ERRORCODES.H
 INCLUDE TU524BP TRANSFER.OPTIONS.H
 INCLUDE TU524BP DOS.PATH.H
*
HEADER = NUL; DOSDIR = DEFT.DOS.PATH
DEFAULT.CONVTYPE = "ascii"; CONVTYPE = NUL; CONV.HANDLE = 0
DEBUGFLAG = FALSE
DATAFILE = HOSTFILENAME<1>
DICTFILE = HOSTFILENAME<2>
IF DICTFILE = "" THEN DICTFILE = DATAFILE
OLD = AM ; NEW = "~"; STRING = HOSTFILENAME
 INCLUDE TU524BP SWAP.STRINGS.H
HOSTFILENAME = STRING
*
IF NUM(STATUS) AND STATUS < 0 THEN QBH = ABS(STATUS) ELSE QBH = 0
STATUS = 0
*
OPTS = OPTIONS
IF TU.TRACE THEN
OPEN '','TUSTATE' TO TUSTATE THEN
TRACE.O = 'PC.DOWNLOAD':VM:HOSTFILENAME:VM:FIELDLIST:VM:SELECTION
TRACE.O = TRACE.O:VM:PCFILENAME:VM:SEPARATORS:VM:OPTS:VM:DESCRIPTION
WRITEV TRACE.O ON TUSTATE,('TRACE.':PORTNO),-1
END
END
*
BEGIN CASE
CASE SEPARATORS = 0; CONVTYPE = "flf"
CASE SEPARATORS = 1; CONVTYPE = "csv"
CASE SEPARATORS = 2; CONVTYPE = "tab"
CASE SEPARATORS = 3; CONVTYPE = "spc"
CASE SEPARATORS = 4; CONVTYPE = "ascii"
CASE INDEX(OPTIONS, "B", 1); CONVTYPE = "bin"
CASE INDEX(OPTIONS, "X", 1); CONVTYPE = "hex"
CASE TRUE; CONVTYPE = DEFAULT.CONVTYPE
END CASE
*
IF ICONV(CONVTYPE, 'MCU') = "FLF" THEN CALL TU.BUILD.HEADER_524(DICTFILE, FIELDLIST, HEADER, "DOWNLOAD", STATUS)
IF STATUS THEN RETURN
*
*-* Check SELECTION for BY-EXP as this is not supported
*
IF INDEX(SELECTION, 'BY-EXP', 1) OR INDEX(SELECTION, 'BY.EXP', 1) THEN
STATUS = FTE.BY.EXP.USED
RETURN
END
*
*-* Create the definition for the RPC transfer
*
CONVCLASS = ICONV(TRIM(CONVTYPE):'CLASS','MCL')
DEFN = ''
DEFN<1> = 'HOSTPCDEFN'; * Direction control for class
DEFN<2> = DATAFILE; * Pick host file name
DEFN<3> = PCFILENAME; * DOS file name
DEFN<6> = FIELDLIST; * dict item(s) (if any)
DEFN<7> = DICTFILE; * Dict host file name
DEFN<5> = SELECTION; * selection crit or item(s)
DEFN<4> = ''; * Selection suffix
DEFN<8> = 1; * Indicates Pick to DOS (flag)
DEFN<9> = OPTIONS; * TCL Transfer options
DEFN<10> = -1; * this is the MV pos in the TXDEFN rec
DEFN<15> = ''; * Description  (not used)
DEFN<17> = DOSDIR; * sbopath need to get this from SB api
DEFN<18> = 'SBFTP.TMP'; * Temp file name in sbopath
DEFN<22> = OCONV(CONVTYPE, "MCU"); * File conversion object name
DEFN<24> = 'HOST'; * Client Handle for the class (N/A)
DEFN<30> = HEADER<HED.LENGTH>; * Dictionary length for FLF conversion
*
*-* Download file to be converted
*
VALS = NUL
CALL RPC.TU.PC.DOWNLOAD_524(DEFN, VALS, STATUS)
IF STATUS THEN GOTO 20
*
*-* Initialise the ROC object handler
*
CALL TU.INIT_524(STATUS)
IF STATUS THEN
GOTO 20; * Error
END
*
CALL ROC.GETHANDLE_524("this", ROC.HANDLE, STATUS)
IF STATUS THEN
GOTO 20; * Error
END
*
*-* Create the file transfer object in DATA/C
*
CONV.HANDLE = NUL; ATTR = NUL
*
CALL ROC.CREATE_524(CONVCLASS, ATTR, NUL, CONV.HANDLE, STATUS)
IF STATUS THEN
GOTO 20; * Error
END
*
*-* Pass the DEFN record to the file conversion object
*
SDEFN = DEFN
CALL ROC.SET_524(CONV.HANDLE, "defn_rec", SDEFN, STATUS)
IF STATUS THEN
GOTO 20; * Error
END
*
*-* Execute the conversion object
*
RVAL = NUL
CALL ROC.GET_524(CONV.HANDLE, "execute", RVAL, STATUS)
*
20 *  ERROR:*
*-* Destroy the created object(s)
IF CONV.HANDLE THEN
ERR = NUL
CALL ROC.DESTROY_524(CONV.HANDLE, ERR)
IF ERR THEN CRT 'Error destroying ':CONVCLASS
END
IF PTERM THEN EXECUTE "PTERM CASE INVERT"
RETURN
*
*-* The END
*
END
