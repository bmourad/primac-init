*--------------------------------------------------------------------
* Copyright 1989 by CBA (Vercom Software,Inc.)
* REVISION       - [08.1]
* SOURCE         - EPS401KBP
* PROGRAM        - EMPLOYEE.MAINT
* BY             - WALID YAMOUT , C.B.A
* DATE           - 06/11/85
* MODIFIED       - 03/16/88 BY KERRY WILSON
*                - 07/25/89]RRG]#14032]UPDATE MISCDED WITH FLAG
* MODIFIED       - 12/05/89 Scott Dickerson,PRISM Software Tech
*                  FEB 1990 401K.MODS ADDED BY SFC
* DESCRIIPTION   - EMPLOYEE MAINTENANCE
*T21851 lanny 04/28/1997 * Default SICK FICA field to Y.
*T26556 cmykleb 05/16/2002 * Rev12 changes.
*--------------------------------------------------------------------
*COPY>CPYLIB>COM1
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>SYSCOM
*T25236 v
*COPY>CPYLIB>TCC
*T25236 ^
*--------------------------------------------------------------------
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.EMPLOYEE
*COPY>PRS.CPYLIB>COM.QTD-YTD
*COPY>PRS.CPYLIB>COM.MISCDED
*COPY>PRS.CPYLIB>COM.PRS.FILE.VARS
*COPY>PRS.CPYLIB>COM.EMPLOYEE.MAINT
   COM DC.REC(5)
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PRS.CPYLIB>PRS.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*--------------------------------------------------------------------
*T25236 v
   PC.PORT.TYPE.SAVE = PC.PORT.TYPE
   PC.PORT.TSEL.SAVE = PC.PORT.TSEL
   PC.PORT.TYPE = "STD"
   PC.PORT.TSEL = "STD"
*T25236 ^
   EQU SN TO ECD.SCRN.NO,AN TO ECD.NUM,VN TO ECD.SUB.NUM
   EQU ACT TO ECD.ACTION,VAL TO ECD.RET.VALUE
*--------------------------------------------------------------------
   MAT FILE.VARS='';DIM S.EMP.REC(110);MAT S.EMP.REC=''
   MAT EDIT.COM='';TYP=0;CALL EDIT.SUB;FILL='#';MSG=''
*--------------------------------------------------------------------
      * GET COMPANY
   OPEN 'COMPANY' TO COMPANY ELSE MSG='COMPANY';GO 92000
   OPEN 'CONTROL' TO CONTROL ELSE MSG='CONTROL';GO 92000
*--------------------------------------------------------------------
   PROCREAD BUF ELSE MSG = "CANNOT READ PROC FROM PROG - EMPLOYEE.MAINT"; GO 92000
   CONO=BUF<1>
   IF CONO='END' THEN GO 99999
   MATREAD COMP.REC FROM COMPANY,CONO ELSE MSG = "CANNOT READ COMPANY RECORD FROM PROG - EMPLOYEE.MAINT"; GO 92000
*--------------------------------------------------------------------
   MATREAD DC.REC FROM CONTROL,CONO:"401K" ELSE MAT DC.REC = ""
*--------------------------------------------------------------------
      * GET INITIALS
*T26556 v
*  PRINT CS:;TRAN.INT='';OLD='';NEW='';OLD.BDAY=''
*  X=10;Y=15;TYP=1;MAXL=3;PMSG='ENTER YOUR INITIALS OR END:'
*  CALL EDIT.SUB
*  IF VALUE='END' THEN GO 99999 ELSE TRAN.INT=VALUE
*T26556 ^
*--------------------------------------------------------------------
      * OPEN FILES
   OPEN 'EMPLOYEE' TO EMPLOYEE ELSE MSG='EMPLOYEE';GO 92000
   S.EMP.FILE='Y'
   OPEN 'S.EMPLOYEE' TO S.EMPLOYEE ELSE S.EMP.FILE='N'
   OPEN 'DIVISION' TO DIVISION ELSE MSG='DIVISION';GO 92000
   OPEN 'DEPARTMENT' TO DEPARTMENT ELSE MSG='DEPARTMENT';GO 92000
   OPEN 'COST.CNTR' TO COST.CNTR ELSE MSG='COST.CNTR';GO 92000
   OPEN 'EMPTRANS' TO EMPTRANS ELSE MSG='EMPTRANS';GO 92000
   OPEN 'UNION' TO UNION ELSE MSG='UNION';GO 92000
   OPEN 'TAX2' TO TAX2 ELSE MSG='TAX2';GO 92000
   OPEN 'PAYTYPE' TO PAYTYPE ELSE MSG='PAYTYPE';GO 92000
   OPEN 'MISCDED' TO MISCDED ELSE MSG='MISCDED';GO 92000
   OPEN 'QTD-YTD' TO QTD.YTD ELSE MSG='QTD-YTD';GO 92000
   OPEN 'PRS.SCREENS' TO M.SCREENS ELSE MSG='PRS.SCREENS';GO 92000
   OPEN 'KEYWORDS' TO KEYWORDS ELSE MSG='KEYWORDS';GO 92000
   OPEN 'INVEST.FUND.CODE' TO INVEST.FUND.CODE ELSE MSG='INVEST.FUND.CODE';GO 92000
   IF CO.EMP.XREF='Y' THEN
      OPEN 'EMPLOYEE.XREF' TO EMPLOYEE.XREF ELSE MSG='EMPLOYEE.XREF';GO 92000
   END
*--------------------------------------------------------------------
      * SCREEN
   MAT EDIT.COM.DRIVER=''
   ECD.SCRN.CNT=4
   ECD.SCRN.NAME<1>='EPS.EMPLOYEE.MAINT'
   ECD.SCRN.NAME<2>='EMPLOYEE.MAINT2'
   ECD.SCRN.NAME<3>='EMPLOYEE.MAINT3'
   ECD.SCRN.NAME<4>='EMPLOYEE.MAINT4'
   ECD.SCRN.NAME<5>='EMPLOYEE.MAINT.INIT' ; * T26556
*T26556 v
   SN = 5
   ACT = 1 ; CALL SCRN.EDIT
   AN=1;ACT=4;CALL SCRN.EDIT
   IF VAL = 'END' THEN GOTO 99999
   TRAN.INT = VAL
   SN = 1
   OLD.BDAY = ''
*T26556 ^
   ACT=1;CALL SCRN.EDIT
      * MAIN PROCESS
100   *
   OLD.BDAY = ""
   SN=1
   MAT SCV.REC=''
   ACT=6;CALL SCRN.EDIT
      * ENTER EMPLOYEE NUMBER
200   *
   EMP.NO='';OLD.NAME='';NEW.FLG='N';TRAN.TYPE='3';S.NEW.FLG='N'
   AN=2
   ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
         GO 99999
      CASE VAL # ''
         MATREADU EMP.REC FROM EMPLOYEE,CONO:VAL ELSE
            EMP.NO=VAL
            NEW.FLG='Y'
            IF S.EMP.FILE='Y' THEN
               MATREADU EMP.REC FROM S.EMPLOYEE,CONO:VAL ELSE
                  S.NEW.FLG='Y'
               END
            END ELSE
               S.NEW.FLG='Y'
            END
            IF S.NEW.FLG='Y' THEN
               MAT EMP.REC=''
               MAT QTD.REC=''
               FOR OPTION=1 TO 23 UNTIL VAL='END'
                  IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
                  IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000,27000
               NEXT OPTION
*LANGUAGE PREFERENCE
*GOSUB 27000
               IF VAL='END' THEN
                  RELEASE EMPLOYEE,CONO:EMP.NO
                  GO 100
               END
               GO 250
            END
         END
         EMP.NO=VAL
         OLD.NAME=EMP.LAST.NAME
         GOSUB 80000
         IF EMP.SOC.SEC='' THEN
            NEW.FLG='Y'
            S.NEW.FLG='Y'
            FOR OPTION=1 TO 23 UNTIL VAL='END'
               IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
               IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000,27000
            NEXT OPTION
*GOSUB 27000
         END
250   *
      CASE VAL=''
         IF CO.EMP.XREF # 'Y' THEN GO 200
         AN=3;ACT=4;CALL SCRN.EDIT
         IF VAL='' OR VAL='END' THEN GO 200
         MAT GEN.XREF.REC=''
         GXR.CO=CONO
         GXR.HEADING<1,2>='LAST NAME'
         GXR.HEADING<1,3>='FIRST NAME'
         GXR.ATT<1,1>=0
         GXR.ATT<1,2>=4
         GXR.ATT<1,3>=5
         GXR.LEN<1,1>=4
         GXR.LEN<1,2>=30
         GXR.LEN<1,3>=30
         GXR.XREF=EMPLOYEE.XREF
         GXR.FILE=EMPLOYEE
         GXR.LOC=1
         GXR.TOP.LINE<1,1>='EMPLOYEE XREF SEARCH'
         GXR.TOP.LINE<1,2>='FOR STRING:':VAL
         GXR.HEADING<1,1>='EMP#'
         GXR.ID=''
         GXR.SRCH.ID=VAL
         CALL GEN.XREF(MAT GEN.XREF.REC,KEYWORDS)
         ACT=2;CALL SCRN.EDIT
         IF GXR.ID='' THEN
            SCV.REC(3)<1>=''
            ACT=3;CALL SCRN.EDIT
            GO 200
         END ELSE
            MATREADU EMP.REC FROM EMPLOYEE,CONO:GXR.ID ELSE
               RELEASE EMPLOYEE,CONO:GXR.ID
               MSG='EMPLOYEE NUMBER ':GXR.ID:' IS MISSING'
               GOSUB 91000
               SCV.REC(3)<1>=''
               GO 200
            END
            EMP.NO=GXR.ID
            OLD.NAME=EMP.LAST.NAME
            SCV.REC(2)<1>=EMP.NO
            GOSUB 80000
            IF EMP.SOC.SEC='' THEN
               NEW.FLG='Y'
               S.NEW.FLG='Y'
               FOR OPTION=1 TO 23 UNTIL VAL='END'
                  IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
                  IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000,27000
               NEXT OPTION
*GOSUB 27000
            END
         END
   END CASE
      * ENTER OPTIONS
500   *
   AN=1
   SCV.REC(AN)<1>=''
   ACT=4;CALL SCRN.EDIT
   OPTION=VAL
   BEGIN CASE
      CASE OPTION='END' OR OPTION='E'
         RELEASE EMPLOYEE,CONO:EMP.NO
         GO 100
      CASE NUM(OPTION) AND NEW.FLG='Y' AND S.NEW.FLG='N' AND OPTION > 6
         MSG="Can't access this option for terminated employee after end of year process";GOSUB 91000
      CASE NUM(OPTION) AND OPTION > 0 AND OPTION < 27
         IF OPTION < 17 THEN ON OPTION GOSUB 1000,2000,3000,4000,5000,7000,8000,9000,10000,11000,12000,13000,6000,14000,16000,17000
         IF OPTION > 16 THEN ON (OPTION-16) GOSUB 18000,19000,20000,21000,22000,23000,24000,25000,26000,27000
         IF VAL # 'END' AND NEW.FLG='N' THEN GOSUB 90000
      CASE OPTION='D'
         AN=45;SCV.REC(45)<SN>=''
         ACT=4;CALL SCRN.EDIT
         IF VAL='END' OR VAL='N' THEN GO 500
         IF NEW.FLG='Y' THEN GO 550
         GROSS=QTD.GROSS<1,1> + QTD.GROSS<1,2> + QTD.GROSS<1,3> + QTD.GROSS<1,4>
         IF GROSS # 0 THEN
            MSG='EMPLOYEE CANNOT BE DELETED UNTIL PRINTING W2'
            GOSUB 91000;GO 550
         END
         DELETE EMPLOYEE,CONO:EMP.NO
         DELETE QTD.YTD,CONO:EMP.NO
         TRAN.TYPE='2';GOSUB 90000
         IF CO.EMP.XREF='Y' THEN
            CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,'',EMPLOYEE.XREF,KEYWORDS)
         END
550   *
         RELEASE EMPLOYEE,CONO:EMP.NO
         GO 100
      CASE OPTION='N' AND NEW.FLG='Y' AND S.NEW.FLG='N'
         MSG="Can't access this option for terminated employee after end of year process";GOSUB 91000
      CASE OPTION='N'
         CALL EP4.EMPLOYEE.MAINT2
         SCV.REC(41)<1>=EMP.REV.DATE<1,1>
         SN=1;ACT=3;CALL SCRN.EDIT
      CASE OPTION='F' AND S.EMP.FILE='N'
         IF EMP.WRK.SCHD = '' OR EMP.NORM.SHFT = '' OR EMP.JOB.CLASS = '' OR EMP.OVT = '' THEN
            MSG='THERE ARE INCOMPLETE FIELDS ON THE SECOND SCREEN THAT ARE REQUIRED FOR EPS'
            GOSUB 91000
            GO 500
         END
         MATWRITE EMP.REC ON EMPLOYEE,CONO:EMP.NO
         IF NEW.FLG='Y' THEN
            TRAN.TYPE='1'
            GOSUB 90000
         END
         IF CO.EMP.XREF='Y' THEN
            CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,KEYWORDS)
         END
         GO 100
      CASE OPTION='F'
         IF EMP.WRK.SCHD = '' OR EMP.NORM.SHFT = '' OR EMP.JOB.CLASS = '' OR EMP.OVT = '' THEN
            MSG='THERE ARE INCOMPLETE FIELDS ON THE SECOND SCREEN THAT ARE REQUIRED FOR EPS'
            GOSUB 91000
            GO 500
         END
         BEGIN CASE
            CASE NEW.FLG='N'
               MATWRITE EMP.REC ON EMPLOYEE,CONO:EMP.NO
               IF CO.EMP.XREF='Y' THEN
                  CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,KEYWORDS)
               END
               EMP.FND=1
               MATREADU S.EMP.REC FROM S.EMPLOYEE,CONO:EMP.NO ELSE
                  RELEASE S.EMPLOYEE,CONO:EMP.NO
                  MAT S.EMP.REC=''
                  EMP.FND=0
               END
               IF EMP.FND THEN
                  S.EMP.REC(4)=EMP.LAST.NAME
                  S.EMP.REC(5)=EMP.FRST.NAME
                  S.EMP.REC(7)=EMP.ADDR1
                  S.EMP.REC(8)=EMP.ADDR2
                  S.EMP.REC(9)=EMP.CITY
                  S.EMP.REC(10)=EMP.STATE
                  S.EMP.REC(11)=EMP.ZIP
                  S.EMP.REC(12)=EMP.PHONE
                  S.EMP.REC(13)=EMP.BIRTHDAY
                  MATWRITE S.EMP.REC ON S.EMPLOYEE,CONO:EMP.NO
               END
            CASE NEW.FLG='Y' AND S.NEW.FLG='N'
               MATWRITE EMP.REC ON S.EMPLOYEE,CONO:EMP.NO
            CASE NEW.FLG='Y' AND S.NEW.FLG='Y'
               MATWRITE EMP.REC ON EMPLOYEE,CONO:EMP.NO
               TRAN.TYPE='1'
               GOSUB 90000
               IF CO.EMP.XREF='Y' THEN
                  CALL GEN.XREF.MAINT(CONO,EMP.NO,OLD.NAME,EMP.LAST.NAME,EMPLOYEE.XREF,KEYWORDS)
               END
         END CASE
      ** Update MISCDED with 'Y' if code is used in EMPLOYEE file
         MCNT=DCOUNT(EMP.MSC.CODE,VM)
         FOR I=1 TO MCNT
            IF EMP.MSC.AMT<1,I> > '0' THEN
               MDED.USAGE.FLAG='Y'
               WRITEV MDED.USAGE.FLAG ON MISCDED,CONO:EMP.MSC.CODE<1,I>,16
            END
         NEXT I
      *
         GO 100
   END CASE
   GO 500
      *-------------*
      * SUBROUTINES *
      *-------------*
      * LAST AND FIRST NAME
1000  *
   AN=3;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 1999
   IF VAL='' THEN GO 1000
   DESC='LAST NAME'
   OLD=EMP.LAST.NAME
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   EMP.LAST.NAME=VAL
1500  *
   AN=4;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 1999
   IF VAL='' THEN GO 1500
   DESC='FIRST NAME'
   OLD=EMP.FRST.NAME
   NEW=VAL
   EMP.FRST.NAME=VAL
1999  *
   RETURN
      * TITLE
2000  *
   AN=5;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 2999
   DESC='TITLE'
   OLD=EMP.TITLE
   NEW=VAL
   EMP.TITLE=VAL
2999  *
   RETURN
      * ADDRESS
3000  *
   AN=6;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 3999
   IF VAL='' THEN GO 3000
   DESC='ADDRESS 1'
   OLD=EMP.ADDR1
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   EMP.ADDR1=VAL
3100  *
   AN=7;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 3999
   DESC='ADRESS 2'
   OLD=EMP.ADDR2
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   EMP.ADDR2=VAL
3500  *
   AN=8;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 3999
   IF VAL='' THEN GO 3500
   DESC='CITY'
   OLD=EMP.CITY
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   EMP.CITY=VAL
3700  *
   AN=9;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 3999
   IF VAL='' THEN GO 3700
   DESC='STATE'
   OLD=EMP.STATE
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   EMP.STATE=VAL
3900  *
   AN=10;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 3999
   IF VAL='' THEN GO 3900
   DESC='ZIP'
   OLD=EMP.ZIP
   NEW=VAL
   EMP.ZIP=VAL
3999  *
   RETURN
      * PHONE
4000  *
   AN=11;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 4999
   DESC='PHONE'
   OLD=EMP.PHONE
   NEW=VAL
   EMP.PHONE=VAL
4999  *
   RETURN
      * BIRTHDAY
5000  *
   IF OLD.BDAY # '' THEN SCV.REC(12)=ICONV(OLD.BDAY,'D2/')
   AN=12;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 5999
   IF VAL > DATE() THEN MSG='**OUT OF RANGE**';GOSUB 91000;GO 5000
   DESC='BIRTH DATE'
   OLD=OCONV(EMP.BIRTHDAY,'D2/')
   NEW=OCONV(VAL,'D2/')
   EMP.BIRTHDAY=VAL
5999  *
   RETURN
      * SOC.SEC
6000  *
   AN=13;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 6999
   DESC='SOCIAL SECURITY'
   OLD=EMP.SOC.SEC
   NEW=VAL
   EMP.SOC.SEC=VAL
6999  *
   RETURN
      * DIVISION & DEPARTMENT
7000  *
   AN=46;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
         MATREAD DIV.REC FROM DIVISION,CONO:EMP.DIV ELSE MAT DIV.REC=''
         MATREAD DEPT.REC FROM DEPARTMENT,CONO:EMP.DEPT ELSE MAT DEPT.REC=''
         IF EMP.COST.CNTR='000' THEN
            MAT CCTR.REC=''
            CCTR.DESC='GENERAL COST CENTER'
         END ELSE
            MATREAD CCTR.REC FROM COST.CNTR,CONO:EMP.COST.CNTR ELSE MAT CCTR.REC=''
         END
         AN=46;SCV.REC(46)<1>=EMP.DIV;ACT=5;CALL SCRN.EDIT
         AN=47;SCV.REC(47)<1>=DIV.DESC;ACT=5;CALL SCRN.EDIT
         AN=14;SCV.REC(14)<1>=EMP.DEPT;ACT=5;CALL SCRN.EDIT
         AN=15;SCV.REC(15)<1>=DEPT.DESC;ACT=5;CALL SCRN.EDIT
         AN=51;SCV.REC(51)<1>=EMP.COST.CNTR;ACT=5;CALL SCRN.EDIT
         AN=52;SCV.REC(52)<1>=CCTR.DESC;ACT=5;CALL SCRN.EDIT
         GO 7999
      CASE 1
         MATREAD DIV.REC FROM DIVISION,CONO:VAL ELSE
            MAT DIV.REC=''
            MSG='DIVISION ':VAL:' IS NOT ON FILE';GOSUB 91000
            GO 7000
         END
   END CASE
   DESC='DIVISION'
   OLD=EMP.DIV
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   EMP.DIV=VAL
   AN=47;SCV.REC(47)<1>=DIV.DESC;ACT=5;CALL SCRN.EDIT
   DESC='DEPARTMENT'
   OLD=EMP.DEPT
7500  *
   AN=14;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
         GO 7000
      CASE VAL='???'
         MAT GEN.XREF.REC=''
         GXR.XREF=DIVISION
         GXR.FILE=DEPARTMENT
         GXR.TOP.LINE='DEPARTMENT XREF SEARCH'
         GXR.HEADING<1,1>='DEPT'
         GXR.HEADING<1,2>='DESCRIPTION'
         GXR.LEN<1,1>=5
         GXR.LEN<1,2>=30
         GXR.LOC=2
         GXR.ATT<1,1>=0
         GXR.ATT<1,2>=2
         GXR.CO=CONO
         GXR.SRCH.ID=EMP.DIV
         GXR.ID=''
         CALL GEN.XREF(MAT GEN.XREF.REC,KEYWORDS)
         ACT=2;CALL SCRN.EDIT
         IF GXR.ID='' THEN
            SCV.REC(14)<1>=''
            SCV.REC(15)<1>=''
            ACT=3;CALL SCRN.EDIT
            GO 7500
         END ELSE
            MATREAD DEPT.REC FROM DEPARTMENT,CONO:GXR.ID ELSE
               MAT DEPT.REC=''
               SCV.REC(14)<1>='';SCV.REC(15)<1>=''
               ACT=3;CALL SCRN.EDIT
               MSG='DEPARTMENT ':GXR.ID:' IS NOT ON FILE';GOSUB 91000;GO 7500
            END
            EMP.DEPT=GXR.ID
            SCV.REC(14)<1>=GXR.ID
            SCV.REC(15)<1>=DEPT.DESC
            ACT=3;CALL SCRN.EDIT
         END
      CASE (VAL MATCHES "2X") OR (VAL MATCHES "2X'-'2X")
         LOCATE VAL IN DIV.DEPT<1>,1 SETTING DFND ELSE
            MSG='DEPARTMENT ':VAL:' DOES NOT BELONG TO DIVISION ':EMP.DIV
            GOSUB 91000;GO 7500
         END
         MATREAD DEPT.REC FROM DEPARTMENT,CONO:VAL ELSE
            MAT DEPT.REC=''
            MSG='DEPARTMENT ':VAL:' IS NOT ON FILE'
            GOSUB 91000;GO 7000
         END
         EMP.DEPT=VAL
         AN=15;SCV.REC(15)<SN>=DEPT.DESC
         ACT=5;CALL SCRN.EDIT
      CASE 1
         MSG='INVALID INPUT';GOSUB 91000;GO 7500
   END CASE
   NEW=EMP.DEPT
   IF NEW.FLG='N' THEN GOSUB 90000
   DESC='COST CENTER'
   OLD =EMP.COST.CNTR
7575  *
   AN=51;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
         GO 7000
      CASE VAL='???'
         MAT GEN.XREF.REC=''
         GXR.XREF=DEPARTMENT
         GXR.FILE=COST.CNTR
         GXR.TOP.LINE='COST CENTER XREF SEARCH'
         GXR.HEADING<1,1>='COST CNTR'
         GXR.HEADING<1,2>='DESCRIPTION'
         GXR.LEN<1,1>=9
         GXR.LEN<1,2>=30
         GXR.LOC=5
         GXR.ATT<1,1>=0
         GXR.ATT<1,2>=1
         GXR.CO=CONO
         GXR.SRCH.ID=EMP.DEPT
         GXR.ID=''
         CALL GEN.XREF(MAT GEN.XREF.REC,KEYWORDS)
         ACT=2;CALL SCRN.EDIT
         IF GXR.ID='' THEN
            SCV.REC(51)<1>=''
            SCV.REC(52)<1>=''
            ACT=3;CALL SCRN.EDIT
            GO 7575
         END ELSE
            MATREAD CCTR.REC FROM COST.CNTR,CONO:GXR.ID ELSE
               MAT CCTR.REC=''
               SCV.REC(51)<1>='';SCV.REC(52)<1>=''
               ACT=3;CALL SCRN.EDIT
               MSG='COST CENTER ':GXR.ID:' IS NOT ON FILE';GOSUB 91000;GO 7575
            END
            EMP.COST.CNTR=GXR.ID
            SCV.REC(51)<1>=GXR.ID
            SCV.REC(52)<1>=CCTR.DESC
            ACT=3;CALL SCRN.EDIT
         END
      CASE VAL='000'
         MAT CCTR.REC=''
         CCTR.DESC='GENERAL COST CENTER'
         EMP.COST.CNTR=VAL
         AN=52;SCV.REC(52)<SN>=CCTR.DESC
         ACT=5;CALL SCRN.EDIT
      CASE (VAL MATCHES '3N')
         LOCATE VAL IN DEPT.CCTRS<1>,1 SETTING DFND ELSE
            MSG='COST CENTER ':VAL:' DOES NOT BELONG TO DEPARTMENT ':EMP.DEPT
            GOSUB 91000;GO 7575
         END
         MATREAD CCTR.REC FROM COST.CNTR,CONO:VAL ELSE
            MAT CCTR.REC=''
            MSG='COST CENTER ':VAL:' IS NOT ON FILE'
            GOSUB 91000;GO 7000
         END
         EMP.COST.CNTR=VAL
         AN=52;SCV.REC(52)<SN>=CCTR.DESC
         ACT=5;CALL SCRN.EDIT
      CASE 1
         MSG='INVALID INPUT';GOSUB 91000;GO 7575
   END CASE
   NEW=EMP.COST.CNTR
7999  *
   RETURN
      * MARITAL STATUS AND EIC
8000  *
   AN=16;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 8999
   DESC='MARITAL STATUS'
   OLD=EMP.MAR.ST
   NEW=VAL
   EMP.MAR.ST=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
8500  *
   AN=17;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 8999
   IF VAL # '' THEN
      READ TAX2.REC FROM TAX2,CONO:VAL ELSE
         MSG='TAX2 ':VAL:' IS MISSING'
         GOSUB 91000;GO 8500
      END
   END
   DESC='EIC'
   OLD=EMP.EIC
   NEW=VAL
   EMP.EIC=VAL
8999  *
   RETURN
      * # DEPENDENT FED & STATE
9000  *
   AN=18;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 9999
   DESC='# DEPENDENTS FED'
   OLD=EMP.DEP.NO<1,1>
   NEW=VAL
   EMP.DEP.NO<1,1>=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   AN=19;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 9999
   DESC='# DEPENDENTS ST'
   OLD=EMP.DEP.NO<1,2>
   NEW=VAL
   EMP.DEP.NO<1,2>=VAL
9999  *
   RETURN
      * ADDITIONAL FED & ST TAX
10000 *
   AN=20;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 10999
   DESC='ADDITIONAL FED TAX'
   OLD=OCONV(EMP.ADD.WHT<1,1>,'MD2')
   NEW=OCONV(VAL,'MD2')
   EMP.ADD.WHT<1,1>=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   AN=21;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 10999
   DESC='ADDITIONAL ST TAX'
   OLD=OCONV(EMP.ADD.WHT<1,2>,'MD2')
   NEW=OCONV(VAL,'MD2')
   EMP.ADD.WHT<1,2>=VAL
10999 *
   RETURN
      * FEDERAL TAX OVER WRITE
11000 *
   TEMP=EMP.FIT.CALC
   IF EMP.FIT.CALC<1,2> # '' THEN GO 11500
   AN=22;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
         GO 11800
      CASE VAL=''
         EMP.FIT.CALC<1,1>=''
         GO 11500
      CASE VAL # ''
         EMP.FIT.CALC=VAL
   END CASE
   GO 11800
11500 *
   AN=23;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
         GO 11800
      CASE VAL=''
         EMP.FIT.CALC=DELETE(EMP.FIT.CALC,1,2,0)
         IF TEMP<1,2> # '' THEN GO 11000
      CASE VAL # ''
         EMP.FIT.CALC<1,2>=VAL
         EMP.FIT.CALC<1,1>=''
   END CASE
11800 *
   IF TEMP<1,1> # '' THEN OLD=OCONV(TEMP<1,1>,'MD2'):'$'
   IF TEMP<1,2> # '' THEN OLD=OCONV(TEMP<1,2>,'MD2'):'%'
   IF EMP.FIT.CALC<1,1> # '' THEN NEW=OCONV(EMP.FIT.CALC<1,1>,'MD2'):'$'
   IF EMP.FIT.CALC<1,2> # '' THEN NEW=OCONV(EMP.FIT.CALC<1,2>,'MD2'):'%'
   DESC='FED TAX OVER WRITE'
   IF VAL='END' AND NEW.FLG='N' THEN GOSUB 90000
11999 *
   RETURN
      * STATE TAX CODE
12000 *
   AN=24;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 12999
   IF VAL # '' THEN
      READ TAX2.REC FROM TAX2,CONO:VAL ELSE
         MSG='TAX2 ':VAL:' IS MISSING'
         GOSUB 91000
         AN=24;SCV.REC(AN)<SN>=EMP.ST.CODE
         ACT=5;CALL SCRN.EDIT
         GO 12000
      END
   END
   DESC='STATE TAX CODE'
   OLD=EMP.ST.CODE
   NEW=VAL
   EMP.ST.CODE=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   OLD=''
   NEW=''
   IF EMP.ST.CODE='' THEN
      IF EMP.STATE.CALC # '' THEN
         IF EMP.STATE.CALC<1,1> # '' THEN
            OLD=OCONV(EMP.STATE.CALC<1,1>,'MD2'):'$'
         END ELSE
            OLD=OCONV(EMP.STATE.CALC<1,2>,'MD2'):'%'
         END
         NEW=''
         DESC='STATE TAX OVER WRITE'
         AN=49;SCV.REC(AN)<SN>=''
         ACT=5;CALL SCRN.EDIT
         AN=50;SCV.REC(AN)<SN>=''
         ACT=5;CALL SCRN.EDIT
         EMP.STATE.CALC=''
      END
      GO 12999
   END
   TEMP=EMP.STATE.CALC
   IF EMP.STATE.CALC<1,2> # '' THEN GO 12500
12300 *
   AN=49;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
      CASE VAL=''
         IF TEMP<1,2> # '' THEN
            EMP.STATE.CALC=''
         END ELSE
            EMP.STATE.CALC<1,1>=''
            GO 12500
         END
      CASE VAL # ''
         EMP.STATE.CALC=VAL
   END CASE
   GO 12800
12500 *
   AN=50;ACT=4;CALL SCRN.EDIT
   BEGIN CASE
      CASE VAL='END'
      CASE VAL=''
         IF TEMP<1,2> # '' THEN
            EMP.STATE.CALC=DELETE(EMP.STATE.CALC,1,2,0)
            GO 12300
         END ELSE
            EMP.STATE.CALC=''
         END
      CASE VAL # ''
         EMP.STATE.CALC<1,1>=''
         EMP.STATE.CALC<1,2>=VAL
   END CASE
12800 *
   IF TEMP<1,1> # '' THEN OLD=OCONV(TEMP<1,1>,'MD2'):'$'
   IF TEMP<1,2> # '' THEN OLD=OCONV(TEMP<1,2>,'MD2'):'%'
   IF EMP.STATE.CALC<1,1> # '' THEN NEW=OCONV(EMP.STATE.CALC<1,1>,'MD2'):'$'
   IF EMP.STATE.CALC<1,2> # '' THEN NEW=OCONV(EMP.STATE.CALC<1,2>,'MD2'):'%'
   DESC='STATE TAX OVER WRITE'
   IF VAL='END' AND NEW.FLG='N' THEN GOSUB 90000
12999 *
   RETURN
      * CITY TAX CODE
13000 *
   AN=25;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 13999
   IF VAL # '' THEN
      READ TAX2.REC FROM TAX2,CONO:VAL ELSE
         MSG='TAX2 ':VAL:' IS MISSING'
         GOSUB 91000;GO 13000
      END
   END
   DESC='CITY TAX CODE'
   OLD=EMP.CITY.CODE
   NEW=VAL
   EMP.CITY.CODE=VAL
13999 *
   RETURN
      * SHIFT
14000 *
   AN=26;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 15999
   DESC='SHIFT'
   OLD=EMP.SHIFT
   NEW=VAL
   EMP.SHIFT=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
      * SEX
15000 *
   AN=27;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 15999
   DESC='SEX'
   OLD=EMP.SEX
   NEW=VAL
   EMP.SEX=VAL
15999 *
   RETURN
      * PENSION
16000 *
   AN=28;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 16999
   DESC='PENSION'
   OLD=EMP.PENSION
   NEW=VAL
   EMP.PENSION=VAL
16999 *
   RETURN
      * SICK FICA
17000 * 04-28-97 This field is no longer valid - Task 21851
*     AN=29;ACT=4;CALL SCRN.EDIT
*     IF VAL='END' THEN GO 17999
*     DESC='SICK FICA'
*     OLD=EMP.SK.FICA
*     NEW=VAL
*     EMP.SK.FICA=VAL
   EMP.SK.FICA='Y'
   AN=29
   SCV.REC(AN)<SN>=EMP.SK.FICA; ACT=5; CALL SCRN.EDIT
17999 *
   RETURN
      * CREDIT UNION
18000 *
   AN=30;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 18999
   IF VAL # '' THEN
      READ UNION.REC FROM UNION,CONO:VAL ELSE
         MSG='UNION ':VAL:' IS MISSING'
         GOSUB 91000;GO 18000
      END
   END
   DESC='CREDIT UNION'
   OLD=EMP.UNION
   NEW=VAL
   EMP.UNION=VAL
18999 *
   RETURN
      * STATUS,PAY CYCLE AND (SALARY AND OT RATE) OR (RATE)
19000 *
   AN=31;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 19000
   DESC='PAY STATUS'
   OLD=EMP.STATUS
   NEW=VAL
   EMP.STATUS=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
19300 *
   AN=32;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 19300
   DESC='PAY CYCLE'
   OLD=EMP.PAY.CYCLE
   EMP.PAY.CYCLE=VAL
   NEW=VAL
   IF NEW.FLG='N' THEN GOSUB 90000
   IF EMP.STATUS='H' THEN GO 19700
   EMP.RATE=''
   SCV.REC(34)<SN>=''
   AN=34;ACT=5;CALL SCRN.EDIT
19500 *
   AN=33;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 19500
   DESC='SALARY'
   OLD=OCONV(EMP.SALARY,'MD2')
   EMP.SALARY=VAL
   EMP.JOB.CL.RATE=EMP.SALARY*100
   NEW=OCONV(VAL,'MD2')
   IF NEW.FLG='N' THEN GOSUB 90000
19600 *
   AN=48;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 19600
   DESC='OT RATE'
   OLD=OCONV(EMP.OT.RATE,'MD4')
   EMP.OT.RATE=VAL
   NEW=OCONV(EMP.OT.RATE,'MD4')
   GO 19999
19700 *
   EMP.SALARY=''
   EMP.OT.RATE=''
   SCV.REC(33)<SN>=''
   SCV.REC(48)<SN>=''
   AN=33;ACT=5;CALL SCRN.EDIT
   AN=48;ACT=5;CALL SCRN.EDIT
19800 *
   AN=34;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 19800
   DESC='RATE'
   OLD=OCONV(EMP.RATE,'MD4')
   EMP.RATE=VAL
   EMP.JOB.CL.RATE=VAL
   NEW=OCONV(VAL,'MD4')
19999 *
   RETURN
      * ALT.NO
20000 *
   AN=35;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 20999
   DESC='EMPLOYEE ALT NUMBER'
   OLD=EMP.ALT.NO
   NEW=VAL
   EMP.ALT.NO=VAL
20999 *
   RETURN
      * MAX.SIC
21000 *
   AN=36;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 21999
   IF VAL - INT((SUM(QTD.SCK.HRS) + .05) / 10) < 0 THEN
      MSG='AVAILABLE SICK HOURS IS LESS THAN ZERO'
      GOSUB 91000;GO 21000
   END
   DESC='MAX SICK HOURS'
   OLD=OCONV(EMP.MAX.SIC,'MD1')
   NEW=OCONV(VAL,'MD1')
   EMP.MAX.SIC=VAL
   AN=37;SCV.REC(37)<SN>=EMP.MAX.SIC - INT((SUM(QTD.SCK.HRS) + .05) / 10)
   ACT=5;CALL SCRN.EDIT
21999 *
   RETURN
      * MAX.VAC
22000 *
   AN=38;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 22999
   IF VAL - INT((SUM(QTD.VAC.HRS) + .05) / 10) < 0 THEN
      MSG='AVAILABLE VACATION HOURS IS LESS THAN ZERO'
      GOSUB 91000;GO 22000
   END
   DESC='MAX VAC HOURS'
   OLD=OCONV(EMP.MAX.VAC,'MD1')
   NEW=OCONV(VAL,'MD1')
   EMP.MAX.VAC=VAL
   AN=39;SCV.REC(39)<SN>=EMP.MAX.VAC - INT((SUM(QTD.VAC.HRS) + .05) / 10)
   ACT=5;CALL SCRN.EDIT
22999 *
   RETURN
      * STRT.DATE
23000 *
   AN=40;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 23999
   DESC='START DATE'
   OLD=OCONV(EMP.STRT.DATE,'D2/')
   NEW=OCONV(VAL,'D2/')
   EMP.STRT.DATE=VAL
23999 *
   RETURN
      * TERM.DATE
24000 *
   AN=42;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 24999
   DESC='TERMINATED DATE'
   OLD=OCONV(EMP.TERM.DATE,'D2/')
   NEW=OCONV(VAL,'D2/')
   EMP.TERM.DATE=VAL
24999 *
   RETURN
      * REHR.DATE
25000 *
   AN=43;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 25999
   DESC='REHIRED DATE'
   OLD=OCONV(EMP.REHR.DATE,'D2/')
   NEW=OCONV(VAL,'D2/')
   EMP.REHR.DATE=VAL
25999 *
   RETURN
      * DISB.DATE
26000 *
   AN=44;ACT=4;CALL SCRN.EDIT
   IF VAL='END' THEN GO 26999
   DESC='DISABLED DATE'
   OLD=OCONV(EMP.DISB.DATE,'D2/')
   NEW=OCONV(VAL,'D2/')
   EMP.DISB.DATE=VAL
26999 *
   RETURN
* LANGUAGE PREFERENCE
27000 *
   AN=53;ACT=4;CALL SCRN.EDIT
   IF VAL='END'THEN GO 27000
      EMP.LANGUAGE=VAL
27999 *
      RETURN
      * MAT SCV.REC
80000 *
      MATREAD QTD.REC FROM QTD.YTD,CONO:EMP.NO ELSE MAT QTD.REC=''
      MATREAD DIV.REC FROM DIVISION,CONO:EMP.DIV ELSE MAT DIV.REC=''
      MATREAD DEPT.REC FROM DEPARTMENT,CONO:EMP.DEPT ELSE MAT DEPT.REC=''
      IF EMP.COST.CNTR='000' THEN
         MAT CCTR.REC=''
         CCTR.DESC='GENERAL COST CENTER'
      END ELSE
         MATREAD CCTR.REC FROM COST.CNTR,CONO:EMP.COST.CNTR ELSE MAT CCTR.REC=''
      END
      SCV.REC(3)<SN>=EMP.LAST.NAME
      SCV.REC(4)<SN>=EMP.FRST.NAME
      SCV.REC(5)<SN>=EMP.TITLE
      SCV.REC(6)<SN>=EMP.ADDR1
      SCV.REC(7)<SN>=EMP.ADDR2
      SCV.REC(8)<SN>=EMP.CITY
      SCV.REC(9)<SN>=EMP.STATE
      SCV.REC(10)<SN>=EMP.ZIP
      SCV.REC(11)<SN>=EMP.PHONE
      SCV.REC(12)<SN>=EMP.BIRTHDAY
      SCV.REC(13)<SN>=EMP.SOC.SEC
      SCV.REC(14)<SN>=EMP.DEPT
      SCV.REC(15)<SN>=DEPT.DESC
      SCV.REC(16)<SN>=EMP.MAR.ST
      SCV.REC(17)<SN>=EMP.EIC
      SCV.REC(18)<SN>=EMP.DEP.NO<1,1>
      SCV.REC(19)<SN>=EMP.DEP.NO<1,2>
      SCV.REC(20)<SN>=EMP.ADD.WHT<1,1>
      SCV.REC(21)<SN>=EMP.ADD.WHT<1,2>
      SCV.REC(22)<SN>=EMP.FIT.CALC<1,1>
      SCV.REC(23)<SN>=EMP.FIT.CALC<1,2>
      SCV.REC(24)<SN>=EMP.ST.CODE
      SCV.REC(25)<SN>=EMP.CITY.CODE
      SCV.REC(26)<SN>=EMP.SHIFT
      SCV.REC(27)<SN>=EMP.SEX
      SCV.REC(28)<SN>=EMP.PENSION
      SCV.REC(29)<SN>=EMP.SK.FICA
      SCV.REC(30)<SN>=EMP.UNION
      SCV.REC(31)<SN>=EMP.STATUS
      SCV.REC(32)<SN>=EMP.PAY.CYCLE
      SCV.REC(33)<SN>=EMP.SALARY
      SCV.REC(34)<SN>=EMP.RATE
      SCV.REC(35)<SN>=EMP.ALT.NO
      SCV.REC(36)<SN>=EMP.MAX.SIC
      SCV.REC(37)<SN>=EMP.MAX.SIC - INT((SUM(QTD.SCK.HRS) + .05) / 10)
      SCV.REC(38)<SN>=EMP.MAX.VAC
      SCV.REC(39)<SN>=EMP.MAX.VAC - INT((SUM(QTD.VAC.HRS) + .05) / 10)
      SCV.REC(40)<SN>=EMP.STRT.DATE
      SCV.REC(41)<SN>=EMP.REV.DATE<1,1>
      SCV.REC(42)<SN>=EMP.TERM.DATE
      SCV.REC(43)<SN>=EMP.REHR.DATE
      SCV.REC(44)<SN>=EMP.DISB.DATE
      SCV.REC(46)<SN>=EMP.DIV
      SCV.REC(47)<SN>=DIV.DESC
      SCV.REC(48)<SN>=EMP.OT.RATE
      SCV.REC(49)<SN>=EMP.STATE.CALC<1,1>
      SCV.REC(50)<SN>=EMP.STATE.CALC<1,2>
      SCV.REC(51)<SN>=EMP.COST.CNTR
      SCV.REC(52)<SN>=CCTR.DESC
      SCV.REC(53)<SN>=EMP.LANGUAGE
      OLD.BDAY=OCONV(EMP.BIRTHDAY,'D2/')
      ACT=3;CALL SCRN.EDIT
      RETURN
      * CREATE EMPTRANS RECORD
90000 *
      IF TRAN.TYPE='3' AND OLD=NEW THEN GO 90999
      READU SEQ FROM CONTROL,CONO:'EMPTRANS' ELSE SEQ=0
      SEQ=SEQ + 1
      WRITE SEQ ON CONTROL,CONO:'EMPTRANS'
      REC=CONO:AM:EMP.NO:AM:TRAN.INT:AM:TRAN.TYPE
      IF TRAN.TYPE='3' THEN REC=REC:AM:DESC:AM:OLD:AM:NEW
      WRITE REC ON EMPTRANS,CONO:SEQ
90999 *
      RETURN
      * ERROR ROUTINE
91000 PRINT @(0,23):MSG:CL:;INPUT REP,1:;PRINT @(0):CL:;RETURN
92000 PRINT @(0,23):MSG:' FILE IS MISSING':CL:;INPUT REP,1:
99999 PRINT CS:
*T25236 v
      PC.PORT.TYPE = PC.PORT.TYPE.SAVE
      PC.PORT.TSEL = PC.PORT.TSEL.SAVE
*T25236 ^
   END
