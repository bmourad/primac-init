*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM     - PRIMAC
* SOURCE     - PRSBP
* PROGRAM    - W2BACK-UP
* BY         - WALID YAMOUT, CBA
* DATE       - 06/11/85
* DESCRIPTION-
*********************************************************************
*
*
*
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>QTD-YTD
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>FICA.TABLE
*COPY>PRS.CPYLIB>401K.MATCH
*COPY>CPYLIB>CHAR
*
*---- OPEN FILES
*
      OPEN "S.QTD-YTD" TO QTD.YTD ELSE PRINT "CAN'T OPEN QTD-YTD FILE! HIT RETURN!":;INPUT WAIT;STOP
      OPEN "S.EMPLOYEE" TO EMPLOYEE ELSE PRINT "CAN'T OPEN EMPLOYEE FILE! HIT RETURN":;INPUT WAIT;STOP
      OPEN "MISCDED" TO MISCDED ELSE PRINT "CAN'T OPEN MISCDED FILE! HIT RETURN!":;INPUT WAIT;STOP
      OPEN "S.COMPANY" TO COMPANY ELSE PRINT "CAN'T OPEN S.COMPANY FILE! HIT RETURN!":;INPUT WAIT;STOP
      OPEN "DICT","S.COMPANY" TO D.COMPANY ELSE PRINT "CAN'T OPEN DICT OF S.COMPANY! HIT RETURN!":;INPUT WAIT;STOP
      OPEN "CONTROL" TO CONTROL ELSE PRINT "CAN'T OPEN CONTROL FILE! HIT RETURN!":;INPUT WAIT;STOP
      OPEN "S.FICA.TABLE" TO FICA.TABLE ELSE PRINT "CAN'T OPEN FICA.TABLE FILE! HIT RETURN!":;INPUT WAIT;STOP
*
*---- DEFINE CONSTANTS
*
      MAT COMP.REC="";MAT EMP.REC="";MAT QTD.REC=""
      ATIME=OCONV(TIME(),"MT");DAT=OCONV(DATE(),"D2/");PG=1
      SOJ="YES"
      TOT.FWHT = 0
      TOT.GRS = 0
      TOT.K401.DED = 0
      TOT.ADJ.GROSS = 0
      TOT.FICA.L1 = 0
      TOT.FICA.L2 = 0
      TOT.ST = 0
      TOT.CITY = 0
      TOT.EIC = 0
*
*---- GET INFO
*
      READV CONO FROM D.COMPANY,"CCD",1 ELSE PRINT "CAN'T READ COMPANY CODE! HIT RETURN!":;INPUT WAIT;STOP
      READ DC.REC FROM CONTROL, CONO:"401K" ELSE PRINT "CAN'T READ CONTROL! HIT RETURN!":;INPUT WAIT;STOP
*CSF10946
      MATCH.CODES = ''
      MATCH.CODES<1>=DC.REC<1>
      MATCH.CODES<2>=DC.REC<3>
      MATREAD MTCH.REC FROM CONTROL, CONO:"401K.MATCH" ELSE MAT MTCH.REC =""
*
      MATREAD COMP.REC FROM COMPANY,CONO ELSE PRINT "CAN'T GET INFO FOR COMPANY ":CONO:" ITEM! HIT RETURN!":;INPUT WAIT;STOP
      MATREAD FIC.REC FROM FICA.TABLE, CONO ELSE
        PRINT  "CANNOT LOCATE FICA.TABLE FOR CONO ":CONO:
        INPUT WAIT:; GOTO 99999
      END
*
      PRINTER ON
10 *
    READNEXT EMPID ELSE GOTO 9000
      MATREAD EMP.REC FROM EMPLOYEE,EMPID ELSE GOTO 10
*csf10946
         MFLAG=0
         MPCT = 0
         FOR CD.VAL = 1 TO DCOUNT(MATCH.CODES,AM)
         LOCATE MATCH.CODES<CD.VAL> IN EMP.MSC.CODE<1>,1 SETTING MLOC ELSE MLOC=0
         IF MLOC NE 0 THEN MPCT = MPCT + EMP.MSC.AMT<1,MLOC>
         MFLAG=1
         MLOC = 0
         NEXT CD.VAL
         IF MPCT GT 0 OR MFLAG THEN
            MTCH.CNT = DCOUNT(MTCH.EMP.PCT<1>,VM)
            FOUND = 0
            FOR MVAL = 1 TO MTCH.CNT UNTIL FOUND
              IF MPCT LE MTCH.EMP.PCT<1,MVAL> THEN
                 MAT.PCT = MTCH.CO.PCT<1,MVAL>
                 FOUND = 1
              END
            NEXT MVAL
            IF NOT(FOUND) THEN
              MAT.PCT = MTCH.CO.PCT<1,MTCH.CNT>
            END
         END
*
      ALT.GROSS=0;ALT.NTSICK=0; ADJ.ALT.GROSS = 0
      S.S.NO = EMP.SOC.SEC
      IF EMP.ALT.NO # "" THEN
         MATREAD QTD.REC FROM QTD.YTD,EMPID[1,3]:EMP.ALT.NO ELSE MAT QTD.REC=""
         ALT.GROSS=SUM(QTD.GROSS)
         ALT.NTSICK=SUM(QTD.SK.NO.FI)
         GOSUB 5000
         ADJ.ALT.GROSS=ALT.GROSS-NON.TAX.DED
      END
      MATREAD QTD.REC FROM QTD.YTD,EMPID ELSE GOTO 10
      EMP.NO = EMPID[4,4]
*
      X=(30-LEN(CO.NAME))+10
      H1 = SPACE(10):CO.NAME:SPACE(X):"W2 BACK-UP REPORT":SPACE(24):"TIME: ":ATIME:"  DATE :":DAT:"  PAGE: "
      H2 = " EMP. NO     S.S. NO"
      H2[77,5] = 'FICA/'
      H3 = ""
      H3[10,4] = "NAME"
      H3[33,4] = "FWHT"
      H3[43,5] = "GROSS"
      H3[55,6] = "401(k)"
      H3[64,9] = "ADJ GROSS"
      H3[75,8] = "MEDICARE"
      H3[85,2] = "ST"
      H3[92,6] = "ST-TAX"
      H3[102,4] = "CITY"
      H3[109,6] = "CT-TAX"
      H3[122,3] = "EIC"
      H4 = ""
      H4[02,26] = STR("-",25)
      H4[30,10] = "----------"
      H4[41,10] = "----------"
      H4[52,10] = "----------"
      H4[63,10] = "----------"
      H4[74,10] = "----------"
      H4[85,04] = "----"
      H4[90,10] = "----------"
      H4[102,04]= "----"
      H4[107,10]= "----------"
      H4[118,10] = "----------"
*
*---- SUM QUARTERLY AMTS FOR GROSS, FWHT, FICA, STATE, CITY
*
      FWHT=SUM(QTD.FWHT)
      GROSS=SUM(QTD.GROSS)
      FICA=SUM(QTD.FICA)
      STATE=SUM(QTD.SWHT)
      CITY=SUM(QTD.CWHT)
      EIC=SUM(QTD.EIC)
      NTSICK = SUM(QTD.SK.NO.FI)
      FICA.L1 = SUM(QTD.LVL1.FICA)
      FICA.L2 = SUM(QTD.LVL2.FICA)
*
      GOSUB 5000
      ADJ.GROSS = GROSS - NON.TAX.DED
      THIS.FICA=ADJ.GROSS-NTSICK;THAT.FICA=ADJ.ALT.GROSS-ALT.NTSICK
      BEGIN CASE
      CASE THIS.FICA > FIC.LVL1.MAX.FICA.TAX
         THIS.FICA=FIC.LVL1.MAX.FICA.TAX
      CASE THAT.FICA > FIC.LVL1.MAX.FICA.TAX
         THIS.FICA=0
      CASE (THIS.FICA+THAT.FICA) > FIC.LVL1.MAX.FICA.TAX
         IF EMP.TERM.DATE='' THEN THIS.FICA=FIC.LVL1.MAX.FICA.TAX-THAT.FICA
         IF THIS.FICA<0 THEN THIS.FICA=0
      END CASE
      TOTFICA.L1=THIS.FICA
      THIS.FICA=ADJ.GROSS-NTSICK;THAT.FICA=ADJ.ALT.GROSS-ALT.NTSICK
      BEGIN CASE
      CASE THIS.FICA > FIC.LVL2.MAX.FICA.TAX
         THIS.FICA=FIC.LVL2.MAX.FICA.TAX
      CASE THAT.FICA > FIC.LVL2.MAX.FICA.TAX
         THIS.FICA=0
      CASE (THIS.FICA+THAT.FICA) > FIC.LVL2.MAX.FICA.TAX
         IF EMP.TERM.DATE='' THEN THIS.FICA=FIC.LVL2.MAX.FICA.TAX-THAT.FICA
         IF THIS.FICA<0 THEN THIS.FICA=0
      END CASE
      TOTFICA.L2=THIS.FICA
*
*---- TOTALS
*
      TOT.FWHT = TOT.FWHT + FWHT
      TOT.GRS = TOT.GRS + GROSS
      TOT.K401.DED = TOT.K401.DED + K401.DED
      TOT.ADJ.GROSS = TOT.ADJ.GROSS + ADJ.GROSS
      TOT.FICA.L1 = TOT.FICA.L1 + FICA.L1
      TOT.FICA.L2 = TOT.FICA.L2 + FICA.L2
      TOT.ST = TOT.ST + STATE
      TOT.CITY = TOT.CITY + CITY
      TOT.EIC = TOT.EIC + EIC
*
*---- START OF PRINT
*
      IF SOJ="YES" THEN
         HEADING H1:PG:"'LL'":H2:"'L'":H3:"'L'":H4:"'L'"
         LINECT=5;SOJ="NO"
      END
      NAME=EMP.LAST.NAME:", ":EMP.FRST.NAME
      NAME = NAME[1,28]
      V1.LINE = ""
      V1.LINE[2,4] = EMP.NO
      V1.LINE[9,11] = S.S.NO
      V1.LINE[74,10] = OCONV(FICA.L1, 'MD2') 'R#10'
      PRINT V1.LINE
      V.LINE = ""
      V.LINE[04,28] = NAME  
      V.LINE[30,10] = OCONV(FWHT,"MD2") "R#10"
      V.LINE[41,10] = OCONV(GROSS,"MD2") "R#10"
      V.LINE[52,10] = OCONV(K401.DED,"MD2") "R#10"
      V.LINE[63,10] = OCONV(ADJ.GROSS,"MD2") "R#10"
      V.LINE[74,10] = OCONV(FICA.L2,"MD2") "R#10"
      V.LINE[85,04] = EMP.ST.CODE "L#4"
      V.LINE[90,10] = OCONV(STATE,"MD2") "R#10"
      V.LINE[102,04]= EMP.CITY.CODE "L#4"
      V.LINE[107,10]= OCONV(CITY,"MD2") "R#10"
      V.LINE[118,10] = OCONV(EIC,"MD2") "R#10"
      PRINT V.LINE
      LINECT=LINECT+2
      IF LINECT >= 55 THEN
         PG=PG+1
         HEADING H1:PG:"'LL'":H2:"'L'":H3:"'L'":H4:"'L'"
         LINECT=5
         GOTO 10
         END ELSE GOTO 10
9000*
      UNDER.LINE = ""
      UNDER.LINE[30,10] = "----------"
      UNDER.LINE[41,10] = "----------"
      UNDER.LINE[52,10] = "----------"
      UNDER.LINE[63,10] = "----------"
      UNDER.LINE[74,10] = "----------"
      UNDER.LINE[90,10] = "----------"
      UNDER.LINE[107,10]= "----------"
      UNDER.LINE[118,10] = "----------"
*
      TOT.LINE = ""
PRINT UNDER.LINE
      TOT.LINE[02,26] = "TOTAL FOR THE COMPANY"
      TOT.LINE[74,10] = OCONV(TOT.FICA.L1, 'MD2') 'R#10'
      PRINT TOT.LINE
      TOT.LINE = ''
      TOT.LINE[30,10] = OCONV(TOT.FWHT,"MD2") "R#10"
      TOT.LINE[41,10] = OCONV(TOT.GRS,"MD2") "R#10"
      TOT.LINE[52,10] = OCONV(TOT.K401.DED,"MD2") "R#10"
      TOT.LINE[63,10] = OCONV(TOT.ADJ.GROSS,"MD2") "R#10"
      TOT.LINE[74,10] = OCONV(TOT.FICA.L2,"MD2") "R#10"
      TOT.LINE[90,10] = OCONV(TOT.ST,"MD2") "R#10"
      TOT.LINE[107,10]= OCONV(TOT.CITY,"MD2") "R#10"
      TOT.LINE[118,10] = OCONV(TOT.EIC,"MD2") "R#10"
*
*     PRINT UNDER.LINE
      PRINT TOT.LINE
*
      GOTO 99999
*
*---- NON TAXABLE DEDUCTIONS
5000*
      NON.TAX.DED=0
      NTD.CNT=DCOUNT(QTD.DED,VM)
      FOR REF=1 TO NTD.CNT
         MATREAD MDED.REC FROM MISCDED,CONO:QTD.DED<1,REF> ELSE MAT MDED.REC=""
         IF MDED.TAXABLE="B" THEN
            FOR X=1 TO 4
               NON.TAX.DED=NON.TAX.DED+QTD.AMNT<1,REF,X>
            NEXT X
         END
      NEXT REF
*
      K401.DED=0
      FOR I = 1 TO 2
         LOCATE DC.REC<I> IN QTD.DED<1> SETTING POS ELSE POS = 0
         IF POS THEN
            MATREAD MDED.REC FROM MISCDED, CONO:QTD.DED<1,POS> ELSE MAT MDED.REC = ""
            IF MDED.TAXABLE = "B" AND MDED.NTXFED = "Y" THEN
               FOR X = 1 TO 4
                  K401.DED = K401.DED + QTD.AMNT<1,POS,X>
               NEXT X
            END
         END
      NEXT I
*csf10946
**      K401.DED.M = 0
**      FOR CD.VAL = 1 TO DCOUNT(MATCH.CODES,AM)
**         LOCATE MATCH.CODES<CD.VAL> IN QTD.DED<1>,1 SETTING MLOC ELSE MLOC=0
**         IF MLOC NE 0 THEN
**         FOR X = 1 TO 4
**           K401.DED.M = K401.DED.M + QTD.AMNT<1,MLOC,X>
**         NEXT X
**         END
**         MLOC = 0
**      NEXT CD.VAL
**     K401.DED.EMPR = 0
**       IF K401.DED.M GT 0 AND MAT.PCT GT 0 THEN
**          K401.DED.EMPR = INT((K401.DED.M/100) * (MAT.PCT/100)+.5)
**       END
**      K401.DED = K401.DED.EMPR
*
      RETURN
*
99999*
      PRINTER OFF
   END
