  SUBROUTINE EMPLOYEE.MAINT4(DED.CODE,DED.NO)
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.EMPLOYEE
*COPY>PRS.CPYLIB>COM.QTD-YTD
*COPY>PRS.CPYLIB>COM.MISCDED
*COPY>PRS.CPYLIB>COM.PRS.FILE.VARS
*COPY>PRS.CPYLIB>COM.EMPLOYEE.MAINT
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SOURCE      - EPSBP
* PROGRAM     - EMPLOYEE.MAINT.DED.SUB
* BY          - MARK TRAYWICK, CBA
* DATE        - 05/12/88
* DESCRIPTION - This sub defines the investment options in the
*               EMPLOYEE file.
*T25978 adelgado 02/01/2002 * Add the use of prompts (SF,SR,SB,ST).
*********************************************************************
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>INVEST.FUND.CODE
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>FILE.VARS
*COPY>PRS.CPYLIB>PRS.FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE=1;CALL SYSCOM(MAT SYSCOM.REC)
*
*--- INITIALIZATION
*
  UNKNOWN = "??????????"
  OPTION = "A"
  OLD.START.LINE =''; PAGE.SIZE = 8
  LN = 1
  SCV.REC(1)<ECD.SCRN.NO> = DED.CODE
  SCV.REC(2)<ECD.SCRN.NO> = MDED.SCRN.DESC
  SCV.REC(3)<ECD.SCRN.NO> = EMP.LAST.NAME:", ":EMP.FRST.NAME
  GOSUB 11000
  IF LAST.LINE THEN
    ECD.ACTION = 3; CALL SCRN.EDIT
  END ELSE
    LAST.LINE = DCOUNT(MDED.INVEST,VM)
    FOR I = 1 TO LAST.LINE
      EMP.INVEST<1,DED.NO,I> = MDED.INVEST<1,I>
      EMP.INVEST.PCT<1,DED.NO,I> = 0
    NEXT I
    GOSUB 11000
    ECD.ACTION = 3; CALL SCRN.EDIT
    FOR LN = 1 TO LAST.LINE UNTIL ECD.RET.VALUE = "END"
      OPTION = LN
      GOSUB 10000
      GOSUB 900
    NEXT LN
  END
  GOSUB 12000
*
*--- MAIN PROCESSING
5 *
  ECD.NUM = 21; ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = 'END' OR OPTION = 'E'
      GOSUB 12000
      BEGIN CASE
        CASE TOT > 100
          ERRMSG = 'INVESTMENT PERCENTAGES GREATER THAN 100%'
          GOSUB 91000
          GOTO 5
        CASE TOT < 100
          ERRMSG = 'INVESTMENT PERCENTAGES LESS THAN 100%'
          GOSUB 91000
          GOTO 5
      END CASE
      GOTO 99999
    CASE OPTION = 'A'
      LOOP
        LN = LAST.LINE + 1
        SCV.REC(6)<ECD.SCRN.NO,LN> = LN
        OLD.START.LINE = 0
        GOSUB 10000
        OLD.LINES = LAST.LINE
        GOSUB 700
        GOSUB 12000
      WHILE LAST.LINE > OLD.LINES DO
      REPEAT
      LN = LAST.LINE
      OLD.START.LINE = 0
      GOSUB 11000
    CASE OPTION = 'D'
      ECD.NUM = 22; ECD.ACTION = 4; CALL SCRN.EDIT
      IF ECD.RET.VALUE # 'END' THEN
        LN = ECD.RET.VALUE
        EMP.INVEST = DELETE(EMP.INVEST,1,DED.NO,LN)
        EMP.INVEST.PCT = DELETE(EMP.INVEST.PCT,1,DED.NO,LN)
        GOSUB 11000
        IF LN > LAST.LINE THEN LN = LAST.LINE
        IF LN < 1 THEN LN = 1
        OLD.START.LINE = 0
      END
    CASE OPTION = 'S'
      LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
      IF LN > LAST.LINE THEN LN = 1
    * T25978 v
    CASE OPTION = 'SR'
      LN = 1 + INT((LN-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
      IF LN < 1 THEN LN = 1
    CASE OPTION = 'ST'
      LN = 1
    CASE OPTION = 'SB'
      LN = LAST.LINE
    * T25978 ^
    CASE NOT(NUM(OPTION))
      GOTO 5
    CASE OPTION > 0 AND OPTION <= LAST.LINE
      LN = OPTION
      GOSUB 10000
      GOSUB 900
    CASE OPTION < 0 OR OPTION > LAST.LINE
      ERRMSG = 'OUT OF RANGE'; GOSUB 91000
  END CASE
  GOSUB 10000
  GOSUB 12000
  GOTO 5
*
*--- GET INVESTMENT CODE
700 *
  ECD.NUM = 7; ECD.SUB.NUM = LN
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END" AND OPTION = "A"
      EMP.INVEST = DELETE(EMP.INVEST,1,DED.NO,LN)
      EMP.INVEST.PCT = DELETE(EMP.INVEST.PCT,1,DED.NO,LN)
      GOTO 959
    CASE ECD.RET.VALUE = "END"
      GOTO 959
    CASE 1
      FOR K = 1 TO LAST.LINE
        IF ECD.RET.VALUE = EMP.INVEST<1,DED.NO,K> AND K # LN THEN
          ERRMSG = 'DUPLICATE CODE ENTERED'; GOSUB 91000
          GOTO 700
        END
      NEXT K
      READ IFCR.DESC FROM INVEST.FUND.CODE, CONO:ECD.RET.VALUE THEN
        EMP.INVEST<1,DED.NO,LN> = ECD.RET.VALUE
        SCV.REC(8)<ECD.SCRN.NO,LN> = IFCR.DESC
        ECD.NUM = 8; ECD.SUB.NUM = LN
        ECD.ACTION = 5; CALL SCRN.EDIT
        LAST.LINE = LN
      END ELSE
        ERRMSG = 'INVESTMENT FUND CODE ':ECD.RET.VALUE:' NOT FOUND'
        GOSUB 91000
        GOTO 700
      END
  END CASE
*
*--- GET INVESTMENT PCT
900 *
  ECD.NUM = 9; ECD.SUB.NUM = LN
  ECD.ACTION = 4; CALL SCRN.EDIT
  BEGIN CASE
    CASE ECD.RET.VALUE = "END" AND OPTION = "A"
      GOTO 700
    CASE ECD.RET.VALUE = "END"
    CASE 1
      EMP.INVEST.PCT<1,DED.NO,LN> = ECD.RET.VALUE
  END CASE
959*
  LAST.LINE = DCOUNT(EMP.INVEST<1,DED.NO>,SVM)
  RETURN
*
*--- SCROLLING ROUTINE
10000 *
  START.LINE = 1 + INT((LN-1)/PAGE.SIZE)* PAGE.SIZE
  IF START.LINE = OLD.START.LINE THEN RETURN
  OLD.START.LINE = START.LINE
  FOR J = 6 TO 9
    ECD.NUM = J
    ECD.SUB.NUM = START.LINE
    ECD.ACTION = 7
    CALL SCRN.EDIT
  NEXT J
  RETURN
*
*--- LOAD SCROLLING AREA
11000 *
  FOR II = 1 TO SCV.REC.SIZE
    SCV.REC(II)<ECD.SCRN.NO> = ""
  NEXT II
  SCV.REC(1)<ECD.SCRN.NO> = DED.CODE
  SCV.REC(2)<ECD.SCRN.NO> = MDED.SCRN.DESC
  SCV.REC(3)<ECD.SCRN.NO> = EMP.LAST.NAME:", ":EMP.FRST.NAME
  LAST.LINE = DCOUNT(EMP.INVEST<1,DED.NO>,SVM)
  IF LAST.LINE THEN
    FOR L = 1 TO LAST.LINE
      SCV.REC(6)<ECD.SCRN.NO,L> = L
      SCV.REC(7)<ECD.SCRN.NO,L> = EMP.INVEST<1,DED.NO,L>
      SCV.REC(9)<ECD.SCRN.NO,L> = EMP.INVEST.PCT<1,DED.NO,L>
      READ IFCR.DESC FROM INVEST.FUND.CODE, CONO:EMP.INVEST<1,DED.NO,L> ELSE
        IFCR.DESC = UNKNOWN
      END
      SCV.REC(8)<ECD.SCRN.NO,L> = IFCR.DESC
    NEXT L
  END
11099 *
  RETURN
*
*--- TOTAL INVESTMENT PERCENTAGES
12000 *
  TOT = 0
  CNT = DCOUNT(EMP.INVEST<1,DED.NO>,SVM)
  FOR L = 1 TO CNT
    TOT = TOT + EMP.INVEST.PCT<1,DED.NO,L>
  NEXT L
  SCV.REC(11)<ECD.SCRN.NO> = TOT
  ECD.NUM = 11; ECD.ACTION = 5; CALL SCRN.EDIT
  RETURN
*
*--- SYSCOM CALLS
91000 * 
  ERR.TYPE = 1; CALL SYSCOM(MAT SYSCOM.REC)
  RETURN
93000 * 
  ERR.TYPE = 3; CALL SYSCOM(MAT SYSCOM.REC)
99999 * 
  RETURN
END
