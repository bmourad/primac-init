  SUBROUTINE EMPLOYEE.MAINT3
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - EPSBP
* PROGRAM     - EMPLOYEE.MAINT.SUB
* BY          - WALID YAMOUT, CBA
* DATE        - 06/14/85
* DESCRIPTION -
* MOD         - CSF10138 RWW 10.27.89
*T21635 lanny 02/25/1997 * Fix display of review dates after RTN from
*                          4th screen.
*T25978 adelgado 02/01/2002 * Add the use of prompts (SF,SR,SB,ST).
*ENDDOC
*********************************************************************
*COPY>CPYLIB>COM1
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*********************************************************************
*COPY>PMC.CPYLIB>COM.COMPANY
*COPY>PMC.CPYLIB>COM.EMPLOYEE
*COPY>PRS.CPYLIB>COM.QTD-YTD
*COPY>PRS.CPYLIB>COM.MISCDED
*COPY>PRS.CPYLIB>COM.PRS.FILE.VARS
*COPY>PRS.CPYLIB>COM.EMPLOYEE.MAINT
*********************************************************************
*COPY>PRS.CPYLIB>PRS.FILE.VARS
*COPY>PMC.CPYLIB>EMPLOYEE
*COPY>PMC.CPYLIB>COMPANY
*COPY>PRS.CPYLIB>MISCDED
*COPY>PRS.CPYLIB>401K
  ERRMSG = ""
  OLD = ""
  NEW = ""
  DESC = ""
  TRAN.TYPE = "3"
  LINE.DESC = ""
  LINE.TYPE = ""
  LINE.TAX = ""
  M.CL = SPACE(40)
  R.CL = SPACE(13)
  TOT.BT = 0
  MAT MDED.REC = ''
*
  MATREAD DC.REC FROM CONTROL,CONO:"401K" ELSE MAT DC.REC = ""
*
*--- INITIALIZATION
*
  ECD.SCRN.NO = 3
  SCV.REC(4)<ECD.SCRN.NO> = EMP.NO
  SCV.REC(5)<ECD.SCRN.NO> = EMP.LAST.NAME:" ":EMP.FRST.NAME
  SCV.REC(6)<ECD.SCRN.NO> = EMP.BONDS<1,3>
  SCV.REC(7)<ECD.SCRN.NO> = EMP.BONDS<1,2>
  SCV.REC(8)<ECD.SCRN.NO> = EMP.BONDS<1,1>
  SCV.REC(9)<ECD.SCRN.NO> = EMP.BONDS<1,4>
  ECD.ACTION=3;CALL SCRN.EDIT
  LINE.SPACE = 1
  PAGE.SIZE = 12
  BEGIN.PAGE = 8
  M.OLD.START = 0
  R.OLD.START = 0
  M.LN = 1
  M.LINES = 0
  IF NEW.FLG = "Y" THEN
    GOSUB 1000
    OPTION = "A"
    LOOP
      M.LN = M.LINES + 1
      OLD.LINES = M.LINES
      GOSUB 5000
    WHILE M.LINES > OLD.LINES DO REPEAT
    M.LN = M.LINES
  END ELSE
    M.LINES = COUNT(EMP.MSC.CODE,VM) + (EMP.MSC.CODE # "")
    FOR I = 1 TO M.LINES
      MATREAD MDED.REC FROM MISCDED, CONO:EMP.MSC.CODE<1,I> ELSE
        MAT MDED.REC = ""
        MDED.TYPE = "$"
        MDED.SCRN.DESC = "??????"
        MDED.TAXABLE = "A"
      END
      IF MDED.TAXABLE = 'B' AND MDED.TYPE = "%" THEN
        IF EMP.MSC.CODE<1,I>=DC.REC(1) OR EMP.MSC.CODE<1,I>=DC.REC(2) THEN
          TOT.BT = TOT.BT + EMP.MSC.AMT<1,I>
        END
      END
      LINE.DESC<I> = MDED.SCRN.DESC
      LINE.TYPE<I> = MDED.TYPE
      LINE.TAX<I> = MDED.TAXABLE
    NEXT I
  END
  GOSUB 10000
  REVIEW.DATES = ""
  R.LINES = COUNT(EMP.REV.DATE,VM) + (EMP.REV.DATE # "")
  FOR R.LN = R.LINES TO 1 STEP -1
    REVIEW.DATES<-1> = EMP.REV.DATE<1,R.LN>
  NEXT R.LN
  R.LN = 1; GOSUB 20000
*
*--- ENTER REQUEST
*
500*
  ECD.NUM = 1; SCV.REC(1)<ECD.SCRN.NO> = ""
  ECD.ACTION = 4; CALL SCRN.EDIT
  REQUEST = ECD.RET.VALUE
  BEGIN CASE
    CASE REQUEST = "E" OR REQUEST = "END"
      EMP.REV.DATE = ""
      FOR R.LN = R.LINES TO 1 STEP -1
        EMP.REV.DATE<1,-1> = REVIEW.DATES<R.LN>
      NEXT R.LN
      GOTO 999999
    CASE REQUEST = "B"
      GOSUB 1000
    CASE REQUEST = "D"
      GOSUB 2000
    CASE REQUEST = "R"
      GOSUB 3000
  END CASE
  GOTO 500
*-----------------*
*   SUBROUTINES   *
*-----------------*
*
*--- BONDS
*
1000*
  ECD.NUM = 6; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1999
  TEMP = EMP.BONDS
  IF ECD.RET.VALUE = "" THEN
    EMP.BONDS = ""
    ECD.NUM = 7; SCV.REC(7)<ECD.SCRN.NO> = ""
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 8; SCV.REC(8)<ECD.SCRN.NO> = ""
    ECD.ACTION = 5; CALL SCRN.EDIT
    ECD.NUM = 9; SCV.REC(9)<ECD.SCRN.NO> = ""
    ECD.ACTION = 5; CALL SCRN.EDIT
    GOTO 1888
  END
  READ XXX FROM PAYTYPE, CONO:"BONDS" ELSE XXX = ""
  LOCATE ECD.RET.VALUE IN XXX,1 SETTING FND ELSE
    ERRMSG = "BOND ":OCONV(ECD.RET.VALUE, "MD2"):" IS NOT SETUP ON BONDS TABLE"
    GOSUB 91000; GOTO 1000
  END
  EMP.BONDS<1,3> = ECD.RET.VALUE
1010*
  ECD.NUM = 7; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1010
  EMP.BONDS<1,2> = ECD.RET.VALUE
1020*
  ECD.NUM = 8; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1020
  EMP.BONDS<1,1> = ECD.RET.VALUE
1030*
  ECD.NUM = 9; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN GOTO 1030
  EMP.BONDS<1,4> = ECD.RET.VALUE
1888*
  IF NEW.FLG = "Y" THEN GOTO 1999
  TRAN.TYPE = "3"
  DESC = "BONDS DEDUCTION"
  OLD = TEMP<1,1>
  NEW = EMP.BONDS<1,1>
  GOSUB 90000
  DESC = "BONDS ACCUMULATION"
  OLD = TEMP<1,2>
  NEW = EMP.BONDS<1,2>
  GOSUB 90000
  DESC = "BONDS DENOMINATION"
  OLD = TEMP<1,3>
  NEW = EMP.BONDS<1,3>
  GOSUB 90000
  DESC = "BONDS WEEK"
  OLD = TEMP<1,4>
  NEW = EMP.BONDS<1,4>
  GOSUB 90000
1999*
  RETURN
*
*--- OPTIONS FOR DEDUCTIONS
*
2000*
  ECD.NUM = 10; SCV.REC(10)<ECD.SCRN.NO> = ""
  ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "" OR OPTION = "END"
      GOTO 2999
    CASE OPTION = "A"
      LOOP
        M.LN = M.LINES + 1
        OLD.LINES = M.LINES
        GOSUB 5000
      WHILE M.LINES > OLD.LINES DO REPEAT
      M.LN = M.LINES
    CASE OPTION = "C"
      GOSUB 30000
      IF LNO THEN
        M.LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(M.LN-1,PAGE.SIZE)
        GOSUB 5010
      END
    CASE OPTION = "D"
      GOSUB 30000
      IF LNO THEN
        M.LN = LNO
        TRAN.TYPE = "4"
        DESC = "MISC DED ":EMP.MSC.CODE<1,M.LN>
        IF NEW.FLG = "N" THEN GOSUB 90000
        IF LINE.TAX<M.LN> = "B" AND LINE.TYPE<M.LN> = "%" THEN
          IF EMP.MSC.CODE<1,M.LN>=DC.REC(1) OR EMP.MSC.CODE<1,M.LN>=DC.REC(2) THEN
            TOT.BT = TOT.BT - EMP.MSC.AMT<1,M.LN>
          END
        END
        EMP.MSC.CODE = DELETE(EMP.MSC.CODE,1,M.LN,0)
        EMP.MSC.AMT = DELETE(EMP.MSC.AMT,1,M.LN,0)
        EMP.MSC.DED = DELETE(EMP.MSC.DED,1,M.LN,0)
        EMP.MSC.WK = DELETE(EMP.MSC.WK,1,M.LN,0)
        EMP.INVEST = DELETE(EMP.INVEST,1,M.LN,0)
        EMP.INVEST.PCT = DELETE(EMP.INVEST.PCT,1,M.LN,0)
        LINE.DESC = DELETE(LINE.DESC,M.LN,0,0)
        LINE.TYPE = DELETE(LINE.TYPE,M.LN,0,0)
        LINE.TAX = DELETE(LINE.TAX,M.LN,0,0)
        M.LINES = COUNT(EMP.MSC.CODE,VM) + (EMP.MSC.CODE # "")
        IF M.LN > M.LINES THEN M.LN = M.LINES
        IF M.LN < 1 THEN M.LN = 1
        M.OLD.START = 0
      END
    CASE OPTION = "S"
      M.LN = 1 + INT((M.LN-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
      IF M.LN > M.LINES THEN M.LN = 1
    * T25978 v
    CASE OPTION = 'SR'
      M.LN = 1 + INT((M.LN-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
      IF M.LN < 1 THEN M.LN = 1
    CASE OPTION = 'ST'
      M.LN = 1
    CASE OPTION = 'SB'
      M.LN = M.LINES
    * T25978 ^
    CASE OPTION = "R"
      GOSUB 30000
      IF LNO THEN
        M.LN = LNO
        MATREAD MDED.REC FROM MISCDED,CONO:EMP.MSC.CODE<1,M.LN> THEN
          IF MDED.CONT.PLAN = 'Y' THEN
            ECD.SCRN.NO = ECD.SCRN.NO + 1
            CALL EP4.EMPLOYEE.MAINT4(EMP.MSC.CODE<1,M.LN>,M.LN)
            ECD.SCRN.NO = ECD.SCRN.NO - 1
            ECD.ACTION = 3; CALL SCRN.EDIT
*T21635 v
            R.LN=1; R.OLD.START=0; GOSUB 20000
*T21635 ^
            M.OLD.START = 0
          END ELSE
            ERRMSG = 'REVIEW FOR DEFINED CONTRIBUTION PLAN DEDUCTION CODES ONLY'; GOSUB 91000
            GOTO 2000
          END
        END
      END
  END CASE
  GOSUB 10000
  GOTO 2000
2999*
  RETURN
*
*--- OPTIONS FOR REVIEW DATES
*
3000*
*CSF10138
  ECD.NUM = 2; SCV.REC(2)<ECD.SCRN.NO> = ""
*
  ECD.ACTION = 4; CALL SCRN.EDIT
  OPTION = ECD.RET.VALUE
  BEGIN CASE
    CASE OPTION = "" OR OPTION = "END"
      GOTO 3999
    CASE OPTION = "A"
      LOOP
        R.LN = R.LINES + 1
        OLD.LINES = R.LINES
        GOSUB 7000
      WHILE R.LINES > OLD.LINES DO REPEAT
      R.LN = R.LINES
    CASE OPTION = "C"
      GOSUB 30000
      IF LNO THEN
        R.LN = LNO
        SLN = BEGIN.PAGE + LINE.SPACE * MOD(R.LN-1,PAGE.SIZE)
        GOSUB 7010
      END
    CASE OPTION = "D"
      GOSUB 30000
      IF LNO THEN
        R.LN = LNO
        TRAN.TYPE = "4"
        DESC = "REVIEW DATE ":OCONV(REVIEW.DATES<R.LN>, "D2/")
        IF NEW.FLG = "N" THEN GOSUB 90000
        REVIEW.DATES = DELETE(REVIEW.DATES,R.LN,0,0)
        R.LINES = COUNT(REVIEW.DATES,AM) + (REVIEW.DATES # "")
        IF R.LN > R.LINES THEN R.LN = R.LINES
        IF R.LN < 1 THEN R.LN = 1
        R.OLD.START = 0
      END
    CASE OPTION = "S"
      R.LN = 1 + INT((R.LN-1)/PAGE.SIZE) * PAGE.SIZE + PAGE.SIZE
      IF R.LN > R.LINES THEN R.LN = 1
    * T25978 v
    CASE OPTION = 'SR'
      R.LN = 1 + INT((R.LN-1)/PAGE.SIZE) * PAGE.SIZE - PAGE.SIZE
      IF R.LN < 1 THEN R.LN = 1
    CASE OPTION = 'ST'
      R.LN = 1
    CASE OPTION = 'SB'
      R.LN = R.LINES
    * T25978 ^
  END CASE
  GOSUB 20000
  GOTO 3000
3999*
  RETURN
*
*--- DEDUCTIONS
*
5000*
  GOSUB 10000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(M.LN-1,PAGE.SIZE)
  PRINT @(0,SLN) : M.CL :
  PRINT @(0,SLN) : M.LN "R#2" :
5010*
  TEMP = ""
  TEMP<1> = EMP.MSC.CODE<1,M.LN>
  TEMP<2> = EMP.MSC.AMT<1,M.LN>
  TEMP<3> = EMP.MSC.DED<1,M.LN>
  TEMP<4> = EMP.MSC.WK<1,M.LN>
  X = 3; Y = SLN; TYP = 1; MAXL = 2; O.R = "O"
  HMSG = "Enter Miscellaneous Deduction Code"
  DEFAULT = EMP.MSC.CODE<1,M.LN>
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END" AND OPTION = "A"
      EMP.MSC.CODE = DELETE(EMP.MSC.CODE,1,M.LN,0)
      EMP.MSC.AMT = DELETE(EMP.MSC.AMT,1,M.LN,0)
      EMP.MSC.DED = DELETE(EMP.MSC.DED,1,M.LN,0)
      EMP.MSC.WK = DELETE(EMP.MSC.WK,1,M.LN,0)
      EMP.INVEST = DELETE(EMP.INVEST,1,M.LN,0)
      EMP.INVEST.PCT = DELETE(EMP.INVEST.PCT,1,M.LN,0)
      LINE.DESC = DELETE(LINE.DESC,M.LN,0,0)
      LINE.TYPE = DELETE(LINE.TYPE,M.LN,0,0)
      LINE.TAX = DELETE(LINE.TAX,M.LN,0,0)
      M.OLD.START = 0
      GOTO 5999
    CASE VALUE = "END"
      M.OLD.START = 0
      GOTO 5999
    CASE VALUE = ""
      GOTO 5010
    CASE VALUE # ""
      FD = 0
      LOCATE VALUE IN EMP.MSC.CODE<1>,1 SETTING FD ELSE FD = 0
      IF FD AND FD # M.LN THEN
        ERRMSG = 'THIS DEDUCTION CODE ALREADY ENTERED'
        GOSUB 91000
        GOTO 5010
      END
      MATREAD MDED.REC FROM MISCDED, CONO:VALUE ELSE
        MAT MDED.REC = ""
        ERRMSG = "MISCDED ":VALUE:" IS NOT ON FILE"
        GOSUB 91000; GOTO 5010
      END
      EMP.MSC.CODE<1,M.LN> = VALUE
      LINE.DESC<M.LN> = MDED.SCRN.DESC
      LINE.TYPE<M.LN> = MDED.TYPE
      LINE.TAX<M.LN> = MDED.TAXABLE
      PRINT @(0,SLN) : M.CL :
      PRINT @(0,SLN) : M.LN "R#2" :
      PRINT @(3,SLN) : EMP.MSC.CODE<1,M.LN> "L#2" :
      PRINT @(6,SLN) : MDED.SCRN.DESC "L#14" :
      PRINT @(21,SLN) : MDED.TYPE "L#1" :
  END CASE
5020*
  X = 23; Y = SLN; TYP = 4; SCALER = 2; MAXL = 6
  HMSG = "Enter Amount to Deduct from Pay Check"
  IF EMP.MSC.AMT<1,M.LN> # "" THEN
    DEFAULT = OCONV(EMP.MSC.AMT<1,M.LN>, "MD2")
    O.R = "O"
  END
  CALL EDIT.SUB
  VALCK = 0
  IF EMP.MSC.CODE<1,M.LN>=DC.REC(1) OR EMP.MSC.CODE<1,M.LN>=DC.REC(2) THEN
    VALCK = VALUE
  END
  BEGIN CASE
    CASE VALUE = "END"
      GOTO 5010
    CASE VALUE = ""
      GOTO 5020
    CASE MDED.TAXABLE = "B" AND MDED.TYPE = "%"
      IF (TOT.BT + VALCK - EMP.MSC.AMT<1,M.LN>) > CO.BT.MAX.PCT<1,1>/10 THEN
        ERRMSG = 'EXCEEDS LIMIT ON BEFORE-TAX DEDUCTIONS'
        GOSUB 91000
        GOTO 5020
      END ELSE
        IF EMP.MSC.CODE<1,M.LN>=DC.REC(1) OR EMP.MSC.CODE<1,M.LN>=DC.REC(2) THEN
          TOT.BT = TOT.BT + VALUE - EMP.MSC.AMT<1,M.LN>
        END
        EMP.MSC.AMT<1,M.LN> = VALUE
      END
    CASE 1
      EMP.MSC.AMT<1,M.LN> = VALUE
  END CASE
5030*
  X = 30; Y = SLN; TYP = 1; MAXL = 8; JUSTIFY = "R"
  HMSG = "Enter Max Balance to Deduct OR 'N' to Deduct Until other Notice"
  BEGIN CASE
    CASE EMP.MSC.DED<1,M.LN> = "N"
      DEFAULT = "N"; O.R = "O"
    CASE NUM(EMP.MSC.DED<1,M.LN>)
      DEFAULT = OCONV(EMP.MSC.DED<1,M.LN>, "MD2"); O.R = "O"
  END CASE
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END"
      GOTO 5010
    CASE VALUE = ""
      GOTO 5030
    CASE NUM(VALUE)
      VALUE = ICONV(VALUE, "MD2")
      EMP.MSC.DED<1,M.LN> = VALUE
      PRINT @(30,SLN) : OCONV(EMP.MSC.DED<1,M.LN>, "MD2") "R#8" :
    CASE VALUE = "N"
      EMP.MSC.DED<1,M.LN> = VALUE
    CASE 1
      ERRMSG = "MUST BE DECIMAL OR 'N'"; GOSUB 91000; GOTO 5030
  END CASE
5040*
  X = 39; Y = SLN; TYP = 3; MAXL = 1; MINV = 0; MAXV = 5
  HMSG = "Enter 0 for weekly OR (1-5) week # for monthly deduction"
  IF EMP.MSC.WK<1,M.LN> # "" THEN
    DEFAULT = EMP.MSC.WK<1,M.LN>; O.R = "O"
  END
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END"
      GOTO 5010
    CASE VALUE = ""
      GOTO 5040
    CASE 1
      EMP.MSC.WK<1,M.LN> = VALUE
  END CASE
  BEGIN CASE
    CASE MDED.CONT.PLAN = "Y" AND OPTION = "A"
      M.LINES = COUNT(EMP.MSC.CODE,VM) + (EMP.MSC.CODE # "")
      ECD.SCRN.NO = ECD.SCRN.NO + 1
      CALL EP4.EMPLOYEE.MAINT4(EMP.MSC.CODE<1,M.LN>,M.LN)
      ECD.SCRN.NO = ECD.SCRN.NO - 1
      ECD.ACTION = 3; CALL SCRN.EDIT
      M.OLD.START = 0
      GOSUB 10000
      SLN = BEGIN.PAGE + LINE.SPACE * MOD(M.LN-1,PAGE.SIZE)
    CASE MDED.CONT.PLAN = "Y"
    CASE 1
      EMP.INVEST<1,M.LN> = ""
      EMP.INVEST.PCT<1,M.LN> = ""
  END CASE
5999*
  M.LINES = COUNT(EMP.MSC.CODE,VM) + (EMP.MSC.CODE # "")
  IF NEW.FLG = "N" THEN
    TRAN.TYPE = "3"
    DESC = "DED. CODE ":M.LN
    OLD = TEMP<1>
    NEW = EMP.MSC.CODE<1,M.LN>
    GOSUB 90000
    DESC = "DED. AMOUNT ":M.LN
    OLD = TEMP<2>
    NEW = EMP.MSC.AMT<1,M.LN>
    GOSUB 90000
    DESC = "DED. BALANCE ":M.LN
    OLD = TEMP<3>
    NEW = EMP.MSC.DED<1,M.LN>
    GOSUB 90000
    DESC = "DED. WEEK ":M.LN
    OLD = TEMP<4>
    NEW = EMP.MSC.WK<1,M.LN>
    GOSUB 90000
  END
  RETURN
*
*--- REV DATES
*
7000*
  GOSUB 20000
  SLN = BEGIN.PAGE + LINE.SPACE * MOD(R.LN-1,PAGE.SIZE)
  PRINT @(60,SLN) : R.CL :
  PRINT @(60,SLN) : R.LN "R#2" :
7010*
  TEMP = REVIEW.DATES<R.LN>
  X = 63; Y = SLN; TYP = 6; MAXL = 8; O.R = "O"
  HMSG = "Enter Review date"
  BEGIN CASE
    CASE OPTION = "A"
      IF R.LN > 1 THEN
        DEFAULT = REVIEW.DATES<R.LN-1>+365
      END ELSE
        DEFAULT = EMP.STRT.DATE+365
      END
    CASE OPTION = "C"
      DEFAULT = REVIEW.DATES<R.LN>
  END CASE
  IF DEFAULT > DATE() THEN DEFAULT = "" ELSE DEFAULT = OCONV(DEFAULT, "D2/")
  CALL EDIT.SUB
  BEGIN CASE
    CASE VALUE = "END" AND OPTION = "A"
      REVIEW.DATES = DELETE(REVIEW.DATES,R.LN,0,0)
    CASE VALUE = "END"
      PRINT @(63,SLN):OCONV(REVIEW.DATES<R.LN>, "D2/") "R#8" :
    CASE VALUE = ""
      GOTO 7010
    CASE VALUE > DATE()
      ERRMSG = "LAST REVIEW DATE SHOULD BE LESS OR EQUAL TO TODAY"
      GOSUB 91000
      GOTO 7010
    CASE VALUE # ""
      BLK = 0
      BEGIN CASE
        CASE R.LN > 1 AND REVIEW.DATES<R.LN+1> # ""
          IF VALUE < REVIEW.DATES<R.LN-1> OR VALUE > REVIEW.DATES<R.LN+1> THEN BLK = 1
        CASE R.LN > 1
          IF VALUE < REVIEW.DATES<R.LN-1> THEN BLK = 1
        CASE REVIEW.DATES<R.LN+1> # ""
          IF VALUE < EMP.STRT.DATE OR VALUE > REVIEW.DATES<R.LN+1> THEN BLK = 1
        CASE 1
          IF VALUE < EMP.STRT.DATE THEN BLK = 1
      END CASE
      IF BLK THEN ERRMSG = "INCORRECT DATE"; GOSUB 91000; GOTO 7010
      REVIEW.DATES<R.LN> = VALUE
  END CASE
7999*
  R.LINES = COUNT(REVIEW.DATES,AM) + (REVIEW.DATES # "")
  IF NEW.FLG = "N" THEN
    TRAN.TYPE = "3"
    DESC = "REVIEW DATE ":R.LN
    OLD = OCONV(TEMP, "D2/")
    NEW = OCONV(REVIEW.DATES<R.LN>, "D2/")
    GOSUB 90000
  END
  RETURN
*
*--- SCROLL DEDUCTIONS
*
10000*
  ST.LINE = 1 + INT((M.LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF ST.LINE = M.OLD.START THEN RETURN
  M.OLD.START = ST.LINE
  LST.LINE = ST.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = ST.LINE TO LST.LINE UNTIL N > M.LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(0,SLN) : N "R#2" :
    PRINT @(3,SLN) : EMP.MSC.CODE<1,N> "L#2" :
    IF LINE.DESC<N> = "" THEN
      MATREAD MDED.REC FROM MISCDED, CONO:EMP.MSC.CODE<1,N> ELSE
        MDED.SCRN.DESC = "??????"; MDED.TYPE = "$"
        MDED.TAXABLE = "A"
      END
      LINE.DESC<N> = MDED.SCRN.DESC
      LINE.TYPE<N> = MDED.TYPE
      LINE.TAX<N> = MDED.TAXABLE
    END
    PRINT @(6,SLN) : LINE.DESC<N> "L#14" :
    PRINT @(21,SLN) : LINE.TYPE<N> "L#1" :
    PRINT @(23,SLN) : OCONV(EMP.MSC.AMT<1,N>, "MD2") "R#6" :
    IF NUM(EMP.MSC.DED<1,N>) THEN
      PRINT @(30,SLN) : OCONV(EMP.MSC.DED<1,N>, "MD2") "R#8" :
    END ELSE
      PRINT @(30,SLN) : EMP.MSC.DED<1,N> "R#8" :
    END
    PRINT @(39,SLN) : EMP.MSC.WK<1,N> "R#1" :
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(0,SLN) : M.CL :
  NEXT N
  RETURN
*
*--- SCROLL REV DATES
*
20000*
  ST.LINE = 1 + INT((R.LN-1)/PAGE.SIZE) * PAGE.SIZE
  IF ST.LINE = R.OLD.START THEN RETURN
  R.OLD.START = ST.LINE
  LST.LINE = ST.LINE + PAGE.SIZE - 1
  CNT = 1
  FOR N = ST.LINE TO LST.LINE UNTIL N > R.LINES
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(60,SLN) : N "R#2" :
    PRINT @(63,SLN) : OCONV(REVIEW.DATES<N>, "D2/") "R#8" :
    CNT = CNT + 1
  NEXT N
  FOR N = CNT TO PAGE.SIZE
    SLN = BEGIN.PAGE + LINE.SPACE * MOD(N-1,PAGE.SIZE)
    PRINT @(60,SLN) : R.CL :
  NEXT N
  RETURN
*
*--- GET LINE
*
30000*
  IF REQUEST = "D" THEN
    GOSUB 10000
    IF LST.LINE > M.LINES THEN ECD.MAXV = M.LINES ELSE ECD.MAXV = LST.LINE
  END ELSE
    GOSUB 20000
    IF LST.LINE > R.LINES THEN ECD.MAXV = R.LINES ELSE ECD.MAXV = LST.LINE
  END
  ECD.MINV = ST.LINE
  ECD.NUM = 3; ECD.ACTION = 4; CALL SCRN.EDIT
  IF ECD.RET.VALUE = "END" THEN LNO = 0 ELSE LNO = ECD.RET.VALUE
  RETURN
*
*--- CREATE EMPTRANS RECORD
*
90000*
  IF TRAN.TYPE = "3" AND OLD = NEW THEN GOTO 90999
  READU SEQ FROM CONTROL, CONO:"EMPTRANS" ELSE SEQ = 0
  SEQ = SEQ + 1
  WRITE SEQ ON CONTROL, CONO:"EMPTRANS"
  REC = CONO :AM: EMP.NO :AM: TRAN.INT :AM: TRAN.TYPE
  IF TRAN.TYPE = "3" THEN REC = REC :AM: DESC :AM: OLD :AM: NEW
  IF TRAN.TYPE = "4" THEN REC = REC :AM: DESC
  WRITE REC ON EMPTRANS, CONO:SEQ
90999*
  RETURN
*
*--- ERROR ROUTINE
*
91000*
  PRINT @(0,23):ERRMSG:CL:
  INPUT REPLY,1 :
  PRINT @(0,23):CL:
  RETURN
999999*
  RETURN
END
