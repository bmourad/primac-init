* This program will rebuild the qty data for INV_SERIAL records from
* the tracking INV_AUDIT_HIST records tied to the SERIAL.
* Used for Cathedral 03/23/2005 - LMR.
CRT @(-1)
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>CATEGORY
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>CPYLIB>CHAR
*COPY>ICS.CPYLIB>INV.CNV
PROMPT''
OPEN 'INVENTORY' TO INVENTORY ELSE STOP
OPEN 'CATEGORY' TO CATEGORY ELSE STOP
OPEN 'INV_SERIAL' TO INV_SERIAL ELSE STOP
OPEN 'INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE STOP
OPEN 'INV.WHSE.LOC' TO INV.WHSE.LOC ELSE STOP
OPEN 'INV.WHSE' TO INV.WHSE ELSE STOP
OPEN 'INV_SERIAL_WORK' TO INV_SERIAL_WORK ELSE STOP
CONO='001'
1 CRT @(0,5):CL:'Enter Product : ':;INPUT PROD:
  IF PROD = 'END' THEN STOP
  MATREAD INV.REC FROM INVENTORY,CONO:PROD ELSE GOTO 1
  MATREAD CATG.REC FROM CATEGORY,CONO:INV.LINE ELSE GOTO 1
*COPY>ICSBP>INV.UM.CNV
2 CRT @(0,7):CL:'Enter Whse : ':;INPUT WHSE:
  IF WHSE = 'END' THEN STOP
  MATREAD IWH.REC FROM INV.WHSE,CONO:PROD:"!":WHSE ELSE GOTO 2
  FOR L = 1 TO DCOUNT(IWH.LOC<1>,VM)
    MATREADU IWLO.REC FROM INV.WHSE.LOC,CONO:PROD:"!":WHSE:"!":IWH.LOC<1,L> ELSE
      CRT @(0,22):CL:'Missing INV.WHSE.LOC for location ':IWH.LOC<1,L>:;INPUT X:; GOTO 1
    END
    SER.LOC.ONHAND=0
    FOR S = 1 TO DCOUNT(IWLO.SERIAL<1>,VM)
      SERIAL=IWLO.SERIAL<1,S>
      AUD.SER.ONHAND=0
      MATREAD ISTK.REC FROM INV_SERIAL,CONO:SERIAL ELSE
        CRT @(0,22):CL:'Missing Serial record ':SERIAL:' for Loc ':IWH.LOC<1,L>:;INPUT X:; GOTO 1
      END
      SER.LOC.ONHAND += ISTK.CUR.QTY
      FOR A = 1 TO DCOUNT(ISTK.AUDIT.NO<1>,VM)
        MATREAD INAH.REC FROM INV_AUDIT_HIST,CONO:ISTK.AUDIT.NO<1,A> THEN
          AUD.SER.ONHAND += INAH.QTY
        END
      NEXT A
      IF AUD.SER.ONHAND # ISTK.CUR.QTY THEN
        ISTK.CUR.QTY = AUD.SER.ONHAND
        MATWRITE ISTK.REC ON INV_SERIAL_WORK,CONO:SERIAL
      END
    NEXT S
    IF SER.LOC.ONHAND # IWLO.LOC.ON.HAND THEN
      CRT @(0,22):CL:'IWLO on hand = ':IWLO.LOC.ON.HAND:' and Serial sum is ':SER.LOC.ONHAND:; INPUT X:
    END
  NEXT L
STOP
