* Verify ORIG AND CUR QTY IN INV_RECP_WHSE FILE.
CRT @(-1):'Verify INV_RECP_WHSE balances.'
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>CPYLIB>CHAR
OPEN 'INV_RECP_WHSE' TO INV_RECP_WHSE ELSE STOP
OPEN 'INV_RECEIPTS' TO INV_RECEIPTS ELSE STOP
OPEN 'INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE STOP
WHSE=''
BBAL=0
CBAL=0
DATA=1
LOOP
  READNEXT IRW.ID ELSE DATA = 0
WHILE DATA DO
  CONO=IRW.ID[1,3]
  MATREAD IRW.REC FROM INV_RECP_WHSE, IRW.ID THEN
    WHSE=FIELD(IRW.ID,"!",2)
    INVR.ID=FIELD(IRW.ID,"!",1)
    MATREAD INVR.REC FROM INV_RECEIPTS,INVR.ID THEN
      ACNT = DCOUNT(INVR.AUDIT.NO,VM)
      FOR A = 1 TO ACNT
        MATREAD INAH.REC FROM INV_AUDIT_HIST,CONO:INVR.AUDIT.NO<1,A> THEN
          IF INAH.WHSE = WHSE THEN
            IF INAH.TYPE # 'S' AND INAH.SRC # 'IC' THEN
              BBAL+=INAH.QTY
            END
            CBAL+=INAH.QTY
          END
        END
      NEXT A
      IF BBAL+0 # IRW.ORG.QTY+0 OR CBAL+0 # IRW.CUR.QTY+0 THEN
        CRT 'IRW.ID = ':IRW.ID'L#20':' BBAL = ':BBAL'R#12':' IRW = ':IRW.ORG.QTY'R#12':' CBAL = ':CBAL'R#12':' IRW = ':IRW.CUR.QTY'R#12':; INPUT XX
        IF XX='END' THEN STOP
      END
      BBAL=0; CBAL=0
    END
  END
REPEAT
CRT 'DONE'
