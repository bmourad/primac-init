* PROGRAM TO POPULATE INAH.DEPL.RECP, QTY AND UNIT COST FROM JOB.MATL.
*LMR 03/21/03
*COPY>JCS.CPYLIB>JOB.MATL
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
CRT @(-1):'Processing JOB.MAT vs INV_AUDIT_HIST'
OPEN 'JOB.MATL' TO JOB.MATL ELSE STOP
OPEN 'INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE STOP
CONO='002'
CMD='SELECT JOB.MATL WITH CONO = "':CONO:'"'
UDTEXECUTE CMD CAPTURING JUNK
DATA=1
CNT=0
LOOP
  READNEXT ID ELSE DATA = 0
WHILE DATA DO
  MATREAD JMT.REC FROM JOB.MATL, ID THEN
    INAH.ID = CONO:JMT.AUDIT.NO
    MATREADU INAH.REC FROM INV_AUDIT_HIST,INAH.ID THEN
      IF JMT.PTR<1,1> # '' THEN
        IF INAH.DEPL.RECP = '' THEN
          INAH.DEPL.RECP = JMT.PTR<1,1>
          INAH.DEPL.QTY = JMT.PTR<1,2>
        END ELSE
          INAH.DEPL.RECP<1,-1> = JMT.PTR<1,1>
          INAH.DEPL.QTY<1,-1> = JMT.PTR<1,2>
        END
        IF INAH.UNIT.COST+0 = 0 THEN INAH.UNIT.COST = JMT.PTR<1,4>
        IF INAH.EXT.COST+0 = 0 THEN INAH.EXT.COST = JMT.DCOST
      END
      IF SUM(INAH.DEPL.QTY) # INAH.QTY THEN
         CRT 'INAH.ID :':INAH.ID:'  QTY = ':INAH.QTY'R#12':' DEPL QTY = ':SUM(INAH.DEPL.QTY)'R#12'
      END
      MATWRITE INAH.REC ON INV_AUDIT_HIST, INAH.ID
   END ELSE RELEASE
   CNT+=1
   IF REM(CNT,100)=0 THEN CRT 'COUNT = ':CNT'R10'
  END
REPEAT
CRT 'DONE'
STOP
