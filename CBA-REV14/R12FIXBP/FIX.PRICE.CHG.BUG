*REBUILD RECEIPT DATA IN INV.WHSE FROM A SAVED LIST AND UPDATE IWH.ON.HAND
* WITH SUM OF ON-HANDS FROM INV.WHSE.LOC RECORDS.
* THIS WAS ORIGINALLY CAUSED BY A BUG IN LTIBP PRICE.CHANGE.POST.
* 01/06/2004
CRT @(-1)
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_RECEIPTS
OPEN 'INV.WHSE' TO INV.WHSE ELSE STOP
OPEN 'INV_RECEIPTS' TO INV_RECEIPTS ELSE STOP
OPEN 'INV.WHSE.LOC' TO INV.WHSE.LOC ELSE STOP
OPEN 'SAVEDLISTS' TO SAVEDLISTS ELSE STOP
PROMPT''
CRT @(0,5):'Enter List Name : ':;INPUT LNAME
IF LNAME='END' THEN STOP
READ LIST FROM SAVEDLISTS,LNAME:'000' ELSE CRT 'NO LIST!!'; STOP
ICNT=DCOUNT(LIST,@AM)
FOR I = 1 TO ICNT
  MATREADU IWH.REC FROM INV.WHSE, LIST<I> THEN
    CONO=LIST<I>[1,3]
    PROD = FIELD(LIST<I>,"!",1)[4,15]
    WHSE = FIELD(LIST<I>,"!",2)
    CMD = 'SSELECT INV_RECEIPTS WITH CONO = "':CONO:'" AND WITH INVR_PROD = "':PROD:'" AND WITH INVR_ORG_WHSE = "':WHSE:'" BY INVR_PERIOD BY INVR_RECV_DATE'
    UDTEXECUTE CMD CAPTURING JUNK
    DONE=0
    LOOP
      READNEXT RID ELSE DONE = 1
    UNTIL DONE DO
      MATREAD INVR.REC FROM INV_RECEIPTS,RID THEN
        LOCATE RID[4,9] IN IWH.RECP.NO<1>,1 SETTING RFND ELSE
          IWH.RECP.NO<1,RFND> = RID[4,9]
          IWH.RECP.PERIOD<1,RFND> = INVR.PERIOD
          IWH.RECP.ENT.DATE<1,RFND> = INVR.ENT.DATE
        END
      END
    REPEAT
    IWH.ON.HAND = 0
    LCNT=DCOUNT(IWH.LOC,@VM)
    FOR L = 1 TO LCNT
      READ LQTY FROM INV.WHSE.LOC,LIST<I>:"!":IWH.LOC<1,L> THEN
        IWH.ON.HAND += LQTY<1>
      END
    NEXT L
    MATWRITE IWH.REC ON INV.WHSE,LIST<I>
  END ELSE RELEASE INV.WHSE,LIST<I>
NEXT I
CRT 'DONE'
STOP
