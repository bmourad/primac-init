* Check the reserves of INV.WHSE vs IRW vs IJS vs JOB
* LMR 01/07/2005 for use at ADPLEX
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*COPY>ICS.CPYLIB>INV.JOB.STATS
*COPY>JCS.CPYLIB>JOB
*COPY>CPYLIB>CHAR
OPEN 'INV.WHSE' TO INV.WHSE ELSE STOP
OPEN 'INV_RECP_WHSE' TO INV_RECP_WHSE ELSE STOP
OPEN'INV.JOB.STATS' TO INV.JOB.STATS ELSE STOP
OPEN 'JOB' TO JOB ELSE STOP
OPEN 'LR.ERRORS' TO ERRFILE ELSE STOP
*
CRT CS
DONE=0
IWH.LIST=''
UDTEXECUTE 'GET-LIST RL1' CAPTURING ERROR RETURNING MSG
IF SYSTEM(11) THEN
  SELECT INV.WHSE TO IWH.LIST
END
LOOP
  READNEXT ID FROM IWH.LIST ELSE DONE = 1
UNTIL DONE DO
  MATREAD IWH.REC FROM INV.WHSE,ID THEN
DEBUG
    IRW.LIST=''
    EREC=''
    PROD=FIELD(ID,"!",1)[4,15]
    WHSE=FIELD(ID,"!",2)
    CMD='SSELECT INV_RECP_WHSE WITH PROD_WHSE_IDX = "':ID:'"'
    IRW.TOT.RESV=0
    UDTEXECUTE CMD CAPTURING ERROR RETURNING MSG
    IF SYSTEM(11) > 0 THEN
      SELECT INV_RECP_WHSE TO IRW.LIST
      CTR=0
      DATA=1
      LOOP
        READNEXT IRWID FROM IRW.LIST ELSE DATA = 0
      WHILE DATA DO
        MATREAD IRW.REC FROM INV_RECP_WHSE, IRWID THEN
          IF IRW.JOB # '' THEN
            FOR I = 1 TO DCOUNT(IRW.JOB,VM)
              IF IRW.JRSVD.QTY+0 # 0 THEN
                CTR+=1
                EREC<CTR>='RECP ':IRWID[4,99]:' HAS JOB ':IRW.JOB<1,I>:' WITH QTY ':IRW.JRSVD.QTY
                MATREAD JOB.REC FROM JOB,ID[1,3]:IRW.JOB<1,I> THEN
                  PTR=1
                  LOOP
                    LOCATE PROD IN JOB.RESV.MATL<1>,PTR SETTING FND ELSE FND=0
                    IF FND THEN
                      IF WHSE # JOB.RESV.WHSE<1,FND> THEN
                        PTR = FND+1
                      END ELSE PTR = 0
                    END ELSE PTR = 0
                  WHILE PTR DO REPEAT
                  IF JOB.RESV.QTY<1,FND> # IRW.JRSVD.QTY<1,I> THEN
                    CTR+=1
                    EREC<CTR>='JOB ':IRW.JOB<1,I>:' HAS RESVD ':JOB.RESV.QTY<1,FND>:' AND IRW HAS ':IRW.JRSVD.QTY<1,I>
                  END
                END
              END
            NEXT I
          END
          IF IRW.CUR.QTY # IRW.RSVB.QTY THEN
            IRW.TOT.RESV += IRW.CUR.QTY - IRW.RSVB.QTY
            TMP = IRW.CUR.QTY - IRW.RSVB.QTY
            CTR+=1
            EREC<CTR>='RECP ':IRWID[4,99]:' HAS RESVD QTY OF ':TMP
          END
        END
      REPEAT
      IF IRW.TOT.RESV # 0 THEN
        CTR+=1
        EREC<CTR>='TOTAL IRW RESERVED = ':IRW.TOT.RESV
      END
    END
    IJS.TOT.RESV=0
    IJS.LIST=''
    CMD='SELECT INV.JOB.STATS WITH PROD = "':PROD:'" AND WITH WHSE = "':WHSE:'"'
    UDTEXECUTE CMD CAPTURING ERROR RETURNING MSG
    IF SYSTEM(11) > 0 THEN
      SELECT INV.JOB.STATS TO IJS.LIST
      DATA = 1
      IJS.TOT.RESV=0
      LOOP
        READNEXT IJSID FROM IJS.LIST ELSE DATA = 0
      WHILE DATA DO
        MATREAD INV.JS.REC FROM INV.JOB.STATS,IJSID THEN
          IF IJS.JOB.QTY+0 # 0 THEN
            IJS.TOT.RESV+=IJS.JOB.QTY
            JOBNO = FIELD(IJSID,"!",3)
            MATREAD JOB.REC FROM JOB,ID[1,3]:JOBNO THEN
              PTR=1
              LOOP
                LOCATE PROD IN JOB.RESV.MATL<1>,PTR SETTING FND ELSE FND=0
                IF FND THEN
                  IF WHSE # JOB.RESV.WHSE<1,FND> THEN
                    PTR = FND+1
                  END ELSE PTR = 0
                END ELSE PTR = 0
              WHILE PTR DO REPEAT
              IF JOB.RESV.QTY<1,FND> # IJS.JOB.QTY THEN
                CTR+=1
                EREC<CTR>='JOB ':JOBNO:' HAS RESVD ':JOB.RESV.QTY<1,FND>:' AND IJS HAS ':IJS.JOB.QTY
              END
            END
          END
        END
      REPEAT
      IF IJS.TOT.RESV # IWH.RESV+0 THEN
        CTR+=1
        EREC<CTR>='INV.WHSE HAS RESV OF ':IWH.RESV:' AND IJS HAS ':IJS.TOT.RESV
      END
    END
  END
  IF EREC # '' THEN
    WRITE EREC ON ERRFILE,ID
  END
REPEAT
CRT 'DONE'
