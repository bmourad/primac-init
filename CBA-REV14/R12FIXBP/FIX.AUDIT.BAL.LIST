* Program to rebuild INV_AUDIT_BAL Begin Qty & Amt for given Period.
CRT @(-1)
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
OPEN 'INV.WHSE' TO INV.WHSE ELSE STOP
OPEN 'INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE STOP
OPEN 'INV_AUDIT_BAL' TO INV_AUDIT_BAL ELSE STOP
PROMPT''
CRT 'Current Period is ':;INPUT CUR.PER:
IF CUR.PER='END' THEN STOP
DONE=0
IWH.LIST=''
LOOP
  READNEXT IWHID ELSE DONE=1
UNTIL DONE DO
  MATREAD IWH.REC FROM INV.WHSE, IWHID THEN
    IWH.LIST<-1> = IWHID
  END
REPEAT
DONE=0
FOR I = 1 TO DCOUNT(IWH.LIST,@AM)
  IWHID=IWH.LIST<I>
  MATREAD IWH.REC FROM INV.WHSE, IWHID THEN
    LMBEGQ=0; LMBEGA=0; LMRQ=0; LMRA=0; LMSQ=0; LMSA=0; LMAQ=0; LMAA=0
    LMIQ=0; LMIA=0; LMOQ=0; LMOA=0
    TMBEGQ=0; TMBEGA=0; TMRQ=0; TMRA=0; TMSQ=0; TMSA=0; TMAQ=0; TMAA=0
    TMIQ=0; TMIA=0; TMOQ=0; TMOA=0
    CRT @(-1)
    CRT @(0,3):IWHID
    LRCNT=DCOUNT(IWH.RECP.ENT.DATE,@VM)
    CRT @(0,4):'Last recp ':OCONV(IWH.RECP.ENT.DATE<1,LRCNT>,'D4/')
    CRT @(0,5):'ON-HAND   ':IWH.ON.HAND
    CMD='SSELECT INV_AUDIT_HIST WITH PROD_WHSE_IDX = "':IWHID:'" AND WITH INAH_PERIOD = "':CUR.PER:'"'
    UDTEXECUTE CMD CAPTURING JUNK RETURNING MSG
    INAH.LIST=''
    DONE2=0
    IF MSG<1,1,1> = '41' THEN SELECT INV_AUDIT_HIST TO INAH.LIST ELSE DONE2=1
    LOOP
      READNEXT INAHID FROM INAH.LIST ELSE DONE2 = 1
    UNTIL DONE2 DO
      MATREAD INAH.REC FROM INV_AUDIT_HIST,INAHID THEN
        BEGIN CASE
        CASE INAH.TYPE = 'R'
          TMRQ+=INAH.QTY
          TMRA+=INAH.EXT.COST
        CASE INAH.TYPE = 'S'
          TMSQ+=INAH.QTY
          TMSA+=INAH.EXT.COST
        CASE INAH.TYPE = 'A'
          TMAQ+=INAH.QTY
          TMAA+=INAH.EXT.COST
        CASE INAH.TYPE = 'I'
          TMIQ+=INAH.QTY
          TMIA+=INAH.EXT.COST
        CASE INAH.TYPE = 'O'
          TMOQ+=INAH.QTY
          TMOA+=INAH.EXT.COST
        END CASE
      END
    REPEAT
    MATREADU ITB.REC FROM INV_AUDIT_BAL,IWHID:"!":CUR.PER ELSE MAT ITB.REC = ''
    CRT @(0,7):'ITB Beg Q = ':ITB.BEG'R#12'
    CRT @(0,8):'ITB Beg A = ':ITB.BEG.AMT/100'R#12'
    CRT @(0,9):'ITB Rec Q = ':ITB.RECV'R#12'
    CRT @(0,10):'ITB Rec A = ':ITB.RECV.AMT/100'R#12'
    CRT @(0,11):'ITB Sel Q = ':ITB.SALE'R#12'
    CRT @(0,12):'ITB Sel A = ':ITB.SALE.AMT/100'R#12'
    CRT @(0,13):'ITB Srk Q = ':ITB.SHRNK'R#12'
    CRT @(0,14):'ITB Srk A = ':ITB.SHRNK.AMT/100'R#12'
    CRT @(0,15):'ITB TrI Q = ':ITB.TRAN.IN'R#12'
    CRT @(0,16):'ITB TrI A = ':ITB.TRAN.IN.AMT/100'R#12'
    CRT @(0,17):'ITB TrO Q = ':ITB.TRAN.OUT'R#12'
    CRT @(0,18):'ITB TrO A = ':ITB.TRAN.OUT.AMT/100'R#12'
    CRT @(30,9):'IAH Rec Q = ':TMRQ'R#12'
    CRT @(30,10):'IAH Rec A = ':TMRA/100'R#12'
    CRT @(30,11):'IAH Sel Q = ':TMSQ'R#12'
    CRT @(30,12):'IAH Sel A = ':TMSA/100'R#12'
    CRT @(30,13):'IAH Srk Q = ':TMAQ'R#12'
    CRT @(30,14):'IAH Srk A = ':TMAA/100'R#12'
    CRT @(30,15):'IAH TrI Q = ':TMIQ'R#12'
    CRT @(30,16):'IAH TrI A = ':TMIA/100'R#12'
    CRT @(30,17):'IAH TrO Q = ':TMOQ'R#12'
    CRT @(30,18):'IAH TrO A = ':TMOA/100'R#12'
    BEG.QTY=IWH.ON.HAND-TMRQ-TMSQ-TMAQ-TMIQ-TMOQ
    BEGIN CASE
    CASE ITB.BEG.AMT+0 # 0
      UC = (ITB.BEG.AMT/100) / (ITB.BEG/1000)
    CASE TMRA+0 # 0
      UC = (TMRA/100) / (TMRQ/1000)
    CASE 1
      UC = 0
    END CASE
    CRT @(0,20):'Calc Beg Qty = ':BEG.QTY:
    BEG.AMT = INT((BEG.QTY/1000) * (UC)*100+.5)
    CRT ' Calc Beg Cost = ':OCONV(BEG.AMT,'MD2')'R#12':
    CRT @(0,22):'Update ? Y/N ':;INPUT ANS:
    IF ANS # 'Y' THEN RELEASE; CONTINUE
    ITB.BEG = BEG.QTY
    ITB.BEG.AMT = BEG.AMT
    MATWRITE ITB.REC ON INV_AUDIT_BAL,IWHID:"!":CUR.PER
  END
NEXT I
