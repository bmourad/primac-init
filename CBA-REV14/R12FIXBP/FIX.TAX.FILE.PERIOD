CRT @(-1):'Fixing period in TAX.BAL file.'
*Fix the period data in tax file due to error in POST.MANUAL.INVOICE.
*COPY>PMC.CPYLIB>TAX.BAL
*COPY>ARS.CPYLIB>MANUAL.INVOICE
OPEN '','MANUAL.INVOICE' TO MANUAL.INVOICE ELSE ERRMSG="MANUAL.INVOICE FILE IS MISSING"; STOP ERRMSG
OPEN '','TAX.BAL' TO TAX.BAL ELSE ERRMSG="TAX.BAL FILE IS MISSING"; STOP ERRMSG
DONE=0
LOOP
  READNEXT MID ELSE DONE = 1
UNTIL DONE DO
  MATREAD MIV.REC FROM MANUAL.INVOICE,MID THEN
    CONO=MID[1,3]
    OID = OCONV(MIV.DATE,'D4/')
    YEAR = OID[7,4]
    MON = OID[1,2]'R%2'
    PERIOD = YEAR:MON
    CNT = COUNT(MIV.CHG.CODE,@VM) + (MIV.CHG.CODE # "")
    FOR I = 1 TO CNT 
      BEGIN CASE
      CASE MIV.CHG.CAT<1,I> = "TAX"
        MATREADU TAX.BAL.REC FROM TAX.BAL, CONO:MIV.DIV:MIV.DEPT:MIV.CCTR:MIV.TAX.JURS<1,I> THEN
          LOCATE MIV.PROC.MON IN TBR.PERIOD<1>,1 SETTING FND.PER THEN
            TBR.CHARGED<1,FND.PER> = TBR.CHARGED<1,FND.PER> - MIV.AMOUNT<1,I>
            TBR.TAXABLE<1,FND.PER> = TBR.TAXABLE<1,FND.PER> - MIV.TAXABLE<1,I>
          END
        END ELSE
          MAT TAX.BAL.REC = ""
        END
        LOCATE PERIOD IN TBR.PERIOD<1> BY "DR" SETTING FND.PER ELSE
          INS PERIOD BEFORE TBR.PERIOD<1,FND.PER>
          INS 0 BEFORE TBR.CHARGED<1,FND.PER>
          INS 0 BEFORE TBR.TAXABLE<1,FND.PER>
        END
        TBR.CHARGED<1,FND.PER> = TBR.CHARGED<1,FND.PER> + MIV.AMOUNT<1,I>
        TBR.TAXABLE<1,FND.PER> = TBR.TAXABLE<1,FND.PER> + MIV.TAXABLE<1,I>
        MATWRITE TAX.BAL.REC ON TAX.BAL, CONO:MIV.DIV:MIV.DEPT:MIV.CCTR:MIV.TAX.JURS<1,I>
      END CASE
    NEXT I
  END
REPEAT
CRT 'DONE'
