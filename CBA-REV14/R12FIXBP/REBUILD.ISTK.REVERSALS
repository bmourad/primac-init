* This program is written specifically for SALT where product SON254 had
* 344 serials posted to job 43804 with a null receipt ID. This caused the
* JOB.MATL records to have all indirect cost. When they reversed the matl
* off the job they didn't go back into inventory and the serials stayed 0.
* Run from a select list of INV_AUDIT_HIST records from the matl posting.
CRT @(-1)
*COPY>ICS.CPYLIB>INV_SERIAL
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*COPY>ICS.CPYLIB>INV_RECEIPTS
*COPY>ICS.CPYLIB>INV_RECP_WHSE
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE.LOC
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>CHAR
DIM HOLD.IRW.REC(20)
DIM HOLD.INVR.REC(80)
OPEN 'INV_SERIAL' TO INV_SERIAL ELSE STOP
OPEN 'INV_SERIAL_DELETED' TO INV_SERIAL_DELETED ELSE STOP
OPEN 'INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE STOP
OPEN 'INV_RECEIPTS' TO INV_RECEIPTS ELSE STOP
OPEN 'INV_RECP_WHSE' TO INV_RECP_WHSE ELSE STOP
OPEN 'INVENTORY' TO INVENTORY ELSE STOP
OPEN 'INV.WHSE.LOC' TO INV.WHSE.LOC ELSE STOP
OPEN 'CONTROL' TO CONTROL ELSE STOP
DEFFUN GET.INAH.SEQ(CONO,CONTROL.FILE,INV_AUDIT_HIST.FILE)
DEFFUN CALC.STK.QTY(COST.QTY,MAT INV.CNV.REC,ROND,LN)
MORE=1
CONO='001'
LOOP
  READNEXT ID ELSE MORE = 0
WHILE MORE DO
  MATREAD INAH.REC FROM INV_AUDIT_HIST,ID THEN
    DEL.SEL=0
    MATREADU ISTK.REC FROM INV_SERIAL,CONO:INAH.SERIAL ELSE
      MATREADU ISTK.REC FROM INV_SERIAL_DELETED,CONO:INAH.SERIAL ELSE
        CRT @(0,22):CL:'Cannot locate INV_SERIAL ':INAH.SERIAL:;INPUT X:
        CONTINUE
      END
      DEL.SEL=1
    END
    MATREADU INVR.REC FROM INV_RECEIPTS,CONO:INAH.RECP.NO ELSE
      CRT @(0,22):CL:'Cannot locate INV_RECEIPTS ':INAH.RECP.NO:;INPUT X:
      CONTINUE
    END
    MATREADU IRW.REC FROM INV_RECP_WHSE,CONO:INAH.RECP.NO:"!":INAH.WHSE ELSE
      CRT @(0,22):CL:'Cannot locate INV_RECP_WHSE ':INAH.RECP.NO:"!":INAH.WHSE:;INPUT X:
      CONTINUE
    END
    MATREAD INV.REC FROM INVENTORY,CONO:INAH.PROD ELSE
      CRT @(0,22):CL:'Cannot locate INVENTORY ':INAH.PROD:;INPUT X:
      CONTINUE
    END
*COPY>ICSBP>INV.UM.CNV
    ADJ.QTY = INAH.QTY * (-1)
    ISTK.CUR.QTY += ADJ.QTY
    IF ISTK.CUR.QTY > ISTK.ORG.QTY THEN
      CRT @(0,22):CL:'ISTK.CUR.QTY exceeds original QTY for ':INAH.SERIAL:;INPUT X:
      CONTINUE
    END
    ISTK.RSVB.QTY += ADJ.QTY
    ISTK.CUR.STK.QTY += CALC.STK.QTY(ADJ.QTY,MAT INV.CNV.REC,ROND,LN)
    IF ISTK.ORG.DIAM > 0 THEN ISTK.CUR.DIAM = ISTK.ORG.DIAM
    INAH.QTY = 0 -INAH.QTY
    INAH.EXT.COST = 0 - INAH.EXT.COST
    IF INAH.DEPL.RECP # '' THEN 
      MAT HOLD.IRW.REC = MAT IRW.REC
      MAT HOLD.INVR.REC = MAT INVR.REC
      FOR I = 1 TO DCOUNT(INAH.DEPL.RECP<1>,VM)
        INAH.DEPL.QTY<1,I> = 0 - INAH.DEPL.QTY<1,I>
        MATREADU IRW.REC FROM INV_RECP_WHSE,CONO:INAH.DEPL.RECP<1,I>:"!":INAH.WHSE THEN
          IRW.CUR.QTY += INAH.DEPL.QTY<1,I>
          IRW.RSVB.QTY += INAH.DEPL.QTY<1,I>
          MATWRITE IRW.REC ON INV_RECP_WHSE,CONO:INAH.DEPL.RECP<1,I>:"!":INAH.WHSE
        END ELSE
          CRT @(0,22):CL:'Cannot update IRW record ':INAH.DEPL.RECP<1,I>:"!":INAH.WHSE:
          INPUT X:; CONTINUE
        END
        MATREADU INVR.REC FROM INV_RECEIPTS,CONO:INAH.DEPL.RECP<1,I> THEN
          INVR.DEPL.QTY += INAH.DEPL.QTY<1,I>
          MATWRITE INVR.REC ON INV_RECEIPTS,CONO:INAH.DEPL.RECP<1,I>
        END ELSE
          CRT @(0,22):CL:'Cannot update INVR record ':INAH.DEPL.RECP<1,I>:
          INPUT X:; CONTINUE
        END
      NEXT I
      MAT INVR.REC = MAT HOLD.INVR.REC
      MAT IRW.REC = MAT HOLD.IRW.REC
    END
    IRW.ON.HAND += ADJ.QTY
    IF IRW.ON.HAND > IRW.ORG.QTY THEN
      CRT @(0,22):CL:'IRW ON HAND ':IRW.ON.HAND:' is greater than orig ':IRW.ORG.QTY:' RCPT ':INAH.RECP.NO:;INPUT X:
      CONTINUE
    END
    LOCATE INAH.SERIAL IN IRW.SERIAL.NO<1>,1 BY 'AR' SETTING SFND ELSE
      INS INAH.SERIAL BEFORE IRW.SERIAL.NO<1,SFND>
    END
    MATREADU IWLO.REC FROM INV.WHSE.LOC,CONO:INAH.PROD:"!":INAH.WHSE:"!":INAH.LOC THEN
      LOCATE INAH.SERIAL IN IWLO.SERIAL<1>,1 BY 'AR' SETTING SFND ELSE
        INS INAH.SERIAL BEFORE IWLO.SERIAL<1,SFND>
      END
    END ELSE
      CRT @(0,22):CL:'Cannot locate INV.WHSE.LOC record ':;INPUT X:
      CONTINUE
    END
    AUDIT.SEQ = GET.INAH.SEQ(CONO,CONTROL,INV_AUDIT_HIST)
    INAH.SRC = 'UR'
    INAH.DATE = DATE()
    MATWRITE INAH.REC ON INV_AUDIT_HIST,CONO:AUDIT.SEQ
    ISTK.AUDIT.NO<1,-1> = AUDIT.SEQ
    INVR.AUDIT.NO<1,-1> = AUDIT.SEQ
    MATWRITE INVR.REC ON INV_RECEIPTS,CONO:INAH.RECP.NO
    MATWRITE IRW.REC ON INV_RECP_WHSE,CONO:INAH.RECP.NO:"!":INAH.WHSE
    MATWRITE IWLO.REC ON INV.WHSE.LOC,CONO:INAH.PROD:"!":INAH.WHSE:"!":INAH.LOC
    MATWRITE ISTK.REC ON INV_SERIAL,CONO:INAH.SERIAL
    IF DEL.SEL THEN DELETE INV_SERIAL_DELETED,CONO:INAH.SERIAL
  END
REPEAT
CRT @(0,10):'Finished!'
