OPEN '','INV.WHSE' TO INV.WHSE ELSE STOP 'INV.WHSE'
OPEN '','INV_AUDIT_BAL' TO INV_AUDIT_BAL ELSE STOP 'INV_AUDIT_BAL'
OPEN '','INV_AUDIT_BAL_TEMP' TO INV_AUDIT_BAL_TEMP ELSE STOP 'INV_AUDIT_BAL_TEMP'
OPEN '','INV_AUDIT_HIST' TO INV_AUDIT_HIST ELSE STOP 'INV_AUDIT_HIST'
*
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV_AUDIT_BAL
*COPY>ICS.CPYLIB>INV_AUDIT_HIST
*
PRINT @(-1)
DIM TMP(13) ; MAT TMP=''
EQU TMP.PERIOD       TO TMP(1)
EQU TMP.BEG          TO TMP(2)
EQU TMP.BEG.AMT      TO TMP(3)
EQU TMP.RECV         TO TMP(4)
EQU TMP.RECV.AMT     TO TMP(5)
EQU TMP.SALE         TO TMP(6)
EQU TMP.SALE.AMT     TO TMP(7)
EQU TMP.SHRNK        TO TMP(8)
EQU TMP.SHRNK.AMT    TO TMP(9)
EQU TMP.TRAN.IN      TO TMP(10)
EQU TMP.TRAN.IN.AMT  TO TMP(11)
EQU TMP.TRAN.OUT     TO TMP(12)
EQU TMP.TRAN.OUT.AMT TO TMP(13)
*
PRINT @(5,3):'ENTER COMPANY ':
INPUT CONO
PRINT @(5,5):'ENTER CURRENT ICS PERIOD ':
INPUT PERIOD
LOOP
  PRINT @(5,7):'ENTER WAREHOUSE ':
  INPUT WHSE
UNTIL WHSE='END' DO 
  LOOP
    PRINT @(5,9):'ENTER PRODUCT NUMBER ':@(-4):
    INPUT PROD
  UNTIL PROD='END' DO
    GOSUB FIX.DATA
  REPEAT
REPEAT
STOP
*
*************
FIX.DATA: 
*************
*
MAT TMP='' ; TEBAL='' ; TEAMT=''
EBAL='' ; EAMT=''
IWH.ID=CONO:PROD:"!":WHSE
MATREAD IWH.REC FROM INV.WHSE,IWH.ID THEN
*
CMD='SSELECT INV_AUDIT_HIST WITH PROD_WHSE_IDX = "':CONO:PROD:'!':WHSE:'" BY INAH_PERIOD'
  UDTEXECUTE CMD CAPTURING MSG
*
  DONE=0 ; OLD.PERIOD='@@@@@' ; CNT=0
  LOOP
    READNEXT INAH.ID ELSE DONE=1
  UNTIL DONE DO
    CONO=INAH.ID[1,3]
    MATREAD INAH.REC FROM INV_AUDIT_HIST, INAH.ID THEN
      IF OLD.PERIOD#INAH.PERIOD THEN
        CNT+=1
        TMP.PERIOD<1,CNT>=INAH.PERIOD
        OLD.PERIOD=INAH.PERIOD
      END
      BEGIN CASE
        CASE INAH.TYPE = "R"
          TMP.RECV<1,CNT>+=INAH.QTY
          TMP.RECV.AMT<1,CNT>+=INAH.EXT.COST
        CASE INAH.TYPE = "S"
          TMP.SALE<1,CNT>+=INAH.QTY
          TMP.SALE.AMT<1,CNT>+=INAH.EXT.COST
        CASE INAH.TYPE = "A"
          TMP.SHRNK<1,CNT>+=INAH.QTY
          TMP.SHRNK.AMT<1,CNT>+=INAH.EXT.COST
        CASE INAH.TYPE = "I"
          TMP.TRAN.IN<1,CNT>+=INAH.QTY
          TMP.TRAN.IN.AMT<1,CNT>+=INAH.EXT.COST
        CASE INAH.TYPE = "O"
          TMP.TRAN.OUT<1,CNT>+=INAH.QTY
          TMP.TRAN.OUT.AMT<1,CNT>+=INAH.EXT.COST
      END CASE
    END
  REPEAT
  LOCATE PERIOD IN TMP.PERIOD<1> BY 'AR' SETTING PLOC ELSE
    TMP.PERIOD=INSERT(TMP.PERIOD,1,PLOC,0,PERIOD)
    TMP.BEG=INSERT(TMP.BEG,1,PLOC,0,'')
    TMP.BEG.AMT=INSERT(TMP.BEG.AMT,1,PLOC,0,'')
    TMP.RECV=INSERT(TMP.RECV,1,PLOC,0,'')
    TMP.RECV.AMT=INSERT(TMP.RECV.AMT,1,PLOC,0,'')
    TMP.SALE=INSERT(TMP.SALE,1,PLOC,0,'')
    TMP.SALE.AMT=INSERT(TMP.SALE.AMT,1,PLOC,0,'')
    TMP.SHRNK=INSERT(TMP.SHRNK,1,PLOC,0,'')
    TMP.SHRNK.AMT=INSERT(TMP.SHRNK.AMT,1,PLOC,0,'')
    TMP.TRAN.IN=INSERT(TMP.TRAN.IN,1,PLOC,0,'')
    TMP.TRAN.IN.AMT=INSERT(TMP.TRAN.IN.AMT,1,PLOC,0,'')
    TMP.TRAN.OUT=INSERT(TMP.TRAN.OUT,1,PLOC,0,'')
    TMP.TRAN.OUT.AMT=INSERT(TMP.TRAN.OUT.AMT,1,PLOC,0,'')
  END
*
  NAME=PROD:"!":WHSE
  CMD='SETPTR ,,,,,3,BANNER ':NAME:',BRIEF'
  UDTEXECUTE CMD
*
  PRINTER ON
  PER.CNT=DCOUNT(TMP.PERIOD<1>,@VM)
  PRINT 'PROD = ':PROD:'    WHSE= ':WHSE
  PRINT
  FIRST.TIME=1
  TEBAL='' ; TEAMT=''
  FOR PP=1 TO PER.CNT
    ITB.ID=CONO:PROD:'!':WHSE:'!':TMP.PERIOD<1,PP>
    MATREAD ITB.REC FROM INV_AUDIT_BAL,ITB.ID ELSE MAT ITB.REC=''
*IF ITB.BEG+0>0 AND TMP.PERIOD<1,PP>[1,4]=PERIOD[1,4] AND FIRST.TIME THEN
IF ITB.BEG+0>0 AND FIRST.TIME THEN
      FIRST.TIME=0
      TEBAL=ITB.BEG ; TEAMT=ITB.BEG.AMT
    END
    PRINT 'PERIOD = ':TMP.PERIOD<1,PP>
    PRINT
    PRINT
    IF ITB.BEG=TEBAL THEN
      PRINT '1. BEG=''L#10':ITB.BEG"R#10":SPACE(3):'TMP.BEG=''L#10':ITB.BEG'R#10'
      PRINT '2. AMT=''L#10':ITB.BEG.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':ITB.BEG.AMT'R#10'
    END ELSE
      PRINT '1. BEG=''L#10':ITB.BEG"R#10":SPACE(3):'TMP.BEG=''L#10':TEBAL'R#10'
      PRINT '2. AMT=''L#10':ITB.BEG.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':TEAMT'R#10'
    END
    TMP.BEG<1,PP>=TEBAL
    TMP.BEG.AMT<1,PP>=TEAMT
    PRINT '3. RECV=''L#10':ITB.RECV"R#10":SPACE(3):'TMP.RECV=''L#10':TMP.RECV<1,PP>'R#10'
    PRINT '4. AMT=''L#10':ITB.RECV.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':TMP.RECV.AMT<1,PP>'R#10'
    PRINT '5. SALE=''L#10':ITB.SALE"R#10":SPACE(3):'TMP.SALE=''L#10':TMP.SALE<1,PP>'R#10'
    PRINT '6. AMT=''L#10':ITB.SALE.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':TMP.SALE.AMT<1,PP>'R#10'
    PRINT '7. SHRNK=''L#10':ITB.SHRNK"R#10":SPACE(3):'TMP.SHRNK=''L#10':TMP.SHRNK<1,PP>'R#10'
    PRINT '8. AMT=''L#10':ITB.SHRNK.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':TMP.SHRNK.AMT<1,PP>'R#10'
    PRINT '9. TRAN.IN=''L#10':ITB.TRAN.IN"R#10":SPACE(3):'TMP.TRAN.IN=''L#10':TMP.TRAN.IN<1,PP>'R#10'
    PRINT '10. AMT=''L#10':ITB.TRAN.IN.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':TMP.TRAN.IN.AMT<1,PP>'R#10'
    PRINT '11. TRAN.OUT=''L#10':ITB.TRAN.OUT"R#10":SPACE(3):'TMP.TRAN.OUT=''L#10':TMP.TRAN.OUT<1,PP>'R#10'
    PRINT '12. AMT=''L#10':ITB.TRAN.OUT.AMT"R#10":SPACE(3):'TMP.AMT=''L#10':TMP.TRAN.OUT.AMT<1,PP>'R#10'
    PRINT '--------------------------------------------'
    EBAL=ITB.BEG+ITB.RECV+ITB.SALE+ITB.SHRNK+ITB.TRAN.IN+ITB.TRAN.OUT
    EAMT=ITB.BEG.AMT+ITB.RECV.AMT+ITB.SALE.AMT+ITB.SHRNK.AMT+ITB.TRAN.IN.AMT+ITB.TRAN.OUT.AMT
    TEBAL=TEBAL+TMP.RECV<1,PP>+TMP.SALE<1,PP>+TMP.SHRNK<1,PP>+TMP.TRAN.IN<1,PP>+TMP.TRAN.OUT<1,PP>
    TEAMT=TEAMT+TMP.RECV.AMT<1,PP>+TMP.SALE.AMT<1,PP>+TMP.SHRNK.AMT<1,PP>+TMP.TRAN.IN.AMT<1,PP>+TMP.TRAN.OUT.AMT<1,PP>
    PRINT 'END BAL''L#10':EBAL'R#10':SPACE(3):'TEBBAL''L#10':TEBAL'R#10'
    PRINT 'END AMT''L#10':EAMT'R#10':SPACE(3):'TEAMT''L#10':TEAMT'R#10'
  NEXT PP
*
  PRINTER OFF
  PRINTER CLOSE
*
  IF TEBAL=IWH.ON.HAND THEN
    FOR PP=1 TO PER.CNT
      IF TMP.PERIOD<1,PP> > PERIOD THEN
        ITB.ID=IWH.ID:"!":TMP.PERIOD<1,PP>
        DELETE INV_AUDIT_BAL, ITB.ID
      END ELSE
        ITB.ID=IWH.ID:"!":TMP.PERIOD<1,PP>
        MAT ITB.REC=''
        IF TMP.PERIOD<1,PP>=PERIOD THEN
          ITB.BEG=TMP.BEG<1,PP>
          ITB.BEG.AMT=TMP.BEG.AMT<1,PP>
        END ELSE
          ITB.BEG=TMP.BEG<1,PP>
          ITB.BEG.AMT=TMP.BEG.AMT<1,PP>
          ITB.RECV=TMP.RECV<1,PP>
          ITB.RECV.AMT=TMP.RECV.AMT<1,PP>
          ITB.SALE=TMP.SALE<1,PP>
          ITB.SALE.AMT=TMP.SALE.AMT<1,PP>
          ITB.SHRNK=TMP.SHRNK<1,PP>
          ITB.SHRNK.AMT=TMP.SHRNK.AMT<1,PP>
          ITB.TRAN.IN=TMP.TRAN.IN<1,PP>
          ITB.TRAN.IN.AMT=TMP.TRAN.IN.AMT<1,PP>
          ITB.TRAN.OUT=TMP.TRAN.OUT<1,PP>
          ITB.TRAN.OUT.AMT=TMP.TRAN.OUT.AMT<1,PP>
        END
*       MATWRITE ITB.REC ON INV_AUDIT_BAL_TEMP,ITB.ID
        MATWRITE ITB.REC ON INV_AUDIT_BAL,ITB.ID
        LOCATE TMP.PERIOD<1,PP> IN IWH.BAL.PERIOD<1> BY "AR" SETTING PLOC ELSE NULL
        IWH.BAL.PERIOD<1,PLOC>=TMP.PERIOD<1,PP>
        IWH.BEG.QTY<1,PLOC>=TMP.BEG<1,PP>
        IWH.BEG.AMT<1,PLOC>=TMP.BEG.AMT<1,PP>
      END
    NEXT PP
*   MATWRITE IWH.REC ON INV.WHSE,IWH.ID
    PRINT @(5,11):'FIX SUCCESSFUL':@(-4)
  END ELSE
    PRINT @(5,11):'FIX UNSUCCESSFUL':@(-4)
  END
END
RETURN
