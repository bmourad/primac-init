*COPY>CPYLIB>COM1
*********************************************************************
*   SYSTEM       - CBA
*   PROGRAM      - ESTIMATOR.MAINT
*   BY           - DIANE TOVAR
*   DATE         - 05/7/96
*   DESCRIPTION  - ESTIMATOR MAINTENANCE
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
*ENDDOC
*********************************************************************
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
   SYS.TYPE = 1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   MAT FILE.VARS = ''
***  OPEN FILES
   OPEN '','ESTIMATOR' TO ESTIMATOR ELSE ERRMSG = 'ESTIMATOR FILE IS MISSING'; GOTO 93000
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG = 'COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG = 'CONTROL FILE MISSING';GOTO 93000
   OPEN '','JES.SCREENS' TO M.SCREENS ELSE ERRMSG = 'JES.SCREENS FILE IS MISSING' ; GOTO 93000
*---- GET COMPANY NUMBER
*
   CONO = ''
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO = 'END' THEN GOTO 99999
*
   EQU CODE.DESC TO CODE.REC<1>
   MAT EDIT.COM.DRIVER = ''
   ECD.SCRN.CNT = 1
   ECD.SCRN.NAME<1> = 'ESTIMATOR.MAINT'
   ECD.ACTION=1;CALL SCRN.EDIT
   ECD.SCRN.NO = 1
   LOOP
      RELEASE         ;* T26126 
      MORE = 1
      CODE.REC = ''
      MAT SCV.REC = ""
      ECD.ACTION=6;CALL SCRN.EDIT
      ECD.NUM = 1
      ECD.VALDAT.CODE = '4'
      ECD.VALDAT.FILE = ESTIMATOR
      ECD.PREFIX.ID = CONO
      ECD.ACTION=4;CALL SCRN.EDIT
      BEGIN CASE
         CASE ECD.RET.VALUE = 'END'
            MORE = 0
         CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM # ''
        * T26126 v
            READU DUMMY FROM ESTIMATOR, CONO:ECD.RET.VALUE LOCKED
               ERRMSG = 'ESTIMATOR record is locked by user - ':GETUSERNAME(STATUS())
               GOSUB 91000;MORE = 2;CONTINUE 
            END ELSE NULL
        * T26126 ^
            ESTIMATOR.ID = ECD.RET.VALUE
            CODE.REC = ECD.VALDAT.ITEM
            SCV.REC(2) = CODE.DESC
            ECD.ACTION=3;CALL SCRN.EDIT
         CASE ECD.RET.VALUE # '' AND ECD.VALDAT.ITEM = ''
            ESTIMATOR.ID = ECD.RET.VALUE
            GOSUB 1100
            IF ECD.RET.VALUE = 'END' THEN MORE = 2
      END CASE
      LOOP WHILE MORE = 1 DO
         ECD.NUM = 3
         SCV.REC(ECD.NUM) = ''
         ECD.ACTION=4;CALL SCRN.EDIT
         OPTION = ECD.RET.VALUE
         BEGIN CASE
            CASE OPTION = 'E' OR OPTION = 'END'
               MORE = 2
            CASE OPTION = 1
               GOSUB 1100
            CASE OPTION = 'F'
               WRITE CODE.REC ON ESTIMATOR, CONO:ESTIMATOR.ID
               MORE = 2
         END CASE
      REPEAT
   WHILE MORE DO REPEAT
   GOTO 99999
1100 ECD.NUM = 2
   ECD.ACTION=4;CALL SCRN.EDIT
   IF ECD.RET.VALUE # 'END' THEN CODE.DESC = ECD.RET.VALUE
   RETURN
91000 ERR.TYPE = 1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 ERR.TYPE = 2
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 ERR.TYPE = 3
   CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
END
