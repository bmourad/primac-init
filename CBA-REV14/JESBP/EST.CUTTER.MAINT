*COPY>CPYLIB>SCOMMON1
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.CUTTER.MAINT
*
* AUTHOR   - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 11/27/85
*
* DESCRIPTION
* This program updates and maintains the EQUIPMENT file (Cutter items).
*
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","EQUIPMENT" TO EQUIPMENT ELSE
    ERRMSG = "CANNOT OPEN EQUIPMENT FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","COST.CNTR" TO COST.CNTR ELSE
    ERRMSG = "CANNOT OPEN COST.CNTR FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.CUTTER.MAINT
  SCREEN FORMAT
*
*---- MAIN PROCESSING
*
100*
  SCREEN FIELD;EST.CUTTER.MAINT;1
  SCREEN INPUT;EST.CUTTER.MAINT;1
  IF S$VALUE = "END" THEN GOTO 99999
  EQP.KEY = S$VALUE
  MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
    ERRMSG = "Item must be a valid cost center"
    GOSUB 90000
    GOTO 100
  END
  S$DATA(2)<S$SCR> = CCTR.DESC
  SCREEN DISPLAY;EST.CUTTER.MAINT;2
  NEW.REC = 0
  * T26126 v
  MATREADU EQUIPMENT.REC FROM EQUIPMENT, CONO:EQP.KEY LOCKED
    ERRMSG = 'CUTTER record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 100 
  END ELSE
  * T26126 ^
    MAT EQUIPMENT.REC = ""
    NEW.REC = 1
  END
  IF EQP.TYPE # "" AND EQP.TYPE # "04" THEN
    RELEASE
    ERRMSG = "Item has already been defined as other than a cutter binder"
    GOSUB 90000
    GOTO 100
  END
  IF NEW.REC THEN
    EQP.TYPE = "04"
    FOR FLD = 1 TO 10
      ON FLD GOSUB 10100,19990,19990,19990,19990,19990,19990,19990,19990,19990
      IF S$VALUE = "END" THEN
        SCREEN CLEAR;EST.CUTTER.MAINT
        RELEASE
        GOTO 100
      END
    NEXT FLD
  END ELSE
    GOSUB 80000
    SCREEN DISPLAY;EST.CUTTER.MAINT;ALL
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.CUTTER.MAINT;10
  SCREEN INPUT;EST.CUTTER.MAINT;10
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION # "" AND NUM(OPTION)
      FLD = OPTION
      ON FLD GOSUB 10100,19990,19990,19990,19990,19990,19990,19990,19990,19990
    CASE OPTION = "E" OR OPTION = "END"
      SCREEN CLEAR;EST.CUTTER.MAINT
      RELEASE
      GOTO 100
    CASE OPTION = "F"
      MATWRITE EQUIPMENT.REC ON EQUIPMENT, CONO:EQP.KEY
      SCREEN CLEAR;EST.CUTTER.MAINT
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET INCHES PER LIFT
*
10100*
  SCREEN FIELD;EST.CUTTER.MAINT;3
  SCREEN INPUT;EST.CUTTER.MAINT;3;RETURN
  EQP.CUTTER.IN.PER.LIFT = S$VALUE
  RETURN
*
*---- DUMMY
*
19990*
  RETURN
*
*---- LOAD SCREEN VALUES
*
80000*
  MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
    MAT CCTR.REC = ""
    CCTR.DESC = "??????????"
  END
  S$DATA(2)<S$SCR> = CCTR.DESC
  S$DATA(3)<S$SCR> = EQP.CUTTER.IN.PER.LIFT
  S$DATA(4)<S$SCR> = EQP.CUTTER.ADD.PCT.1
  S$DATA(5)<S$SCR> = EQP.CUTTER.ADD.PCT.2
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  STOP
END
