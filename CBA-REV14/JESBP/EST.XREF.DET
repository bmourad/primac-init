      SUBROUTINE EST.XREF.DET(CONO, KEY)
*********************************************************************
*
* REVISION - [08.1]
*
* PROGRAM  - EST.XREF.DET
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/05/91
*
* DESCRIPTION
*
* This program is used to view estimate demographics.
*
*T21913 diane 06/09/1997 * Close screens for gui
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>SALESMAN
*COPY>JES.CPYLIB>ESTIMATOR
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      SCREEN DEFINE;EST.XREF.DET
      SCREEN FORMAT
      SCREEN COUNT;;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      LINE.CNT1 = 0
      REF.NO1 = ""
      REF.NO2 = ""
      CURR.REF.NO1 = ""
      CURR.REF.NO2 = ""
*
*---- MAIN PROCESSING
*
100*
      MATREAD EST.REC FROM ESTIMATE, CONO:KEY ELSE
         MAT EST.REC = ""
      END
      GOSUB 80000
      GOSUB 81000
      LINE.CNT1 = DCOUNT(EST.COMMENTS,VM)
      LINE.CNT2 = DCOUNT(EST.SUBS,VM)
      REF.NO1 = 1
      CURR.REF.NO1 = ""
      GOSUB 50000
      GOSUB 55000
*
*---- GET OPERATOR REPLY
*
500*
      SCREEN FIELD;;21
      SCREEN INPUT;;21
      OPT = S$VALUE
510*
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END" OR OPT = ""
         GOTO 99999
      CASE OPT = "S" OR OPT = "SF"
         REF.NO1 = CURR.REF.NO1 + LINE.COUNT
         IF REF.NO1 > LINE.CNT1 THEN REF.NO1 = 1
         GOSUB 50000
      CASE OPT = "SR"
         REF.NO1 = CURR.REF.NO1 - LINE.COUNT
         IF REF.NO1 < 1 THEN REF.NO1 = 1
         GOSUB 50000
      CASE OPT = "ST"
         REF.NO1 = 1
         GOSUB 50000
      CASE OPT = "SB"
         REF.NO1 = LINE.CNT1
         IF REF.NO1 < 1 THEN REF.NO1 = 1
         GOSUB 50000
      CASE OPT = "SS" OR OPT = "SSF"
         REF.NO2 = CURR.REF.NO2 + LINE.COUNT
         IF REF.NO2 > LINE.CNT2 THEN REF.NO2 = 1
         GOSUB 55000
      CASE OPT = "SSR"
         REF.NO2 = CURR.REF.NO2 - LINE.COUNT
         IF REF.NO2 < 1 THEN REF.NO2 = 1
         GOSUB 55000
      CASE OPT = "SST"
         REF.NO2 = 1
         GOSUB 55000
      CASE OPT = "SSB"
         REF.NO2 = LINE.CNT2
         IF REF.NO2 < 1 THEN REF.NO2 = 1
         GOSUB 55000
      END CASE
      GOTO 500
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000*
      START.REF.NO1 = 1 + INT((REF.NO1-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO1 = CURR.REF.NO1 THEN RETURN
      CURR.REF.NO1 = START.REF.NO1
      S$VAL = START.REF.NO1
      S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
      SCREEN MULTI;;S;11;12
      RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
55000*
      START.REF.NO2 = 1 + INT((REF.NO2-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO2 = CURR.REF.NO2 THEN RETURN
      CURR.REF.NO2 = START.REF.NO2
      S$VAL = START.REF.NO2
      S$CNT = DCOUNT(S$DATA(13)<S$SCR>,VM)
      SCREEN MULTI;;C;13
      RETURN
*
*---- LOAD SCREEN DATA
*
80000*
      S$DATA(1)<S$SCR> = DATE()
      S$DATA(2)<S$SCR> = KEY
      IF EST.MASTER # KEY THEN
         S$DATA(3)<S$SCR> = EST.MASTER
      END
      IF EST.CUST # "P" THEN
         MATREAD CUST.REC FROM CUSTOMER,CONO:EST.CUST ELSE
            MAT CUST.REC = ""
         END
         S$DATA(4)<S$SCR> = CUST.NAME
      END ELSE
         S$DATA(4)<S$SCR> = EST.CUST.NAME
      END
      IF EST.SALESMAN # "" THEN
         MATREAD SALESMAN.REC FROM SALESMAN,CONO:EST.SALESMAN ELSE
            MAT SALESMAN.REC = ""
         END
         S$DATA(5)<S$SCR> = SALS.NAME
      END
      IF EST.ESTIMATOR # "" THEN
         MATREAD ESTIMATOR.REC FROM ESTIMATOR,CONO:EST.ESTIMATOR ELSE
            MAT ESTIMATOR.REC = ""
         END
         S$DATA(6)<S$SCR> = ESTMTR.NAME
      END
      IF EST.CATG # "" THEN
         MATREAD JCATG.REC FROM JOB.CATEGORY,CONO:EST.CATG ELSE
            MAT JCATG.REC = ""
         END
         S$DATA(7)<S$SCR> = JCATG.DESC
      END
      S$DATA(8)<S$SCR> = EST.DATE.ENTERED
      S$DATA(9)<S$SCR> = EST.DIV
      S$DATA(10)<S$SCR> = EST.STATUS<1,1>
80050*
      SCREEN DISPLAY;;ALL
      RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000*
      S$DATA(11)<S$SCR> = EST.COMMENT.TYPE
      S$DATA(12)<S$SCR> = EST.COMMENTS
      S$DATA(13)<S$SCR> = EST.SUBS
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):@(-4):ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):@(-4):
*       RETURN
*
*---- END OF PROGRAM
*
99999*
*T29193   SCREEN CLEAR;;D
*T29193   SCREEN CLOSE
      RETURN
   END
