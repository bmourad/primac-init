*******************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc )
* PROGRAM  - EST.PRESS.LIST
* AUTHOR   - KERRY WILSON
* DATE     - 04/16/90
* DESC     - LISTING FOR PRESS EQUIPMENT
*T21871 larryt 05/02/1997 * CHANGE DISPLAY OF COLOR UNITS FROM 1 TO 2
*                           COLUMNS
*T25901 cmykleb 03/07/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*******************************************
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.DC.CODE
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- OPEN FILES
*
   OPEN "COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "EQUIPMENT" TO EQUIPMENT ELSE
      ERRMSG = "CANNOT OPEN EQUIPMENT FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "COST.CNTR" TO COST.CNTR ELSE
      ERRMSG = "CANNOT OPEN COST.CNTR FILE"
      GOSUB 90000
      GOTO 99999
   END
   OPEN "ESTIMATE.DC.CODE" TO ESTIMATE.DC.CODE ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.DC.CODE FILE"
      GOSUB 90000
      GOTO 99999
   END
*
*---- PROCREAD
*
   PROCREAD KEY ELSE
      ERRMSG = 'MUST BE RUN FROM A PROC'
      GOSUB 90000
      GOTO 99999
   END
   CONO = KEY<1>
   CONO.NAME = KEY<2>
*
*---- INITIALIZATION
*
*T25901 v
*  HLINE1 = "RUN D - T: ":OCONV(DATE(), "D2/"):" - ":OCONV(TIME(), "MTS")
*  HLINE1 = HLINE1 'L#33'
*  REPORT.NUMBER = 'XXXX'
*  LNAME = LEN(CONO.NAME)
*  HD1B = HLINE1:SPACE(INT((67-LNAME)/2)):CONO.NAME
*  HD1B = HD1B "L#110"
*  HD1 = HD1B:'   PAGE '
*  HD2 = 'REPORT # : ':REPORT.NUMBER
*  HD2 = HD2:SPACE(39):'P R E S S   L I S T I N G'
*T26493 v
*  REPORT.NAME = "PRESS LISTING"
*  REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = KEY<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26493 ^
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HD1,HD2)
*T25901 ^
   HD3 = SPACE(69):'P N   H'
   HD3 = HD3:SPACE(5):'B      S G '
   HD3 = HD3:SPACE(8):'COLOR '
   HD3 = HD3:'TAIL  '
   HD3 = HD3:'CLAMP S R'
   HD4 = 'EQUIP':SPACE(10)
   HD4 = HD4:'DESCRIPTION':SPACE(11)
   HD4 = HD4:'MIN LEN '
   HD4 = HD4:'MAX LEN '
   HD4 = HD4:'MIN WID '
   HD4 = HD4:'MAX WID '
   HD4 = HD4:'T W F S S F B CU P U E '
   HD4 = HD4:'G WID '
   HD4 = HD4:'P ALLOW ALLOW ALLOW '
   HD4 = HD4:'A A SP TBL'
   HD5 = '----- ':STR('-',30):' '
   HD5 = HD5:STR('-',7):' '
   HD5 = HD5:STR('-',7):' '
   HD5 = HD5:STR('-',7):' '
   HD5 = HD5:STR('-',7):' '
   HD5 = HD5:'- - - - - - - -- - - - '
   HD5 = HD5:'----- - '
   HD5 = HD5:'----- ----- ----- '
   HD5 = HD5:'- - ------'
   HD6 = SPACE(10):'DC CODE DC RATE  CONFIGURATION DESCRIPTION'
   HD7 = SPACE(10):'------- ------- ----------------------------'
   PAGE.CNT = 0
   LINE.CNT = 99
*
*---- MAIN PROCESSING
*
100*
   PRINTER ON
   DATA = 1
   LOOP
      READNEXT ID ELSE DATA = 0
   WHILE DATA DO
      IF LINE.CNT >= 55 THEN GOSUB 1000
      MATREAD EQUIPMENT.REC FROM EQUIPMENT,ID ELSE GOTO 199
      MATREAD CCTR.REC FROM COST.CNTR,ID ELSE
         MAT CCTR.REC = ''
         CCTR.DESC = 'UNKNOWN'
      END
      DLINE = ID[4,5] "L#5 "
      DLINE = DLINE:CCTR.DESC "L#30 "
      DLINE = DLINE:OCONV(EQP.PRESS.MIN.LENGTH,"MD4")"R#7 "
      DLINE = DLINE:OCONV(EQP.PRESS.CUTOFF.LEN,"MD4")"R#7 "
      DLINE = DLINE:OCONV(EQP.PRESS.MIN.WIDTH,"MD4")"R#7 "
      DLINE = DLINE:OCONV(EQP.PRESS.MAX.WIDTH,"MD4")"R#7 "
      DLINE = DLINE:EQP.PRESS.TYPE "L#1 "
      DLINE = DLINE:EQP.PRESS.WEB.CNT "L#1 "
      DLINE = DLINE:EQP.PRESS.FOLDER "L#1 "
      DLINE = DLINE:EQP.PRESS.HEAT.SET "L#1 "
      DLINE = DLINE:EQP.PRESS.SHEETER "L#1 "
      DLINE = DLINE:EQP.PRESS.FINISHER "L#1 "
      DLINE = DLINE:EQP.PRESS.BLK.TO.BLK "L#1 "
      DLINE = DLINE:EQP.PRESS.CLR.CNT "R#2 "
      DLINE = DLINE:EQP.PRESS.PERFECTOR "L#1 "
      DLINE = DLINE:EQP.PRESS.SWITCHABLE "L#1 "
      DLINE = DLINE:EQP.PRESS.GRIPPER.CNT "L#1 "
      DLINE = DLINE:OCONV(EQP.PRESS.GRIPPER.WIDTH,"MD3")"R#5 "
      DLINE = DLINE:EQP.PRESS.GRIPPER.POS "L#1 "
      DLINE = DLINE:OCONV(EQP.PRESS.CLR.BAR.ALLOW,"MD3")"R#5 "
      DLINE = DLINE:OCONV(EQP.PRESS.TAIL.ALLOW,"MD3")"R#5 "
      DLINE = DLINE:OCONV(EQP.PRESS.CLAMP.ALLOW,"MD3")"R#5 "
      DLINE = DLINE:EQP.PRESS.SOLID.ABILITY "L#1 "
      DLINE = DLINE:EQP.PRESS.REGISTER.ABILITY "L#1 "
      DLINE = DLINE:EQP.PRESS.RUN.SPOIL "L#6"
      PRINT DLINE
      PRINT
      LINE.CNT = LINE.CNT + 2
      IF EQP.PRESS.DC.CODE # '' THEN
         IF LINE.CNT >= 50 THEN GOSUB 1000
         GOSUB 2000
         DC.CNT = DCOUNT(EQP.PRESS.DC.CODE,VM)
         FOR XX = 1 TO DC.CNT
            MATREAD ESTDC.REC FROM ESTIMATE.DC.CODE,CONO:EQP.PRESS.DC.CODE<1,XX> ELSE
               MAT ESTDC.REC = ''
               ESTDC.DESC = 'UNKNOWN'
            END
            DC.LINE = SPACE(10):EQP.PRESS.DC.CODE<1,XX> "L#7 "
            DC.LINE = DC.LINE:EQP.PRESS.DC.MAX<1,XX> "R#7 "
            DC.LINE = DC.LINE:ESTDC.DESC "L#30"
            PRINT DC.LINE
            LINE.CNT = LINE.CNT + 1
            IF LINE.CNT >= 55 THEN
               GOSUB 1000
               GOSUB 2000
            END
         NEXT XX
         PRINT
         LINE.CNT = LINE.CNT + 1
      END
199 *
   REPEAT
   GOTO 99999
*
*---- PRINT HEADINGS
*
1000*
   PRINT CHAR(12)
   PAGE.CNT = PAGE.CNT + 1
   PRINT HD1:PAGE.CNT
   PRINT HD2
   PRINT
   PRINT HD3
   PRINT HD4
   PRINT HD5
   PRINT
   LINE.CNT = 7
   RETURN
*
*---- PRINT CODE HEADINGS
*
2000 *
   PRINT HD6
   PRINT HD7
   PRINT
   LINE.CNT = LINE.CNT + 3
   RETURN
*
*---- ERROR ROUTINE
*
90000 *
   PRINT @(0,23):CL:ERRMSG:
   INPUT REPLY:
   PRINT @(0,23):CL:
   RETURN
*
*---- END OF PROGRAM
*
99999 *
   PRINTER OFF
   PRINT @(-1):
END
