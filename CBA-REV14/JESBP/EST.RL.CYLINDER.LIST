*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RES.BP
* PROGRAM     - EST.RL.CYLINDER.LIST
* BY          - KERRY WILSON
* DATE        - 01/19/90
* DESCRIPTION - This program lists the Roll Label Cylinder.
* TASK 16749 DMT 11/02/92 ADD LOCATION, TYPE, PERF INCHES AND MULTI COST CNTR
*********************************************************************
*COPY>JES.CPYLIB>ESTIMATE.RL.CYLINDER
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET SYSCOM
*
      SYS.TYPE=1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN 'ESTIMATE.RL.CYLINDER' TO ESTIMATE.RL.CYLINDER ELSE
         ERRMSG = 'CANNOT OPEN ESTIMATE.RL.CYLINDER FILE'
         GOTO 93000
      END
      OPEN 'COST.CNTR' TO COST.CNTR ELSE
         ERRMSG = 'CANNOT OPEN COST.CNTR FILE'
         GOTO 93000
      END
      OPEN 'COMPANY' TO COMPANY ELSE
         ERRMSG = 'CANNOT OPEN COMPANY FILE'
         GOTO 93000
      END
      OPEN 'CONTROL' TO CONTROL ELSE
         ERRMSG = 'CANNOT OPEN CONTROL FILE'
         GOTO 93000
      END
*
*---- PROC READ
*
      PROCREAD ID ELSE
      ERRMSG = "MUST RUN FROM MENU"
      GOSUB 91000
      GOTO 99999
      END
      CONO = ID<1>
      CONO.NAME = ID<2>
      CCTR.SELECT = ID<3>
*
*---- SET UP HEADINGS
*
      REPORT.NAME = "ROLL CYLINDER REPORT"
      REPORT.NUMBER = "XXXX"
      REPORT.DATE = DATE()
*     HD1 = OCONV(REPORT.DATE,"D2/")"R#8"
      HD1 = ''
      HD1B = SPACE(INT((100-LEN(CONO.NAME))/2)):CONO.NAME
      HD1B = HD1B "L#100"
      HD1 = HD1:HD1B:'   PAGE '
      HD2 = 'REPORT # : ':REPORT.NUMBER
      HD2 = HD2:SPACE(42):'R O L L   C Y L I N D E R   R E P O R T'
      HD3 = 'COST CENTER '
      HD4 = ' #T  REPEAT CYLINDER  QTY LOC TYPE PERF IN'
      HD5 = '--- ------- --------- --- --- ---- -------'
*
*---- INITIALIZATION
*
      LINE.CNT = 999
      PAGE.CNT = 0
      PRINTER ON
*
*---- MAIN PROCESSING
*
100 *
      READNEXT KEY ELSE GOTO 99999
      CYLINDER.KEY = KEY<1,1>
      CYLINDER = CYLINDER.KEY[4,12]
      CCTR.PTR = KEY<1,2>
      MATREAD RLCYN.REC FROM ESTIMATE.RL.CYLINDER,CYLINDER.KEY ELSE GOTO 100
      IF CCTR.SELECT NE 'ALL' THEN
         IF RLCYN.CCTR<1,CCTR.PTR> NE CCTR.SELECT THEN
            GOTO 100
         END
      END
      PREV.CCTR = RLCYN.CCTR<1,CCTR.PTR>
      BEGIN CASE
         CASE LINE.CNT + 3 > 54
            GOSUB 10000
            GOSUB 1000
         CASE 1
            GOSUB 1000
      END CASE
      DATA = 1
      LOOP
         READNEXT KEY ELSE DATA = 0
      WHILE DATA DO
         CYLINDER.KEY = KEY<1,1>
         CYLINDER = CYLINDER.KEY[4,12]
         CCTR.PTR = KEY<1,2>
         MATREAD RLCYN.REC FROM ESTIMATE.RL.CYLINDER,CYLINDER.KEY ELSE GOTO 110
         IF CCTR.SELECT NE 'ALL' THEN
            IF RLCYN.CCTR<1,CCTR.PTR> NE CCTR.SELECT THEN
               GOTO 110
            END
         END
         BEGIN CASE
            CASE PREV.CCTR # RLCYN.CCTR<1,CCTR.PTR>
               PREV.CCTR = RLCYN.CCTR<1,CCTR.PTR>
               GOSUB 10000
               GOSUB 1000
            CASE LINE.CNT + 3 > 54
               GOSUB 10000
               GOSUB 1000
            CASE 1
               GOSUB 1000
         END CASE
110 *
      REPEAT
      GOTO 99999
*
*---- PRINT DETAIL
*
1000 *
      PLINE = RLCYN.TEETH "R#3 "
      PLINE = PLINE:OCONV(RLCYN.REPEAT,"MD4")"R#7 "
      PLINE = PLINE:CYLINDER "L#9 "
      PLINE = PLINE:RLCYN.QUANTITY "R#3 "
      PLINE = PLINE:RLCYN.LOC "R#3 "
      PLINE = PLINE:RLCYN.TYPE "R#4 "
      PLINE = PLINE:OCONV(RLCYN.PERF.INCH,"MD4") "R#7"
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      RETURN
*
*---- PRINT HEADING
*
10000 *
      MATREAD CCTR.REC FROM COST.CNTR,CONO:PREV.CCTR ELSE
         MAT CCTR.REC = ''
         CCTR.DESC = 'UNKNOWN'
      END
      PAGE.CNT = PAGE.CNT + 1
      PRINT CHAR(12)
      PRINT HD1:PAGE.CNT
      PRINT HD2
      PRINT
      PRINT HD3:PREV.CCTR:' ':CCTR.DESC
      PRINT
      PRINT HD4
      PRINT HD5
      PRINT
      LINE.CNT = 8
      RETURN
*
*---- SYSCOM MESSAGES
*
91000 *
      ERR.TYPE=1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 *
      ERR.TYPE=2
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE=3
      CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
      PRINTER OFF
   END
