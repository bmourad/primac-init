***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.GATHER.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/85
*
* DESCRIPTION
*
* This program updates and maintains the EQUIPMENT file (Gatherer items).
*
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","EQUIPMENT" TO EQUIPMENT ELSE
    ERRMSG = "CANNOT OPEN EQUIPMENT FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","COST.CNTR" TO COST.CNTR ELSE
    ERRMSG = "CANNOT OPEN COST.CNTR FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.GATHER.MAINT
  SCREEN FORMAT
  GOTO 110
*
*---- MAIN PROCESSING
*
100*
  RELEASE
  SCREEN CLEAR
110*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;EST.GATHER.MAINT;1
  SCREEN FIELD;EST.GATHER.MAINT;2
  SCREEN INPUT;EST.GATHER.MAINT;2
  IF S$VALUE = "END" THEN GOTO 99999
  EQP.KEY = S$VALUE
  MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
    ERRMSG = "Item must be a valid cost center"
    GOSUB 90000
    GOTO 100
  END
  S$DATA(3)<S$SCR> = CCTR.DESC
  SCREEN DISPLAY;EST.GATHER.MAINT;3
  NEW.REC = 0
  * T26126 v
  MATREADU EQUIPMENT.REC FROM EQUIPMENT, CONO:EQP.KEY LOCKED
    ERRMSG = 'GATHERER record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 100 
  END ELSE
  * T26126 ^
    MAT EQUIPMENT.REC = ""
    NEW.REC = 1
  END
  IF EQP.TYPE # "" AND EQP.TYPE # "22" THEN
    ERRMSG = "Item has already been defined as other than a gatherer"
    GOSUB 90000
    GOTO 100
  END
  IF NEW.REC THEN
    EQP.TYPE = "22"
    FOR FLD = 1 TO 5
      ON FLD GOSUB 1100,1200,1300,1400,1500
      IF S$VALUE = "END" THEN
        GOTO 100
      END
    NEXT FLD
  END ELSE
    GOSUB 80000
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.GATHER.MAINT;21
  SCREEN INPUT;EST.GATHER.MAINT;21
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION # "" AND NUM(OPTION)
      FLD = OPTION
      ON FLD GOSUB 1100,1200,1300,1400,1500
    CASE OPTION = "E" OR OPTION = "END"
      GOTO 100
    CASE OPTION = "F"
      MATWRITE EQUIPMENT.REC ON EQUIPMENT, CONO:EQP.KEY
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET NUMBER OF POCKETS
*
1100*
  SCREEN FIELD;EST.GATHER.MAINT;4
  SCREEN INPUT;EST.GATHER.MAINT;4;RETURN
  EQP.GATHER.PKT.CNT = S$VALUE
  RETURN
*
*---- GET SPINE SPECS
*
1200*
  SCREEN FIELD;EST.GATHER.MAINT;5
  SCREEN INPUT;EST.GATHER.MAINT;5;RETURN
  EQP.GATHER.SPINE.MIN.LENGTH = S$VALUE
  SCREEN FIELD;EST.GATHER.MAINT;6
  SCREEN INPUT;EST.GATHER.MAINT;6;RETURN
  EQP.GATHER.SPINE.MAX.LENGTH = S$VALUE
  RETURN
*
*---- GET SIG SPECS
*
1300*
  SCREEN FIELD;EST.GATHER.MAINT;7
  SCREEN INPUT;EST.GATHER.MAINT;7;RETURN
  EQP.GATHER.SIG.MIN.WIDTH = S$VALUE
  SCREEN FIELD;EST.GATHER.MAINT;8
  SCREEN INPUT;EST.GATHER.MAINT;8;RETURN
  EQP.GATHER.SIG.MAX.WIDTH = S$VALUE
  RETURN
*
*---- GET POCKETS PER HELPER
*
1400*
  SCREEN FIELD;EST.GATHER.MAINT;9
  SCREEN INPUT;EST.GATHER.MAINT;9;RETURN
  EQP.GATHER.PKT.PER.HELPER = S$VALUE
  RETURN
*
*---- GET PERCENT RUN-TIME INCREMENT PER POCKET
*
1500*
  SCREEN FIELD;EST.GATHER.MAINT;10
  SCREEN INPUT;EST.GATHER.MAINT;10;RETURN
  EQP.GATHER.PKT.PCT.INCR = S$VALUE
  RETURN
*
*---- LOAD SCREEN VALUES
*
80000*
  MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
    MAT CCTR.REC = ""
    CCTR.DESC = "??????????"
  END
  S$DATA(3)<S$SCR> = CCTR.DESC
  S$DATA(4)<S$SCR> = EQP.GATHER.PKT.CNT
  S$DATA(5)<S$SCR> = EQP.GATHER.SPINE.MIN.LENGTH
  S$DATA(6)<S$SCR> = EQP.GATHER.SPINE.MAX.LENGTH
  S$DATA(7)<S$SCR> = EQP.GATHER.SIG.MIN.WIDTH
  S$DATA(8)<S$SCR> = EQP.GATHER.SIG.MAX.WIDTH
  S$DATA(9)<S$SCR> = EQP.GATHER.PKT.PER.HELPER
  S$DATA(10)<S$SCR> = EQP.GATHER.PKT.PCT.INCR
80090*
  SCREEN DISPLAY;EST.GATHER.MAINT;ALL
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  STOP
END
