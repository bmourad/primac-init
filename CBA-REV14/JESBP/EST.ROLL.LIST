*COPY>CPYLIB>COM1
****************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - EST.ROLL.LIST
*
* SYSTEM        - JESBP
*
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 04/18/86
*
* DESCRIPTION   -
* This program produces an Estimate Material (Roll) report.
*
*T25901 cmykleb 03/08/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*ENDDOC
****************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','ESTIMATE.MATL' TO ESTIMATE.MATL ELSE ERRMSG='ESTIMATE.MATL FILE MISSING';GOTO 93000
   OPEN '','ESTIMATE.PAPER.GROUP' TO ESTIMATE.PAPER.GROUP ELSE ERRMSG='ESTIMATE.PAPER.GROUP FILE MISSING';GOTO 93000
*
*---- READ FROM PROC
*
   PROCREAD BUFFER ELSE
      ERRMSG = "CANNOT READ FROM PROC"
      GOTO 93000
   END
*
*---- GET COMPANY NUMBER
*
   CONO=""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO='END' THEN GOTO 99999
*
*---- INITIALIZATION
*
   PG.NO=0
   LINE.CNT=0
   ERRMSG=""
   PREV.PGRP = ""
   RPT.NO="XXXX"
   UNKNOWN=STR("?",30)
   SP = " "
   SP2 = "  "
   SP3 = "   "
   SP4 = "    "
   NODATA=1
*T25901 v
*  K = SPACE(INT((50 - LEN(CO.NAME)) / 2))
*  CONAME = K:CO.NAME:K
*  CONAME = CONAME "L#50"
*  HEAD1 = OCONV(DATE(),"D2/"):SPACE(33):CONAME:SPACE(32):"PAGE  "
*  HEAD2 = SPACE(45):"E S T I M A T E   R O L L   L I S T I N G"
*T26493 v
*  REPORT.NAME = "ESTIMATE ROLL LISTING"
*  REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = BUFFER<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26493 ^
   HEAD1 = "" ; HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HEAD1,HEAD2)
*T25901 ^
   HEAD3 = "PAPER GROUP : "
   HEAD4 = SPACE(55):"GRAIN           COAT  FORMULA":SPACE(19):"<------- PRICING -------->"
   HEAD5 = " PROD#                 FULL DESCRIPTION                 DIR   CAL/PPI  CODE    ID      FOH%    MKUP%   FROM-WGT  TO-WGT    PRICE"
   HEAD6 = "------  ---------------------------------------------  -----  -------  ----  -------  ------  ------   -------- -------- --------"
   PRINTER ON
*
*---- MAIN PROCESSING
*
10*
   READNEXT ESTM.KEY ELSE 
      IF NODATA THEN GOTO 99999
      GOTO 15
   END
   MATREAD ESTM.REC FROM ESTIMATE.MATL,ESTM.KEY ELSE GOTO 10
   NODATA=0
   PGRP.NO = ESTM.KEY[5,2]
   IF PREV.PGRP # PGRP.NO THEN
      MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP,CONO:PGRP.NO ELSE ESTPG.DESC = "UNKNOWN"
      GOSUB 20
      PREV.PGRP=PGRP.NO
   END
   PROD.CNT = COUNT(ESTM.PROD,VM) + (ESTM.PROD # "")
   FOR ZZ = 1 TO PROD.CNT
      PROD.NO = ESTM.PROD<1,ZZ> "L#6"
      DESC = ESTM.FULL.DESC<1,ZZ> "L#45"
      GRAIN = ESTM.GRAIN<1,ZZ> "L#1"
      IF CO.JES.PARAM<1,10> = "Y" THEN
         CAL.PPI = ESTM.PPI<1,ZZ>"R#7"
      END ELSE
         CAL.PPI = " ":OCONV(ESTM.CALIPER<1,ZZ>,"MD3Z")"R#6"
      END
      COAT.CD = ESTM.COAT.CD<1,ZZ> "L#1"
      FORM.ID = ESTM.QPARAM<1,ZZ> "L#7"
      IF NUM(ESTM.FOH.PCT<1,ZZ>) THEN
         FOH = OCONV(ESTM.FOH.PCT<1,ZZ>,"MD2") "R#6"
      END ELSE
         FOH = ESTM.FOH.PCT<1,ZZ> "R#6"
      END
      IF NUM(ESTM.MARKUP<1,ZZ>) THEN
         MKUP = OCONV(ESTM.MARKUP<1,ZZ>,"MD2") "R#6"
      END ELSE
         MKUP = ESTM.MARKUP<1,ZZ> "R#6"
      END
      BQ.CNT = COUNT(ESTM.BEG.QTY<1,ZZ>,SVM) + (ESTM.BEG.QTY<1,ZZ> # "")
      BEG.QTY = ESTM.BEG.QTY<1,ZZ,1> "R#8"
      END.QTY = ESTM.END.QTY<1,ZZ,1> "R#8"
      QCOST = OCONV(ESTM.QCOST<1,ZZ,1>,"MD2") "R#8"
      GOSUB 30
   NEXT ZZ
   GOTO 10
15*
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
   PRINT CHAR(12)
   PRINT
   PG.NO=PG.NO + 1
   PRINT HEAD1:PG.NO
   PRINT HEAD2
   PRINT HEAD3:PGRP.NO:"  ":ESTPG.DESC
   PRINT
   PRINT HEAD4
   PRINT HEAD5
   PRINT HEAD6
   LINE.CNT = 7
   RETURN
*
*---- PRINT THE VALUES
*
30*
   PRINT
   PRINT PROD.NO:SP2:DESC:SP4:GRAIN:SP4:CAL.PPI:SP3:COAT.CD:SP4:FORM.ID:SP2:FOH:SP2:MKUP:SP3:BEG.QTY:SP:END.QTY:SP:QCOST
   LINE.CNT = LINE.CNT + 2
   IF LINE.CNT >= 55 THEN GOSUB 20
   IF BQ.CNT > 1 THEN
      FOR QQ = 2 TO BQ.CNT
         BEG.QTY = ESTM.BEG.QTY<1,ZZ,QQ> "R#8"
         END.QTY = ESTM.END.QTY<1,ZZ,QQ> "R#8"
         QCOST = OCONV(ESTM.QCOST<1,ZZ,QQ>,"MD2") "R#8"
         PRINT SPACE(103):BEG.QTY:SP:END.QTY:SP:QCOST
         LINE.CNT = LINE.CNT + 1
         IF LINE.CNT >= 55 THEN GOSUB 20
      NEXT QQ
   END
   RETURN
*
*---- CALLS FOR SYSCOM
*
93000*
   PRINTER OFF
   ERR.TYPE=3
   CALL SI_SYSCOM(MAT SYSCOM.REC)
99999*
END
