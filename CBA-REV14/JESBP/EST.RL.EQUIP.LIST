*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RES.BP
* PROGRAM     - EST.RL.EQUIP.LIST
* BY          - KERRY WILSON
* DATE        - 01/30/90
* DESCRIPTION - This program lists the Roll Equipment file.
*********************************************************************
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET SYSCOM
*
      SYS.TYPE=1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN 'EQUIPMENT' TO EQUIPMENT ELSE
         ERRMSG = 'CANNOT OPEN EQUIPMENT FILE'
         GOTO 93000
      END
      OPEN 'COST.CNTR' TO COST.CNTR ELSE
         ERRMSG = 'CANNOT OPEN COST.CNTR FILE'
         GOTO 93000
      END
      OPEN 'COMPANY' TO COMPANY ELSE
         ERRMSG = 'CANNOT OPEN COMPANY FILE'
         GOTO 93000
      END
      OPEN 'CONTROL' TO CONTROL ELSE
         ERRMSG = 'CANNOT OPEN CONTROL FILE'
         GOTO 93000
      END
*
*---- PROC READ
*
      PROCREAD ID ELSE
         ERRMSG = "MUST RUN FROM MENU"
         GOSUB 91000
         GOTO 99999
      END
      CONO = ID<1>
      CONO.NAME = ID<2>
      PRT.FLG = ID<3>
*
*---- SET UP HEADINGS
*
      REPORT.NUMBER = "XXXX"
      REPORT.DATE = DATE()
      HD1 = ''
      HD1B = SPACE(INT((100-LEN(CONO.NAME))/2)):CONO.NAME
      HD1B = HD1B "L#100"
      HD1 = HD1:HD1B:'   PAGE '
      HD2 = 'REPORT # : ':REPORT.NUMBER
      HD2 = HD2:SPACE(36):'R O L L   L A B E L   P R E S S   L I S T I N G'
      HD4 = 'PRESS        DESCRIPTION         R MIN  R MAX  W MIN  W MAX CU DU SPOIL'
      HD5 = '----- ------------------------- ------ ------ ------ ------ -- -- ------'
*
*---- INITIALIZATION
*
      LINE.CNT = 999
      PAGE.CNT = 0
      PRINTER ON
*
*---- MAIN PROCESSING
*
100 *
      READNEXT KEY ELSE GOTO 99999
      PRESS = KEY[4,12]
      MATREAD CCTR.REC FROM COST.CNTR,CONO:PRESS ELSE
         MAT CCTR.REC = ''
         CCTR.DESC = 'UNKNOWN'
      END
      MATREAD EQUIPMENT.REC FROM EQUIPMENT,KEY ELSE GOTO 100
      BEGIN CASE
      CASE LINE.CNT + 3 > 54
         GOSUB 10000
         GOSUB 1000
      CASE 1
         GOSUB 1000
      END CASE
      DONE = 0
      LOOP
         READNEXT KEY ELSE DONE = 1
      UNTIL DONE DO
         PRESS = KEY[4,12]
         MATREAD EQUIPMENT.REC FROM EQUIPMENT,KEY ELSE GOTO 100
         MATREAD CCTR.REC FROM COST.CNTR,CONO:PRESS ELSE
            MAT CCTR.REC = ''
            CCTR.DESC = 'UNKNOWN'
         END
         BEGIN CASE
         CASE LINE.CNT + 3 > 54
            GOSUB 10000
            GOSUB 1000
         CASE 1
            GOSUB 1000
         END CASE
      REPEAT
      GOTO 99999
*
*---- PRINT DETAIL
*
1000 *
      PLINE = PRESS "L#5 "
      PLINE = PLINE:CCTR.DESC "L#25 "
      PLINE = PLINE:OCONV(EQP.RL.PRESS.REPEAT.MIN,"MD3")"R#6 "
      PLINE = PLINE:OCONV(EQP.RL.PRESS.REPEAT.MAX,"MD3")"R#6 "
      PLINE = PLINE:OCONV(EQP.RL.PRESS.WIDTH.MIN,"MD3")"R#6 "
      PLINE = PLINE:OCONV(EQP.RL.PRESS.WIDTH.MAX,"MD3")"R#6 "
      PLINE = PLINE:EQP.RL.PRESS.COLOR.UNITS "R#2 "
      PLINE = PLINE:EQP.RL.PRESS.DIE.UNITS "R#2 "
      PLINE = PLINE:EQP.RL.PRESS.SPOIL.TBL "L#6 "
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      RETURN
*
*---- PRINT HEADING
*
10000 *
      PAGE.CNT = PAGE.CNT + 1
      PRINT CHAR(12)
      PRINT HD1:PAGE.CNT
      PRINT HD2
      PRINT
      PRINT HD4
      PRINT HD5
      PRINT
      LINE.CNT = 6
      RETURN
*
*---- SYSCOM MESSAGES
*
91000 *
      ERR.TYPE=1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
92000 *
      ERR.TYPE=2
      CALL SI_SYSCOM(MAT SYSCOM.REC)
      RETURN
93000 *
      ERR.TYPE=3
      CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
      PRINTER OFF
      END
