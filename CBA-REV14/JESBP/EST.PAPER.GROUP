*COPY>CPYLIB>SCOMMON1
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.PAPER.GROUP
*
* SOURCE   - JESBP 
*
* AUTHOR   - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/24/86
*
* DESCRIPTION
*
* This program updates and maintains the ESTIMATE.PAPER.GROUP file.
*
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.PAPER.GROUP" TO ESTIMATE.PAPER.GROUP ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.PAPER.GROUP FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.PAPER.GROUP
  SCREEN FORMAT
*
*---- MAIN PROCESSING
*
100*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;EST.PAPER.GROUP;1
  SCREEN FIELD;EST.PAPER.GROUP;2
  SCREEN INPUT;EST.PAPER.GROUP;2
  IF S$VALUE = "END" THEN GOTO 99999
  ESTPG.KEY = S$VALUE
  NEW.REC = 0
  * T26126 v
  MATREADU ESTPG.REC FROM ESTIMATE.PAPER.GROUP, CONO:ESTPG.KEY LOCKED
    ERRMSG = 'PAPER GROUP record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 100 
  END ELSE
  * T26126 ^
    MAT ESTPG.REC = ""
    NEW.REC = 1
  END
  IF NEW.REC THEN
    FOR FLD = 1 TO 3
      BEGIN CASE
        CASE FLD <= 10
          ON FLD GOSUB 10100,10200,10300,19990,19990,19990,19990,19990,19990,19990
        CASE FLD <= 20
          ON FLD-10 GOSUB 19990,19990,19990,19990,19990
      END CASE
      IF S$VALUE = "END" THEN
        SCREEN CLEAR;EST.PAPER.GROUP
        RELEASE
        GOTO 100
      END
    NEXT FLD
  END ELSE
    GOSUB 80000
    SCREEN DISPLAY;EST.PAPER.GROUP;ALL
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.PAPER.GROUP;6
  SCREEN INPUT;EST.PAPER.GROUP;6
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION # "" AND NUM(OPTION)
      FLD = OPTION
      BEGIN CASE
        CASE FLD <= 10
          ON FLD GOSUB 10100,10200,10300,19990,19990,19990,19990,19990,19990,19990
        CASE FLD <= 20
          ON FLD-10 GOSUB 19990,19990,19990,19990,19990
      END CASE
    CASE OPTION = "E" OR OPTION = "END"
      SCREEN CLEAR;EST.PAPER.GROUP
      RELEASE
      GOTO 100
    CASE OPTION = "F"
      MATWRITE ESTPG.REC ON ESTIMATE.PAPER.GROUP, CONO:ESTPG.KEY
      SCREEN CLEAR;EST.PAPER.GROUP
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET PAPER GROUP DESCRIPTION
*
10100*
  SCREEN FIELD;EST.PAPER.GROUP;3
  SCREEN INPUT;EST.PAPER.GROUP;3;RETURN
  ESTPG.DESC = S$VALUE
  RETURN
*
*---- GET WIDTH AND LENGTH
*
10200*
  SCREEN FIELD;EST.PAPER.GROUP;4
  SCREEN INPUT;EST.PAPER.GROUP;4;RETURN
  ESTPG.WIDTH = S$VALUE
  SCREEN FIELD;EST.PAPER.GROUP;5
  SCREEN INPUT;EST.PAPER.GROUP;5;RETURN
  ESTPG.LENGTH = S$VALUE
  RETURN
*
*---- GET WEIGHT CLASSIFICATION
*
10300*
  SCREEN FIELD;EST.PAPER.GROUP;7
  SCREEN INPUT;EST.PAPER.GROUP;7;RETURN
  ESTPG.WGT.CLASS<1,1> = S$VALUE
  SCREEN FIELD;EST.PAPER.GROUP;8
  SCREEN INPUT;EST.PAPER.GROUP;8;RETURN
  ESTPG.WGT.CLASS<1,2> = S$VALUE
  RETURN
*
*---- DUMMY
*
19990*
  RETURN
*
*---- LOAD SCREEN VALUES
*
80000*
  S$DATA(3)<S$SCR> = ESTPG.DESC
  S$DATA(4)<S$SCR> = ESTPG.WIDTH
  S$DATA(5)<S$SCR> = ESTPG.LENGTH
  S$DATA(7)<S$SCR> = ESTPG.WGT.CLASS<1,1>
  S$DATA(8)<S$SCR> = ESTPG.WGT.CLASS<1,2>
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  STOP
END
