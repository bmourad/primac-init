      SUBROUTINE EST.FORMULA.Q025 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q025
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate the number of square feet of
* text line film required for specified component.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEMP.QTY = ""
      TYP = 0
      IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
*
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MP ELSE RETURN
*
10*
      IF QPTR = 1 AND ACTION # "R" THEN
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=13;MAXL=4;MINV=1;MAXV=9999;JUSTIFY="L"
         BEGIN CASE
         CASE EST.PROD.PG.CNT<1,COMP,1>+0 > 0
            DEFAULT = EST.PROD.PG.CNT<1,COMP,1>
         CASE TEMP.FVAR1 # ""
            DEFAULT = TEMP.FVAR1
         CASE 1
            DEFAULT = ""
         END CASE
         IF DEFAULT = "" THEN
            PMSG = "Total pages for component ":COMP:" ?"
         END ELSE
            O.R = "O"
            PMSG = "Total pages for component ":COMP:" (Default = ":DEFAULT:") ?"
         END
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         PGS = VALUE
         TEMP.FVAR1 = VALUE
      END ELSE
         PGS = TEMP.FVAR1
      END
*
20*
      IF QPTR = 1 AND ACTION # "R" THEN
         IF EST.PROD.US.WIDTH<1,COMP,MPTR> = "" THEN
            IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
            X=0;Y=23;TYP=14;SCALER=4;MAXL=7;MINV=1;MAXV=99.9999;JUSTIFY="L"
            BEGIN CASE
            CASE TEMP.FVAR2 # ""
               DEFAULT = TEMP.FVAR2 / 10000
            CASE 1
               DEFAULT = ""
            END CASE
            IF DEFAULT = "" THEN
               PMSG = "Untrimmed signature width for component ":COMP:" ?"
            END ELSE
               O.R = "O"
               PMSG = "Untrimmed signature width for component ":COMP:" (Default = ":DEFAULT:") ?"
            END
            CALL EDIT.SUB1 (MAT EDIT.COM)
            IF VALUE = "END" THEN RETURN
            IF VALUE = "" THEN GOTO 20
            WIDTH = VALUE
            TEMP.FVAR2 = WIDTH
         END ELSE
            WIDTH = EST.PROD.US.WIDTH<1,COMP,MPTR>
            TEMP.FVAR2 = WIDTH
         END
      END ELSE
         WIDTH = TEMP.FVAR2
      END
*
30*
      IF QPTR = 1 AND ACTION # "R" THEN
         IF EST.PROD.US.LENGTH<1,COMP,MPTR> = "" THEN
            IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
            X=0;Y=23;TYP=14;SCALER=4;MAXL=7;MINV=1;MAXV=99.9999;JUSTIFY="L"
            BEGIN CASE
            CASE TEMP.FVAR3 # ""
               DEFAULT = TEMP.FVAR3 / 10000
            CASE 1
               DEFAULT = ""
            END CASE
            IF DEFAULT = "" THEN
               PMSG = "Untrimmed signature length for component ":COMP:" ?"
            END ELSE
               O.R = "O"
               PMSG = "Untrimmed signature length for component ":COMP:" (Default = ":DEFAULT:") ?"
            END
            CALL EDIT.SUB1 (MAT EDIT.COM)
            IF VALUE = "END" THEN RETURN
            IF VALUE = "" THEN GOTO 30
            XXLENGTH = VALUE
            TEMP.FVAR3 = VALUE
         END ELSE
            XXLENGTH = EST.PROD.US.LENGTH<1,COMP,MPTR>
            TEMP.FVAR3 = XXLENGTH
         END
      END ELSE
         XXLENGTH = TEMP.FVAR3
      END
*
*---- MAIN PROCESSING
*
      SQ.FT = INT(WIDTH/10000 * XXLENGTH/10000 / 144 * 100 + 0.99)
      TEMP.QTY = INT(PGS * SQ.FT + 0.99)
      TEMP.QTY = TEMP.QTY / 100
      TEMP.FCTR = 1000
      TEMP.STD = ESTM.COST<1,MP> / 100
      TEMP.STD.TYPE = "$/":ESTM.UM<1,MP>
      TEMP.UM = ESTM.COST.UNIT<1,MP>
      TEMP.IND.1 = ESTM.FOH.PCT<1,MP>
      TEMP.MARKUP = ESTM.MARKUP<1,MP>
      TEMP.DESC = ESTM.FULL.DESC<1,MP>
      RETURN
   END
