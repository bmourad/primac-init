      SUBROUTINE EST.RES.COST.TYPE.SUB (CONO, EST.KEY, DSEL, CSEL, QSEL, MAT CSUM.REC)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RES.COST.TYPE.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/05/86
*
* DESCRIPTION
*
* This subroutine summarizes estimate data by type. (Labor, Material, O/P,
* Shipping, Miscellaneous).
* Summarization may occur for a single department or "ALL", a single
* component or "ALL", and a specified order quantity.
*
*MODIFIED    - G JARVIS   Pacrim Software Pty Ltd
*              Roll Label Estimating REV10
*C32159 rik 11/25/1998 * Mat'l detail amounts displaying in wrong places.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>EST.RES.COST.TYPE.SUM
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      MAT CSUM.REC = ""
      READ EST.RL.PP FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE EST.RL.PP=''
*
*---- MAIN PROCESSING
*
100 *
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
*         IF EST.RL.PP.INCLUDE#'Y' THEN
*           LOCATE ESTD.DEPT IN EST.RL.PP<1>,1 SETTING DPOS THEN GO 190
*         END
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>[1,1]
            IF TYPE = "G" THEN GOTO 180
            TSUB = ESTD.TYPE<1,TP>[2,1]
            HRS = ESTD.HRS<1,TP>
            DCOST = ESTD.DCOST<1,TP>
            COST = ESTD.COST<1,TP>
            IF EST.RL.PP.INCLUDE#'Y' THEN
               LOCATE ESTD.DEPT IN EST.RL.PP<1>,1 SETTING DPOS THEN
                  SALE = 0
               END ELSE
                  SALE = ESTD.TSALE<1,TP>
               END
            END ELSE
               SALE = ESTD.TSALE<1,TP>
            END
            RATE = ESTD.RATE<1,TP>
            PROD = ESTD.OPV<1,TP>
            QCALC = ESTD.GRP.QCALC<1,TP>
            QPARAM = ESTD.GRP.QPARAM<1,TP>
            BEGIN CASE
               CASE TYPE = "L"
                  IF QCALC = "A" THEN
                     BEGIN CASE
                        CASE QPARAM = "100"
                           CSUM.LB.COPY = CSUM.LB.COPY + RATE
                        CASE QPARAM = "200"
                           CSUM.LB.COLOR = CSUM.LB.COLOR + RATE
                        CASE QPARAM = "300"
                           CSUM.LB.VERSION = CSUM.LB.VERSION + RATE
                     END CASE
                  END
                  CSUM.LB.HRS = CSUM.LB.HRS + HRS
                  CSUM.LB.DCOST = CSUM.LB.DCOST + DCOST
                  CSUM.LB.COST = CSUM.LB.COST + COST
                  CSUM.LB.SALE = CSUM.LB.SALE + SALE
               CASE TYPE = "M" OR TYPE = "I"
                  IF QCALC = "A" THEN
                     BEGIN CASE
                        CASE QPARAM = "100"
                           CSUM.MT.COPY<1,-1> = PROD
                        CASE QPARAM = "200"
                           CSUM.MT.COLOR<1,-1> = PROD
                        CASE QPARAM = "300"
                           CSUM.MT.VERSION<1,-1> = PROD
                     END CASE
                  END
                  CSUM.MT.DCOST = CSUM.MT.DCOST + DCOST
                  CSUM.MT.COST = CSUM.MT.COST + COST
                  CSUM.MT.SALE = CSUM.MT.SALE + SALE
                  BEGIN CASE
* CSF 32159 v
                     CASE ESTD.TYPE<1,TP> = 'MR' OR ESTD.TYPE<1,TP> = 'MS'
                        DET = 1
                     CASE ESTD.TYPE<1,TP> = 'MI'
                        DET = 2
                     CASE ESTD.TYPE<1,TP> = 'MF'
                        DET = 3
                     CASE ESTD.TYPE<1,TP> = 'MP'
                        DET = 4
                     CASE ESTD.TYPE<1,TP> = 'MC'
                        DET = 5
                     CASE ESTD.TYPE<1,TP> = 'MO'
                        DET = 6
*                    CASE TSUB = "S" OR TSUB = "R" OR TSUB = "L"
*                       DET = 1
*                    CASE TSUB = 'I' AND PROD[1,1]='L'
*                       DET = 2
*                    CASE TSUB = 'I' AND PROD[1,1]='S'
*                       DET = 3
*                    CASE TSUB = 'I'
*                       DET = 4
*                    CASE TSUB = "F"
*                       DET = 5
*                    CASE TSUB = "P"
*                       DET = 6
*                    CASE TSUB = "C"
*                       DET = 7
*                    CASE 1
*                       DET = 8
*CSF 32159 ^
                  END CASE
                  CSUM.MT.DCOST.DET<1,DET> = CSUM.MT.DCOST.DET<1,DET> + DCOST
                  CSUM.MT.COST.DET<1,DET> = CSUM.MT.COST.DET<1,DET> + COST
                  CSUM.MT.SALE.DET<1,DET> = CSUM.MT.SALE.DET<1,DET> + SALE
               CASE TYPE = "P"
                  CSUM.OS.DCOST = CSUM.OS.DCOST + DCOST
                  CSUM.OS.COST = CSUM.OS.COST + COST
                  CSUM.OS.SALE = CSUM.OS.SALE + SALE
               CASE TYPE = "S"
                  CSUM.SP.DCOST = CSUM.SP.DCOST + DCOST
                  CSUM.SP.COST = CSUM.SP.COST + COST
                  CSUM.SP.SALE = CSUM.SP.SALE + SALE
               CASE TYPE = "O"
                  CSUM.MS.DCOST = CSUM.MS.DCOST + DCOST
                  CSUM.MS.COST = CSUM.MS.COST + COST
                  CSUM.MS.SALE = CSUM.MS.SALE + SALE
            END CASE
            CSUM.TOT.DCOST = CSUM.TOT.DCOST + DCOST
            CSUM.TOT.COST = CSUM.TOT.COST + COST
            CSUM.TOT.SALE = CSUM.TOT.SALE + SALE
180      NEXT TP
190   NEXT DP
      RETURN
   END
