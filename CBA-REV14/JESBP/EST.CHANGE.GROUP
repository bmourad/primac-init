   SUBROUTINE EST.CHANGE.GROUP (CONO, EQTY, DEPT, COMP, REF.NO)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.CHANGE.GROUP
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/28/87
*
* DESCRIPTION
*
* This subroutine in used to modify group related data when the group
* definition item is modified on the department detail screen.
*
*T27716 cmykleb 10/09/2003 * Add Pre-pres inclusive functionality.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>CPYLIB>FILE.VARS
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- MAIN PROCESSING
*
100*
   LPTR = REF.NO
   GRP.ID = ESTD.GRP.ID<1,LPTR>
   GRP.QTY = ESTD.QTY<1,REF.NO> / 100
   IF GRP.QTY = INT(GRP.QTY) THEN GRP.QTY = INT(GRP.QTY)
   GRP.FCTR = ESTD.FCTR<1,LPTR>
   LPTR = REF.NO
   LOOP
      LPTR = LPTR + 1
   WHILE ESTD.GRP.ID<1,LPTR> = GRP.ID DO
      QCALC = ESTD.GRP.QCALC<1,LPTR>
      FCALC = ESTD.GRP.FCALC<1,LPTR>
      IF QCALC = "M" OR QCALC = "F" OR FCALC = "M" OR FCALC = "F" OR FCALC = "T" THEN
         MAT ESTD.TEMP = ""
         FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
            ESTD.TEMP(A) = ESTD.REC(A)<1,LPTR>
         NEXT A
         TEMP.QTY = TEMP.QTY / 100
         IF TEMP.QTY = INT(TEMP.QTY) THEN TEMP.QTY = INT(TEMP.QTY)
         TEMP.STD = TEMP.STD / 100
         IF TEMP.STD = INT(TEMP.STD) THEN TEMP.STD = INT(TEMP.STD)
         TEMP.GRP.QTY = GRP.QTY
         TEMP.GRP.FCTR = GRP.FCTR
         IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
         BEGIN CASE
            CASE TEMP.TYPE[1,1] = "M"
               BEGIN CASE
                  CASE TEMP.TYPE = "MS" OR TEMP.TYPE = "MR"
                     ESTM.KEY = EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,MPTR>
                  CASE 1
                     ESTM.KEY = TEMP.TYPE[2,1]
               END CASE
               MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:ESTM.KEY ELSE GOTO 390
         END CASE
         BEGIN CASE
            CASE TEMP.GRP.QCALC = ""
            CASE TEMP.GRP.QCALC = "C"
            CASE TEMP.GRP.QCALC = "M"
               TEMP.QTY = GRP.QTY * TEMP.GRP.QPARAM
            CASE TEMP.GRP.QCALC = "F"
               TEMP.FPTR = REF.NO
               SUB.NAME = "EST.FORMULA.Q":TEMP.GRP.QPARAM
               CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
         END CASE
*
         BEGIN CASE
            CASE TEMP.GRP.FCALC = ""
            CASE TEMP.GRP.FCALC = "C"
            CASE TEMP.GRP.FCALC = "M"
               TEMP.FCTR = INT(GRP.FCTR*TEMP.GRP.FPARAM+0.5)
            CASE TEMP.GRP.FCALC = "F"
               TEMP.FPTR = REF.NO
               SUB.NAME = "EST.FORMULA.F":TEMP.GRP.FPARAM
               CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
            CASE TEMP.GRP.FCALC = "T"
               TBL.ID = TEMP.GRP.FPARAM
               CALL EST.TABLE.FCTR (CONO, TBL.ID, TEMP.GRP.FCTR)
               IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
         END CASE
*
*T27716 v
*        CALL EST.CALC.COST (CONO)
         CALL EST.CALC.COST (CONO,DEPT)
*T27716 ^
*
         FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
            ESTD.REC(A)<1,LPTR> = ESTD.TEMP(A)
         NEXT A
         ESTD.QTY<1,LPTR> = TEMP.QTY * 100
         ESTD.STD<1,LPTR> = TEMP.STD * 100
         ESTD.GRP.QTY<1,LPTR> = TEMP.GRP.QTY * 100
      END
390 REPEAT
   RETURN
END
