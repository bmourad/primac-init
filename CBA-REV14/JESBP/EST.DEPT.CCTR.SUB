      SUBROUTINE EST.DEPT.CCTR.SUB (CONO, EST.KEY, DSEL, CSEL, QSEL, MAT DSUM.REC1)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.DEPT.CCTR.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/14/86
*
* DESCRIPTION
*
* This subroutine summarizes estimate data by department, cost center and
* type.  Summarization may occur for a single department or "ALL", a single
* component or "ALL", and a specified order quantity.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>EST.DEPT.CCTR.SUM
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>CPYLIB>FILE.VARS
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      MAT DSUM.REC1 = ""
*
*---- MAIN PROCESSING
*
100*
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
         DEPT = ESTD.DEPT
         LOCATE DEPT IN DSUM.DEPT<1>,1 BY "AL" SETTING P1 ELSE
            IF P1 <= DCNT THEN
               FOR N = 1 TO 40
                  DSUM.REC1(N) = INSERT(DSUM.REC1(N),1,P1,0,"")
               NEXT N
            END
            DSUM.DEPT<1,P1> = DEPT
            LOCATE DEPT IN EST.DEPT<1>,1 SETTING PP ELSE NULL
            DSUM.DEPT.DESC<1,P1> = EST.DEPT.DESC<1,PP>
         END
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>
            IF TYPE = "G" THEN GOTO 180
            CCTR = ESTD.CCTR<1,TP>
            LOCATE CCTR IN DSUM.CCTR<1,P1>,1 BY "AL" SETTING P2 ELSE
               IF P2 <= COUNT(DSUM.CCTR<1,P1>,SM) + (DSUM.CCTR<1,P1> # "") THEN
                  FOR N = 21 TO 40
                     DSUM.REC1(N) = INSERT(DSUM.REC1(N),1,P1,P2,"")
                  NEXT N
               END
               DSUM.CCTR<1,P1,P2> = CCTR
               READV DESC FROM COST.CNTR, CONO:CCTR,1 ELSE DESC = "??????????"
               DSUM.CCTR.DESC<1,P1,P2> = DESC
            END
            TYPE = ESTD.TYPE<1,TP>[1,1]
            HRS = ESTD.HRS<1,TP>
            COST = ESTD.COST<1,TP>
            SALE = ESTD.TSALE<1,TP>
            BEGIN CASE
            CASE TYPE = "L"
               DSUM.DEPT.LABOR.HRS<1,P1> = DSUM.DEPT.LABOR.HRS<1,P1> + HRS
               DSUM.DEPT.LABOR.COST<1,P1> = DSUM.DEPT.LABOR.COST<1,P1> + COST
               DSUM.DEPT.LABOR.SALE<1,P1> = DSUM.DEPT.LABOR.SALE<1,P1> + SALE
               DSUM.CCTR.LABOR.HRS<1,P1,P2> = DSUM.CCTR.LABOR.HRS<1,P1,P2> + HRS
               DSUM.CCTR.LABOR.COST<1,P1,P2> = DSUM.CCTR.LABOR.COST<1,P1,P2> + COST
               DSUM.CCTR.LABOR.SALE<1,P1,P2> = DSUM.CCTR.LABOR.SALE<1,P1,P2> + SALE
            CASE TYPE = "M" OR TYPE = "I"
               DSUM.DEPT.MATL.COST<1,P1> = DSUM.DEPT.MATL.COST<1,P1> + COST
               DSUM.DEPT.MATL.SALE<1,P1> = DSUM.DEPT.MATL.SALE<1,P1> + SALE
               DSUM.CCTR.MATL.COST<1,P1,P2> = DSUM.CCTR.MATL.COST<1,P1,P2> + COST
               DSUM.CCTR.MATL.SALE<1,P1,P2> = DSUM.CCTR.MATL.SALE<1,P1,P2> + SALE
            CASE TYPE = "P"
               DSUM.DEPT.OSP.COST<1,P1> = DSUM.DEPT.OSP.COST<1,P1> + COST
               DSUM.DEPT.OSP.SALE<1,P1> = DSUM.DEPT.OSP.SALE<1,P1> + SALE
               DSUM.CCTR.OSP.COST<1,P1,P2> = DSUM.CCTR.OSP.COST<1,P1,P2> + COST
               DSUM.CCTR.OSP.SALE<1,P1,P2> = DSUM.CCTR.OSP.SALE<1,P1,P2> + SALE
            CASE TYPE = "S"
               DSUM.DEPT.SHIP.COST<1,P1> = DSUM.DEPT.SHIP.COST<1,P1> + COST
               DSUM.DEPT.SHIP.SALE<1,P1> = DSUM.DEPT.SHIP.SALE<1,P1> + SALE
               DSUM.CCTR.SHIP.COST<1,P1,P2> = DSUM.CCTR.SHIP.COST<1,P1,P2> + COST
               DSUM.CCTR.SHIP.SALE<1,P1,P2> = DSUM.CCTR.SHIP.SALE<1,P1,P2> + SALE
            CASE TYPE = "O"
               DSUM.DEPT.MISC.COST<1,P1> = DSUM.DEPT.MISC.COST<1,P1> + COST
               DSUM.DEPT.MISC.SALE<1,P1> = DSUM.DEPT.MISC.SALE<1,P1> + SALE
               DSUM.CCTR.MISC.COST<1,P1,P2> = DSUM.CCTR.MISC.COST<1,P1,P2> + COST
               DSUM.CCTR.MISC.SALE<1,P1,P2> = DSUM.CCTR.MISC.SALE<1,P1,P2> + SALE
            END CASE
180      NEXT TP
190   NEXT DP
      RETURN
   END
