*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JESBP
* PROGRAM     - DIE.FILE.CONV
* DESCRIPTION - Used to convert die records from the old roll label
*               format to the new roll label format 
*               ESTIMATE.RL.DIE ----> ESTIMATE.RES.DIE
*
*********************************************************************
*COPY>JES.CPYLIB>ESTIMATE.RL.DIE
*COPY>JES.CPYLIB>ESTIMATE.RES.GEARSET
*COPY>JES.CPYLIB>ESTIMATE.RES.CYLINDER
*COPY>ICS.CPYLIB>INV.FNGD
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
   DIM NEW.REC(25)
*
*---- SET SYSCOM
*
   SYS.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN 'ESTIMATE.RL.DIE' TO ESTIMATE.RL.DIE ELSE
     ERRMSG = 'CANNOT OPEN ESTIMATE.RL.DIE FILE'
     GOTO 93000
   END
   OPEN 'ESTIMATE.RES.DIE' TO ESTIMATE.RES.DIE ELSE
     ERRMSG = 'CANNOT OPEN ESTIMATE.RES.DIE FILE'
     GOTO 93000
   END
   OPEN 'ESTIMATE.RES.CYLINDER' TO ESTIMATE.RES.CYLINDER ELSE
     ERRMSG = 'CANNOT OPEN ESTIMATE.RES.CYLINDER FILE'
     GOTO 93000
   END
   OPEN 'ESTIMATE.RES.GEARSET' TO ESTIMATE.RES.GEARSET ELSE
     ERRMSG = 'CANNOT OPEN ESTIMATE.RES.GEARSET FILE'
     GOTO 93000
   END
   OPEN 'ESTIMATE.RES.SHAPE' TO ESTIMATE.RES.SHAPE ELSE
     ERRMSG = 'CANNOT OPEN ESTIMATE.RES.SHAPE FILE'
     GOTO 93000
   END
   OPEN 'INV.FNGD' TO INV.FNGD ELSE
     ERRMSG = 'CANNOT OPEN INV.FNGD FILE'
     GOTO 93000
   END
   OPEN 'DICT','INV.FNGD' TO D.INV.FNGD ELSE
     ERRMSG = 'CANNOT OPEN DICT INV.FNGD FILE'
     GOTO 93000
   END
   OPEN 'OLD.DIE.XREF' TO OLD.DIE.XREF ELSE
     ERRMSG = 'CANNOT OPEN OLD.DIE.XREF FILE'
     GOTO 93000
   END
   OPEN 'COMPANY' TO COMPANY ELSE
     ERRMSG = 'CANNOT OPEN COMPANY FILE'
     GOTO 93000
   END
   OPEN 'CONTROL' TO CONTROL ELSE
     ERRMSG = 'CANNOT OPEN CONTROL FILE'
     GOTO 93000
   END
   OPEN 'JES.SCREENS' TO JES.SCREENS ELSE
     ERRMSG = 'CANNOT OPEN JES.SCREENS FILE'
     GOTO 93000
   END
   OPEN 'ESTIMATE.RES.DIE.XREF' TO ESTIMATE.RES.DIE.XREF ELSE
     ERRMSG = 'CANNOT OPEN ESTIMATE.RES.DIE.XREF FILE'
     GOTO 93000
   END
   OPEN 'PREFIX' TO PREFIX ELSE
     ERRMSG = 'CANNOT OPEN PREFIX FILE'
     GOTO 93000
   END
   OPEN 'XREF.DATA' TO XREF.DATA ELSE
     ERRMSG = 'CANNOT OPEN XREF.DATA FILE'
     GOTO 93000
   END
   READ REC FROM D.INV.FNGD, "CONO" ELSE
     ERRMSG = "Dictionary item CONO is missing from the IOF_DIE file"
     GOTO 93000
   END
   READ REC FROM D.INV.FNGD, "IOF_DIE" ELSE
     ERRMSG = "Dictionary item IOF_DIE is missing from the IOF_DIE file"
     GOTO 93000
   END
*
*---- INITIALIZATION
*
   CONO = ""
   CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
   SCREEN INIT;#
   S$SCR = 1
   SCREEN DEFINE;DIE.FILE.CONV;JES.SCREENS
   SCREEN FORMAT
   MAT GEN.XREF.REC = ''
*
*---- MAIN PROCESSING
*
100 *
   SCREEN FIELD;;1
   SCREEN INPUT;;1
   IF S$VALUE = 'END' THEN GOTO 99999
   RL.ID = S$VALUE
   MATREADU RLDIE.REC FROM ESTIMATE.RL.DIE, CONO:RL.ID ELSE
     ERRMSG = "Die does not exist"; GOSUB 91000
     RELEASE
     GOTO 100
   END
   MATREAD RLCYN.REC FROM ESTIMATE.RES.CYLINDER, CONO:RLDIE.CYLINDER ELSE
     ERRMSG = "Cylinder on this die does not exist"; GOSUB 91000
     RELEASE
     GOTO 100
   END
* GOSUB 80000
   FOR I = 1 TO 5
     ON I GOSUB 10100,10200,10300,10400,10500
     IF S$VALUE = "END" THEN
       RELEASE
       GOTO 100
     END
   NEXT I
*
*---- ENTER PROMPT
*
500 *
   SCREEN FIELD;;21
   SCREEN INPUT;;21
   OPTION = S$VALUE
   BEGIN CASE
   CASE NUM(OPTION)
     ON OPTION GOSUB 10100,10200,10300,10400,10500
   CASE OPTION = 'E' OR OPTION = 'END'
     SCREEN CLEAR
     RELEASE
     GOTO 100
   CASE OPTION = 'F'
     WRITE RL.ID ON OLD.DIE.XREF, CONO:RES.ID
     SENTENCE = 'SELECT INV.FNGD WITH CONO = "':CONO:'" AND WITH IOF_DIE = "':RL.ID:'"'
     PERFORM SENTENCE CAPTURING MSG
     DATA = 1
     LOOP
       READNEXT ID ELSE DATA = 0
     WHILE DATA DO
       MATREADU IOF.REC FROM INV.FNGD, ID THEN
         LOCATE RL.ID IN IOF.DIE<1>,1 SETTING FND ELSE NULL
         IOF.DIE<1,FND> = RES.ID
         MATWRITE IOF.REC ON INV.FNGD, ID
       END ELSE
         RELEASE INV.FNGD, ID
       END
     REPEAT
     MAT NEW.REC = ""
     NEW.REC(1) = DESC
     NEW.REC(2) = RLDIE.VEND
     NEW.REC(3) = RLCYN.TEETH
     NEW.REC(4) = RLCYN.REPEAT
     FOR I = 5 TO 16
       NEW.REC(I) = RLDIE.REC(I)
     NEXT I
     NEW.REC(17) = RF
     NEW.REC(18) = CORNERS
     NEW.REC(19) = SHAPE
     NEW.REC(21) = RLDIE.REC(18)
NEW.REC(22) = BLANK
     MATWRITE NEW.REC ON ESTIMATE.RES.DIE, CONO:RES.ID
     DELETE ESTIMATE.RL.DIE, CONO:RL.ID
     NEW.NAME = RF:INT(RLDIE.SZ.ACROSS/10000):'*':INT(RLDIE.SZ.AROUND/10000)
     OLD.NAME = ""
     CALL GEN.XREF.MAINT(CONO,RES.ID,OLD.NAME,NEW.NAME,ESTIMATE.RES.DIE.XREF,PREFIX)
     SCREEN CLEAR
     RELEASE
     GOTO 100
   END CASE
   GOTO 500
*
*---- CYLINDER COST.CNTR
*
10100 *
   GEARSET = RLDIE.CYLINDER[1,2]
   S$DEFAULT = GEARSET
   SCREEN FIELD;;2
   SCREEN INPUT;;2;RETURN
   IF LEN(S$VALUE) < 3 THEN
     ERRMSG = "Die ID must be at least 3 characters long"; GOSUB 91000
     GOTO 10100
   END
   IF S$VALUE[1,2] # GEARSET THEN
     ERRMSG = "First two characters must be the gearset code ":GEARSET; GOSUB 91000
     GOTO 10100
   END
   MATREAD ESTGS.REC FROM ESTIMATE.RES.GEARSET, CONO:S$VALUE[1,2] ELSE
     ERRMSG = "First two characters must be a vaild gearset code"; GOSUB 91000
     GOTO 10100
   END
   READ REC FROM ESTIMATE.RES.DIE, CONO:S$VALUE THEN
     ERRMSG = "New Die ID is already on file"; GOSUB 91000
     GOTO 10100
   END
   IF ESTGS.IMPRESS = "" THEN
     RF = 'R'
   END ELSE
     RF = 'F'
   END
   RES.ID = S$VALUE
   RETURN
*
*---- DIE DESCRIPTION
*
10200 *
   SCREEN FIELD;;3
   SCREEN INPUT;;3;RETURN
   DESC = S$VALUE
   RETURN
*
*---- RADIAL CORNERS
*
10300 *
   SCREEN FIELD;;5
   SCREEN INPUT;;5
   CORNERS = S$VALUE
   RETURN
*
*--- SHAPE
*
10400 *
   SCREEN FIELD;;6
   SCREEN INPUT;;6;RETURN
   READ REC FROM ESTIMATE.RES.SHAPE, CONO:S$VALUE ELSE
     ERRMSG = "Invalid die shape"; GOSUB 91000
     GOTO 10400
   END
   SHAPE=S$VALUE
   RETURN
*
*---- DIE BLANK
*
10500 *
   SCREEN FIELD;;7
   SCREEN INPUT;;7
   BLANK = S$VALUE
   RETURN
*
*---- LOAD SCREEN
*
80000 *
   S$DATA(4)<S$SCR> = RLDIE.TEETH
   S$DATA(5)<S$SCR> = RLDIE.REPEAT
   S$DATA(6)<S$SCR> = RLDIE.QUANTITY
   S$DATA(8)<S$SCR> = RLDIE.LOC
   S$DATA(9)<S$SCR> = RLDIE.TYPE
   S$DATA(10)<S$SCR> = RLDIE.PERF.INCH
   SCREEN DISPLAY;;ALL
   RETURN
*
*---- SYSCOM MESSAGES
*
91000 *
   ERR.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE=2
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE=3
   CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
*    PRINT @(-1)
 END
