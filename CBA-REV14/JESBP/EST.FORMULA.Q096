   SUBROUTINE EST.FORMULA.Q096 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q096
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate the number of cartons required to
* ship bound books.
*
* T27924 cmykleb 03/17/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.QTY = ""
   TYP = 0
   PLEN = ""
   PWGT = ""
   PLEN<1> =  6;  PWGT<1> = 0.2000
   PLEN<2> =  9;  PWGT<2> = 0.2500
   PLEN<3> = 11;  PWGT<3> = 0.3333
   PLEN<4> = 99;  PWGT<4> = 0.4000
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*T27924 v
   CQTY = EST.COMP.QTY<1,COMP,QPTR>
   BEGIN CASE
      CASE CQTY=""
         COMPQTY = EQTY
      CASE (CQTY[1,1]="+" OR CQTY[1,1]="-") AND CQTY[LEN(CQTY),1]="%"
         COMPQTY = INT(EQTY * (1 + (CQTY[1,LEN(CQTY)-1]/100)) + 0.5)
      CASE CQTY[1,1]="+" OR CQTY[1,1]="-"
         COMPQTY = EQTY + CQTY
      CASE 1
         COMPQTY = CQTY
   END CASE
   IF COMPQTY < 0 THEN COMPQTY = 0
*T27924 ^
   IF TEMP.TYPE[1,1] = "M" THEN
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MP ELSE RETURN
   END
10*
   IF QPTR = 1 AND ACTION # "R" THEN
      IF EST.BIND.LBS.PER.BOX+0 = 0 THEN
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=13;MAXL=4;MINV=1;MAXV=9999;JUSTIFY="L"
         BEGIN CASE
            CASE TEMP.FVAR1 # ""
               DEFAULT = TEMP.FVAR1
            CASE 1
               DEFAULT = 40
         END CASE
         IF DEFAULT = "" THEN
            PMSG = "Pounds per box ?"
         END ELSE
            O.R = "O"
            PMSG = "Pounds per box (Default = ":DEFAULT:") ?"
         END
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         LBS.PER.BOX = VALUE
         TEMP.FVAR1 = VALUE
      END ELSE
         LBS.PER.BOX = EST.BIND.LBS.PER.BOX
      END
   END ELSE
      LBS.PER.BOX = TEMP.FVAR1
   END
20*
   IF QPTR = 1 AND ACTION # "R" THEN
      IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
      X=0;Y=23;TYP=14;MAXL=6;MINV=100;MAXV=99999;JUSTIFY="L"
      BEGIN CASE
         CASE EST.PROD.BASIS.WT<1,COMP,1>+0 > 0
            DEFAULT = EST.PROD.BASIS.WT<1,COMP,1>
         CASE TEMP.FVAR2 # ""
            DEFAULT = TEMP.FVAR2
         CASE 1
            DEFAULT = ""
      END CASE
      IF DEFAULT = "" THEN
         PMSG="Basis weight for carton calculation ?"
      END ELSE
         O.R = "O"
         PMSG = "Basis weight for carton calculation (Default = ":DEFAULT:") ?"
      END
      CALL EDIT.SUB1 (MAT EDIT.COM)
      IF VALUE = "END" THEN RETURN
      IF VALUE = "" THEN GOTO 20
      BASIS = VALUE / 100
      TEMP.FVAR2 = VALUE / 100
   END ELSE
      BASIS = TEMP.FVAR2
   END
*
*T27924 v
*  WIDTH = EST.BIND.TP.WIDTH / 10000
*  XXLENGTH = EST.BIND.TP.LENGTH / 10000
*  PCNT = COUNT(EST.BIND.FOLD.PGS,VM) + (EST.BIND.FOLD.PGS # "")
*  PAGES = 0
*  FOR P = 1 TO PCNT
*     PAGES = PAGES + EST.BIND.FOLD.PGS<1,P>
*  NEXT P
   WIDTH = EST.BIND.TP.WIDTH<1,COMP> / 10000
   XXLENGTH = EST.BIND.TP.LENGTH<1,COMP> / 10000
   PCNT = DCOUNT(EST.BIND.FOLD.PGS<1,COMP>,SM)
   PAGES = 0
   FOR P = 1 TO PCNT
      PAGES = PAGES + EST.BIND.FOLD.PGS<1,COMP,P>
   NEXT P
*T27924 ^
*
*---- MAIN PROCESSING
*
*T27924 v
*  PROD.QTY = EQTY
   PROD.QTY = COMPQTY
*T27924 ^
   BOOK.WEIGHT = INT(WIDTH * XXLENGTH * PAGES * BASIS / 95 + 0.5) / 10000
   IF BOOK.WEIGHT <= 0 THEN
      TEMP.QTY = 0
   END ELSE
      BEGIN CASE
*T27924 v
*        CASE EST.BIND.COVER.STYLE = "P"
         CASE EST.BIND.COVER.STYLE<1,COMP> = "P"
*T27924 ^
            BOOK.WEIGHT = BOOK.WEIGHT + 0.1250
*T27924 v
*        CASE EST.BIND.COVER.STYLE = "C"
         CASE EST.BIND.COVER.STYLE<1,COMP> = "C"
*T27924 ^
            LEN = INT(XXLENGTH + 0.5)
            LOCATE LEN IN PLEN<1>,1 BY "AR" SETTING WX ELSE NULL
            BOOK.WEIGHT = BOOK.WEIGHT + PWGT<1,WX>
      END CASE
      BOOKS.PER.BOX = INT(LBS.PER.BOX / BOOK.WEIGHT + 0.99)
      TEMP.QTY = INT(PROD.QTY / BOOKS.PER.BOX + 0.99)
   END
   IF TEMP.TYPE[1,1] = "M" THEN
      TEMP.FCTR = 1000
      TEMP.STD = ESTM.COST<1,MP> / 100
      TEMP.STD.TYPE = "$/":ESTM.UM<1,MP>
      TEMP.UM = ESTM.COST.UNIT<1,MP>
      TEMP.IND.1 = ESTM.FOH.PCT<1,MP>
      TEMP.MARKUP = ESTM.MARKUP<1,MP>
      TEMP.DESC = ESTM.FULL.DESC<1,MP>
   END
   TEMP.VSALE = "Y"
   RETURN
END
