***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.AUTO.PURGE
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/23/86
*
* DESCRIPTION
*
* This program is used to purge estimates from system based on criteria
* specified on the COMPANY file.
*
*T23278 markt 11/19/1998 * Add check for divisional security. 
*T25237 cm 06/16/2000 * Add process to purge Roll Label information.
*T25719 lanny 03/26/2001 * If Div 'ALL' entered it will not find a match.
**************************************************************************
*
*--------- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*T25237 v
*COPY>JES.CPYLIB>ESTIMATE.RES
*T25237 ^
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
***** T23278 v
*
*--- PROCREAD
*
      PROCREAD XX ELSE  ERRMSG="MUST BE RUN FROM PROC";GOSUB 91000;GOTO 99999
      DIV.CODE = XX<3>
***** T23278 ^
*
*--- SETUP SYSTEM ERROR MESSAGES
*
      SYS.TYPE = 1
      CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN ALL FIILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG="CANNOT OPEN COMPANY FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG="CANNOT OPEN CONTROL FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","PREFIX" TO PREFIX ELSE
         ERRMSG="CANNOT OPEN PREFIX FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","ESTIMATE" TO ESTIMATE ELSE
         ERRMSG="CANNOT OPEN ESTIMATE FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE
         ERRMSG="CANNOT OPEN ESTIMATE.DEPT FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","ESTIMATE.PURGE.BU" TO ESTIMATE.PURGE.BU ELSE
         ERRMSG="CANNOT OPEN ESTIMATE.PURGE.BU FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","CUST.EST.XREF" TO CUST.EST.XREF ELSE
         ERRMSG="CANNOT OPEN CUST.EST.XREF FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","PROSPECT.EST.XREF" TO PROSPECT.EST.XREF ELSE
         ERRMSG="CANNOT OPEN PROSPECT.EST.XREF FILE"
         GOSUB 91000
         STOP
      END
*T25237 v
      OPEN "","ESTIMATE.RES" TO ESTIMATE.RES ELSE
         ERRMSG = "CANNOT OPEN ESTIMATE.RES FILE"
         GOSUB 91000
         STOP
      END
      OPEN "","ESTIMATE.RL" TO ESTIMATE.RL ELSE
         ERRMSG = "CANNOT OPEN ESTIMATE.RL FILE"
         GOSUB 91000
         STOP
      END
*T25237 ^
*
*---- INITIALIZATION
*
      TODAY = DATE()
      CONO=""
      MAT COMP.REC=""
      CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         MAT COMP.REC = ""
      END
      IF CO.JES.PURGE.DAYS<1,1> = "" THEN
         DATE1 = TODAY - 365
      END ELSE
         DATE1 = TODAY - CO.JES.PURGE.DAYS<1,1>
      END
      IF CO.JES.PURGE.DAYS<1,2> = "" THEN
         DATE2 = TODAY - 30
      END ELSE
         DATE2 = TODAY - CO.JES.PURGE.DAYS<1,2>
      END
      IF CO.JES.PURGE.DAYS<1,3> = "" THEN
         DATE3 = TODAY - 30
      END ELSE
         DATE3 = TODAY - CO.JES.PURGE.DAYS<1,3>
      END
      IF CO.JES.PURGE.DAYS<1,4> = "" THEN
         DATE4 = TODAY - 180
      END ELSE
         DATE4 = TODAY - CO.JES.PURGE.DAYS<1,4>
      END
*
*---- MAIN PROCESSING
*
      SELECT ESTIMATE
100*
      RELEASE
      READNEXT ID ELSE
         GOTO 99999
      END
      IF ID[1,3] # CONO THEN GOTO 100
      IF INDEX(ID,"-",1) = 0 THEN GOTO 100
      EST.KEY = ID[4,99]
      MATREADU EST.REC FROM ESTIMATE, CONO:EST.KEY ELSE GOTO 100
      IF EST.MASTER # "" AND EST.MASTER # EST.KEY THEN GOTO 100
      IF EST.PURGE.TYPE # "A" THEN GOTO 100
*     IF EST.DIV # DIV.CODE THEN GOTO 100;* T23278
      IF DIV.CODE # 'ALL' AND EST.DIV[1,2] # DIV.CODE THEN GOTO 100;* T25719
      STATUS = EST.STATUS<1,1>
      STATUS.DATE = EST.STAT.DATE<1,1>
      BEGIN CASE
      CASE STATUS = "BOOKED" AND EST.JOB.PURGE.DATE = ""
         NULL
      CASE STATUS = "BOOKED" AND EST.JOB.PURGE.DATE < DATE1
         GOSUB 2000
      CASE STATUS = "LOST" AND STATUS.DATE < DATE2
         GOSUB 2000
      CASE STATUS = "CANCELLED" AND STATUS.DATE < DATE3
         GOSUB 2000
      CASE STATUS = "" AND EST.DATE.ENTERED < DATE4
         GOSUB 2000
      END CASE
      GOTO 100
*
*---- PURGE ESTIMATE
*
2000*
P_X  = 0 ; P_Y = 21 ; P_VALUE = "Processing Estimate - ":EST.KEY ; P_OPT = "CL"
CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      DEL.KEY = EST.KEY
      DEL.DEPT.COMP = EST.DEPT.COMP
      DEL.SUBS = EST.SUBS
      IF DEL.SUBS # "" THEN
         SAVE.KEY = DEL.KEY
         SAVE.DEPT.COMP = DEL.DEPT.COMP
         SC = COUNT(DEL.SUBS,VM) + (DEL.SUBS # "")
         FOR SP = 1 TO SC
            DEL.KEY = DEL.SUBS<1,SP>
            MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE GOTO 2090
            DEL.DEPT.COMP = EST.DEPT.COMP
            GOSUB 3000
2090     NEXT SP
         DEL.KEY = SAVE.KEY
         DEL.DEPT.COMP = SAVE.DEPT.COMP
         MATREADU EST.REC FROM ESTIMATE, CONO:DEL.KEY ELSE
            MAT EST.REC = ""
         END
      END
      GOSUB 3000
      RETURN
*
*---- DELETE ESTIMATE RECORDS
*
3000*
      DC = COUNT(DEL.DEPT.COMP,VM) + (DEL.DEPT.COMP # "")
      FOR DP = 1 TO DC
         DDKEY = DEL.KEY:"!":DEL.DEPT.COMP<1,DP>
         READU REC FROM ESTIMATE.DEPT, CONO:DDKEY ELSE
            RELEASE ESTIMATE.DEPT, CONO:DDKEY
            GOTO 3090
         END
         WRITE REC ON ESTIMATE.PURGE.BU, CONO:DDKEY
         DELETE ESTIMATE.DEPT, CONO:DDKEY
3090  NEXT DP
      MATWRITE EST.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY
      BEGIN CASE
      CASE EST.CUST = "P"
         CALL GEN.XREF.MAINT(CONO,DEL.KEY,EST.CUST.NAME,"",PROSPECT.EST.XREF,PREFIX)
      CASE 1
         CALL GEN.XREF.MAINT(CONO,DEL.KEY,EST.CUST,"",CUST.EST.XREF,PREFIX)
      END CASE
      DELETE ESTIMATE, CONO:DEL.KEY
*T25237 v
      MATREADU EST.RL.REC FROM ESTIMATE.RES, CONO:DEL.KEY THEN
         MATWRITE EST.RL.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY:'!RES'
         DELETE ESTIMATE.RES, CONO:DEL.KEY
      END
      READU OLD.REC FROM ESTIMATE.RL, CONO:DEL.KEY THEN
         WRITE OLD.REC ON ESTIMATE.PURGE.BU, CONO:DEL.KEY:'!RL'
         DELETE ESTIMATE.RL, CONO:DEL.KEY
      END
*T25236 ^
      RETURN
*
*---- ERROR ROUTINE
*
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
91000 *
      ERR.TYPE = 1;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
*---- END OF PROGRAM
*
99999*
*       PRINT @(-1):
      STOP
   END
