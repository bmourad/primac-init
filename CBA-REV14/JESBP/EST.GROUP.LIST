*COPY>CPYLIB>COM1
****************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM       - EST.GROUP.LIST
*
* SYSTEM        - JESBP
*
* BY            - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE          - 04/08/86
*
* DESCRIPTION   -
* This program produces an Estimate Group report.
*
*T25901 cmykleb 03/07/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*ENDDOC
****************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.GRP
*COPY>PMC.CPYLIB>DEPARTMENT
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','DEPARTMENT' TO DEPARTMENT ELSE ERRMSG='DEPARTMENT FILE MISSING';GOTO 93000
   OPEN '','ESTIMATE.GRP' TO ESTIMATE.GRP ELSE ERRMSG='ESTIMATE.GRP FILE MISSING';GOTO 93000
*
*---- GET PROC INFO
*
   PROCREAD BUFFER ELSE
      ERRMSG = "CANNOT READ FROM PROC"
      GOTO 93000
   END
*
*---- GET COMPANY NUMBER
*
   CONO=""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO='END' THEN GOTO 99999
*
*---- INITIALIZATION
*
   PG.NO=0
   LINE.CNT=0
   ERRMSG=""
   RPT.NO="XXXX"
   UNKNOWN=STR("?",30)
   PREV.DEPT=""
   SP = " "
   SP2 = "  "
   NODATA=1
*T25901 v
*  K = SPACE(INT((50 - LEN(CO.NAME)) / 2))
*  CONAME = K:CO.NAME:K
*  CONAME = CONAME "L#50"
*  HEAD1 = OCONV(DATE(),"D2/"):SPACE(33):CONAME:SPACE(32):"PAGE  "
*  HEAD2 = SPACE(44):"O P E R A T I O N   G R O U P   L I S T I N G"
*T26493 v
*  REPORT.NAME = "OPERATION GROUP LISTING"
*  REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = BUFFER<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26493 ^
   HEAD1 = "" ; HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HEAD1,HEAD2)
*T25901 ^
   HEAD3 = "DEPT : "
   HEAD4 = SPACE(100):"<- QTY -> <- FCT ->"
   HEAD5 = "GROUP           DESCRIPTION           TYPE CTR O/P/V       OPERATION / MATERIAL      OSP/SH  STD $  T PARAM O T PARAM O"
   HEAD6 = "------ ------------------------------ ---- --- ------ ------------------------------ ------ ------- - ----- - - ----- -"
   PRINTER ON
*
*---- MAIN PROCESSING
*
10*
   READNEXT ESTG.KEY ELSE 
      IF NODATA THEN GOTO 99999
      GOTO 15
   END
   MATREAD ESTG.REC FROM ESTIMATE.GRP,ESTG.KEY ELSE GOTO 10
   NODATA=0
   DEPT.NO = FIELD(ESTG.KEY,"!",1)[4,99]
   GRP.NO = FIELD(ESTG.KEY,"!",2)
   IF PREV.DEPT # DEPT.NO THEN
      GOSUB 20
      PREV.DEPT=DEPT.NO
   END
   TYP.CNT = COUNT(ESTG.TYPE,VM) + (ESTG.TYPE # "")
   GRP.NUM = GRP.NO "L#6"
   DESC = ESTG.DESC "L#30"
   TYPE = ESTG.TYPE<1,1> "L#2"
   CCTR = ESTG.CCTR<1,1> "L#3"
   OPER = ESTG.OPER<1,1> "L#6"
   OPER.DESC = ESTG.OPER.DESC<1,1> "L#30"
   OSP.CODE = ESTG.CAT.SP.CODE<1,1> "L#6"
   OSP.COST = OCONV(ESTG.CAT.SP.COST<1,1>,"MD2") "R#7"
   QCALC = ESTG.OPER.QCALC<1,1> "L#1"
   QPARAM = ESTG.OPER.QPARAM<1,1> "L#5"
   QOR = ESTG.OPER.QOR<1,1> "L#1"
   FCALC = ESTG.OPER.FCALC<1,1> "L#1"
   FPARAM = ESTG.OPER.FPARAM<1,1> "L#5"
   F.OR = ESTG.OPER.FOR<1,1> "L#1"
   GOSUB 30
   GOTO 10
15*
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
   PRINT CHAR(12)
*    PRINT
   PG.NO=PG.NO + 1
   MATREAD DEPT.REC FROM DEPARTMENT,CONO:DEPT.NO ELSE DEPT.DESC=UNKNOWN
   PRINT HEAD1:PG.NO
   PRINT HEAD2
   PRINT HEAD3:DEPT.NO:SP:DEPT.DESC
   PRINT HEAD4
   PRINT HEAD5
   PRINT HEAD6
   LINE.CNT = 6
   RETURN
*
*---- PRINT THE VALUES
*
30*
   PRINT
   PRINT GRP.NUM:SP:DESC:SP2:TYPE:SP2:CCTR:SP:OPER:SP:OPER.DESC:SP:OSP.CODE:SP:OSP.COST:SP:QCALC:SP:QPARAM:SP:QOR:SP:FCALC:SP:FPARAM:SP:F.OR
   LINE.CNT = LINE.CNT + 2
   IF LINE.CNT >= 55 THEN GOSUB 20
   IF TYP.CNT > 1 THEN
      FOR QQ = 2 TO TYP.CNT
         TYPE = ESTG.TYPE<1,QQ> "L#2"
         CCTR = ESTG.CCTR<1,QQ> "L#3"
         OPER = ESTG.OPER<1,QQ> "L#6"
         OPER.DESC = ESTG.OPER.DESC<1,QQ> "L#30"
         OSP.CODE = ESTG.CAT.SP.CODE<1,QQ> "L#6"
         OSP.COST = OCONV(ESTG.CAT.SP.COST<1,QQ>,"MD2") "R#7"
         QCALC = ESTG.OPER.QCALC<1,QQ> "L#1"
         QPARAM = ESTG.OPER.QPARAM<1,QQ> "L#5"
         QOR = ESTG.OPER.QOR<1,QQ> "L#1"
         FCALC = ESTG.OPER.FCALC<1,QQ> "L#1"
         FPARAM = ESTG.OPER.FPARAM<1,QQ> "L#5"
         F.OR = ESTG.OPER.FOR<1,QQ> "L#1"
         PRINT SPACE(39):TYPE:SP2:CCTR:SP:OPER:SP:OPER.DESC:SP:OSP.CODE:SP:OSP.COST:SP:QCALC:SP:QPARAM:SP:QOR:SP:FCALC:SP:FPARAM:SP:F.OR
         LINE.CNT = LINE.CNT + 1
         IF LINE.CNT >= 55 THEN GOSUB 20
      NEXT QQ
   END
   RETURN
*
*---- CALLS FOR SYSCOM
*
93000*
   PRINTER OFF
   ERR.TYPE=3
   CALL SI_SYSCOM(MAT SYSCOM.REC)
99999*
END
