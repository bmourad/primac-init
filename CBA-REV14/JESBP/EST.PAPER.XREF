      SUBROUTINE EST.PAPER.XREF (CONO, ACTION, PTYPE, PGROUP, SELECTION)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.PAPER.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/27/86
*
* DESCRIPTION
*
* This subroutine displays all paper materials for specified type and
* group for selection purposes.
*
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      SELECTION = ""
      SCREEN DEFINE;EST.PAPER.XREF
      SCREEN CLEAR;EST.PAPER.XREF;D
      SCREEN FORMAT
      SCREEN COUNT;EST.PAPER.XREF;5
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
*
*---- MAIN PROCESSING
*
100*
      CURR.REF.NO = ""
      LINE.CNT = COUNT(ESTM.PROD,VM) + (ESTM.PROD # "")
      S$DATA(1)<S$SCR> = DATE()
      S$DATA(2)<S$SCR> = PTYPE
      S$DATA(3)<S$SCR> = PGROUP
      S$DATA(6)<S$SCR> = ESTM.PROD
      S$DATA(7)<S$SCR> = ESTM.FULL.DESC
      SCREEN DISPLAY;EST.PAPER.XREF;ALL
      REF.NO = 1
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
500*
      SCREEN FIELD;EST.PAPER.XREF;11
      SCREEN INPUT;EST.PAPER.XREF;11
      OPT = S$VALUE
      BEGIN CASE
         CASE OPT # "" AND NUM(OPT)
            IF OPT < CURR.REF.NO OR OPT > LINE.CNT OR OPT > (CURR.REF.NO + LINE.COUNT - 1) THEN
               ERRMSG = "** OUT OF RANGE **"
               GOSUB 90000
            END ELSE
               SELECTION = ESTM.PROD<1,OPT>
               GOTO 99999
            END
         CASE OPT = "E" OR OPT = "END"
            GOTO 99999
         CASE OPT = "S" OR OPT = "SF"
            REF.NO = CURR.REF.NO + LINE.COUNT
            IF REF.NO > LINE.CNT THEN REF.NO = 1
            GOSUB 50000
         CASE OPT = "ST"
            REF.NO = 1
            GOSUB 50000
         CASE OPT = "SB"
            REF.NO = LINE.CNT
            GOSUB 50000
         CASE OPT = "SR"
            REF.NO = REF.NO - LINE.COUNT
            IF REF.NO < 1 THEN REF.NO = 1
            GOSUB 50000
      END CASE
      GOTO 500
*
*---- SCROLL MULTI-LINE DATA
*
50000*
      START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = COUNT(S$DATA(6)<S$SCR>,VM) + (S$DATA(6)<S$SCR> # "")
      SCREEN MULTI;EST.PAPER.XREF;C;5;6;7
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      SCREEN CLEAR;EST.PAPER.XREF;D
      SCREEN CLOSE
      RETURN
   END
