*********************************************************************
* REVISION    - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - LMFPBP FORMULA.PRINT
* PROGRAM     - QUOTE.LETTER
* BY          - BOB POWELL, VERCOM SOFTWARE, INC.
* DATE        - 06/08/93
* DESCRIPTION -
* PRINTS THE ESTIMATE QUOTE LETTER PER LP&D SPEC DATE 05/12/93
* NOTE SPECIAL ESCAPE CODES USED FOR USE ON A HEWLETT PACKARD LAZER JET II PRINTER
*********************************************************************
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RL
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATOR
*COPY>PMC.CPYLIB>SALESMAN
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
**** SET UP FOR SYSCOM
      SYS.TYPE = 1; CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
      OPEN '','ESTIMATE' TO ESTIMATE ELSE
         ERRMSG = 'ESTIMATE FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','ESTIMATE.RL' TO ESTIMATE.RL ELSE
         ERRMSG = 'ESTIMATE.RL FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','ESTIMATE.DEPT' TO ESTIMATE.DEPT ELSE
         ERRMSG = 'ESTIMATE.DEPT FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','ESTIMATOR' TO ESTIMATOR ELSE
         ERRMSG = 'ESTIMATOR FILE IS MISSING'
         GOTO 93000
      END
      OPEN '','SALESMAN' TO SALESMAN ELSE
         ERRMSG = 'SALESMAN FILE IS MISSING'
         GOTO 93000
      END
*
*---- INITIALIZE
*
      RESET = CHAR(27):CHAR(69)
      PAGE.LEN = CHAR(27):CHAR(38):CHAR(108):CHAR(54):CHAR(48):CHAR(80)
      COUR = CHAR(27):CHAR(40):CHAR(115):CHAR(49):CHAR(48):CHAR(72)
      * 80 COLUMN MODE, 10 CPI
      LINE.PTR = CHAR(27):CHAR(40):CHAR(115):CHAR(49):CHAR(54):CHAR(46):CHAR(54):CHAR(54):CHAR(72)
      * 132 COLUMN MODE, 16 CPI
      COUR.12 = CHAR(27):CHAR(40):CHAR(115):CHAR(49):CHAR(50):CHAR(72)
      * COURIER FONT , 12 CPI
      * NOTE CAN'T USE THIS FONT ON HP LAZERJET II
      BOLD.ON = CHAR(27):CHAR(40):CHAR(115):CHAR(51):CHAR(66)
      BOLD.OFF = CHAR(27):CHAR(40):CHAR(115):CHAR(48):CHAR(66) ; * MEDIUM STRIKE
      UND.ON = CHAR(27):CHAR(38):CHAR(100):CHAR(68) ; * UNDERLINE ON
      UND.OFF = CHAR(27):CHAR(38):CHAR(100):CHAR(64) ; * UNDERLINE OFF
      EJECT.PAGE = CHAR(27):CHAR(38):CHAR(108):CHAR(48):CHAR(72)
      FORM.FEED = CHAR(12)
      LINE.FEED = CHAR(10)
      LFT.MRG = SPACE(10) ; * SET LEFT MARGIN
      DASHES = STR('-',77)
      LINE.CNT = 0
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      SP4 = SPACE(4)
      HD1 = '' ; HD2 = ''
      LINE = ''
      DIM ADD.CHGS(10)
      STOCK = ''
      PRINTER ON
      LINE = RESET
      PRINT LINE
*
*---- HEADINGS
*
*
*---- MAIN PROCESSING
*
100 *
      READNEXT EST.ID ELSE GOTO 99999
      CONO = EST.ID[1,3]
      LINE = COUR
      * LINE = RESET:LINE.PTR
      PRINT LINE
      * THIS LINE SELECTS THE FONT TO USE
      LINE = ''
      FOR I = 1 TO 7 ;* 2 INCH TOP MARGIN
         PRINT
      NEXT I
      MATREAD EST.REC FROM ESTIMATE, EST.ID ELSE GOTO 100
      DATE = EST.DATE.ENTERED
      DATE.O = DATE 'DMA':" ":DATE 'DD':", ":DATE 'DY'
      LINE = LFT.MRG:SPACE((66-LEN(DATE.O))/2):DATE.O
      GOSUB 10000
      GOSUB 10100
      LINE = LFT.MRG:BOLD.ON:"ATTN: ":BOLD.OFF:SPACE(28):BOLD.ON:"Quote #    ":BOLD.OFF:EST.ID[4,8] 'L#8'
      GOSUB 10000
      LINE = LFT.MRG:SPACE(2):EST.CUST.ATTN 'L#30' :SP2:BOLD.ON:"Customer # ":BOLD.OFF:EST.CUST 'L#8'
      GOSUB 10000
      MATREAD SALESMAN.REC FROM SALESMAN, CONO:EST.SALESMAN ELSE
         MAT SALESMAN.REC = ''
      END
      LINE = LFT.MRG:SPACE(2):EST.CUST.NAME 'L#30' :SP2:BOLD.ON:"Sales Rep : ":BOLD.OFF
      GOSUB 10000
      LINE = LFT.MRG:SP2:EST.CUST.ADDR1 'L#30':SPACE(4):SALS.NAME 'L#30'
      GOSUB 10000
      LINE = LFT.MRG:SP2:EST.CUST.ADDR2 'L#30'
      GOSUB 10000
500 *
      IF EST.CUST.ADDR3 = '' THEN
         GOTO 600
      END ELSE
         LINE = LFT.MRG:SPACE(2):EST.CUST.ADDR3 'L#30'
         GOSUB 10000
      END
600 *
      IF EST.CUST.ADDR4 = '' THEN
         GOTO 700
      END ELSE
         LINE = LFT.MRG:SPACE(2):EST.CUST.ADDR4 'L#30'
         GOSUB 10000
      END
700 *
      GOSUB 10100
      LINE = LFT.MRG:SPACE(28):BOLD.ON:"QUOTATION":BOLD.OFF
      GOSUB 10000
      LINE = LFT.MRG:BOLD.ON:UND.ON:SPACE(66):UND.OFF:BOLD.OFF
      GOSUB 10000
      LINE = LFT.MRG:BOLD.ON:"We are pleased to submit prices based on the specifications below.":BOLD.OFF
      GOSUB 10000
      LINE = LFT.MRG:BOLD.ON:UND.ON:"This quotation is subject to change without notice after 30 days. ":UND.OFF:BOLD.OFF
      GOSUB 10000
      GOSUB 10100
      LINE = LFT.MRG:BOLD.ON:"Description:   ":BOLD.OFF:EST.COMMENTS<1,1> 'L#50'
      GOSUB 10000
      MATREAD EST.RL.REC FROM ESTIMATE.RL, EST.ID ELSE
         MAT EST.RL.REC = ''
      END
      MAT ADD.CHGS = ''
      STOCK = ''
      PTR = 0
      PTR2 = 0
      DEPT.CNT = DCOUNT(EST.DEPT,VM)
      FOR I = 1 TO DEPT.CNT
         COMP.CNT = EST.COMPONENT.CNT
         FOR J = 1 TO COMP.CNT
               EST.DEPT.KEY = EST.ID:"!":I:"!":J:"!":EST.QTY<1,1>
               DEPT.COMP.QTY.KEY = I:"!":J:"!":EST.QTY<1,1>
               LOCATE DEPT.COMP.QTY.KEY IN EST.DEPT.COMP<1>,1 SETTING FND ELSE FND = 0
               IF FND = 0 THEN GOTO 800
               MATREAD ESTD.REC FROM ESTIMATE.DEPT, EST.DEPT.KEY ELSE
                  MAT ESTD.REC = ''
                  GOTO 800
               END
               TYP.CNT = DCOUNT(ESTD.TYPE,VM)
               FOR L = 1 TO TYP.CNT
                  BEGIN CASE
                     CASE ESTD.TYPE<1,L> = "MO"
                        PTR = PTR + 1
                        IF PTR < 11 THEN ADD.CHGS(PTR) = ESTD.DESC<1,L>
                  END CASE
               NEXT L
800 *
         NEXT J
      NEXT I
      FOR I = 1 TO 3
         IF EST.PROD.INV.ID<1,1,I> # "" THEN
            STOCK<I,1> = EST.PROD.INV.ID<1,1,I>
         END ELSE
            STOCK<I,1> = EST.PROD.OS.PROD<1,1,I>
         END
         STOCK<I,2> = EST.PROD.OS.DESC<1,1,I>
      NEXT I
      LINE = LFT.MRG:BOLD.ON:"Stock #1  ":BOLD.OFF:STOCK<1,1> 'L#15':SP1:STOCK<1,2> 'L#23':SP1:BOLD.ON:"Width    ":BOLD.OFF
      LINE = LINE:OCONV(EST.RL.DIM.AC,"MD4") 'L#7'
      GOSUB 10000
      LINE = LFT.MRG:BOLD.ON:"Stock #2  ":BOLD.OFF:STOCK<2,1> 'L#15':SP1:STOCK<2,2> 'L#23':SP1:BOLD.ON:"Length   ":BOLD.OFF
      LINE = LINE:OCONV(EST.RL.DIM.AR,"MD4") 'L#7'
      GOSUB 10000
      LINE = LFT.MRG:BOLD.ON:"Stock #3  ":BOLD.OFF:STOCK<3,1> 'L#15':SP1:STOCK<3,2> 'L#23':SP1:BOLD.ON:"Colors     ":BOLD.OFF
      IF EST.PROD.COLORS.2 = "" THEN EST.PROD.COLORS.2 = 0
      LINE = LINE:SP1:OCONV(EST.PROD.COLORS.1,'MD0') 'L#1':"/":OCONV(EST.PROD.COLORS.2,'MD0') 'L#1'
      GOSUB 10000
      GOSUB 10100
900 *
      LINE = LFT.MRG:BOLD.ON:UND.ON:SP1:"Quantity":SP1:UND.OFF:BOLD.OFF:SPACE(5):BOLD.ON:UND.ON:"Price/M":UND.OFF:BOLD.OFF:SPACE(13):BOLD.ON:UND.ON:"Additional Charges":UND.OFF:BOLD.OFF
      GOSUB 10000
1100 * ; CALCULATE PRICE PER 1000 FOR QTYS
      TOTAL.TSALE = ''
      SALE.PER.1000 = ''
      QC = DCOUNT(EST.QTY,VM)
      DC = DCOUNT(EST.DEPT.COMP,VM)
      FOR DP = 1 TO DC
         CNUM = FIELD(EST.DEPT.COMP<1,DP>,"!",2)
         EQTY = FIELD(EST.DEPT.COMP<1,DP>,"!",3)
         LOCATE EQTY IN EST.QTY<1>,1 SETTING EP THEN
            TOTAL.TSALE<1,EP>=TOTAL.TSALE<1,EP>+EST.DEPT.COMP.TSALE<1,DP>
         END
      NEXT DP
      FOR Q = 1 TO QC
         QTY = EST.QTY<1,Q>
         SALE.PER.1000<1,Q> = INT(TOTAL.TSALE<1,Q>/(QTY/1000)+0.5)
      NEXT Q
      LINE = LFT.MRG:OCONV(EST.QTY<1,1>,"MD0,") 'R#10'
      LINE = LINE:SPACE(2):OCONV(SALE.PER.1000<1,1>,"MD2Z,") 'R#10'
      LINE = LINE:SPACE(13):ADD.CHGS(1) 'L#30'
      GOSUB 10000
      LINE = LFT.MRG:OCONV(EST.QTY<1,2>,"MD0,") 'R#10'
      LINE = LINE:SPACE(2):OCONV(SALE.PER.1000<1,2>,"MD2Z,") 'R#10'
      LINE = LINE:SPACE(13):ADD.CHGS(2) 'L#30'
      GOSUB 10000
      LINE = LFT.MRG:OCONV(EST.QTY<1,3>,"MD0,") 'R#10'
      LINE = LINE:SPACE(2):OCONV(SALE.PER.1000<1,3>,"MD2Z,") 'R#10'
      LINE = LINE:SPACE(13):ADD.CHGS(3) 'L#30'
      GOSUB 10000
      FOR I = 4 TO 10
         LINE = SPACE(45):ADD.CHGS(I) 'L#30'
         GOSUB 10000
      NEXT I
      GOSUB 10100
      LINE = LFT.MRG:BOLD.ON:"Special Instructions:":BOLD.OFF
      GOSUB 10000
      GOSUB 10100
      FOR I = 2 TO 6
         LINE = LFT.MRG:EST.COMMENTS<1,I> 'L#65'
         GOSUB 10000
      NEXT I
      GOSUB 10100
      LINE = LINE.PTR:LFT.MRG:SPACE(10):BOLD.ON:"Artwork, type, plates, dies, negatives, positives unless supplied by the customer, will remain"
      GOSUB 10000
      LINE = LFT.MRG:SPACE(10):"the exclusive property of Label Products & Design, Inc. unless otherwise agreed in writing.":BOLD.OFF
      GOSUB 10000
      GOSUB 10100
      LINE = LFT.MRG:SPACE(10):BOLD.ON:UND.ON:SPACE(94):UND.OFF:BOLD.OFF
      GOSUB 10000
      MATREAD ESTIMATOR.REC FROM ESTIMATOR, EST.ID[1,3]:EST.ESTIMATOR ELSE
         MAT ESTIMATOR.REC = ''
      END
      LINE = LFT.MRG:SPACE(10):BOLD.ON:ESTMTR.NAME 'L#30':SPACE(56):"Date":BOLD.OFF
      GOSUB 10000
      GOSUB 10100
      LINE = LFT.MRG:SPACE(19):BOLD.ON:"**TERMS: NET 30 - F.O.B. LABEL PRODUCTS & DESIGN, INC. Green Bay, WI 54302 **":BOLD.OFF
      GOSUB 10000
      LINE = FORM.FEED
      PRINT LINE
      PRINTER OFF
      PRINTER ON
      GOTO 100
10000 *
      PRINT LINE
      LINE = ''
      RETURN
*
10100 *
      PRINT
      RETURN
*
93000 ERR.TYPE = 3 ; CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 
      LINE = COUR
      PRINT LINE
      PRINTER OFF
      PRINTER CLOSE
   END
