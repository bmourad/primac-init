*********************************************************************
*
* Copyright 1994 by Vercom Software Inc.
*
* REVISION - [10.0]
*
* PROGRAM  - EST.RES.SHAPE.MAINT
*
* AUTHOR   - AMENDOLA, NASTech, Inc.
*
* DATE     - 07/06/94
*
* DESCRIPTION
*
* This program is used to maintain the Die Shape code file.
*
*C36987 cm 08/23/2000 * Screen is not clearing correctly when calling
*                       X-ref and then returning to maintenance screen.
*T26126 adelgado 03/01/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.RES.SHAPE
*COPY>CPYLIB>GEN.XREF.SUB
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- PRE-INITIALIZATION
*
  PROCREAD PARAM ELSE PARAM = ""
  ACTION = PARAM<1>
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.RES.SHAPE" TO ESTIMATE.RES.SHAPE ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.RES.SHAPE FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","XREF.DATA" TO XREF.DATA ELSE
    ERRMSG = "CANNOT OPEN XREF.DATA FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RES.SHAPE.MAINT
  SCREEN FORMAT
  CODE = ""
  MAT GEN.XREF.REC = ""
  GXR.CO = CONO
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  SCREEN CLEAR
  CODE = "" ; * T26556
110 *
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  S$DATA(2)<S$SCR> = CODE
  SCREEN FIELD;;2
  SCREEN INPUT;;2;GOTO 99999
  IF S$VALUE="???" THEN
*  FLD=2;FREF=1;XTYPE="ESTIMATE.RES.SHAPE";XFILE=ESTIMATE.RES.SHAPE;XFLD=1;GOSUB 70000
    FLD=2
    FREF=1
    GXR.FILE=ESTIMATE.RES.SHAPE
    GXR.SORT.FILE = "ESTIMATE.RES.SHAPE"
    GXR.NAME = 'GEN.CODE1'
    CALL GEN.XREF.SUB(MAT GEN.XREF.REC, PREFIX, XREF.DATA)
    S$VALUE = GXR.ID
*C36987 v
    SCREEN FORMAT
*C36987 ^
    S$DATA(FLD)<S$SCR,FREF>=GXR.ID
    IF GXR.ID="" THEN GOTO 110
  END
  NEW.REC = 0
  IF ACTION = "M" THEN
    IF S$VALUE = "B" THEN
      ERRMSG = "'B' is reserved for Butt Cut"; GOSUB 90000
      GOTO 110
    END
    * T26126 v
    MATREADU ESTRL.SHAPE.REC FROM ESTIMATE.RES.SHAPE, CONO:S$VALUE LOCKED
      ERRMSG = 'SHAPE record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 90000;GOTO 110 
    END ELSE
    * T26126 ^
      MAT ESTRL.SHAPE.REC = ""
      NEW.REC = 1
    END
  END ELSE
    MATREAD ESTRL.SHAPE.REC FROM ESTIMATE.RES.SHAPE, CONO:S$VALUE ELSE
      ERRMSG = "Invalid Item. Try again! "
      GOSUB 90000
      GOTO 100
    END
  END
  CODE = S$VALUE
120 *
  IF NEW.REC THEN
    FOR REF = 1 TO 1
      ON REF GOSUB 1010
      IF S$VALUE = "END" THEN GOTO 100
    NEXT REF
  END ELSE
    GOSUB 80000
  END
*
*---- GET OPERATOR REPLY
*
500 *
  BEGIN CASE
    CASE ACTION = "M"
      SCREEN FIELD;;21
      SCREEN INPUT;;21
    CASE 1
      SCREEN FIELD;;23
      SCREEN INPUT;;23
  END CASE
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      IF ACTION = "M" THEN
        MISMATCH = 0
        READ VREC FROM ESTIMATE.RES.SHAPE, CONO:CODE ELSE VREC = ""
        FOR VPTR = 1 TO ESTRL.SHAPE.SIZE UNTIL MISMATCH
          IF ESTRL.SHAPE.REC(VPTR) # VREC<VPTR> THEN MISMATCH = 1
        NEXT VPTR
        IF MISMATCH THEN
          SCREEN FIELD;;25
          SCREEN INPUT;;25
          IF S$VALUE # "Y" THEN GOTO 500
        END
      END
      GOTO 100
    CASE NUM(OPT) AND OPT # ""
      ON OPT GOSUB 1010
    CASE OPT = "F"
      MATWRITE ESTRL.SHAPE.REC ON ESTIMATE.RES.SHAPE, CONO:CODE
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET FIELD 1
*
1010 *
  SCREEN FIELD;;3
  SCREEN INPUT;;3;RETURN
  ESTRL.SHAPE.DESC = S$VALUE
  RETURN
*
*---- CODE SELECTION
*
70000 *
* CALL GEN.CODE.XREF (CONO,XTYPE,XFILE,XFLD,XCODE)
* S$VALUE=XCODE
* S$DATA(FLD)<S$SCR,FREF>=XCODE
*
*---- REFORMAT SCREEN
*
70100 *
  SCREEN FORMAT
  GOSUB 80050
  RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
  S$DATA(3)<S$SCR> = ESTRL.SHAPE.DESC
80050 *
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
* PRINT @(0,23):@(-4):ERRMSG:
* INPUT REPLY:
* PRINT @(0,23):@(-4):
* RETURN
*
*---- END OF PROGRAM
*
99999 *
* PRINT @(-1)
END
