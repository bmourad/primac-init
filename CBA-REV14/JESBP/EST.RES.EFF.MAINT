*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RL.EFF.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 11/25/86
*
* MOD BY   - KERRY WILSON
* MOD DATE - 03/14/90
* MOD TASK - 14166
* MOD REAS - ADD INITIAL AND SUBSEQUENT MAKEREADY
*
* DESCRIPTION
*
* This program is used to maintain the equipment efficiency table.
*
* MODIFIED - Graham Jarvis, Pacrim Software Pty Ltd
* T26138 cm 09/20/2001 * Expand qty from from 8 to 9 digits.
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.RUN.EFF
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","COST.CNTR" TO COST.CNTR ELSE
    ERRMSG = "CANNOT OPEN COST.CNTR FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN '','EQUIPMENT' TO EQUIPMENT ELSE
    ERRMSG = "CANNOT OPEN EQUIPMENT FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.RES.RUN.EFF" TO ESTIMATE.RES.RUN.EFF ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.RES.RUN.EFF FILE"
    GOSUB 90000
    STOP
  END
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RES.EFF.MAINT
  SCREEN FORMAT
  SCREEN COUNT;;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  SCREEN CLEAR
110 *
  DUP.FLG = 0
  NEW.REC=1
  DONE=0
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  SCREEN FIELD;;2
  SCREEN INPUT;;2
  BEGIN CASE
    CASE S$VALUE = 'D'
      SCREEN FIELD;;23
      SCREEN INPUT;;23
      IF S$VALUE = "" OR S$VALUE = "END" THEN GOTO 100
      MATREAD REFF.REC FROM ESTIMATE.RES.RUN.EFF, CONO:S$VALUE ELSE
        ERRMSG = "Cannot locate run efficiency table. Try again! "
        GOSUB 90000
        GOTO 100
      END
120 *
      SCREEN FIELD;;24
      SCREEN INPUT;;24
      IF S$VALUE = "" OR S$VALUE = "END" THEN GOTO 100
      DTBL = S$VALUE
      MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:DTBL[1,3] ELSE
        ERRMSG = "Invalid equipment ID. Try again !"
        GOSUB 90000
        GOTO 120
      END
      IF EQP.TYPE # 40 THEN
        ERRMSG = "Equipment is not a Roll Label Press"
        GOSUB 90000
        GOTO 120
      END
* MATWRITE REFF.REC ON ESTIMATE.RES.RUN.EFF,CONO:DTBL
* GOTO 100
      DUP.FLG = 1
      NEW.REC = 0
      S$DATA(2)<S$SCR> = DTBL
      SCREEN DISPLAY;;2
      MATREAD CCTR.REC FROM COST.CNTR, CONO:DTBL ELSE
        MAT CCTR.REC = ""
        CCTR.DESC = "???????????????"
      END
      S$DATA(3)<S$SCR> = CCTR.DESC
      SCREEN DISPLAY;;3
    CASE 1
      IF S$VALUE = "END" THEN GOTO 99999
      EQP.KEY = S$VALUE
      MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:EQP.KEY ELSE
        ERRMSG = "Invalid equipment ID. Try again !"
        GOSUB 90000
        GOTO 100
      END
      IF EQP.TYPE # 40 THEN
        ERRMSG = "Equipment is not a Roll Label Press"
        GOSUB 90000
        GOTO 100
      END
      MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
        MAT CCTR.REC = ""
        CCTR.DESC = "???????????????"
      END
      S$DATA(3)<S$SCR> = CCTR.DESC
      SCREEN DISPLAY;;3
      IF EQP.TYPE = "1" OR EQP.TYPE = "40" THEN
        FOR REF = 1 TO 3
          ON REF GOSUB 1100,1300,1400
          IF S$VALUE = "END" THEN GOTO 100
        NEXT REF
        TBL.KEY = EQP.KEY:"*":DIE.TYPE:"*":PAPER.WEIGHT:"*":INK.COVER
      END ELSE
        TBL.KEY = EQP.KEY
      END
      NEW.REC = 0
      * T26126 v
      MATREADU REFF.REC FROM ESTIMATE.RES.RUN.EFF, CONO:TBL.KEY LOCKED
        ERRMSG = 'PRESS RUN EFFICIENCY TABLE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 90000;GOTO 100 
      END ELSE
      * T26126 ^
        MAT REFF.REC = ""
        NEW.REC = 1
      END
  END CASE
  IF NEW.REC THEN
    GOSUB 1600
    IF S$VALUE = "END" THEN GOTO 100
    CNT = 0
    REF.NO = 0
    CURR.REF.NO = ""
    OPT = "A"
    DONE = 0
    FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
      GOSUB 50000
      GOSUB 10100
      IF S$VALUE = "END" THEN
        IF CNT = 0 THEN GOTO 100
        DONE = 1
        GOSUB 600
      END ELSE
        CNT = CNT + 1
      END
    NEXT REF.NO
    REF.NO = CNT
    CURR.REF.NO = ""
    GOSUB 50000
  END ELSE
    CNT = DCOUNT(REFF.QTY,VM)
    S$DATA(9)<S$SCR> = REFF.EXTR
    SCREEN DISPLAY;;9
    S$DATA(12)<S$SCR> = REFF.QTY
    S$DATA(13)<S$SCR> = REFF.FCTR
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 50000
    IF DUP.FLG THEN
      DONE = 0
      FOR XX =1 TO 3 UNTIL DONE
        ON XX GOSUB 1100,1300,1400
        IF S$VALUE = 'END' THEN DONE = 1
      NEXT XX
      IF DONE THEN GO 100
      FND = 1
      READU DUMMY FROM ESTIMATE.RES.RUN.EFF,CONO:DTBL:"*":DIE.TYPE:"*":PAPER.WEIGHT:"*":INK.COVER ELSE FND=0
      IF FND THEN
        ERRMSG = "Run efficiency table already present. Try again! "
        GOSUB 90000
        SCREEN CLEAR
        GOTO 100
      END
      TBL.KEY = DTBL:"*":DIE.TYPE:"*":PAPER.WEIGHT:"*":INK.COVER
      DUP.FLG = 0
    END
  END
*
*---- GET OPERATOR REPLY
*
500 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      GOTO 100
    CASE OPT = "A" AND CNT < 99
      DONE = 0
      FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10100
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 600
        END ELSE
          CNT = CNT + 1
        END
      NEXT REF.NO
      REF.NO = CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10100
      END
    CASE OPT = "I" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        CNT = CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10100
        IF S$VALUE = "END" THEN
          GOSUB 600
          CNT = CNT - 1
        END
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "D" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 600
        CNT = CNT - 1
        IF REF.NO > CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPT = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPT = 'SB'
      REF.NO = CNT
      GOSUB 50000
    * T25978 ^
    CASE NUM(OPT)
      GOSUB 1600
      GOTO 500
    CASE OPT='F'
      MATWRITE REFF.REC ON ESTIMATE.RES.RUN.EFF,CONO:TBL.KEY
      GOTO 100
  END CASE
  GOTO 500
*
*---- DELETE MULTI-LINE DATA
*
600 *
  S$DATA(12) = DELETE(S$DATA(12),S$SCR,REF.NO,0)
  S$DATA(13) = DELETE(S$DATA(13),S$SCR,REF.NO,0)
  REFF.QTY = DELETE(REFF.QTY,1,REF.NO,0)
  REFF.FCTR = DELETE(REFF.FCTR,1,REF.NO,0)
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
700 *
  S$DATA(12) = INSERT(S$DATA(12),S$SCR,REF.NO,0,"")
  S$DATA(13) = INSERT(S$DATA(13),S$SCR,REF.NO,0,"")
  REFF.QTY = INSERT(REFF.QTY,1,REF.NO,0,"")
  REFF.FCTR = INSERT(REFF.FCTR,1,REF.NO,0,"")
  RETURN
*
*---- GET INK COLORS
*
1100 *
  IF EQP.RL.PRESS.DIE.TYPE = "B" THEN
    SCREEN FIELD;;4
    SCREEN INPUT;;4;RETURN
    DIE.TYPE = S$VALUE
  END ELSE
    DIE.TYPE = EQP.RL.PRESS.DIE.TYPE
    S$DATA(4)<S$SCR> = DIE.TYPE
    SCREEN DISPLAY;;4
  END
  RETURN
*
*---- GET INK COVERAGE
*
*1200 *
*      SCREEN FIELD;;5
*      SCREEN INPUT;;5;RETURN
*      INK.COVER = S$VALUE
*      RETURN
*
*---- GET PAPER WEIGHT
*
1300 *
  SCREEN FIELD;;6
  SCREEN INPUT;;6;RETURN
  PAPER.WEIGHT = S$VALUE
  RETURN
*
*---- GET INITIAL MAKEREADY
*
1400 *
  SCREEN FIELD;;25
  SCREEN INPUT;;25;RETURN
  INK.COVER = S$VALUE
  RETURN
*
*---- GET SUBSEQUENT MAKEREADY
*
*1500 *
*      SCREEN FIELD;;8
*      SCREEN INPUT;;8;RETURN
*      REFF.MKRDY<1,2> = S$VALUE
*      RETURN
*
*---- GET EXTRAPOLATE FLAG
*
1600 *
  SCREEN FIELD;;9
  SCREEN INPUT;;9;RETURN
  REFF.EXTR = S$VALUE
  RETURN
*
*---- GET MULTI-LINE DATA
*
10100 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;11
  S$VAL = REF.NO
  SCREEN FIELD;;12
  IF REF.NO = 1 THEN S$MINV = 1 ELSE S$MINV = REFF.QTY<1,REF.NO-1>+1
*T26138 v
*  IF REF.NO >= CNT THEN S$MAXV = 99999999 ELSE S$MAXV = REFF.QTY<1,REF.NO+1>-1
  IF REF.NO >= CNT THEN S$MAXV = 999999999 ELSE S$MAXV = REFF.QTY<1,REF.NO+1>-1
*T26138 ^
  SCREEN INPUT;;12;RETURN
  REFF.QTY<1,REF.NO> = S$VALUE
  S$VAL = REF.NO
  SCREEN FIELD;;13
  SCREEN INPUT;;13;RETURN
  REFF.FCTR<1,REF.NO> = S$VALUE
  RETURN
*
*---- GET LINE REFERENCE NUMBER
*
30000 *
  SCREEN FIELD;;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > CNT THEN S$MAXV = CNT
  SCREEN INPUT;;22
  RETURN
*
*---- DISPLAY MULTI-LINE DATA
*
50000 *
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = COUNT(S$DATA(12)<S$SCR>,VM) + (S$DATA(12)<S$SCR> # "")
  SCREEN MULTI;;C;11;12;13
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999 *
END
