*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RES.BP
* PROGRAM     - EST.RES.CYLINDER.LIST
* BY          - KERRY WILSON
* DATE        - 01/19/90
* DESCRIPTION - This program lists the Roll Label Cylinder.
* TASK 16749 DMT 11/02/92 ADD LOCATION, TYPE, PERF INCHES AND MULTI COST CNTR
*T23379 cindy 10/28/1998 * loc field only displaying 3 of the 4
*                          characters
*T24312 gat 10/01/1999 * ADD PRINT CYLINDERS TO REPORT
*T25901 cmykleb 03/07/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26090 ajibaly 03/22/2002 * CONVERT TO R12 RPT.SCRN
*********************************************************************
*COPY>JES.CPYLIB>ESTIMATE.RES.CYLINDER
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET SYSCOM
*
   SYS.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN 'ESTIMATE.RES.CYLINDER' TO ESTIMATE.RES.CYLINDER ELSE
      ERRMSG = 'CANNOT OPEN ESTIMATE.RES.CYLINDER FILE'
      GOTO 93000
   END
   OPEN 'EQUIPMENT' TO EQUIPMENT ELSE
      ERRMSG = 'CANNOT OPEN EQUIPMENT FILE'
      GOTO 93000
   END
   OPEN 'COST.CNTR' TO COST.CNTR ELSE
      ERRMSG = 'CANNOT OPEN COST.CNTR FILE'
      GOTO 93000
   END
   OPEN 'COMPANY' TO COMPANY ELSE
      ERRMSG = 'CANNOT OPEN COMPANY FILE'
      GOTO 93000
   END
   OPEN 'CONTROL' TO CONTROL ELSE
      ERRMSG = 'CANNOT OPEN CONTROL FILE'
      GOTO 93000
   END
*
*---- PROC READ
*
   PROCREAD ID ELSE
      ERRMSG = "MUST RUN FROM MENU"
      GOSUB 91000
      GOTO 99999
   END
   CONO = ID<1>
*T26090   CONO.NAME = ID<2>
   CONO.NAME = ''
   CCTR.SELECT = ID<3>
*
*---- SET UP HEADINGS
*
*T25901 v
*  HD1B = SPACE(INT((100-LEN(CONO.NAME))/2)):CONO.NAME
*  HD1B = HD1B "L#100"
*  HD1 = HD1:HD1B:'   PAGE '
*  HD2 = 'REPORT # : ':REPORT.NUMBER
*  HD2 = HD2:SPACE(42):'R O L L   C Y L I N D E R   R E P O R T'
*T26090   REPORT.NAME = "ROLL CYLINDER REPORT"
   REPORT.NAME = ''
   REPORT.NUMBER = ID<2>
   HD1 = "" ; HD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HD1,HD2)
*T25901 ^
   HD3 = 'COST CENTER '
*T23379 v
   HD31 ='                      ----DIE CYLINDER-----   PRINT CYLINDER'
   HD4 = ' #T  REPEAT CYLINDER  QTY LOC  TYPE PERF IN   QTY   LOC'
   HD5 = '--- ------- --------- --- ---- ---- -------   ---   ---'
*T23379 ^
*
*---- INITIALIZATION
*
   LINE.CNT = 999
   PAGE.CNT = 0
   CCTR = ""; CCTR.CYL = ""
   IDLIST = ""
   PRINTER ON
*
*---- MAIN PROCESSING
*
   DATA = 1
   LOOP
      READNEXT KEY ELSE DATA = 0
   WHILE DATA DO
      SELECTINDEX "GSROTARY", KEY[4,2] FROM EQUIPMENT TO IDLIST
      DATA2 = 1
      LOOP
         READNEXT ID FROM IDLIST ELSE DATA2 = 0
      WHILE DATA2 DO
         IF ID[1,3] = CONO THEN
            LOCATE ID IN CCTR,1 BY "AR" SETTING PTR THEN
               CCTR.CYL<PTR,-1> = KEY
            END ELSE
               CCTR = INSERT(CCTR,PTR,0,0,ID)
               CCTR.CYL = INSERT(CCTR.CYL,PTR,0,0,KEY)
            END
         END
      REPEAT
      SELECTINDEX "GSFBED", KEY[4,2] FROM EQUIPMENT TO IDLIST
      DATA2 = 1
      LOOP
         READNEXT ID FROM IDLIST ELSE DATA2 = 0
      WHILE DATA2 DO
         IF ID[1,3] = CONO THEN
            LOCATE ID IN CCTR,1 BY "AR" SETTING PTR THEN
               CCTR.CYL<PTR,-1> = KEY
            END ELSE
               CCTR = INSERT(CCTR,PTR,0,0,ID)
               CCTR.CYL = INSERT(CCTR.CYL,PTR,0,0,KEY)
            END
         END
      REPEAT
      SELECTINDEX "GSPRINT", KEY[4,2] FROM EQUIPMENT TO IDLIST
      DATA2 = 1
      LOOP
         READNEXT ID FROM IDLIST ELSE DATA2 = 0
      WHILE DATA2 DO
         IF ID[1,3] = CONO THEN
            LOCATE ID IN CCTR,1 BY "AR" SETTING PTR THEN
*      LOCATE ID IN CCTR,1 BY "AR" SETTING PTR ELSE
               CCTR.CYL<PTR,-1> = KEY
*      END ELSE
*        CCTR = INSERT(CCTR,PTR,0,0,ID)
*        CCTR.CYL = INSERT(CCTR.CYL,PTR,0,0,KEY)
            END
         END
      REPEAT
   REPEAT
   CCTR.CNT = DCOUNT(CCTR,AM)
   FOR I = 1 TO CCTR.CNT
      IF CCTR.SELECT NE 'ALL' THEN
         IF CCTR<I>[4,99] NE CCTR.SELECT THEN
            GOTO 199
         END
      END
      GOSUB 10000
      CYL.CNT = DCOUNT(CCTR.CYL<I>,VM)
      FOR J = 1 TO CYL.CNT
         MATREAD RLCYN.REC FROM ESTIMATE.RES.CYLINDER, CCTR.CYL<I,J> THEN
            BEGIN CASE
               CASE LINE.CNT + 3 > 54
                  GOSUB 10000
                  GOSUB 1000
               CASE 1
                  GOSUB 1000
            END CASE
         END
      NEXT J
199*
   NEXT I
   GOTO 99999
*
*---- PRINT DETAIL
*
1000 *
   PLINE = RLCYN.TEETH "R#3 "
   PLINE = PLINE:OCONV(RLCYN.REPEAT,"MD4")"R#7 "
   PLINE = PLINE:CCTR.CYL<I,J>[4,99] "L#9 "
   PLINE = PLINE:RLCYN.QUANTITY "R#3 "
*T23379 v
   PLINE = PLINE:RLCYN.LOC "R#4 "
   PLINE = PLINE:RLCYN.TYPE "R#4 "
   PLINE = PLINE:OCONV(RLCYN.PERF.INCH,"MD4") "R#7"
   PLINE = PLINE:SPACE(2):RLCYN.PQTY "R#3"
   PLINE = PLINE:SPACE(2):RLCYN.PLOC "R#4"
   PRINT PLINE
   LINE.CNT = LINE.CNT + 1
   RETURN
*
*---- PRINT HEADING
*
10000 *
   MATREAD CCTR.REC FROM COST.CNTR, CCTR<I> ELSE
      MAT CCTR.REC = ''
      CCTR.DESC = 'UNKNOWN'
   END
   PAGE.CNT = PAGE.CNT + 1
   PRINT CHAR(12)
   PRINT HD1:PAGE.CNT
   PRINT HD2
   PRINT
   PRINT HD3:CCTR<I>[4,99]:' ':CCTR.DESC
   PRINT
   PRINT HD31
   PRINT HD4
   PRINT HD5
   PRINT
   LINE.CNT = 9
   RETURN
*
*---- SYSCOM MESSAGES
*
91000 *
   ERR.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
92000 *
   ERR.TYPE=2
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
93000 *
   ERR.TYPE=3
   CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
   PRINTER CLOSE
   PRINTER OFF
END
