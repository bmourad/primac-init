   SUBROUTINE EST.FORMULA.Q973 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q973
*
* AUTHOR   - SARA CRISMON, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/24/91
*
* DESCRIPTION
*
* This formula is used calculate the quantity of ink required
* for Roll-Label Estimating
*
* This formula is cloned from Q953
* MOD TASK 16691 DMT 09/28/92 CHANGE PRESS COUNT TO CALC BASED ON 12 INCH
*T26774 lross 07/30/2002 * Base inches is not in PC's so no need to
*                          convert.
*T26913 cmykleb 10/14/2002 * Include spoilage in calcs.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.RES.PRESS.SPOIL ;*T26913
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
   IF EST.PROD.INK.ID<1,COMP,MPTR> = "" THEN
      TEMP.QTY = 0
      GOTO 99999
   END
   TEMP.QTY = ""
   TYP = 0
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
   IP1 = TEMP.FVAR1
   IP2 = TEMP.FVAR2
*
*---- MAIN PROCESSING
*
   LNGTH = INT(EQTY*(EST.RL.DIE.REPEAT<1,COMP,1>/10000)+0.99)
   NO.UP = EST.RL.DIE.NO.ACR<1,COMP,1>*EST.RL.DIE.NO.ARD<1,COMP,1>
   WIDTH = EST.PROD.OS.WIDTH<1,COMP,1>/10000
   IF LNGTH = 0 OR NO.UP = 0 OR WIDTH = 0 THEN GOTO 99999
   COATING = EST.PROD.COATING<1,COMP,MPTR>
   INK.SIDES = FIELD(EST.PROD.INK.SIDES<1,COMP,MPTR>,"!",IP1)
   INK.COVER = FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",IP1)
*T26774 v SQ.IN should not be based on PC's
*  BASE.INCHES = INT(LNGTH / NO.UP + 0.99)
   BASE.PC = INT(LNGTH / NO.UP + 0.99) ;*T26774
*  BASE.PC = INT(BASE.INCHES / 10 + 0.99)
*  PAPER.SQ.IN = (BASE.PC * 10 * WIDTH)    ;* SQUARE INCHES PER SIDE
*  PAPER.SQ.IN = (BASE.PC * 12 * WIDTH)    ;* SQUARE INCHES PER SIDE
*T26913 v From EST.FORMULA.Q871
   MR.PC=0; PR.SPOIL.PC=0; FIN.SPOIL.PC=0; TOT.PR.SPOIL.PC=0
   QQTY=EQTY<1,COMP>
   NO.PASS = DCOUNT(EST.RL.PRESS.CCTR<1,COMP>,SM)
*
*--- Calculate Spoilage
*
   FOR MPTR=1 TO NO.PASS
      NO.COLS=0
      NO.COLS=EST.PROD.COLORS.1<1,COMP,MPTR>+EST.RL.BACKPRINT<1,COMP,MPTR>
      NO.COLS=NO.COLS + (EST.RL.ADHESIVE<1,COMP,MPTR>+0)
      NO.COLS=NO.COLS + (EST.RL.SSCREEN<1,COMP,MPTR>+0)
      NO.COLS=NO.COLS + (EST.RL.GRAVURE<1,COMP,MPTR>+0)
      IF EST.RL.FULL.SPOT<1,COMP,MPTR> = 'S' THEN
         NO.COLS=NO.COLS+1
      END
      PRESS.ID=EST.RL.PRESS.CCTR<1,COMP,MPTR>
      MATREAD PST.REC FROM ESTIMATE.PRESS.SPOIL,CONO:PRESS.ID:"*":NO.COLS:"*":EST.RL.FOIL<1,COMP,MPTR> ELSE
         MAT PST.REC=''
      END
      READV PRESS.TYPE FROM COST.CNTR,CONO:PRESS.ID,3 ELSE PRESS.TYPE='W'
*
*--- Convert Make Ready to Paper Unit of Measure (LMS or MSI)
*
      IF PRESS.TYPE='S' THEN
         IMPMTS=EST.RL.DIE.REPEAT<1,COMP,1>/10000
      END
      PR.SPOIL.PC=""
      FIN.SPOIL.PC=""
*
*--- Now Calculate Using Factor Tables
*
      SPOIL.BRK.CNT=DCOUNT(PST.QTY,VM)
      FLG=0
      VAL.PC = INT(BASE.PC/12) ;*Convert base in. to ft.
      LOCATE VAL.PC IN PST.QTY<1>,1 BY "AR" SETTING P ELSE NULL
      BEGIN CASE
         CASE P = 1
            PSPCT = PST.PCT<1,P>
         CASE P > COUNT(PST.QTY,VM) + 1
            PSPCT = PST.PCT<1,P-1>
         CASE PST.EXTR = "Y"
            PSPCT = INT(PST.PCT<1,P>-(PST.QTY<1,P>-VAL.PC)/(PST.QTY<1,P>-PST.QTY<1,P-1>)*(PST.PCT<1,P>-PST.PCT<1,P-1>)+0.5)
         CASE 1
            PSPCT = PST.PCT<1,P>
      END CASE
      PR.SPOIL.PC = INT(VAL.PC*PSPCT/100000+0.99)
      TOT.PR.SPOIL.PC=TOT.PR.SPOIL.PC+PR.SPOIL.PC
* Add makeready spoilage (Not incl in Q871)
      MR.PC += PST.INIT.QTY
      IF NO.COLS > 1 THEN MR.PC += (PST.COLOUR * (NO.COLS-1))
      MR.PC += (EST.RL.PLATE.CHANGES<1,COMP,MPTR> * PST.PLATE)
      MR.PC += (EST.RL.COLOR.CHANGES<1,COMP,MPTR> * PST.COL.CHNGE)
      IF EST.RL.FOIL<1,COMP,MPTR> = 'Y' THEN MR.PC += PST.FOIL
      IF EST.RL.SPROCKET<1,COMP,MPTR> = 'Y' THEN MR.PC += PST.SPROCKET
   NEXT MPTR
   PR.SPOIL.PC=TOT.PR.SPOIL.PC
   TEMP.QTY = VAL.PC + PR.SPOIL.PC + MR.PC
   BASE.PC = TEMP.QTY * 12 ;* Convert ft. back to in.
*T26913 ^
   PAPER.SQ.IN = (BASE.PC * WIDTH)    ;* SQUARE INCHES PER SIDE
   INK.SQ.IN = INT(PAPER.SQ.IN * (INK.COVER / 100) + 0.5)    ;* SQUARE INCHES OF INK PER SIDE
   BEGIN CASE
      CASE COATING = 1 AND INK.SIDES = 2
         TEMP.QTY = (INK.SQ.IN / ESTM.COVER.C<1,IP2>) + (INK.SQ.IN / ESTM.COVER.U<1,IP2>)
      CASE COATING+0 = 0
         TEMP.QTY = INK.SQ.IN * INK.SIDES / ESTM.COVER.U<1,IP2>
      CASE 1
         TEMP.QTY = INK.SQ.IN * INK.SIDES / ESTM.COVER.C<1,IP2>
   END CASE
   TEMP.QTY = INT(TEMP.QTY * 100 + 0.99) / 100
   MIN.QTY = ESTM.MIN.QTY<1,IP2> / 100
   IF TEMP.QTY < MIN.QTY THEN TEMP.QTY = MIN.QTY
99999 *
   TEMP.VSALE = "Y"
   RETURN
END
