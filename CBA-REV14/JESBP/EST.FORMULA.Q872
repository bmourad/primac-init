   SUBROUTINE EST.FORMULA.Q872 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q872
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/26/91
*
* DESCRIPTION
*
* This formula is used to calculate the LINEAL required for the
* rewind operation. The last PRESS sizes are used in the calculations.
*
*T18588 Formula moved over from EST.FORMULA.Q962
*T21565 doug 02/05/1997 * Pull lineal footage calculated from Product
*                         Definition.
*T25781 cm 05/01/2001 * Formula should not include makeready spoilage
*                       in calculation of qty.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.RES.PRESS.SPOIL             ; *T25781
*CPPY>JES.CPYLIB>ESTIMATE.MATL                        ; *T25781
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS                        ; *T25781
*COPY>CPYLIB>FILE.VARS                                ; *T25781
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   IF EST.RL.DIE.PRESS.NO<1,COMP,2>#'' THEN MPTR=2 ELSE MPTR=1
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*
*---- MAIN PROCESSING
*
   TEMP.QTY=0
*T25781 v
*     TEMP.QTY = INT(FIELD(EST.RL.TOTAL.PC<1,COMP,MPTR>,"!",QPTR)/12+.5)
   IF EST.RL.DIE.REPEAT<1,COMP,1> = "" AND EST.RL.DIE.NO.ACR<1,COMP,1> = "" AND EST.RL.DIE.NO.ARD<1,COMP,1> = "" THEN TEMP.QTY=0;RETURN
   VAR1 = EQTY<1,COMP>*(EST.RL.DIE.REPEAT<1,COMP,1>/10000)
   IF EST.RL.DIE.NO.ARD<1,COMP,1>='' THEN
      VAR3 = 1
   END ELSE
      VAR3 = EST.RL.DIE.NO.ARD<1,COMP,1>
   END
   VAR4 = EST.RL.SETS<1,COMP,2>
   IF VAR4 = '' THEN VAR4 = 1
   VAR2=EST.RL.DIE.NO.ACR<1,COMP,1>
   VAR2=(VAR2*VAR3)/VAR4
   BASE.PC = INT((VAR1/VAR2)+ .5); * IMPERIAL
   MR.PC=0; PR.SPOIL.PC=0; FIN.SPOIL.PC=0; TOT.PR.SPOIL.PC=0
   QQTY=EQTY<1,COMP>
   NO.PASS = DCOUNT(EST.RL.PRESS.CCTR<1,COMP>,SM)
*
*--- Calculate Spoilage
*
   FOR MPTR=1 TO NO.PASS
      NO.COLS=0
      NO.COLS=EST.PROD.COLORS.1<1,COMP,MPTR>+EST.RL.BACKPRINT<1,COMP,MPTR>
      NO.COLS=NO.COLS + (EST.RL.ADHESIVE<1,COMP,MPTR>+0)
      NO.COLS=NO.COLS + (EST.RL.SSCREEN<1,COMP,MPTR>+0)
      NO.COLS=NO.COLS + (EST.RL.GRAVURE<1,COMP,MPTR>+0)
      IF EST.RL.FULL.SPOT<1,COMP,MPTR> = 'S' THEN
         NO.COLS=NO.COLS+1
      END
      PRESS.ID=EST.RL.PRESS.CCTR<1,COMP,MPTR>
      MATREAD PST.REC FROM ESTIMATE.PRESS.SPOIL,CONO:PRESS.ID:"*":NO.COLS:"*":EST.RL.FOIL<1,COMP,MPTR> ELSE
         MAT PST.REC=''
      END
      READV PRESS.TYPE FROM COST.CNTR,CONO:PRESS.ID,3 ELSE PRESS.TYPE='W'
*
*--- Convert Make Ready to Paper Unit of Measure (LMS or MSI)
*
      IF PRESS.TYPE='S' THEN
         IMPMTS=EST.RL.DIE.REPEAT<1,COMP,1>/10000
      END
      PR.SPOIL.PC=""
      FIN.SPOIL.PC=""
*
*--- Now Calculate Using Factor Tables
*
      SPOIL.BRK.CNT=DCOUNT(PST.QTY,VM)
      FLG=0
      VAL.PC = INT(BASE.PC/12)
      LOCATE VAL.PC IN PST.QTY<1>,1 BY "AR" SETTING P ELSE NULL
      BEGIN CASE
         CASE P = 1
            PSPCT = PST.PCT<1,P>
         CASE P > COUNT(PST.QTY,VM) + 1
            PSPCT = PST.PCT<1,P-1>
         CASE PST.EXTR = "Y"
            PSPCT = INT(PST.PCT<1,P>-(PST.QTY<1,P>-VAL.PC)/(PST.QTY<1,P>-PST.QTY<1,P-1>)*(PST.PCT<1,P>-PST.PCT<1,P-1>)+0.5)
         CASE 1
            PSPCT = PST.PCT<1,P>
      END CASE
      PR.SPOIL.PC = INT(VAL.PC*PSPCT/100000+0.99)
      TOT.PR.SPOIL.PC=TOT.PR.SPOIL.PC+PR.SPOIL.PC
   NEXT MPTR
   PR.SPOIL.PC=TOT.PR.SPOIL.PC
   TEMP.QTY = VAL.PC + PR.SPOIL.PC + MR.PC
*T25781 ^
   TEMP.VSALE="Y"
   RETURN
END
