*COPY>CPYLIB>SCOMMON1
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.DEPT.DEFINE
*
* AUTHOR   - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/09/85
*
* DESCRIPTION
* This program updates and maintaines the ESTIMATE.DIV file.
*
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>ESTIMATE.DIV
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
   OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG = "CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG = "CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","JES.SCREENS" TO M.SCREENS ELSE
      ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","ESTIMATE.DIV" TO ESTIMATE.DIV ELSE
      ERRMSG = "CANNOT OPEN ESTIMATE.DIV FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","DIVISION" TO DIVISION ELSE
      ERRMSG = "CANNOT OPEN DIVISION FILE"
      GOSUB 90000
      STOP
   END
   OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
      ERRMSG = "CANNOT OPEN DEPARTMENT FILE"
      GOSUB 90000
      STOP
   END
*
*---- INITIALIZATION
*
   CONO = ""
   CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
   SCREEN INIT;#
   S$SCR = 1
   SCREEN DEFINE;EST.DEPT.DEFINE
   SCREEN FORMAT
   SCREEN COUNT;EST.DEPT.DEFINE;3
   LINE.COUNT = S$LCNT
   LINE.SPACE = S$LSPC
   ERRMSG = ""
   MAT DIV.REC = ""
   MAT DEPT.REC = ""
*
*---- MAIN PROCESSING
*
100*
   CURR.REF.NO = ""
   SCREEN FIELD;EST.DEPT.DEFINE;1
   SCREEN INPUT;EST.DEPT.DEFINE;1
   IF S$VALUE = "END" THEN GOTO 99999
   KEY = S$VALUE
   DIV.KEY = FIELD(KEY,"-",1)
   MATREAD DIV.REC FROM DIVISION, CONO:DIV.KEY ELSE
      ERRMSG = "Item must be a valid division"
      GOSUB 90000
      GOTO 100
   END
   S$DATA(2)<S$SCR> = DIV.DESC
   SCREEN DISPLAY;EST.DEPT.DEFINE;2
   NEW.REC = 0
  * T26126 v
   MATREADU ESTV.REC FROM ESTIMATE.DIV, CONO:KEY LOCKED
      ERRMSG = 'ESTIMATE DIVISION record is locked by user - ':GETUSERNAME(STATUS())
      GOSUB 90000;GOTO 100 
   END ELSE
  * T26126 ^
      MAT ESTV.REC = ""
      NEW.REC = 1
   END
   CNT = COUNT(ESTV.DEPT,VM) + (ESTV.DEPT # "")
   IF NEW.REC THEN
      DONE = 0
      FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
         GOSUB 50000
         GOSUB 10100
         IF S$VALUE = "END" THEN
            DONE = 1
            S$DATA(4) = DELETE(S$DATA(4),S$SCR,REF.NO,0)
            S$DATA(5) = DELETE(S$DATA(5),S$SCR,REF.NO,0)
         END ELSE
            CNT = CNT + 1
         END
      NEXT REF.NO
      REF.NO = CNT
      CURR.REF.NO = ""
      GOSUB 50000
   END ELSE
      S$DATA(4)<S$SCR> = ESTV.DEPT
      S$DATA(5)<S$SCR> = ESTV.DEPT.DESC
      IF ESTV.DEPT # "" THEN
         REF.NO = 1
         GOSUB 50000
      END
   END
*
*---- GET OPERATOR REPLY
*
500*
   S$DATA(6)<S$SCR> = ""
   SCREEN FIELD;EST.DEPT.DEFINE;6
   SCREEN INPUT;EST.DEPT.DEFINE;6
   OPT = S$VALUE
   BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         SCREEN CLEAR
         RELEASE
         GOTO 100
      CASE OPT = "A" AND CNT < 99
         DONE = 0
         FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
            GOSUB 50000
            GOSUB 10100
            IF S$VALUE = "END" THEN
               DONE = 1
               S$DATA(4) = DELETE(S$DATA(4),S$SCR,REF.NO,0)
               S$DATA(5) = DELETE(S$DATA(5),S$SCR,REF.NO,0)
               ESTV.DEPT = DELETE(ESTV.DEPT,1,REF.NO,0)
               ESTV.DEPT.TYPE = DELETE(ESTV.DEPT.TYPE,1,REF.NO,0)
               ESTV.DEPT.DESC = DELETE(ESTV.DEPT.DESC,1,REF.NO,0)
            END ELSE
               CNT = CNT + 1
            END
         NEXT REF.NO
         REF.NO = CNT
         CURR.REF.NO = ""
         GOSUB 50000
      CASE OPT = "C" AND CNT > 0
         GOSUB 30000
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            GOSUB 10100
         END
      CASE OPT = "I" AND CNT > 0
         GOSUB 30000
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            S$DATA(4) = INSERT(S$DATA(4),S$SCR,REF.NO,0,"")
            S$DATA(5) = INSERT(S$DATA(5),S$SCR,REF.NO,0,"")
            ESTV.DEPT = INSERT(ESTV.DEPT,1,REF.NO,0,"")
            ESTV.DEPT.TYPE = INSERT(ESTV.DEPT.TYPE,1,REF.NO,0,"")
            ESTV.DEPT.DESC = INSERT(ESTV.DEPT.DESC,1,REF.NO,0,"")
            CNT = CNT + 1
            CURR.REF.NO = ""
            GOSUB 50000
            GOSUB 10100
            IF S$VALUE = "END" THEN
               S$DATA(4) = DELETE(S$DATA(4),S$SCR,REF.NO,0)
               S$DATA(5) = DELETE(S$DATA(5),S$SCR,REF.NO,0)
               ESTV.DEPT = DELETE(ESTV.DEPT,1,REF.NO,0)
               ESTV.DEPT.TYPE = DELETE(ESTV.DEPT.TYPE,1,REF.NO,0)
               ESTV.DEPT.DESC = DELETE(ESTV.DEPT.DESC,1,REF.NO,0)
               CNT = CNT - 1
            END
            CURR.REF.NO = ""
            GOSUB 50000
         END
      CASE OPT = "D" AND CNT > 0
         GOSUB 30000
         IF S$VALUE # "" AND S$VALUE # "END" THEN
            REF.NO = S$VALUE
            S$DATA(4) = DELETE(S$DATA(4),S$SCR,REF.NO,0)
            S$DATA(5) = DELETE(S$DATA(5),S$SCR,REF.NO,0)
            ESTV.DEPT = DELETE(ESTV.DEPT,1,REF.NO,0)
            ESTV.DEPT.TYPE = DELETE(ESTV.DEPT.TYPE,1,REF.NO,0)
            ESTV.DEPT.DESC = DELETE(ESTV.DEPT.DESC,1,REF.NO,0)
            CNT = CNT - 1
            IF REF.NO > CNT THEN REF.NO = REF.NO - 1
            CURR.REF.NO = ""
            GOSUB 50000
         END
      CASE OPT = "S"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > CNT THEN REF.NO = 1
         GOSUB 50000
    * T25978 v
      CASE OPT = 'SR'
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = 'ST'
         REF.NO = 1
         GOSUB 50000
      CASE OPT = 'SB'
         REF.NO = CNT
         GOSUB 50000
    * T25978 ^
      CASE OPT = "F"
         MATWRITE ESTV.REC ON ESTIMATE.DIV,CONO:KEY
         SCREEN CLEAR
         GOTO 100
   END CASE
   GOTO 500
*
*---- GET VALID DEPARTMENT
*
10100*
   S$VAL = REF.NO
   SCREEN FIELD;EST.DEPT.DEFINE;4
   SCREEN INPUT;EST.DEPT.DEFINE;4;RETURN
   BEGIN CASE
      CASE S$VALUE = "END"
         GOTO 10199
      CASE S$VALUE = ""
         GOTO 10100
      CASE S$VALUE = "CON"
         MAT DEPT.REC = ""
         DEPT.DESC = "CONSOLIDATED"
      CASE 1
         LOCATE S$VALUE IN DIV.DEPT<1>,1 SETTING DD ELSE
            ERRMSG = "MUST ENTER A DEPARTMENT FROM DIVISION ":DIV.KEY
            GOSUB 90000
            GOTO 10100
         END
         MATREAD DEPT.REC FROM DEPARTMENT,CONO:S$VALUE ELSE
            ERRMSG = "MUST ENTER A VALID DEPARTMENT"
            GOSUB 90000
            GOTO 10100
         END
   END CASE
   ESTV.DEPT<1,REF.NO> = S$VALUE
   ESTV.DEPT.TYPE<1,REF.NO> = DEPT.TYPE
   ESTV.DEPT.DESC<1,REF.NO> = DEPT.DESC
   S$DATA(5)<S$SCR,REF.NO> = ESTV.DEPT.DESC<1,REF.NO>
   S$VAL = REF.NO
   SCREEN DISPLAY;EST.DEPT.DEFINE;5
10199*
   RETURN
*
*---- GET DEPARTMENT LINE REFERENCE NUMBER
*
30000*
   S$DATA(7)<S$SCR> = ""
   SCREEN FIELD;EST.DEPT.DEFINE;7
   S$MINV = CURR.REF.NO
   S$MAXV = S$MINV + LINE.COUNT - 1
   IF S$MAXV > CNT THEN S$MAXV = CNT
   SCREEN INPUT;EST.DEPT.DEFINE;7
   RETURN
*
*---- SCROLL DEPARTMENT
*
50000*
   START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
   IF START.REF.NO = CURR.REF.NO THEN RETURN
   CURR.REF.NO = START.REF.NO
   S$VAL = START.REF.NO
   S$CNT = COUNT(S$DATA(4)<S$SCR>,VM) + (S$DATA(4)<S$SCR> # "")
   SCREEN MULTI;EST.DEPT.DEFINE;C;3;4;5
   RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
END
