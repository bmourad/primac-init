  SUBROUTINE EST.PRESS.MAINT.2 (CONO, EQP.KEY, DESC, MAT EQUIPMENT.REC)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.PRESS.MAINT.2
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/12/87
*
* DESCRIPTION
*
* This subroutine is used to maintain the delivery configuration data for
* a defined press.
*
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.DC.CODE
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","ESTIMATE.DC.CODE" TO ESTIMATE.DC.CODE ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.DC.CODE FILE"
    GOSUB 90000
    GOTO 99999
  END
*
*---- INITIALIZATION
*
  SCREEN DEFINE;EST.PRESS.MAINT.2
  SCREEN FORMAT
  SCREEN COUNT;EST.PRESS.MAINT.2;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  CNT = COUNT(EQP.PRESS.DC.CODE,VM) + (EQP.PRESS.DC.CODE # "")
  CURR.REF.NO = ""
  REF.NO = 1
  GOSUB 80000
  GOSUB 50000
*
*---- MAIN PROCESSING
*
100*
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.PRESS.MAINT.2;21
  SCREEN INPUT;EST.PRESS.MAINT.2;21
  OPT = S$VALUE
510*
  BEGIN CASE
    CASE OPT = ""
      GOTO 99999
    CASE OPT = "A" AND CNT < 99
      MODE = "A"
      DONE = 0
      FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 600
        END ELSE
          CNT = CNT + 1
        END
      NEXT REF.NO
      REF.NO = CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND CNT > 0
      MODE = "C"
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10000
        IF S$VALUE = "END" THEN
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "I" AND CNT > 0
      MODE = "I"
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        CNT = CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          GOSUB 600
          CNT = CNT - 1
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "D" AND CNT > 0
      MODE = "D"
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 600
        CNT = CNT - 1
        IF REF.NO > CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPT = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPT = 'SB'
      REF.NO = CNT
      GOSUB 50000
    * T25978 ^
  END CASE
  GOTO 500
*
*---- DELETE MULTI-LINE DATA
*
600*
  S$DATA(12) = DELETE(S$DATA(12),S$SCR,REF.NO,0)
  S$DATA(13) = DELETE(S$DATA(13),S$SCR,REF.NO,0)
  S$DATA(14) = DELETE(S$DATA(14),S$SCR,REF.NO,0)
  EQP.PRESS.DC.CODE = DELETE(EQP.PRESS.DC.CODE,1,REF.NO,0)
  EQP.PRESS.DC.MAX = DELETE(EQP.PRESS.DC.MAX,1,REF.NO,0)
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
700*
  S$DATA(12) = INSERT(S$DATA(12),S$SCR,REF.NO,0,"")
  S$DATA(13) = INSERT(S$DATA(13),S$SCR,REF.NO,0,"")
  S$DATA(14) = INSERT(S$DATA(14),S$SCR,REF.NO,0,"")
  EQP.PRESS.DC.CODE = INSERT(EQP.PRESS.DC.CODE,1,REF.NO,0,"")
  EQP.PRESS.DC.MAX = INSERT(EQP.PRESS.DC.MAX,1,REF.NO,0,"")
  RETURN
*
*---- GET MULTI-LINE DATA
*
10000*
  S$VAL = REF.NO
  SCREEN DISPLAY;EST.PRESS.MAINT.2;11
  SAVE.DESC = S$DATA(13)<S$SCR,REF.NO>
10100*
  S$DATA(12)<S$SCR,REF.NO> = EQP.PRESS.DC.CODE<1,REF.NO>
  S$VAL = REF.NO
  SCREEN FIELD;EST.PRESS.MAINT.2;12
  SCREEN INPUT;EST.PRESS.MAINT.2;12;GOTO 10990
  SAVE.VAL = EQP.PRESS.DC.CODE<1,REF.NO>
  EQP.PRESS.DC.CODE<1,REF.NO> = ""
  LOCATE S$VALUE IN EQP.PRESS.DC.CODE<1>,1 SETTING FND ELSE FND = 0
  EQP.PRESS.DC.CODE<1,REF.NO> = SAVE.VAL
  IF FND THEN
    ERRMSG = "Duplicate configuration code. Try again! "
    GOSUB 90000
    GOTO 10100
  END
  MATREAD ESTDC.REC FROM ESTIMATE.DC.CODE, CONO:S$VALUE ELSE
    ERRMSG = "Invalid configuration code. Try again! "
    GOSUB 90000
    GOTO 10100
  END
  CODE = S$VALUE
*
  S$DATA(13)<S$SCR,REF.NO> = ESTDC.DESC
  S$VAL = REF.NO
  SCREEN DISPLAY;EST.PRESS.MAINT.2;13
10200*
  S$VAL = REF.NO
  SCREEN FIELD;EST.PRESS.MAINT.2;14
  SCREEN INPUT;EST.PRESS.MAINT.2;14;GOTO 10990
  DCMAX = S$VALUE
10900*
  EQP.PRESS.DC.CODE<1,REF.NO> = CODE
  EQP.PRESS.DC.MAX<1,REF.NO> = DCMAX
  RETURN
10990*
  S$DATA(12)<S$SCR,REF.NO> = EQP.PRESS.DC.CODE<1,REF.NO>
  S$DATA(13)<S$SCR,REF.NO> = SAVE.DESC
  S$DATA(14)<S$SCR,REF.NO> = EQP.PRESS.DC.MAX<1,REF.NO>
  RETURN
*
*---- GET LINE REFERENCE NUMBER
*
30000*
  SCREEN FIELD;EST.PRESS.MAINT.2;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > CNT THEN S$MAXV = CNT
  SCREEN INPUT;EST.PRESS.MAINT.2;22
  RETURN
*
*---- DISPLAY MULTI-LINE DATA
*
50000*
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = COUNT(S$DATA(12)<S$SCR>,VM) + (S$DATA(12)<S$SCR> # "")
  SCREEN MULTI;EST.PRESS.MAINT.2;C;11;12;13;14
  RETURN
*
*---- LOAD SCREEN DATA
*
80000*
  S$DATA(1)<S$SCR> = DATE()
  S$DATA(2)<S$SCR> = EQP.KEY
  S$DATA(3)<S$SCR> = DESC
  S$DATA(12)<S$SCR> = EQP.PRESS.DC.CODE
  FOR REF = 1 TO CNT
    MATREAD ESTDC.REC FROM ESTIMATE.DC.CODE, CONO:EQP.PRESS.DC.CODE<1,REF> ELSE
      MAT ESTDC.REC = ""
      ESTDC.DESC = "???????????????"
    END
    S$DATA(13)<S$SCR,REF> = ESTDC.DESC
  NEXT REF
  S$DATA(14)<S$SCR> = EQP.PRESS.DC.MAX
80090*
  SCREEN DISPLAY;EST.PRESS.MAINT.2;ALL
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
*       PRINT @(-1):
  SCREEN CLEAR;EST.PRESS.MAINT.2;D
  RETURN
END
