*COPY>CPYLIB>SCOMMON1
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.DESC.CODE.MAINT
*
* AUTHOR   - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/31/85
*
* DESCRIPTION
*
* This program updates and maintains the EST.DESC.CODE file.
*
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>EST.DESC.CODE
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG="CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG="CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG="CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","EST.DESC.CODE" TO EST.DESC.CODE ELSE
    ERRMSG="CANNOT OPEN EST.DESC.CODE FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO=""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR=1
  SCREEN DEFINE;EST.DESC.CODE.MAINT
  SCREEN FORMAT
  SCREEN COUNT;EST.DESC.CODE.MAINT;2
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  ERRMSG = ""
*
*---- MAIN PROCESSING
*
100*
  CURR.REF.NO = ""
  NEW.REC = 0
  SCREEN FIELD;EST.DESC.CODE.MAINT;1
  SCREEN INPUT;EST.DESC.CODE.MAINT;1
  IF S$VALUE = "END" THEN GOTO 99999
  EDC.KEY = S$VALUE
  * T26126 v
  MATREADU EDC.REC FROM EST.DESC.CODE,CONO:EDC.KEY LOCKED
    ERRMSG = 'DESCRIPTION record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 100 
  END ELSE
  * T26126 ^
    MAT EDC.REC = ""
    NEW.REC = 1
  END
  CNT = COUNT(EDC.DESC,VM) + (EDC.DESC # "")
  IF NEW.REC THEN
    GOSUB 10100
    IF S$VALUE = "END" THEN
      SCREEN CLEAR;EST.DESC.CODE.MAINT
      RELEASE
      GOTO 100
    END
    DONE = 0
    FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
      GOSUB 50000
      GOSUB 10200
      IF S$VALUE = "END" THEN
        DONE = 1
        S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
        EDC.DESC = DELETE(EDC.DESC,1,REF.NO,0)
      END ELSE
        CNT = CNT + 1
      END
    NEXT REF.NO
    REF.NO = CNT
    CURR.REF.NO = ""
    GOSUB 50000
  END ELSE
    S$DATA(4)<S$SCR> = EDC.HEADING
    SCREEN DISPLAY;EST.DESC.CODE.MAINT;4
    S$DATA(3)<S$SCR> = EDC.DESC
    IF EDC.DESC # "" THEN
      REF.NO = 1
      GOSUB 50000
    END
  END
*
*---- GET OPERATOR REPLY
*
500*
  S$DATA(5)<S$SCR> = ""
  SCREEN FIELD;EST.DESC.CODE.MAINT;5
  SCREEN INPUT;EST.DESC.CODE.MAINT;5
  OPT = S$VALUE
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      SCREEN CLEAR
      RELEASE
      GOTO 100
    CASE OPT # "" AND NUM(OPT)
      GOSUB 10100
    CASE OPT = "D"
      GOSUB 20000
    CASE OPT = "F"
      MATWRITE EDC.REC ON EST.DESC.CODE,CONO:EDC.KEY
      SCREEN CLEAR
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET HEADING
*
10100*
  SCREEN FIELD;EST.DESC.CODE.MAINT;4
  SCREEN INPUT;EST.DESC.CODE.MAINT;4;RETURN
  EDC.HEADING = S$VALUE
  RETURN
*
*---- GET DESCRIPTION 
*
10200*
  S$VAL = REF.NO
  SCREEN FIELD;EST.DESC.CODE.MAINT;3
  SCREEN INPUT;EST.DESC.CODE.MAINT;3
  BEGIN CASE
    CASE S$VALUE = "END"
      GOTO 10299
    CASE S$VALUE = ""
      GOTO 10200
    CASE S$VALUE # ""
      EDC.DESC<1,REF.NO> = S$VALUE
  END CASE
10299*
  RETURN
*
*---- GET DESCRIPTION OPTION
*
20000*
  S$DATA(6)<S$SCR> = ""
  SCREEN FIELD;EST.DESC.CODE.MAINT;6
  SCREEN INPUT;EST.DESC.CODE.MAINT;6
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION = "" OR OPTION = "E" OR OPTION = "END"
      GOTO 20099
    CASE OPTION = "A" AND CNT < 99
      DONE = 0
      FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10200
        IF S$VALUE = "END" THEN
          DONE = 1
          S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
          EDC.DESC = DELETE(EDC.DESC,1,REF.NO,0)
        END ELSE
          CNT = CNT + 1
        END
      NEXT REF.NO
      REF.NO = CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPTION = "C" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10200
      END
    CASE OPTION = "I" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        S$DATA(3) = INSERT(S$DATA(3),S$SCR,REF.NO,0,"")
        EDC.DESC = INSERT(EDC.DESC,1,REF.NO,0,"")
        CNT = CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10200
        IF S$VALUE = "END" THEN
          S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
          EDC.DESC = DELETE(EDC.DESC,1,REF.NO,0)
          CNT = CNT - 1
        END
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPTION = "D" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
        EDC.DESC = DELETE(EDC.DESC,1,REF.NO,0)
        CNT = CNT - 1
        IF REF.NO > CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPTION = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPTION = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPTION = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPTION = 'SB'
      REF.NO = CNT
      GOSUB 50000
    * T25978 ^
  END CASE
  GOTO 20000
20099*
  RETURN
*
*---- GET LINE REFERENCE NUMBER
*
30000*
  S$DATA(7)<S$SCR> = ""
  SCREEN FIELD;EST.DESC.CODE.MAINT;7
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > CNT THEN S$MAXV = CNT
  SCREEN INPUT;EST.DESC.CODE.MAINT;7
  RETURN
*
*---- SCROLL DESCRIPTION
*
50000*
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = COUNT(S$DATA(3)<S$SCR>,VM) + (S$DATA(3)<S$SCR> # "")
  SCREEN MULTI;EST.DESC.CODE.MAINT;C;2;3
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
*       PRINT @(-1):
END
