SUBROUTINE EST.REPORT.1 (CONO,CO.NAME,EST.KEY,PAGE.NO,ESTIMATE.RL.CYLINDER,PRT.QTY)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.REPORT.1
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/18/86
*
* DESCRIPTION
*
* This program will print the estimate report.
*
* MOD 18484 RKB 10/19/94 ADD SUPPORT FOR 99 QTYS
*T19656 rick 08/06/1996 * INCLUDE FIX
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RL
*COPY>JES.CPYLIB>ESTIMATOR
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.RL.CYLINDER
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>SALESMAN
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
10 *
      PAGE.NO = 0
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      SP4 = SPACE(4)
      SP5 = SPACE(5)
      SP6 = SPACE(6)
      UNKNOWN = STR("?",45)
      CDATE = DATE()
      CTIME = TIME()
      IF CTIME < 10 THEN CDATE = DATE()
      CTIME = OCONV(CTIME,"MTS")
      CHR = CTIME[1,2]+0
      CMN = CTIME[4,2]
      BEGIN CASE
      CASE CHR < 12
        PTIME = CHR:":":CMN:" AM"
      CASE CHR = 12
        PTIME = CHR:":":CMN:" PM"
      CASE 1
        PTIME = (CHR-12):":":CMN:" PM"
      END CASE
      HD1A = OCONV(CDATE,"D2/"):SP3:PTIME
      HD1B = SPACE((40-LEN(CO.NAME))/2):CO.NAME
      HD1 = HD1A"L#46":HD1B"L#40":SPACE(34):"PAGE: "
      HD2 = SPACE(51):"E S T I M A T E   R E P O R T"
20 *
*
*---- MAIN PROCESSING
*
100 *
      BEGIN CASE
      CASE EST.TYPE = "R"
        ETYPE = "REGULAR"
      CASE EST.TYPE = "C"
        ETYPE = "CHANGE ORDER"
      CASE EST.TYPE = "S"
        ETYPE = "SPOILAGE"
      CASE EST.TYPE = "N"
        ETYPE = "NON-CHARGEABLE"
      CASE 1
        ETYPE = ""
      END CASE
      MATREAD DIV.REC FROM DIVISION, CONO:FIELD(EST.DIV,"-",1) ELSE
        MAT DIV.REC = ""
      END
      IF EST.SALESMAN = "" THEN
        MAT SALESMAN.REC = ""
      END ELSE
        MATREAD SALESMAN.REC FROM SALESMAN, CONO:EST.SALESMAN ELSE
          MAT SALESMAN.REC = ""
        END
      END
      IF EST.CATG = "" THEN
        MAT JCATG.REC = ""
      END ELSE
        MATREAD JCATG.REC FROM JOB.CATEGORY, CONO:EST.CATG ELSE
          MAT JCATG.REC = ""
        END
      END
      IF EST.ESTIMATOR = "" THEN
        MAT ESTIMATOR.REC = ""
      END ELSE
        MATREAD ESTIMATOR.REC FROM ESTIMATOR, CONO:EST.ESTIMATOR ELSE
          MAT ESTIMATOR.REC = ""
        END
      END
*
*---- PRINT DEMOGRAPHICS
*
2000 *
      PRINTER ON
      GOSUB 10000
      PLINE = "CUSTOMER : ":EST.CUST.NAME"L#30"
      PLINE = PLINE:SP5:"DIVISION : "
      PLINE = PLINE:DIV.DESC"L#30"
      PLINE = PLINE:SP5:"ESTIMATE TYPE  : ":ETYPE
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PLINE = SPACE(11):EST.CUST.ADDR1"L#30"
      PLINE = PLINE:SP5:"CATEGORY : "
      PLINE = PLINE:JCATG.DESC"L#30"
      PLINE = PLINE:SP5:"DATE ENTERED   : ":OCONV(EST.DATE.ENTERED,"D2/")
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PLINE = SPACE(11):EST.CUST.ADDR2"L#30"
      PLINE = PLINE:SP5:"SALES CD : "
      PLINE = PLINE:EST.SALES.CODE"L#30"
      PLINE = PLINE:SP5:"DATE REQUIRED  : ":OCONV(EST.DATE.REQUIRED,"D2/")
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PLINE = SPACE(11):EST.CUST.ADDR3"L#30"
      PLINE = PLINE:SP5:"SALESREP : "
      PLINE = PLINE:SALS.NAME"L#30"
      PLINE = PLINE:SP5:"JOB TYPE       : ":EST.RL.JOB.TYPE
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PLINE = SPACE(11):EST.CUST.ADDR4"L#30"
      PLINE = PLINE:SP5:"ESTIMATOR: "
      PLINE = PLINE:ESTMTR.NAME"L#30"
      PLINE = PLINE:SP5:"PREVIOUS EST # : ":EST.RL.PREV.EST
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PLINE = "ATTENTION: ":EST.CUST.ATTN"L#30"
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PRINT
      LINE.CNT = LINE.CNT + 1
      PLINE = " QUANTITIES                         JOB DESCRIPTION / REMARKS                                    SPECIAL INSTRUCTIONS"
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PLINE = "------------     ----------------------------------------------------------------    ---------------------------------------------"
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
*18484      ECNT = DCOUNT(EST.QTY,VM)
*18484 v
      ECNT = DCOUNT(PRT.QTY,SVM) 
      QPTR = ''
      FOR Q = 1 TO ECNT
        LOCATE PRT.QTY<1,1,Q> IN EST.QTY<1>,1 SETTING Q1 THEN
          QPTR<1,Q> = Q1
        END
      NEXT Q
*18484 ^      
      DC = DCOUNT(EST.COMMENTS,VM)
      CMNT.TYPE = ""
      CMNTS = ""
      DCNT = 0
      FOR DP = 1 TO DC
        IF EST.COMMENT.TYPE<1,DP>="" OR EST.COMMENT.TYPE<1,DP>="I" THEN
          DCNT = DCNT + 1
          CMNT.TYPE<1,DCNT> = EST.COMMENT.TYPE<1,DP>
          CMNTS<1,DCNT> = EST.COMMENTS<1,DP>
        END
      NEXT DP
      GOSUB 5000
      LC = ECNT
      IF DCNT > LC THEN LC = DCNT
      IF SCNT > LC THEN LC = SCNT
      FOR LP = 1 TO LC
        IF LINE.CNT > 55 THEN GOSUB 10000
*99QTY  v
        IF ECNT < LP THEN
          QVAL = ''
        END ELSE
          QVAL = OCONV(EST.QTY<1,QPTR<1,LP>>,"MD0")
        END
        PLINE = QVAL"R#11"
*99QTY   ^        
        PLINE = PLINE:SP6:CMNT.TYPE<1,LP>"L#2"
        PLINE = PLINE:SP2:CMNTS<1,LP>"L#60"
        PLINE = PLINE:SP4:SREC<LP>"L#45"
        PRINT PLINE
        LINE.CNT = LINE.CNT + 1
      NEXT LP
*
*---- PRINT STOCK AND PRODUCTION INFORMATION
*
3000 *
      DCNT = DCOUNT(EST.DESC,VM)
      IF DCNT <= 10 THEN
        LC = DCNT
      END ELSE
        LC = INT(DCNT/2+0.99)
      END
      IF (LINE.CNT + LC + 4) > 55 THEN GOSUB 10000
      PRINT
      PRINT STR("*",55):"  JOB INSTRUCTIONS  ":STR("*",55)
      LINE.CNT = LINE.CNT + 2
      PRINT
      LINE.CNT = LINE.CNT + 1
      FOR LP = 1 TO LC
        PLINE=EST.DESC.HEADING<1,LP>"L#12":SP2:EST.DESC<1,LP>"L#50"
        PLINE=PLINE:" | ":EST.DESC.HEADING<1,LP+LC>"L#12":SP2:EST.DESC<1,LP+LC>"L#50"
        PRINT PLINE
        LINE.CNT = LINE.CNT + 1
      NEXT LP
      IF (LINE.CNT + 7) > 55 THEN GOSUB 10000
      PRINT
      PRINT STR("*",54):"  JOB SPECIFICATIONS  ":STR("*",54)
      PRINT
      LINE.CNT = LINE.CNT + 3
      PLINE = "QTY IN PC'S   PG    PROD     INVENTORY ITEM                 FULL DESCRIPTION                   WIDTH "
      ULINE = "-----------   --   ------   ---------------   ---------------------------------------------   -------"
      IF EST.RL.ORD.STK = "Y" THEN
        PLINE = PLINE:SP5:"* * * * * * * * * * * * *"
        ULINE = ULINE:SP5:"*  ORDER STOCK !!!!!!!  *"
      END
      PRINT PLINE; PRINT ULINE
      LINE.CNT = LINE.CNT + 2
      LOOP.CNT = 0
      COMP = DCOUNT(EST.PROD.OS.USAGE,VM)
      FOR C = 1 TO COMP
        WEB = DCOUNT(EST.PROD.OS.USAGE<1,C>,SVM)
        FOR W = 1 TO WEB
          IF LINE.CNT + ECNT > 55 THEN GOSUB 10000
          FOR Q = 1 TO ECNT
            LOOP.CNT = LOOP.CNT + 1
*T19656            PC = FIELD(EST.RL.TOTAL.PC<1,C,W>,"!",Q)
            PC = FIELD(EST.RL.TOTAL.PC<1,C,1>,"!",QPTR<1,Q>)
            IF Q = 1 THEN
              MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:"R":EST.PROD.OS.USAGE<1,C,W> ELSE MAT ESTM.REC = ""
              LOCATE EST.PROD.OS.PROD<1,C,W> IN ESTM.PROD<1>,1 SETTING POS ELSE ESTM.FULL.DESC<1,POS> = UNKNOWN
              PLINE = OCONV(PC,"MD0,") "R#11":SP3:EST.PROD.OS.USAGE<1,C,W> "L#2":SP3:EST.PROD.OS.PROD<1,C,W> "L#6":SP3
              PLINE = PLINE:EST.PROD.INV.ID<1,C,W> "L#15":SP3:ESTM.FULL.DESC<1,POS> "L#45":SP3:OCONV(EST.PROD.OS.WIDTH<1,C,W>,"MD4") "R#7"
            END ELSE
              PLINE = OCONV(PC,"MD0,") "R#11":SPACE(90)
            END
            IF EST.RL.ORD.STK = "Y" THEN
              IF LOOP.CNT = 1 THEN PLINE = PLINE:SP5:"*  MINIMUM ":EST.RL.ORD.REQ "R#8":" MSI *"
              IF LOOP.CNT = 2 THEN PLINE = PLINE:SP5:"* * * * * * * * * * * * *"
            END
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
          NEXT Q
        NEXT W
      NEXT C
      IF EST.RL.ORD.STK = "Y" THEN
        BEGIN CASE
        CASE LOOP.CNT = 0
          PRINT SPACE(106):"*  MINIMUM ":EST.RL.ORD.QTY "R#8":" MSI *"
          PRINT SPACE(106):"* * * * * * * * * * * * *"
          LINE.CNT = LINE.CNT + 2
        CASE LOOP.CNT = 1
          PRINT SPACE(106):"* * * * * * * * * * * * *"
          LINE.CNT = LINE.CNT + 1
        END CASE
      END
      DCNT = DCOUNT(EST.RL.DIE,SM)
      IF DCNT > 5 THEN DCNT = 5
      IF (LINE.CNT + DCNT + 5) > 55 THEN GOSUB 10000
      PRINT;PRINT
      LINE.CNT = LINE.CNT + 2
      PLINE = SPACE(10):"DIE ID #   REPEAT   AC  AR  CYLINDER   QTY"
      ULINE = SPACE(10):"---------  -------  --  --  ---------  ---"
      PLINE = PLINE:SPACE(15):"DIE ID #   REPEAT   AC  AR  CYLINDER   QTY"
      ULINE = ULINE:SPACE(15):"---------  -------  --  --  ---------  ---"
      IF EST.RL.CYL.PRICE # "" THEN
        MATREAD RLCYN.REC FROM ESTIMATE.RL.CYLINDER, CONO:EST.RL.DIE.CYL.ID<1,1,1> ELSE MAT RLCYN.REC=''
        CYL.ORDER = EST.RL.DIE.QTY<1,1,1> - RLCYN.QUANTITY
        IF CYL.ORDER < 0 THEN CYL.ORDER = 0
        PLINE = PLINE:SP3:"* * * * * * * * * *"
        ULINE = ULINE:SP3:"*    ORDER ":CYL.ORDER "R#3":"    *"
      END
      PRINT PLINE; PRINT ULINE
      LINE.CNT = LINE.CNT + 2
      FOR D = 1 TO DCNT STEP 2
        BEGIN CASE
        CASE D = 1
          A = "1ST DIE:  ";B = "2ND DIE:  "
        CASE D = 3
          A = "3RD DIE:  ";B = "4TH DIE:  "
        CASE 1
          A = "5TH DIE:  "
        END CASE
        PLINE = A:EST.RL.DIE<1,1,D> "L#9":SP2:OCONV(EST.RL.DIE.REPEAT<1,1,D>,"MD4") "R#7":SP2:EST.RL.DIE.NO.ACR<1,1,D> "R#2":SP2
        PLINE = PLINE:EST.RL.DIE.NO.ARD<1,1,D> "R#2":SP2:EST.RL.DIE.CYL.ID<1,1,D> "L#9":SP2:EST.RL.DIE.QTY<1,1,D> "R#3"
        IF EST.RL.DIE<1,1,D+1> # "" THEN
          PLINE = PLINE:SP5:B:EST.RL.DIE<1,1,D+1> "L#9":SP2:OCONV(EST.RL.DIE.REPEAT<1,1,D+1>,"MD4") "R#7":SP2:EST.RL.DIE.NO.ACR<1,1,D+1> "R#2":SP2
          PLINE = PLINE:EST.RL.DIE.NO.ARD<1,1,D+1> "R#2":SP2:EST.RL.DIE.CYL.ID<1,1,D+1> "L#9":SP2:EST.RL.DIE.QTY<1,1,D+1> "R#3":SP3
        END ELSE
          PLINE = PLINE:SPACE(60)
        END
        IF EST.RL.CYL.PRICE # "" THEN
          IF D = 1 THEN PLINE = PLINE:"*   CYLINDERS @   *"
          IF D = 3 THEN PLINE = PLINE:"*  ":OCONV(EST.RL.CYL.PRICE,"MD2") "R#7":" EACH   *"
          IF D = 5 THEN PLINE = PLINE:"* * * * * * * * * *"
        END
        PRINT PLINE
        LINE.CNT = LINE.CNT + 1
      NEXT D
      IF EST.RL.CYL.PRICE # "" THEN
        BEGIN CASE
        CASE DCNT = 0
          PRINT SPACE(112):"*   CYLINDERS @   *"
          PRINT SPACE(112):"*  ":OCONV(EST.RL.CYL.PRICE,"MD2") "R#7":" EACH   *"
          PRINT SPACE(112):"* * * * * * * * * *"
          LINE.CNT = LINE.CNT + 3
        CASE DCNT < 3
          PRINT SPACE(112):"*  ":OCONV(EST.RL.CYL.PRICE,"MD2") "R#7":" EACH   *"
          PRINT SPACE(112):"* * * * * * * * * *"
          LINE.CNT = LINE.CNT + 2
        CASE DCNT < 5
          PRINT SPACE(112):"* * * * * * * * * *"
          LINE.CNT = LINE.CNT + 1
        END CASE
      END
      IF (LINE.CNT + 18) > 55 THEN GOSUB 10000
      PRINT;PRINT
      PLINE =  "LABEL SIZE      : ":OCONV(EST.RL.DIM.AC,"MD4") "R#7":" ACROSS X "
      PRINT PLINE:OCONV(EST.RL.DIM.AR,"MD4") "R#7":" AROUND"
      PRINT
      PRINT "COPY DIRECTION  : ":EST.RL.CPY.DIR
      PLINE = "GANG RUN        : "
      IF EST.RL.KINDS > 1 THEN
        PLINE = PLINE:"YES"
      END ELSE
        PLINE = PLINE:"NO "
      END
      PLINE = PLINE:SPACE(20):"# ADD'L KINDS    : ":EST.RL.KINDS "L#3"
      PLINE = PLINE:SPACE(29):"# ADD'L VERSIONS  : ":EST.RL.VERSION.CHANGES "L#3"
      PRINT PLINE
      PLINE = "LAMINATE        : "
      IF EST.RL.LAM = "Y" THEN
        MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:"R":EST.RL.LAM.USAGE<1,1,1> ELSE MAT ESTM.REC = ""
        LOCATE EST.RL.LAM.PROD<1,1,1> IN ESTM.PROD<1>,1 SETTING POS ELSE ESTM.FULL.DESC<1,POS> = UNKNOWN
        PLINE = PLINE:"YES"
      END ELSE
*       INV.FULL.DESC = ""
        ESTM.FULL.DESC = ""
        POS = 1
        PLINE = PLINE:"NO "
      END
      PLINE = PLINE:SPACE(20):ESTM.FULL.DESC<1,POS> "L#45"
      PRINT PLINE
      PLINE = "COLORS FRONT    : ":EST.PROD.COLORS.1 "L#2":SPACE(21):"COLORS BACK      : ":EST.PROD.COLORS.2 "L#2"
      PRINT PLINE
      IF EST.PROD.INK.VARNISH = "Y" THEN
        PLINE = "VARNISH         : TYPE ":EST.RL.VARNISH.TYPE "L#2":SPACE(33)
        IF EST.RL.FULL.SPOT = "F" THEN
          PLINE = PLINE:": FULL"
        END ELSE
          PLINE = PLINE:": SPOT"
        END
        PRINT PLINE
      END
      IF EST.RL.COLOR.MATCH # "" THEN
        PLINE = "STANDARD MATCH  : ":EST.RL.STD.MATCH "L#2":SPACE(21):"PREMIUM MATCH    : ":EST.RL.PREM.MATCH "L#2"
        PRINT PLINE
      END
      PLINE = ""
      IF EST.RL.COPY.CHANGES # "" THEN
        PLINE = "ADDT'L COPY CHNG: ":EST.RL.COPY.CHANGES "L#3":SPACE(20)
      END
      IF EST.RL.COLOR.CHANGES # "" THEN
        IF PLINE = "" THEN PLINE = SPACE(41)
        PLINE = PLINE:"ADDT'L COLOR CHNG: ":EST.RL.COLOR.CHANGES "L#3":SPACE(28)
      END
      IF EST.RL.VERSION.CHANGES # "" THEN
        IF PLINE = "" THEN PLINE = SPACE(91)
        IF LEN(PLINE) = 41 THEN PLINE = PLINE:SPACE(50)
        PLINE = PLINE:"ADDT'L VERSION CHNG: ":EST.RL.VERSION.CHANGES "L#3":SPACE(17)
      END
      IF PLINE # "" THEN PRINT PLINE
      PLINE = "LINEAL SLIT     : "
      IF EST.RL.LINEAL.SLIT = "Y" THEN
        PLINE = PLINE:EST.RL.NO.SLITS "L#2":SPACE(21)
      END ELSE
        PLINE = PLINE:"NO":SPACE(21)
      END
      IF EST.RL.ROLLS = "Y" THEN
        PLINE = PLINE:"#/ROLL           : ":EST.RL.NO.PER.ROLL "L#5":SPACE(26)
      END
      IF EST.RL.CORE = "Y" THEN
        PLINE = PLINE:"CORE               : YES"
      END
      PRINT PLINE
      IF EST.RL.ROLLS = "Y" THEN
        PLINE = "CORE TYPE       : "
        IF EST.RL.REG.HDUTY = "R" THEN
          PLINE = PLINE:"REG":SPACE(20)
        END ELSE
          PLINE = PLINE:"HD ":SPACE(20)
        END
        PLINE = PLINE:"CORE/MAND DIAM   : ":OCONV(EST.RL.CORE.SIZE,"MD2") "L#4":SPACE(27)
        PLINE = PLINE:"MAXIMUM OD         : ":OCONV(EST.RL.MAX.OD,"MD2") "L#5"
        PRINT PLINE
      END
      PLINE="APPLIED         : "
      BEGIN CASE
      CASE EST.RL.APPLY = "H"
         PLINE=PLINE:"HAND"
      CASE EST.RL.APPLY = "M"
         PLINE=PLINE:"MACHINE"
      CASE EST.RL.APPLY = "P"
         PLINE=PLINE:"PHARMACEUTICAL"
      END CASE
      PRINT PLINE
      IF EST.RL.SHEETS = "Y" THEN
        PLINE = "SHEETED         : YES":SPACE(20):"#/PACKAGE        : ":EST.RL.NO.PER.PKG "L#5"
        PRINT PLINE
      END
      IF EST.RL.FANFOLD = "Y" THEN
        PLINE = "FANFOLD         : YES":SPACE(20):"#/BATCH          : ":EST.RL.NO.PER.BATCH "L#6":SPACE(25):"#/FOLD             : ":EST.RL.NO.PER.FOLD "L#4"
        PRINT PLINE
      END
      IF EST.RL.SHRK.WRP = "Y" THEN
        PLINE = "SHRINK WRAP     : YES"
        PRINT PLINE
      END
      IF EST.RL.CORELABELS = "Y" THEN
        PLINE = "CORE LABELS     : YES"
        PRINT PLINE
      END
      IF EST.RL.PKG.LABELS = "Y" THEN
        PLINE = "CARTON LABELS   : YES"
        PRINT PLINE
      END
      GOTO 99999
*
*---- GET SPECIAL INSTRUCTIONS
*
5000 *
      SCNT=0; SREC=""
      COMP = DCOUNT(EST.PROD.OS.USAGE,VM)
      FOR C = 1 TO COMP
        OP=0; OLD.DESC = ""
        FOR LP = 1 TO DC
          IF EST.COMMENT.TYPE<1,LP> = C THEN
            OP = OP + 1
            OLD.DESC<1,OP> = EST.COMMENTS<1,LP>
          END
        NEXT LP
        CALL DESC.REFORMAT (OLD.DESC,45,NEW.DESC)
        LP = DCOUNT(NEW.DESC,VM)
        FOR L = 1 TO LP
          SCNT = SCNT + 1
          SREC<SCNT> = NEW.DESC<1,L>
        NEXT L
      NEXT C
      RETURN
*
*---- PRINT PAGE HEADINGS
*
10000 *
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1 : PAGE.NO
      PRINT HD2
      PRINT
      PRINT "ESTIMATE : ":EST.KEY
      PRINT
      LINE.CNT = 6
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      PRINTER OFF
      RETURN
      END

