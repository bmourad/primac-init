   SUBROUTINE EST.FORMULA.Q044 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q044
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate the number of square inches of
* foil required for stamping.
*
*T26258 edwin 11/07/2001 * Enable formula to handle Outside purchase
*                          orders.
*T27924 cmykleb 03/12/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JCS.CPYLIB>CATEGORY.OSP
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
   MAT SYSCOM.REC = "" ; SYS.TYPE = 2
*
*---- INITIALIZATION
*
   OPEN "","CATEGORY.OSP" TO CATEGORY.OSP ELSE
      ERRMSG = "CANNOT OPEN CATEGORY.OSP FILE"
      GOSUB 91000
      GOTO 99999
   END
   TEMP.QTY = ""
   TYP = 0
   IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
*
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*T26351 v
   CQTY = EST.COMP.QTY<1,COMP,QPTR>
   BEGIN CASE
      CASE CQTY = ""
         COMPQTY = EQTY
      CASE (CQTY[1,1]="+" OR CQTY[1,1]="-") AND CQTY[LEN(CQTY),1]="%"
         COMPQTY = INT(EQTY * (1 + (CQTY[1,LEN(CQTY)-1]/100)) + 0.5)
      CASE CQTY[1,1]="+" OR CQTY[1,1]="-"
         COMPQTY = EQTY + CQTY
      CASE 1
         COMPQTY = CQTY
   END CASE
   IF COMPQTY LT 0 THEN COMPQTY = 0
*T26351 ^
10*
   IF TEMP.TYPE[1,1] # 'P' THEN             ; *T26258
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MP ELSE 
         ERRMSG = "Material '":TEMP.OPV:"' not on file"
         GOSUB 91000
         RETURN
      END
      IF QPTR = 1 AND ACTION # "R" THEN
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=14;SCALER=2;MAXL=5;MINV=1;MAXV=9999;JUSTIFY="L"
         BEGIN CASE
            CASE TEMP.FVAR1 # ""
               DEFAULT = TEMP.FVAR1 / 100
            CASE 1
               DEFAULT = ""
         END CASE
         IF DEFAULT = "" THEN
            PMSG = "Square inches of foil per piece ?"
         END ELSE
            O.R = "O"
            PMSG = "Square inches of foil per piece (Default = ":DEFAULT:") ?"
         END
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         SQ.IN = VALUE
         TEMP.FVAR1 = VALUE
      END ELSE
         SQ.IN = TEMP.FVAR1
      END
*T26351 v
*     PROD.QTY = EQTY
      PROD.QTY = COMPQTY
*T26351 ^
      TEMP.QTY = INT(PROD.QTY * SQ.IN + 0.99)
      TEMP.QTY = TEMP.QTY / 100
      TEMP.FCTR = 1000
      TEMP.STD = ESTM.COST<1,MP> / 100
      TEMP.STD.TYPE = "$/":ESTM.UM<1,MP>
      TEMP.UM = ESTM.COST.UNIT<1,MP>
      TEMP.IND.1 = ESTM.FOH.PCT<1,MP>
      TEMP.MARKUP = ESTM.MARKUP<1,MP>
      TEMP.DESC = ESTM.FULL.DESC<1,MP>
      TEMP.VSALE = "Y"
   END ELSE           ; *T26258 v
      IF QPTR = 1 AND ACTION # "R" THEN
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=14;SCALER=2;MAXL=5;MINV=1;MAXV=9999;JUSTIFY="L"
         BEGIN CASE
            CASE TEMP.FVAR1 # ""
               DEFAULT = TEMP.FVAR1 / 100
            CASE 1
               DEFAULT = ""
         END CASE
         IF DEFAULT = "" THEN
            PMSG = "Square inches of foil per piece ?"
         END ELSE
            O.R = "O"
            PMSG = "Square inches of foil per piece (Default = ":DEFAULT:") ?"
         END
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         SQ.IN = VALUE
         TEMP.FVAR1 = VALUE
      END ELSE
         SQ.IN = TEMP.FVAR1
      MATREAD CAOS.REC FROM CATEGORY.OSP, CONO:TEMP.CODE ELSE MAT CAOS.REC=''
      BEGIN CASE
         CASE TEMP.TYPE = "PM"
            TEMP.STD.TYPE = "$/M"
            TEMP.UM = 1000
         CASE TEMP.TYPE = "PC"
            TEMP.STD.TYPE = "$/C"
            TEMP.UM = 100
         CASE TEMP.TYPE = "PT"
            TEMP.STD.TYPE = "$/TTL"
            TEMP.UM = 100
         CASE 1
            TEMP.STD.TYPE = "$/EA"
            TEMP.UM = 1
      END CASE
      PROD.QTY = EQTY
      TEMP.QTY = INT(PROD.QTY * SQ.IN + 0.99)
      TEMP.QTY = TEMP.QTY / 100
      TEMP.FCTR = 1000
      TEMP.STD = ""
      TEMP.IND.1 = CAOS.FOH.PCT
      TEMP.MARKUP = CAOS.MARKUP
      TEMP.DESC = CAOS.DESC
      TEMP.VSALE = "Y"
   END                ; *T26258 ^
   RETURN
*
90000 ERR.TYPE = 1 ; CALL SI_SYSCOM(MAT SYSCOM.REC) ; RETURN
91000 ERR.TYPE = 1 ; CALL SI_SYSCOM(MAT SYSCOM.REC) ; RETURN
*
99999 * 
END
