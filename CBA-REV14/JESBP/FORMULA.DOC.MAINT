***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - FORMULA.DOC.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/15/86
*
* DESCRIPTION
*
* Maintain estimate formula documentation.
*
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>COMMON3
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
*--- SETUP SYSTEM ERROR MESSAGES
*
  SYS.TYPE = 1
  CALL SI_SYSCOM(MAT SYSCOM.REC)
*
  MAT EDIT.COM = "";TYP=0;CALL EDIT.SUB
*
*
*---- OPEN ALL FILES
*
  OPEN "","FORMULA.DOC" TO FORMULA.DOC ELSE
    P_X  = 3 ; P_Y = 23 ; P_VALUE = "CANNOT OPEN FORMULA.DOC FILE" ; P_OPT = ""
    CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    STOP
  END
*
*---- INITIALIZATION
*
  PROMPT ""
  TYP = 0
  CALL EDIT.SUB
10*
*       PRINT @(-1):
*      PRINT @(20,11):CL:"Formula suffix ? ":
*      INPUT DOC.ID:
  TYP=1;X=20;Y=11;PMSG="Formula suffix ?";CALL EDIT.SUB
  DOC.ID = VALUE
  IF DOC.ID = "" OR DOC.ID = "END" THEN GOTO 99999
  FIRST.LINE = 2
  LINE.COUNT = 20
  LINE.SPACE = 1
  READU DOC.REC FROM FORMULA.DOC, DOC.ID ELSE
    DOC.REC = ""
  END
  CURR.REF.NO = ""
  DOC.CNT = COUNT(DOC.REC, AM) + (DOC.REC # "")
*
*---- MAIN PROCESSING
*
100*
  GOSUB 80000
  REF.NO = 1
  GOSUB 50000
*
*---- GET OPERATOR REQUEST
*
500*
  GOSUB 11100
  OPTION = VALUE
  BEGIN CASE
    CASE OPTION = "A" AND DOC.CNT < 200
      DONE = 0
      FOR REF.NO = DOC.CNT+1 TO 200 UNTIL DONE
        GOSUB 50000
        GOSUB 10200
        IF VALUE = "END" THEN
          DONE = 1
          DOC.REC = DELETE(DOC.REC,REF.NO,0,0)
        END ELSE
          DOC.CNT = DOC.CNT + 1
        END
      NEXT REF.NO
      REF.NO = DOC.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPTION = "C" AND DOC.CNT > 0
      GOSUB 11200
      IF VALUE # "" AND VALUE # "END" THEN
        REF.NO = VALUE
        GOSUB 10200
      END
    CASE OPTION = "D" AND DOC.CNT > 0
      GOSUB 11200
      IF VALUE # "" AND VALUE # "END" THEN
        REF.NO = VALUE
        DOC.REC = DELETE(DOC.REC,REF.NO,0,0)
        DOC.CNT = DOC.CNT - 1
        IF REF.NO > DOC.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPTION = "I" AND DOC.CNT > 0
      GOSUB 11200
      IF VALUE # "" AND VALUE # "END" THEN
        REF.NO = VALUE
        DOC.REC = INSERT(DOC.REC,REF.NO,0,0,"")
        DOC.CNT = DOC.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10200
        IF VALUE = "END" THEN
          DOC.REC = DELETE(DOC.REC,REF.NO,0,0)
          DOC.CNT = DOC.CNT - 1
        END
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPTION = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > DOC.CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPT = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPT = 'SB'
      REF.NO = DOC.CNT
      GOSUB 50000
    * T25978 ^
    CASE OPTION = "E"
      RELEASE FORMULA.DOC, DOC.ID
      GOTO 10
    CASE OPTION = "F"
      IF DOC.REC = "" THEN
        DELETE FORMULA.DOC, DOC.ID
      END ELSE
        WRITE DOC.REC ON FORMULA.DOC, DOC.ID
      END
      GOTO 10
    CASE 1
      ERRMSG = "Invalid Selection"
      GOSUB 91000
  END CASE
  GOTO 500
*
*---- GET DOCUMENTATION LINE
*
10200*
  Y = FIRST.LINE + MOD((REF.NO-1),LINE.COUNT)*LINE.SPACE
  P_X  = 0 ; P_Y = Y ; P_VALUE = REF.NO "R#3" ; P_OPT = ""
  CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
10300*
  X = 5
  O.R = "O"; DEFAULT = DOC.REC<REF.NO>[1,70]
  PVALUE = DEFAULT
  MAXL = 70
  CALL EDIT.SUB
  IF VALUE # "END" THEN
    DOC.REC<REF.NO> = VALUE
  END
  RETURN
*
*---- GET LINE DETAIL ACTION REQUEST
*
11100*
  X = 0
  Y = 23
  PMSG = CL:"(A)dd, (C)hange, (D)elete, (I)nsert, (S)croll, (E)xit, (F)ile"
  * T25978 v
  * VALDAT = "A":VM:"C":VM:"D":VM:"I":VM:"S":VM:"E":VM:"F"
  VALDAT = "A":VM:"C":VM:"D":VM:"I":VM:"S":VM:"E":VM:"F":VM:'SR':VM:'SB':VM:'ST'
  * T25978 ^
  CALL EDIT.SUB
  RETURN
*
*---- GET LINE REFERENCE NUMBER
*
11200*
  TYP = 3
  X = 0
  Y = 23
  MAXL = 3
  O.R = "O"
  JUSTIFY = "L"
  PMSG = CL:"Line Number"
  MINV = CURR.REF.NO
  MAXV = MINV + LINE.COUNT - 1
  IF MAXV > DOC.CNT THEN MAXV = DOC.CNT
  CALL EDIT.SUB
  RETURN
*
*---- DISPLAY PAGE
*
50000*
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  VAL = START.REF.NO
  CNT = COUNT(DOC.REC, AM) + (DOC.REC # "")
  LAST.LINE = FIRST.LINE + (LINE.COUNT * LINE.SPACE) - 1
  FOR Y = FIRST.LINE TO LAST.LINE
    IF VAL > CNT THEN
      P_X  = 0 ; P_Y = Y ; P_VALUE = "" ; P_OPT = "CL"
      CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END ELSE
      P_X  = 0 ; P_Y = Y ; P_VALUE = VAL "R#3" ; P_OPT = ""
      P_X  := AM:5 ; P_Y := AM:Y ; P_VALUE := AM:DOC.REC<VAL> "L#70"
      CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
    END
    VAL = VAL + 1
  NEXT Y
  RETURN
*
*---- DISPLAY SCREEN
*
80000*
*       PRINT @(-1):
  FORMULA.NAME = "EF.":DOC.ID
  P_X  = 0 ; P_Y = 0 ; P_VALUE = FORMULA.NAME ; P_OPT = ""
  P_X  := AM:0 ; P_Y := AM:1 ; P_VALUE := AM:STR("=",LEN(FORMULA.NAME))
  CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RETURN
*
*---- ERROR ROUTINE
*
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
91000 *
  ERR.TYPE = 1;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
92000 *
  ERR.TYPE = 2;CALL SI_SYSCOM(MAT SYSCOM.REC); RETURN
93000 *
  ERR.TYPE = 3;CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- END OF PROGRAM
*
99999*
*       PRINT @(-1):
  STOP
END
