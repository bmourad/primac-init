*********************************************************************
*
*Copyright 1982 by Computer Business Associates (Vercom Software Inc)
*
* PROGRAM  - EST.RL.MR.LIST
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/26/92
*
* DESCRIPTION
*
* This program produces the Roll Label M/R default listing.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- READ PARAMETERS
*
      PROCREAD PARAM ELSE
         ERRMSG = "THIS REPORT MUST RUN FROM A PROC"
         GOSUB 90000
         GOTO 99999
      END
      CONO  = PARAM<1>
      CNAME = PARAM<2>
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE
         ERRMSG = "CANNOT OPEN COMPANY FILE"
         GOSUB 90000
         GOTO 95000
      END
      OPEN "","CONTROL" TO CONTROL ELSE
         ERRMSG = "CANNOT OPEN CONTROL FILE"
         GOSUB 90000
         GOTO 95000
      END
      OPEN "","DIVISION" TO DIVISION ELSE
         ERRMSG = "CANNOT OPEN DIVISION FILE"
         GOSUB 90000
         GOTO 95000
      END
*
*---- INITIALIZATION
*
      TODAY = DATE()
      SP1 = SPACE(1)
      SP2 = SPACE(2)
      SP3 = SPACE(3)
      LINE.COUNT = 99
      PAGE.NO = 0
      PRINT.FLAG = 0
*
*---- DEFINE HEADINGS
*
      REPORT.NAME = "M/R DEFAULT LISTING"
      RNO = ""
      HD1 = ""
      HD2 = ""
      REPORT.NUMBER = "XXXX"
      HD1B=SPACE(INT((100-LEN(CNAME))/2)):CNAME
      HD1B=HD1B "L#100"
      HD1=HD1:HD1B:"   PAGE "
      HD2="REPORT # : ":REPORT.NUMBER
      HD2=HD2:SPACE(36):" M / R   D E F A U L T   L I S T I N G "
      BH1 = "Division: "
      DIM SH(20)
      MAT SH = ""
      SH(1) = "M/R hours per die ............. "
      SH(2) = "M/R hours per premium ......... "
      SH(3) = "M/R hours per standard ........ "
      SH(4) = "M/R hours for backprint ....... "
      SH(5) = "M/R hours per web ............. "
      SH(6) = "M/R hours per lineal slit ..... "
      SH(7) = "M/R hours per copy change ..... "
      SH(8) = "M/R hours per color change .... "
      SH(9) = "M/R hours per version change .. "
*
*---- MAIN PROCESSING
*
100 *
      PRINTER ON
      DATA = 1
      LOOP
         READNEXT ID ELSE DATA = 0
      WHILE DATA DO
         IF ID[1,3] = CONO THEN
            SID = ID[4,99]
            READ MRTBL FROM CONTROL, CONO:SID THEN
               DIV = ID[4,2]
               MATREAD DIV.REC FROM DIVISION, CONO:DIV ELSE
                  MAT DIV.REC = ""
                  DIV.DESC = "???????????????"
               END
               GOSUB 1000
            END
         END
      REPEAT
      IF NOT(PRINT.FLAG) THEN
         MRTBL = ""
         GOSUB 10000
         PRINT
         PRINT "NO ITEMS SELECTED"
      END
      PRINTER OFF
      PRINTER CLOSE
      GOTO 99999
*
*---- PRINT DETAIL LINE(S)
*
1000 *
      LCNT = COUNT(MRTBL,AM)+1
      IF (LINE.COUNT + 4 + LCNT) > 55 THEN GOSUB 10000
      PRINT
      PRINT
      PRINT BH1:ID[4,2]:SP3:DIV.DESC
      PRINT
      FOR N = 1 TO LCNT
         PRINT SH(N):OCONV(MRTBL<N>,"MD2")
      NEXT N
      LINE.COUNT = LINE.COUNT + 4 + LCNT
      PRINT.FLAG = 1
      RETURN
*
*---- PRINT HEADINGS
*
10000 *
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1: PAGE.NO
      PRINT HD2
      PRINT
      LINE.COUNT = 3
      RETURN
*
*---- ERROR ROUTINE
*
90000 *
      PRINT @(0,23):@(-4):ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):@(-4):
      RETURN
*
*---- ABNORMAL END OF JOB
*
95000 *
      PROCWRITE "END"
      STOP
*
*---- END OF JOB
*
99999 *
      STOP
   END
