      SUBROUTINE EST.COST.SALE.SUM (CONO, EST.KEY, DSEL, CSEL, QSEL)
*********************************************************************
*
* Copyright 1982 Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.COST.SALE.SUM
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 09/25/92
*
* DESCRIPTION
*
* This subroutine displays the following data for all quantities.
*
*   Fixed, Variable and Total Cost
*   Cost per thousand
*   Cost per additional thousand
*   Fixed, Variable and Total Price (includes overall markup)
*   Price per thousand (includes overall markup)
*   Price per additional thousand (includes overall markup)
*   Profit margin dollars
*   Profit margin percent
*
* Data may be selected for one or all components.
*
* MOD TASK 17811 DMT 4/19/94 ALLOW 99 QUANTITIES
* MOD TASK 17918 DMT 8/19/94 ADD BACKMARGIN CALC
*T22072 ct6 07/14/1997 * CLEAR DATA UPON SCREEN FORMAT TO PREVENT
*                        GARBAGE DATA FROM BEING PASSED FROM PREVIOUS
*                        SCREEN AT THIS SCREEN LEVEL.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- DIMENSIONED VARIABLES
*
      DIM TOT.REC(12)
      EQU TOTAL.FCOST           TO TOT.REC(1)
      EQU TOTAL.VCOST           TO TOT.REC(2)
      EQU TOTAL.TCOST           TO TOT.REC(3)
*
      EQU TOTAL.FSALE           TO TOT.REC(6)
      EQU TOTAL.VSALE           TO TOT.REC(7)
      EQU TOTAL.TSALE           TO TOT.REC(8)
      EQU TOTAL.FSALE.MO        TO TOT.REC(9)
      EQU TOTAL.VSALE.MO        TO TOT.REC(10)
      EQU TOTAL.TSALE.MO        TO TOT.REC(11)
*
*---- INITIALIZATION
*
      SCREEN DEFINE;EST.COST.SALE.SUM
      SCREEN FORMAT
*T22072v
      SCREEN CLEAR
*T22072^
      COMP = CSEL
*
*---- MAIN PROCESSING
*
100 *
      GOSUB 80000
      SCREEN DISPLAY;;ALL
      S$VAL=1
      S$CNT=3
      SCREEN MULTI;;C;11;12;13
      S$VAL=1
      S$CNT=2
      SCREEN MULTI;;C;16;17;18
      S$VAL=1
      S$CNT=3
      SCREEN MULTI;;C;31;32;33
      S$VAL=1
      S$CNT=2
      SCREEN MULTI;;C;36;37;38
      S$VAL=1
      S$CNT=2
      SCREEN MULTI;;C;41;42;43
*
*---- GET OPERATOR REPLY
*
500 *
      BEGIN CASE
      CASE EST.COMPONENT.CNT > 1
         SCREEN FIELD;;21
         SCREEN INPUT;;21
      CASE 1
         SCREEN FIELD;;22
         SCREEN INPUT;;22
      END CASE
      OPTION = S$VALUE
      BEGIN CASE
      CASE OPTION = "" OR OPTION = "END"
         GOTO 99999
      CASE OPTION = "C"
         GOSUB 550
         IF S$VALUE = "" OR S$VALUE = "END" THEN GOTO 500
         IF S$VALUE = COMP THEN GOTO 500
         COMP = S$VALUE
         SCREEN CLEAR
         GOTO 100
      END CASE
      GOTO 500
*
*---- GET COMPONENT NUMBER
*
550 *
      SCREEN FIELD;;23
      S$VALDAT = "ALL"
      FOR N = 1 TO EST.COMPONENT.CNT
         S$VALDAT = S$VALDAT:",":N
      NEXT N
      SCREEN INPUT;;23;RETURN
      RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
      MAT TOT.REC = ""
      MAX.QC = DCOUNT(EST.QTY,VM)
      BEGIN CASE
         CASE MAX.QC = 1
            START.QC = 1
            END.QC = 1
         CASE MAX.QC LE 3
            START.QC = 1
            END.QC = MAX.QC
         CASE 1
            LOCATE QSEL IN EST.QTY<1>,1 SETTING START.QC ELSE START.QC = 1
            IF START.QC + 3 > MAX.QC THEN
               START.QC = MAX.QC - 2
            END
            END.QC = START.QC + 2
      END CASE
      DC = DCOUNT(EST.DEPT.COMP,VM)
      FOR DP = 1 TO DC
         CNUM = FIELD(EST.DEPT.COMP<1,DP>,"!",2)
         IF COMP = "ALL" OR CNUM = COMP THEN
            EQTY = FIELD(EST.DEPT.COMP<1,DP>,"!",3)
            LOCATE EQTY IN EST.QTY<1>,1 SETTING EP THEN
               TOTAL.TCOST<1,EP>=TOTAL.TCOST<1,EP>+EST.DEPT.COMP.COST<1,DP>
               TOTAL.VCOST<1,EP>=TOTAL.VCOST<1,EP>+EST.DEPT.COMP.VCOST<1,DP>
               TOTAL.FCOST<1,EP>=TOTAL.TCOST<1,EP>-TOTAL.VCOST<1,EP>
               TOTAL.TSALE<1,EP>=TOTAL.TSALE<1,EP>+EST.DEPT.COMP.TSALE<1,DP>
               TOTAL.VSALE<1,EP>=TOTAL.VSALE<1,EP>+EST.DEPT.COMP.VSALE<1,DP>
               TOTAL.FSALE<1,EP>=TOTAL.TSALE<1,EP>-TOTAL.VSALE<1,EP>
            END
         END
      NEXT DP
      ECNT = DCOUNT(EST.QTY,VM)
      FOR EP = 1 TO ECNT
         TOTAL.TSALE.MO<1,EP>=TOTAL.TSALE<1,EP> + (INT(OCONV(TOTAL.TSALE<1,EP>,'MD2') * OCONV(EST.OM.PCT<1,EP>,'MD4') + .005 / 100))
         TOTAL.VSALE.MO<1,EP>=TOTAL.VSALE<1,EP> + (INT(OCONV(TOTAL.VSALE<1,EP>,'MD2') * OCONV(EST.OM.PCT<1,EP>,'MD4') + .005 / 100))
         TOTAL.FSALE.MO<1,EP>=TOTAL.TSALE.MO<1,EP>-TOTAL.VSALE.MO<1,EP>
      NEXT EP
      S$DATA(1)<S$SCR> = DATE()
      IF COMP = "ALL" THEN
         S$DATA(2)<S$SCR> = "ALL COMPONENTS"
      END ELSE
         S$DATA(2)<S$SCR> = EST.PROD.DESC<1,COMP>
      END
      Q = 0
      FOR QPTR = START.QC TO END.QC
         Q = Q + 1
         FN = 5 - 1 + Q
         QTY = EST.QTY<1,QPTR>
         PQTY = OCONV(QTY,"MD0,")
         S$DATA(FN)<S$SCR> = PQTY:SPACE(INT((12-LEN(PQTY))/2))
         FN = 11 - 1 + Q
         S$DATA(FN)<S$SCR,1> = OCONV(TOTAL.FCOST<1,QPTR>,"MD2Z,")
         S$DATA(FN)<S$SCR,2> = OCONV(TOTAL.VCOST<1,QPTR>,"MD2Z,")
         S$DATA(FN)<S$SCR,3> = OCONV(TOTAL.TCOST<1,QPTR>,"MD2Z,")
         FN = 16 - 1 + Q
         COST.PER.1000=INT(TOTAL.TCOST<1,QPTR>/(QTY/1000)+0.5)
         S$DATA(FN)<S$SCR,1> = OCONV(COST.PER.1000,"MD2Z,")
         COST.ADD.1000=INT(TOTAL.VCOST<1,QPTR>/(QTY/1000)+0.5)
         S$DATA(FN)<S$SCR,2> = OCONV(COST.ADD.1000,"MD2Z,")
         FN = 31 - 1 + Q
         S$DATA(FN)<S$SCR,1> = OCONV(TOTAL.FSALE.MO<1,QPTR>,"MD2Z,")
         S$DATA(FN)<S$SCR,2> = OCONV(TOTAL.VSALE.MO<1,QPTR>,"MD2Z,")
         S$DATA(FN)<S$SCR,3> = OCONV(TOTAL.TSALE.MO<1,QPTR>,"MD2Z,")
         FN = 36 - 1 + Q
         SALE.PER.1000=INT(TOTAL.TSALE.MO<1,QPTR>/(QTY/1000)+0.5)
         S$DATA(FN)<S$SCR,1> = OCONV(SALE.PER.1000,"MD2Z,")
         SALE.ADD.1000=INT(TOTAL.VSALE.MO<1,QPTR>/(QTY/1000)+0.5)
         S$DATA(FN)<S$SCR,2> = OCONV(SALE.ADD.1000,"MD2Z,")
         FN = 41 - 1 + Q
         PROFIT = TOTAL.TSALE.MO<1,QPTR> - TOTAL.TCOST<1,QPTR>
         S$DATA(FN)<S$SCR,1> = OCONV(PROFIT,"MD2Z,")
         IF TOTAL.TSALE.MO<1,QPTR>+0 # 0 THEN
            MARGIN = INT(PROFIT * 100 / TOTAL.TSALE.MO<1,QPTR> * 100 + 0.5)
            S$DATA(FN)<S$SCR,2> = OCONV(MARGIN,"MD2Z,")
         END
      NEXT QPTR
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999 *
      SCREEN CLEAR;;D
      RETURN
      END
