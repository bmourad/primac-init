   SUBROUTINE EST.INSERT.STK (CONO, EQTY, DEPT, COMP, REF.NO)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.INSERT.STK
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/28/87
*
* DESCRIPTION
*
* This subroutine updates the ESTIMATE.DEPT data with data from the
* paper or material file.
*T25614 cm 01/29/2001 * Problem when product is changed in Product screen
*                       prod info is not being pulled into the est dept
*                       record correctly.
*T27716 cmykleb 10/09/2003 * Add Pre-pres inclusive functionality.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QP ELSE RETURN
*
*---- MAIN PROCESSING
*
   GRP.ID = TEMP.GRP.ID
   GRP.QTY = TEMP.GRP.QTY
   GRP.FCTR = TEMP.GRP.FCTR
   GRP.TYPE = TEMP.TYPE
   GRP.CCTR = TEMP.CCTR
   GRP.QCALC = TEMP.GRP.QCALC
   GRP.QPARAM = TEMP.GRP.QPARAM
   GRP.QOR = TEMP.GRP.QOR
   GRP.FCALC = TEMP.GRP.FCALC
   GRP.FPARAM = TEMP.GRP.FPARAM
   GRP.FOR = TEMP.GRP.FOR
*** CSF TEST DT 5/11/95 NEXT LINE ADDED
   GRP.GRP.CCTR = TEMP.GRP.CCTR
*** CSF TEST DT 5/11/95 END TEST
   WCNT = EST.PROD.WEB.CNT<1,COMP>+1
   IF WCNT = 0 THEN WCNT = 1
   FOR MPTR = 1 TO WCNT
      MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,MPTR> ELSE GOTO 390
      MAT ESTD.TEMP = ""
      TEMP.GRP.ID = GRP.ID
      TEMP.GRP.QTY = GRP.QTY
      TEMP.GRP.FCTR = GRP.FCTR
      TEMP.TYPE = "M":EST.PROD.OS.TYPE<1,COMP>
      TEMP.CCTR = GRP.CCTR
      TEMP.GRP.QCALC = GRP.QCALC
      TEMP.GRP.QPARAM = GRP.QPARAM
      TEMP.GRP.QOR = GRP.QOR
      TEMP.GRP.FCALC = GRP.FCALC
      TEMP.GRP.FPARAM = GRP.FPARAM
      TEMP.GRP.FOR = GRP.FOR
*** CSF TEST DT 5/11/95 NEXT LINE ADDED
      TEMP.GRP.CCTR = GRP.GRP.CCTR
*** CSF TEST DT 5/11/95 END TEST
      TEMP.OPV = EST.PROD.OS.PROD<1,COMP,MPTR>
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MT.PTR ELSE GOTO 390
*T25614  IF TEMP.GRP.QCALC = "" THEN
      TEMP.GRP.QCALC = ESTM.QCALC<1,MT.PTR>
      TEMP.GRP.QPARAM = ESTM.QPARAM<1,MT.PTR>
      TEMP.GRP.QOR = ESTM.QOR<1,MT.PTR>
*T25614  END
*T25614  IF TEMP.GRP.FCALC = "" THEN
      TEMP.GRP.FCALC = ESTM.FCALC<1,MT.PTR>
      TEMP.GRP.FPARAM = ESTM.FPARAM<1,MT.PTR>
      TEMP.GRP.FOR = ESTM.FOR<1,MT.PTR>
*T25614  END
      TEMP.MPTR = MPTR
      TEMP.FVAR1 = MT.PTR
      BEGIN CASE
         CASE TEMP.GRP.QCALC = ""
            TEMP.QTY = ""
         CASE TEMP.GRP.QCALC = "C"
            TEMP.QTY = TEMP.GRP.QPARAM
         CASE TEMP.GRP.QCALC = "F"
            TEMP.FPTR = REF.NO + 1
            SUB.NAME = "EST.FORMULA.Q":TEMP.GRP.QPARAM
            CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
            IF TEMP.QTY = "" THEN
               TEMP.GRP.QOR = ""
            END
      END CASE
      BEGIN CASE
         CASE TEMP.GRP.FCALC = ""
            TEMP.FCTR = 1000
         CASE TEMP.GRP.FCALC = "C"
            TEMP.FCTR = INT(TEMP.GRP.FPARAM*1000+0.5)
         CASE TEMP.GRP.FCALC = "M"
            TEMP.FCTR = INT(GRP.FCTR*TEMP.GRP.FPARAM+0.5)
         CASE TEMP.GRP.FCALC = "F"
            TEMP.FPTR = REF.NO + 1
            SUB.NAME = "EST.FORMULA.F":TEMP.GRP.FPARAM
            CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
            IF TEMP.FCTR = "" THEN TEMP.GRP.FOR = ""
         CASE TEMP.GRP.FCALC = "T"
            TBL.ID = TEMP.GRP.FPARAM
            CALL EST.TABLE.FCTR (CONO, TBL.ID, TEMP.GRP.FCTR)
            IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
      END CASE
      TEMP.STD = FIELD(EST.PROD.PCST<1,COMP,MPTR>,"!",QP) / 100
      BEGIN CASE
         CASE TEMP.TYPE = "MS"
            TEMP.STD.TYPE = "$/M"
            TEMP.UM = 1000
         CASE 1
            TEMP.STD.TYPE = "$/CWT"
            TEMP.UM = 100
      END CASE
      TEMP.IND.1 = ESTM.FOH.PCT<1,MT.PTR>
      TEMP.MARKUP = ESTM.MARKUP<1,MT.PTR>
      TEMP.DESC = EST.PROD.OS.DESC<1,COMP,MPTR>
*
*T27716 v
*     CALL EST.CALC.COST (CONO)
      CALL EST.CALC.COST (CONO,DEPT)
*T27716 ^
*
      REF.NO = REF.NO + 1
      FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
         ESTD.REC(A) = INSERT(ESTD.REC(A),1,REF.NO,0,ESTD.TEMP(A))
      NEXT A
      ESTD.QTY<1,REF.NO> = TEMP.QTY * 100
      ESTD.STD<1,REF.NO> = TEMP.STD * 100
      ESTD.GRP.QTY<1,REF.NO> = TEMP.GRP.QTY * 100
390 NEXT MPTR
   RETURN
END
