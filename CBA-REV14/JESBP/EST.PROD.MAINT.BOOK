      SUBROUTINE EST.PROD.MAINT.BOOK(CONO,ACTION,EST.KEY,STATUS)
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - EST.PROD.MAINT.BOOK
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
* DATE     - 01/06/86
* DESCRIPTION
* This subroutine captures specification data for use in subsequent
* calculations.
* MOD TASK 18262 RKB 10/27/94 INCREASE ESTIMATE QUANTITIES TO 99
*T23155 markt 08/10/1998 * Add NumUp AND #Form fields to screen.
*T25924 bilal 07/03/2001 * Remove field 1 from line 0.
*********************************************************************
*
*-COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.PRESS.SPOIL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
      MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*-INITIALIZATION
*
      SCREEN DEFINE;EST.PROD.MAINT.BOOK
      SCREEN FORMAT
      SCREEN COUNT;;42
      PRICE.LINE.COUNT=S$LCNT
      PRICE.LINE.SPACE=S$LSPC
      PRICE.CURR.REF.NO=""
      SCREEN COUNT;;36
      COLOR.LINE.COUNT=S$LCNT
      COLOR.LINE.SPACE=S$LSPC
      COLOR.CURR.REF.NO=""
      PRICE.LINE.CNT=0
      COLOR.LINE.CNT=0
      PRICE.REF.NO=1
      COLOR.REF.NO=1
      SCREEN FIELD;;17
      IF S$TYP="3" THEN CAL.PPI="P" ELSE CAL.PPI="C"
      S$TYP=0; CALL SCREEN.INPUT; S$MASK="#"
      COMP=1
      MAXQ=3
      PRICE.FLAG=COMP.REC(57)<1,11>
      IF PRICE.FLAG="" THEN PRICE.FLAG="X"
      GOTO 110
*
*-MAIN PROCESSING
*
100 *
      SCREEN CLEAR
110 *
*T25924      S$DATA(1)<S$SCR>=DATE()
*T25924      SCREEN DISPLAY;;1
      S$DATA(2)<S$SCR>=COMP
      SCREEN DISPLAY;;2
120 *
      IF EST.PROD.OS.TYPE<1,COMP>="" THEN
         NEW.REC=1
         WEB.CNT=0
         CURR.WEB.CNT=0
         MODE="A"
         FOR FLD=1 TO 30
            BEGIN CASE
               CASE FLD=1
                  GOSUB 1010
               CASE FLD=11
                  GOSUB 1110
               CASE FLD=21 AND EST.PROD.OS.TYPE<1,COMP>#""
                  GOSUB 1210
            END CASE
            IF S$VALUE="END" THEN
               CALL EST.COMP.DEL(CONO,"P",EST.KEY,COMP)
               IF COMP=1 THEN
                  SCREEN CLEAR
                  GOTO 500
               END ELSE
                  COMP=COMP-1
                  GOTO 100
               END
            END
         NEXT FLD
         IF EST.PROD.OS.TYPE<1,COMP>="" OR EST.PROD.PRESS.ID<1,COMP>="" THEN
            PRICE.LINE.CNT=0
            COLOR.LINE.CNT=0
            GOTO 500
         END
         PRICE.LINE.CNT=COUNT(EST.QTY,VM)+1
         GOSUB 85000
         MODE="A"
         GOSUB 40000
         MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"I" ELSE
            MAT ESTM.REC=""
         END
         COLOR.REF.NO=0
         MPTR=1
         CALL EST.PROD.INK.MAINT.BOOK(CONO,ACTION,"A",COMP,MPTR,COLOR.REF.NO,COLOR.CURR.REF.NO)
         SCREEN DEFINE;EST.PROD.MAINT.BOOK
      END ELSE
         NEW.REC=0
         WEB.CNT=EST.PROD.WEB.CNT<1,COMP>+0
         CURR.WEB.CNT=DCOUNT(EST.PROD.OS.PROD<1,COMP>,SM)
         COLOR.LINE.CNT=DCOUNT(EST.PROD.INK.ID<1,COMP,1>,"!")
         PRICE.LINE.CNT=COUNT(EST.QTY,VM)+1
         GOSUB 80000
         PRICE.REF.NO=1
         GOSUB 85100
         COLOR.REF.NO=1
         GOSUB 50000
         IF COUNT(EST.QTY,VM)+1#DCOUNT(EST.PROD.PQTY<1,COMP,1>,"!") THEN
            GOSUB 85000
         END
         MODE="A"
*RKB  GOSUB 40000
         GOSUB 40100 
      END
*
*-OPERATOR REQUEST
*
500 *
      IF ACTION="M" THEN
         SCREEN FIELD;;39
         SCREEN INPUT;;39
      END ELSE
         SCREEN FIELD;;51
         SCREEN INPUT;;51
      END
      OPTION=S$VALUE
      BEGIN CASE
         CASE OPTION="" OR OPTION="E" OR OPTION="END"
            GOSUB 86000
            IF WEBERR THEN GOTO 500
            IF EST.PROD.DESC="" THEN
               EST.COMPONENT.CNT=""
            END ELSE
               EST.COMPONENT.CNT=COUNT(EST.PROD.DESC,VM)+1
            END
            GOTO 99999
         CASE NUM(OPTION)
            MODE="C"
            FLD=OPTION
            BEGIN CASE
               CASE FLD >= 1 AND FLD <= 1
                  ON FLD GOSUB 1010
               CASE FLD=11
                  IF EST.PROD.OS.TYPE<1,COMP>="" THEN GOTO 120
                  GOSUB 1110
                  GOSUB 85000
                  MODE="A"
                  GOSUB 40000
               CASE FLD=21 AND EST.PROD.OS.TYPE<1,COMP>#""
                  GOSUB 1210
                  GOSUB 85000
                  MODE="A"
                  GOSUB 40000
            END CASE
         CASE OPTION="C"
            SCREEN FIELD;;40
            S$MINV=1;S$MAXV=DCOUNT(EST.PROD.DESC,VM)
            IF ACTION # "I" THEN S$MAXV=S$MAXV + 1
            IF S$MAXV > 16 THEN S$MAXV=16
            SCREEN INPUT;;40
            IF S$VALUE#"" AND S$VALUE#"END" THEN
               COMP=S$VALUE
               GOTO 100
            END
         CASE OPTION="D"
            SCREEN FIELD;;46
            SCREEN INPUT;;46
            IF S$VALUE="Y" THEN
               CALL EST.COMP.DEL(CONO,"",EST.KEY,COMP)
               IF COMP=1 THEN
                  SCREEN CLEAR
               END ELSE
                  COMP=COMP-1
                  GOTO 100
               END
            END
         CASE OPTION="P"
            MODE="C"
            MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,1> ELSE
               MAT ESTM.REC=""
            END
            LOCATE EST.PROD.OS.PROD<1,COMP,1> IN ESTM.PROD<1>,1 SETTING MP ELSE
               MP=0
            END
            IF MP > 0 THEN
*      IF (PRICE.FLAG="X" AND ESTM.QCOST<1,MP,1>="") OR (PRICE.FLAG#"X" AND EST.PROD.INV.ID<1,COMP,1>#"") THEN GOSUB 40000
               BEGIN CASE
                  CASE (PRICE.FLAG="X" AND ESTM.QCOST<1,MP,1>="")
                     GOSUB 40000
                  CASE (PRICE.FLAG#"X" AND EST.PROD.INV.ID<1,COMP,1>="")
                     IF ESTM.QCOST<1,MP,1>="" THEN GOSUB 40000
               END CASE
            END
         CASE OPTION = "PS"
            PRICE.REF.NO=PRICE.CURR.REF.NO+PRICE.LINE.COUNT
            IF PRICE.REF.NO > PRICE.LINE.CNT THEN PRICE.REF.NO = 1
            GOSUB 85100
         CASE OPTION="W" AND WEB.CNT > 1
            S$SCR=S$SCR+1
            CALL EST.PROD.WEB.MAINT.BOOK (CONO,ACTION,EST.KEY,COMP,STATUS)
            SCREEN CLOSE
            S$SCR=S$SCR-1
            SCREEN FORMAT
            CURR.WEB.CNT=DCOUNT(EST.PROD.OS.PROD<1,COMP>,SM)
            GOSUB 80700
            GOSUB 85100
            GOSUB 50000
         CASE OPTION="I"
            MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"I" ELSE
               MAT ESTM.REC=""
            END
            MPTR=1
            CALL EST.PROD.INK.MAINT.BOOK(CONO,ACTION,"",COMP,MPTR,COLOR.REF.NO,COLOR.CURR.REF.NO)
***    SCREEN DEFINE;EST.PROD.MAINT.BOOK
         CASE OPTION="B"
            GOSUB 86000
            IF WEBERR THEN GOTO 500
            IF EST.PROD.DESC="" THEN
               EST.COMPONENT.CNT=""
            END ELSE
               EST.COMPONENT.CNT=COUNT(EST.PROD.DESC,VM)+1
            END
            S$SCR=S$SCR+1
            CALL EST.BIND.MAINT(CONO,ACTION,EST.KEY,STATUS)
            SCREEN CLOSE
            S$SCR=S$SCR-1
            SCREEN FORMAT
            GOSUB 80900
            PRICE.CURR.REF.NO=''
            GOSUB 85100
            GOSUB 50000
      END CASE
      GOTO 500
*
*-COMPONENT DESCRIPTION
*
1010 *
      SCREEN FIELD;;3
      S$O.R="O";S$DEFAULT=COMP
      SCREEN INPUT;;3;RETURN
      EST.PROD.DESC<1,COMP>=S$VALUE
      RETURN
*
*-PAPER TYPE
*
1110 *
      NEW.PROD=0
      PREV.TYPE=EST.PROD.OS.TYPE<1,COMP>
      PREV.USAGE=EST.PROD.OS.USAGE<1,COMP,1>
      PREV.PROD=EST.PROD.OS.PROD<1,COMP,1>
      PREV.INV.ID=EST.PROD.INV.ID<1,COMP,1>
      S$DATA(4)<S$SCR>=EST.PROD.OS.TYPE<1,COMP>
      SCREEN FIELD;;4
      IF EST.PROD.OS.TYPE<1,COMP>#"" THEN S$O.R="R"
      SCREEN INPUT;;4;RETURN
      PAP.TYPE=S$VALUE
      EST.PROD.OS.TYPE<1,COMP>=S$VALUE
      BEGIN CASE
         CASE PAP.TYPE="S"
            S$DATA(5)<S$SCR>="SHEET"
         CASE PAP.TYPE="R"
            S$DATA(5)<S$SCR>="ROLL"
         CASE PAP.TYPE=""
            S$DATA(5)<S$SCR>=""
         CASE 1
            S$DATA(5)<S$SCR>="??????"
      END CASE
      SCREEN DISPLAY;;5
      IF PAP.TYPE="" THEN
         EST.PROD.OS.USAGE<1,COMP,1>=""
         EST.PROD.OS.PROD<1,COMP,1>=""
         MAT ESTM.REC=""
         MP=1
         GOSUB 1180
         RETURN
      END ELSE
         SCREEN FIELD;;47
         SCREEN INPUT;;47;RETURN
         USE.CODE=S$VALUE
         MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:PAP.TYPE:USE.CODE ELSE
            ERRMSG="Cannot locate on material file"
            GOSUB 90000
            GOTO 1110
         END
         EST.PROD.OS.USAGE<1,COMP,1>=USE.CODE
      END
*
*-PRODUCT ID
*
1120 *
      S$DATA(6)<S$SCR>=EST.PROD.OS.PROD<1,COMP,1>
      SCREEN FIELD;;6
      SCREEN INPUT;;6;RETURN
      IF S$VALUE="???" THEN
         S$SCR=S$SCR+1
         CALL EST.PAPER.XREF(CONO,"S",EST.PROD.OS.TYPE<1,COMP>,EST.PROD.OS.USAGE<1,COMP,1>,SELECTION)
         SCREEN CLOSE
         S$SCR=S$SCR-1
         SCREEN FORMAT
***  GOSUB 75000
         GOSUB 80900
         PRICE.CURR.REF=''
         GOSUB 85100
         GOSUB 50000
         IF SELECTION="" THEN GOTO 1120
         S$VALUE=SELECTION
         S$DATA(6)<S$SCR>=SELECTION
         SCREEN DISPLAY;;6
      END
      LOCATE S$VALUE IN ESTM.PROD<1>,1 SETTING MP ELSE
         ERRMSG="Invalid product ID. Try again! "
         GOSUB 90000
         GOTO 1120
      END
      EST.PROD.OS.PROD<1,COMP,1>=S$VALUE
      IF S$VALUE#PREV.PROD THEN S$DATA(7)<S$SCR>=""
      SCREEN FIELD;;7
      S$O.R="O"
      S$DEFAULT=ESTM.DESC<1,MP>
      SCREEN INPUT;;7;RETURN
      EST.PROD.OS.DESC<1,COMP,1>=S$VALUE
*
*-INVENTORY ID
*
1125 *
      S$DATA(20)<S$SCR>=EST.PROD.INV.ID<1,COMP,1>
      SCREEN FIELD;;20
      SCREEN INPUT;;20;RETURN
      BEGIN CASE
         CASE S$VALUE=""
            EST.PROD.INV.ID<1,COMP,1>=S$VALUE
         CASE 1
            INV.ID=S$VALUE
            IF INV.ID="???" THEN
               S$SCR=S$SCR+1
               PLINE=EST.PROD.PLINE<1,COMP,1>
               BASIS=EST.PROD.BASIS.WT<1,COMP,1>
               FIN=EST.PROD.FINISH<1,COMP,1>
               CLR=EST.PROD.COLOR<1,COMP,1>
               WIDTH=""
               XXLENGTH=""
               CALL EST.INV.XREF(CONO,"S",PLINE,BASIS,FIN,CLR,WIDTH,XXLENGTH,SELECTION)
               SCREEN CLOSE
               S$SCR=S$SCR-1
               SCREEN FORMAT
               IF SELECTION="" THEN
                  GOSUB 80900
                  PRICE.CURR.REF.NO=''
                  GOSUB 85100
                  GOSUB 50000
                  GOTO 1125
               END ELSE
                  INV.ID=SELECTION
                  MODE="A"
               END
            END
            MATREAD INV.REC FROM INVENTORY,CONO:INV.ID ELSE
               ERRMSG="Invalid inventory item. Try again! "
               GOSUB 90000
               GOTO 1125
            END
            EST.PROD.INV.ID<1,COMP,1>=INV.ID
      END CASE
      BEGIN CASE
         CASE EST.PROD.OS.TYPE<1,COMP>#PREV.TYPE
            MODE="A"
         CASE EST.PROD.OS.USAGE<1,COMP,1>#PREV.USAGE
            MODE="A"
         CASE EST.PROD.OS.PROD<1,COMP,1>#PREV.PROD
            MODE="A"
         CASE EST.PROD.INV.ID<1,COMP,1>#PREV.INV.ID
            MODE="A"
      END CASE
      IF MODE="A" THEN
         GOSUB 1180
         EST.PROD.PCST<1,COMP,1>=""
      END
*
*-COATING CODE
*
      IF MODE="C" OR EST.PROD.COATING<1,COMP,1>="" THEN
         SCREEN FIELD;;15
         SCREEN INPUT;;15;RETURN
         EST.PROD.COATING<1,COMP,1>=S$VALUE
      END
*
*-ORDER PAPER SIZE, GRAIN
*
1130 *
      IF EST.PROD.INV.ID<1,COMP,1>="" THEN
         IF MODE="C" OR EST.PROD.OS.WIDTH<1,COMP,1>="" THEN
            SCREEN FIELD;;8
            SCREEN INPUT;;8;RETURN
            EST.PROD.OS.WIDTH<1,COMP,1>=S$VALUE
         END
         IF MODE="C" OR EST.PROD.OS.LENGTH<1,COMP,1>="" THEN
            IF EST.PROD.OS.TYPE<1,COMP>="S" THEN
               SCREEN FIELD;;9
               SCREEN INPUT;;9;RETURN
               EST.PROD.OS.LENGTH<1,COMP,1>=S$VALUE
            END
         END
      END
      IF MODE="C" OR EST.PROD.OS.GRAIN<1,COMP,1>="" THEN
         SCREEN FIELD;;10
         SCREEN INPUT;;10;RETURN
         EST.PROD.OS.GRAIN<1,COMP,1>=S$VALUE
      END
*
*-PRESS SHEET SIZE, GRAIN
*
1140 *
      IF EST.PROD.OS.TYPE<1,COMP>="S" THEN
         SCREEN FIELD;;11
         S$O.R="O";S$DEFAULT=EST.PROD.OS.WIDTH<1,COMP,1>
         S$MAXV=EST.PROD.OS.WIDTH<1,COMP,1>
         SCREEN INPUT;;11;RETURN
         EST.PROD.PS.WIDTH<1,COMP,1>=S$VALUE
         SCREEN FIELD;;12
         S$O.R="O";S$DEFAULT=EST.PROD.OS.LENGTH<1,COMP,1>
         S$MAXV=EST.PROD.OS.LENGTH<1,COMP,1>
         SCREEN INPUT;;12;RETURN
         EST.PROD.PS.LENGTH<1,COMP,1>=S$VALUE
      END ELSE
         EST.PROD.PS.WIDTH<1,COMP,1>=EST.PROD.OS.WIDTH<1,COMP,1>
         S$DATA(11)<S$SCR>=EST.PROD.PS.WIDTH<1,COMP,1>
         SCREEN DISPLAY;;11
      END
      IF EST.PROD.OS.WIDTH<1,COMP,1>=EST.PROD.PS.WIDTH<1,COMP,1> AND EST.PROD.OS.LENGTH<1,COMP,1>=EST.PROD.PS.LENGTH<1,COMP,1> THEN
         EST.PROD.PS.GRAIN<1,COMP,1>=EST.PROD.OS.GRAIN<1,COMP,1>
         S$DATA(13)<S$SCR>=EST.PROD.PS.GRAIN<1,COMP,1>
         SCREEN DISPLAY;;13
      END ELSE
         SCREEN FIELD;;13
         S$O.R="O";S$DEFAULT=EST.PROD.OS.GRAIN<1,COMP,1>
         SCREEN INPUT;;13;RETURN
         EST.PROD.PS.GRAIN<1,COMP,1>=S$VALUE
      END
*
*-BASIS WEIGHT
*
1150 *
      IF EST.PROD.INV.ID<1,COMP,1>="" THEN
         IF MODE="C" OR EST.PROD.BASIS.WT<1,COMP,1>="" THEN
            SCREEN FIELD;;14
            SCREEN INPUT;;14;RETURN
            EST.PROD.BASIS.WT<1,COMP,1>=S$VALUE
         END
      END
*
*-PAPER CALIPER OR PPI
*
1155 *
      IF MODE="C" OR (CAL.PPI="P" AND EST.PROD.PPI<1,COMP,1>="") OR (CAL.PPI="C" AND EST.PROD.CALIPER<1,COMP,1>="") THEN
         SCREEN FIELD;;17
         SCREEN INPUT;;17;RETURN
         IF CAL.PPI="P" THEN
            EST.PROD.PPI<1,COMP,1>=S$VALUE
            EST.PROD.CALIPER<1,COMP,1>=INT(2000/S$VALUE*1000+0.5)
         END ELSE
            EST.PROD.CALIPER<1,COMP,1>=S$VALUE
            EST.PROD.PPI<1,COMP,1>=INT(2000/(S$VALUE/1000)+0.5)
         END
      END
*
*-PRODUCT LINE
*
1160 *
      IF EST.PROD.INV.ID<1,COMP,1>="" THEN
         IF MODE="C" OR EST.PROD.PLINE<1,COMP,1>="" THEN
            SCREEN FIELD;;16
            SCREEN INPUT;;16;RETURN
            EST.PROD.PLINE<1,COMP,1>=S$VALUE
         END
      END
*
*-PSS/OSS
*
      IF EST.PROD.OS.TYPE<1,COMP>="S" THEN
         IF MODE="C" THEN
            SCREEN FIELD;;21
            SCREEN INPUT;;21;RETURN
            EST.PROD.PSS.OSS<1,COMP,1>=S$VALUE
         END
      END
*
*-COLOR
*
1164 *
      IF EST.PROD.INV.ID<1,COMP,1>="" THEN
         IF MODE="C" OR EST.PROD.COLOR<1,COMP,1>="" THEN
            S$DATA(18)<S$SCR>=EST.PROD.COLOR<1,COMP,1>
            SCREEN FIELD;;18
            SCREEN INPUT;;18;RETURN
            IF S$VALUE#"" THEN
               READ REC FROM COLOR,CONO:S$VALUE ELSE
                  ERRMSG="Invalid color code. Try again! "
                  GOSUB 90000
                  GOTO 1164
               END
            END
            EST.PROD.COLOR<1,COMP,1>=S$VALUE
         END
      END
*
*-FINISH
*
1166 *
      IF EST.PROD.INV.ID<1,COMP,1>="" THEN
         IF MODE="C" OR EST.PROD.FINISH<1,COMP,1>="" THEN
            S$DATA(19)<S$SCR>=EST.PROD.FINISH<1,COMP,1>
            SCREEN FIELD;;19
            SCREEN INPUT;;19;RETURN
            IF S$VALUE#"" THEN
               READ REC FROM FINISH,CONO:S$VALUE ELSE
                  ERRMSG="Invalid finish code. Try again! "
                  GOSUB 90000
                  GOTO 1166
               END
            END
            EST.PROD.FINISH<1,COMP,1>=S$VALUE
         END
      END
      RETURN
*
*-DISPLAY PAPER DATA
*
1180 *
      IF EST.PROD.INV.ID<1,COMP,1>="" THEN
         EST.PROD.OS.WIDTH<1,COMP,1>=ESTM.WIDTH<1,MP>
         IF EST.PROD.OS.TYPE<1,COMP> = "S" THEN
            EST.PROD.OS.LENGTH<1,COMP,1>=ESTM.LENGTH<1,MP>
         END
         EST.PROD.OS.GRAIN<1,COMP,1>=ESTM.GRAIN<1,MP>
         EST.PROD.PLINE<1,COMP,1>=ESTM.PLINE<1,MP>
         EST.PROD.BASIS.WT<1,COMP,1>=ESTM.BASIS.WT<1,MP>
         EST.PROD.FINISH<1,COMP,1>=ESTM.FINISH.CD<1,MP>
         EST.PROD.COLOR<1,COMP,1>=ESTM.CLR.CD<1,MP>
         EST.PROD.CALIPER<1,COMP,1>=ESTM.CALIPER<1,MP>
         EST.PROD.PPI<1,COMP,1>=ESTM.PPI<1,MP>
      END ELSE
         EST.PROD.OS.DESC<1,COMP,1>=INV.DESC
         EST.PROD.OS.WIDTH<1,COMP,1>=INV.PAP.WIDTH        ;* REL 08.0
         IF EST.PROD.OS.TYPE<1,COMP> = "S" THEN
            EST.PROD.OS.LENGTH<1,COMP,1>=INV.PAP.LEN      ;* REL 08.0
         END
         EST.PROD.OS.GRAIN<1,COMP,1>=INV.GRAIN
         EST.PROD.PLINE<1,COMP,1>=INV.LINE
         IF NUM(INV.BAS.WT) THEN
            EST.PROD.BASIS.WT<1,COMP,1>=INV.BAS.WT/100
         END ELSE
            EST.PROD.BASIS.WT<1,COMP,1>=""
         END
         EST.PROD.FINISH<1,COMP,1>=INV.PAP.FINSH
         EST.PROD.COLOR<1,COMP,1>=INV.COLOR
         IF INV.PPI+0 > 0 THEN
            EST.PROD.PPI<1,COMP,1>=INV.PPI
            EST.PROD.CALIPER<1,COMP,1>=INT(2000/INV.PPI*1000+0.5)
         END ELSE
            EST.PROD.CALIPER<1,COMP,1>=ESTM.CALIPER<1,MP>
            EST.PROD.PPI<1,COMP,1>=ESTM.PPI<1,MP>
         END
      END
      EST.PROD.PS.WIDTH<1,COMP,1>=EST.PROD.OS.WIDTH<1,COMP,1>
      EST.PROD.PS.LENGTH<1,COMP,1>=EST.PROD.OS.LENGTH<1,COMP,1>
      EST.PROD.PS.GRAIN<1,COMP,1>=EST.PROD.OS.GRAIN<1,COMP,1>
      EST.PROD.COATING<1,COMP,1>=ESTM.COAT.CD<1,MP>
      EST.PROD.M.WT<1,COMP,1>=ESTM.M.WEIGHT<1,MP>
      IF EST.PROD.OS.TYPE<1,COMP>="" THEN
         AC=COUNT(EST.A9B,",")+1
         FOR AP=1 TO AC
            AA=FIELD(EST.A9B,",",AP)
            AA1=FIELD(AA,"-",1)
            AA2=FIELD(AA,"-",2)
            IF AA2="" THEN AA2=AA1
            FOR AAP=AA1 TO AA2
               EST.REC(AAP)<1,COMP>=""
            NEXT AAP
         NEXT AP
      END
      PRICE.LINE.CNT=DCOUNT(EST.PROD.PQTY<1,COMP,1>,"!")
      COLOR.LINE.CNT=DCOUNT(EST.PROD.INK.ID<1,COMP,1>,"!")
      GOSUB 80000
      PRICE.REF.NO=1
      GOSUB 85100
      COLOR.REF.NO=1
      GOSUB 50000
      RETURN
*
*-PRESS ID
*
1210 *
      PREV.PRESS.ID=EST.PROD.PRESS.ID<1,COMP>
1212 *
      IF EST.PROD.DC.CODE<1,COMP>="" THEN
         S$DATA(24)<S$SCR>=EST.PROD.PRESS.ID<1,COMP>
      END ELSE
         S$DATA(24)<S$SCR>=EST.PROD.PRESS.ID<1,COMP>:"-":EST.PROD.DC.CODE<1,COMP>
      END
      SCREEN FIELD;;24
      SCREEN INPUT;;24;RETURN
      IF S$VALUE="???" THEN
         CALL EST.EQUIP.XREF(CONO,"01",EQUIPMENT,COST.CNTR,XCODE)
         SCREEN FORMAT
         S$VALUE=XCODE
         S$DATA(24)<S$SCR>=XCODE
         GOSUB 80900
         PRICE.CURR.REF.NO=''
         GOSUB 85100
         GOSUB 50000
         IF XCODE="" THEN GOTO 1212
      END
      PRS=FIELD(S$VALUE,"-",1)
      CNF=FIELD(S$VALUE,"-",2)
      MATREAD CCTR.REC FROM COST.CNTR,CONO:PRS ELSE
         ERRMSG="Invalid press ID. Try again! "
         GOSUB 90000
         GOTO 1212
      END
      MATREAD EQUIPMENT.REC FROM EQUIPMENT,CONO:PRS ELSE
         ERRMSG="Cannot locate press on equipment file. Try again! "
         GOSUB 90000
         GOTO 1212
      END
      IF EQP.TYPE#"01" THEN
         ERRMSG="Invalid equipment type. Try again! "
         GOSUB 90000
         GOTO 1212
      END
      IF CNF#"" THEN
         LOCATE CNF IN EQP.PRESS.DC.CODE<1>,1 SETTING FND ELSE
            ERRMSG="Invalid configuration code for specified press. Try again! "
            GOSUB 90000
            GOTO 1212
         END
      END
      EST.PROD.PRESS.ID<1,COMP>=PRS
      EST.PROD.DC.CODE<1,COMP>=CNF
      IF PREV.PRESS.ID#EST.PROD.PRESS.ID<1,COMP> THEN
         S$DATA(25)<S$SCR>=CCTR.DESC
         SCREEN DISPLAY;;25
         S$DATA(53)<S$SCR>=EQP.PRESS.RUN.SPOIL
         SCREEN DISPLAY;;53
      END
*
*-PRINT MODE
*
      SCREEN FIELD;;26
      SCREEN INPUT;;26;RETURN
      EST.PROD.MODE<1,COMP,1>=S$VALUE
*
*-NUMBER OF WEBS
*
      IF EQP.PRESS.TYPE = "W" THEN
         EST.PROD.OS.LENGTH<1,COMP>=''
         EST.PROD.PS.LENGTH<1,COMP>=''
      END
      IF EQP.PRESS.WEB.CNT+0 > 1 THEN
         SCREEN FIELD;;22
         SCREEN INPUT;;22;RETURN
         EST.PROD.WEB.CNT<1,COMP>=S$VALUE
         WEB.CNT=EST.PROD.WEB.CNT<1,COMP>+0
      END ELSE
         EST.PROD.WEB.CNT<1,COMP>=""
         WEB.CNT=0
         S$DATA(22)<S$SCR>=""
         SCREEN DISPLAY;;22
      END
*
*-PAGE COUNT
*
1220 *
      SCREEN FIELD;;28
      SCREEN INPUT;;28;RETURN
      PG.CNT=S$VALUE
*
*-PAGES PER FORM (ALL WEBS)
*
1221 *
      IF WEB.CNT+0 <= 1 THEN
         PGS.ALL.WEBS=""
      END ELSE
         SCREEN FIELD;;27
         SCREEN INPUT;;27;RETURN
         PGS.ALL.WEBS=S$VALUE
         BEGIN CASE
            CASE MOD(PGS.ALL.WEBS,PG.CNT)=0
               EST.PROD.NO.UP<1,COMP,1>=PGS.ALL.WEBS/PG.CNT
               EST.PROD.FORMS<1,COMP,1>=1
            CASE MOD(PG.CNT,PGS.ALL.WEBS)=0
               EST.PROD.NO.UP<1,COMP,1>=1
               EST.PROD.FORMS<1,COMP,1>=PG.CNT/PGS.ALL.WEBS
            CASE 1
               ERRMSG="Invalid layout. Try again! "
               GOSUB 90000
               GOTO 1221
         END CASE
      END
      EST.PROD.PGS.ALL.WEBS<1,COMP>=PGS.ALL.WEBS
*
*-PAGES PER FORM
*
1222 *
      SCREEN FIELD;;29
      IF WEB.CNT+0 > 1 THEN
         S$MAXV=PGS.ALL.WEBS-1
      END
      SCREEN INPUT;;29;RETURN
      PGS.PER.FORM=S$VALUE
      BEGIN CASE
         CASE WEB.CNT > 1
         CASE MOD(PGS.PER.FORM,PG.CNT)=0
            EST.PROD.NO.UP<1,COMP,1>=PGS.PER.FORM/PG.CNT
            EST.PROD.FORMS<1,COMP,1>=1
         CASE MOD(PG.CNT,PGS.PER.FORM)=0
            EST.PROD.NO.UP<1,COMP,1>=1
            EST.PROD.FORMS<1,COMP,1>=PG.CNT/PGS.PER.FORM
         CASE 1
            ERRMSG="Invalid layout. Try again! "
            GOSUB 90000
            GOTO 1220
      END CASE
      EST.PROD.PG.CNT<1,COMP,1>=PG.CNT
      EST.PROD.PGS.PER.FORM<1,COMP,1>=PGS.PER.FORM
*
*-COLORS ON SIDE 1
*
      SCREEN FIELD;;30
      SCREEN INPUT;;30;RETURN
      EST.PROD.COLORS.1<1,COMP,1>=S$VALUE
*
*-COLORS ON SIDE 2
*
      PMODE=EST.PROD.MODE<1,COMP,1>
      BEGIN CASE
         CASE PMODE="SS"
            C2=""
            S$DATA(31)<S$SCR>=C2
            SCREEN DISPLAY;;31
         CASE PMODE="WT" OR PMODE="WR"
            C2=EST.PROD.COLORS.1<1,COMP,1>
            S$DATA(31)<S$SCR>=C2
            SCREEN DISPLAY;;31
         CASE 1
            SCREEN FIELD;;31
            SCREEN INPUT;;31;RETURN
            C2=S$VALUE
      END CASE
      EST.PROD.COLORS.2<1,COMP,1>=C2
***** T23155
*
*-NUMBER UP
*
   SCREEN FIELD;;23
   SCREEN INPUT;;23;RETURN
   EST.PROD.NO.UP<1,COMP,1>=S$VALUE
*
*-NUMBER OF FORMS
*
   SCREEN FIELD;;49
   DEF.VAL = 1
   IF EST.PROD.PGS.PER.FORM<1,COMP,1> # "" AND EST.PROD.PG.CNT<1,COMP,1> # "" THEN
      IF EST.PROD.PGS.PER.FORM<1,COMP,1> > 0 THEN
         DEF.VAL = INT(EST.PROD.PG.CNT<1,COMP,1> / EST.PROD.PGS.PER.FORM<1,COMP,1> + .5)
      END
   END
   S$O.R="O";S$DEFAULT=DEF.VAL
   SCREEN INPUT;;49;RETURN
   EST.PROD.FORMS<1,COMP,1>=S$VALUE
*          
***** T23155 END
*
*-NUMBER OF PLATES
*
      GOSUB 84000
      SCREEN FIELD;;32
      SCREEN INPUT;;32;RETURN
      EST.PROD.PLATES<1,COMP,1>=S$VALUE
*
*-COMPUTE NUMBER OF PASSES PER FORM
*
      PMODE=EST.PROD.MODE<1,COMP,1>
      C1=EST.PROD.COLORS.1<1,COMP,1>+0
      C2=EST.PROD.COLORS.2<1,COMP,1>+0
      CC=EQP.PRESS.CLR.CNT
      BEGIN CASE
         CASE EQP.PRESS.TYPE="W"
            PP=1
         CASE PMODE="SS"
            PP=INT(C1/CC+0.99)
         CASE PMODE="WT" OR PMODE="WR"
            PP=INT(C1/CC+0.99)*2
         CASE PMODE="SW"
            PP=INT(C1/CC+0.99)+INT(C2/CC+0.99)
         CASE 1
            IF C1 > C2 THEN
               PP=INT(C1/CC+0.99)
            END ELSE
               PP=INT(C2/CC+0.99)
            END
      END CASE
      EST.PROD.PASSES<1,COMP,1>=PP
*
*-UNTRIMMED SIG SIZE
*
      SCREEN FIELD;;57
      SCREEN INPUT;;57;RETURN
      EST.PROD.US.WIDTH<1,COMP,1>=S$VALUE
      SCREEN FIELD;;58
      SCREEN INPUT;;58;RETURN
      EST.PROD.US.LENGTH<1,COMP,1>=S$VALUE
*
*-TRIMMED SIG SIZE
*
      SCREEN FIELD;;59
      SCREEN INPUT;;59;RETURN
      EST.PROD.TS.WIDTH<1,COMP,1>=S$VALUE
      SCREEN FIELD;;60
      SCREEN INPUT;;60;RETURN
      EST.PROD.TS.LENGTH<1,COMP,1>=S$VALUE
*
*-BINDERY SPOILAGE TABLE OR PERCENTAGE
*
1250 *
      SCREEN FIELD;;34
      SCREEN INPUT;;34;RETURN
      BEGIN CASE
         CASE NUM(S$VALUE) AND S$VALUE # "+" AND S$VALUE # "." AND S$VALUE # "-"
         CASE S$VALUE="MULT"AND COMP=1
            S$SCR=S$SCR+1
            CALL EST.PROD.MULTI.QTY(CONO,ACTION,EST.KEY,COMP,STATUS)
            SCREEN CLOSE
            S$SCR=S$SCR-1
            SCREEN FORMAT
***RKB    GOSUB 75000
            GOSUB 80900
            PRICE.CURR.REF.NO=''
            GOSUB 85100
            GOSUB 50000
            S$VALUE="MULT"
         CASE S$VALUE="MULT"
         CASE 1
            READV REC FROM ESTIMATE.BIND.SPOIL,CONO:S$VALUE,1 ELSE
               ERRMSG="Invalid bindery spoilage table. Try again! "
               GOSUB 90000
               GOTO 1250
            END
      END CASE
      EST.PROD.BIND.SPOIL<1,COMP,1>=S$VALUE
*
*-PRESS SPOILAGE TABLE OR PERCENTAGE
*
1260 *
      SCREEN FIELD;;53
      SCREEN INPUT;;53;RETURN
      IF NUM(S$VALUE) AND S$VALUE # "+" AND S$VALUE # "." AND S$VALUE # "-" THEN
         EST.PROD.PRESS.SPOIL<1,COMP,1>=S$VALUE
      END ELSE
         MATREAD PST.REC FROM ESTIMATE.PRESS.SPOIL,CONO:S$VALUE ELSE
            ERRMSG="Invalid press spoilage table. Try again! "
            GOSUB 90000
            GOTO 1260
         END
         EST.PROD.PRESS.SPOIL<1,COMP,1>=S$VALUE
         EST.PROD.INIT.MR.IMP<1,COMP,1>=PST.INIT.QTY
         EST.PROD.SUBS.MR.IMP<1,COMP,1>=PST.SUB.QTY
         S$DATA(33)<S$SCR>=PST.INIT.QTY
         SCREEN DISPLAY;;33
         S$DATA(54)<S$SCR>=PST.SUB.QTY
         SCREEN DISPLAY;;54
      END
*
*-INITIAL M/R COUNT
*
      SCREEN FIELD;;55
      SCREEN INPUT;;55;RETURN
      EST.PROD.INIT.MR.CNT<1,COMP,1>=S$VALUE
*
*-SUBSEQUENT M/R COUNT
*
      S$DATA(56)<S$SCR>=EST.PROD.PASSES<1,COMP,1>*EST.PROD.FORMS<1,COMP,1>-EST.PROD.INIT.MR.CNT<1,COMP,1>
      IF S$DATA(56)<S$SCR> < 0 THEN S$DATA(56)<S$SCR>=0
      SCREEN FIELD;;56
      SCREEN INPUT;;56;RETURN
      EST.PROD.SUBS.MR.CNT<1,COMP,1>=S$VALUE
*
*-INITIAL M/R IMPRESSIONS
*
      SCREEN FIELD;;33
      SCREEN INPUT;;33;RETURN
      EST.PROD.INIT.MR.IMP<1,COMP,1>=S$VALUE
*
*-SUBSEQUENT M/R IMPRESSIONS
*
      SCREEN FIELD;;54
      SCREEN INPUT;;54;RETURN
      EST.PROD.SUBS.MR.IMP<1,COMP,1>=S$VALUE
      IF EQP.PRESS.TYPE = "W" THEN
         IF EQP.PRESS.MIN.LENGTH = EQP.PRESS.CUTOFF.LEN THEN
            S$DATA(50)<S$SCR>=EQP.PRESS.CUTOFF.LEN
            EST.PROD.OS.LENGTH<1,COMP>=EQP.PRESS.MIN.LENGTH
            EST.PROD.PS.LENGTH<1,COMP>=EQP.PRESS.MIN.LENGTH
            SCREEN DISPLAY;;50
         END ELSE
            SCREEN FIELD;;50
            S$DATA(50)<S$SCR>=''
            S$MINV=EQP.PRESS.MIN.LENGTH
            S$MAXV=EQP.PRESS.CUTOFF.LEN
            IF EST.PROD.OS.LENGTH<1,COMP>= "" THEN
               S$DEFAULT=EQP.PRESS.CUTOFF.LEN
            END ELSE
               S$DEFAULT=EST.PROD.OS.LENGTH<1,COMP>
            END
            SCREEN INPUT;;50;RETURN
            EST.PROD.OS.LENGTH<1,COMP>=S$VALUE
            EST.PROD.PS.LENGTH<1,COMP>=S$VALUE
            S$DATA(50)<S$SCR>=S$VALUE
            SCREEN DISPLAY;;50
         END
      END
      RETURN
*
*-PAPER PRICE DATA
*
40000 *
      PRICE.CURR.REF.NO=''
      PRICE.REF.NO=1
      GOSUB 85100
40100 *
      FOR PRICE.REF.NO = 1 TO PRICE.LINE.CNT
         GOSUB 40200
      NEXT PRICE.REF.NO
      RETURN
40200 *
      IF MODE="C" OR FIELD(EST.PROD.PCST<1,COMP,1>,"!",PRICE.REF.NO)="" THEN
         IF MOD(PRICE.REF.NO,8) AND PRICE.REF.NO # PRICE.LINE.CNT THEN GOSUB 85100  
         S$VAL=PRICE.REF.NO
         SCREEN FIELD;;43
         IF PRICE.REF.NO > 1 THEN
            S$O.R="O";S$DEFAULT=FIELD(EST.PROD.PCST<1,COMP,1>,"!",PRICE.REF.NO-1)
         END
         SCREEN INPUT;;43
         IF S$VALUE#"END" THEN
            MACRO REPVAL;EST.PROD.PCST<1,COMP,1>;"!";PRICE.REF.NO;S$VALUE
         END
      END
      RETURN
*
*-DISPLAY COLOR DATA
*
50000 *
      START.REF.NO=1+INT((COLOR.REF.NO-1)/COLOR.LINE.COUNT)*COLOR.LINE.COUNT
      COLOR.CURR.REF.NO=START.REF.NO
      S$VAL=START.REF.NO
      S$CNT=DCOUNT(EST.PROD.INK.ID<1,COMP,1>,"!")
      SCREEN MULTI;;C;35;36;37;38
      RETURN
*
*-LOAD SCREEN DATA
*
80000 *
      S$DATA(1)<S$SCR>=DATE()
      S$DATA(2)<S$SCR>=COMP
      S$DATA(3)<S$SCR>=EST.PROD.DESC<1,COMP>
      S$DATA(4)<S$SCR>=EST.PROD.OS.TYPE<1,COMP>
      TYPE=EST.PROD.OS.TYPE<1,COMP>
      BEGIN CASE
         CASE TYPE="S"
            S$DATA(5)<S$SCR>="SHEET"
         CASE TYPE="R"
            S$DATA(5)<S$SCR>="ROLL"
         CASE TYPE="L"
            S$DATA(5)<S$SCR>="LABEL"
         CASE TYPE=""
            S$DATA(5)<S$SCR>=""
         CASE 1
            S$DATA(5)<S$SCR>="??????"
      END CASE
      S$DATA(47)<S$SCR>=EST.PROD.OS.USAGE<1,COMP,1>
      S$DATA(6)<S$SCR>=EST.PROD.OS.PROD<1,COMP,1>
      IF EST.PROD.OS.TYPE<1,COMP>="" THEN
         MAT ESTM.REC=""
         MP=1
      END ELSE
         MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,1> ELSE
            MAT ESTM.REC=""
         END
         LOCATE EST.PROD.OS.PROD<1,COMP,1> IN ESTM.PROD<1>,1 SETTING MP ELSE
            MP=999
         END
      END
      S$DATA(7)<S$SCR>=EST.PROD.OS.DESC<1,COMP,1>
      S$DATA(8)<S$SCR>=EST.PROD.OS.WIDTH<1,COMP,1>
      S$DATA(9)<S$SCR>=EST.PROD.OS.LENGTH<1,COMP,1>
      S$DATA(10)<S$SCR>=EST.PROD.OS.GRAIN<1,COMP,1>
      S$DATA(11)<S$SCR>=EST.PROD.PS.WIDTH<1,COMP,1>
      S$DATA(12)<S$SCR>=EST.PROD.PS.LENGTH<1,COMP,1>
      S$DATA(13)<S$SCR>=EST.PROD.PS.GRAIN<1,COMP,1>
      S$DATA(14)<S$SCR>=EST.PROD.BASIS.WT<1,COMP,1>
      S$DATA(15)<S$SCR>=EST.PROD.COATING<1,COMP,1>
      S$DATA(16)<S$SCR>=EST.PROD.PLINE<1,COMP,1>
      IF CAL.PPI="P" THEN
         S$DATA(17)<S$SCR>=EST.PROD.PPI<1,COMP,1>
      END ELSE
         S$DATA(17)<S$SCR>=EST.PROD.CALIPER<1,COMP,1>
      END
      S$DATA(18)<S$SCR>=EST.PROD.COLOR<1,COMP,1>
      S$DATA(19)<S$SCR>=EST.PROD.FINISH<1,COMP,1>
      S$DATA(20)<S$SCR>=EST.PROD.INV.ID<1,COMP,1>
      S$DATA(21)<S$SCR>=EST.PROD.PSS.OSS<1,COMP,1>
      IF EST.PROD.DC.CODE<1,COMP>="" THEN
         S$DATA(24)<S$SCR>=EST.PROD.PRESS.ID<1,COMP>
      END ELSE
         S$DATA(24)<S$SCR>=EST.PROD.PRESS.ID<1,COMP>:"-":EST.PROD.DC.CODE<1,COMP>
      END
      IF EST.PROD.PRESS.ID<1,COMP>="" THEN
         MAT CCTR.REC=""
         MAT EQUIPMENT.REC=""
      END ELSE
         MATREAD CCTR.REC FROM COST.CNTR,CONO:EST.PROD.PRESS.ID<1,COMP> ELSE
            MAT CCTR.REC=""
            CCTR.DESC="???????????????"
         END
         MATREAD EQUIPMENT.REC FROM EQUIPMENT,CONO:EST.PROD.PRESS.ID<1,COMP> ELSE
            MAT EQUIPMENT.REC=""
         END
      END
      S$DATA(25)<S$SCR>=CCTR.DESC
      S$DATA(26)<S$SCR>=EST.PROD.MODE<1,COMP,1>
      S$DATA(22)<S$SCR>=EST.PROD.WEB.CNT<1,COMP>
      S$DATA(28)<S$SCR>=EST.PROD.PG.CNT<1,COMP,1>
      S$DATA(27)<S$SCR>=EST.PROD.PGS.ALL.WEBS<1,COMP>
      S$DATA(29)<S$SCR>=EST.PROD.PGS.PER.FORM<1,COMP,1>
      S$DATA(30)<S$SCR>=EST.PROD.COLORS.1<1,COMP,1>
      S$DATA(31)<S$SCR>=EST.PROD.COLORS.2<1,COMP,1>
      S$DATA(32)<S$SCR>=EST.PROD.PLATES<1,COMP,1>
      S$DATA(34)<S$SCR>=EST.PROD.BIND.SPOIL<1,COMP,1>
      S$DATA(53)<S$SCR>=EST.PROD.PRESS.SPOIL<1,COMP,1>
      S$DATA(55)<S$SCR>=EST.PROD.INIT.MR.CNT<1,COMP,1>
      S$DATA(56)<S$SCR>=EST.PROD.SUBS.MR.CNT<1,COMP,1>
      S$DATA(33)<S$SCR>=EST.PROD.INIT.MR.IMP<1,COMP,1>
      S$DATA(54)<S$SCR>=EST.PROD.SUBS.MR.IMP<1,COMP,1>
      S$DATA(57)<S$SCR>=EST.PROD.US.WIDTH<1,COMP,1>
      S$DATA(58)<S$SCR>=EST.PROD.US.LENGTH<1,COMP,1>
      S$DATA(59)<S$SCR>=EST.PROD.TS.WIDTH<1,COMP,1>
      S$DATA(60)<S$SCR>=EST.PROD.TS.LENGTH<1,COMP,1>
      IF EQP.PRESS.TYPE = "W" THEN
         S$DATA(50)<S$SCR>=EST.PROD.OS.LENGTH<1,COMP>
      END
80700 *
      IF COLOR.LINE.CNT=0 THEN
         S$DATA(36)<S$SCR>=""
         S$DATA(37)<S$SCR>=""
         S$DATA(38)<S$SCR>=""
      END ELSE
         FOR IPTR=1 TO COLOR.LINE.CNT
            S$DATA(36)<S$SCR,IPTR>=FIELD(EST.PROD.INK.ID<1,COMP,1>,"!",IPTR)
            S$DATA(37)<S$SCR,IPTR>=FIELD(EST.PROD.INK.SIDES<1,COMP,1>,"!",IPTR)
            S$DATA(38)<S$SCR,IPTR>=FIELD(EST.PROD.INK.COVER<1,COMP,1>,"!",IPTR)
         NEXT IPTR
      END
80800 *
      IF PRICE.LINE.CNT=0 THEN
         S$DATA(42)<S$SCR>=""
         S$DATA(43)<S$SCR>=""
      END ELSE
         FOR QPTR=1 TO PRICE.LINE.CNT
            S$DATA(42)<S$SCR,QPTR>=FIELD(EST.PROD.PQTY<1,COMP,1>,"!",QPTR)
            S$DATA(43)<S$SCR,QPTR>=FIELD(EST.PROD.PCST<1,COMP,1>,"!",QPTR)
         NEXT QPTR
      END
80900 *
      SCREEN DISPLAY;;ALL
      RETURN
*
*-CALC PLATE REQ
*
84000 *
      PMODE=EST.PROD.MODE<1,COMP,1>
      C1=EST.PROD.COLORS.1<1,COMP,1>+0
      C2=EST.PROD.COLORS.2<1,COMP,1>+0
      BEGIN CASE
         CASE PMODE="WT" OR PMODE="WR"
            EST.PROD.PLATES<1,COMP,1>=C1
         CASE 1
            EST.PROD.PLATES<1,COMP,1>=C1+C2
      END CASE
      EST.PROD.PLATES<1,COMP,1>=EST.PROD.PLATES<1,COMP,1>*EST.PROD.FORMS<1,COMP,1>
      S$DATA(32)<S$SCR>=EST.PROD.PLATES<1,COMP,1>
      SCREEN DISPLAY;;32
      RETURN
*
*-CALC PAPER REQ
*
85000 *
      IF EST.PROD.OS.TYPE<1,COMP>="" OR EST.PROD.PRESS.ID<1,COMP>="" THEN
         EST.PROD.PQTY<1,COMP,1>=""
         EST.PROD.PCST<1,COMP,1>=""
         S$DATA(42)<S$SCR>=""
         S$DATA(43)<S$SCR>=""
         PRICE.LINE.CNT=0
         RETURN
      END ELSE
         MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,1> ELSE
            MAT ESTM.REC=""
         END
      END
      PRICE.LINE.CNT=COUNT(EST.QTY,VM)+1
      BEGIN CASE
         CASE EST.PROD.OS.TYPE<1,COMP>="S"
            MPTR=1
            CALL EST.PROD.SUB.1A(CONO,"",COMP,MPTR)
         CASE EST.PROD.OS.TYPE<1,COMP>="R"
            MPTR=1
            CALL EST.PROD.SUB.1B(CONO,"",COMP,MPTR)
      END CASE
      CALL EST.CALC.STK.PRICE(CONO,"")
      S$DATA(21)<S$SCR>=EST.PROD.PSS.OSS<1,COMP,1>
      SCREEN DISPLAY;;21
      FOR QPTR=1 TO PRICE.LINE.CNT
         S$DATA(42)<S$SCR,QPTR>=FIELD(EST.PROD.PQTY<1,COMP,1>,"!",QPTR)
         S$DATA(43)<S$SCR,QPTR>=FIELD(EST.PROD.PCST<1,COMP,1>,"!",QPTR)
      NEXT QPTR
      RETURN
*
* - DISPLAY PAPER QTY.PRICE.DATA
*
85100 *
      START.REF.NO=1+INT((PRICE.REF.NO-1)/PRICE.LINE.COUNT)*PRICE.LINE.COUNT
      IF START.REF.NO=PRICE.CURR.REF.NO THEN RETURN
      PRICE.CURR.REF.NO=START.REF.NO
      S$VAL=START.REF.NO
      S$CNT=PRICE.LINE.CNT
      SCREEN MULTI;;C;41;42;43
      RETURN
*
*-CHECK MULTI-WEB REQ
*
86000 *
      WEBERR=0
      CC=DCOUNT(EST.PROD.OS.TYPE,VM)
      FOR CP=1 TO CC
         WEBS=EST.PROD.WEB.CNT<1,CP>+0
         IF WEBS=0 THEN WEBS=1
         WC=COUNT(EST.PROD.OS.PROD<1,CP>,SM)+1
         BEGIN CASE
            CASE WEBS > WC
               ERRMSG="All webs not defined for component ":CP
               GOSUB 90000
               WEBERR=1
            CASE WC > WEBS
               FOR W=WC TO WEBS+1 STEP -1
                  AC=COUNT(EST.A9C,",")+1
                  FOR AP=1 TO AC
                     AA=FIELD(EST.A9C,",",AP)
                     AA1=FIELD(AA,"-",1)
                     AA2=FIELD(AA,"-",2)
                     IF AA2="" THEN AA2=AA1
                     FOR AAP=AA1 TO AA2
                        EST.REC(AAP)=DELETE(EST.REC(AAP),1,CP,W)
                     NEXT AAP
                  NEXT AP
               NEXT W
         END CASE
      NEXT CP
      RETURN
*
*-ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
* PRINT @(0,23):CL:ERRMSG:
* INPUT REPLY,1:
* PRINT @(0,23):CL:
* RETURN
*
*-END OF PROGRAM
*
99999 *
      SCREEN CLEAR;;D
      RETURN
   END
