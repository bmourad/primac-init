      SUBROUTINE EST.RES.PP.COST.QSUB (CONO, EST.KEY, DSEL, CSEL, QSEL, MAT CSUM.PP.REC)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RES.PP.COST.QSUB
*
* AUTHOR   - Graham Jarvis, Pacrim Software Pty Ltd
*
* DATE     - 12 May 1993
*
* DESCRIPTION
*
* Summarises Estimate Pre-Press Data Using GRP Prefix Codes
* PL = Plates, DI=Dies, SC=Screens, PR=Proofs
*
*
* MODIFIED - G Jarvis, 12 May 93
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>EST.RES.PP.COST.TYPE.SUM
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      MAT CSUM.PP.REC = ""
      COST=''
      SALE=''
      GRP.DESC=''
      GRP.TYPE=''
      READ PPDEPT FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE PPDEPT=''
*
*---- MAIN PROCESSING
*
100 *
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
         LOCATE ESTD.DEPT IN PPDEPT<1>,1 SETTING PTR ELSE PTR=0
         IF NOT(PTR) OR EST.RL.PP.INCLUDE = 'Y' THEN GOTO 190; *Pre-Press Check
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>[1,1]
            IF TYPE = "G" THEN
               GRP.TYPE = ESTD.OPV<1,TP>[1,2]
               GRP.DESC = ESTD.DESC<1,TP>
               GOTO 180
            END
            COST = ESTD.COST<1,TP>
            SALE = ESTD.TSALE<1,TP>
            BEGIN CASE
            CASE GRP.TYPE = 'PL'
               CSUM.PP.PL.COST = CSUM.PP.PL.COST + COST
               CSUM.PP.PL.SALE = CSUM.PP.PL.SALE + SALE
               CSUM.PP.PL.DESC = GRP.DESC
            CASE GRP.TYPE = 'DI'
               CSUM.PP.DI.COST = CSUM.PP.DI.COST + COST
               CSUM.PP.DI.SALE = CSUM.PP.DI.SALE + SALE
               CSUM.PP.DI.DESC = GRP.DESC
            CASE GRP.TYPE = 'SC'
               CSUM.PP.SC.COST = CSUM.PP.SC.COST + COST
               CSUM.PP.SC.SALE = CSUM.PP.SC.SALE + SALE
               CSUM.PP.SC.DESC = GRP.DESC
            CASE GRP.TYPE = 'PR'
               CSUM.PP.PR.COST = CSUM.PP.PR.COST + COST
               CSUM.PP.PR.SALE = CSUM.PP.PR.SALE + SALE
               CSUM.PP.PR.DESC = GRP.DESC
            CASE GRP.TYPE = 'MI'
               CSUM.PP.MI.COST = CSUM.PP.MI.COST + COST
               CSUM.PP.MI.SALE = CSUM.PP.MI.SALE + SALE
               CSUM.PP.MI.DESC = GRP.DESC
            END CASE
180      NEXT TP
190   NEXT DP
      RETURN
   END
