      SUBROUTINE EST.FORMULA.F501 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.F501
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/15/86
*
* DESCRIPTION
*
* This formula is used to calculate the factor to be used to modify the
* press running standard.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.RUN.EFF
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      MATREAD COMP.REC FROM COMPANY, CONO ELSE
         MAT COMP.REC = ""
      END
      IF CO.JES.PARAM<1,8> = "" OR CO.JES.PARAM<1,9> = "" THEN
         CO.JES.PARAM<1,8> = 0              ;* MAX LIGHT INK COVERAGE
         CO.JES.PARAM<1,9> = 999            ;* MAX MEDIUM INK COVERAGE
      END
      MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP, CONO:EST.PROD.OS.USAGE<1,COMP,1> ELSE
         MAT ESTPG.REC = ""
      END
      IF ESTPG.WGT.CLASS<1,1> = "" OR ESTPG.WGT.CLASS<1,2> = "" THEN
         ESTPG.WGT.CLASS<1,1> = 0           ;* MAX LIGHT PAPER WEIGHT
         ESTPG.WGT.CLASS<1,2> = 999         ;* MAX MEDIUM PAPER WEIGHT
      END
      IF EST.PROD.PRESS.ID<1,COMP> = "" THEN
         TEMP.FCTR = 1000
         RETURN
      END
*
      TEMP.FCTR = 1000
      TYP = 0
      IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
*
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*
      MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:TEMP.CCTR ELSE
         MAT EQUIPMENT.REC = ""
      END
*
      IF EST.PROD.COLORS.1<1,COMP,MPTR>+0 >= EST.PROD.COLORS.2<1,COMP,MPTR>+0 THEN
         INK.CLR = EST.PROD.COLORS.1<1,COMP,MPTR>
      END ELSE
         INK.CLR = EST.PROD.COLORS.2<1,COMP,MPTR>
      END
      MAX.COVER = 0
      ICNT = COUNT(EST.PROD.INK.ID<1,COMP,MPTR>,"!") + (EST.PROD.INK.ID<1,COMP,MPTR> # "")
      FOR IP = 1 TO ICNT
         IF FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",IP) > MAX.COVER THEN
            MAX.COVER = FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",IP)
         END
      NEXT IP
      BEGIN CASE
      CASE MAX.COVER <= CO.JES.PARAM<1,8>
         INK.COVER = "L"
      CASE MAX.COVER <= CO.JES.PARAM<1,9>
         INK.COVER = "M"
      CASE 1
         INK.COVER = "H"
      END CASE
      PAPER.WEIGHT = EST.PROD.BASIS.WT<1,COMP,MPTR>
      BEGIN CASE
      CASE PAPER.WEIGHT <= ESTPG.WGT.CLASS<1,1>
         PWEIGHT = "L"
      CASE PAPER.WEIGHT <= ESTPG.WGT.CLASS<1,2>
         PWEIGHT = "M"
      CASE 1
         PWEIGHT = "H"
      END CASE
      EFF.KEY = TEMP.CCTR:"*":INK.CLR:"*":INK.COVER:"*":PWEIGHT
      MATREAD REFF.REC FROM ESTIMATE.RUN.EFF, CONO:EFF.KEY ELSE
         EFF.KEY = TEMP.CCTR:"*":EQP.PRESS.CLR.CNT:"*M*M"
         MATREAD REFF.REC FROM ESTIMATE.RUN.EFF, CONO:EFF.KEY ELSE
            IF QPTR = 1 THEN
               ERRMSG = "Cannot locate run efficiency table"
               GOSUB 90000
            END
            TEMP.FCTR = 1000
            GOTO 99999
         END
      END
*
*---- MAIN PROCESSING
*
      MF = EST.PROD.PASSES<1,COMP,MPTR>
      IMP = FIELD(EST.PROD.IMP<1,COMP,MPTR>,"!",QPTR)
      IMP.PER.PASS = INT(IMP / MF + 0.5)
      LOCATE IMP.PER.PASS IN REFF.QTY<1>,1 BY "AR" SETTING P ELSE NULL
      BEGIN CASE
      CASE P = 1
         TEMP.FCTR = REFF.FCTR<1,P>
      CASE P > COUNT(REFF.QTY,VM) + 1
         TEMP.FCTR = REFF.FCTR<1,P-1>
      CASE REFF.EXTR = "Y"
         TEMP.FCTR = INT(REFF.FCTR<1,P>-(REFF.QTY<1,P>-IMP.PER.PASS)/(REFF.QTY<1,P>-REFF.QTY<1,P-1>)*(REFF.FCTR<1,P>-REFF.FCTR<1,P-1>)+0.5)
      CASE 1
         TEMP.FCTR = REFF.FCTR<1,P>
      END CASE
      IF TEMP.FCTR+0 <= 0 THEN TEMP.FCTR = 1000
*
      DC.CODE = EST.PROD.DC.CODE<1,COMP>
      IF DC.CODE # "" THEN
         LOCATE DC.CODE IN EQP.PRESS.DC.CODE<1>,1 SETTING FND ELSE FND = 0
         IF FND > 0 THEN
            DC.MAX = EQP.PRESS.DC.MAX<1,FND>
            IF DC.MAX < INT(TEMP.STD*(TEMP.FCTR/1000)+0.5) THEN
               TEMP.FCTR = INT(DC.MAX/TEMP.STD*1000+0.5)
            END
         END
      END
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
