*COPY>CPYLIB>SCOMMON1
********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - EST.COLLATOR.MAINT
* SOURCE   - JESBP (NJESBP)
* AUTHOR   - TINA BOND, C.B.A
* DATE     - 05/08/87
* DESCRIPTION
* This program updates and maintains the EQUIPMENT file (Collator items).
*T26126 adelgado 03/12/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","EQUIPMENT" TO EQUIPMENT ELSE
    ERRMSG = "CANNOT OPEN EQUIPMENT FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","COST.CNTR" TO COST.CNTR ELSE
    ERRMSG = "CANNOT OPEN COST.CNTR FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.COLLATOR.MAINT
  SCREEN FORMAT
*
*---- MAIN PROCESSING
*
100*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;EST.COLLATOR.MAINT;1
  SCREEN FIELD;EST.COLLATOR.MAINT;2
  SCREEN INPUT;EST.COLLATOR.MAINT;2
  IF S$VALUE = "END" THEN GOTO 99999
  EQP.KEY = S$VALUE
  MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
    ERRMSG = "Must be a valid cost center"
    GOSUB 90000
    GOTO 100
  END
  S$DATA(3)<S$SCR> = CCTR.DESC
  SCREEN DISPLAY;EST.COLLATOR.MAINT;3
  NEW.REC = 0
  * T26126 v
  MATREADU EQUIPMENT.REC FROM EQUIPMENT, CONO:EQP.KEY LOCKED
    ERRMSG = 'COLLATOR record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 100 
  END ELSE
  * T26126 ^
    MAT EQUIPMENT.REC = ""
    NEW.REC = 1
  END
  IF EQP.TYPE # "" AND EQP.TYPE # "34" THEN
    RELEASE
    ERRMSG = "Item has already been defined as other than a Collator binder"
    GOSUB 90000
    GOTO 100
  END
  IF NEW.REC THEN
    EQP.TYPE = "34"
    FOR FLD = 1 TO 10
      ON FLD GOSUB 10100,10200,10300,10400,10500,11100,11200,11300,11400,11500
      IF S$VALUE = "END" THEN
        SCREEN CLEAR;EST.COLLATOR.MAINT
        RELEASE
        GOTO 100
      END
    NEXT FLD
  END ELSE
    GOSUB 80000
    SCREEN DISPLAY;EST.COLLATOR.MAINT;ALL
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.COLLATOR.MAINT;21
  SCREEN INPUT;EST.COLLATOR.MAINT;21
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION # "" AND NUM(OPTION)
      FLD = OPTION
      ON FLD GOSUB 10100,10200,10300,10400,10500,19990,19990,19990,19990,19990,11100,11200,11300,11400,11500
    CASE OPTION = "E" OR OPTION = "END"
      SCREEN CLEAR;EST.COLLATOR.MAINT
      RELEASE
      GOTO 100
    CASE OPTION = "F"
      MATWRITE EQUIPMENT.REC ON EQUIPMENT, CONO:EQP.KEY
      SCREEN CLEAR;EST.COLLATOR.MAINT
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET NUMBER OF BINS
*
10100*
  SCREEN FIELD;EST.COLLATOR.MAINT;4
  SCREEN INPUT;EST.COLLATOR.MAINT;4;RETURN
  EQP.COLLATOR.BIN.CNT = S$VALUE
  RETURN
*
*---- GET BIN CAPACITY
*
10200*
  SCREEN FIELD;EST.COLLATOR.MAINT;5
  SCREEN INPUT;EST.COLLATOR.MAINT;5;RETURN
  EQP.COLLATOR.CAPACITY = S$VALUE
  RETURN
*
*---- GET TAB/CARD
*
10300*
  SCREEN FIELD;EST.COLLATOR.MAINT;6
  SCREEN INPUT;EST.COLLATOR.MAINT;6;RETURN
  EQP.COLLATOR.TAB.CARD = S$VALUE
  RETURN
*
*---- GET PROGRAMMING SETS PER CYCLE
*
10400*
  SCREEN FIELD;EST.COLLATOR.MAINT;7
  SCREEN INPUT;EST.COLLATOR.MAINT;7;RETURN
  EQP.COLLATOR.PROG.SETS = S$VALUE
  RETURN
*
*---- SHEETS PER SET FOR PROGRAMMING
*
10500*
  IF EQP.COLLATOR.PROG.SETS = "" THEN
    S$VALUE = ""
  END ELSE
    SCREEN FIELD;EST.COLLATOR.MAINT;8
    SCREEN INPUT;EST.COLLATOR.MAINT;8;RETURN
  END
  EQP.COLLATOR.PROG.SHTS = S$VALUE
  RETURN
*
*---- GET SET-UP
*
11100*
  S$DATA(11)<S$SCR> = EQP.COLLATOR.SET.UP
  SCREEN FIELD;EST.COLLATOR.MAINT;11
  SCREEN INPUT;EST.COLLATOR.MAINT;11;RETURN
  LOCATE S$VALUE IN CCTR.OPER<1>,1 SETTING FND ELSE
    ERRMSG = "Invalid operation for specified collator"
    GOSUB 90000
    GOTO 11100
  END
  EQP.COLLATOR.SET.UP = S$VALUE
  RETURN
*
*---- GET LAYOUT
*
11200*
  S$DATA(12)<S$SCR> = EQP.COLLATOR.LAYOUT
  SCREEN FIELD;EST.COLLATOR.MAINT;12
  SCREEN INPUT;EST.COLLATOR.MAINT;12;RETURN
  LOCATE S$VALUE IN CCTR.OPER<1>,1 SETTING FND ELSE
    ERRMSG = "Invalid operation for specified collator"
    GOSUB 90000
    GOTO 11200
  END
  EQP.COLLATOR.LAYOUT = S$VALUE
  RETURN
*
*---- GET LOAD STOCK
*
11300*
  S$DATA(13)<S$SCR> = EQP.COLLATOR.LOAD.SHT
  SCREEN FIELD;EST.COLLATOR.MAINT;13
  SCREEN INPUT;EST.COLLATOR.MAINT;13;RETURN
  LOCATE S$VALUE IN CCTR.OPER<1>,1 SETTING FND ELSE
    ERRMSG = "Invalid operation for specified collator"
    GOSUB 90000
    GOTO 11300
  END
  EQP.COLLATOR.LOAD.SHT = S$VALUE
  RETURN
*
*---- GET RUN SHEETS
*
11400*
  S$DATA(14)<S$SCR> = EQP.COLLATOR.RUN.SHT
  SCREEN FIELD;EST.COLLATOR.MAINT;14
  SCREEN INPUT;EST.COLLATOR.MAINT;14;RETURN
  LOCATE S$VALUE IN CCTR.OPER<1>,1 SETTING FND ELSE
    ERRMSG = "Invalid operation for specified collator"
    GOSUB 90000
    GOTO 11400
  END
  EQP.COLLATOR.RUN.SHT = S$VALUE
  RETURN
*
*---- GET RUN TABS
*
11500*
  S$DATA(15)<S$SCR> = EQP.COLLATOR.RUN.TAB
  SCREEN FIELD;EST.COLLATOR.MAINT;15
  SCREEN INPUT;EST.COLLATOR.MAINT;15;RETURN
  LOCATE S$VALUE IN CCTR.OPER<1>,1 SETTING FND ELSE
    ERRMSG = "Invalid operation for specified collator"
    GOSUB 90000
    GOTO 11500
  END
  EQP.COLLATOR.RUN.TAB = S$VALUE
  RETURN
*
*---- DUMMY
*
19990*
  RETURN
*
*---- LOAD SCREEN VALUES
*
80000*
  MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
    MAT CCTR.REC = ""
    CCTR.DESC = "??????????"
  END
  S$DATA(3)<S$SCR> = CCTR.DESC
  S$DATA(4)<S$SCR> = EQP.COLLATOR.BIN.CNT
  S$DATA(5)<S$SCR> = EQP.COLLATOR.CAPACITY
  S$DATA(6)<S$SCR> = EQP.COLLATOR.TAB.CARD
  S$DATA(7)<S$SCR> = EQP.COLLATOR.PROG.SETS
  S$DATA(8)<S$SCR> = EQP.COLLATOR.PROG.SHTS
  S$DATA(11)<S$SCR> = EQP.COLLATOR.SET.UP
  S$DATA(12)<S$SCR> = EQP.COLLATOR.LAYOUT
  S$DATA(13)<S$SCR> = EQP.COLLATOR.LOAD.SHT
  S$DATA(14)<S$SCR> = EQP.COLLATOR.RUN.SHT
  S$DATA(15)<S$SCR> = EQP.COLLATOR.RUN.TAB
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  STOP
END
