      SUBROUTINE EST.UPDATE (CONO, ACTION, EST.DUP.KEY, EST.KEY, ESTIMATE.DEPT, ESTIMATE.DEPT.BU)
**************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.UPDATE
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/31/86
*
* DESCRIPTION
*
* This program will back-up or update the ESTIMATE.DEPT file based on the
* ACTION selected as follows:
*
*   ACTION = "C" - Copy ESTIMATE.DEPT data to ESTIMATE.DEPT.BU file
*            "F" - Copy ESTIMATE.DEPT.BU data to ESTIMATE.DEPT file
*            "E" - Delete data from ESTIMATE.DEPT.BU file.
*
**************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>CPYLIB>CHAR
*
*---- MAIN PROCESSING
*
      BEGIN CASE
         CASE ACTION = "C"
            READU SAVE.KEYS FROM ESTIMATE.DEPT.BU, CONO:"*":EST.DUP.KEY:"*" ELSE SAVE.KEYS = ""
            DCNT = COUNT(SAVE.KEYS,VM) + (SAVE.KEYS # "")
            FOR DPTR = 1 TO DCNT
               ESTD.ID = SAVE.KEYS<1,DPTR>
               DELETE ESTIMATE.DEPT.BU, CONO:EST.DUP.KEY:"!":ESTD.ID
            NEXT DPTR
            WRITE EST.DEPT.COMP ON ESTIMATE.DEPT.BU, CONO:"*":EST.KEY:"*"
            DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
            FOR DPTR = 1 TO DCNT
               ESTD.ID = EST.DEPT.COMP<1,DPTR>
               READ REC FROM ESTIMATE.DEPT, CONO:EST.DUP.KEY:"!":ESTD.ID ELSE GOTO 190
               WRITE REC ON ESTIMATE.DEPT.BU, CONO:EST.KEY:"!":ESTD.ID
190         NEXT DPTR
         CASE ACTION = "F"
            READ SAVE.KEYS FROM ESTIMATE.DEPT.BU, CONO:"*":EST.KEY:"*" ELSE SAVE.KEYS = ""
            DCNT = COUNT(SAVE.KEYS,VM) + (SAVE.KEYS # "")
            FOR DPTR = 1 TO DCNT
               ESTD.ID = SAVE.KEYS<1,DPTR>
               LOCATE ESTD.ID IN EST.DEPT.COMP<1>,1 SETTING P ELSE
                  DELETE ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID
               END
            NEXT DPTR
            DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
            FOR DPTR = 1 TO DCNT
               ESTD.ID = EST.DEPT.COMP<1,DPTR>
               READ REC FROM ESTIMATE.DEPT.BU, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 290
               WRITE REC ON ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID
               DELETE ESTIMATE.DEPT.BU, CONO:EST.KEY:"!":ESTD.ID
290         NEXT DPTR
            DELETE ESTIMATE.DEPT.BU, CONO:"*":EST.KEY:"*"
         CASE ACTION = "E"
            READ SAVE.KEYS FROM ESTIMATE.DEPT.BU, CONO:"*":EST.KEY:"*" ELSE SAVE.KEYS = ""
            DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
            FOR DPTR = 1 TO DCNT
               ESTD.ID = EST.DEPT.COMP<1,DPTR>
               LOCATE ESTD.ID IN SAVE.KEYS<1>,1 SETTING P ELSE
                  DELETE ESTIMATE.DEPT.BU, CONO:EST.KEY:"!":ESTD.ID
               END
            NEXT DPTR
            DCNT = COUNT(SAVE.KEYS,VM) + (SAVE.KEYS # "")
            FOR DPTR = 1 TO DCNT
               ESTD.ID = SAVE.KEYS<1,DPTR>
               DELETE ESTIMATE.DEPT.BU, CONO:EST.KEY:"!":ESTD.ID
            NEXT DPTR
            DELETE ESTIMATE.DEPT.BU, CONO:"*":EST.KEY:"*"
      END CASE
      RETURN
   END
