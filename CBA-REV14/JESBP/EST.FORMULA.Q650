      SUBROUTINE EST.FORMULA.Q650 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q650
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/27/86
*
* DESCRIPTION
*
* This formula is used to calculate lift requirements for specified
* cutter and job specifications. It should be used for prepress
* cutting when the order size is larger than the press size.
*
* CLONED FROM FORMULA 651
* MOD TASK 16263 DMT 8/26/93 USE PAPER QTY
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEMP.QTY = ""
      TYP = 0
*
      MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:TEMP.CCTR ELSE
         MAT EQUIPMENT.REC = ""
      END
*
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
10*
      BEGIN CASE
*     CASE EST.FORMULA.VAR<1,2> # ""
*        TEMP.FVAR1 = EST.FORMULA.VAR<1,2>
      CASE QPTR > 1 OR ACTION = "R"
         NULL
      CASE 1
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=14;MAXL=4;MINV=1;MAXV=999;JUSTIFY="L"
         BEGIN CASE
         CASE TEMP.FVAR1 # ""
            DEFAULT = TEMP.FVAR1 / 100
         CASE EQP.CUTTER.IN.PER.LIFT # ""
            DEFAULT = EQP.CUTTER.IN.PER.LIFT / 100
         CASE 1
            DEFAULT = ""
         END CASE
         IF DEFAULT = "" THEN
            PMSG = "Inches per lift ?"
         END ELSE
            O.R = "O"
            PMSG = "Inches per lift (Default = ":DEFAULT:") ?"
         END
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         TEMP.FVAR1 = VALUE
*        EST.FORMULA.VAR<1,2> = TEMP.FVAR1
      END CASE
      INCHES.PER.LIFT = TEMP.FVAR1
*
      WEB.CNT = EST.PROD.WEB.CNT<1,COMP>+0
      IF WEB.CNT = 0 THEN WEB.CNT = 1
      TOT.PPI = 0
      FOR MPTR = 1 TO WEB.CNT
         IF EST.PROD.CALIPER<1,COMP,MPTR>="" THEN
            PPI = EST.PROD.PPI<1,COMP,MPTR>
         END ELSE
            PPI = INT(2000 / (EST.PROD.CALIPER<1,COMP,MPTR>/1000) + 0.5)
         END
         TOT.PPI = TOT.PPI + PPI
      NEXT MPTR
      PPI = INT(TOT.PPI/WEB.CNT+0.5)
*
*---- MAIN PROCESSING
*
      NUMBER.UP = EST.PROD.NO.UP<1,COMP,1>+0
      IF NUMBER.UP = 0 THEN NUMBER.UP = 1
      FORMS = EST.PROD.FORMS<1,COMP,1>+0
      IF FORMS = 0 THEN FORMS = 1
      PROD.QTY = FIELD(EST.PROD.PQTY<1,COMP,1>,"!",QPTR)
      IF INCHES.PER.LIFT + 0 = 0 THEN
         TEMP.QTY = 0
       END ELSE
*         TEMP.QTY = INT((CUT.SHT / 1000) * (2000/PPI) / (INCHES.PER.LIFT / 100) + 0.99)
         TEMP.QTY = INT((PROD.QTY / 1000) * (2000/PPI) / (INCHES.PER.LIFT / 100) + 0.99)
       END
      TEMP.VSALE = "Y"
      RETURN
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
