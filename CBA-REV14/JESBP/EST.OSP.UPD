      SUBROUTINE EST.OSP.UPD (CONO, ACTION, DPTR, COMP, EQTY, LPTR)
*********************************************************************
*
* PROGRAM  - EST.OSP.UPD
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 06/30/88
*
* DESCRIPTION
*
* This program will update the summary O/P data stored on the
* estimate from the detail data as sepecified by the action code.
*
* ACTION   (P)urge summary data
*          (R)eplace summary data
*          (A)dd summary data
*          (I)nsert summary data
*          (C)hange summary data
* TASK 20636 JR OUTSIDE PURCHASES USING Q030 ONLY UPDATE FIRST QTY
*
*T22603 gat 02/12/1998 * FIX PRICE PROBLEM WITH MULTIPLE QTY.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      LOCATE EQTY IN EST.QTY<1>,1 SETTING EPTR ELSE GOTO 99999
*
*---- MAIN PROCESSING
*
100*
      BEGIN CASE
         CASE ACTION = "P"
            OSP.DC = DPTR:"!":COMP
            LOCATE OSP.DC IN EST.DEPT.OSP<1>,1 SETTING DCP ELSE DCP = 0
            IF DCP > 0 THEN
               BEGIN CASE
                  CASE EPTR = 1
                     EST.DEPT.OSP = DELETE(EST.DEPT.OSP,1,DCP,0)
                     EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DCP,0)
                     EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DCP,0)
                     EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DCP,0)
                     EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DCP,0)
                  CASE 1
                     OPCNT = DCOUNT(EST.DEPT.OSP.ID<1,DCP>,SM)
                     FOR VCP = 1 TO OPCNT
                        MACRO REPVAL;EST.DEPT.OSP.QTY<1,DCP,VCP>;"!";EPTR;""
                        MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;""
                     NEXT VCP
               END CASE
            END
         CASE ACTION = "D" OR (ACTION = "C" AND TEMP.CODE # ESTD.CODE<1,LPTR>)
            OSP.DC = DPTR:"!":COMP
            LOCATE OSP.DC IN EST.DEPT.OSP<1>,1 SETTING DCP ELSE DCP = 0
            IF DCP > 0 THEN
               OSP.ID = ESTD.OPV<1,LPTR>:"!":ESTD.CODE<1,LPTR>
               LOCATE OSP.ID IN EST.DEPT.OSP.ID<1,DCP>,1 SETTING VCP ELSE VCP = 0
               IF VCP > 0 THEN
                  OPCNT = DCOUNT(EST.DEPT.OSP.ID<1,DCP>,SM)
                  BEGIN CASE
                     CASE OPCNT = 1 AND EPTR = 1
                        EST.DEPT.OSP = DELETE(EST.DEPT.OSP,1,DCP,0)
                        EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DCP,0)
                        EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DCP,0)
                        EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DCP,0)
                        EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DCP,0)
                     CASE EPTR = 1
                        EST.DEPT.OSP.ID = DELETE(EST.DEPT.OSP.ID,1,DCP,VCP)
                        EST.DEPT.OSP.UM = DELETE(EST.DEPT.OSP.UM,1,DCP,VCP)
                        EST.DEPT.OSP.QTY = DELETE(EST.DEPT.OSP.QTY,1,DCP,VCP)
                        EST.DEPT.OSP.PRICE = DELETE(EST.DEPT.OSP.PRICE,1,DCP,VCP)
                     CASE 1
                        MACRO REPVAL;EST.DEPT.OSP.QTY<1,DCP,VCP>;"!";EPTR;""
                        MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;""
                  END CASE
               END
            END
      END CASE
      IF ACTION = "A" OR ACTION = "C" OR ACTION = "I" OR ACTION = "R" THEN
         OSP.DC = DPTR:"!":COMP
         OSP.ID = TEMP.OPV:"!":TEMP.CODE
         DCFND = 1
         VCFND = 0
         LOCATE OSP.DC IN EST.DEPT.OSP<1>,1 SETTING DCP ELSE
            DCFND = 0
            DCP = 0
            LOOP
               DCP = DCP + 1
               DC = EST.DEPT.OSP<1,DCP>
               D = FIELD(DC,"!",1)
               C = FIELD(DC,"!",2)
            UNTIL (DC = "") OR (D > DPTR) OR (D = DPTR AND C > COMP) DO
            REPEAT
         END
         IF DCFND THEN
            VCFND = 1
            LOCATE OSP.ID IN EST.DEPT.OSP.ID<1,DCP>,1 SETTING VCP ELSE VCFND = 0
         END
         BEGIN CASE
            CASE DCFND AND VCFND
               IF TEMP.TYPE = "P" THEN
                  EST.DEPT.OSP.UM<1,DCP,VCP> = "EA"
               END ELSE
                  EST.DEPT.OSP.UM<1,DCP,VCP> = TEMP.TYPE[2,1]
               END
               MACRO REPVAL;EST.DEPT.OSP.QTY<1,DCP,VCP>;"!";EPTR;TEMP.QTY*100
               BEGIN CASE
                  CASE ACTION = "R"
                     PRICE = FIELD(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR)
* T20636
*                     IF PRICE = "" THEN
                        IF TEMP.GRP.QPARAM = "030" THEN
                           MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;TEMP.STD*100
                        END ELSE
*T22603
                           PRICE = FIELD(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR)
*T22603
                           MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;PRICE
                        END
*                     END
                     PRICE = FIELD(EST.DEPT.OSP.PRICE<1,DCP,VCP>,"!",EPTR)
* T20636
                     IF PRICE # "" THEN TEMP.STD = PRICE/100
                  CASE 1
                     MACRO REPVAL;EST.DEPT.OSP.PRICE<1,DCP,VCP>;"!";EPTR;TEMP.STD*100
               END CASE
            CASE DCFND
               EST.DEPT.OSP.ID<1,DCP,VCP> = OSP.ID
               IF TEMP.TYPE = "P" THEN
                  EST.DEPT.OSP.UM<1,DCP,VCP> = "EA"
               END ELSE
                  EST.DEPT.OSP.UM<1,DCP,VCP> = TEMP.TYPE[2,1]
               END
               EST.DEPT.OSP.QTY<1,DCP,VCP> = TEMP.QTY*100
               EST.DEPT.OSP.PRICE<1,DCP,VCP> = TEMP.STD*100
            CASE 1
               EST.DEPT.OSP = INSERT(EST.DEPT.OSP,1,DCP,0,OSP.DC)
               EST.DEPT.OSP.ID = INSERT(EST.DEPT.OSP.ID,1,DCP,0,OSP.ID)
               IF TEMP.TYPE = "P" THEN
                  EST.DEPT.OSP.UM = INSERT(EST.DEPT.OSP.UM,1,DCP,0,"EA")
               END ELSE
                  EST.DEPT.OSP.UM = INSERT(EST.DEPT.OSP.UM,1,DCP,0,TEMP.TYPE[2,1])
               END
               EST.DEPT.OSP.QTY = INSERT(EST.DEPT.OSP.QTY,1,DCP,0,TEMP.QTY*100)
               EST.DEPT.OSP.PRICE = INSERT(EST.DEPT.OSP.PRICE,1,DCP,0,TEMP.STD*100)
         END CASE
      END
*
*---- END OF PROGRAM
*
99999*
      RETURN
   END
