   SUBROUTINE EST.COST.TYPE.SUB (CONO, EST.KEY, DSEL, CSEL, QSEL, MAT CSUM.REC)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.COST.TYPE.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/05/86
*
* DESCRIPTION
*
* This subroutine summarizes estimate data by type. (Labor, Material, O/P,
* Shipping, Miscellaneous).
* Summarization may occur for a single department or "ALL", a single
* component or "ALL", and a specified order quantity.
*
*T27716 cmykleb 10/08/2003 * Add Pre-press inclusive functionality.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>EST.COST.TYPE.SUM
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   MAT CSUM.REC = ""
*T27716 v
   READ EST.PP FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE EST.PP=''
*T27716 ^
*
*---- MAIN PROCESSING
*
100*
   DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
   FOR DP = 1 TO DCNT
      ESTD.ID = EST.DEPT.COMP<1,DP>
      IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
      IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
      IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
      MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
      TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
      FOR TP = 1 TO TCNT
         TYPE = ESTD.TYPE<1,TP>[1,1]
         IF TYPE = "G" THEN GOTO 180
         TSUB = ESTD.TYPE<1,TP>[2,1]
         HRS = ESTD.HRS<1,TP>
         DCOST = ESTD.DCOST<1,TP>
         COST = ESTD.COST<1,TP>
*T27716 v
*        SALE = ESTD.TSALE<1,TP>
         IF EST.PP.INCLUDE#'Y' THEN
            LOCATE ESTD.DEPT IN EST.PP<1>,1 SETTING DPOS THEN
               SALE = 0
            END ELSE
               SALE = ESTD.TSALE<1,TP>
            END
         END ELSE
            SALE = ESTD.TSALE<1,TP>
         END
*T27716 ^
         BEGIN CASE
            CASE TYPE = "L"
               CSUM.LB.HRS = CSUM.LB.HRS + HRS
               CSUM.LB.DCOST = CSUM.LB.DCOST + DCOST
               CSUM.LB.COST = CSUM.LB.COST + COST
               CSUM.LB.SALE = CSUM.LB.SALE + SALE
            CASE TYPE = "M" OR TYPE = "I"
               CSUM.MT.DCOST = CSUM.MT.DCOST + DCOST
               CSUM.MT.COST = CSUM.MT.COST + COST
               CSUM.MT.SALE = CSUM.MT.SALE + SALE
               BEGIN CASE
                  CASE TSUB = "S" OR TSUB = "R" OR TSUB = "L"
                     DET = 1
                  CASE TSUB = "I"
                     DET = 2
                  CASE TSUB = "F"
                     DET = 3
                  CASE TSUB = "P"
                     DET = 4
                  CASE TSUB = "C"
                     DET = 5
                  CASE 1
                     DET = 6
               END CASE
               CSUM.MT.DCOST.DET<1,DET> = CSUM.MT.DCOST.DET<1,DET> + DCOST
               CSUM.MT.COST.DET<1,DET> = CSUM.MT.COST.DET<1,DET> + COST
               CSUM.MT.SALE.DET<1,DET> = CSUM.MT.SALE.DET<1,DET> + SALE
            CASE TYPE = "P"
               CSUM.OS.DCOST = CSUM.OS.DCOST + DCOST
               CSUM.OS.COST = CSUM.OS.COST + COST
               CSUM.OS.SALE = CSUM.OS.SALE + SALE
            CASE TYPE = "S"
               CSUM.SP.DCOST = CSUM.SP.DCOST + DCOST
               CSUM.SP.COST = CSUM.SP.COST + COST
               CSUM.SP.SALE = CSUM.SP.SALE + SALE
            CASE TYPE = "O"
               CSUM.MS.DCOST = CSUM.MS.DCOST + DCOST
               CSUM.MS.COST = CSUM.MS.COST + COST
               CSUM.MS.SALE = CSUM.MS.SALE + SALE
         END CASE
         CSUM.TOT.DCOST = CSUM.TOT.DCOST + DCOST
         CSUM.TOT.COST = CSUM.TOT.COST + COST
         CSUM.TOT.SALE = CSUM.TOT.SALE + SALE
180   NEXT TP
190 NEXT DP
   RETURN
END
