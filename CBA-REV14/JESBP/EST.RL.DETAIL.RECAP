      SUBROUTINE EST.DETAIL.RECAP (CONO,CO.NAME,EST.KEY,JOB.DESC, PAGE.NO)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.DETAIL.RECAP
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 06/08/87
*
* DESCRIPTION
*
* This subroutine will summarize the detail estimate data by department,
* cost center and operation displaying up to three quantities accross the
* page.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RL
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- DIMENSIONED VARIABLES
*
      DIM PRT.REC(15)
      EQU P.TYPE    TO PRT.REC(1)    ;* (MV1)
      EQU P.CCTR    TO PRT.REC(2)    ;* (MV1)
      EQU P.OPV     TO PRT.REC(3)    ;* (MV1)
      EQU P.DESC    TO PRT.REC(4)    ;* (MV1)
      EQU P.QTY     TO PRT.REC(5)    ;* (MV1,MSV)
      EQU P.HRS     TO PRT.REC(6)    ;* (MV1,MSV)
      EQU P.FCTR    TO PRT.REC(7)    ;* (MV1,MSV)
      EQU P.UM      TO PRT.REC(8)    ;* (MV1)
      EQU P.SELL    TO PRT.REC(9)    ;* (MV1,MSV)
      EQU P.THRS    TO PRT.REC(10)   ;* (MSV)
      EQU P.TSELL   TO PRT.REC(11)   ;* (MSV)
      EQU P.GHRS    TO PRT.REC(12)   ;* (MSV)
      EQU P.GSELL   TO PRT.REC(13)   ;* (MSV)
*
*---- INITIALIZATION
*
      N = ((50 - LEN(CO.NAME)) / 2)
      COMPY.NAME = SPACE(N):CO.NAME:SPACE(N)
      HD1 = OCONV(DATE(),"D2/"):SPACE(33):COMPY.NAME:SPACE(30):"PAGE: "
      HD2 = SPACE(38):"E S T I M A T E   D E T A I L   R E C A P   R E P O R T"
      HD3 = "ESTIMATE : ":EST.KEY "L#12":"    ":EST.CUST.NAME "L#40":"    ":JOB.DESC "L#60"
      ECNT = COUNT(EST.QTY,VM) + (EST.QTY # "")
      IF ECNT > 3 THEN ECNT = 3
      SH1 = SPACE(34)
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         QQ = OCONV(EST.QTY<1,EPTR>,"MD0,")
         QQ = QQ:SPACE((9-LEN(QQ))/2)
         SH1 = SH1:FILL1:"<------- ":QQ"R#9":" ------->"
         FILL1 = " : "
      NEXT EPTR 
* SH2 = SPACE(34)
* FILL1 = "  "
* FOR EPTR = 1 TO ECNT
* SH2 = SH2:FILL1:"                RECOMMENDED"
* FILL1 = " : "
* NEXT EPTR 
      SH3 = "           DESCRIPTION            "
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         SH3 = SH3:FILL1:"   UNITS   U/M     SELL    "
         FILL1 = " : "
      NEXT EPTR 
      SH4 = "----------------------------------"
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         SH4 = SH4:FILL1:"---------- --- ------------"
         FILL1 = " : "
      NEXT EPTR 
      UL1 = SPACE(34)
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         UL1 = UL1:FILL1:"----------     ------------"
         FILL1 = " : "
      NEXT EPTR 
      UL2 = SPACE(34)
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         UL2 = UL2:FILL1:"==========     ============"
         FILL1 = " : "
      NEXT EPTR 
      VL1 = ""
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         VL1 = VL1:FILL1:SPACE(27)
         FILL1 = " : "
      NEXT EPTR 
      MAT PRT.REC = ""
      PRINTER ON
*
*---- MAIN PROCESSING
*
100 *
      CCNT = EST.COMPONENT.CNT
      DCNT = COUNT(EST.DEPT,VM) + (EST.DEPT # "")
      FOR CPTR = 1 TO CCNT
         LINE.CNT = 99
         FOR DPTR = 1 TO DCNT
            GOSUB 1000
         NEXT DPTR
      NEXT CPTR
      PRINT UL2
      PLINE = SPACE(34)
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         PLINE = PLINE:FILL1:OCONV(P.GHRS<1,1,EPTR>,"MD2,") "R#10"
         PLINE = PLINE:SPACE(1):"HRS"
         PLINE = PLINE:SPACE(1):OCONV(P.GSELL<1,1,EPTR>,"MD2,") "R#12"
         FILL1 = " : "
      NEXT EPTR
      PRINT PLINE
      GOTO 99999
*
*---- PRINT DEPARTMENT DETAIL FOR SPECIFIED COMPONENT
*
1000 *
      P.CCTR = ""
      P.OPV = ""
      P.DESC = ""
      P.QTY = ""
      P.HRS = ""
      FOR EPTR = 1 TO ECNT
         EQTY = EST.QTY<1,EPTR>
         ESTD.ID = DPTR:"!":CPTR:"!":EQTY
         LOCATE ESTD.ID IN EST.DEPT.COMP<1>,1 SETTING DCP ELSE RETURN
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE RETURN
         LPTR = 0
         TC = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TC
            TYPE = ESTD.TYPE<1,TP>
            IF TYPE[1,1] = "G" THEN
               LPTR = LPTR + 1
               P.TYPE<1,LPTR> = "G"
               P.OPV<1,LPTR> = ESTD.OPV<1,TP>
               P.DESC<1,LPTR> = ESTD.DESC<1,TP>
               GOTO 1090
            END
            LPTR = LPTR + 1
            IF EPTR = 1 THEN
               P.TYPE<1,LPTR> = TYPE
               P.CCTR<1,LPTR> = ESTD.CCTR<1,TP>
               P.OPV<1,LPTR> = ESTD.OPV<1,TP>
               P.DESC<1,LPTR> = ESTD.DESC<1,TP>
               P.UM<1,LPTR> = ESTD.STD.TYPE<1,TP>
            END ELSE
               IF ESTD.CCTR<1,TP> # P.CCTR<1,LPTR> THEN
                  ERRMSG = "Department detail mismatch"
               END
               IF ESTD.CCTR<1,TP> # P.CCTR<1,LPTR> THEN
                  ERRMSG = "Department detail mismatch"
               END
            END
            BEGIN CASE
            CASE TYPE[1,1] = "L"
               P.HRS<1,LPTR,EPTR> = ESTD.HRS<1,TP>
               P.THRS<1,1,EPTR> = P.THRS<1,1,EPTR> + P.HRS<1,LPTR,EPTR>
               P.GHRS<1,1,EPTR> = P.GHRS<1,1,EPTR> + P.HRS<1,LPTR,EPTR>
            CASE 1
               P.QTY<1,LPTR,EPTR> = ESTD.QTY<1,TP>
            END CASE
            P.FCTR<1,LPTR,EPTR> = ESTD.FCTR<1,TP>
            P.SELL<1,LPTR,EPTR> = ESTD.TSALE<1,TP>
            P.TSELL<1,1,EPTR> = P.TSELL<1,1,EPTR> + ESTD.TSALE<1,TP>
            P.GSELL<1,1,EPTR> = P.GSELL<1,1,EPTR> + ESTD.TSALE<1,TP>
1090     NEXT TP
      NEXT EPTR
      IF LPTR = 0 THEN RETURN
*
*---- PRINT DEPARTMENT DETAIL
*
      LCNT = LPTR
      IF LINE.CNT + 4 > 55 THEN GOSUB 10000
      PLINE = EST.DEPT.DESC<1,DPTR> "L#34"
      PLINE = PLINE : VL1
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      PREV.CCTR = ""
      FOR LPTR = 1 TO LCNT
         BEGIN CASE
         CASE P.TYPE<1,LPTR> = "G"
            IF LINE.CNT + 3 > 55 THEN GOSUB 10000
            PLINE = (SPACE(1):P.DESC<1,LPTR>) "L#34"
            PLINE = PLINE : VL1
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
            PREV.CCTR = ""
            GOTO 1590
         CASE P.CCTR<1,LPTR> # PREV.CCTR
            IF LINE.CNT + 2 > 55 THEN GOSUB 10000
            PREV.CCTR = P.CCTR<1,LPTR>
            MATREAD CCTR.REC FROM COST.CNTR, CONO:P.CCTR<1,LPTR> ELSE
               MAT CCTR.REC = ""
               CCTR.DESC = "???????????????"
            END
            PLINE = (SPACE(2):CCTR.DESC) "L#34"
            PLINE = PLINE : VL1
            PRINT PLINE
            LINE.CNT = LINE.CNT + 1
         END CASE
         IF LINE.CNT > 55 THEN GOSUB 10000
         PLINE = (SPACE(4):P.DESC<1,LPTR>) "L#34"
         FILL1 = "  "
         FOR EPTR = 1 TO ECNT
            IF P.HRS<1,LPTR,EPTR> # "" THEN
               PLINE = PLINE:FILL1:OCONV(P.HRS<1,LPTR,EPTR>,"MD2,") "R#10"
               UM = "HRS"
            END ELSE
               QTY = P.QTY<1,LPTR,EPTR>
               IF MOD(QTY,100) = 0 THEN
*                 PLINE = PLINE:FILL1:OCONV(INT(QTY/100),"MD0,") "R#7" : "   "
                  PLINE = PLINE:FILL1:OCONV(INT(QTY/100),"MD0,") "R#10"
               END ELSE
                  PLINE = PLINE:FILL1:OCONV(QTY,"MD2,") "R#10"
               END
               TYPE = P.TYPE<1,LPTR>
               UM = P.UM<1,LPTR>[3,3]
               BEGIN CASE
               CASE EST.RL.JOB.TYPE # "" AND (TYPE="MS" OR TYPE="MR")
                  UM = "MSI"
               CASE TYPE = "MS"
                  UM = "SHT"
               CASE TYPE = "MR"
                  UM = "LBS"
               CASE UM = "LB"
                  UM = "LBS"
* CASE UM = "EA"
* UM = "PCS"
* CASE UM = "M"
* UM = "PCS"
* CASE UM = "C"
* UM = "PCS"
* CASE UM = "TTL"
* UM = "PCS"
               CASE 1
                  UM = ""
               END CASE
            END
            PLINE = PLINE:SPACE(1):UM"L#3"
            PLINE = PLINE:SPACE(1):OCONV(P.SELL<1,LPTR,EPTR>,"MD2,") "R#12"
            FILL1 = " : "
         NEXT EPTR
         PRINT PLINE
         LINE.CNT = LINE.CNT + 1
1590  NEXT LPTR
      PRINT UL1
      LINE.CNT = LINE.CNT + 1
      PLINE = SPACE(34)
      FILL1 = "  "
      FOR EPTR = 1 TO ECNT
         PLINE = PLINE:FILL1:OCONV(P.THRS<1,1,EPTR>,"MD2,") "R#10"
         PLINE = PLINE:SPACE(1):"HRS"
         PLINE = PLINE:SPACE(1):OCONV(P.TSELL<1,1,EPTR>,"MD2,") "R#12"
         FILL1 = " : "
      NEXT EPTR
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      P.THRS = ""
      P.TSELL = ""
      RETURN
*
*---- PRINT HEADING
*
10000 *
      PRINT CHAR(12)
      PAGE.NO = PAGE.NO + 1
      PRINT HD1:PAGE.NO
      PRINT HD2
      PRINT
      PRINT HD3
      PRINT "COMPONENT: ":EST.PROD.DESC<1,CPTR>
      PRINT
      PRINT SH1
* PRINT SH2
      PRINT SH3
      PRINT SH4
* LINE.CNT = 10
      LINE.CNT = 9
      RETURN
*
*---- END OF PROGRAM
*
99999 *
      PRINTER OFF
      RETURN
      END
