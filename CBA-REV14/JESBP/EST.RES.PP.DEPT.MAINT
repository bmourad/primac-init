*********************************************************************
*
* Copyright 1982 by Vercom Software Inc.
*
* REVISION - [10.0]
*
* PROGRAM  - EST.RES.PP.DEPT.MAINT
*
* AUTHOR   - RICK BROWN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 10/05/94
*
* DESCRIPTION
*
* This program will maintain the the control entry record 
* CONO:"EST.RES.PRE.PRESS"
*  the format of the record is 1 attr with MV dept IDS
*
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*
* --- SET SYSCOM
*
  SYS.TYPE = 1
  CALL SYSCOM(MAT SYSCOM.REC)
*
*RKB*---- PRE-INITIALIZATION
*RKB*
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","DEPARTMENT" TO DEPARTMENT ELSE
    ERRMSG = "CANNOT OPEN DEPARTMENT FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RES.PP.DEPT.MAINT
  SCREEN FORMAT
  SCREEN COUNT;;2
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  LINE.CNT = 0
  REF.NO = ''
  CURR.REF.NO = ''
  CODE = ''
  DUP.ID = ''
  DEPT.NAMES = ''
  GOTO 110
*
*
*---- MAIN PROCESSING
*
100 *
  CURR.REF.NO = ''
  RELEASE
  SCREEN CLEAR
110 *
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  REC.ID = CONO:"EST.RES.PRE.PRESS"
  * T26126 v
  READVU CTRL.ATTR FROM CONTROL,REC.ID,1 LOCKED
    ERRMSG = 'PRE-PRESS DEFINITION record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 99999 
  END THEN
  * T26126 ^
    NUM.VM = DCOUNT(CTRL.ATTR,VM)
    FOR I = 1 TO NUM.VM
      CURR.DEPT = CONO:CTRL.ATTR<1,I>
      READV DEPT FROM DEPARTMENT,CURR.DEPT,2 ELSE DEPT = ''
      IF I = 1 THEN
        DEPT.NAMES = DEPT
      END ELSE
        DEPT.NAMES<1,-1> = DEPT
      END
    NEXT I
  END ELSE
    CTRL.ATTR = ''
    DEPT.NAMES = ''
  END
  ORIG.REC = CTRL.ATTR
  IF CTRL.ATTR = "" THEN
         * THIS IS A EMPTY RECORD
         * SET THE PROMPT UP FOR NEW RECORD ENTRY (A,E)
    S$DATA(6)<S$SCR> = ""
    SCREEN FIELD;;6
    SCREEN INPUT;;6;GOTO 100
    BEGIN CASE
      CASE S$VALUE = 'A'
              * GODO THE ADD THING
        OPT = 'A'
        REF.NO = 1
        CURR.REF.NO = ''
        GOTO 510
      CASE 1
*              GOTO 100
        GOTO 99999
    END CASE
  END ELSE
         * THIS IS A FULL RECORD DISPLAY AND USE PROMPT 5 (ALL OPTIONS)
120 *
    GOSUB 80000
    GOSUB 81000
    LINE.CNT = DCOUNT(CTRL.ATTR,VM)
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 50000
  END
*
*---- GET OPERATOR REPLY
*
500 *
  SCREEN FIELD;;5
  SCREEN INPUT;;5
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      MISMATCH = 0
      IF ORIG.REC # CTRL.ATTR THEN MISMATCH = 1
      IF MISMATCH THEN
        SCREEN FIELD;;7
        SCREEN INPUT;;7
        IF S$VALUE # "Y" THEN GOTO 500
      END
*         GOTO 100
      GOTO 99999
    CASE OPT = "A" AND LINE.CNT < 99
      MODE = "A"
      DONE = 0
      FOR REF.NO = LINE.CNT+1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 700
        END ELSE
          LINE.CNT = LINE.CNT + 1
        END
      NEXT REF.NO
      REF.NO = LINE.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND LINE.CNT > 0
      MODE = "C"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10000
        IF S$VALUE = "END" THEN
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "D" AND LINE.CNT > 0
      MODE = "D"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        LINE.CNT = LINE.CNT - 1
        IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "I" AND LINE.CNT > 0
      MODE = "I"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 800
        LINE.CNT = LINE.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          GOSUB 700
          LINE.CNT = LINE.CNT - 1
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPT = "S" OR OPT = "SF"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "SR"
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "ST"
      REF.NO = 1
      GOSUB 50000
    CASE OPT = "SB"
      REF.NO = LINE.CNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "F"
      WRITE CTRL.ATTR ON CONTROL,REC.ID
      GOTO 99999
  END CASE
  GOTO 500
*
*---- GET LINE NUMBER
*
600 *
  SCREEN FIELD;;8
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
  SCREEN INPUT;;8
  RETURN
*
*---- DELETE MULTI-LINE DATA
*
700 *
  CTRL.ATTR = DELETE(CTRL.ATTR,1,REF.NO,0)
  DEPT.NAMES = DELETE(DEPT.NAMES,1,REF.NO,0)
  GOSUB 81000
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
800 *
  CTRL.ATTR = INSERT(CTRL.ATTR,1,REF.NO,0,"")
  DEPT.NAMES = INSERT(DEPT.NAMES,1,REF.NO,0,"")
  GOSUB 81000
  RETURN
*
*---- GET MULTI-LINE DATA
*
10000 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;2
10100 *
  S$VAL = REF.NO
  SCREEN FIELD;;3
  SCREEN INPUT;;3;GOTO 19950
  TEMP1 = S$VALUE
  MATREAD DEPT.REC FROM DEPARTMENT,CONO:TEMP1  THEN
    IF DEPT.TYPE = 'A' OR DEPT.TYPE = 'M' OR DEPT.TYPE = 'R' THEN
      LOCATE TEMP1 IN CTRL.ATTR<1> SETTING POS THEN
        VALID.DEPT = 0
        ERRMSG = TEMP1:" already exist in list."
      END ELSE
        VALID.DEPT = 1
        C.DEPT = DEPT.DESC
      END
    END ELSE
      VALID.DEPT = 0
      ERRMSG = "Department Code ":TEMP1:" is not valid.  Enter 'RETURN'"
    END
  END ELSE
    VALID.DEPT = 0
    ERRMSG = "Invalid Department Code ":TEMP1:" Enter 'RETURN'"
  END
  IF NOT(VALID.DEPT) THEN 
    GOSUB 90000
    GOSUB 700
    GOTO 10100
  END

19900 *
  CTRL.ATTR<1,REF.NO> = TEMP1
  DEPT.NAMES<1,REF.NO> = C.DEPT
  GOSUB 81000
  S$VAL = REF.NO
  SCREEN DISPLAY;;4
  RETURN
19950 *
  GOSUB 81000
  RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = DCOUNT(S$DATA(3)<S$SCR>,VM)
  SCREEN MULTI;;C;2;3;4
  RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
80050 *
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
  S$DATA(3)<S$SCR> = CTRL.ATTR
  S$DATA(4)<S$SCR> = DEPT.NAMES
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999 *
  SCREEN CLOSE
END
