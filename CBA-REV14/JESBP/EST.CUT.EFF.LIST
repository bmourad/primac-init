****************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.CUT.EFF.LIST
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This program will list the cutter efficiency tables.
*
*T25901 cmykleb 03/07/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
*T27431 cmykleb 05/14/2003 * The Basis Weight column is not correct.
****************************************************************
*
*---- COPY STATEMENTS
*
*COPY>CPYLIB>COM1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.CUT.EFF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOSUB 90000;STOP
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOSUB 90000;STOP
   OPEN '','ESTIMATE.CUT.EFF' TO ESTIMATE.CUT.EFF ELSE ERRMSG='ESTIMATE.CUT.EFF FILE MISSING';GOSUB 90000;STOP
*
*--- READ FROM PROC
*
   PROCREAD BUFFER ELSE
      ERRMSG = "CANNOT READ FROM PROC"
      GOSUB 90000
      STOP
   END
*
*---- GET COMPANY
*
   CONO=""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO='END' THEN GOTO 99999
*
*---- INITIALIZATION
*
   PAGE.NO=0
   LINE.CNT = 99
   SP1 = SPACE(1)
   SP2 = SPACE(2)
   SP3 = SPACE(3)
   SP4 = SPACE(4)
   NODATA=1
*T25901 v
*  K = SPACE(INT((50 - LEN(CO.NAME)) / 2))
*  CONAME = K:CO.NAME:K
*  CONAME = CONAME "L#50"
*  HEAD1 = OCONV(DATE(),"D2/"):SPACE(33):CONAME:SPACE(32):"PAGE "
*  HEAD2 = SPACE(35):"C U T T E R   E F F I C I E N C Y   T A B L E   L I S T I N G"
*T26493 v
*  REPORT.NAME = "CUTTER EFFICIENCY TABLE LISTING"
*  REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = BUFFER<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26493 ^
   HEAD1 = "" ; HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HEAD1,HEAD2)
*T25901 ^
   HEAD4 = "BASIS    EXTR      MAXIMUM      <-------------------------  NUMBER OF CUTS  ------------------------->"
   HEAD5 = "WEIGHT   FLAG   SQUARE INCHES     1     2     3     4     5     6     7     8     9    10    11    12 "
   HEAD6 = "------   ----   -------------   ----  ----  ----  ----  ----  ----  ----  ----  ----  ----  ----  ----"
   PRINTER ON
*
*---- MAIN PROCESSING
*
100*
   READNEXT ID ELSE
      GOTO 99999
   END
   MATREAD CEFF.REC FROM ESTIMATE.CUT.EFF,ID ELSE GOTO 100
   NODATA=0
*T27431 v
*  BASIS = ID[3,99]
   BASIS = ID[4,99]
*T27431 ^
   EXTR = CEFF.EXTR
   CNT = DCOUNT(CEFF.QTY,VM)
   IF LINE.CNT + CNT > 55 THEN GOSUB 10000
   PRINT
   LINE.CNT = LINE.CNT + 1
   FOR S = 1 TO CNT
      PLINE = ""
      PLINE = PLINE : SP2 : BASIS "R#3" : SP1
      PLINE = PLINE : SP3 : SP1 : EXTR "R#1" : SP2
      PLINE = PLINE : SP3 : OCONV(CEFF.QTY<1,S>,"MD0,") "R#9" : SP4
      PLINE = PLINE : SP1
      FOR C = 1 TO 12
         PLINE = PLINE : SP2 : OCONV(CEFF.FCTR<1,S,C>,"MD2") "R#4"
      NEXT C
      PRINT PLINE
      LINE.CNT = LINE.CNT + 1
      BASIS = ""
      EXTR = ""
   NEXT S
   GOTO 100
*
*---- PRINT THE HEADINGS
*
10000*
   PRINT CHAR(12):
   PAGE.NO=PAGE.NO + 1
   PRINT HEAD1:PAGE.NO
   PRINT HEAD2
   PRINT
   PRINT
   PRINT HEAD4
   PRINT HEAD5
   PRINT HEAD6
   LINE.CNT = 7
   RETURN
*
*---- EROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
   PRINTER OFF
   STOP
END
