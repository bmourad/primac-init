   SUBROUTINE EST.FORMULA.Q502 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q502
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/15/86
*
* DESCRIPTION
*
* This formula is used to calculate impression requirements for specified
* press and job specifications.
* This formula is for impressions per pass calculation.
*
*T27924 cmykleb 03/12/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.QTY = ""
   TYP = 0
   IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
   IF EST.PROD.PRESS.ID<1,COMP> = "" THEN
      TEMP.QTY = 0
      RETURN
   END
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*T27924 v
   CQTY = EST.COMP.QTY<1,COMP,QPTR>
   BEGIN CASE
      CASE CQTY=""
         COMPQTY = EQTY
      CASE (CQTY[1,1]="+" OR CQTY[1,1]="-") AND CQTY[LEN(CQTY),1]="%"
         COMPQTY = INT(EQTY * (1 + (CQTY[1,LEN(CQTY)-1]/100)) + 0.5)
      CASE CQTY[1,1]="+" OR CQTY[1,1]="-"
         COMPQTY = EQTY + CQTY
      CASE 1
         COMPQTY = CQTY
   END CASE
   IF COMPQTY < 0 THEN COMPQTY = 0
*T27924 ^
   NUMBER.UP = EST.PROD.NO.UP<1,COMP,1>+0
   IF NUMBER.UP = 0 THEN NUMBER.UP = 1
   FORMS = EST.PROD.FORMS<1,COMP,1>+0
   IF FORMS = 0 THEN FORMS = 1
   MF = EST.PROD.PASSES<1,COMP,MPTR>
*
*---- MAIN PROCESSING
*
   PROD.QTY = COMPQTY + FIELD(EST.PROD.BS.REQ<1,COMP,1>,"!",QPTR) ;*T27924
   RUN.SHT = INT(PROD.QTY / NUMBER.UP + 0.99)
   SPL.SHT = FIELD(EST.PROD.PS.REQ<1,COMP,1>,"!",QPTR)
   TOT.SHT = (RUN.SHT + SPL.SHT) * FORMS
   TEMP.QTY = TOT.SHT
   TEMP.VSALE = "Y"
   RETURN
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
