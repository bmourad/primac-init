*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - QUOTE.PRINT
* AUTHOR   - ANN POWLEY, COMPUTER BUSINESS ASSOCIATES
* DATE     - 08/17/90
* DESCRIPTION
* This program will print the client letter.
* T21177 REV11 UPG CLOSE SCREEN AFTER PRINT DT 2/4/98
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T28457 wyamout 03/03/2005 * ADD VIEW/EMAIL
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RPT
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
* T26090 v
*COPY>PMC.CPYLIB>PMC_REPORTS
* T26090 ^
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    PRINT "CANNOT OPEN COMPANY FILE"
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    PRINT "CANNOT OPEN CONTROL FILE"
    STOP
  END
  OPEN "","QUOTE.PRT" TO QUOTE.PRT ELSE
    PRINT "CANNOT OPEN QUOTE.PRT FILE"
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    PRINT "CANNOT OPEN JES.SCREENS FILE"
    STOP
  END
* T26090 v
   OPEN "","PMC_REPORTS" TO PMC_REPORTS ELSE
     PRINT "PMC_REPORTS FILE IS MISSING"
     STOP
   END
* T26090 ^
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1 (CONO,MAT COMP.REC,COMPANY,CONTROL)
  PROMPT ""
  SP2 = SPACE(2)
  PAGE.NO = 0
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;QUOTE.PRINT
  SCREEN FORMAT
  SCREEN COUNT;QUOTE.PRINT;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  CURR.REF.NO = ""
  REF.NO = 1
* T26090 v
   PORT_NO = 'TTY'
   CALL SYSVARS.SUB(PORT_NO)
   ACCT_NO = "ACCT"
   CALL SYSVARS.SUB(ACCT_NO)
   USER_ID = UPCASE(@LOGNAME)
   HF_NAME = PORT_NO :"_": USER_ID
   READ EMAIL_FORMAT FROM CONTROL, CONO:"EMAIL_FORMAT" ELSE
      EMAIL_FORMAT = ""
   END
   IF EMAIL_FORMAT<1> # "PDF" AND EMAIL_FORMAT<1> # "RTF" AND EMAIL_FORMAT<1> # "BOTH" THEN
      EMAIL_FORMAT<1> = ""
   END
   P.MSG = ""
   P.VALDAT = ""
   REPORT_ID = "JE432"
   MATREAD PRPT.REC FROM PMC_REPORTS, REPORT_ID ELSE
      MAT PRPT.REC = ""
      ERRMSG = "Report Definition (":REPORT_ID:") is missing"
      GOSUB 90000
   END
   P.MSG = ""
   P.VALDAT = ""
   IF PRPT.VIEW.FLAG = "Y" THEN
      P.MSG := ", (V)iew"
      P.VALDAT := ",V"
   END
   IF PRPT.EMAIL.FLAG = "Y" AND EMAIL_FORMAT<1> # "" THEN
      P.MSG := ", e(M)ail"
      P.VALDAT := ",M"
   END
* T26090 ^
  GOTO 110
*
*---- MAIN PROCESSING
*
100*
  RELEASE
  SCREEN CLEAR
110*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;QUOTE.PRINT;1
120*
  NEW.REC = 0
  MATREADU ESTR.REC FROM QUOTE.PRT, CONO ELSE
    MAT ESTR.REC = ""
    NEW.REC = 1
  END
  IF NEW.REC THEN
    LINE.CNT = 0
    OPTION = "A"
    GOTO 510
  END ELSE
    LINE.CNT = DCOUNT(ESTR.EST.ID,VM)
    GOSUB 80000
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 50000
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;QUOTE.PRINT;21
  SCREEN INPUT;QUOTE.PRINT;21
  OPTION = S$VALUE
510*
  BEGIN CASE
    CASE OPTION = "A"
      MODE = "A"
      DONE = 0
      FOR REF.NO = LINE.CNT+1 TO 999 UNTIL DONE
        GOSUB 50000
        GOSUB 1000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 700
        END ELSE
          LINE.CNT = REF.NO
        END
      NEXT REF.NO
      REF.NO = LINE.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPTION = "C" AND LINE.CNT > 0
      MODE = "C"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 1000
      END
    CASE OPTION = "D" AND LINE.CNT > 0
      MODE = "D"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        LINE.CNT = LINE.CNT - 1
        IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPTION = "I" AND LINE.CNT > 0
      MODE = "I"
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 800
        LINE.CNT = LINE.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 1000
        IF S$VALUE = "END" THEN
          GOSUB 700
          LINE.CNT = LINE.CNT - 1
          CURR.REF.NO = ""
          GOSUB 50000
        END
      END
    CASE OPTION = "S" AND LINE.CNT > 0
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPTION = 'SR' AND LINE.CNT > 0 
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPTION = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPTION = 'SB'
      REF.NO = LINE.CNT
      GOSUB 50000
    * T25978 ^
    CASE OPTION = "E" OR OPTION = "END"
      SCREEN CLOSE
      GOTO 99999
    CASE OPTION = "F"
      MATWRITE ESTR.REC ON QUOTE.PRT, CONO
      SCREEN CLOSE
      GOTO 99999
* T26090 v
*   CASE OPTION = "P"
    CASE OPTION = "V" AND PRPT.VIEW.FLAG # "Y"
       GOSUB 90000
    CASE OPTION = "M" AND PRPT.EMAIL.FLAG # "Y"
       ERRMSG = "** INVALID RESPONSE **"
       GOSUB 90000
    CASE OPTION = "P" OR OPTION = "V" OR OPTION = "M"
* T26090 ^
      S$DEFAULT = DATE()
      SCREEN FIELD;QUOTE.PRINT;23
      SCREEN INPUT;QUOTE.PRINT;23
      QUOTE.DATE = ""
      IF S$VALUE # "END" THEN QUOTE.DATE = S$VALUE
* T26090 v
      IF OPTION = "P" THEN
         READU HF_REC FROM CONTROL, HF_NAME THEN
            DELETE CONTROL, HF_NAME
         END ELSE
            RELEASE CONTROL, HF_NAME
         END
      END ELSE
         READU HF_REC FROM CONTROL, HF_NAME ELSE HF_REC = ""
         HF_REC<1> = OPTION
         WRITE HF_REC ON CONTROL, HF_NAME
         CMD = 'SETPTR ,,,,,3,BRIEF,BANNER ':HF_NAME:',NOMESSAGE,NHEAD'
         EXECUTE CMD CAPTURING JUNK
      END
* T26090 ^
      PRINTER ON
      FOR REF = 1 TO LINE.CNT
        EST.KEY = ESTR.EST.ID<1,REF>
        FND = 1
        READ QUOTE FROM QUOTE.PRT, CONO:EST.KEY ELSE FND = 0
        IF FND THEN
          GOSUB 5000
        END
        IF QUOTE<4> # "Y" THEN DELETE QUOTE.PRT, CONO:EST.KEY
      NEXT REF
      DELETE QUOTE.PRT, CONO
      PRINTER CLOSE
      SCREEN CLOSE
      GOTO 99999
  END CASE
  GOTO 500
*
*---- GET LINE REFERENCE NUMBER
*
600*
  SCREEN FIELD;QUOTE.PRINT;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
  SCREEN INPUT;QUOTE.PRINT;22
  RETURN
*
*---- DELETE MULTI-LINE DATA
*
700*
  ESTR.EST.ID = DELETE(ESTR.EST.ID,1,REF.NO,0)
  ESTR.CUST.NAME = DELETE(ESTR.CUST.NAME,1,REF.NO,0)
  GOSUB 80000
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
800*
  ESTR.EST.ID = INSERT(ESTR.EST.ID,1,REF.NO,0,"")
  ESTR.CUST.NAME = INSERT(ESTR.CUST.NAME,1,REF.NO,0,"")
  GOSUB 80000
  RETURN
*
*---- GET MULTI-LINE DATA
*
1000*
  S$VAL = REF.NO
  SCREEN DISPLAY;QUOTE.PRINT;11
1010*
  S$VAL = REF.NO
  SCREEN FIELD;QUOTE.PRINT;12
  SCREEN INPUT;QUOTE.PRINT;12;RETURN
  READ QUOTE FROM QUOTE.PRT,CONO:S$VALUE ELSE
    ERRMSG = "Estimate not on file. Try again! "
    GOSUB 90000
    GOTO 1010
  END
  EST.ID = S$VALUE
  S$VAL = REF.NO
  S$DATA(13)<S$SCR,REF.NO> = QUOTE<1>
  SCREEN DISPLAY;QUOTE.PRINT;13
  ESTR.EST.ID<1,REF.NO> = EST.ID
  ESTR.CUST.NAME<1,REF.NO> = QUOTE<1>
  RETURN
*
*---- PRINT ESTIMATE REPORT
*
5000*
  FOR I = 1 TO 8
    PRINT
  NEXT I
  PRINT SP2:"ESTIMATE: ":QUOTE<1> "L#35 ":" DATE:  ":OCONV(QUOTE.DATE,"D2/")
  IF QUOTE<2> = "Y" THEN
    PRINT SPACE(15):QUOTE<3> "L#23 ":SPACE(18):"(Budget Quote)"
  END ELSE
    PRINT SPACE(15):QUOTE<3> "L#23 "
  END
  PRINT
  LN.CNT = 12
  PAGE.CNT = 1
  QCNT = DCOUNT(QUOTE,AM)
  NIL = ""
  LOCATE NIL IN QUOTE,1 SETTING NEXT.NULL ELSE NULL
  FOR I = 5 TO (QCNT-2)
    BEGIN CASE
      CASE QUOTE<I> = ""
        LOCATE NIL IN QUOTE,I SETTING NEXT.NULL ELSE NULL
        IF ((LN.CNT + 1) + (I-NEXT.NULL)) > 50 THEN
          PAGE.CNT = PAGE.CNT + 1
          PRINT
          PRINT SP2:"Estimate #":EST.KEY "L#15":"SEE PAGE ":PAGE.CNT
          LN.CNT = LN.CNT + 2
          FOR Q = LN.CNT TO 66
            PRINT
          NEXT Q
          FOR Q = 1 TO 10
            PRINT
          NEXT Q
          LN.CNT = 11
        END
        PRINT
      CASE QUOTE<I,2> = ""
        PRINT SP2:QUOTE<I,1> "L#65"
      CASE 1
        PRINT SP2:QUOTE<I,1> "L#23":QUOTE<I,2>"L#56"
    END CASE
    LN.CNT = LN.CNT + 1
  NEXT I
  FOR X = LN.CNT TO 50
    PRINT
    LN.CNT = LN.CNT + 1
  NEXT X
  FOR X = I TO QCNT
    PRINT SP2:QUOTE<I,1> "L#65"
    LN.CNT = LN.CNT + 1
    I = I +1
  NEXT X
  FOR I = LN.CNT TO 60
    PRINT
  NEXT I
  FOR I = 1 TO 6
    PRINT
  NEXT I
  RETURN
*
*---- DISPLAY MULTI-LINE DATA
*
50000*
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = DCOUNT(S$DATA(12)<S$SCR>,VM)
  SCREEN MULTI;QUOTE.PRINT;C;11;12;13
  RETURN
*
*---- LOAD SCREEN DATA
*
80000*
  S$DATA(1)<S$SCR> = DATE()
  S$DATA(12)<S$SCR> = ESTR.EST.ID
  S$DATA(13)<S$SCR> = ESTR.CUST.NAME
80090*
  SCREEN DISPLAY;QUOTE.PRINT;ALL
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  PRINTER OFF
  PRINTER CLOSE
END
