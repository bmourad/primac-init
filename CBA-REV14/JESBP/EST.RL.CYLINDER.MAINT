*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RES.BP
* PROGRAM     - EST.RL.CYLINDER.MAINT
* BY          - KERRY WILSON
* DATE        - 01/18/90
* DESCRIPTION - This program maintains the Roll Label Cylinder.
*
* TASK 16749 DMT 11/01/92 ADD LOCATION, TYPE, CHANGE DEPT TO MULTI COST CENTERS
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 03/12/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*COPY>JES.CPYLIB>ESTIMATE.RL.CYLINDER
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET SYSCOM
*
  SYS.TYPE=1
  CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN 'ESTIMATE.RL.CYLINDER' TO ESTIMATE.RL.CYLINDER ELSE
    ERRMSG = 'CANNOT OPEN ESTIMATE.RL.CYLINDER FILE'
    GOTO 93000
  END
  OPEN 'COST.CNTR' TO COST.CNTR ELSE
    ERRMSG = 'CANNOT OPEN COST.CNTR FILE'
    GOTO 93000
  END
  OPEN 'COMPANY' TO COMPANY ELSE
    ERRMSG = 'CANNOT OPEN COMPANY FILE'
    GOTO 93000
  END
  OPEN 'CONTROL' TO CONTROL ELSE
    ERRMSG = 'CANNOT OPEN CONTROL FILE'
    GOTO 93000
  END
  OPEN 'JES.SCREENS' TO JES.SCREENS ELSE
    ERRMSG = 'CANNOT OPEN JES.SCREENS FILE'
    GOTO 93000
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RL.CYLINDER.MAINT;JES.SCREENS
  SCREEN FORMAT
  SCREEN COUNT;;12
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
*
*---- MAIN PROCESSING
*
100 *
  DESC = ''
  CURR.REF.NO = ''
  REF.NO = 1
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  SCREEN FIELD;;2
  SCREEN INPUT;;2
  IF S$VALUE = 'END' THEN GOTO 99999
  RLKEY = CONO:S$VALUE
  NEW.REC = 0
  * T26126 v
  MATREADU RLCYN.REC FROM ESTIMATE.RL.CYLINDER,RLKEY LOCKED
    ERRMSG = 'CYLINDER record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 100 
  END THEN
  * T26126 ^
    CNT = COUNT(RLCYN.CCTR,VM) + (RLCYN.CCTR # "")
  END ELSE
    MAT RLCYN.REC = ''
    NEW.REC = 1
    CNT = 0
    OPT2 = 'A'
  END
  IF NEW.REC THEN
    FOR FLD = 1 TO 6
      ON FLD GOSUB 41000,10200,10300,10400,10500,10600
      IF S$VALUE = 'END' THEN 
        SCREEN CLEAR
        RELEASE
        GOTO 100
      END
    NEXT FLD
  END ELSE
*         CNT = DCOUNT(RLCYN.CCTR,VM)
    GOSUB 80000
*         SCREEN DISPLAY;;ALL
  END
*
*---- ENTER PROMPT
*
500 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21
  OPTION = S$VALUE
  BEGIN CASE
    CASE NUM(OPTION)
      ON OPTION GOSUB 10100,10200,10300,10400,10500,10600
    CASE OPTION = 'D'
510 *
* P_X  = 0 ; P_Y = 23 ; P_VALUE = 'Are you sure you want to Delete (Y/N)? ' ; P_OPT = ""
* CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*             INPUT ANS:
* P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
* CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      SCREEN FIELD;;11
      SCREEN INPUT;;11
      BEGIN CASE
        CASE S$VALUE = 'Y'
          DELETE ESTIMATE.RL.CYLINDER,RLKEY
          SCREEN CLEAR
          RELEASE
          GOTO 100
        CASE S$VALUE = 'N'
          GOTO 500
        CASE 1
          GOTO 510
      END CASE
    CASE OPTION = 'E' OR OPTION = 'END'
      IF CNT = 0 THEN
        ERRMSG = 'Must have at least one cost center.'
        GOSUB 91000
        GOTO 500
      END
      SCREEN CLEAR
      RELEASE
      GOTO 100
    CASE OPTION = 'F'
      IF CNT = 0 THEN
        ERRMSG = 'Must have at least one cost center.'
        GOSUB 91000
        GOTO 500
      END
      MATWRITE RLCYN.REC ON ESTIMATE.RL.CYLINDER,RLKEY
      SCREEN CLEAR
      RELEASE
      GOTO 100
  END CASE
  GOTO 500
*
*---- CYLINDER COST.CNTR
*
10100 *
  SCREEN FIELD;;23
  SCREEN INPUT;;23
  OPT2 = S$VALUE
  BEGIN CASE
    CASE OPT2 = "" OR OPT2 = "E" OR OPT2 = "END"
      GOTO 10110
    CASE OPT2 = "A" AND CNT < 99
      GOSUB 41000
    CASE OPT2 = "C" AND CNT > 0
      GOSUB 31000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10120
      END
    CASE OPT2 = "I" AND CNT > 0
      GOSUB 31000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        S$DATA(3) = INSERT(S$DATA(3),S$SCR,REF.NO,0,"")
        RLCYN.CCTR = INSERT(RLCYN.CCTR,1,REF.NO,0,"")
        DESC = INSERT(DESC,1,REF.NO,0,"")
        CNT = CNT + 1
        CURR.REF.NO = ""
        GOSUB 51000
        GOSUB 10120
        IF S$VALUE = "END" THEN
          S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
          RLCYN.CCTR = DELETE(RLCYN.CCTR,1,REF.NO,0)
          DESC = DELETE(DESC,1,REF.NO,0)
          CNT = CNT - 1
        END
        CURR.REF.NO = ""
        GOSUB 51000
      END
    CASE OPT2 = "D" AND CNT > 0
      GOSUB 31000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
        RLCYN.CCTR = DELETE(RLCYN.CCTR,1,REF.NO,0)
        DESC = DELETE(DESC,1,REF.NO,0)
        CNT = CNT - 1
        IF REF.NO > CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 51000
      END
    CASE OPT2 = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > CNT THEN REF.NO = 1
      GOSUB 51000
    * T25978 v
    CASE OPT2 = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 51000
    CASE OPT2 = 'ST'
      REF.NO = 1
      GOSUB 51000
    CASE OPT2 = 'SB'
      REF.NO = CNT
      GOSUB 51000
    * T25978 ^
  END CASE
  GOTO 10100
10110 *
  RETURN
*
*
*---- GET COST.CNTRS
*
10120 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;12
  S$VAL = REF.NO
  SCREEN FIELD;;3
  SCREEN INPUT;;3
  BEGIN CASE
    CASE S$VALUE = "END"
      DONE = 1
      GOTO 10129
    CASE S$VALUE = ""
      DONE = 1
      GOTO 10120
    CASE S$VALUE # ""
      RLCYN.CCTR<1,REF.NO> = S$VALUE
  END CASE
  MATREAD CCTR.REC FROM COST.CNTR,CONO:RLCYN.CCTR<1,REF.NO> ELSE
    ERRMSG = 'INVALID COST CENTER'
    GOSUB 91000
    GOTO 10120
  END
  DESC<1,REF.NO> = CCTR.DESC "L#28"
  S$DATA(7)<S$SCR> = DESC
  S$VAL = REF.NO
  SCREEN DISPLAY;;7
10129 *
  RETURN
*
*
*---- TEETH
*
10200 *
  SCREEN FIELD;;4
  SCREEN INPUT;;4;RETURN
  RLCYN.TEETH = S$VALUE
  RETURN
*
*---- REPEAT
*
10300 *
  SCREEN FIELD;;5
  SCREEN INPUT;;5;RETURN
  RLCYN.REPEAT = S$VALUE
  RETURN
*
*---- QTY ON HAND
*
10400 *
  SCREEN FIELD;;6
  SCREEN INPUT;;6;RETURN
  RLCYN.QUANTITY = S$VALUE
  RETURN
*
*---- LOCATION
*
10500 *
  SCREEN FIELD;;8
  SCREEN INPUT;;8;RETURN
  RLCYN.LOC = S$VALUE
  RETURN
*
*---- TYPE
*
10600 *
  SCREEN FIELD;;9
  SCREEN INPUT;;9;RETURN
  RLCYN.TYPE = S$VALUE
  IF RLCYN.TYPE = 'PF' THEN
    GOSUB 10700
  END ELSE
    RLCYN.PERF.INCH = ''
    S$DATA(10)<S$SCR> = ''
    SCREEN DISPLAY;;10
  END
  RETURN
*
*---- PERF INCHES
*
10700 *
  SCREEN FIELD;;10
  SCREEN INPUT;;10;RETURN
  RLCYN.PERF.INCH = S$VALUE
  RETURN
*
*---- GET LINE REFERENCE NUMBER FOR COST CENTERS
*
31000 *
  S$DATA(22)<S$SCR> = ""
  SCREEN FIELD;;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > CNT THEN S$MAXV = CNT
  SCREEN INPUT;;22
  RETURN
*
*---- ADD COST CENTERS
*
41000 *
  DONE = 0
  FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
    GOSUB 51000
    GOSUB 10120
    IF S$VALUE = "END" THEN
      DONE = 1
      S$DATA(3) = DELETE(S$DATA(3),S$SCR,REF.NO,0)
      RLCYN.CCTR = DELETE(RLCYN.CCTR,1,REF.NO,0)
      DESC = DELETE(DESC,1,REF.NO,0)
      S$VALUE = ''
    END ELSE
      CNT = CNT + 1
    END
  NEXT REF.NO
  REF.NO = CNT
  CURR.REF.NO = ""
  GOSUB 51000
  RETURN
*
*---- SCROLL DESCRIPTION FOR COST CENTERS
*
51000 *
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$DATA(7)<S$SCR> = DESC
  S$VAL = START.REF.NO
  S$CNT = CNT
  SCREEN MULTI;EST.RL.CYLINDER.MAINT;C;12;3;7
  RETURN
*
*---- LOAD SCREEN
*
80000 *
  S$DATA(3)<S$SCR> = RLCYN.CCTR
  S$DATA(4)<S$SCR> = RLCYN.TEETH
  S$DATA(5)<S$SCR> = RLCYN.REPEAT
  S$DATA(6)<S$SCR> = RLCYN.QUANTITY
  S$DATA(8)<S$SCR> = RLCYN.LOC
  S$DATA(9)<S$SCR> = RLCYN.TYPE
  S$DATA(10)<S$SCR> = RLCYN.PERF.INCH
  IF RLCYN.CCTR # "" THEN
    DESC = ''
    PCNT = CNT
    PTR = 0
    LOOP
      PTR += 1
      MATREAD CCTR.REC FROM COST.CNTR,CONO:RLCYN.CCTR<1,PTR> ELSE
        MAT CCTR.REC = ''
      END
      DESC<1,PTR> = CCTR.DESC "L#28"
      PCNT -= 1
    UNTIL PCNT = 0
    REPEAT
    S$DATA(7)<S$SCR> = DESC
    REF.NO = 1
    GOSUB 51000
  END
  SCREEN DISPLAY;;ALL
  RETURN
*
*---- SYSCOM MESSAGES
*
91000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE=2;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
END
