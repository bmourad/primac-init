*COPY>CPYLIB>COM1
****************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.MATL.LIST
*
* AUTHOR   - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/17/86
*
* DESCRIPTION
*
* This program produces an Estimate Material report.
*
*T25901 cmykleb 03/07/2002 * Change heading to use GET.PROG.HEAD pgm.
*T26493 cmykleb 03/22/2002 * Change report to use rpt # from proc.
****************************************************************
*
*---- FILE EQUATES
*
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE.MATL
*
*---- SETUP FOR SYSTEM ERROR MESSAGES
*
   SYS.TYPE=1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
   OPEN '','COMPANY' TO COMPANY ELSE ERRMSG='COMPANY FILE MISSING';GOTO 93000
   OPEN '','CONTROL' TO CONTROL ELSE ERRMSG='CONTROL FILE MISSING';GOTO 93000
   OPEN '','ESTIMATE.MATL' TO ESTIMATE.MATL ELSE ERRMSG='ESTIMATE.MATL FILE MISSING';GOTO 93000
*
*---- GET PROC BUFFER
*
   PROCREAD BUFFER ELSE
      ERRMSG = "CANNOT READ FROM PROC"
      GOTO 93000
   END
*
*---- GET COMPANY NUMBER
*
   CONO=""
   CALL GET.CONO(CONO,MAT COMP.REC)
   IF CONO='END' THEN GOTO 99999
*
*---- INITIALIZATION
*
   PG.NO=0
   LINE.CNT=0
   ERRMSG=""
   PREV.GRP = ""
   RPT.NO="XXXX"
   UNKNOWN=STR("?",30)
   SP = " "
   SP2 = "  "
   NODATA=1
*T25901 v
*  K = SPACE(INT((50 - LEN(CO.NAME)) / 2))
*  CONAME = K:CO.NAME:K
*  CONAME = CONAME "L#50"
*  HEAD1 = OCONV(DATE(),"D2/"):SPACE(33):CONAME:SPACE(32):"PAGE  "
*  HEAD2 = SPACE(41):"E S T I M A T E   M A T E R I A L   L I S T I N G"
*T26493 v
*  REPORT.NAME = "ESTIMATE MATERIAL LISTING"
*  REPORT.NUMBER = "XXXX"
   REPORT.NUMBER = BUFFER<2>
   REPORT.NAME = ""
   CONO.NAME = ""
*T26493 ^
   HEAD1 = "" ; HEAD2 = ""
   CALL GET.PROG.HEAD(CONO,CONO.NAME,REPORT.NAME,REPORT.NUMBER,"",HEAD1,HEAD2)
*T25901 ^
   HEAD3 = "GROUP : "
   HEAD4 = "                                                                           COST                  <- QTY ->"
   HEAD5 = "PROD #         FULL DESCRIPTION          WIDTH    LENGTH     COST    U/M   UNIT   FOH%    MKUP%  T PARAM O"
   HEAD6 = "------  ------------------------------  -------  -------  ---------  ---  -----  ------  ------  - ----- -"
   PRINTER ON
*
*---- MAIN PROCESSING
*
10*
   READNEXT ESTM.KEY ELSE 
      IF NODATA THEN GOTO 99999
      GOTO 15
   END
   MATREAD ESTM.REC FROM ESTIMATE.MATL,ESTM.KEY ELSE GOTO 10
   NODATA=0
   GRP.NO = ESTM.KEY[4,99]
   IF PREV.GRP # GRP.NO THEN
      BEGIN CASE
         CASE GRP.NO = "F"
            GRP.DESC = "FILM"
         CASE GRP.NO = "P"
            GRP.DESC = "PLATE"
         CASE GRP.NO = "X"
            GRP.DESC = "STRIPPING"
         CASE GRP.NO = "B"
            GRP.DESC = "BLANKET"
         CASE GRP.NO = "C"
            GRP.DESC = "CASEMAKING"
         CASE GRP.NO = "L"
            GRP.DESC = "LONG RUN PLATE"
         CASE GRP.NO = "O"
            GRP.DESC = "OTHER"
      END CASE
      GOSUB 20
      PREV.GRP=GRP.NO
   END
   PROD.CNT = COUNT(ESTM.PROD,VM) + (ESTM.PROD # "")
   FOR ZZ = 1 TO PROD.CNT
      PROD.NO = ESTM.PROD<1,ZZ> "L#6"
      DESC = ESTM.DESC<1,ZZ> "L#30"
      WIDTH = OCONV(ESTM.WIDTH<1,ZZ>,"MD4Z")"R#7"
      XXLENGTH = OCONV(ESTM.LENGTH<1,ZZ>,"MD4Z")"R#7"
      COST = OCONV(ESTM.COST<1,ZZ>,"MD2") "R#9"
      UM = ESTM.UM<1,ZZ>"L#3"
      CUN = OCONV(ESTM.COST.UNIT<1,ZZ>,"MD0,")"R#5"
      IF NUM(ESTM.FOH.PCT<1,ZZ>) THEN
         FOH = OCONV(ESTM.FOH.PCT<1,ZZ>,"MD2") "R#6"
      END ELSE
         FOH = ESTM.FOH.PCT<1,ZZ> "R#6"
      END
      IF NUM(ESTM.MARKUP<1,ZZ>) THEN
         MKUP = OCONV(ESTM.MARKUP<1,ZZ>,"MD2") "R#6"
      END ELSE
         MKUP = ESTM.MARKUP<1,ZZ> "R#6"
      END
      C.CNT = COUNT(ESTM.QCALC<1,ZZ>,SVM) + 1
      QCALC = ESTM.QCALC<1,ZZ,1> "L#1"
      QPARAM = ESTM.QPARAM<1,ZZ,1> "L#5"
      QOR = ESTM.QOR<1,ZZ,1> "L#1"
      GOSUB 30
   NEXT ZZ
   GOTO 10
15*
   PRINTER OFF
   GOTO 99999
*
*---- PRINT THE HEADINGS
*
20*
   PRINT CHAR(12)
   PRINT
   PG.NO=PG.NO + 1
   PRINT HEAD1:PG.NO
   PRINT HEAD2
   PRINT HEAD3:GRP.NO:"  ":GRP.DESC
   PRINT
   PRINT HEAD4
   PRINT HEAD5
   PRINT HEAD6
   LINE.CNT = 7
   RETURN
*
*---- PRINT THE VALUES
*
30*
   PRINT
   PRINT PROD.NO:SP2:DESC:SP2:WIDTH:SP2:XXLENGTH:SP2:COST:SP2:UM:SP2:CUN:SP2:FOH:SP2:MKUP:SP2:QCALC:SP:QPARAM:SP:QOR
   LINE.CNT = LINE.CNT + 2
   IF LINE.CNT >= 55 THEN GOSUB 20
   IF C.CNT > 1 THEN
      FOR QQ = 2 TO C.CNT
         QCALC = ESTM.QCALC<1,ZZ,QQ> "L#1"
         QPARAM = ESTM.QPARAM<1,ZZ,QQ> "L#5"
         QOR = ESTM.QOR<1,ZZ,QQ> "L#1"
         PRINT SPACE(112):QCALC:SP:QPARAM:SP:QOR
         LINE.CNT = LINE.CNT + 1
         IF LINE.CNT >= 55 THEN GOSUB 20
      NEXT QQ
   END
   RETURN
*
*---- CALLS FOR SYSCOM
*
93000*
   PRINTER OFF
   ERR.TYPE=3
   CALL SI_SYSCOM(MAT SYSCOM.REC)
99999*
END
