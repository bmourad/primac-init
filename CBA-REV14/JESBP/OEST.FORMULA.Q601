      SUBROUTINE EST.FORMULA.Q601 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q601
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/04/86
*
* DESCRIPTION
*
* This formula is used to calculate quantity of ink required.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      TEMP.QTY = ""
      TYP = 0
      IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
      IP1 = TEMP.FVAR1
      IP2 = TEMP.FVAR2
*
      IF EST.PROD.OS.TYPE<1,COMP> = "S" THEN
         MAT EQUIPMENT.REC = ""
      END ELSE
         MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:EST.PROD.PRESS.ID<1,COMP> ELSE
            ERRMSG = "Invalid press ID - ":EST.PROD.PRESS.ID<1,COMP>
            GOSUB 90000
            RETURN
         END
      END
*
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE RETURN
*
      COATING = EST.PROD.COATING<1,COMP,MPTR>
      INK.SIDES = FIELD(EST.PROD.INK.SIDES<1,COMP,MPTR>,"!",IP1)
      INK.COVER = FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",IP1)
*
      NUMBER.UP = EST.PROD.NO.UP<1,COMP,1>+0
      IF NUMBER.UP = 0 THEN NUMBER.UP = 1
      FORMS = EST.PROD.FORMS<1,COMP,1>+0
      IF FORMS = 0 THEN FORMS = 1
      MF = EST.PROD.PASSES<1,COMP,MPTR>
*
      PSW = EST.PROD.PS.WIDTH<1,COMP,MPTR>
      IF EST.PROD.OS.TYPE<1,COMP> = "S" THEN
         PSL = EST.PROD.PS.LENGTH<1,COMP,MPTR>
      END ELSE
         PSL = EQP.PRESS.CUTOFF.LEN
      END
*
*---- MAIN PROCESSING
*
      PROD.QTY = EQTY + FIELD(EST.PROD.BS.REQ<1,COMP,1>,"!",QPTR)
      RUN.SHT = INT(PROD.QTY / NUMBER.UP + 0.99)
      SPL.SHT = FIELD(EST.PROD.PS.REQ<1,COMP,1>,"!",QPTR)
      TOT.SHT = (RUN.SHT + SPL.SHT) * FORMS    ;* TOTAL SHEETS TO PRINT
      PAPER.SQ.IN = INT(PSW / 10000 * PSL / 10000 + 0.5)    ;* SQUARE INCHES PER SIDE
      INK.SQ.IN = INT(TOT.SHT * (PAPER.SQ.IN * (INK.COVER / 100)) + 0.5)    ;* SQUARE INCHES OF INK PER SIDE
      BEGIN CASE
      CASE COATING = 1 AND INK.SIDES = 2
         TEMP.QTY = (INK.SQ.IN / ESTM.COVER.C<1,IP2>) + (INK.SQ.IN / ESTM.COVER.U<1,IP2>)
      CASE COATING+0 = 0
         TEMP.QTY = INK.SQ.IN * INK.SIDES / ESTM.COVER.U<1,IP2>
      CASE 1
         TEMP.QTY = INK.SQ.IN * INK.SIDES / ESTM.COVER.C<1,IP2>
      END CASE
      TEMP.QTY = INT(TEMP.QTY * 100 + 0.99) / 100
      MIN.QTY = ESTM.MIN.QTY<1,IP2> / 100
      IF TEMP.QTY < MIN.QTY THEN TEMP.QTY = MIN.QTY
      TEMP.VSALE = "Y"
      RETURN
*
*---- ERROR ROUTINE
*
90000*
      PRINT @(0,23):CL:ERRMSG:
      INPUT REPLY,1:
      PRINT @(0,23):CL:
      RETURN
   END
