  SUBROUTINE EST.GROUP.XREF (CONO, DEPT, ACTION, SEL.GRP)
***************************************************************************
*
* PROGRAM  - EST.GROUP.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/28/86
*
* DESCRIPTION
*
* This subroutine will display valid groups for the specified department.
*
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.GRP.XREF
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
  SEL.GRP = ""
  MATREAD ESTGX.REC FROM ESTIMATE.GRP.XREF, CONO:DEPT ELSE
    SEL.GRP = "X"
    GOTO 99999
  END
  SCREEN DEFINE;EST.GROUP.XREF
  SCREEN FORMAT;EST.GROUP.XREF
  SCREEN COUNT;EST.GROUP.XREF;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  LINE.CNT = COUNT(ESTGX.GRP,VM) + (ESTGX.GRP # "")
*
*---- MAIN PROCESSING
*
  S$DATA(1)<S$SCR> = DATE()
  S$DATA(12)<S$SCR> = ESTGX.GRP
  S$DATA(13)<S$SCR> = ESTGX.GRP.DESC
  REF.NO = 1
  CURR.REF.NO = ""
  GOSUB 50000
*
*---- GET OPERATOR REPLY
*
500*
  IF ACTION = "I" THEN
    SCREEN FIELD;EST.GROUP.XREF;22
    S$VALDAT = "S"
    SCREEN INPUT;EST.GROUP.XREF;22
  END ELSE
    SCREEN FIELD;EST.GROUP.XREF;21
    S$VALDAT = "S"
    MAX = CURR.REF.NO + LINE.COUNT - 1
    IF MAX > LINE.CNT THEN MAX = LINE.CNT
    FOR V = CURR.REF.NO TO MAX
      S$VALDAT = S$VALDAT:",":V
    NEXT V
    SCREEN INPUT;EST.GROUP.XREF;21
  END
  S$VALDAT := ',SR,SB,ST'     ;* T25978
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION = "" OR OPTION = "END"
      GOTO 99990
    CASE OPTION # "" AND NUM(OPTION)
      SEL.GRP = ESTGX.GRP<1,OPTION>
      GOTO 99990
    CASE OPTION = "S"
      REF.NO = REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPTION = 'SR'
      REF.NO -= LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPTION = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPTION = 'SB'
      REF.NO = LINE.CNT
      GOSUB 50000
    * T25978 ^
  END CASE
  GOTO 500
*
*---- DISPLAY MULTI-VALUE DATA
*
50000*
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = LINE.CNT
  SCREEN MULTI;EST.GROUP.XREF;C;11;12;13
  RETURN
99990*
  SCREEN CLOSE
99999*
  RETURN
END
