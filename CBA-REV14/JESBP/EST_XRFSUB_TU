   SUBROUTINE EST.XREF.SUB(CONO, ACTION, SELEST, SELECTION)
*********************************************************************
*
* REVISION - [08.1]
*
* PROGRAM  - EST.XREF.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/04/91
*
* DESCRIPTION
*
* This program is used to display estimates selected by EST.XREF.
*
*T21177 diane 02/27/1997 * REV11 UPG Prompt for Review
*T21913 diane 06/09/1997 * Close screens for gui
*T25024 diane 05/02/2000 * Update with version
*T26690 cmykleb 06/20/2002 * Screen column headings are incorrect.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>TCC
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>ROC.H
*COPY>CPYLIB>SPECIAL.H
*
*
*---- INITIALIZATION
*
*T21913 v
   SAVE.S$GUIFORM = S$GUIFORM
   SAVE.S$SCR = S$SCR
   SAVE.S$SCRN.PC = S$SCRN.PC
*T21913 ^
   LINE.CNT = COUNT(SELEST,AM) + 1
*
   TYP = 0 ; CALL EDIT.SUB1 (MAT EDIT.COM)
*
   BOX.ROWS = 19
   IF LINE.CNT + 3 < BOX.ROWS THEN
      BOX.ROWS = LINE.CNT + 3
   END
   BOX.COLS = 74
   PAGE.SIZE = BOX.ROWS - 3
*
   TITLE = "E S T I M A T E   C R O S S - R E F E R E N C E"
   WIDTH = ""
   BOX.X = INT((80 - BOX.COLS) / 2)
   BOX.Y = INT((24 - BOX.ROWS) / 2 - 0.5)
   DX = BOX.X + 1; DY = BOX.Y; MAX.COLS = BOX.COLS - 2
   HDRLN = "Ln#"
   HDLEN = 3
   HDRLN<1,2> = "Date"
   HDLEN<1,2> = 8
   HDRLN<1,3> = "Estimate"
   HDLEN<1,3> = 11
   HDRLN<1,4> = "Typ"
   HDLEN<1,4> = 3
   HDRLN<1,5> = "Customer/Prospect/Desc"
   HDLEN<1,5> = 30
   HDRLN<1,6> = "Cat"
   HDLEN<1,6> = 5
   HDRLN<1,7> = "Status"
*T26690 v
*  HDLEN<1,7> = 6
   HDLEN<1,7> = 9
*T26690 ^
   FOR DLP = 1 TO 7
      WIDTH<1,DLP> = INT(HDLEN<1,DLP> / 2 + .5)
   NEXT DLP
   TITLE = TITLE:VM:HDRLN ; * T26690
   IF LINE.CNT > PAGE.SIZE THEN
      DEPTH = PAGE.SIZE
   END ELSE
      DEPTH = PAGE.SIZE
   END
   XYPOS = BOX.X + 0; XYPOS<1,2> = BOX.Y + 0
   ILIST = ""
   FOR PL = 1 TO LINE.CNT
      EST.ID = SELEST<PL>[4,99]
      IF EST.ID[LEN(EST.ID)-2,3] = "-00" THEN
         EST.ID = EST.ID[1,LEN(EST.ID)-3]
         STD.FLG = 1
      END ELSE
         STD.FLG = 0
      END
      MATREAD EST.REC FROM ESTIMATE, CONO:EST.ID ELSE
         MAT EST.REC = ""
         EST.STATUS = STR("?",6)
      END
      ILIST<1,PL> = PL
      ILIST<1,PL,2> = OCONV(EST.DATE.ENTERED,"D2/")
      ILIST<1,PL,3> = EST.ID
      BEGIN CASE
         CASE EST.SUBS # ""
            ILIST<1,PL,4> = "MST"
         CASE EST.MASTER # "" AND EST.MASTER # EST.ID
            ILIST<1,PL,4> = "SUB"
         CASE 1
            ILIST<1,PL,4> = ""
      END CASE
      IF STD.FLG THEN
         ILIST<1,PL,5> = EST.COMMENTS<1,1>[1,30]
      END ELSE
*
         IF EST.CUST = "P" THEN
            ILIST<1,PL,5> = EST.CUST.NAME
         END ELSE
            MATREAD CUST.REC FROM CUSTOMER, CONO:EST.CUST ELSE
               MAT CUST.REC = ""
            END
            ILIST<1,PL,5> = CUST.NAME
         END
      END
      ILIST<1,PL,6> = EST.CATG
      ILIST<1,PL,7> = EST.STATUS<1,1>
   NEXT PL
   BKGRND = ""; FRGRND = ""
   OPT = "M"; VALUE = ""; ERROR = ""
   DONE = ""
   LOOP
      TU_FUNC = "TU.FORM.SELECTLIST":TU_VERNO     ;*T25024
      CALL @TU_FUNC(TITLE,ILIST,WIDTH,DEPTH,XYPOS,BKGRND,FRGRND,OPT,VALUE,ERROR)     ;*T25024
      OPT = VALUE<1,1>
      IF NUM(OPT) THEN
         IF OPT > 0 AND OPT <= LINE.CNT THEN
            EST.ID = SELEST<OPT>[4,99]
            IF EST.ID[LEN(EST.ID)-2,3] = "-00" THEN
               EST.ID = EST.ID[1,LEN(EST.ID)-3]
            END
            SELECTION = EST.ID
            GOSUB 200
            IF VALUE = "S" THEN DONE = 1
            VALUE = ""
         END ELSE
            SELECTION = ""
            DONE = 1
         END
      END ELSE
         SELECTION = ""
         DONE = 1
      END
   UNTIL DONE
   REPEAT
   GOTO 99999
*
*---- Prompt for Review
200 *
   PMSG = 'Do you want to "R"eview or "S"elect this estimate?'
   TYP = 11 ; X = 3 ; Y = 21
   MAXL = 1 ; VALDAT = "R":VM:"S" ; O.R = "R"
   DEFAULT = "S"
   CALL EDIT.SUB1 (MAT EDIT.COM)
   IF VALUE = "R" THEN
      S$SCRN.PC<S$SCR> = S$GUIFORM     ;* T21913
      S$SCR = S$SCR + 1
      CALL EST.XREF.DET(CONO, EST.ID)
*T21913 v
      SCREEN CLOSE
*T21913^
      S$SCR = S$SCR - 1
      S$GUIFORM = S$SCRN.PC<S$SCR>     ;* T21913
   END
   RETURN
*
99999*
*T21913 v
   S$GUIFORM = SAVE.S$GUIFORM
   S$SCR = SAVE.S$SCR
   S$SCRN.PC = SAVE.S$SCRN.PC
*T21913 ^
   RETURN
END
