   THE REASON THIS FORMULA WAS CREATED WAS TO FIT A SPECIFIC NEED AT
   A AUSTRALIAN USER...DOES NOT CONFORM TO ANY US NEEDS OR STANDARDS 
   AS BEST WE CAN TELL
   SUBROUTINE EST.FORMULA.F864 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.F864
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/26/91
*
* DESCRIPTION
*
* This program is used to derive the run efficiency factor for the
* rewinder based upon the specified number of stops and the stop time
* entered.
* MODIFIED - G Jarvis, Pacrim Software Pty Ltd
*            As the Rewinder Formula is usually used before any
*            calculations of of Roll & Cartons Quantities, we will
*            call the formula Q869 to establish these values as they
*            are mandatory within this formula.
*
*T18588 Formula moved over from EST.FORMULA.F962
*T21177 diane 11/06/1996 * REV11 UPG
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   SAVE.QTY=TEMP.QTY
   CALL EST.FORMULA.Q869(CONO,ACTION,EQTY,DEPT,COMP)
   TEMP.QTY=SAVE.QTY
   TEMP.FCTR = 1000
   TYP = 0
*
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE RETURN
10*
   IF QPTR = 1 AND ACTION # "R" THEN
      IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
      X=0;Y=23;TYP=14;MAXL=5;MINV=1;MAXV=9999;JUSTIFY="L"
      HMSG="Enter stop time in minutes "
      BEGIN CASE
         CASE TEMP.FVAR3 # ""
            DEFAULT = TEMP.FVAR3 / 100
         CASE 1
            DEFAULT = 5.00
      END CASE
      O.R = "O"
      PMSG = "Rewinder stop time in minutes (Default = ":DEFAULT:") ?"
      CALL EDIT.SUB1 (MAT EDIT.COM)
      IF VALUE = "END" THEN RETURN
      IF VALUE = "" THEN GOTO 10
      STOPMIN = VALUE
      TEMP.FVAR3 = VALUE
   END ELSE
      STOPMIN = TEMP.FVAR3
   END
*
*---- MAIN PROCESSING
*
   NO.PER.ROLL=0
   IF EST.RL.ROLLS<1,COMP,1>='Y' THEN
      IF EST.RL.NO.PER.ROLL<1,COMP,1>+0 > 0 THEN
         NO.PER.ROLL = EST.RL.NO.PER.ROLL<1,COMP,1>
      END
      IF NO.PER.ROLL+0=0 THEN
         NO.PER.ROLL = EST.RL.CALC.ROLL.QTY<1,COMP,1>
      END
   END
   ROLLS = EST.RL.NO.ROLLS<1,COMP,QPTR> ;*T21177
   IF ROLLS+0=0 THEN
      ROLLS = INT(EQTY/1000+.5) ;* Assume 1000 per roll
   END
   STOPS = ROLLS
   RUN.HRS = INT(TEMP.QTY / TEMP.STD * 100 + 0.5)
   STP.HRS = INT(STOPS * (STOPMIN/100) / 60 * 100 + 0.5)
   TEMP.FCTR = INT(RUN.HRS/(RUN.HRS+STP.HRS)*1000+0.5)
   IF TEMP.FCTR+0=0 THEN TEMP.FCTR=1000
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
