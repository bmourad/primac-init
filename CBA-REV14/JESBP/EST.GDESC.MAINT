  SUBROUTINE EST.GDESC.MAINT (CONO, ACTION, GRP.DIV, JOB.NO, STATUS)
**************************************************************************
*
* PROGRAM  - EST.GDESC.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/14/86
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* DESCRIPTION
*
* This program is used to maintain descriptive data based upon request
* defined in the description group.
*
*T23278 markt 01/05/1999 * Add check for divisional security.
*T25978 adelgado 03/07/2002 * Fix scrolling
*T26126 adelgado 03/12/2002 * Implement the LOCKED clause for READU.
**************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>ESTIMATE.DESC.GRP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","ESTIMATE.DESC.GRP" TO ESTIMATE.DESC.GRP ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.DESC.GRP FILE"
    GOSUB 90000
    STATUS = "E"
    GOTO 99999
  END
  OPEN "","ESTIMATE.GDESC" TO ESTIMATE.GDESC ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.GDESC FILE"
    GOSUB 90000
    STATUS = "E"
    GOTO 99999
  END
*
*---- INITIALIZATION
*
  STATUS = ""
  SCREEN DEFINE;EST.GDESC.MAINT
  SCREEN FORMAT
  SCREEN COUNT;;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
30*
  BEGIN CASE
    CASE GRP.DIV = ""
      SCREEN FIELD;;4
      SCREEN INPUT;;4
      IF S$VALUE = "END" THEN RETURN
*T23278 v
      DIV.CODE = S$VALUE; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
      CALL SI.CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
      IF ERRMSG # '' THEN
        GOSUB 90000; GOTO 30
      END
*T23278 ^
      GRP.DIV = S$VALUE
      SCREEN FIELD;;3
      SCREEN INPUT;;3
      IF S$VALUE = "END" THEN RETURN
      JOB.NO = S$VALUE
    CASE 1
      S$DATA(4)<S$SCR> = GRP.DIV
      SCREEN DISPLAY;;4
      S$DATA(3)<S$SCR> = JOB.NO
      SCREEN DISPLAY;;3
  END CASE
  GROUPS = ""
  SELECT ESTIMATE.DESC.GRP
40*
  READNEXT ID ELSE GOTO 50
  IF ID[1,3] # CONO THEN GOTO 40
  GRP.ID = ID[4,99]
  DIV = FIELD(GRP.ID,"-",1)
  IF DIV # GRP.DIV THEN GOTO 40
  LOCATE ID[4,99] IN GROUPS<1>,1 BY "AR" SETTING P ELSE NULL
  GROUPS = INSERT(GROUPS,1,P,0,GRP.ID)
  GRP.CNT = COUNT(GROUPS,VM) + (GROUPS # "")
  GOTO 40
50*
  IF GROUPS = "" THEN RETURN
  GPTR = 1
*
  * T26126 v
  READU ESTGD.REC FROM ESTIMATE.GDESC, CONO:JOB.NO LOCKED
    ERRMSG = 'JOB JACKET record is locked by user - ':GETUSERNAME(STATUS())
    GRP.DIV = '' ; JOB.NO = ''
    GOSUB 90000;GOTO 30 
  END ELSE
  * T26126 ^
    ESTGD.REC = ""
    SCREEN FIELD;;28
    SCREEN INPUT;;28
    IF S$VALUE # "Y" THEN
      RELEASE ESTIMATE.GDESC, CONO:JOB.NO
      GRP.DIV = ""
      JOB.NO = ""
      S$DATA(3)<S$SCR> = ""
      GOTO 30
    END
  END
  GOTO 110
*
*----- MAIN PROCESSING
*
100*
  SCREEN CLEAR
  S$DATA(4)<S$SCR> = GRP.DIV
  SCREEN DISPLAY;;4
  S$DATA(3)<S$SCR> = JOB.NO
  SCREEN DISPLAY;;3
110*
  GRP.ID = GROUPS<1,GPTR>
  GROUP = FIELD(GRP.ID,"-",2)
  MATREAD ESTDG.REC FROM ESTIMATE.DESC.GRP, CONO:GRP.ID ELSE
    MAT ESTDG.REC = ""
  END
  LINE.CNT = COUNT(ESTDG.REQUEST,VM) + (ESTDG.REQUEST # "")
  NEW.REC = 0
  IF ACTION = "M" AND ESTGD.REC<GROUP> = "" THEN
    NEW.REC = 1
  END
  GOSUB 80000
  SCREEN DISPLAY;;ALL
  IF NEW.REC THEN
    MODE = "A"
    CURR.REF.NO = ""
    FOR REF.NO = 1 TO LINE.CNT
      GOSUB 50000
      GOSUB 10000
      IF S$VALUE = "END" THEN GOTO 500
    NEXT REF.NO
  END ELSE
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 50000
  END
*
*---- GET OPERATOR REQUEST
*
500*
  BEGIN CASE
    CASE ACTION = "M" AND NEW.REC
      IF GPTR = GRP.CNT THEN
        SCREEN FIELD;;26
        SCREEN INPUT;;26
      END ELSE
        SCREEN FIELD;;22
        SCREEN INPUT;;22
      END
    CASE ACTION = "M"
      IF GPTR = GRP.CNT THEN
        SCREEN FIELD;;26
        SCREEN INPUT;;26
      END ELSE
        SCREEN FIELD;;21
        SCREEN INPUT;;21
      END
    CASE 1
      SCREEN FIELD;;23
      SCREEN INPUT;;23
  END CASE
  OPTION = S$VALUE
510*
  BEGIN CASE
    CASE OPTION = "C" AND LINE.CNT > 0
      GOSUB 600
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        MODE = "C"
        IF S$VALUE = "ALL" THEN
          FOR REF.NO = 1 TO LINE.CNT
            GOSUB 50000
            GOSUB 10000
            IF S$VALUE = "END" THEN
              GOSUB 80000
              CURR.REF.NO = ""
              GOSUB 50000
              GOTO 500
            END
          NEXT REF.NO
        END ELSE
          REF.NO = S$VALUE
          GOSUB 10000
          IF S$VALUE = "END" THEN
            GOSUB 80000
            CURR.REF.NO = ""
            GOSUB 50000
          END
        END
      END
    CASE OPTION = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPTION = "ST"
      REF.NO = 1
      GOSUB 50000
    CASE OPTION = "SB"
      * T25978 v
      * REF.NO = COUNT(DEPTS,VM) + 1
      REF.NO = LINE.CNT
      * T25978 ^
      GOSUB 50000
    CASE OPTION = "SR"
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPTION = "N"
      GPTR = GPTR + 1
      IF GROUPS<1,GPTR> = "" THEN
        RELEASE ESTIMATE.GDESC, CONO:JOB.NO
        GOTO 99999
      END
      GOTO 100
    CASE OPTION = "P" AND GPTR > 1
      GPTR = GPTR - 1
      GOTO 100
    CASE OPTION = "E" OR OPTION = "END"
      IF ACTION = "I" THEN
        RELEASE ESTIMATE.GDESC, CONO:JOB.NO
        GOTO 99999
      END ELSE
        SCREEN FIELD;;27
        SCREEN INPUT;;27
        IF S$VALUE = "Y" THEN
          RELEASE ESTIMATE.GDESC, CONO:JOB.NO
          GOTO 99999
        END
      END
    CASE OPTION = "F"
      WRITE ESTGD.REC ON ESTIMATE.GDESC, CONO:JOB.NO
      GOTO 99999
    CASE 1
      ERRMSG = "Invalid Selection"
      GOSUB 90000
  END CASE
  GOTO 500
*
*---- GET LINE REFERENCE NUMBER
*
600*
  SCREEN FIELD;;25
  SCREEN INPUT;;25
  MINVAL = CURR.REF.NO
  MAXVAL = MINVAL + LINE.COUNT - 1
  IF MAXVAL > LINE.CNT THEN MAXVAL = LINE.CNT
  BEGIN CASE
    CASE S$VALUE = ""
    CASE S$VALUE = "END"
    CASE S$VALUE = "ALL"
    CASE NOT(NUM(S$VALUE))
      ERRMSG = "** INVALID REPLY **"
      GOSUB 90000
      GOTO 600
    CASE S$VALUE < MINVAL OR S$VALUE > MAXVAL
      ERRMSG = "** OUT OF RANGE **"
      GOSUB 90000
      GOTO 600
  END CASE
  RETURN
*
*---- GET MULTI-LINE DATA
*
10000*
  IF ESTDG.TYPE<1,REF.NO> = "0" THEN
    S$VALUE = ""
  END ELSE
    S$VAL = REF.NO
    SCREEN FIELD;;13
    S$TYP = ESTDG.TYPE<1,REF.NO>
    S$MAXL = ESTDG.MAXL<1,REF.NO>
    S$O.R = ESTDG.O.R<1,REF.NO>
    S$JUSTIFY = ESTDG.JUSTIFY<1,REF.NO>
    BEGIN CASE
      CASE ESTDG.PATRN<1,REF.NO> = "*DATE*"
        S$PATRN = "2N-2N-2N":VM:"2N/2N/2N"
        S$DEFAULT = OCONV(DATE(),"D2-")
      CASE ESTDG.TYPE<1,REF.NO>="7"
        S$PATRN = ESTDG.PATRN<1,REF.NO>
      CASE 1
        S$DEFAULT = ESTDG.PATRN<1,REF.NO>
    END CASE
    SCREEN INPUT;;13;RETURN
  END
  ESTGD.REC<GROUP,REF.NO> = S$VALUE
  RETURN
*
*---- DISPLAY MULTI-VALUE DATA
*
50000*
  START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = LINE.CNT
  SCREEN MULTI;;C;11;12;13
  RETURN
*
*---- LOAD MULTI-LINE DATA
*
80000*
  S$DATA(1)<S$SCR> = DATE()
  S$DATA(2)<S$SCR> = SPACE(INT((40-LEN(ESTDG.DESC))/2)):ESTDG.DESC
  S$DATA(12)<S$SCR> = ESTDG.REQUEST
  S$DATA(13)<S$SCR> = ESTGD.REC<GROUP>
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  SCREEN CLEAR;;D
  RETURN
END
