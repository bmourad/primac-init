   SUBROUTINE EST.FORMULA.F963 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.F963
*
* AUTHOR   - DUANE GREEN, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 06/18/93
*
* DESCRIPTION
*
* This formula is used to calculate the factor for Run Time
* The formula prompts for run speed in feet
* This formula is used in Roll-Label Estimating
*
*MOD TASK 18190 DMT 8/9/94 determine run speed from efficiency table
*T19563 gat 02/29/1996 * MAJOR CHANGES TO ESTIMATING
*C28744 gat 05/22/1997 * FIX RUN EFF PROBLEM
*T25663 cm 07/05/2001 * Upgrade to rev11 and add changes.
*C39608 cmykleb 02/20/2002 * Program is not reading the run eff table
*                            file correctly.
*T26538 cmykleb 04/18/2002 * If formula factor was overridden do not
*                            recalculate this line.
* 11/8/04 - R01 - Remove bypass code
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.RUN.EFF
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
   OPEN '','EST.RES.FDIE.SPEEDS' TO EST.RES.FDIE.SPEEDS ELSE STOP
   OPEN '','ESTIMATE.RES.RUN.EFF' TO ESTIMATE.RES.RUN.EFF ELSE STOP
   DIE.GSET=''
   DIE.TEETH=''
*T26538 v
   IF TEMP.OVR.FLG # "" THEN
      TEMP.FCTR= TEMP.OVR.FLG
      RETURN
   END
*T26538 ^
   TEMP.FCTR = 1000
   TYP = 0
   MPTR=1
   LOCATE TEMP.CCTR IN EST.RL.PRESS.CCTR<1,COMP>,1 SETTING MPTR ELSE MPTR=1
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*
* Calculate Number of Colours in Job
*
   NO.OF.COLS=0
   NO.OF.COLS= EST.PROD.COLORS.1<1,COMP,MPTR>+NO.OF.COLS ;* colours front
   NO.OF.COLS= EST.PROD.COLORS.2<1,COMP,MPTR>+NO.OF.COLS ;* colours back
   NO.OF.COLS= EST.RL.ADHESIVE<1,COMP,MPTR>+NO.OF.COLS      ;* colours adhesive
   NO.OF.COLS=(EST.RL.SSCREEN<1,COMP,MPTR>+0)+NO.OF.COLS
   NO.OF.COLS=(EST.RL.GRAVURE<1,COMP,MPTR>+0)+NO.OF.COLS
   IF EST.RL.FULL.SPOT<1,COMP,MPTR>='S' THEN
      NO.OF.COLS=NO.OF.COLS+1
   END
****** TEMPORARY BUSTING OF CODE BY SBU REQUEST **********
*R01   NO.OF.COLS= EST.PROD.COLORS.1<1,COMP,MPTR>
*R01   IF EST.PROD.COLORS.2<1,COMP,MPTR> GT NO.OF.COLS THEN
*R01      NO.OF.COLS= EST.PROD.COLORS.2<1,COMP,MPTR>
*R01   END
**** END BUSTING *****************************************
*
* Check If Foil Stamper in Use
*
   IF EST.RL.FOIL<1,COMP,MPTR>#'Y' AND EST.RL.FOIL<1,COMP,MPTR>#'N' THEN
      EST.RL.FOIL<1,COMP,MPTR>='N'
      FOIL.SET='N'
   END
   IF EST.RL.FOIL<1,COMP,MPTR>='Y' THEN
      FOIL.SET='Y'
   END ELSE
      FOIL.SET='N'
   END
   IF EST.RL.FOIL.SPEED<1,COMP,MPTR>='Y' THEN
      FOIL.SET='Y'
   END
*
*--- Establish Die Details for 'S' type presses Max Speed Calculations
*
   DIE.TYPE=EST.RL.DIETYPE<1,COMP,MPTR>
   DIE.GSET=EST.RL.DIE.CYL.ID<1,COMP,MPTR>[1,2]:'*':EST.RL.DIE.CYL.ID<1,COMP,MPTR>[3,4]
   DIE.TEETH=EST.RL.DIE.CYL.ID<1,COMP,MPTR>[3,4]
   IF DIE.TYPE='' THEN DIE.TYPE='F'
*
* Setup The Key To Access Run Efficiency table
*
1000 *
   EFF.KEY=TEMP.CCTR:"*":DIE.TYPE:"*":NO.OF.COLS:"*":FOIL.SET
   MATREAD REFF.REC FROM ESTIMATE.RES.RUN.EFF, CONO:EFF.KEY ELSE
      NO.OF.COLS=NO.OF.COLS-1
      IF NO.OF.COLS >=0 THEN
         GO 1000
      END
      IF QPTR = 1 THEN
         ERRMSG = "Cannot locate run efficiency table"
         GOSUB 90000
      END
      TEMP.FCTR = 1000
      GOTO 99999
   END
*
*---- MAIN PROCESSING
*
   IMP = TEMP.QTY
   LOCATE IMP IN REFF.QTY<1>,1 BY "AR" SETTING P ELSE NULL
   BEGIN CASE
      CASE P = 1
         TEMP.FCTR = REFF.FCTR<1,P>
      CASE P > COUNT(REFF.QTY,VM) + 1
         TEMP.FCTR = REFF.FCTR<1,P-1>
      CASE REFF.EXTR = "Y"
         TEMP.FCTR = INT(REFF.FCTR<1,P>-(REFF.QTY<1,P>-IMP)/(REFF.QTY<1,P>-REFF.QTY<1,P-1>)*(REFF.FCTR<1,P>-REFF.FCTR<1,P-1>)+0.5)
      CASE 1
         TEMP.FCTR = REFF.FCTR<1,P>
   END CASE
   IF TEMP.FCTR+0 <= 0 THEN TEMP.FCTR = 1000
   DC.CODE = EST.PROD.DC.CODE<1,COMP>
   IF DC.CODE # "" THEN
      LOCATE DC.CODE IN EQP.PRESS.DC.CODE<1>,1 SETTING FND ELSE FND = 0
      IF FND > 0 THEN
         DC.MAX = EQP.PRESS.DC.MAX<1,FND>
         IF DC.MAX < INT(TEMP.STD*(TEMP.FCTR/1000)+0.5) THEN
            TEMP.FCTR = INT(DC.MAX/TEMP.STD*1000+0.5)
         END
      END
   END
   RETURN
*
*---- ERROR ROUTINE
*
90000*
   ERR.TYPE = 1
   CALL SI_SYSCOM(MAT SYSCOM.REC)
   RETURN
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
