      SUBROUTINE EST.RES.DEPT.COMP.SUB (CONO, EST.KEY, DSEL, CSEL, QSEL, MAT DSUM.REC2)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.DEPT.COMP.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/14/86
*
* DESCRIPTION
*
* This subroutine summarizes estimate data by department, component and
* type.  Summarization may occur for a single department or "ALL", a single
* component or "ALL", and a specified order quantity.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>EST.DEPT.COMP.SUM
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>CPYLIB>FILE.VARS
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      MAT DSUM.REC2 = ""
*
*---- MAIN PROCESSING
*
      READ EST.RL.PP FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE EST.RL.PP=''
100*
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
         DEPT = ESTD.DEPT
         COMP = FIELD(ESTD.ID,"!",2)
         LOCATE DEPT IN DSUM.DEPT<1>,1 BY "AL" SETTING P1 ELSE
            IF P1 <= DCNT THEN
               FOR N = 1 TO 40
                  DSUM.REC2(N) = INSERT(DSUM.REC2(N),1,P1,0,"")
               NEXT N
            END
            DSUM.DEPT<1,P1> = DEPT
            LOCATE DEPT IN EST.DEPT<1>,1 SETTING PP ELSE NULL
            DSUM.DEPT.DESC<1,P1> = EST.DEPT.DESC<1,PP>
         END
         P2 = COMP
         DSUM.COMP<1,P1,P2> = COMP
         DSUM.COMP.DESC<1,P1,P2> = EST.PROD.DESC<1,COMP>
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>[1,1]
            IF TYPE = "G" THEN GOTO 180
            HRS = ESTD.HRS<1,TP>
            COST = ESTD.COST<1,TP>
            IF EST.RL.PP.INCLUDE#'Y' THEN
               LOCATE ESTD.DEPT IN EST.RL.PP<1>,1 SETTING DPOS THEN
                  SALE = 0
               END ELSE
                  SALE = ESTD.TSALE<1,TP>
               END
            END ELSE
               SALE = ESTD.TSALE<1,TP>
            END
            VFLG = ESTD.VSALE<1,TP>
            BEGIN CASE
            CASE TYPE = "L"
               DSUM.DEPT.LABOR.HRS<1,P1> = DSUM.DEPT.LABOR.HRS<1,P1> + HRS
               DSUM.DEPT.LABOR.COST<1,P1> = DSUM.DEPT.LABOR.COST<1,P1> + COST
               DSUM.DEPT.LABOR.SALE<1,P1> = DSUM.DEPT.LABOR.SALE<1,P1> + SALE
               DSUM.COMP.LABOR.HRS<1,P1,P2> = DSUM.COMP.LABOR.HRS<1,P1,P2> + HRS
               DSUM.COMP.LABOR.COST<1,P1,P2> = DSUM.COMP.LABOR.COST<1,P1,P2> + COST
               DSUM.COMP.LABOR.SALE<1,P1,P2> = DSUM.COMP.LABOR.SALE<1,P1,P2> + SALE
            CASE TYPE = "M" OR TYPE = "I"
               DSUM.DEPT.MATL.COST<1,P1> = DSUM.DEPT.MATL.COST<1,P1> + COST
               DSUM.DEPT.MATL.SALE<1,P1> = DSUM.DEPT.MATL.SALE<1,P1> + SALE
               DSUM.COMP.MATL.COST<1,P1,P2> = DSUM.COMP.MATL.COST<1,P1,P2> + COST
               DSUM.COMP.MATL.SALE<1,P1,P2> = DSUM.COMP.MATL.SALE<1,P1,P2> + SALE
            CASE TYPE = "P"
               DSUM.DEPT.OSP.COST<1,P1> = DSUM.DEPT.OSP.COST<1,P1> + COST
               DSUM.DEPT.OSP.SALE<1,P1> = DSUM.DEPT.OSP.SALE<1,P1> + SALE
               DSUM.COMP.OSP.COST<1,P1,P2> = DSUM.COMP.OSP.COST<1,P1,P2> + COST
               DSUM.COMP.OSP.SALE<1,P1,P2> = DSUM.COMP.OSP.SALE<1,P1,P2> + SALE
            CASE TYPE = "S"
               DSUM.DEPT.SHIP.COST<1,P1> = DSUM.DEPT.SHIP.COST<1,P1> + COST
               DSUM.DEPT.SHIP.SALE<1,P1> = DSUM.DEPT.SHIP.SALE<1,P1> + SALE
               DSUM.COMP.SHIP.COST<1,P1,P2> = DSUM.COMP.SHIP.COST<1,P1,P2> + COST
               DSUM.COMP.SHIP.SALE<1,P1,P2> = DSUM.COMP.SHIP.SALE<1,P1,P2> + SALE
            CASE TYPE = "O"
               DSUM.DEPT.MISC.COST<1,P1> = DSUM.DEPT.MISC.COST<1,P1> + COST
               DSUM.DEPT.MISC.SALE<1,P1> = DSUM.DEPT.MISC.SALE<1,P1> + SALE
               DSUM.COMP.MISC.COST<1,P1,P2> = DSUM.COMP.MISC.COST<1,P1,P2> + COST
               DSUM.COMP.MISC.SALE<1,P1,P2> = DSUM.COMP.MISC.SALE<1,P1,P2> + SALE
            END CASE
            BEGIN CASE
            CASE VFLG = ""
               DSUM.DEPT.SETUP.SALE<1,P1> = DSUM.DEPT.SETUP.SALE<1,P1> + SALE
               DSUM.COMP.SETUP.SALE<1,P1,P2> = DSUM.COMP.SETUP.SALE<1,P1,P2> + SALE
            CASE NUM(VFLG)
               SALE = SALE - INT((SALE * (VFLG/10000))+.5)
               DSUM.DEPT.SETUP.SALE<1,P1> = DSUM.DEPT.SETUP.SALE<1,P1> + SALE
               DSUM.COMP.SETUP.SALE<1,P1,P2> = DSUM.COMP.SETUP.SALE<1,P1,P2> + SALE
            END CASE
180      NEXT TP
190   NEXT DP
      RETURN
   END
