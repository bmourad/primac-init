*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - RES.BP
* PROGRAM     - EST.RL.DIE.MAINT
* BY          - KERRY WILSON
* DATE        - 01/22/90
* DESCRIPTION - This program maintains the Roll Label Die.
* MOD TASK 16694 DMT 09/28/92 ADD LOCATION AND COST CENTER
* MOD TASK 16695 DMT 10/14/92 ADD UPDATE DIE.XREF
* MOD TASK 16863 BP 01/18/93 REMOVE ALL REFERENCES TO DEPARTMENT
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 03/12/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*COPY>JES.CPYLIB>ESTIMATE.RL.DIE
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>VEND
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET SYSCOM
*
  SYS.TYPE=1
  CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
  OPEN 'ESTIMATE.RL.DIE' TO ESTIMATE.RL.DIE ELSE
    ERRMSG = 'CANNOT OPEN ESTIMATE.RL.DIE FILE'
    GOTO 93000
  END
  OPEN 'ESTIMATE.RL.CYLINDER' TO ESTIMATE.RL.CYLINDER ELSE
    ERRMSG = 'CANNOT OPEN ESTIMATE.RL.CYLINDER FILE'
    GOTO 93000
  END
  OPEN 'COST.CNTR' TO COST.CNTR ELSE
    ERRMSG = 'CANNOT OPEN COST.CNTR FILE'
    GOTO 93000
  END
  OPEN 'VEND' TO VEND ELSE
    ERRMSG = 'CANNOT OPEN VEND FILE'
    GOTO 93000
  END
  OPEN 'JES.SCREENS' TO JES.SCREENS ELSE
    ERRMSG = 'CANNOT OPEN JES.SCREENS FILE'
    GOTO 93000
  END
  OPEN 'VENDOR.XREF' TO VENDOR.XREF ELSE
    ERRMSG = 'CANNOT OPEN VENDOR.XREF FILE'
    GOTO 93000
  END
  OPEN 'DIE.XREF' TO DIE.XREF ELSE
    ERRMSG = 'CANNOT OPEN DIE.XREF FILE'
    GOTO 93000
  END
  OPEN 'COMPANY' TO COMPANY ELSE
    ERRMSG = 'CANNOT OPEN COMPANY FILE'
    GOTO 93000
  END
  OPEN 'CONTROL' TO CONTROL ELSE
    ERRMSG = 'CANNOT OPEN CONTROL FILE'
    GOTO 93000
  END
  OPEN 'PREFIX' TO PREFIX ELSE
    ERRMSG = 'CANNOT OPEN PREFIX FILE'
    GOTO 93000
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RL.DIE.MAINT;JES.SCREENS
  SCREEN FORMAT
  SCREEN COUNT;;32
  COM.LINE.COUNT = S$LCNT
  COM.LINE.SPACE = S$LSPC
  SCREEN COUNT;;12
  PRS.LINE.COUNT = S$LCNT
  PRS.LINE.SPACE = S$LSPC
  NEW.NAME = ''
  OLD.NAME = ''
*
*---- SET UP GEN.XREF.REC
*
  MAT GEN.XREF.REC = ''
  GXR.CO = CONO
  GXR.HEADING<1,2> = 'DESCRIPTION'
  GXR.ATT<1,1> = 0
  GXR.ATT<1,2> = 1
  GXR.LEN<1,1> = 8
  GXR.LEN<1,2> = 30
*
*---- MAIN PROCESSING
*
100 *
  DIE.SELECT = ''
  PRS.DESC = ''
  COM.CURR.REF.NO = ''
  PRS.CURR.REF.NO = ''
  COM.REF.NO = 1
  PRS.REF.NO = 1
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  SCREEN FIELD;;2
  SCREEN INPUT;;2
  IF S$VALUE = 'END' THEN GOTO 99999
  IF S$VALUE = '???' THEN
    CALL DIE.SZ.AR.XREF(CONO,DIE.SELECT)
    SCREEN DEFINE;EST.RL.DIE.MAINT;JES.SCREENS
    SCREEN FORMAT
*         GOSUB 80000
*         SCREEN DISPLAY;;ALL
    IF DIE.SELECT = '' THEN
      GOTO 100
    END
    S$VALUE = DIE.SELECT
    S$DATA(2)<S$SCR> = DIE.SELECT
    S$DATA(1)<S$SCR> = DATE()
    SCREEN DISPLAY;;1
    SCREEN DISPLAY;;2
  END
  RLKEY = CONO:S$VALUE
  NEW.REC = 0
  * T26126 v
  MATREADU RLDIE.REC FROM ESTIMATE.RL.DIE,RLKEY LOCKED
    ERRMSG = 'DIE record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 91000;GOTO 100 
  END THEN
  * T26126 ^
*         NEW.NAME = RLDIE.SZ.AROUND
    NEW.NAME = FMT(RLDIE.SZ.AROUND,"R#6\0")
    NEW.NAME = CONVERT(" ","0",NEW.NAME)
    OLD.NAME = NEW.NAME
  END ELSE
    MAT RLDIE.REC = ''
    NEW.REC = 1
    NEW.NAME = ''
    OLD.NAME = ''
  END
  COM.CNT = COUNT(RLDIE.COMMENTS,VM) + (RLDIE.COMMENTS # "")
  PRS.CNT = COUNT(RLDIE.CCTR,VM) + (RLDIE.CCTR # "")
  IF NEW.REC THEN
    FOR FLD = 1 TO 15
      ON FLD GOSUB 10200,10300,10400,10500,10600,10700,10800,10900,11000,11100,11200,11300,11400,11500,11550
      IF S$VALUE = 'END' THEN 
        SCREEN CLEAR
        RELEASE
        GOTO 100
      END
    NEXT FLD
    COM.CNT = 0
    PRS.CNT = 0
    OPT2 = 'A'
    OPT3 = 'A'
    GOSUB 40000
    GOSUB 41000
  END ELSE
    COM.CNT = DCOUNT(RLDIE.COMMENTS,VM)
    PRS.CNT = DCOUNT(RLDIE.CCTR,VM)
    GOSUB 80000
    SCREEN DISPLAY;;ALL
  END
*
*---- ENTER PROMPT
*
500 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21
  OPTION = S$VALUE
  BEGIN CASE
    CASE NUM(OPTION)
      ON OPTION GOSUB 10200,10300,10400,10500,10600,10700,10800,10900,11000,11100,11200,11300,11400,11500,11550
    CASE OPTION = 'D'
510 *
* P_X  = 0 ; P_Y = 23 ; P_VALUE = 'Are you sure you want to Delete (Y/N)? ' ; P_OPT = ""
* CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
*             INPUT ANS:
* P_X  = 0 ; P_Y = 23 ; P_VALUE = "" ; P_OPT = "CL"
* CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      SCREEN FIELD;;41
      SCREEN INPUT;;41
      BEGIN CASE
        CASE S$VALUE = 'Y'
          NEW.NAME = ""
          RLKEY.DIE = RLKEY[4,LEN(RLKEY)-3]
          CALL GEN.XREF.MAINT(CONO,RLKEY.DIE,OLD.NAME,NEW.NAME,DIE.XREF,PREFIX)
          DELETE ESTIMATE.RL.DIE,RLKEY
          SCREEN CLEAR
          RELEASE
          GOTO 100
        CASE S$VALUE = 'N'
          GOTO 500
        CASE 1
          GOTO 510
      END CASE
    CASE OPTION = 'C'
520 *
      SCREEN FIELD;;34
      SCREEN INPUT;;34
      OPT2 = S$VALUE
      BEGIN CASE
        CASE OPT2 = "" OR OPT2 = "E" OR OPT2 = "END"
          GOTO 530
        CASE OPT2 = "A" AND COM.CNT < 99
          GOSUB 40000
        CASE OPT2 = "C" AND COM.CNT > 0
          GOSUB 30000
          IF S$VALUE # "" AND S$VALUE # "END" THEN
            COM.REF.NO = S$VALUE
            GOSUB 11600
          END
        CASE OPT2 = "I" AND COM.CNT > 0
          GOSUB 30000
          IF S$VALUE # "" AND S$VALUE # "END" THEN
            COM.REF.NO = S$VALUE
            S$DATA(33) = INSERT(S$DATA(33),S$SCR,COM.REF.NO,0,"")
            RLDIE.COMMENTS = INSERT(RLDIE.COMMENTS,1,COM.REF.NO,0,"")
            COM.CNT = COM.CNT + 1
            COM.CURR.REF.NO = ""
            GOSUB 50000
            GOSUB 11600
            IF S$VALUE = "END" THEN
              S$DATA(33) = DELETE(S$DATA(33),S$SCR,COM.REF.NO,0)
              RLDIE.COMMENTS = DELETE(RLDIE.COMMENTS,1,COM.REF.NO,0)
              COM.CNT = COM.CNT - 1
            END
            COM.CURR.REF.NO = ""
            GOSUB 50000
          END
        CASE OPT2 = "D" AND COM.CNT > 0
          GOSUB 30000
          IF S$VALUE # "" AND S$VALUE # "END" THEN
            COM.REF.NO = S$VALUE
            S$DATA(33) = DELETE(S$DATA(33),S$SCR,COM.REF.NO,0)
            RLDIE.COMMENTS = DELETE(RLDIE.COMMENTS,1,COM.REF.NO,0)
            COM.CNT = COM.CNT - 1
            IF COM.REF.NO > COM.CNT THEN COM.REF.NO = COM.REF.NO - 1
            COM.CURR.REF.NO = ""
            GOSUB 50000
          END
        CASE OPT2 = "S"
          COM.REF.NO = COM.CURR.REF.NO + COM.LINE.COUNT
          IF COM.REF.NO > COM.CNT THEN COM.REF.NO = 1
          GOSUB 50000
    * T25978 v
        CASE OPT2 = 'SR'
          COM.REF.NO = COM.CURR.REF.NO - COM.LINE.COUNT
          IF COM.REF.NO < 1 THEN COM.REF.NO = 1
          GOSUB 50000
        CASE OPT2 = 'ST'
          COM.REF.NO = 1
          GOSUB 50000
        CASE OPT2 = 'SB'
          COM.REF.NO = COM.CNT
          GOSUB 50000
    * T25978 ^
      END CASE
      GOTO 520
    CASE OPTION = 'P'
525 *
      SCREEN FIELD;;15
      SCREEN INPUT;;15
      OPT3 = S$VALUE
      BEGIN CASE
        CASE OPT3 = "" OR OPT3 = "E" OR OPT3 = "END"
          GOTO 530
        CASE OPT3 = "A" AND PRS.CNT < 99
          GOSUB 41000
        CASE OPT3 = "C" AND PRS.CNT > 0
          GOSUB 31000
          IF S$VALUE # "" AND S$VALUE # "END" THEN
            PRS.REF.NO = S$VALUE
            GOSUB 11700
          END
        CASE OPT3 = "I" AND PRS.CNT > 0
          GOSUB 31000
          IF S$VALUE # "" AND S$VALUE # "END" THEN
            PRS.REF.NO = S$VALUE
            S$DATA(13) = INSERT(S$DATA(13),S$SCR,PRS.REF.NO,0,"")
            RLDIE.CCTR = INSERT(RLDIE.CCTR,1,PRS.REF.NO,0,"")
            PRS.DESC = INSERT(PRS.DESC,1,PRS.REF.NO,0,"")
            PRS.CNT = PRS.CNT + 1
            PRS.CURR.REF.NO = ""
            GOSUB 51000
            GOSUB 11700
            IF S$VALUE = "END" THEN
              S$DATA(13) = DELETE(S$DATA(13),S$SCR,PRS.REF.NO,0)
              RLDIE.CCTR = DELETE(RLDIE.CCTR,1,PRS.REF.NO,0)
              PRS.DESC = DELETE(PRS.DESC,1,PRS.REF.NO,0)
              PRS.CNT = PRS.CNT - 1
            END
            PRS.CURR.REF.NO = ""
            GOSUB 51000
          END
        CASE OPT3 = "D" AND PRS.CNT > 0
          GOSUB 31000
          IF S$VALUE # "" AND S$VALUE # "END" THEN
            PRS.REF.NO = S$VALUE
            S$DATA(13) = DELETE(S$DATA(13),S$SCR,PRS.REF.NO,0)
            RLDIE.CCTR = DELETE(RLDIE.CCTR,1,PRS.REF.NO,0)
            PRS.DESC = DELETE(PRS.DESC,1,PRS.REF.NO,0)
            S$DATA(14)<S$SCR> = PRS.DESC
            PRS.CNT = PRS.CNT - 1
            IF PRS.REF.NO > PRS.CNT THEN PRS.REF.NO = PRS.REF.NO - 1
            PRS.CURR.REF.NO = ""
            GOSUB 51000
          END
        CASE OPT3 = "S"
          PRS.REF.NO = PRS.CURR.REF.NO + PRS.LINE.COUNT
          IF PRS.REF.NO > PRS.CNT THEN PRS.REF.NO = 1
          GOSUB 51000
    * T25978 v
        CASE OPT3 = 'SR'
          PRS.REF.NO = PRS.CURR.REF.NO - PRS.LINE.COUNT
          IF PRS.REF.NO < 1 THEN PRS.REF.NO = 1
          GOSUB 51000
        CASE OPT3 = 'ST'
          PRS.REF.NO = 1
          GOSUB 51000
        CASE OPT3 = 'SB'
          PRS.REF.NO = PRS.CNT
          GOSUB 51000
    * T25978 ^
      END CASE
      GOTO 525
530 *
    CASE OPTION = 'E' OR OPTION = 'END'
      SCREEN CLEAR
      RELEASE
      GOTO 100
    CASE OPTION = 'F'
      MATWRITE RLDIE.REC ON ESTIMATE.RL.DIE,RLKEY
      IF NEW.NAME OR OLD.NAME # NEW.NAME THEN
        RLKEY.DIE = RLKEY[4,LEN(RLKEY)-3]
        CALL GEN.XREF.MAINT(CONO,RLKEY.DIE,OLD.NAME,NEW.NAME,DIE.XREF,PREFIX)
      END
      SCREEN CLEAR
      RELEASE
      GOTO 100
  END CASE
  GOTO 500
*
*---- DIE DEPARTMENT
*
10100 *
*     SCREEN FIELD;;3
*     SCREEN INPUT;;3;RETURN
*     RLDIE.DEPT = S$VALUE
*     MATREAD DEPT.REC FROM DEPARTMENT,CONO:RLDIE.DEPT ELSE
*        ERRMSG = 'INVALID DEPARTMENT'
*        GOSUB 91000
*        GOTO 10100
*     END
*     S$DATA(25)<S$SCR> = DEPT.DESC
*     SCREEN DISPLAY;;25
  RETURN
*
*---- VEND
*
10200 *
  SCREEN FIELD;;4
  SCREEN INPUT;;4;RETURN
  RLDIE.VEND= S$VALUE
  IF S$VALUE = '' THEN
    SCREEN FIELD;;26
    SCREEN INPUT;;26
    IF S$VALUE = '' OR S$VALUE = 'END' THEN GOTO 10200
    GXR.XREF = VENDOR.XREF
    GXR.FILE = VEND
    GXR.LOC = 1
    GXR.TOP.LINE<1,1> = "VENDOR XREF SEARCH"
    GXR.TOP.LINE<1,2> = "FOR STRING : ":S$VALUE
    GXR.HEADING<1,1> = 'VENDOR #'
    GXR.ID = ''
    GXR.SRCH.ID = S$VALUE
*        CALL GEN.XREF1(MAT GEN.XREF.REC,PREFIX)
    CALL GEN.XREF(MAT GEN.XREF.REC,PREFIX)
    IF GXR.ID = '' THEN
      S$DATA(26)<S$SCR> = ''
    END
    SCREEN FORMAT
    SCREEN DISPLAY;;ALL
    COM.CURR.REF.NO = ""
    GOSUB 50000
    RLDIE.VEND = GXR.ID
    IF GXR.ID = '' THEN
      GOTO 10200
    END ELSE
      MATREAD VEND.REC FROM VEND, CONO:GXR.ID ELSE
        ERRMSG = 'VENDOR NUMBER ':GXR.ID:' IS MISSING'
        GOSUB 91000
        GOTO 10200
      END
*            OLD.NAME = VEND.DESC
      STATUS = '0'
      VEND.NUM = GXR.ID
      S$DATA(4)<S$SCR> = VEND.NUM
      SCREEN DISPLAY;;4
    END
  END
  MATREAD VEND.REC FROM VEND,CONO:RLDIE.VEND ELSE
    ERRMSG = 'INVALID VENDOR'
    GOSUB 91000
    GOTO 10200
  END
  S$DATA(26)<S$SCR> = VEND.DESC
  SCREEN DISPLAY;;26
  RETURN
*
*---- TEETH
*
10300 *
  SCREEN FIELD;;5
  SCREEN INPUT;;5;RETURN
  RLDIE.TEETH = S$VALUE
  RETURN
*
*---- REPEAT
*
10400 *
  SCREEN FIELD;;6
  SCREEN INPUT;;6;RETURN
  RLDIE.REPEAT = S$VALUE
  RETURN
*
*---- NUMBER AROUND
*
10500 *
  SCREEN FIELD;;7
  SCREEN INPUT;;7;RETURN
  RLDIE.NO.AROUND = S$VALUE
  RETURN
*
*---- SIZE AROUND
*
10600 *
  SCREEN FIELD;;8
  SCREEN INPUT;;8;RETURN
  RLDIE.SZ.AROUND = S$VALUE
*      NEW.NAME = RLDIE.SZ.AROUND
  NEW.NAME = FMT(RLDIE.SZ.AROUND,"R#6\0")
  NEW.NAME = CONVERT(" ","0",NEW.NAME)
  RETURN
*
*---- SPACE AROUND
*
10700 *
  SCREEN FIELD;;9
  SCREEN INPUT;;9;RETURN
  RLDIE.SP.AROUND = S$VALUE
  RETURN
*
*---- NUMBER ACROSS
*
10800 *
  SCREEN FIELD;;10
  SCREEN INPUT;;10;RETURN
  RLDIE.NO.ACROSS = S$VALUE
  RETURN
*
*---- SIZE ACROSS
*
10900 *
  SCREEN FIELD;;23
  SCREEN INPUT;;23;RETURN
  RLDIE.SZ.ACROSS = S$VALUE
  RETURN
*
*---- STEP ACROSS
*
11000 * 
  SCREEN FIELD;;24
  SCREEN INPUT;;24;RETURN
  RLDIE.STEP.ACROSS = S$VALUE
  RETURN
*
*---- RETOOLING
*
11100 *
  SCREEN FIELD;;27
  SCREEN INPUT;;27;RETURN
  RLDIE.RETOOL = S$VALUE
  RETURN
*
*---- ORDER DUE IN
*
11200 *
  SCREEN FIELD;;28
  SCREEN INPUT;;28;RETURN
  RLDIE.ORDER.DATE = S$VALUE
  RETURN
*
*---- JOB AVAILABLE
*
11300 *
  SCREEN FIELD;;29
  SCREEN INPUT;;29;RETURN
  RLDIE.JOB.DATE = S$VALUE
  RETURN
*
*---- DAMAGED DIE
*
11400 *
  SCREEN FIELD;;30
  SCREEN INPUT;;30;RETURN
  RLDIE.DAMAGED = S$VALUE
  RETURN
*
*---- CYLINDER ID
*
11500 *
  SCREEN FIELD;;31
  SCREEN INPUT;;31;RETURN
  CREC = ''
  READ CREC FROM ESTIMATE.RL.CYLINDER,CONO:S$VALUE ELSE
    ERRMSG = "Invalid Cylinder ID"
    GOSUB 91000
    GOTO 11500
  END
  RLDIE.CYLINDER = S$VALUE
  RETURN
*
*---- LOCATION
*
11550 *
  SCREEN FIELD;;11
  SCREEN INPUT;;11;RETURN
  RLDIE.LOC = S$VALUE
  RETURN
*
*---- GET COMMENTS
*
11600 *
  S$VAL = COM.REF.NO
  SCREEN DISPLAY;;32
  S$VAL = COM.REF.NO
  SCREEN FIELD;;33
  SCREEN INPUT;;33
  BEGIN CASE
    CASE S$VALUE = "END"
      GOTO 11699
    CASE S$VALUE = ""
      GOTO 11600
    CASE S$VALUE # ""
      RLDIE.COMMENTS<1,COM.REF.NO> = S$VALUE
  END CASE
11699 *
  RETURN
*
*---- GET PRESSES
*
11700 *
  S$VAL = PRS.REF.NO
  SCREEN DISPLAY;;12
  S$VAL = PRS.REF.NO
  SCREEN FIELD;;13
  SCREEN INPUT;;13
  BEGIN CASE
    CASE S$VALUE = "END"
      GOTO 11799
    CASE S$VALUE = ""
      GOTO 11700
    CASE S$VALUE # ""
      RLDIE.CCTR<1,PRS.REF.NO> = S$VALUE
  END CASE
  MATREAD CCTR.REC FROM COST.CNTR,CONO:RLDIE.CCTR<1,PRS.REF.NO> ELSE
    ERRMSG = 'INVALID PRESS'
    GOSUB 91000
    GOTO 11700
  END
  PRS.DESC<1,PRS.REF.NO> = CCTR.DESC "L#28"
  S$DATA(14)<S$SCR> = PRS.DESC
  S$VAL = PRS.REF.NO
  SCREEN DISPLAY;;14
11799 *
  RETURN
*
*---- GET LINE REFERENCE NUMBER FOR COMMENTS
*
30000 *
  S$DATA(22)<S$SCR> = ""
  SCREEN FIELD;;22
  S$MINV = COM.CURR.REF.NO
  S$MAXV = S$MINV + COM.LINE.COUNT - 1
  IF S$MAXV > COM.CNT THEN S$MAXV = COM.CNT
  SCREEN INPUT;;22
  RETURN
*
*---- ADD COMMENTS
*
40000 *
  DONE = 0
  FOR COM.REF.NO = COM.CNT + 1 TO 99 UNTIL DONE
    GOSUB 50000
    GOSUB 11600
    IF S$VALUE = "END" THEN
      DONE = 1
      S$DATA(33) = DELETE(S$DATA(33),S$SCR,COM.REF.NO,0)
      RLDIE.COMMENTS = DELETE(RLDIE.COMMENTS,1,COM.REF.NO,0)
    END ELSE
      COM.CNT = COM.CNT + 1
    END
  NEXT COM.REF.NO
  COM.REF.NO = COM.CNT
  COM.CURR.REF.NO = ""
  GOSUB 50000
  RETURN
*
*---- SCROLL DESCRIPTION FOR COMMENTS
*
50000 *
  START.COM.REF.NO = 1 + INT((COM.REF.NO-1)/COM.LINE.COUNT)*COM.LINE.COUNT
  IF START.COM.REF.NO = COM.CURR.REF.NO THEN RETURN
  COM.CURR.REF.NO = START.COM.REF.NO
  S$VAL = START.COM.REF.NO
  S$CNT = COM.CNT
  SCREEN MULTI;EST.RL.DIE.MAINT;C;32;33
  RETURN
*
*---- GET LINE REFERENCE NUMBER FOR PRESSES
*
31000 *
  S$DATA(22)<S$SCR> = ""
  SCREEN FIELD;;22
  S$MINV = PRS.CURR.REF.NO
  S$MAXV = S$MINV + PRS.LINE.COUNT - 1
  IF S$MAXV > PRS.CNT THEN S$MAXV = PRS.CNT
  SCREEN INPUT;;22
  RETURN
*
*---- ADD PRESSES
*
41000 *
  DONE = 0
  FOR PRS.REF.NO = PRS.CNT + 1 TO 99 UNTIL DONE
    GOSUB 51000
    GOSUB 11700
    IF S$VALUE = "END" THEN
      DONE = 1
      S$DATA(13) = DELETE(S$DATA(13),S$SCR,PRS.REF.NO,0)
      RLDIE.CCTR = DELETE(RLDIE.CCTR,1,PRS.REF.NO,0)
      PRS.DESC = DELETE(PRS.DESC,1,PRS.REF.NO,0)
    END ELSE
      PRS.CNT = PRS.CNT + 1
    END
  NEXT PRS.REF.NO
  PRS.REF.NO = PRS.CNT
  PRS.CURR.REF.NO = ""
  GOSUB 51000
  COM.REF.NO = COM.CNT
  COM.CURR.REF.NO = ""
  GOSUB 50000
  RETURN
*
*---- SCROLL DESCRIPTION FOR PRESSES
*
51000 *
  START.PRS.REF.NO = 1 + INT((PRS.REF.NO-1)/PRS.LINE.COUNT)*PRS.LINE.COUNT
  IF START.PRS.REF.NO = PRS.CURR.REF.NO THEN RETURN
  PRS.CURR.REF.NO = START.PRS.REF.NO
  S$VAL = START.PRS.REF.NO
  S$CNT = PRS.CNT
  SCREEN MULTI;EST.RL.DIE.MAINT;C;12;13;14
  RETURN
*
*---- LOAD SCREEN
*
80000 *
*     S$DATA(3)<S$SCR> = RLDIE.DEPT
  S$DATA(4)<S$SCR> = RLDIE.VEND
  S$DATA(5)<S$SCR> = RLDIE.TEETH
  S$DATA(6)<S$SCR> = RLDIE.REPEAT
  S$DATA(7)<S$SCR> = RLDIE.NO.AROUND
  S$DATA(8)<S$SCR> = RLDIE.SZ.AROUND
  S$DATA(9)<S$SCR> = RLDIE.SP.AROUND
  S$DATA(10)<S$SCR> = RLDIE.NO.ACROSS
  S$DATA(23)<S$SCR> = RLDIE.SZ.ACROSS
  S$DATA(24)<S$SCR> = RLDIE.STEP.ACROSS
  S$DATA(27)<S$SCR> = RLDIE.RETOOL
  S$DATA(28)<S$SCR> = RLDIE.ORDER.DATE
  S$DATA(29)<S$SCR> = RLDIE.JOB.DATE
  S$DATA(30)<S$SCR> = RLDIE.DAMAGED
  S$DATA(31)<S$SCR> = RLDIE.CYLINDER
  S$DATA(33)<S$SCR> = RLDIE.COMMENTS
  IF RLDIE.COMMENTS # "" THEN
    COM.REF.NO = 1
    GOSUB 50000
  END
  S$DATA(13)<S$SCR> = RLDIE.CCTR
  IF RLDIE.CCTR # "" THEN
    PRS.DESC = ''
    PCNT = PRS.CNT
    PTR = 0
    LOOP
      PTR += 1
      MATREAD CCTR.REC FROM COST.CNTR,CONO:RLDIE.CCTR<1,PTR> ELSE
        MAT CCTR.REC = ''
      END
      PRS.DESC<1,PTR> = CCTR.DESC "L#28"
      PCNT -= 1
    UNTIL PCNT = 0
    REPEAT
    S$DATA(14)<S$SCR> = PRS.DESC
    PRS.REF.NO = 1
    GOSUB 51000
  END
*     MATREAD DEPT.REC FROM DEPARTMENT,CONO:RLDIE.DEPT ELSE
*        MAT DEPT.REC = ''
*     END
*     S$DATA(25)<S$SCR> = DEPT.DESC
  MATREAD VEND.REC FROM VEND,CONO:RLDIE.VEND ELSE
    MAT VEND.REC = ''
  END
  S$DATA(26)<S$SCR> = VEND.DESC
  RETURN
*
*---- SYSCOM MESSAGES
*
91000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE=2;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
END
