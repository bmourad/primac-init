  SUBROUTINE EST.RL.PROD.SUB2(CONO,ACTION,EST.KEY,COMP,STATUS)
*********************************************************************
*
* REVISION - [08.1]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RL.PROD.SUB2
*
* AUTHOR   - DENISE JURGENS, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/26/90
*
* DESCRIPTION
*
* This subroutine allows input and maintenance of the 2nd product
* definition screen in roll label estimating.
*
*T25740 epitka 11/30/2001 * REV12
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*********************************************************************
*
*-COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.PRESS.SPOIL
*COPY>JES.CPYLIB>ESTIMATE.RL.DIE
*COPY>JES.CPYLIB>ESTIMATE.RL.CYLINDER
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
*COPY>JES.CPYLIB>ESTIMATE.BIND.SPOIL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>JES.CPYLIB>ESTIMATE.RL
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>EDIT.COM.DRIVER
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>GEN.XREF
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
  IF FILEINFO(WAREHOUSE,0)=0 THEN        
    OPEN '','WAREHOUSE' TO WAREHOUSE ELSE
      ERRMSG='WAREHOUSE FILE IS MISSING' 
      GOSUB 93000                        
    END                                  
  END                                    
*
  EQU TRUE TO "1"
  EQU FALSE TO "0"
  CHANGE.OPT=''
* OPEN NECESSARY FILES
*
  STATUS = ""
*
  OPEN "JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS"
    GOSUB 90000
    GOTO 99999
  END
*18484 REMOVE OPEN DEV.SCREENS BELOW

*
*-INITIALIZATION
*
*18484 DELETE LINE BELOW WITH "DEV.SCREENS" REMOVE COMMT ON SCREEN DEFINE
  SCREEN DEFINE;EST.RL.PROD.SUB2
  SCREEN FORMAT
  SCREEN COUNT;;16
  LINE.COUNT = S$LCNT
  MAT GEN.XREF.REC = ""
  MAINT.FLAG = ""
  COMP = 1
  SCREEN COUNT;;2
  OPTION = ""
  PROD.LINE.COUNT = S$LCNT
  PROD.LINE.SPACE = S$LSPC
  PROD.REF.NO = 1
*18484 Begin Display Qty info
  SCREEN COUNT;;25
  QTY.LINE.COUNT = S$LCNT
  QTY.LINE.SPACE = S$LSPC
  PTR.OFFSET = 1
*18484 End      
  PRICE.FLAG = CO.JES.PARAM<1,11>
  IF PRICE.FLAG = "" THEN PRICE.FLAG = "X"
*
*-MAIN PROCESSING
*
100 *
  SCREEN CLEAR
  QTY.CURR.REF.NO=''
  QTY.CNT=0
  S$DATA(1)<S$SCR>=DATE()
  SCREEN DISPLAY;;1
  IF EST.PROD.BIND.SPOIL<1,COMP,1> # "" THEN
    NEW.FLAG = ''
    GOSUB 80000
*18484 Begin     ADD DISPLAY FOR QTY
    QTY.REF.NO=1
    QTY.CURR.REF.NO=''
    QTY.CNT = DCOUNT(EST.QTY,VM)
    GOSUB 50000;* DISPLAY QTY TBL
    GOTO 7000
  END ELSE
    NEW.FLAG = 'Y'
    S$DATA(25)<S$SCR> = EST.QTY
    QTY.REF.NO=1
    QTY.CURR.REF.NO=''
    QTY.CNT = DCOUNT(EST.QTY,VM)
    GOSUB 50000;* DISPLAY QTY TBL
  END
*18484 End
*
* BINDERY SPOILAGE TABLE OR PERCENTAGE
*
1000 *
  SCREEN FIELD;;16
  SCREEN INPUT;;16;GOTO 7000
  BEGIN CASE
    CASE S$VALUE = "+" OR S$VALUE = "." OR S$VALUE = "-"
      ERRMSG = "INVALID ENTRY. TRY AGAIN!"
      GOSUB 90000
      GOTO 1000
    CASE NOT(NUM(S$VALUE))
      MATREAD BST.REC FROM ESTIMATE.BIND.SPOIL,CONO:S$VALUE ELSE
        ERRMSG = "INVALID BINDERY SPOILAGE TABLE. TRY AGAIN!"
        GOSUB 90000
        GOTO 1000
      END
  END CASE
  EST.PROD.BIND.SPOIL<1,COMP,1> = S$VALUE
  IF MAINT.FLAG = "X" THEN GOTO 7000
*
* PRESS SPOILAGE TABLE OR PERCENTAGE
*
1100 *
  SCREEN FIELD;;17
  MATREAD EQUIPMENT.REC FROM EQUIPMENT,CONO:EST.RL.PRESS.CCTR<1,COMP,1> ELSE
    MAT EQUIPMENT.REC = ""
  END
  S$DEFAULT = EQP.RL.PRESS.SPOIL.TBL
  SCREEN INPUT;;17;GOTO 7000
  BEGIN CASE
    CASE S$VALUE = "+" OR S$VALUE = "." OR S$VALUE = "-"
      ERRMSG = "INVALID ENTRY. TRY AGAIN!"
      GOSUB 90000
      GOTO 1100
    CASE NOT(NUM(S$VALUE))
      MATREAD PST.REC FROM ESTIMATE.PRESS.SPOIL,CONO:S$VALUE ELSE
        ERRMSG = "Invalid press spoilage table.  Try again!"
        GOSUB 90000
        GOTO 1100
      END
      EST.PROD.INIT.MR.IMP<1,COMP,1> = PST.INIT.QTY
      EST.PROD.SUBS.MR.IMP<1,COMP,1> = PST.SUB.QTY
      S$DATA(20)<S$SCR> = PST.INIT.QTY
      SCREEN DISPLAY;;20
      S$DATA(21)<S$SCR> = PST.SUB.QTY
      SCREEN DISPLAY;;21
  END CASE
  EST.PROD.PRESS.SPOIL<1,COMP,1> = S$VALUE
  IF MAINT.FLAG = "X" THEN GOTO 7000
*
* INITIAL M/R COUNT
*
1200 *
  SCREEN FIELD;;18
  SCREEN INPUT;;18;GOTO 7000
  EST.PROD.INIT.MR.CNT<1,COMP,1> = S$VALUE
*
* SUBSEQUENT M/R COUNT
*
1300 *
  SCREEN FIELD;;19
  SCREEN INPUT;;19;GOTO 1200
  EST.PROD.SUBS.MR.CNT<1,COMP,1> = S$VALUE
  IF MAINT.FLAG = "X" THEN GOTO 7000
*
* INITIAL M/R IMPRESSIONS
*
1500 *
  SCREEN FIELD;;20
  SCREEN INPUT;;20;GOTO 7000
  EST.PROD.INIT.MR.IMP<1,COMP,1> = S$VALUE
*
* SUBSEQUENT M/R IMPRESSIONS
*
1600 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21;GOTO 1500
  EST.PROD.SUBS.MR.IMP<1,COMP,1> = S$VALUE
  IF MAINT.FLAG = "X" THEN GOTO 7000
*
* ORDER STOCK
*
1700 *
  SCREEN FIELD;;14
  SCREEN INPUT;;14;GOTO 7000
  EST.RL.ORD.STK<1,COMP,1> = S$VALUE
  IF MAINT.FLAG = "X" THEN
    IF EST.RL.ORD.STK<1,COMP,1> = "Y" THEN
      GOTO 1800
    END ELSE
      EST.RL.ORD.REQ<1,COMP,1> = ""
      S$DATA(15)<S$SCR> = ""
      SCREEN DISPLAY;;15
      GOTO 7000
    END
  END
  IF EST.RL.ORD.STK<1,COMP,1> # "Y" THEN GOTO 1900
*
* MINIMUM ORDER REQUIRED
*
1800 *
  SCREEN FIELD;;15
  S$DEFAULT = ""
  SCREEN INPUT;;15;GOTO 1700
  EST.RL.ORD.REQ<1,COMP,1> = S$VALUE
  IF MAINT.FLAG = "X" THEN GOTO 7000
*
* PAPER GROUP
*
1900 *
  OPTION = "A"
  GOSUB 20000
  IF MAINT.FLAG = "X" THEN GOTO 7000
*
* LAMINATE
*
2500 *
  SCREEN FIELD;;22
  S$DEFAULT = "N"
  SCREEN INPUT;;22;GOTO 7000
  EST.RL.LAM<1,COMP,1> = S$VALUE
  IF S$VALUE = "N" THEN
    EST.RL.LAM.USAGE<1,COMP,1> = ""
    EST.RL.LAM.PROD<1,COMP,1> = ""
    EST.RL.LAM.INV<1,COMP,1> = ""
    EST.RL.LAM.DESC<1,COMP,1> = ""
    EST.RL.LAM.WIDTH<1,COMP,1> = ""
    EST.RL.LAM.MSI<1,COMP,1> = ""
    EST.RL.LAM.PRICE<1,COMP,1> = ""
    S$DATA(9)<S$SCR> = ""
    S$DATA(10)<S$SCR> = ""
    S$DATA(11)<S$SCR> = ""
    S$DATA(12)<S$SCR> = ""
    S$DATA(13)<S$SCR> = ""
    S$DATA(23)<S$SCR> = ""
    S$DATA(24)<S$SCR> = ""
    SCREEN DISPLAY;;23
    SCREEN DISPLAY;;24
    SCREEN DISPLAY;;9
    SCREEN DISPLAY;;10
    SCREEN DISPLAY;;11
    S$VAL = 1
    S$CNT = 3
    SCREEN MULTI;;C;12;13
    GOTO 7000
  END
*
* LAMINATE PAPER GROUP
*
2550 *
  S$DATA(23)<S$SCR> = EST.RL.LAM.USAGE<1,COMP,1>
  SCREEN FIELD;;23
  SCREEN INPUT;;23;GOTO 2500
  MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP,CONO:S$VALUE ELSE
    ERRMSG="Invalid Paper Group ID, Try Again!"
    GOSUB 90000
    GOTO 2550
  END
  IF ESTPG.TYPE # "R" THEN
    ERRMSG = "Paper Group ID Not Roll Label, Try Again!"
    GOSUB 90000
    GOTO 2550
  END
  EST.RL.LAM.USAGE<1,COMP,1> = S$VALUE
*
* LAMINATE PRODUCT
*
2560 *
  MAT ESTM.REC = ""
  IF EST.RL.LAM.PROD<1,COMP,1> = '' THEN
    OPT = 'A'
  END ELSE
    OPT = 'C'
    CHANGE.OPT = 'C'
  END
  S$DATA(24)<S$SCR> = EST.RL.LAM.PROD<1,COMP,1>
  SCREEN FIELD;;24
  S$DEFAULT = ""
  SCREEN INPUT;;24;GOTO 2550
  IF S$VALUE="???" THEN
    MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"R":EST.RL.LAM.USAGE<1,COMP,1> ELSE
      MAT ESTM.REC = ""
    END
    S$SCR=S$SCR+1
    CALL EST.PAPER.XREF(CONO,"S","R",EST.RL.LAM.USAGE<1,COMP,1>,INV.ID)
    SCREEN CLOSE
    S$SCR=S$SCR-1
    EST.RL.LAM.PROD<1,COMP,1>=INV.ID
    SCREEN FORMAT
    GOSUB 80000
    QTY.CURR.REF.NO=''
    GOSUB 50000 ;* REDISPLAY QTYS
    IF INV.ID="" THEN GOTO 2560
  END ELSE
    INV.ID = S$VALUE
  END
  MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"R":EST.RL.LAM.USAGE<1,COMP,1> ELSE
    MAT ESTM.REC = ""
  END
  LOCATE INV.ID IN ESTM.PROD<1>,1 SETTING MP ELSE
    ERRMSG="Invalid product ID. Try again! "
    GOSUB 90000
    GOTO 2560
  END
  EST.RL.LAM.PROD<1,COMP,1>=INV.ID
*
  S$DATA(10)<S$SCR> = EST.RL.LAM.DESC<1,COMP,1>
  SCREEN FIELD;;10
  S$O.R="O"
  S$DEFAULT=ESTM.DESC<1,MP>
  SCREEN INPUT;;10;GOTO 2560
  EST.RL.LAM.DESC<1,COMP,1>=S$VALUE
*
* LAMINATE INVENTORY ID
*
2600 *
  SCREEN FIELD;;9
  S$DEFAULT = ""
  SCREEN INPUT;;9;GOTO 2500
  BEGIN CASE
    CASE S$VALUE=""
      EST.RL.LAM.INV<1,COMP,1>=S$VALUE
    CASE 1
      INV.ID=S$VALUE
      IF INV.ID="???" THEN
        S$SCR=S$SCR+1
        PLINE = ""
        BASIS = ""
        FIN = ""
        CLR = ""
        WIDTH=""
        LNGTH=""
        CALL EST.INV.XREF(CONO,"S",PLINE,BASIS,FIN,CLR,WIDTH,LNGTH,INV.ID)
        SCREEN CLOSE
        S$SCR=S$SCR-1
        EST.RL.LAM.INV<1,COMP,1>=INV.ID
        SCREEN FORMAT
        GOSUB 80000
        QTY.CURR.REF.NO=''
        GOSUB 50000 ;* REDISPLAY QTYS
        IF INV.ID="" THEN GOTO 2600
      END
      MATREAD INV.REC FROM INVENTORY,CONO:INV.ID ELSE
        ERRMSG="Invalid inventory item. Try again! "
        GOSUB 90000
        GOTO 2600
      END
      GOSUB 35000
      EST.RL.LAM.INV<1,COMP,1>=INV.ID
  END CASE
  IF EST.RL.LAM.INV<1,COMP,1> # "" THEN
    S$DATA(10)<S$SCR> = EST.RL.LAM.DESC<1,COMP,1>
    SCREEN FIELD;;10
    S$DEFAULT = INV.DESC
    SCREEN INPUT;;10
    EST.RL.LAM.DESC<1,COMP,1> = S$VALUE
  END
*
* LAMINATE WIDTH
*
  S$DATA(11)<S$SCR> = EST.RL.LAM.WIDTH<1,COMP,1>
  SCREEN FIELD;;11
  IF EST.RL.LAM.INV<1,COMP,1> # "" THEN
    S$DEFAULT = INV.PAP.WIDTH
  END ELSE
    S$DEFAULT = ESTM.WIDTH<1,MP>
  END
  SCREEN INPUT;;11;GOTO 2600
  EST.RL.LAM.WIDTH<1,COMP,1> = S$VALUE
*
* LAMINATE MSI AND UNIT PRICE
*
  QTY.CNT = DCOUNT(EST.QTY,VM)
* Calculate msi for all qtys and display for a new product
  IF OPT = 'A' THEN
    FOR Y = 1 TO QTY.CNT
      GOSUB 30000
      TOT.MSI = INT(((EST.RL.LAM.WIDTH<1,COMP,1>*TOT.PC)/1000000)+.9)
      MACRO REPVAL;EST.RL.LAM.MSI<1,COMP,1>;"!";Y;TOT.MSI
    NEXT Y
    QTY.CURR.REF.NO = ''
    QTY.REF.NO=1
    PTR.OFFSET = 1
    GOSUB 81500
  END
*
  IF CHANGE.OPT = 'C' THEN
*     THIS IS ONLY A CHANGE ONLY PROMPT FOR VALUES ON THE SCREEN
    START.POS = QTY.REF.NO
    END.POS = QTY.REF.NO + QTY.LINE.COUNT - 1
    IF END.POS > QTY.CNT THEN
      END.POS = QTY.CNT
    END
  END ELSE
    START.POS = 1
    END.POS = QTY.CNT
  END
  CHANGE.OPT=''
  TEMPY=0
  IF UNASSIGNED(UNIT.PR) THEN UNIT.PR=''
  FOR Y = START.POS TO END.POS
   ** GOSUB 30000
    TEMPY=TEMPY+1
    IF TEMPY > QTY.LINE.COUNT THEN TEMPY = 1
  **  TOT.MSI = INT(((EST.RL.LAM.WIDTH<1,COMP,1>*TOT.PC)/1000000)+.9)
  **  MACRO REPVAL;EST.RL.LAM.MSI<1,COMP,1>;"!";Y;TOT.MSI
    TOT.MSI = FIELD(EST.RL.LAM.MSI<1,COMP,1>,"!",Y)
    IF PRICE.FLAG = "X" OR EST.RL.LAM.INV<1,COMP,1> = "" THEN
      PRICE.BRK.COUNT = DCOUNT(ESTM.END.QTY<1,MP>,SVM)
      FLG=0
      FOR X = 1 TO PRICE.BRK.COUNT UNTIL FLG=1
        TEST.QTY = ESTM.END.QTY<1,MP,X>
        IF TOT.MSI <= TEST.QTY THEN
          UNIT.PR = ESTM.QCOST<1,MP,X>
          FLG=1
        END
      NEXT X
      IF NOT(FLG) = "" THEN
        UNIT.PR = ESTM.QCOST<1,MP,PRICE.BRK.COUNT>
      END
    END
    IF UNIT.PR="" THEN
      IF TEMPY = 1 THEN
        QTY.CURR.REF.NO = ''
        QTY.REF.NO = Y
        GOSUB 50000
        GOSUB 81500
        GOSUB 82000
      END
      S$VAL=Y
      SCREEN FIELD;;13
      IF Y > 1 THEN
        S$O.R="O";S$DEFAULT=FIELD(EST.RL.LAM.PRICE<1,COMP,1>,"!",Y-1)
      END
      SCREEN INPUT;;13
      IF S$VALUE#"END" THEN
        MACRO REPVAL;EST.RL.LAM.PRICE<1,COMP,1>;"!";Y;S$VALUE
      END
    END ELSE
      MACRO REPVAL;EST.RL.LAM.PRICE<1,COMP,1>;"!";Y;UNIT.PR
    END
  NEXT Y
  GOSUB 82000
  GOTO 7000
*
* PAPER GROUP
*
3000 *
  S$DATA(2)<S$SCR> = EST.PROD.OS.USAGE<1,COMP,PROD.REF.NO>
  S$VAL = PROD.REF.NO
  SCREEN DISPLAY;;28
  SCREEN FIELD;;2
  SCREEN INPUT;;2;RETURN
  MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP,CONO:S$VALUE ELSE
    ERRMSG="Invalid Paper Group ID, Try Again!"
    GOSUB 90000
    GOTO 3000
  END
  IF ESTPG.TYPE # "R" THEN
    ERRMSG = "Paper Group ID Not Roll Label, Try Again!"
    GOSUB 90000
    GOTO 3000
  END
  EST.PROD.OS.TYPE<1,COMP> = "R"
  EST.PROD.OS.USAGE<1,COMP,PROD.REF.NO> = S$VALUE
*
* PRODUCT
*
3100 *
  MAT ESTM.REC = ""
  S$DATA(3)<S$SCR> = EST.PROD.OS.PROD<1,COMP,PROD.REF.NO>
  SCREEN FIELD;;3
  S$DEFAULT = ""
  SCREEN INPUT;;3;GOTO 3000
  IF S$VALUE="???" THEN
    MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"R":EST.PROD.OS.USAGE<1,COMP,PROD.REF.NO> ELSE
      MAT ESTM.REC = ""
    END
    S$SCR=S$SCR+1
    CALL EST.PAPER.XREF(CONO,"S","R",EST.PROD.OS.USAGE<1,COMP,PROD.REF.NO>,INV.ID)
    SCREEN CLOSE
    S$SCR=S$SCR-1
    EST.PROD.OS.PROD<1,COMP,PROD.REF.NO>=INV.ID
    SCREEN FORMAT
    GOSUB 80000
    QTY.CURR.REF.NO=''
    GOSUB 50000 ;* REDISPLAY QTYS
    IF INV.ID="" THEN GOTO 3100
  END ELSE
    INV.ID = S$VALUE
  END
  MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"R":EST.PROD.OS.USAGE<1,COMP,PROD.REF.NO> ELSE
    MAT ESTM.REC = ""
  END
  LOCATE INV.ID IN ESTM.PROD<1>,1 SETTING MP ELSE
    ERRMSG="Invalid product ID. Try again! "
    GOSUB 90000
    GOTO 3100
  END
  EST.PROD.OS.PROD<1,COMP,PROD.REF.NO>=INV.ID
*
  IF EST.PROD.OS.DESC<1,COMP,PROD.REF.NO> # ESTM.DESC<1,MP> THEN
    S$DATA(5)<S$SCR> = ESTM.DESC<1,MP>
  END ELSE
    S$DATA(5)<S$SCR> = EST.PROD.OS.DESC<1,COMP,PROD.REF.NO>
  END
*  S$DATA(5)<S$SCR> = EST.PROD.OS.DESC<1,COMP,PROD.REF.NO>
  SCREEN FIELD;;5
  S$O.R="O"
  S$DEFAULT=ESTM.DESC<1,MP>
  SCREEN INPUT;;5;GOTO 3100
  EST.PROD.OS.DESC<1,COMP,PROD.REF.NO>=S$VALUE
*
*-INVENTORY ID
*
3200 *
  S$DATA(4)<S$SCR> = EST.PROD.INV.ID<1,COMP,PROD.REF.NO>
  SCREEN FIELD;;4
  S$DEFAULT = ""
  SCREEN INPUT;;4;GOTO 3100
  BEGIN CASE
    CASE S$VALUE=""
      EST.PROD.INV.ID<1,COMP,PROD.REF.NO>=S$VALUE
    CASE 1
      INV.ID=S$VALUE
      IF INV.ID="???" THEN
        S$SCR=S$SCR+1
        PLINE=EST.PROD.PLINE<1,COMP,PROD.REF.NO>
        BASIS=EST.PROD.BASIS.WT<1,COMP,PROD.REF.NO>
        FIN=EST.PROD.FINISH<1,COMP,PROD.REF.NO>
        CLR=EST.PROD.COLOR<1,COMP,PROD.REF.NO>
        WIDTH=""
        LNGTH=""
        CALL EST.INV.XREF(CONO,"S",PLINE,BASIS,FIN,CLR,WIDTH,LNGTH,INV.ID)
        SCREEN CLOSE
        S$SCR=S$SCR-1
        EST.PROD.INV.ID<1,COMP,PROD.REF.NO>=INV.ID
        SCREEN FORMAT
        GOSUB 80000
        QTY.CURR.REF.NO=''
        GOSUB 50000 ;* REDISPLAY QTYS
        IF INV.ID = "" THEN GOTO 3200
      END
      MATREAD INV.REC FROM INVENTORY,CONO:INV.ID ELSE
        ERRMSG="Invalid inventory item. Try again! "
        GOSUB 90000
        GOTO 3200
      END
      GOSUB 35000
      EST.PROD.INV.ID<1,COMP,PROD.REF.NO>=INV.ID
  END CASE
  GOSUB 5000
  IF EST.PROD.INV.ID<1,COMP,PROD.REF.NO> # "" THEN
    S$DATA(5)<S$SCR> = EST.PROD.OS.DESC<1,COMP,PROD.REF.NO>
    SCREEN FIELD;;5
    S$DEFAULT = INV.DESC
    SCREEN INPUT;;5
    EST.PROD.OS.DESC<1,COMP,PROD.REF.NO> = S$VALUE
  END
*
* WIDTH
*
3300 *
  S$DATA(6)<S$SCR> = EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>
  SCREEN FIELD;;6
  S$DEFAULT = EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>
  SCREEN INPUT;;6;GOTO 3200
  EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO> = S$VALUE
*
* MSI AND UNIT PRICE
*
3400 *
  QTY.CNT = DCOUNT(EST.QTY,VM)
* Calculate msi for all qtys and display for a new product
  IF OPT = 'A' THEN
    FOR Y = 1 TO QTY.CNT
      GOSUB 30000
      TOT.MSI = INT(((EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>*TOT.PC)/1000000)+.9)
      MACRO REPVAL;EST.RL.TOTAL.PC<1,COMP,PROD.REF.NO>;"!";Y;INT(TOT.PC)
      MACRO REPVAL;EST.RL.PAP.MSI<1,COMP,PROD.REF.NO>;"!";Y;TOT.MSI
    NEXT Y
    QTY.CURR.REF.NO = ''
    QTY.REF.NO=1
    PTR.OFFSET = 1
    GOSUB 81500
  END
*
*18484 v
  IF CHANGE.OPT = 'C' THEN
*     THIS IS A CHANGE ONLY ALLOW # OF SCROLL
    START.POS = QTY.REF.NO
    END.POS = QTY.REF.NO + QTY.LINE.COUNT - 1
    IF END.POS > QTY.CNT THEN
      END.POS = QTY.CNT
    END
  END ELSE
    START.POS = 1
    END.POS = QTY.CNT
  END
  CHANGE.OPT=''
  TEMPY=0
  FOR Y = START.POS TO END.POS
**    GOSUB 30000
    TEMPY=TEMPY+1
    IF TEMPY > QTY.LINE.COUNT THEN TEMPY = 1
**    TOT.MSI = INT(((EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>*TOT.PC)/1000000)+.9)
**    MACRO REPVAL;EST.RL.TOTAL.PC<1,COMP,PROD.REF.NO>;"!";Y;INT(TOT.PC)
**    MACRO REPVAL;EST.RL.PAP.MSI<1,COMP,PROD.REF.NO>;"!";Y;TOT.MSI
    TOT.MSI = FIELD(EST.RL.PAP.MSI<1,COMP,PROD.REF.NO>,"!",Y)
    IF PRICE.FLAG = "X" OR EST.PROD.INV.ID<1,COMP,PROD.REF.NO> = "" THEN
      PRICE.BRK.COUNT = DCOUNT(ESTM.END.QTY<1,MP>,SVM)
      FLG=0
      FOR X = 1 TO PRICE.BRK.COUNT UNTIL FLG=1
        TEST.QTY = ESTM.END.QTY<1,MP,X>
        IF TOT.MSI <= TEST.QTY THEN
          UNIT.PR = ESTM.QCOST<1,MP,X>
          FLG=1
        END
      NEXT X
      IF NOT(FLG) THEN
        UNIT.PR = ESTM.QCOST<1,MP,PRICE.BRK.COUNT>
      END
    END
    IF UNIT.PR="" THEN
      IF TEMPY = 1 THEN
        QTY.CURR.REF.NO = ''
        QTY.REF.NO=Y
        GOSUB 50000
        GOSUB 81500
        GOSUB 82000
      END
      S$VAL=Y
      SCREEN FIELD;;8
      IF Y > 1 THEN
        S$O.R="O";S$DEFAULT=FIELD(EST.RL.PAP.PRICE<1,COMP,PROD.REF.NO>,"!",Y-1)
      END
      SCREEN INPUT;;8
      IF S$VALUE#"END" THEN
        MACRO REPVAL;EST.RL.PAP.PRICE<1,COMP,PROD.REF.NO>;"!";Y;S$VALUE
      END
    END ELSE
      MACRO REPVAL;EST.RL.PAP.PRICE<1,COMP,PROD.REF.NO>;"!";Y;UNIT.PR
    END
  NEXT Y
  GOSUB 81000
  RETURN
*
* LOAD PAPER DATA
*
5000 *
  IF EST.PROD.INV.ID<1,COMP,PROD.REF.NO>="" THEN
    EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>=ESTM.WIDTH<1,MP>
    EST.PROD.OS.LENGTH<1,COMP,PROD.REF.NO>=ESTM.LENGTH<1,MP>
    EST.PROD.OS.GRAIN<1,COMP,PROD.REF.NO>=ESTM.GRAIN<1,MP>
    EST.PROD.PLINE<1,COMP,PROD.REF.NO>=ESTM.PLINE<1,MP>
    EST.PROD.BASIS.WT<1,COMP,PROD.REF.NO>=ESTM.BASIS.WT<1,MP>
    EST.PROD.FINISH<1,COMP,PROD.REF.NO>=ESTM.FINISH.CD<1,MP>
    EST.PROD.COLOR<1,COMP,PROD.REF.NO>=ESTM.CLR.CD<1,MP>
    EST.PROD.CALIPER<1,COMP,PROD.REF.NO>=ESTM.CALIPER<1,MP>
    EST.PROD.PPI<1,COMP,PROD.REF.NO>=ESTM.PPI<1,MP>
  END ELSE
    EST.PROD.OS.DESC<1,COMP,PROD.REF.NO>=INV.DESC
    EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>=INV.PAP.WIDTH;* REL 08.0
    EST.PROD.OS.LENGTH<1,COMP,PROD.REF.NO>=INV.PAP.LEN;* REL 08.0
    EST.PROD.OS.GRAIN<1,COMP,PROD.REF.NO>=INV.GRAIN
    EST.PROD.PLINE<1,COMP,PROD.REF.NO>=INV.LINE
    IF NUM(INV.BAS.WT) THEN
      EST.PROD.BASIS.WT<1,COMP,PROD.REF.NO>=INV.BAS.WT/100
    END ELSE
      EST.PROD.BASIS.WT<1,COMP,PROD.REF.NO>=""
    END
    EST.PROD.FINISH<1,COMP,PROD.REF.NO>=INV.PAP.FINSH
    EST.PROD.COLOR<1,COMP,PROD.REF.NO>=INV.COLOR
    IF INV.PPI+0 > 0 THEN
      EST.PROD.PPI<1,COMP,PROD.REF.NO>=INV.PPI
      EST.PROD.CALIPER<1,COMP,PROD.REF.NO>=INT(2000/INV.PPI*1000+0.5)
    END ELSE
      EST.PROD.CALIPER<1,COMP,PROD.REF.NO>=ESTM.CALIPER<1,MP>
      EST.PROD.PPI<1,COMP,PROD.REF.NO>=ESTM.PPI<1,MP>
    END
  END
  EST.PROD.PS.WIDTH<1,COMP,PROD.REF.NO>=EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>
  EST.PROD.PS.LENGTH<1,COMP,PROD.REF.NO>=EST.PROD.OS.LENGTH<1,COMP,PROD.REF.NO>
  EST.PROD.PS.GRAIN<1,COMP,PROD.REF.NO>=EST.PROD.OS.GRAIN<1,COMP,PROD.REF.NO>
  EST.PROD.COATING<1,COMP,PROD.REF.NO>=ESTM.COAT.CD<1,MP>
  EST.PROD.M.WT<1,COMP,PROD.REF.NO>=ESTM.M.WEIGHT<1,MP>
  IF EST.PROD.OS.TYPE<1,COMP>="" THEN
    AC=COUNT(EST.A9B,",")+1
    FOR AP=1 TO AC
      AA=FIELD(EST.A9B,",",AP)
      AA1=FIELD(AA,"-",1)
      AA2=FIELD(AA,"-",2)
      IF AA2="" THEN AA2=AA1
      FOR AAP=AA1 TO AA2
        EST.REC(AAP)<1,COMP,PROD.REF.NO>=""
      NEXT AAP
    NEXT AP
  END
  RETURN
*
*  DELETE PAPER DATA
*
6000 *
  DEL EST.PROD.OS.USAGE<1,COMP,PROD.REF.NO>
  DEL EST.PROD.OS.PROD<1,COMP,PROD.REF.NO>
  DEL EST.PROD.OS.DESC<1,COMP,PROD.REF.NO>
  DEL EST.PROD.OS.WIDTH<1,COMP,PROD.REF.NO>
  DEL EST.PROD.OS.LENGTH<1,COMP,PROD.REF.NO>
  DEL EST.PROD.OS.GRAIN<1,COMP,PROD.REF.NO>
  DEL EST.PROD.PS.WIDTH<1,COMP,PROD.REF.NO>
  DEL EST.PROD.PS.LENGTH<1,COMP,PROD.REF.NO>
  DEL EST.PROD.PS.GRAIN<1,COMP,PROD.REF.NO>
  DEL EST.PROD.BASIS.WT<1,COMP,PROD.REF.NO>
  DEL EST.PROD.COATING<1,COMP,PROD.REF.NO>
  DEL EST.PROD.M.WT<1,COMP,PROD.REF.NO>
  DEL EST.PROD.CALIPER<1,COMP,PROD.REF.NO>
  DEL EST.PROD.PLINE<1,COMP,PROD.REF.NO>
  DEL EST.PROD.COLOR<1,COMP,PROD.REF.NO>
  DEL EST.PROD.FINISH<1,COMP,PROD.REF.NO>
  DEL EST.PROD.INV.ID<1,COMP,PROD.REF.NO>
  DEL EST.PROD.PPI<1,COMP,PROD.REF.NO>
  DEL EST.RL.TOTAL.PC<1,COMP,PROD.REF.NO>
  DEL EST.RL.PAP.MSI<1,COMP,PROD.REF.NO>
  DEL EST.RL.PAP.PRICE<1,COMP,PROD.REF.NO>
  IF EST.PROD.OS.USAGE<1,COMP> = "" THEN
    DEL EST.PROD.OS.TYPE<1,COMP>
  END
  RETURN
*
* OPTIONS
*
7000 *
  MAINT.FLAG = "X"
  SCREEN FIELD;;27
  SCREEN INPUT;;27;RETURN
  BEGIN CASE
    CASE S$VALUE = 6
      IF EST.RL.ORD.STK<1,COMP,1> # "Y" THEN
        ERRMSG = "Order Stock must be Y to change this field"
        GOSUB 90000
        GOTO 7000
      END
    CASE S$VALUE = 7
      ACTION = "M"
      GOSUB 20000
      GOTO 7000
    CASE S$VALUE = "N"
      STATUS = "P3"
      GOTO 99999
    CASE S$VALUE[1,1] = "P"
      PNUM = S$VALUE[2,1]
      IF ACTION = "M" AND EST.DEPT = "" AND PNUM > "3" THEN
        ERRMSG = "Invalid option while in entry mode. Try again!"
        GOSUB 90000
      END ELSE
        STATUS = S$VALUE
        GOTO 99999
      END
    CASE S$VALUE = "E" OR S$VALUE = "END"
      EST.PROD.WEB.CNT<1,COMP> = DCOUNT(EST.PROD.OS.USAGE<1,COMP>,SVM)
      GOTO 99999
    CASE S$VALUE[1,1] = 'S'
      SCR.V = S$VALUE[2,99]
      QTY.CNT = DCOUNT(EST.QTY,VM)
      BEGIN CASE
        CASE SCR.V = '' OR SCR.V = 'F'
          QTY.REF.NO = QTY.CURR.REF.NO + QTY.LINE.COUNT
          QTY.REF.NO = 1+INT((QTY.REF.NO-1)/QTY.LINE.COUNT)*QTY.LINE.COUNT
          IF QTY.REF.NO > QTY.CNT THEN
            QTY.REF.NO = 1
          END
        CASE SCR.V = 'R'
          QTY.REF.NO = QTY.CURR.REF.NO - QTY.LINE.COUNT
          IF QTY.REF.NO < 1 THEN
            QTY.REF.NO = 1
          END
        CASE SCR.V = 'T'
          QTY.REF.NO = 1
        CASE SCR.V = 'B'
          QTY.REF.NO = QTY.CNT
        CASE SCR.V = 'N'
          SCREEN FIELD;;32
          S$MINV = 1
          S$MAXV = QTY.CNT
          SCREEN INPUT;;32
          IF S$VALUE = 'E' OR S$VALUE = 'END' THEN GOTO 7000
          QTY.REF.NO = 1+INT((S$VALUE-1)/QTY.LINE.COUNT)*QTY.LINE.COUNT
      END CASE
      PTR.OFFSET = QTY.REF.NO
      GOSUB 50000 ;* REDISPLAY QUANTITY TABLE
      GOSUB 81500 ;* REDISPLAY PRODUCT MSI AND PRICE
      GOSUB 82000 ;* REDISPLAY LAMINATE MSI AND PRICE
      GOTO 7000 ;* START PROMPT OVER
  END CASE
  ON S$VALUE GOTO 1000,1100,1200,1500,1700,1800,1900,2500,2600
*
*---- PRODUCT INFORMATION
*
20000 *
  CNT = DCOUNT(EST.PROD.OS.USAGE<1,COMP>,SVM)
  MORE = 1
  LOOP
    IF OPTION = "A" THEN
      OPT = "A"
    END ELSE
      S$DATA(30)<S$SCR>=""
      SCREEN FIELD;;30
      SCREEN INPUT;;30
      OPT=S$VALUE
    END
    BEGIN CASE
      CASE OPT="A" AND CNT < 99
        DONE=0
        FOR PROD.REF.NO=CNT+1 TO 99 UNTIL DONE
          GOSUB 81000
          GOSUB 3000
          IF S$VALUE="END" THEN
            DONE=1
            GOSUB 6000
          END ELSE
            CNT=CNT+1
          END
        NEXT PROD.REF.NO
        CNT = DCOUNT(EST.PROD.OS.USAGE<1,COMP>,SVM)
        PROD.REF.NO=CNT
        GOSUB 81000
        OPTION = ""
      CASE OPT="C" AND CNT > 0
        GOSUB 40000
        IF S$VALUE#"" AND S$VALUE#"END" THEN
          PROD.REF.NO=S$VALUE
          CHANGE.OPT='C'
          GOSUB 3000
        END
      CASE OPT="D" AND CNT > 0
        GOSUB 40000
        IF S$VALUE#"" AND S$VALUE#"END" THEN
          PROD.REF.NO=S$VALUE
          GOSUB 6000
          CNT=CNT-1
          IF PROD.REF.NO > CNT THEN PROD.REF.NO=PROD.REF.NO-1
          IF PROD.REF.NO < 1 THEN PROD.REF.NO = 1
          GOSUB 81000
        END
      CASE OPT="S"
        PROD.REF.NO=PROD.CURR.REF.NO+PROD.LINE.COUNT
        IF PROD.REF.NO > CNT THEN PROD.REF.NO=1
        GOSUB 81000
      * T25978 v
      CASE OPT = 'SR'
        PROD.REF.NO = PROD.CURR.REF.NO - PROD.LINE.COUNT
        IF PROD.REF.NO < 1 THEN PROD.REF.NO = 1
        GOSUB 81000
      CASE OPT = 'ST'
        PROD.REF.NO = 1
        GOSUB 81000
      CASE OPT = 'SB'
        PROD.REF.NO = CNT
        GOSUB 81000
      * T25978 ^
      CASE OPT="" OR OPT="END" OR OPT="E"
        MORE=0
    END CASE
  WHILE MORE DO REPEAT
  RETURN
*
* EQUATIONS TO FIGURE MSI
*
30000 *
  IF EST.RL.DIE.REPEAT<1,COMP,1> # "" AND EST.RL.DIE.NO.ACR<1,COMP,1> # "" AND EST.RL.DIE.NO.ARD<1,COMP,1> # "" THEN
    EQTY = EST.QTY<1,Y>
    IF NUM(EST.PROD.BIND.SPOIL<1,COMP,1>) THEN
      FIN.SPOIL = EQTY*EST.PROD.BIND.SPOIL<1,COMP,1>/100
    END ELSE
      MATREAD BST.REC FROM ESTIMATE.BIND.SPOIL,CONO:EST.PROD.BIND.SPOIL<1,COMP,1> ELSE
        ERRMSG = "Invalid bindery spoilage table"
        GOSUB 90000
        GOTO 7000
      END
      LOCATE EQTY IN BST.QTY<1>,1 BY "AR" SETTING BP ELSE NULL
      BEGIN CASE
        CASE BP = 1
          BSPCT = BST.PCT<1,BP>
        CASE BP > COUNT(BST.QTY,VM) + 1
          BSPCT = BST.PCT<1,BP-1>
        CASE BST.EXTR = "Y"
          BSPCT = INT(BST.PCT<1,BP>-(BST.QTY<1,BP>-EQTY)/(BST.QTY<1,BP>-BST.QTY<1,BP-1>)*(BST.PCT<1,BP>-BST.PCT<1,BP-1>)+0.5)
        CASE 1
          BSPCT = BST.PCT<1,BP>
      END CASE
      FIN.SPOIL = INT(EQTY*(BSPCT/10000)+0.99)
      IF FIN.SPOIL < BST.MIN.QTY THEN FIN.SPOIL = BST.MIN.QTY
    END
    VAR1 = EST.PROD.INIT.MR.CNT<1,COMP,1>*EST.PROD.INIT.MR.IMP<1,COMP,1>
    VAR2 = EST.PROD.SUBS.MR.CNT<1,COMP,1>*EST.PROD.SUBS.MR.IMP<1,COMP,1>
    MR.PC = VAR1 + VAR2
    VAR1 = EST.RL.DIE.REPEAT<1,COMP,1>/10000
    VAR2 = EST.RL.DIE.NO.ACR<1,COMP,1>*EST.RL.DIE.NO.ARD<1,COMP,1>
    BASE.PC = INT(EQTY/VAR2*VAR1/10+0.99)
    FIN.PC = INT(FIN.SPOIL/VAR2*VAR1/10+0.99)
    RUN.PC = BASE.PC + FIN.PC
    IF NUM(EST.PROD.PRESS.SPOIL<1,COMP,1>) THEN
      PR.SPOIL.PC = RUN.PC*EST.PROD.PRESS.SPOIL<1,COMP,1>/100
    END ELSE
      MATREAD PST.REC FROM ESTIMATE.PRESS.SPOIL,CONO:EST.PROD.PRESS.SPOIL<1,COMP,1> ELSE
        ERRMSG = "Invalid press spoilage table."
        GOSUB 90000
        GOTO 7000
      END
      LOCATE RUN.PC IN PST.QTY<1>,1 BY "AR" SETTING P ELSE NULL
      BEGIN CASE
        CASE P = 1
          PSPCT = PST.PCT<1,P>
        CASE P > COUNT(PST.QTY,VM) + 1
          PSPCT = PST.PCT<1,P-1>
        CASE PST.EXTR = "Y"
          PSPCT = INT(PST.PCT<1,P>-(PST.QTY<1,P>-RUN.PC)/(PST.QTY<1,P>-PST.QTY<1,P-1>)*(PST.PCT<1,P>-PST.PCT<1,P-1>)+0.5)
        CASE 1
          PSPCT = PST.PCT<1,P>
      END CASE
      PR.SPOIL.PC = INT(RUN.PC*PSPCT/10000+0.99)
    END
    TOT.PC = MR.PC + RUN.PC + PR.SPOIL.PC
  END ELSE
    TOT.PC = 0
  END
  RETURN
*
*---- GET INVENTORY PRICE
*
35000 *
  ICOST = ""
  WCNT = DCOUNT(INV.WHSE.CODE,VM)
  FOR WP = 1 TO WCNT
    WHSE = INV.WHSE.CODE<1,WP>
    IWH.ID=CONO:INV.ID:"!":WHSE
    MATREAD IWH.REC FROM INV.WHSE,IWH.ID ELSE
      MAT IWH.REC = ""
    END
    SCOST = ""
    BEGIN CASE
      CASE PRICE.FLAG = "L"
        IF IWH.LIST.COST # "" THEN
          SCOST = IWH.LIST.COST
        END
      CASE PRICE.FLAG = "A"
        IF IWH.AVG.COST # "" THEN
          SCOST = IWH.AVG.COST
        END
      CASE PRICE.FLAG = "S"
        IF IWH.STD.COST # "" THEN
          SCOST = IWH.STD.COST
        END
      CASE PRICE.FLAG = "R"
        DIV.POS='' ; FISCAL.FLAG='IC' ; TRAN.PERIOD=''
        CALL GET.LAST.COST(IWH.ID,MAT IWH.REC,WAREHOUSE,CONTROL,ERR.FLG,ERRMSG,DIV.POS,FISCAL.FLAG,TRAN.PERIOD,LAST.PRICE)
        BEGIN CASE
          CASE ERR.FLG=''
            SCOST=LAST.PRICE
          CASE ERR.FLG="-1"
            GOSUB 90000
            CONTINUE
          CASE ERR.FLG='-2'
            GOSUB 93000
        END CASE
    END CASE
    BEGIN CASE
      CASE SCOST = ""
      CASE ICOST = ""
        ICOST = SCOST
      CASE SCOST > ICOST
        ICOST = SCOST
    END CASE
  NEXT WP
  UNIT.PR = ICOST
  RETURN
*
* GET LINE NUMBER
*
40000 *
  S$DATA(31)<S$SCR> = ""
  SCREEN FIELD;;31
  S$MINV = PROD.CURR.REF.NO
  S$MAXV = S$MINV + PROD.LINE.COUNT
  IF S$MAXV > CNT THEN S$MAXV = CNT
  SCREEN INPUT;;31
  IF S$VALUE = "" THEN OPT = "END"
  RETURN
*
*  DISPLAY QUANTITY
*
50000 *
***PRINT @(0,23)
***PRINT EST.QTY
***PRINT "QTY.REF.NO":QTY.REF.NO
****PRINT QTY.CURR.REF.NO:"QYT.CURR.REF.NO"
****INPUT XXX
  QTY.START.REF.NO=QTY.REF.NO
  PTR.OFFSET = QTY.START.REF.NO
  IF QTY.START.REF.NO=QTY.CURR.REF.NO THEN RETURN
  QTY.CURR.REF.NO=QTY.START.REF.NO
  S$VAL=QTY.START.REF.NO
  S$CNT=DCOUNT(S$DATA(25)<S$SCR>,VM)
  SCREEN MULTI;;C;26;25
  RETURN
*
*  DISPLAY SCREEN
*
80000 *
  S$DATA(1)<S$SCR> = DATE()
  S$DATA(16)<S$SCR> = EST.PROD.BIND.SPOIL<1,COMP,1>
  S$DATA(17)<S$SCR> = EST.PROD.PRESS.SPOIL<1,COMP,1>
  S$DATA(18)<S$SCR> = EST.PROD.INIT.MR.CNT<1,COMP,1>
  S$DATA(19)<S$SCR> = EST.PROD.SUBS.MR.CNT<1,COMP,1>
  S$DATA(20)<S$SCR> = EST.PROD.INIT.MR.IMP<1,COMP,1>
  S$DATA(21)<S$SCR> = EST.PROD.SUBS.MR.IMP<1,COMP,1>
  S$DATA(14)<S$SCR> = EST.RL.ORD.STK<1,COMP,1>
  S$DATA(15)<S$SCR> = EST.RL.ORD.REQ<1,COMP,1>
  S$DATA(22)<S$SCR> = EST.RL.LAM<1,COMP,1>
  S$DATA(23)<S$SCR> = EST.RL.LAM.USAGE<1,COMP,1>
  S$DATA(24)<S$SCR> = EST.RL.LAM.PROD<1,COMP,1>
  S$DATA(9)<S$SCR> = EST.RL.LAM.INV<1,COMP,1>
  S$DATA(10)<S$SCR> = EST.RL.LAM.DESC<1,COMP,1>
  S$DATA(11)<S$SCR> = EST.RL.LAM.WIDTH<1,COMP,1>
  S$DATA(25)<S$SCR> = EST.QTY
  S$DATA(2)<S$SCR> = ""
  S$DATA(3)<S$SCR> = ""
  S$DATA(4)<S$SCR> = ""
  S$DATA(5)<S$SCR> = ""
  S$DATA(6)<S$SCR> = ""
  S$DATA(7)<S$SCR> = ""
  S$DATA(8)<S$SCR> = ""
  S$DATA(12)<S$SCR> = ""
  S$DATA(13)<S$SCR> = ""
  SCREEN DISPLAY;;ALL
  GOSUB 81000
  GOSUB 82000
  RETURN
81000 *
  PROD.CURR.REF.NO = PROD.REF.NO
  PRD.CNT = DCOUNT(EST.PROD.OS.USAGE<1,COMP>,SVM)
  IF PRD.CNT = 0 THEN PCNT = 1 ELSE PCNT = PRD.CNT
  FOR I = 1 TO PCNT
    S$DATA(2)<S$SCR,I> = EST.PROD.OS.USAGE<1,COMP,I>
    S$DATA(3)<S$SCR,I> = EST.PROD.OS.PROD<1,COMP,I>
    S$DATA(4)<S$SCR,I> = EST.PROD.INV.ID<1,COMP,I>
    S$DATA(5)<S$SCR,I> = EST.PROD.OS.DESC<1,COMP,I>
    S$DATA(6)<S$SCR,I> = EST.PROD.OS.WIDTH<1,COMP,I>
  NEXT I
  S$VAL = PROD.REF.NO
  S$CNT = PRD.CNT
  SCREEN MULTI;;C;28;2;3;4;5;6
*
*---- Scroll Paper MSI and Price
81500 *
*
  MSI.CNT = DCOUNT(EST.RL.PAP.MSI<1,COMP,PROD.REF.NO>,"!")
  IF MSI.CNT = 0 THEN MCNT = 1 ELSE MCNT = MSI.CNT
  FOR I = 1 TO MCNT
    S$DATA(7)<S$SCR,I> = FIELD(EST.RL.PAP.MSI<1,COMP,PROD.REF.NO>,"!",I)
    S$DATA(8)<S$SCR,I> = FIELD(EST.RL.PAP.PRICE<1,COMP,PROD.REF.NO>,"!",I)
  NEXT I
  S$VAL = PTR.OFFSET
  S$CNT = MSI.CNT
  SCREEN MULTI;;C;7;8
  RETURN
*
*---- Scroll Laminate MSI and Price
82000 *
  MSI.CNT = DCOUNT(EST.RL.LAM.MSI<1,COMP,1>,"!")
  IF MSI.CNT = 0 THEN MCNT = 1 ELSE MCNT = MSI.CNT
  FOR I = 1 TO MCNT
    S$DATA(12)<S$SCR,I> = FIELD(EST.RL.LAM.MSI<1,COMP,1>,"!",I)
    S$DATA(13)<S$SCR,I> = FIELD(EST.RL.LAM.PRICE<1,COMP,1>,"!",I)
  NEXT I
  S$VAL = PTR.OFFSET
  S$CNT = MSI.CNT
  SCREEN MULTI;;C;12;13
  RETURN
*
*-ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*-END OF PROGRAM
*
99999 *
  SCREEN CLEAR;;D
  RETURN
END
