   SUBROUTINE EST.RL.PP.COST.PSUB (CONO, EST.KEY, DSEL, CSEL, QSEL)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RL.PP.COST.QSUB
*
* AUTHOR   - Graham Jarvis, Pacrim Software Pty Ltd
*
* DATE     - 12 May 1993
*
* DESCRIPTION
*
* Summarises Estimate Pre-Press Data Using GRP Prefix Codes
* PL = Plates, DI=Dies, SC=Screens, PR=Proofs
*
* MODIFIED - G Jarvis, 12 May 93
*
* T27924 cmykleb 03/23/2004 * Print this section by component.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   COST=''
   SALE=''
   TCOST=''
   TSALE=''
   GRP.DESC=''
   GRP.TYPE=''
   GRP.QTY=''
   READ PPDEPT FROM CONTROL,CONO:'EST.RES.PRE.PRESS' ELSE PPDEPT=''
*
*---- MAIN PROCESSING
*
100 *
   FOR CP = 1 TO EST.COMPONENT.CNT ; * T27924
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         IF FIELD(ESTD.ID,"!",2) # CP THEN GO 190 ; * T27924
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
         LOCATE ESTD.DEPT IN PPDEPT<1>,1 SETTING PTR ELSE PTR = 0
         IF NOT(PTR) THEN GOTO 190 ;* Only Pre-Press
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>[1,1]
            IF TYPE = "G" THEN
               GRP.TYPE = ESTD.OPV<1,TP>[1,2]
               GRP.DESC = ESTD.DESC<1,TP>
               GRP.QTY = ESTD.QTY<1,TP>
               GRP.QTY = INT(GRP.QTY/100)
               GOTO 180
            END
            COST = ESTD.COST<1,TP>
            SALE = ESTD.TSALE<1,TP>
            TCOST = TCOST + COST
            TSALE = TSALE + SALE
            IF ESTD.TYPE<1,TP+1>[1,1]='G' OR ESTD.TYPE<1,TP+1>[1,1]='' THEN
*T27924 v
*           PRINT GRP.DESC "L#35":OCONV(GRP.QTY,'MD0') "R#5":OCONV(TCOST,'MD2,') "R#12":OCONV(TSALE,'MD2,') "R#12"
               PRINT GRP.DESC "L#35":OCONV(CP,'MD0') "R#5":OCONV(TCOST,'MD2,') "R#12":OCONV(TSALE,'MD2,') "R#12"
*T27924 ^
               TSALE=''
               TCOST=''
            END
180      NEXT TP
190   NEXT DP
*T27924 v
      PRINT
   NEXT CP
*T27924 ^
   RETURN
END
