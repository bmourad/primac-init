      SUBROUTINE EST.FORMULA.Q953 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q953
*
* AUTHOR   - SARA CRISMON, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/24/91
*
* DESCRIPTION
*
* This formula is used calculate the quantity of ink required
* for Roll-Label Estimating
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RL
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
      IF EST.PROD.INK.ID<1,COMP,MPTR> = "" THEN
         TEMP.QTY = 0
         GOTO 99999
      END
*
      TEMP.QTY = ""
      TYP = 0
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
      IP1 = TEMP.FVAR1
      IP2 = TEMP.FVAR2
*
*---- MAIN PROCESSING
*
      LNGTH = INT(EQTY*(EST.RL.DIE.REPEAT<1,COMP,1>/10000)+0.99)
      NO.UP = EST.RL.DIE.NO.ACR<1,COMP,1>*EST.RL.DIE.NO.ARD<1,COMP,1>
      WIDTH = EST.PROD.OS.WIDTH<1,COMP,1>/10000
      IF LNGTH = 0 OR NO.UP = 0 OR WIDTH = 0 THEN GOTO 99999
*
      COATING = EST.PROD.COATING<1,COMP,MPTR>
      INK.SIDES = FIELD(EST.PROD.INK.SIDES<1,COMP,MPTR>,"!",IP1)
      INK.COVER = FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",IP1)
*
      BASE.INCHES = INT(LNGTH / NO.UP + 0.99)
      BASE.PC = INT(BASE.INCHES / 10 + 0.99)
      PAPER.SQ.IN = (BASE.PC * 10 * WIDTH)    ;* SQUARE INCHES PER SIDE
      INK.SQ.IN = INT(PAPER.SQ.IN/1000000 * (INK.COVER / 100) + 0.5)    ;* SQUARE INCHES OF INK PER SIDE
*
      BEGIN CASE
      CASE COATING = 1 AND INK.SIDES = 2
         TEMP.QTY = (INK.SQ.IN / ESTM.COVER.C<1,IP2>) + (INK.SQ.IN / ESTM.COVER.U<1,IP2>)
      CASE COATING+0 = 0
         TEMP.QTY = INK.SQ.IN * INK.SIDES / ESTM.COVER.U<1,IP2>
      CASE 1
         TEMP.QTY = INK.SQ.IN * INK.SIDES / ESTM.COVER.C<1,IP2>
      END CASE
*
      TEMP.QTY = INT(TEMP.QTY * 100 + 0.99) / 100
*     TEMP.QTY = INT(INK.SQ.IN / 1000000 * INK.SIDES * 100 + 0.99)
*     TEMP.QTY = TEMP.QTY / 100
      MIN.QTY = ESTM.MIN.QTY<1,IP2> / 100
      IF TEMP.QTY < MIN.QTY THEN TEMP.QTY = MIN.QTY
99999 *
      TEMP.VSALE = "Y"
      RETURN
      END
