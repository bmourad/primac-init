   SUBROUTINE EST.FORMULA.Q777 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q777
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 02/26/86
*
* DESCRIPTION
*
* This formula is used to calculate run time requirements for specified
* miscellaneous operations.
*
*T27924 cmykleb 03/12/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.QTY = ""
   TYP = 0
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*T27924 v
   CQTY = EST.COMP.QTY<1,COMP,QPTR>
   BEGIN CASE
      CASE CQTY=""
         COMPQTY = EQTY
      CASE (CQTY[1,1]="+" OR CQTY[1,1]="-") AND CQTY[LEN(CQTY),1]="%"
         COMPQTY = INT(EQTY * (1 + (CQTY[1,LEN(CQTY)-1]/100)) + 0.5)
      CASE CQTY[1,1]="+" OR CQTY[1,1]="-"
         COMPQTY = EQTY + CQTY
      CASE 1
         COMPQTY = CQTY
   END CASE
   IF COMPQTY < 0 THEN COMPQTY = 0
*T27924 ^
10*
   IF QPTR = 1 AND ACTION # "R" THEN
      IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
*T27924 v
      X=0;Y=23;TYP=13;MAXL=4;MINV=1;MAXV=9999;JUSTIFY="L"
*T27924 ^
      BEGIN CASE
         CASE TEMP.FVAR1 # ""
            DEFAULT = TEMP.FVAR1
         CASE 1
            DEFAULT = 1
      END CASE
      IF DEFAULT = "" THEN
         PMSG = "Number up ?"
      END ELSE
         O.R = "O"
         PMSG = "Number up (Default = ":DEFAULT:") ?"
      END
      CALL EDIT.SUB1 (MAT EDIT.COM)
      IF VALUE = "END" THEN RETURN
      IF VALUE = "" THEN GOTO 10
      NUMBER.UP = VALUE
      TEMP.FVAR1 = VALUE
   END ELSE
      NUMBER.UP = TEMP.FVAR1
   END
*
*---- MAIN PROCESSING
*
   PROD.QTY = COMPQTY + FIELD(EST.PROD.BS.REQ<1,COMP,1>,"!",QPTR) ;*T27924
   IF NUMBER.UP + 0 = 0 THEN
      TEMP.QTY = 0
   END ELSE
      TEMP.QTY = INT(PROD.QTY / NUMBER.UP + 0.99)
   END
   TEMP.VSALE = "Y"
   RETURN
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
