      SUBROUTINE EST.COMP.DEPT.SUB (CONO, EST.KEY, DSEL, CSEL, QSEL, MAT DSUM.REC)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.COMP.DEPT.SUB
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/14/86
*
* DESCRIPTION
*
* This subroutine summarizes estimate data by component, department and
* type.  Summarization may occur for a single department or "ALL", a single
* component or "ALL", and a specified order quantity.
*
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>EST.COMP.DEPT.SUM
*COPY>PMC.CPYLIB>DEPARTMENT
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>CPYLIB>FILE.VARS
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
      MAT DSUM.REC = ""
*
*---- MAIN PROCESSING
*
100*
      DCNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
      FOR DP = 1 TO DCNT
         ESTD.ID = EST.DEPT.COMP<1,DP>
         IF DSEL # "ALL" AND DSEL # EST.DEPT<1,FIELD(ESTD.ID,"!",1)> THEN GOTO 190
         IF CSEL # "ALL" AND CSEL # FIELD(ESTD.ID,"!",2) THEN GOTO 190
         IF QSEL # FIELD(ESTD.ID,"!",3) THEN GOTO 190
         MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.KEY:"!":ESTD.ID ELSE GOTO 190
         DEPT = ESTD.DEPT
         COMP = FIELD(ESTD.ID,"!",2)
         P1 = COMP
         DSUM.COMP<1,P1> = COMP
         DSUM.COMP.DESC<1,P1> = EST.PROD.DESC<1,COMP>
         LOCATE DEPT IN DSUM.DEPT<1,P1>,1 BY "AL" SETTING P2 ELSE
            IF P2 <= COUNT(DSUM.DEPT<1,P1>,SM) + (DSUM.DEPT<1,P1> # "") THEN
               FOR N = 21 TO 40
                  DSUM.REC(N) = INSERT(DSUM.REC(N),1,P1,P2,"")
               NEXT N
            END
            DSUM.DEPT<1,P1,P2> = DEPT
            LOCATE DEPT IN EST.DEPT<1>,1 SETTING PP ELSE NULL
            DSUM.DEPT.DESC<1,P1,P2> = EST.DEPT.DESC<1,PP>
         END
         TCNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
         FOR TP = 1 TO TCNT
            TYPE = ESTD.TYPE<1,TP>[1,1]
            IF TYPE = "G" THEN GOTO 180
            HRS = ESTD.HRS<1,TP>
            COST = ESTD.COST<1,TP>
            SALE = ESTD.TSALE<1,TP>
            BEGIN CASE
            CASE TYPE = "L"
               DSUM.COMP.LABOR.HRS<1,P1> = DSUM.COMP.LABOR.HRS<1,P1> + HRS
               DSUM.COMP.LABOR.COST<1,P1> = DSUM.COMP.LABOR.COST<1,P1> + COST
               DSUM.COMP.LABOR.SALE<1,P1> = DSUM.COMP.LABOR.SALE<1,P1> + SALE
               DSUM.DEPT.LABOR.HRS<1,P1,P2> = DSUM.DEPT.LABOR.HRS<1,P1,P2> + HRS
               DSUM.DEPT.LABOR.COST<1,P1,P2> = DSUM.DEPT.LABOR.COST<1,P1,P2> + COST
               DSUM.DEPT.LABOR.SALE<1,P1,P2> = DSUM.DEPT.LABOR.SALE<1,P1,P2> + SALE
            CASE TYPE = "M" OR TYPE = "I"
               DSUM.COMP.MATL.COST<1,P1> = DSUM.COMP.MATL.COST<1,P1> + COST
               DSUM.COMP.MATL.SALE<1,P1> = DSUM.COMP.MATL.SALE<1,P1> + SALE
               DSUM.DEPT.MATL.COST<1,P1,P2> = DSUM.DEPT.MATL.COST<1,P1,P2> + COST
               DSUM.DEPT.MATL.SALE<1,P1,P2> = DSUM.DEPT.MATL.SALE<1,P1,P2> + SALE
            CASE TYPE = "P"
               DSUM.COMP.OSP.COST<1,P1> = DSUM.COMP.OSP.COST<1,P1> + COST
               DSUM.COMP.OSP.SALE<1,P1> = DSUM.COMP.OSP.SALE<1,P1> + SALE
               DSUM.DEPT.OSP.COST<1,P1,P2> = DSUM.DEPT.OSP.COST<1,P1,P2> + COST
               DSUM.DEPT.OSP.SALE<1,P1,P2> = DSUM.DEPT.OSP.SALE<1,P1,P2> + SALE
            CASE TYPE = "S"
               DSUM.COMP.SHIP.COST<1,P1> = DSUM.COMP.SHIP.COST<1,P1> + COST
               DSUM.COMP.SHIP.SALE<1,P1> = DSUM.COMP.SHIP.SALE<1,P1> + SALE
               DSUM.DEPT.SHIP.COST<1,P1,P2> = DSUM.DEPT.SHIP.COST<1,P1,P2> + COST
               DSUM.DEPT.SHIP.SALE<1,P1,P2> = DSUM.DEPT.SHIP.SALE<1,P1,P2> + SALE
            CASE TYPE = "O"
               DSUM.COMP.MISC.COST<1,P1> = DSUM.COMP.MISC.COST<1,P1> + COST
               DSUM.COMP.MISC.SALE<1,P1> = DSUM.COMP.MISC.SALE<1,P1> + SALE
               DSUM.DEPT.MISC.COST<1,P1,P2> = DSUM.DEPT.MISC.COST<1,P1,P2> + COST
               DSUM.DEPT.MISC.SALE<1,P1,P2> = DSUM.DEPT.MISC.SALE<1,P1,P2> + SALE
            END CASE
180      NEXT TP
190   NEXT DP
      RETURN
   END
