      SUBROUTINE EST.STD.XREF(CONO, XCATG, SEL.EST, SEL.CMP)
*********************************************************************
*
* REVISION - [08.1B]
*
* PROGRAM  - EST.STD.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/01/91
*
* DESCRIPTION
*
* This program is used to display all standard estimates with the
* specified category code for selection purposes.
*
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      SCREEN DEFINE;EST.STD.XREF
      SCREEN FORMAT
      SCREEN COUNT;;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      MAXLINES = 99
      LINE.CNT = 0
      REF.NO = ""
      CURR.REF.NO = ""
      SEL.EST = ""
      SEL.CMP = ""
*
*---- MAIN PROCESSING
*
100 *
      S$DATA(1)<S$SCR> = DATE()
      SCREEN DISPLAY;;1
      IF XCATG = "" THEN
         READV XREC FROM CONTROL, CONO:"STD.XREF",1 ELSE XREC = ""
      END ELSE
         MATREAD JCATG.REC FROM JOB.CATEGORY, CONO:XCATG ELSE
            MAT JCATG.REC = ""
         END
         READV XREC FROM ESTIMATE.XREF, CONO:XCATG,1 ELSE XREC = ""
      END
      GOSUB 80000
      GOSUB 81000
      REF.NO = 1
      CURR.REF.NO = ""
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
500 *
      SCREEN FIELD;;21
      SCREEN INPUT;;21
      OPT = S$VALUE
510 *
      BEGIN CASE
      CASE OPT = "E" OR OPT = "END"
         GOTO 99999
      CASE NUM(OPT) AND OPT # ""
         MINV = CURR.REF.NO
         MAXV = MINV + LINE.COUNT - 1
         IF MAXV > LINE.CNT THEN MAXV = LINE.CNT
         IF OPT < MINV OR OPT > MAXV THEN
            ERRMSG = "** OUT OF RANGE **"
            GOSUB 90000
         END ELSE
            SEL.EST = STDKEY<1,OPT>
            SEL.CMP = STDCMP<1,OPT>
            GOTO 99999
         END
**      CASE OPT = "R"
**         GOSUB 600
**         IF S$VALUE # "" AND S$VALUE # "END" THEN
**            REF.NO = S$VALUE
**            EST = STDKEY<1,REF.NO>
**            COMP = STDCMP<1,REF.NO>
**            IF COMP = "" THEN
**               ERRMSG = "Must select component line for review."
**               GOSUB 90000
**            END ELSE
**               S$SCR = S$SCR + 1
**               CALL EST.PROD.INQ.1(CONO, EST, COMP)
**               S$SCR = S$SCR - 1
**               SCREEN FORMAT
**               GOSUB 80050
**               CURR.REF.NO = ""
**               GOSUB 50000
**            END
**         END
      CASE OPT = "S" OR OPT = "SF"
         REF.NO = CURR.REF.NO + LINE.COUNT
         IF REF.NO > LINE.CNT THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "SR"
         REF.NO = CURR.REF.NO - LINE.COUNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      CASE OPT = "ST"
         REF.NO = 1
         GOSUB 50000
      CASE OPT = "SB"
         REF.NO = LINE.CNT
         IF REF.NO < 1 THEN REF.NO = 1
         GOSUB 50000
      END CASE
      GOTO 500
*
*---- GET LINE NUMBER
*
600*
      SCREEN FIELD;;22
      S$MINV = CURR.REF.NO
      S$MAXV = S$MINV + LINE.COUNT - 1
      IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
      SCREEN INPUT;;22
      RETURN
*
*---- MULTI-LINE SCROLL ROUTINE
*
50000 *
      START.REF.NO = 1 + INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = LINE.CNT
      SCREEN MULTI;;C;11;12;13;14
      RETURN
*
*---- LOAD SCREEN DATA
*
80000 *
      S$DATA(2)<S$SCR>=XCATG
      S$DATA(3)<S$SCR>=JCATG.DESC
80050 *
      SCREEN DISPLAY;;ALL
      RETURN
*
*---- LOAD SCREEN DATA (MULTI-LINE)
*
81000 *
      STDKEY = ""
      STDEST = ""
      STDCMP = ""
      STDDSC = ""
      LPTR = 0
      XCNT = DCOUNT(XREC,VM)
      FOR XPTR = 1 TO XCNT
         READV ESTREC FROM ESTIMATE, CONO:XREC<1,XPTR>,32 ELSE
            ESTREC = ""
         END
         IF ESTREC # "" THEN
            LPTR = LPTR + 1
            STDKEY<1,LPTR> = XREC<1,XPTR>
            STDEST<1,LPTR> = XREC<1,XPTR>
            STDDSC<1,LPTR> = ESTREC
            READV CDESC FROM ESTIMATE, CONO:XREC<1,XPTR>,69 ELSE
               CDESC = ""
            END
            DCNT = DCOUNT(CDESC,VM)
            FOR DPTR = 1 TO DCNT
               LPTR = LPTR + 1
               STDKEY<1,LPTR> = XREC<1,XPTR>
               STDCMP<1,LPTR> = DPTR
               STDDSC<1,LPTR> = CDESC<1,DPTR>
            NEXT DPTR
         END
      NEXT XPTR
      LINE.CNT = LPTR
      S$DATA(12)<S$SCR> = STDEST
      S$DATA(13)<S$SCR> = STDCMP
      S$DATA(14)<S$SCR> = STDDSC
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000 *
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999 *
      SCREEN CLEAR;;D
      RETURN
      END
