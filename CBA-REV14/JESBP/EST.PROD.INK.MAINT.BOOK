  SUBROUTINE EST.PROD.INK.MAINT (CONO,ACTION,OPTION,COMP,MPTR,COLOR.REF.NO,COLOR.CURR.REF.NO)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.PROD.INK.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/21/86
*
* DESCRIPTION
*
* This subroutine is used to maintain the ink(s) to be used on the job as
* defined on the product definition screen.
*
* MOD TASK 18262 RKB 10/27/94 INCREASE ESTIMATE QUANTITIES TO 99
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
* ----- THIS CODE ONLY IN PLACE FOR TESTING
*
*---- INITIALIZATION
*
*  THE LINE BELOW NEED TO BE UNCOMMENTED AFTER TESTING
  SCREEN DEFINE;EST.PROD.MAINT.BOOK
  SCREEN COUNT;EST.PROD.MAINT.BOOK;36
  COLOR.LINE.COUNT=S$LCNT
  COLOR.LINE.SPACE=S$LSPC
  COLOR.LINE.CNT = COUNT(EST.PROD.INK.ID<1,COMP,MPTR>,"!") + (EST.PROD.INK.ID<1,COMP,MPTR> # "")
*
*---- MAIN PROCESSING
*
100*
  IF OPTION = "A" THEN
    OPT2 = "A"
    GOTO 520
  END
*
*---- GET OPERATOR REPLY
*
500*
  IF ACTION="M" THEN
    SCREEN FIELD;EST.PROD.MAINT.BOOK;44
    SCREEN INPUT;EST.PROD.MAINT.BOOK;44
  END ELSE
    SCREEN FIELD;EST.PROD.MAINT.BOOK;52
    SCREEN INPUT;EST.PROD.MAINT.BOOK;52
  END
  OPT2=S$VALUE
520*
  BEGIN CASE
    CASE OPT2="" OR OPT2="END"
      GOTO 99999
    CASE OPT2="A" AND COLOR.LINE.CNT < 99
      COLOR.REF.NO=COLOR.LINE.CNT
      DONE=0
      LOOP
        COLOR.REF.NO=COLOR.REF.NO+1
      UNTIL COLOR.LINE.CNT >= 99 OR DONE DO
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE="END" THEN
          DONE=1
          GOSUB 700
        END ELSE
          COLOR.LINE.CNT=COLOR.REF.NO
        END
      REPEAT
      COLOR.REF.NO=COLOR.LINE.CNT
      COLOR.CURR.REF.NO=""
      GOSUB 50000
      GOTO 500
    CASE OPT2="C" AND COLOR.LINE.CNT > 0
      GOSUB 650
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        COLOR.REF.NO=S$VALUE
        GOSUB 10000
      END
    CASE OPT2="D" AND COLOR.LINE.CNT > 0
      GOSUB 650
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        COLOR.REF.NO=S$VALUE
        GOSUB 700
        COLOR.LINE.CNT=COLOR.LINE.CNT-1
        IF COLOR.REF.NO > COLOR.LINE.CNT THEN COLOR.REF.NO=COLOR.REF.NO-1
        COLOR.CURR.REF.NO=""
        GOSUB 50000
      END
    CASE OPT2="S"
      COLOR.REF.NO=COLOR.CURR.REF.NO+COLOR.LINE.COUNT
      IF COLOR.REF.NO > COLOR.LINE.CNT THEN COLOR.REF.NO=1
      GOSUB 50000
    * T25978 v
    CASE OPT2 = 'SR'
      COLOR.REF.NO = COLOR.CURR.REF.NO - COLOR.LINE.COUNT
      IF COLOR.REF.NO < 1 THEN COLOR.REF.NO = 1
      GOSUB 50000
    CASE OPT2 = 'ST'
      COLOR.REF.NO = 1
      GOSUB 50000
    CASE OPT2 = 'SB'
      COLOR.REF.NO = COLOR.LINE.CNT
      GOSUB 50000
    * T25978 ^
  END CASE
  GOTO 500
*
*---- GET LINE NUMBER
*
650*
  SCREEN FIELD;EST.PROD.MAINT.BOOK;45
  S$MINV = COLOR.CURR.REF.NO
  S$MAXV = COLOR.CURR.REF.NO + COLOR.LINE.CNT - 1
  IF S$MAXV > COLOR.LINE.COUNT THEN S$MAXV = COLOR.LINE.CNT
  SCREEN INPUT;EST.PROD.MAINT.BOOK;45
  RETURN
*
*---- DELETE COLOR DATA
*
700*
*     EST.PROD.INK.ID=DELETE(EST.PROD.INK.ID,1,COMP,COLOR.REF.NO)
  MACRO DELVAL;EST.PROD.INK.ID<1,COMP,MPTR>;"!";COLOR.REF.NO
*     EST.PROD.INK.SIDES=DELETE(EST.PROD.INK.SIDES,1,COMP,COLOR.REF.NO)
  MACRO DELVAL;EST.PROD.INK.SIDES<1,COMP,MPTR>;"!";COLOR.REF.NO
*     EST.PROD.INK.COVER=DELETE(EST.PROD.INK.COVER,1,COMP,COLOR.REF.NO)
  MACRO DELVAL;EST.PROD.INK.COVER<1,COMP,MPTR>;"!";COLOR.REF.NO
  S$DATA(36)=DELETE(S$DATA(36),S$SCR,COLOR.REF.NO,0)
  S$DATA(37)=DELETE(S$DATA(37),S$SCR,COLOR.REF.NO,0)
  S$DATA(38)=DELETE(S$DATA(38),S$SCR,COLOR.REF.NO,0)
  RETURN
*
*---- INSERT COLOR DATA
*
800*
*     EST.PROD.INK.ID=INSERT(EST.PROD.INK.ID,1,COMP,COLOR.REF.NO,"")
  MACRO INSVAL;EST.PROD.INK.ID<1,COMP,MPTR>;"!";COLOR.REF.NO;""
*     EST.PROD.INK.SIDES=INSERT(EST.PROD.INK.SIDES,1,COMP,COLOR.REF.NO,"")
  MACRO INSVAL;EST.PROD.INK.SIDES<1,COMP,MPTR>;"!";COLOR.REF.NO;""
*     EST.PROD.INK.COVER=INSERT(EST.PROD.INK.COVER,1,COMP,COLOR.REF.NO,"")
  MACRO INSVAL;EST.PROD.INK.COVER<1,COMP,MPTR>;"!";COLOR.REF.NO;""
  S$DATA(36)=INSERT(S$DATA(36),S$SCR,COLOR.REF.NO,0,"")
  S$DATA(37)=INSERT(S$DATA(37),S$SCR,COLOR.REF.NO,0,"")
  S$DATA(38)=INSERT(S$DATA(38),S$SCR,COLOR.REF.NO,0,"")
  RETURN
*
*---- GET COLOR DATA
*
10000*
  S$VAL=COLOR.REF.NO
  SCREEN DISPLAY;EST.PROD.MAINT.BOOK;35
10100*
  S$DATA(36)<S$SCR,COLOR.REF.NO>=FIELD(EST.PROD.INK.ID<1,COMP,MPTR>,"!",COLOR.REF.NO)
  S$VAL=COLOR.REF.NO
  SCREEN FIELD;EST.PROD.MAINT.BOOK;36
  IF COLOR.REF.NO=1 THEN S$O.R="R"
  SCREEN INPUT;EST.PROD.MAINT.BOOK;36;GOTO 10900
  IF S$VALUE="" THEN S$VALUE="END";GOTO 10900
  IF S$VALUE="???" THEN
    S$SCR=S$SCR+1
    CALL EST.INK.XREF (CONO,"S",SELECTION)
    SCREEN CLOSE
    S$SCR=S$SCR-1
    SCREEN FORMAT
    SCREEN DISPLAY;EST.PROD.MAINT.BOOK;ALL
    S$VAL=1
    S$CNT=COUNT(EST.PROD.PQTY<1,COMP,MPTR>,"!") + (EST.PROD.PQTY<1,COMP,MPTR> # "")
    SCREEN MULTI;EST.PROD.MAINT.BOOK;C;41;42;43
    COLOR.CURR.REF.NO=""
    GOSUB 50000
    IF SELECTION="" THEN GOTO 10000
    S$VALUE=SELECTION
    S$DATA(36)<S$SCR,COLOR.REF.NO>=SELECTION
    S$VAL=COLOR.REF.NO
    SCREEN DISPLAY;EST.PROD.MAINT.BOOK;35
    S$VAL=COLOR.REF.NO
    SCREEN DISPLAY;EST.PROD.MAINT.BOOK;36
  END
  LOCATE S$VALUE IN ESTM.PROD<1>,1 SETTING FND ELSE
    ERRMSG="Invalid product ID. Try again! "
    GOSUB 90000
    GOTO 10100
  END
  INK.ID=S$VALUE
10200*
  S$VAL=COLOR.REF.NO
  SCREEN FIELD;EST.PROD.MAINT.BOOK;37
  S$O.R = "O"
  IF EST.PROD.MODE<1,COMP,MPTR> = "SS" THEN
    S$DEFAULT = "1"
  END ELSE
    S$DEFAULT = "2"
  END
  SCREEN INPUT;EST.PROD.MAINT.BOOK;37;GOTO 10900
  INK.SIDES=S$VALUE
10300*
  S$VAL=COLOR.REF.NO
  SCREEN FIELD;EST.PROD.MAINT.BOOK;38
  SCREEN INPUT;EST.PROD.MAINT.BOOK;38;GOTO 10900
  INK.COVER=S$VALUE
10800*
*     EST.PROD.INK.ID<1,COMP,COLOR.REF.NO>=INK.ID
  MACRO REPVAL;EST.PROD.INK.ID<1,COMP,MPTR>;"!";COLOR.REF.NO;INK.ID
*     EST.PROD.INK.SIDES<1,COMP,COLOR.REF.NO>=INK.SIDES
  MACRO REPVAL;EST.PROD.INK.SIDES<1,COMP,MPTR>;"!";COLOR.REF.NO;INK.SIDES
*     EST.PROD.INK.COVER<1,COMP,COLOR.REF.NO>=INK.COVER
  MACRO REPVAL;EST.PROD.INK.COVER<1,COMP,MPTR>;"!";COLOR.REF.NO;INK.COVER
  RETURN
10900*
  S$DATA(36)<S$SCR,COLOR.REF.NO>=FIELD(EST.PROD.INK.ID<1,COMP,MPTR>,"!",COLOR.REF.NO)
  S$DATA(37)<S$SCR,COLOR.REF.NO>=FIELD(EST.PROD.INK.SIDES<1,COMP,MPTR>,"!",COLOR.REF.NO)
  S$DATA(38)<S$SCR,COLOR.REF.NO>=FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",COLOR.REF.NO)
  GOSUB 50000
  RETURN
*
*---- DISPLAY COLOR DATA
*
50000*
  START.REF.NO=1+INT((COLOR.REF.NO-1)/COLOR.LINE.COUNT)*COLOR.LINE.COUNT
  IF START.REF.NO=COLOR.CURR.REF.NO THEN RETURN
  COLOR.CURR.REF.NO=START.REF.NO
  S$VAL=START.REF.NO
  S$CNT=COLOR.LINE.CNT
  SCREEN MULTI;EST.PROD.MAINT.BOOK;C;35;36;37;38
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY,1:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
  RETURN
END
