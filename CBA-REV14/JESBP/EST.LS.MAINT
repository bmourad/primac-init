*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - EST.LS.MAINT
* SOURCE   - JESBP 
* AUTHOR   - RHONDA PERRIN, COMPUTER BUSINESS ASSOCIATES
* DATE     - 04/24/86
* DESCRIPTION
* This program updates and maintains the ESTIMATE.LOST.SALES file.
*T22429 rik 12/17/1997 * COMMENTS LINES: ALLOW END TO GO TO COMMAND
*                        PROMPT LINE INSTEAD OF ESTIMATE ID FIELD
*                        PROMPT.
*T23063 lanny 07/10/98 * Use Competitor File not Vend File.
*T23278 markt 10/23/1998 * Add check for divisional security
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>DIVISION
*COPY>PMC.CPYLIB>SALESMAN
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>PMC.CPYLIB>VEND
*COPY>JCS.CPYLIB>JOB.CATEGORY
*COPY>JES.CPYLIB>ESTIMATE.LOST.SALES
*COPY>JES.CPYLIB>ESTIMATE.LS.CODE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATOR
*COPY>JES.CPYLIB>COMPETITOR
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","DIVISION" TO DIVISION ELSE
    ERRMSG = "CANNOT OPEN DIVISION FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CUSTOMER" TO CUSTOMER ELSE
    ERRMSG = "CANNOT OPEN CUSTOMER FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","SALESMAN" TO SALESMAN ELSE
    ERRMSG = "CANNOT OPEN SALESMAN FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","VEND" TO VEND ELSE
    ERRMSG = "CANNOT OPEN VEND FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","COMPETITOR" TO COMPETITOR ELSE
    ERRMSG = "CANNOT OPEN COMPETITOR FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JOB.CATEGORY" TO JOB.CATEGORY ELSE
    ERRMSG = "CANNOT OPEN JOB.CATEGORY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.LOST.SALES" TO ESTIMATE.LOST.SALES ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.LOST.SALES FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE" TO ESTIMATE ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATOR" TO ESTIMATOR ELSE
    ERRMSG = "CANNOT OPEN ESTIMATOR FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.LS.CODE" TO ESTIMATE.LS.CODE ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.LS.CODE FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.LS.MAINT
  SCREEN FORMAT
*
*---- MAIN PROCESSING
*
100*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;EST.LS.MAINT;1
  SCREEN FIELD;EST.LS.MAINT;2
  SCREEN INPUT;EST.LS.MAINT;2
  IF S$VALUE = "END" THEN GOTO 99999
  ESTLS.KEY = S$VALUE
  NEW.REC = 0
  * T26126 v
  MATREADU ESTLS.REC FROM ESTIMATE.LOST.SALES, CONO:ESTLS.KEY LOCKED
    ERRMSG = 'ESTIMATE record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 100 
  END ELSE
  * T26126 ^
    MAT ESTLS.REC = ""
    NEW.REC = 1
    MATREAD EST.REC FROM ESTIMATE,CONO:ESTLS.KEY ELSE
      ERRMSG = "Invalid Estimate number.  Try again !"
      GOSUB 90000
      GOTO 100
    END
*T23278 v
    DIV.CODE = EST.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
    CALL SI.CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
    IF ERRMSG # '' THEN
      GOSUB 90000; GOTO 100
    END
*T23278 ^
    IF EST.STATUS<1,1> = "LOST" AND ESTLS.KEY # EST.MASTER AND EST.MASTER # "" THEN
      ERRMSG = 'This sub-estimate has a status of "LOST".  Try again !'
      GOSUB 90000
      GOTO 100
    END
    IF EST.STATUS<1,1> = "BOOKED" THEN
      ERRMSG = 'This Estimate is "BOOKED" to a Job.  Try again !'
      GOSUB 90000
      GOTO 100
    END
    ESTLS.ESTIMATOR = EST.ESTIMATOR
    ESTLS.CUST = EST.CUST
    ESTLS.CUST.NAME = EST.CUST.NAME
    ESTLS.CUST.ADDR1 = EST.CUST.ADDR1
    ESTLS.CUST.ADDR2 = EST.CUST.ADDR2
    ESTLS.CUST.ADDR3 = EST.CUST.ADDR3
    ESTLS.CUST.ADDR4 = EST.CUST.ADDR4
    ESTLS.SALESMAN = EST.SALESMAN
    ESTLS.DIV = FIELD(EST.DIV,"-",1)
    ESTLS.CATG = EST.CATG
    ESTLS.DATE.ENTERED = EST.DATE.ENTERED
    ESTLS.DATE.REQUIRED = EST.DATE.REQUIRED
    MIN.COST = 0; MIN.SALE = 0
    MAX.COST = 0; MAX.SALE = 0
    IF EST.SUBS = "" THEN
      EST.IDS = ESTLS.KEY
      N2 = 1
    END ELSE
      EST.IDS = ESTLS.KEY:VM:EST.SUBS
      N2 = COUNT(EST.IDS,VM) + 1
    END
    FOR N = 1 TO N2
      FND = 1
      IF N > 1 THEN
        MATREAD EST.REC FROM ESTIMATE,CONO:EST.IDS<1,N> ELSE FND = 0
      END
      ECNT = COUNT(EST.QTY,VM) + 1
      EQTY = EST.QTY<1,1>
      GOSUB 50000
      MIN.COST = MIN.COST + TOTAL.COST
      MIN.SALE = MIN.SALE + TOTAL.TSALE
      EQTY = EST.QTY<1,ECNT>
      GOSUB 50000
      MAX.COST = MAX.COST + TOTAL.COST
      MAX.SALE = MAX.SALE + TOTAL.TSALE
    NEXT N
    ESTLS.MIN.MARGIN = MIN.SALE - MIN.COST
    ESTLS.MAX.MARGIN = MAX.SALE - MAX.COST
    GOSUB 80000
    SCREEN DISPLAY;EST.LS.MAINT;ALL
  END
*T23278 v
  DIV.CODE = ESTLS.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
  CALL SI.CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
  IF ERRMSG # '' THEN
    GOSUB 90000; RELEASE; GOTO 100
  END
*T23278 ^
  IF NEW.REC THEN
    FOR FLD = 1 TO 3
      BEGIN CASE
        CASE FLD <= 10
          ON FLD GOSUB 10100,10200,10300,19990,19990,19990,19990,19990,19990,19990
        CASE FLD <= 20
          ON FLD-10 GOSUB 19990,19990,19990,19990,19990
      END CASE
      IF S$VALUE = "END" THEN
        IF FLD = 3 THEN EXIT;*T22429
        SCREEN CLEAR;EST.LS.MAINT
        RELEASE
        GOTO 100
      END
    NEXT FLD
  END ELSE
    MATREAD EST.REC FROM ESTIMATE,CONO:ESTLS.KEY ELSE MAT EST.REC = ""
    IF EST.SUBS = "" THEN
      EST.IDS = ESTLS.KEY
      N2 = 1
    END ELSE
      EST.IDS = ESTLS.KEY:VM:EST.SUBS
      N2 = COUNT(EST.IDS,VM) + 1
    END
    GOSUB 80000
    SCREEN DISPLAY;EST.LS.MAINT;ALL
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.LS.MAINT;31
  SCREEN INPUT;EST.LS.MAINT;31
  OPTION = S$VALUE
  BEGIN CASE
    CASE OPTION # "" AND NUM(OPTION)
      FLD = OPTION
      BEGIN CASE
        CASE FLD <= 10
          ON FLD GOSUB 10100,10200,10300,19990,19990,19990,19990,19990,19990,19990
        CASE FLD <= 20
          ON FLD-10 GOSUB 19990,19990,19990,19990,19990
      END CASE
    CASE OPTION = "E" OR OPTION = "END"
      SCREEN CLEAR;EST.LS.MAINT
      RELEASE
      GOTO 100
    CASE OPTION = "P"
      SCREEN FIELD;EST.LS.MAINT;32
      SCREEN INPUT;EST.LS.MAINT;32
      IF S$VALUE = "Y" THEN
        FOR NN = 1 TO N2
          MATREADU EST.REC FROM ESTIMATE,CONO:EST.IDS<1,NN> ELSE GOTO 505
          LOCATE "LOST" IN EST.STATUS<1>,1 SETTING FNDS ELSE FNDS = 0
          IF FNDS THEN
            EST.STATUS = DELETE(EST.STATUS,1,FNDS,0)
            EST.STAT.DATE = DELETE(EST.STAT.DATE,1,FNDS,0)
            MATWRITE EST.REC ON ESTIMATE,CONO:EST.IDS<1,NN>
          END ELSE
            RELEASE ESTIMATE,CONO:EST.IDS<1,NN>
          END
505*
        NEXT NN
        DELETE ESTIMATE.LOST.SALES,CONO:ESTLS.KEY
        SCREEN CLEAR;EST.LS.MAINT
        GOTO 100
      END
    CASE OPTION = "F"
      MATWRITE ESTLS.REC ON ESTIMATE.LOST.SALES, CONO:ESTLS.KEY
      FOR NN = 1 TO N2
        MATREADU EST.REC FROM ESTIMATE,CONO:EST.IDS<1,NN> ELSE GOTO 510
        LOCATE "LOST" IN EST.STATUS<1>,1 SETTING FNDS ELSE FNDS = 0
        IF FNDS = 0 THEN
          EST.STATUS = INSERT(EST.STATUS,1,FNDS,0,"LOST")
          EST.STAT.DATE = INSERT(EST.STAT.DATE,1,FNDS,0,DATE())
          MATWRITE EST.REC ON ESTIMATE,CONO:EST.IDS<1,NN>
        END ELSE
          RELEASE ESTIMATE,CONO:EST.IDS<1,NN>
        END
510*
      NEXT NN
      SCREEN CLEAR;EST.LS.MAINT
      GOTO 100
  END CASE
  GOTO 500
*
*---- GET LOST SALES CODE AND DESCRIPTION
*
10100*
  S$DATA(21)<S$SCR> = ESTLS.CODE
  SCREEN FIELD;EST.LS.MAINT;21
  SCREEN INPUT;EST.LS.MAINT;21;RETURN
  MATREAD ESTLC.REC FROM ESTIMATE.LS.CODE,CONO:S$VALUE ELSE
    ERRMSG = "Invalid lost sales code.  Try again !"
    GOSUB 90000
    GOTO 10100
  END
  ESTLS.CODE = S$VALUE
  S$DATA(22)<S$SCR> = ESTLC.DESC
  SCREEN DISPLAY;EST.LS.MAINT;22
  RETURN
*
*---- GET COMPETITOR CODE AND DESCRIPTION
*
10200*
  S$DATA(23)<S$SCR> = ESTLS.COMPETITOR
  SCREEN FIELD;EST.LS.MAINT;23
  SCREEN INPUT;EST.LS.MAINT;23;RETURN
  IF S$VALUE = "" THEN
*T23063 MAT VEND.REC = ""
    MAT COMPETITOR.REC = ""
  END ELSE
    MATREAD COMPETITOR.REC FROM COMPETITOR,CONO:S$VALUE ELSE
      ERRMSG = "Invalid competitor code.  Try again !"
      GOSUB 90000
      GOTO 10200
    END
  END
  ESTLS.COMPETITOR = S$VALUE
*T23063   S$DATA(24)<S$SCR> = VEND.DESC
  S$DATA(24)<S$SCR> = COMPTR.NAME
  SCREEN DISPLAY;EST.LS.MAINT;24
  RETURN
*
*---- GET COMMENTS
*
10300*
  SCREEN FIELD;EST.LS.MAINT;25
  SCREEN INPUT;EST.LS.MAINT;25;RETURN
  ESTLS.COMMENTS<1,1> = S$VALUE
  SCREEN FIELD;EST.LS.MAINT;34
  SCREEN INPUT;EST.LS.MAINT;34;RETURN
  ESTLS.COMMENTS<1,2> = S$VALUE
  SCREEN FIELD;EST.LS.MAINT;35
  SCREEN INPUT;EST.LS.MAINT;35;RETURN
  ESTLS.COMMENTS<1,3> = S$VALUE
  RETURN
*
*---- DUMMY
*
19990*
  RETURN
*
*---- CALCULATE DOLLAR VALUE
*
50000*
  TOTAL.COST = 0
  TOTAL.TSALE = 0
  DC = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
  FOR DP = 1 TO DC
    ESTD.ID = EST.DEPT.COMP<1,DP>
    IF EQTY = FIELD(ESTD.ID,"!",3) THEN
      TOTAL.COST = TOTAL.COST + EST.DEPT.COMP.COST<1,DP>
      TOTAL.TSALE = TOTAL.TSALE + EST.DEPT.COMP.TSALE<1,DP>
    END
  NEXT DP
*CSF 23787 v
  LOCATE EQTY IN EST.QTY<1>,1 SETTING EP THEN
    TOTAL.TSALE=TOTAL.TSALE + (INT(OCONV(TOTAL.TSALE,'MD2') * OCONV(EST.OM.PCT<1,EP>,'MD4') + .005 / 100))
  END
*CSF 23787 ^
  TOTAL.COST = INT(TOTAL.COST / 100 + 0.5)
  TOTAL.TSALE = INT(TOTAL.TSALE / 100 + 0.5)
  RETURN
*
*---- LOAD SCREEN VALUES
*
80000*
  MATREAD ESTIMATOR.REC FROM ESTIMATOR,CONO:ESTLS.ESTIMATOR ELSE ESTMTR.NAME = "ESTIMATOR UNKNOWN"
  MATREAD SALESMAN.REC FROM SALESMAN,CONO:ESTLS.SALESMAN ELSE SALS.NAME = "SALESMAN UNKNOWN"
  MATREAD DIV.REC FROM DIVISION,CONO:ESTLS.DIV ELSE DIV.DESC = "DIVISION UNKNOWN"
  MATREAD JCATG.REC FROM JOB.CATEGORY,CONO:ESTLS.CATG ELSE JCATG.DESC = "CATEGORY UNKNOWN"
  S$DATA(3)<S$SCR> = ESTLS.CUST
  S$DATA(4)<S$SCR> = ESTLS.CUST.NAME
  S$DATA(5)<S$SCR> = ESTLS.CUST.ADDR1
  S$DATA(6)<S$SCR> = ESTLS.CUST.ADDR2
  S$DATA(7)<S$SCR> = ESTLS.CUST.ADDR3
  S$DATA(33)<S$SCR> = ESTLS.CUST.ADDR4
  S$DATA(8)<S$SCR> = ESTMTR.NAME
  S$DATA(9)<S$SCR> = SALS.NAME
  S$DATA(10)<S$SCR> = DIV.DESC
  S$DATA(11)<S$SCR> = JCATG.DESC
  S$DATA(16)<S$SCR> = ESTLS.DATE.ENTERED
  S$DATA(17)<S$SCR> = ESTLS.DATE.REQUIRED
*T26138 v
*  S$DATA(18)<S$SCR> = ESTLS.MIN.MARGIN
*  S$DATA(19)<S$SCR> = ESTLS.MAX.MARGIN
  S$DATA(18)<S$SCR> = OCONV(ESTLS.MIN.MARGIN,'MD0,')
  S$DATA(19)<S$SCR> = OCONV(ESTLS.MAX.MARGIN,'MD0,')
*T26138 ^
  IF NEW.REC THEN GOTO 89999
  MATREAD ESTLC.REC FROM ESTIMATE.LS.CODE,CONO:ESTLS.CODE ELSE ESTLC.DESC = "LOST SALES DESC UNKNOWN"
*T23063   MATREAD VEND.REC FROM VEND,CONO:ESTLS.COMPETITOR ELSE VEND.DESC = "COMPETITOR UNKNOWN"
  MATREAD COMPETITOR.REC FROM COMPETITOR,CONO:ESTLS.COMPETITOR ELSE COMPTR.NAME = "COMPETITOR UNKNOWN"
  S$DATA(21)<S$SCR> = ESTLS.CODE
  S$DATA(22)<S$SCR> = ESTLC.DESC
  S$DATA(23)<S$SCR> = ESTLS.COMPETITOR
*T23063  S$DATA(24)<S$SCR> = VEND.DESC
  S$DATA(24)<S$SCR> = COMPTR.NAME
  S$DATA(25)<S$SCR> = ESTLS.COMMENTS<1,1>
  S$DATA(34)<S$SCR> = ESTLS.COMMENTS<1,2>
  S$DATA(35)<S$SCR> = ESTLS.COMMENTS<1,3>
89999*
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  STOP
END
