   SUBROUTINE EST.FORMULA.Q870 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q870
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 07/28/86
*
* DESCRIPTION
*
* This formula is used to calculate the makeready time
*
*T18588 Formula moved over from EST.FORMULA.Q960
*T21149 doug 10/28/1996 * Calculating MR.COL
*T21890 julie 05/07/1997 * FORMULA IS NOT GETTING CORRECT LINE NUMBER
*T21927 diane 05/13/1997 * Include Slits in calculation
*T27789 cmykleb 11/17/2003 * If the varnish field = "F", also bump up
*                            the # of colors by one.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
* T21890
   IF TEMP.GRP.CCTR[1,2] = "PR" AND NUM(TEMP.GRP.CCTR[3,999]) THEN
      MPTR = TEMP.GRP.CCTR[3,999]
      IF MPTR < 1 OR MPTR > 9 THEN MPTR = 1
   END ELSE
* T21890
      LOCATE TEMP.CCTR IN EST.RL.DIE.PRESS.NO<1,COMP>,1 SETTING MPTR ELSE MPTR=1
   END;* T21890
   MKR.DIE=0
   MKR.COLS=0
   MKR.LAM=0
   MKR.SLT=0     ;*T21927
   MKR.SSC=0
   MKR.GRV=0
   MKR.EMB=0
   MKR.FOIL=0
   MKR.COLORS=0
   MKR.PLATE=0
   MKR.SPR=0
   MKR.MIX=0
   MKR.QTY=0
*
*---- MAIN PROCESSING
*
   TEMP.QTY = 0
*
*--- Rotary & Flatbed Dies
*
   DIETYPE=EST.RL.DIETYPE<1,COMP,MPTR>
   BEGIN CASE
      CASE DIETYPE='R' 
         MKR.DIE=EST.RL.MR.DIE<1,COMP,MPTR>
      CASE DIETYPE = "F" 
         MKR.DIE=EST.RL.MR.FDIE<1,COMP,MPTR>
      CASE 1
         MKR.DIE = 0
   END CASE
*
*--- Calculate No of Colours
*
   NO.OF.COLS=EST.PROD.COLORS.1<1,COMP,MPTR>+EST.PROD.COLORS.2<1,COMP,MPTR>
   NO.OF.COLS=EST.RL.ADHESIVE<1,COMP,MPTR>+NO.OF.COLS;* Adhesive
   IF EST.RL.FULL.SPOT<1,COMP,MPTR>='S' THEN
      NO.OF.COLS=NO.OF.COLS+1
   END
*T27789 v
   IF EST.RL.FULL.SPOT<1,COMP,MPTR>='F' THEN
      NO.OF.COLS=NO.OF.COLS+1
   END
*T27789 ^
*T21149      IF NO.OF.COLS+0 > 1 THEN
   MKR.COLS=(EST.RL.MR.COLORS<1,COMP,MPTR>+0) * (NO.OF.COLS+0)
*T21149      END
   IF EST.RL.OVERLAM<1,COMP,MPTR>='Y' THEN
      MKR.LAM = EST.RL.MR.OVERLAM<1,COMP,MPTR>+0
   END
   MKR.SSC=(EST.RL.MR.SSCREEN<1,COMP,MPTR>+0)*(EST.RL.SSCREEN<1,COMP,MPTR>+0)
   MKR.GRV=(EST.RL.MR.GRAVURE<1,COMP,MPTR>+0)*(EST.RL.GRAVURE<1,COMP,MPTR>+0)
   IF EST.RL.EMBOSS<1,COMP,1>='Y' THEN
      MKR.EMB=(EST.RL.MR.EMBOSS<1,COMP,MPTR>+0)
   END
   IF EST.RL.FOIL<1,COMP,MPTR>='Y' THEN
      MKR.FOIL=(EST.RL.MR.FOIL<1,COMP,MPTR>+0)
   END
   MKR.COLORS=(EST.RL.MR.COL.CHANGE<1,COMP,MPTR>+0)*(EST.RL.COLOR.CHANGES<1,COMP,MPTR>+0)
   MKR.PLATE=(EST.RL.MR.PLATE<1,COMP,MPTR>+0)*(EST.RL.PLATE.CHANGES<1,COMP,MPTR>+0)
   IF EST.RL.SPROCKET<1,COMP,MPTR>='Y' THEN
      MKR.SPR=EST.RL.MR.SPROCKET<1,COMP,MPTR>
   END
   MKR.MIX=(EST.RL.MR.COL.MIX<1,COMP,MPTR>+0)*(EST.RL.COL.MIX<1,COMP,MPTR>+0)
*T21927 v
   IF EST.RL.LINEAL.SLIT = "Y" THEN
      MKR.SLT = (EST.RL.MR.LIN.SLITS<1,COMP,MPTR>+0) * EST.RL.NO.SLITS+0
   END
*    MKR.QTY=MKR.QTY+MKR.DIE+MKR.COLS+MKR.SSC+MKR.GRV+MKR.EMB+MKR.FOIL+MKR.COLORS+MKR.PLATE+MKR.SPR+MKR.MIX+MKR.LAM
   MKR.QTY=MKR.QTY+MKR.DIE+MKR.COLS+MKR.SSC+MKR.GRV+MKR.EMB+MKR.FOIL+MKR.COLORS+MKR.PLATE+MKR.SPR+MKR.MIX+MKR.LAM+MKR.SLT
*T21927 ^
   TEMP.QTY = MKR.QTY / 100
   EST.RL.TOT.COLOURS<1,COMP>=NO.OF.COLS
   RETURN
END
