      SUBROUTINE EST.FORMULA.Q869 (CONO, ACTION, EQTY, DEPT, COMP)
THE REASON THIS FORMULA WAS CREATED WAS TO FIT A SPECIFIC NEED AT
A AUSTRALIAN USER...DOES NOT CONFORM TO ANY US NEEDS OR STANDARDS 
AS BEST WE CAN TELL
*********************************************************************
*
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q869
*
* AUTHOR   - GRAHAM JARVIS, PACRIM SOFTWARE PTY LTD
*
* DATE     - 17 MAR 93
*
* DESCRIPTION
*
* This formula is used to calculate:-
*        1. The MAX OD Given the Qty of Labels/Roll and No Across
*        2. The Qty of Labels/Roll Given the Max OD and No Across
*        3. Theorectical Weight of Each Roll using PSI of Stock
*        4. Theoretical No of Cartons Required using 35Lbs per Carton
*        5. The Totals No of Rolls
* The TEMP.QTY returned is the No of Cartons
*
* VARIABLES
*        MAX.OD = The maximum OD in inches
*        CORE.SZ= The size of the core (internal, add 0.25 inch for thickness)
*        NO.ROLL= The Actual Quantity/Roll (user set)
*        LACR   = The number of labels across the roll (user set)
*        REPT   = The repeat of a SINGLE label (Die Repeat/No Around)
*
* FORMULA for Calculating No of Labels/Roll given MAX OD
*
*        TOT.LINEAL =  MAX.OD**2 - CORE.SZ**2
*                      ----------------------
*                          1.27 * TOT.CAL
*
*        ROLL.QTY   =  TOT.LINEAL * REPT * LACR
*
*T18588 Formula moved over from EST.FORMULA.Q958
*T21556 doug 02/04/1997 * Corrections to formula Q869
*T22620 gat 02/17/1998 * FIX PROBLEM IN CALC OF # ROLLS OUPUT
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.RES
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
*IF @LOGNAME = "gat" THEN DEBUG
      IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
      LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR=1
*
*---- MAIN PROCESSING
*
      TEMP.QTY = 0
*
*--- Calculate Weight and Caliper by adding Stocks
*
      WEB.CNT=DCOUNT(EST.PROD.OS.USAGE<1,COMP>,SM)
      TOT.PMSI=0
      TOT.CAL=0
      TOT.WGT=0
      PMSI=0
      FOR WPTR=1 TO WEB.CNT
         PMSI=(EST.RL.PAP.GSM<1,COMP,WPTR>/10000)
         TOT.PMSI=TOT.PMSI+PMSI
         MSI=FIELD(EST.RL.PAP.MSI<1,COMP,WPTR>,'!',QPTR)
         TOT.WGT=TOT.WGT+(MSI*PMSI)
         TOT.CAL=EST.RL.PAP.CAL<1,COMP,WPTR> + TOT.CAL
      NEXT WPTR
      TOT.WGT=INT(TOT.WGT/10000+.5)
*     TOT.CAL=TOT.CAL+30; *** Add Adhesive Caliper
      TOT.CAL=TOT.CAL/10000
*
*--- Calculate Lineal/Roll, Max OD and Qty/Roll
*
      ROLL.LINEAL=0
      ROLL.QTY=0
      ROLL.MAXOD=0
      MAX.OD=EST.RL.MAX.OD<1,COMP,1>+0
      CORE.SZ=(EST.RL.CORE.SIZE<1,COMP,1>+2500)/10000 ;* Add 0.25 inch for Core Thickness (6mm)
      NO.ROLL=EST.RL.NO.PER.ROLL<1,COMP,1>+0
      LACR=EST.RL.LAB.ACR<1,COMP,1>
      IF LACR+0=0 THEN LACR = EST.RL.DIE.NO.ACR<1,COMP,1>
      LWTH=EST.PROD.OS.WIDTH<1,COMP,1>/10000
      LWTH=LWTH/EST.RL.DIE.NO.ACR<1,COMP,1>*LACR
      REPT=EST.RL.DIE.REPEAT<1,COMP,1>/10000
      REPT=REPT/EST.RL.DIE.NO.ARD<1,COMP,1>
      BEGIN CASE
      CASE NO.ROLL=0 AND MAX.OD#0
         VAR1=((MAX.OD/10000)**2) - (CORE.SZ**2)
         VAR2=1.27*TOT.CAL
         ROLL.LINEAL=INT(VAR1/VAR2+.5)
         ROLL.MAXOD=MAX.OD
         ROLL.QTY=INT((ROLL.LINEAL/REPT)*LACR+.99)
*        ROLL.QTY=INT(ROLL.QTY/250+.5)*250 ;* Round QTY to Nearest 250
         IF ROLL.QTY=0 THEN ROLL.QTY=EQTY
      CASE MAX.OD=0 AND NO.ROLL#0
         ROLL.LINEAL=INT(NO.ROLL*REPT/LACR+.5)
         VAR1=SQRT((1.27*ROLL.LINEAL*TOT.CAL)+(CORE.SZ**2))
         ROLL.MAXOD=INT(VAR1*10000+.5)
         ROLL.QTY=NO.ROLL
      CASE 1
         ROLL.LINEAL=INT(NO.ROLL*REPT/LACR+.5)
         ROLL.MAXOD=MAX.OD
         ROLL.QTY=NO.ROLL
         IF ROLL.QTY=0 THEN ROLL.QTY=EQTY
      END CASE
*
*-- Calculate Values
*
      ROLL.SQI=ROLL.LINEAL*LWTH
*CRT @(0,23):"LWTH     ":LWTH
*INPUT XXXX
*CRT @(0,23):"ROLL.LINEAL ":ROLL.LINEAL
*INPUT XXXX
*CRT @(0,23):"ROLL.SQI ":ROLL.SQI
*INPUT XXXX
*CRT @(0,23):"TOT.PMSI   ":TOT.PMSI
*INPUT XXXX
*      ROLL.WGT=ROLL.SQI*(TOT.PMSI/10000000)
      ROLL.WGT=INT(ROLL.SQI*(TOT.PMSI/10000000))
*CRT @(0,23):" ROLL.WGT * 100000 ":ROLL.WGT * 100000
*INPUT XXXX
* Use 35Lbs as Maximum Weight per Carton (16Kgs)
      IF ROLL.WGT <= 0 THEN ROLL.WGT = 35
*CRT @(0,23):"ROLL WGT  ":ROLL.WGT
      PER.CTN=INT(35/ROLL.WGT)
      IF PER.CTN=0 THEN PER.CTN=1
* T22620
*      ROLL.TOT=EQTY/ROLL.QTY
      ROLL.TOT = (EQTY/LACR)/ROLL.QTY
* T22620
*CRT @(0,23):"PRINT ROLL.TOT ":ROLL.TOT
*INPUT XXX
*CRT @(0,23):"PRINT PER.CTN  ":PER.CTN
*INPUT XXX
      TEMP.QTY=INT(ROLL.TOT/PER.CTN+.99) + 1
*CRT @(0,23):"PRINT TOT.QTY ":TEMP.QTY
*INPUT XXXX
      IF TEMP.QTY < 1 THEN TEMP.QTY = 1
      TEMP.VSALE = "Y"
*
*--- Update ESTIMATE.RES fields with Calculated Values
*
      EST.RL.NO.ROLLS<1,COMP,QPTR>=ROLL.TOT
      EST.RL.NO.CTNS<1,COMP,QPTR>=TEMP.QTY
      EST.RL.NO.KGS<1,COMP,QPTR>=TOT.WGT
      EST.RL.CALC.MAX.OD<1,COMP,QPTR>=ROLL.MAXOD
      EST.RL.CALC.ROLL.QTY<1,COMP,QPTR>=ROLL.QTY
*
      RETURN
   END
