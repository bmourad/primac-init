***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.IMPO.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/06/86
*
* DESCRIPTION
*
* This program is used to maintain the imposition file.
*
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>ESTIMATE.IMPO
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.IMPO" TO ESTIMATE.IMPO ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.IMPO FILE"
    GOSUB 90000
    STOP
  END
*
*---- INITIALIZATION
*
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.IMPO.MAINT
  SCREEN FORMAT
  SCREEN COUNT;EST.IMPO.MAINT;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  REF.NO = ""
  CURR.REF.NO = ""
  LINE.CNT = 0
*
*---- MAIN PROCESSING
*
100*
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;EST.IMPO.MAINT;1
  NEW.REC = 0
  * T26126 v
  MATREADU IMPO.REC FROM ESTIMATE.IMPO, CONO:"IMPO" LOCKED
    ERRMSG = 'IMPOSITION TABLE record is locked by user - ':GETUSERNAME(STATUS())
    GOSUB 90000;GOTO 99999 
  END ELSE
  * T26126 ^
    MAT IMPO.REC = ""
    NEW.REC = 1
  END
  IF NEW.REC THEN
    OPT = "A"
    GOTO 510
  END ELSE
    LINE.CNT = COUNT(IMPO.ID,VM) + (IMPO.ID # "")
    REF.NO = 1
    GOSUB 80000
    GOSUB 50000
  END
*
*---- GET OPERATOR REPLY
*
500*
  SCREEN FIELD;EST.IMPO.MAINT;21
  SCREEN INPUT;EST.IMPO.MAINT;21
  OPT = S$VALUE
510*
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      RELEASE          ;* T26126
      GOTO 99999
    CASE OPT = "A" AND LINE.CNT < 999
      MODE = "A"
      DONE = 0
      FOR REF.NO = LINE.CNT + 1 TO 999 UNTIL DONE
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 60000
        END ELSE
          LINE.CNT = LINE.CNT + 1
        END
      NEXT REF.NO
      REF.NO = LINE.CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND LINE.CNT > 0
      MODE = "C"
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10000
      END
    CASE OPT = "D" AND LINE.CNT > 0
      MODE = "D"
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 60000
        LINE.CNT = LINE.CNT - 1
        IF REF.NO > LINE.CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "I" AND LINE.CNT > 0
      MODE = "I"
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 70000
        LINE.CNT = LINE.CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10000
        IF S$VALUE = "END" THEN
          GOSUB 60000
          LINE.CNT = LINE.CNT - 1
        END
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > LINE.CNT THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "ST"
      REF.NO = 1
      GOSUB 50000
    CASE OPT = "SB"
      REF.NO = COUNT(IMPO.ID,VM) + 1
      GOSUB 50000
    CASE OPT = "SR"
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = "F"
      MATWRITE IMPO.REC ON ESTIMATE.IMPO,CONO:"IMPO"
      GOTO 99999
  END CASE
  GOTO 500
*
*---- GET MULTI-LINE DATA
*
10000*
  S$VAL = REF.NO
  SCREEN DISPLAY;EST.IMPO.MAINT;11
10100*
  S$VAL = REF.NO
  SCREEN FIELD;EST.IMPO.MAINT;12
  SCREEN INPUT;EST.IMPO.MAINT;12;GOTO 10995
  TEMP.ID = S$VALUE
10110*
  S$VAL = REF.NO
  SCREEN FIELD;EST.IMPO.MAINT;13
  SCREEN INPUT;EST.IMPO.MAINT;13;GOTO 10995
  TEMP.DESC = S$VALUE
10120*
  S$VAL = REF.NO
  SCREEN FIELD;EST.IMPO.MAINT;14
  SCREEN INPUT;EST.IMPO.MAINT;14;GOTO 10995
  TEMP.SIG.PER.SHT = S$VALUE
10125*
  S$VAL = REF.NO
  SCREEN FIELD;EST.IMPO.MAINT;15
  SCREEN INPUT;EST.IMPO.MAINT;15;GOTO 10995
  TEMP.PGS.PER.SHT = S$VALUE
10130*
*     S$VAL = REF.NO
*     SCREEN FIELD;EST.IMPO.MAINT;16
*     SCREEN INPUT;EST.IMPO.MAINT;16;GOTO 10995
*     TEMP.TYPE = S$VALUE
10140*
  S$VAL = REF.NO
  SCREEN FIELD;EST.IMPO.MAINT;17
  SCREEN INPUT;EST.IMPO.MAINT;17;GOTO 10995
  TEMP.MR.FCTR = S$VALUE
10150*
  S$VAL = REF.NO
  SCREEN FIELD;EST.IMPO.MAINT;18
  SCREEN INPUT;EST.IMPO.MAINT;18;GOTO 10995
  TEMP.RUN.FCTR = S$VALUE
10190*
  IMPO.ID<1,REF.NO> = TEMP.ID
  IMPO.DESC<1,REF.NO> = TEMP.DESC
  IMPO.SIG.PER.SHT<1,REF.NO> = TEMP.SIG.PER.SHT
  IMPO.PGS.PER.SHT<1,REF.NO> = TEMP.PGS.PER.SHT
*     IMPO.TYPE<1,REF.NO> = TEMP.TYPE
  IMPO.MR.FCTR<1,REF.NO> = TEMP.MR.FCTR
  IMPO.RUN.FCTR<1,REF.NO> = TEMP.RUN.FCTR
  RETURN
*---- BACK-OUT ENTRY
10995*
  S$DATA(12)<S$SCR,REF.NO> = IMPO.ID<1,REF.NO>
  S$DATA(13)<S$SCR,REF.NO> = IMPO.DESC<1,REF.NO>
  S$DATA(14)<S$SCR,REF.NO> = IMPO.SIG.PER.SHT<1,REF.NO>
  S$DATA(15)<S$SCR,REF.NO> = IMPO.PGS.PER.SHT<1,REF.NO>
*     S$DATA(16)<S$SCR,REF.NO> = IMPO.TYPE<1,REF.NO>
  S$DATA(17)<S$SCR,REF.NO> = IMPO.MR.FCTR<1,REF.NO>
  S$DATA(18)<S$SCR,REF.NO> = IMPO.RUN.FCTR<1,REF.NO>
  RETURN
*
*---- GET LINE NUMBER
*
30000*
  S$DATA(22)<S$SCR> = ""
  SCREEN FIELD;EST.IMPO.MAINT;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > LINE.CNT THEN S$MAXV = LINE.CNT
  SCREEN INPUT;EST.IMPO.MAINT;22
  RETURN
*
*---- SCROLL
*
50000*
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = COUNT(S$DATA(12)<S$SCR>,VM) + (S$DATA(12)<S$SCR> # "")
  SCREEN MULTI;EST.IMPO.MAINT;C;11;12;13;14;15;17;18
  RETURN
*
*---- DELETE LINE
*
60000*
  IMPO.ID = DELETE(IMPO.ID,1,REF.NO,0)
  IMPO.DESC = DELETE(IMPO.DESC,1,REF.NO,0)
  IMPO.SIG.PER.SHT = DELETE(IMPO.SIG.PER.SHT,1,REF.NO,0)
  IMPO.PGS.PER.SHT = DELETE(IMPO.PGS.PER.SHT,1,REF.NO,0)
*     IMPO.TYPE = DELETE(IMPO.TYPE,1,REF.NO,0)
  IMPO.MR.FCTR = DELETE(IMPO.MR.FCTR,1,REF.NO,0)
  IMPO.RUN.FCTR = DELETE(IMPO.RUN.FCTR,1,REF.NO,0)
  S$DATA(12) = DELETE(S$DATA(12),S$SCR,REF.NO,0)
  S$DATA(13) = DELETE(S$DATA(13),S$SCR,REF.NO,0)
  S$DATA(14) = DELETE(S$DATA(14),S$SCR,REF.NO,0)
  S$DATA(15) = DELETE(S$DATA(15),S$SCR,REF.NO,0)
*     S$DATA(16) = DELETE(S$DATA(16),S$SCR,REF.NO,0)
  S$DATA(17) = DELETE(S$DATA(17),S$SCR,REF.NO,0)
  S$DATA(18) = DELETE(S$DATA(18),S$SCR,REF.NO,0)
  RETURN
*
*---- INSERT MATRIX LINE
*
70000*
  IMPO.ID = INSERT(IMPO.ID,1,REF.NO,0,"")
  IMPO.DESC = INSERT(IMPO.DESC,1,REF.NO,0,"")
  IMPO.SIG.PER.SHT = INSERT(IMPO.SIG.PER.SHT,1,REF.NO,0,"")
  IMPO.PGS.PER.SHT = INSERT(IMPO.PGS.PER.SHT,1,REF.NO,0,"")
*     IMPO.TYPE = INSERT(IMPO.TYPE,1,REF.NO,0,"")
  IMPO.MR.FCTR = INSERT(IMPO.MR.FCTR,1,REF.NO,0,"")
  IMPO.RUN.FCTR = INSERT(IMPO.RUN.FCTR,1,REF.NO,0,"")
  S$DATA(12) = INSERT(S$DATA(12),S$SCR,REF.NO,0,"")
  S$DATA(13) = INSERT(S$DATA(13),S$SCR,REF.NO,0,"")
  S$DATA(14) = INSERT(S$DATA(14),S$SCR,REF.NO,0,"")
  S$DATA(15) = INSERT(S$DATA(15),S$SCR,REF.NO,0,"")
*     S$DATA(16) = INSERT(S$DATA(16),S$SCR,REF.NO,0,"")
  S$DATA(17) = INSERT(S$DATA(17),S$SCR,REF.NO,0,"")
  S$DATA(18) = INSERT(S$DATA(18),S$SCR,REF.NO,0,"")
  RETURN
*
*---- LOAD MATRIX
*
80000*
  S$DATA(12)<S$SCR> = IMPO.ID
  S$DATA(13)<S$SCR> = IMPO.DESC
  S$DATA(14)<S$SCR> = IMPO.SIG.PER.SHT
  S$DATA(15)<S$SCR> = IMPO.PGS.PER.SHT
*     S$DATA(16)<S$SCR> = IMPO.TYPE
  S$DATA(17)<S$SCR> = IMPO.MR.FCTR
  S$DATA(18)<S$SCR> = IMPO.RUN.FCTR
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
  SCREEN CLOSE
END
