***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RECOVER
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 03/26/87
*
* DESCRIPTION
*
* This program is used to recover purged estimates.  The purged estimates
* are stored on the ESTIMATE.PURGE.BU file until cleared by operations.
*
*T23278 markt 11/19/1998 * Add check for divisional security.
*T25237 cm 06/16/2000 * Add recovery of Roll Label estimate information
*                       from the ESTIMATE.RES and the ESTIMATE.RL files.
**************************************************************************
*
*--------- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*T25237 v
*COPY>JES.CPYLIB>ESTIMATE.RES
*T25237 ^
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- OPEN ALL FIILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
      ERRMSG="CANNOT OPEN COMPANY FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
      ERRMSG="CANNOT OPEN CONTROL FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","PREFIX" TO PREFIX ELSE
      ERRMSG="CANNOT OPEN PREFIX FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","ESTIMATE" TO ESTIMATE ELSE
      ERRMSG="CANNOT OPEN ESTIMATE FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
      ERRMSG="CANNOT OPEN JES.SCREENS FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","ESTIMATE.DEPT" TO ESTIMATE.DEPT ELSE
      ERRMSG="CANNOT OPEN ESTIMATE.DEPT FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","ESTIMATE.PURGE.BU" TO ESTIMATE.PURGE.BU ELSE
      ERRMSG="CANNOT OPEN ESTIMATE.PURGE.BU FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","CUST.EST.XREF" TO CUST.EST.XREF ELSE
      ERRMSG="CANNOT OPEN CUST.EST.XREF FILE"
      GOSUB 90000
      STOP
  END
  OPEN "","PROSPECT.EST.XREF" TO PROSPECT.EST.XREF ELSE
      ERRMSG="CANNOT OPEN PROSPECT.EST.XREF FILE"
      GOSUB 90000
      STOP
  END
*T25237 v
  OPEN "","ESTIMATE.RES" TO ESTIMATE.RES ELSE
     ERRMSG="CANNOT OPEN ESTIMATE.RES FILE"
     GOSUB 90000
     STOP
  END
  OPEN "","ESTIMATE.RL" TO ESTIMATE.RL ELSE
     ERRMSG="CANNOT OPEN ESTIMATE.RL FILE"
     GOSUB 90000
     STOP
  END
*T25237 ^
*
*---- INITIALIZATION
*
  TODAY = DATE()
  CONO=""
  MAT COMP.REC=""
  CALL GET.CONO1 (CONO, MAT COMP.REC, COMPANY, CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RECOVER
  SCREEN FORMAT;EST.RECOVER
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;EST.RECOVER;1
*
*---- MAIN PROCESSING
*
100*
  RELEASE
  S$DATA(2)<S$SCR> = ""
  SCREEN FIELD;EST.RECOVER;2
  SCREEN INPUT;EST.RECOVER;2
  IF S$VALUE = "END" THEN GOTO 99999
  EST.KEY = S$VALUE
  MATREADU EST.REC FROM ESTIMATE.PURGE.BU, CONO:EST.KEY ELSE
      P_X  = 3 ; P_Y = 23 ; P_VALUE = "Estimate number ":EST.KEY:" cannot be recovered." ; P_OPT = "CL"
      CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
      GOTO 100
  END
*T23278 v
  DIV.CODE = EST.DIV; USER.ID = UPCASE(@LOGNAME); ERRMSG = ''
  CALL SI.CK.DIV.SEC.SUB(CONO,DIV.CODE,USER.ID,ERRMSG)
  IF ERRMSG # '' THEN
      GOSUB 91000; GOTO 100
  END
*T23278 ^
  GOSUB 2000
  GOTO 100
*
*---- RECOVER ESTIMATE
*
2000*
  P_X  = 3 ; P_Y = 23 ; P_VALUE = "Processing Estimate - ":EST.KEY ; P_OPT = "CL"
  CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RCV.KEY = EST.KEY
  RCV.DEPT.COMP = EST.DEPT.COMP
  RCV.SUBS = EST.SUBS
  IF RCV.SUBS # "" THEN
      SAVE.KEY = RCV.KEY
      SAVE.DEPT.COMP = RCV.DEPT.COMP
      SC = COUNT(RCV.SUBS,VM) + (RCV.SUBS # "")
      FOR SP = 1 TO SC
          RCV.KEY = RCV.SUBS<1,SP>
          MATREADU EST.REC FROM ESTIMATE.PURGE.BU, CONO:RCV.KEY ELSE GOTO 2090
          RCV.DEPT.COMP = EST.DEPT.COMP
          GOSUB 3000
2090  NEXT SP
      RCV.KEY = SAVE.KEY
      RCV.DEPT.COMP = SAVE.DEPT.COMP
      MATREADU EST.REC FROM ESTIMATE.PURGE.BU, CONO:RCV.KEY ELSE
          MAT EST.REC = ""
      END
  END
  GOSUB 3000
  P_X  = 3 ; P_Y = 23 ; P_VALUE = "** Recovery Complete **" ; P_OPT = "CL"
  CALL VSI_SI_PLINE(P_X,P_Y,P_VALUE,P_OPT)
  RETURN
*
*---- RESTORE ESTIMATE RECORDS
*
3000*
  DC = COUNT(RCV.DEPT.COMP,VM) + (RCV.DEPT.COMP # "")
  FOR DP = 1 TO DC
      DDKEY = RCV.KEY:"!":RCV.DEPT.COMP<1,DP>
      READU REC FROM ESTIMATE.PURGE.BU, CONO:DDKEY ELSE
          RELEASE ESTIMATE.PURGE.BU, CONO:DDKEY
          GOTO 3090
      END
      WRITE REC ON ESTIMATE.DEPT, CONO:DDKEY
      DELETE ESTIMATE.PURGE.BU, CONO:DDKEY
3090 NEXT DP
  MATWRITE EST.REC ON ESTIMATE, CONO:RCV.KEY
  BEGIN CASE
      CASE EST.CUST = "P"
          CALL GEN.XREF.MAINT(CONO,EST.KEY,"",EST.CUST.NAME,PROSPECT.EST.XREF,PREFIX)
      CASE 1
          CALL GEN.XREF.MAINT(CONO,EST.KEY,"",EST.CUST,CUST.EST.XREF,PREFIX)
  END CASE
  DELETE ESTIMATE.PURGE.BU, CONO:RCV.KEY
*T25237 v
  MATREAD EST.RL.REC FROM ESTIMATE.PURGE.BU, CONO:RCV.KEY:'!RES' THEN
     MATWRITE EST.RL.REC ON ESTIMATE.RES, CONO:RCV.KEY
     DELETE ESTIMATE.PURGE.BU, CONO:RCV.KEY:'!RES'
  END
  READ OLD.REC FROM ESTIMATE.PURGE.BU, CONO:RCV.KEY:'!RL' THEN
     WRITE OLD.REC ON ESTIMATE.RL, CONO:RCV.KEY
     DELETE ESTIMATE.PURGE.BU, CONO:RCV.KEY:'!RL'
  END
*T25237 ^
  RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
***** T23278 v
*
*---- SYSCOM MESSSAGES
*
91000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
92000 ERR.TYPE=2;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
93000 ERR.TYPE=3;CALL SI_SYSCOM(MAT SYSCOM.REC)
*
***** T23278 ^
*
*---- END OF PROGRAM
*
99999*
STOP
END
