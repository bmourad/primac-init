**********************************************************
* REVISION    - [08.0]
* PROGRAM     - JOB.JACKET.TICKET
* BY          - BILAL MOGHRABI, C.B.A.
* DATE        - 02/02/88
* DESCRIPTION - This program prints the job jacket ticket.
*
* MOD         - CSF 20020 MWS 11/9/93 CHANGE DREF COUNTER TO RP TO
*               KEEP ESTIMATE.DESC.GRP AND ESTIMATE.GDESC FILES
*               IN SYNC.
*
*T26138 cm 10/18/2001 * Expand qty from 8 to 11 digits.
**********************************************************
*
*---- COPY STATEMENTS
*
*COPY>PMC.CPYLIB>COMPANY
*COPY>JCS.CPYLIB>JOB
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>JES.CPYLIB>ESTIMATE.DESC.GRP
*COPY>CPYLIB>CHAR
*
*---- DEFINE PRINT IMAGE
*
      DIM PL(132)
*
*---- OPEN ALL FILES
*
      OPEN "","COMPANY" TO COMPANY ELSE ERRMSG = "CANNOT OPEN COMPANY FILE";GOSUB 90000;GOTO 99999
      OPEN "","CONTROL" TO CONTROL ELSE ERRMSG = "CANNOT OPEN CONTROL FILE";GOSUB 90000;GOTO 99999
      OPEN "","JOB" TO JOB ELSE ERRMSG = "CANNOT OPEN JOB FILE";GOSUB 90000;GOTO 99999
      OPEN "","CUSTOMER" TO CUSTOMER ELSE ERRMSG = "CANNOT OPEN CUSTOMER FILE";GOSUB 90000;GOTO 99999
      OPEN "","ESTIMATE.DESC.GRP" TO ESTIMATE.DESC.GRP ELSE ERRMSG = "CANNOT OPEN ESTIMATE.DESC.GRP FILE";GOSUB 90000;GOTO 99999
      OPEN "","ESTIMATE.GDESC" TO ESTIMATE.GDESC ELSE ERRMSG = "CANNOT OPEN ESTIMATE.GDESC FILE";GOSUB 90000;GOTO 99999
*
*---- INITIALIZATION
*
      CONO = ""
      CALL GET.CONO1 (CONO,MAT COMP.REC,COMPANY,CONTROL)
      ESC = CHAR(27)
      MASK = STR("X",38)
      SLINE = CS
      SLINE = SLINE:@(0,0):"C.B.A.                     J O B  J A C K E T"
      SLINE = SLINE:@(0,1):STR("-",80)
      SLINE = SLINE:@(0,22):STR("-",80)
      PRINT SLINE:
*
*---- ALIGNMENT ROUTINE
*
50*
      PRINT @(20,12):CL:"Print alignment (Y/N) : ":
      INPUT REPLY,5 _:
      BEGIN CASE
      CASE REPLY = "END"
         STOP
      CASE REPLY = "Y"
51       JOB.NO = MASK
         MAT JOB.REC = MASK
         MAT CUST.REC = MASK
         ESTGD.REC = ""
         JOB.DIV = "01"
         ALIGN = "Y"
         GOSUB 1000
52       PRINT@(5,15):"Did the alignment routine print satisfactorily ? (RTN/N): ":;INPUT AN
         PRINT@(5,15):SPACE(65)
         IF AN = "N" THEN GOTO 51
         IF AN = "END" THEN STOP
         IF AN # "" THEN GOTO 52
         ALIGN = "N"
      CASE REPLY = "N"
         ALIGN = "N"
      CASE 1
         ERRMSG = "Invalid reply. Try again! "
         GOSUB 90000
         GOTO 50
      END CASE
*
*---- MAIN PROCESSING
*
      JOB.NOS = ""
      JPTR = 0
100*
      PRINT @(20,12):CL:"Job Number : ":
      INPUT JOB.NO,8 _:
      IF JOB.NO = "" THEN GOTO 100
      IF JOB.NO = "END" THEN GOTO 500
      MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE
         PRINT @(0,23):"Job ":JOB.NO:" Is Not On File":
         INPUT REPLY,1 _:
         PRINT @(0,23):@(-4):
         GOTO 100
      END
      JPTR = JPTR + 1
      JOB.NOS<JPTR> = JOB.NO
      GOTO 100
500   FOR J = 1 TO JPTR
         JOB.NO = JOB.NOS<J>
         MATREAD JOB.REC FROM JOB, CONO:JOB.NO ELSE MAT JOB.REC = ''
         MATREAD CUST.REC FROM CUSTOMER,CONO:JOB.CUST ELSE MAT CUST.REC = ""
         READ ESTGD.REC FROM ESTIMATE.GDESC, CONO:JOB.NO ELSE ESTGD.REC = ""
         GOSUB 1000
599   NEXT J
      GOTO 99999
*
*---- PROCESS ENTRY DATA
*
1000*
      MAT PL = SPACE(100)
      SELECT ESTIMATE.DESC.GRP
1200*
      READNEXT ID ELSE GOTO 1300
      IF ID[1,3] # CONO THEN GOTO 1200
      GRP.ID = ID[4,99]
      DIV = FIELD(GRP.ID,"-",1)
      GRP = FIELD(GRP.ID,"-",2)
*     IF DIV # JOB.DIV THEN GOTO 1200
      MATREAD ESTDG.REC FROM ESTIMATE.DESC.GRP, ID ELSE
         GOTO 1200
      END
      BZ = DCOUNT(ESTGD.REC<GRP>,VM)
      FOR A = 1 TO BZ
         IF ESTGD.REC<GRP,A> = "Y" THEN
            ESTGD.REC<GRP,A> = "X"
         END
         IF ESTGD.REC<GRP,A> = "N" THEN
            ESTGD.REC<GRP,A> = ""
         END
      NEXT A
      RC = DCOUNT(ESTDG.DREF,VM)
      FOR RP = 1 TO RC
         DREF = ESTDG.DREF<1,RP>
         DROW = ESTDG.DROW<1,RP>
         DCOL = ESTDG.DCOL<1,RP>
         DJST = ESTDG.JUSTIFY<1,RP>
         DLEN = ESTDG.MAXL<1,RP>
         IF DROW # "" AND DCOL # "" THEN
            FM = DJST:"#":DLEN
            IF ALIGN = "Y" THEN
               PL(DROW) = PL(DROW)[1,DCOL-1] : MASK FM : PL(DROW)[DCOL+DLEN,132]
            END ELSE
* CSF 20020
*               PL(DROW) = PL(DROW)[1,DCOL-1] : ESTGD.REC<GRP,DREF> FM : PL(DROW)[DCOL+DLEN,132]
                PL(DROW) = PL(DROW)[1,DCOL-1] : ESTGD.REC<GRP,RP> FM : PL(DROW)[DCOL+DLEN,132]
*
            END
         END
      NEXT RP
      GOTO 1200
*
*---- PROCESS DATA FROM OTHER SOURCES
*
1300*
      NW = 2                        ;* NARROW WIDTH IN 1/2 POINTS
      WW = 5                        ;* WIDE WIDTH IN 1/2 POINTS
      IW = 3                        ;* INTERCHAR WIDTH IN 1/2 POINTS
      PL(9) = PL(9)[1,15]:"  ":PL(9)[18,82]
      PL(9) = PL(9)[1,75]:JOB.NO "L#8":PL(9)[82,16]
      PL(9) = PL(9)[1,85]:JOB.CUST "L#6":PL(9)[83,17]
      PL(10) = PL(10)[1,86]
      PL(19) = PL(19)[1,99]
      NEW.JOB.DESC = ""
      LEN = 38
      CALL DESC.REFORMAT(JOB.DESC,LEN,NEW.JOB.DESC)
*T26138 v
*     PL(20) = PL(20)[1,5]:OCONV(JOB.QTY<1,1>,"MD0,") "R#8 ":NEW.JOB.DESC<1,1> "L#38" : PL(20)[53,132]
      PL(20) = PL(20)[1,5]:OCONV(JOB.QTY<1,1>,"MD0,") "R#11 ":NEW.JOB.DESC<1,1> "L#35" : PL(20)[53,132]
*T26138 ^
      PL(21) = PL(21)[1,14]:NEW.JOB.DESC<1,2> "L#38" : PL(21)[53,132]
      PL(22) = PL(22)[1,14]:NEW.JOB.DESC<1,3> "L#38" : PL(22)[53,132]
      PL(23) = PL(23)[1,14]:NEW.JOB.DESC<1,4> "L#38" : PL(23)[53,132]
      PL(24) = PL(24)[1,14]:NEW.JOB.DESC<1,5> "L#38" : PL(24)[53,132]
      PL(25) = PL(25)[1,14]:NEW.JOB.DESC<1,6> "L#38" : PL(25)[53,132]
      PL(26) = PL(26)[1,14]:NEW.JOB.DESC<1,7> "L#38" : PL(26)[53,132]
      IF ESTGD.REC<50,2> # "" OR ESTGD.REC<50,3> # "" THEN
         PL(61)[76,1] = "X"
      END
*
*---- PRINT JOB JACKET
*
2000*
         PRINTER ON
         FOR L = 1 TO 84
            PLINE = PL(L)
            PLINE = PLINE[1,100]
            PRINT PLINE
         NEXT L
*        FOR K = 1 TO 86
**          PRINT
*        NEXT K
         PRINTER OFF
         PRINTER CLOSE
         RETURN
*
*---- ERROR ROUTINE
*
90000*
         PRINT @(0,23):CL:ERRMSG:
         INPUT REPLY,1:
         PRINT @(0,23):CL:
         RETURN
*
*---- END OF JOB
*
99999*
      END
