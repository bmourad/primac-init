   SUBROUTINE EST.FORMULA.Q740 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q740
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate run time requirements for trimming.
*
* T27924 cmykleb 03/17/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.QTY = ""
   TYP = 0
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE QPTR = 1
*T27924 v
   CQTY = EST.COMP.QTY<1,COMP,QPTR>
   BEGIN CASE
      CASE CQTY=""
         COMPQTY = EQTY
      CASE (CQTY[1,1]="+" OR CQTY[1,1]="-") AND CQTY[LEN(CQTY),1]="%"
         COMPQTY = INT(EQTY * (1 + (CQTY[1,LEN(CQTY)-1]/100)) + 0.5)
      CASE CQTY[1,1]="+" OR CQTY[1,1]="-"
         COMPQTY = EQTY + CQTY
      CASE 1
         COMPQTY = CQTY
   END CASE
   IF COMPQTY < 0 THEN COMPQTY = 0
*T27924 ^
   MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:TEMP.CCTR ELSE
      GOTO 99999
   END
   TOT.BULK = 0
*T27924 v
*  C2 = COUNT(EST.BIND.FOLD.BULK,VM) + (EST.BIND.FOLD.BULK # "")
*  FOR C = 1 TO C2
*     TOT.BULK = TOT.BULK + EST.BIND.FOLD.BULK<1,C>
*  NEXT C
   C2 = DCOUNT(EST.BIND.FOLD.BULK<1,COMP>,SM)
   FOR C = 1 TO C2
      TOT.BULK = TOT.BULK + EST.BIND.FOLD.BULK<1,COMP,C>
   NEXT C
*T27924 ^
10*
   BEGIN CASE
      CASE TOT.BULK > 0
         NULL
      CASE EQP.TRIMMER.TYPE = "C"
         NULL
      CASE QPTR = 1 AND ACTION # "R"
         IF TYP = 0 THEN CALL EDIT.SUB1 (MAT EDIT.COM)
         X=0;Y=23;TYP=14;SCALER=4;MAXL=7
         BEGIN CASE
            CASE TEMP.FVAR1 # ""
               DEFAULT = TEMP.FVAR1 / 10000
            CASE 1
               DEFAULT = ""
         END CASE
         IF DEFAULT = "" THEN
            PMSG = "Total bulk ?"
         END ELSE
            O.R = "O"
            PMSG = "Total bulk (Default = ":DEFAULT:") ?"
         END
         CALL EDIT.SUB1 (MAT EDIT.COM)
         IF VALUE = "END" THEN RETURN
         IF VALUE = "" THEN GOTO 10
         TOT.BULK = VALUE
         TEMP.FVAR1 = VALUE
      CASE 1
         TOT.BULK = TEMP.FVAR1
   END CASE
   TOT.BULK = TOT.BULK / 10000
*
*---- MAIN PROCESSING
*
*T27924 v
*  PROD.QTY = EQTY + FIELD(EST.PROD.BS.REQ<1,COMP,1>,"!",QPTR)
   PROD.QTY = COMPQTY + FIELD(EST.PROD.BS.REQ<1,COMP,1>,"!",QPTR)
*T27924 ^
   BEGIN CASE
      CASE EQP.TRIMMER.TYPE = "P"
         IF TOT.BULK + 0 = 0 THEN
            TEMP.QTY = ""
         END ELSE
            BEGIN CASE
               CASE TOT.BULK + 0 = 0
                  TEMP.QTY = ""
               CASE INT((EQP.TRIMMER.MAX.BULK/10000) / TOT.BULK) + 0 = 0
                  TEMP.QTY = ""
               CASE 1
                  TEMP.QTY = INT(PROD.QTY / INT((EQP.TRIMMER.MAX.BULK/10000) / TOT.BULK) + 0.99)
            END CASE
         END
      CASE EQP.TRIMMER.TYPE = "C"
         TEMP.QTY = PROD.QTY
   END CASE
   TEMP.VSALE = "Y"
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
