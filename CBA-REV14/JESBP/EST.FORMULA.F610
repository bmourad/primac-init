   SUBROUTINE EST.FORMULA.F610 (CONO, ACTION, EQTY, DEPT, COMP)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.F610
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to determine the run efficiency factor for the
* cutter run operation. The factor is basis upon the basis weight of
* the stock to be cut, the size of the sheets to be cut and the
* number of cuts to be made.
*
* T27924 cmykleb 03/17/2004 * Move estimate qty to the component level.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.CUT.EFF
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.FCTR = 1000
   TYP = 0
   IF TEMP.MPTR = "" THEN MPTR = 1 ELSE MPTR = TEMP.MPTR
*T27924 v
*  BASIS = EST.BIND.CUT.BASIS<1,MPTR>
*  WIDTH = EST.BIND.CUT.WIDTH<1,MPTR> / 10000
*  XXLENGTH = EST.BIND.CUT.LENGTH<1,MPTR> / 10000
*  CUTS = EST.BIND.CUT.CUTS<1,MPTR>
   BASIS = EST.BIND.CUT.BASIS<1,COMP,MPTR>
   WIDTH = EST.BIND.CUT.WIDTH<1,COMP,MPTR> / 10000
   XXLENGTH = EST.BIND.CUT.LENGTH<1,COMP,MPTR> / 10000
   CUTS = EST.BIND.CUT.CUTS<1,COMP,MPTR>
*T27924 ^
   FND = 0
   FOR BAS = BASIS TO 500 UNTIL FND
      FND = 1
      MATREAD CEFF.REC FROM ESTIMATE.CUT.EFF, CONO:BAS ELSE FND = 0
   NEXT BAS
   IF NOT(FND) THEN GOTO 99999
*
*---- MAIN PROCESSING
*
   SQIN = INT(WIDTH * XXLENGTH + 0.5)
   LOCATE SQIN IN CEFF.QTY<1>,1 BY "AR" SETTING Y2 ELSE NULL
   BEGIN CASE
      CASE Y2 = 1
         Y1 = 1
      CASE Y2 > COUNT(CEFF.QTY,VM)+1
         Y2 = Y2 - 1
         Y1 = Y2
      CASE 1
         Y1 = Y2 - 1
   END CASE
   BEGIN CASE
      CASE CEFF.EXTR = "Y"
         TF2 = CEFF.FCTR<1,Y2,CUTS>
         TF1 = CEFF.FCTR<1,Y1,CUTS>
         IF CEFF.QTY<1,Y2> = CEFF.QTY<1,Y1> THEN
            TF = TF2
         END ELSE
            TF = INT(TF2-(CEFF.QTY<1,Y2>-SQIN)/(CEFF.QTY<1,Y2>-CEFF.QTY<1,Y1>)*(TF2-TF1)+0.5)
         END
      CASE 1
         TF = CEFF.FCTR<1,Y2,CUTS>
   END CASE
   TEMP.FCTR = TF * 10
   IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
*
*---- END OF PROGRAM
*
99999*
   RETURN
END
