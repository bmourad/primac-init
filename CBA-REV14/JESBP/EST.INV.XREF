      SUBROUTINE EST.INV.XREF (CONO, ACTION, PLINE, XBASIS, FIN, CLR, WIDTH, XXLENGTH, SELECTION)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.INV.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/14/86
*
* DESCRIPTION
*
* This subroutine displays all inventory products with specified product
* line and finish. Color, width and length may be optionally specified to
* reduce list of items displayed.
*
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>ICS.CPYLIB>INVENTORY
*COPY>ICS.CPYLIB>INV.WHSE
*COPY>ICS.CPYLIB>INV.CNV
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- DEFINE X-REF FILE
*
      DIM XREF(10)
      EQU X.PROD      TO XREF(1)
      EQU X.COLOR     TO XREF(2)
      EQU X.LENGTH    TO XREF(3)
      EQU X.WIDTH     TO XREF(4)
*
*---- OPEN ALL FILES
*
      OPEN "","INV.WHSE" TO INV.WHSE ELSE
         ERRMSG = "Cannot open INV.WHSE file"
         GOSUB 90000
         GOTO 99999
      END
*
*---- INITIALIZATION
*
      SELECTION = ""
      SCREEN DEFINE;EST.INV.XREF
      SCREEN CLEAR;EST.INV.XREF;D
      SCREEN FORMAT
      SCREEN COUNT;EST.INV.XREF;11
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
      BASIS = XBASIS
      IF BASIS # "" AND NUM(BASIS) THEN BASIS = BASIS*100
      GOTO 110
*
*---- MAIN PROCESSING
*
100*
      SCREEN CLEAR
110*
      S$DATA(1)<S$SCR> = DATE()
      S$DATA(2)<S$SCR> = PLINE
      S$DATA(3)<S$SCR> = BASIS
      S$DATA(4)<S$SCR> = FIN
      S$DATA(5)<S$SCR> = CLR
      S$DATA(6)<S$SCR> = WIDTH
      S$DATA(7)<S$SCR> = XXLENGTH
      SCREEN DISPLAY;EST.INV.XREF;ALL
      FOR REF = 1 TO 6
         ON REF GOSUB 1100,1200,1300,1400,1500,1600
         IF S$VALUE = "END" THEN GOTO 99999
      NEXT REF
      MATREAD XREF FROM PRODUCT.XREF, CONO:PLINE:FIN:BASIS ELSE
         MAT XREF = ""
      END
      CURR.REF.NO = ""
      LINE.CNT = 0
      CNT = COUNT(X.PROD,VM) + (X.PROD # "")
      FOR C = 1 TO CNT
         IF CLR # "" AND CLR # X.COLOR<1,C> THEN GOTO 190
*        IF WIDTH # "" AND WIDTH # INT(X.WIDTH<1,C>/100) THEN GOTO 190      ;* REL 07.0
         IF WIDTH # "" AND WIDTH # INT(X.WIDTH<1,C>/10000) THEN GOTO 190    ;* REL 08.0
*        IF LENGTH # "" AND LENGTH # INT(X.LENGTH<1,C>/100) THEN GOTO 190   ;* REL 07.0
         IF XXLENGTH # "" AND XXLENGTH # INT(X.LENGTH<1,C>/10000) THEN GOTO 190 ;* REL 08.0
         LINE.CNT = LINE.CNT + 1
         S$DATA(12)<S$SCR,LINE.CNT> = X.PROD<1,C>
         MATREAD INV.REC FROM INVENTORY, CONO:X.PROD<1,C> ELSE
            MAT INV.REC = ""
            INV.FULL.DESC = "????????????????????"
         END
         S$DATA(13)<S$SCR,LINE.CNT> = INV.FULL.DESC
         AVAIL = 0
         WCNT = COUNT(INV.WHSE.CODE,VM) + (INV.WHSE.CODE # "")
         FOR WP = 1 TO WCNT
            MATREAD IWH.REC FROM INV.WHSE, CONO:X.PROD<1,C>:"!":INV.WHSE.CODE<1,WP> ELSE
               MAT IWH.REC = ""
            END
            AVAIL = AVAIL + (IWH.ON.HAND - IWH.RESV)
         NEXT WP
         MAT INV.CNV.REC = ''
         BEGIN CASE
         CASE INV.UNIT<1,2> = 'SHT' AND INV.UNIT<1,3> = 'LBS'
            ICR.CNV = "MD0"
*           ICR.DV1 = INV.M.WT; ICR.MT1 = 1000; ICR.DV2 = 1   ;* REL 07.0
            ICR.DV1 = INV.M.WT; ICR.MT1 = 1; ICR.DV2 = 1      ;* REL 08.0
         CASE 1
            ICR.CNV = "MD2"
*           ICR.DV1 = 1; ICR.MT1 = 1; ICR.DV2 = 1     ;* REL 07.0
            ICR.DV1 = 10 ; ICR.MT1 = 1; ICR.DV2 = 1   ;* REL 08.0
         END CASE
         IF AVAIL < 0 THEN
            AVAIL = INT(((AVAIL / ICR.DV1) * ICR.MT1) / ICR.DV2 - .5)
         END ELSE
            AVAIL = INT(((AVAIL / ICR.DV1) * ICR.MT1) / ICR.DV2 + .5)
         END
         AVAIL = INT(OCONV(AVAIL,ICR.CNV))
         S$DATA(14)<S$SCR,LINE.CNT> = AVAIL
190   NEXT C
      SCREEN DISPLAY;EST.INV.XREF;ALL
      REF.NO = 1
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
500*
      SCREEN FIELD;EST.INV.XREF;21
      SCREEN INPUT;EST.INV.XREF;21
      OPT = S$VALUE
      BEGIN CASE
         CASE OPT # "" AND NUM(OPT)
            IF OPT < CURR.REF.NO OR OPT > LINE.CNT OR OPT > (CURR.REF.NO + LINE.COUNT - 1) THEN
               ERRMSG = "** OUT OF RANGE **"
               GOSUB 90000
            END ELSE
               SELECTION = S$DATA(12)<S$SCR,OPT>
               GOTO 99999
            END
         CASE OPT = "N"
            GOTO 100
         CASE OPT = "E" OR OPT = "END"
            GOTO 99999
         CASE OPT = "S" OR OPT = "SF"
            REF.NO = CURR.REF.NO + LINE.COUNT
            IF REF.NO > LINE.CNT THEN REF.NO = 1
            GOSUB 50000
         CASE OPT = "ST"
            REF.NO = 1
            GOSUB 50000
         CASE OPT = "SB"
            REF.NO = LINE.CNT
            GOSUB 50000
         CASE OPT = "SR"
            REF.NO = REF.NO - LINE.COUNT
            IF REF.NO < 1 THEN REF.NO = 1
            GOSUB 50000
      END CASE
      GOTO 500
*
*---- GET PRODUCT LINE
*
1100*
      SCREEN FIELD;EST.INV.XREF;2
      SCREEN INPUT;EST.INV.XREF;2;RETURN
      PLINE = S$VALUE
      RETURN
*
*---- GET BASIS WEIGHT
*
1200*
      SCREEN FIELD;EST.INV.XREF;3
      SCREEN INPUT;EST.INV.XREF;3;RETURN
      BASIS = S$VALUE
      RETURN
*
*---- GET PAPER FINISH
*
1300*
      SCREEN FIELD;EST.INV.XREF;4
      SCREEN INPUT;EST.INV.XREF;4;RETURN
      FIN = S$VALUE
      RETURN
*
*---- GET PAPER COLOR
*
1400*
      SCREEN FIELD;EST.INV.XREF;5
      SCREEN INPUT;EST.INV.XREF;5;RETURN
      CLR = S$VALUE
      RETURN
*
*---- GET PAPER WIDTH
*
1500*
      SCREEN FIELD;EST.INV.XREF;6
      SCREEN INPUT;EST.INV.XREF;6;RETURN
      WIDTH = S$VALUE
      RETURN
*
*---- GET PAPER LENGTH
*
1600*
      SCREEN FIELD;EST.INV.XREF;7
      SCREEN INPUT;EST.INV.XREF;7;RETURN
      XXLENGTH = S$VALUE
      RETURN
*
*---- SCROLL DEPARTMENT
*
50000*
      START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = COUNT(S$DATA(12)<S$SCR>,VM) + (S$DATA(12)<S$SCR> # "")
      SCREEN MULTI;EST.INV.XREF;C;11;12;13;14
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*       PRINT @(0,23):CL:ERRMSG:
*       INPUT REPLY:
*       PRINT @(0,23):CL:
*       RETURN
*
*---- END OF PROGRAM
*
99999*
      SCREEN CLEAR;EST.INV.XREF;D
      RETURN
   END
