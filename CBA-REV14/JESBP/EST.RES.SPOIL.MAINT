*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RL.SPOIL.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 11/25/86
*
* MOD BY   - KERRY WILSON
* MOD DATE - 03/14/90
* MOD TASK - 14166
* MOD REAS - ADD INITIAL AND SUBSEQUENT MAKEREADY
*
* DESCRIPTION
*
* This program is used to maintain the equipment efficiency table.
*
* MODIFIED - Graham Jarvis, Pacrim Software Pty Ltd
*C30727 gat 04/09/1998 * FIX WRITE ON DUPLICATE.
*T26138 cm 09/20/2001 * Expand qty from from 8 to 9 digits.
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
*T26126 adelgado 03/11/2002 * Implement the LOCKED clause for READU.
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.RES.PRESS.SPOIL
*COPY>PMC.CPYLIB>COMPANY
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
  MAT SYSCOM.REC = ""; SYS.TYPE = 2
*
*---- OPEN ALL FILES
*
  OPEN "","COMPANY" TO COMPANY ELSE
    ERRMSG = "CANNOT OPEN COMPANY FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","CONTROL" TO CONTROL ELSE
    ERRMSG = "CANNOT OPEN CONTROL FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","COST.CNTR" TO COST.CNTR ELSE
    ERRMSG = "CANNOT OPEN COST.CNTR FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","JES.SCREENS" TO M.SCREENS ELSE
    ERRMSG = "CANNOT OPEN JES.SCREENS FILE"
    GOSUB 90000
    STOP
  END
  OPEN '','EQUIPMENT' TO EQUIPMENT ELSE
    ERRMSG = "CANNOT OPEN EQUIPMENT FILE"
    GOSUB 90000
    STOP
  END
  OPEN "","ESTIMATE.RES.PRESS.SPOIL" TO ESTIMATE.RES.PRESS.SPOIL ELSE
    ERRMSG = "CANNOT OPEN ESTIMATE.RES.PRESS.SPOIL FILE"
    GOSUB 90000
    STOP
  END
  CONO = ""
  CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
  SCREEN INIT;#
  S$SCR = 1
  SCREEN DEFINE;EST.RES.SPOIL.MAINT
  SCREEN FORMAT
  SCREEN COUNT;;11
  LINE.COUNT = S$LCNT
  LINE.SPACE = S$LSPC
  DUP.FLG = 0
  NEW.REC=1
  DONE=0
  GOTO 110
*
*---- MAIN PROCESSING
*
100 *
  RELEASE
  SCREEN CLEAR
110 *
  S$DATA(1)<S$SCR> = DATE()
  SCREEN DISPLAY;;1
  SCREEN FIELD;;2
  SCREEN INPUT;;2
  BEGIN CASE
    CASE S$VALUE = 'D'
      SCREEN FIELD;;23
      SCREEN INPUT;;23
      IF S$VALUE = "" OR S$VALUE = "END" THEN GOTO 100
      MATREAD PST.REC FROM ESTIMATE.RES.PRESS.SPOIL, CONO:S$VALUE ELSE
        ERRMSG = "Cannot locate press spoilage table. Try again! "
        GOSUB 90000
        GOTO 100
      END
120 *
      SCREEN FIELD;;24
      SCREEN INPUT;;24
      IF S$VALUE = "" OR S$VALUE = "END" THEN GOTO 100
      DTBL = S$VALUE
      EQP.KEY=DTBL[1,3]
      MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:DTBL[1,3] ELSE
        ERRMSG = "Invalid equipment ID. Try again !"
        GOSUB 90000
        GOTO 120
      END
      IF EQP.TYPE # "40" THEN
        ERRMSG = "Equipment not a Roll Label Press ID"
        GOSUB 90000
        GOTO 120
      END
*        MATWRITE PST.REC ON ESTIMATE.RES.PRESS.SPOIL,CONO:DTBL
*        GOTO 100
      DUP.FLG = 1
      NEW.REC = 0
      S$DATA(2)<S$SCR> = DTBL
      SCREEN DISPLAY;;2
      MATREAD CCTR.REC FROM COST.CNTR, CONO:DTBL ELSE
        MAT CCTR.REC = ""
        CCTR.DESC = "???????????????"
      END
      S$DATA(3)<S$SCR> = CCTR.DESC
      SCREEN DISPLAY;;3
    CASE 1
      IF S$VALUE = "END" THEN GOTO 99999
      EQP.KEY = S$VALUE
      MATREAD EQUIPMENT.REC FROM EQUIPMENT, CONO:EQP.KEY ELSE
        ERRMSG = "Invalid equipment ID. Try again !"
        GOSUB 90000
        GOTO 100
      END
      IF EQP.TYPE # "40" THEN
        ERRMSG = "Equipment not a Roll Label Press ID"
        GOSUB 90000
        GOTO 110
      END
      MATREAD CCTR.REC FROM COST.CNTR, CONO:EQP.KEY ELSE
        MAT CCTR.REC = ""
        CCTR.DESC = "???????????????"
      END
      S$DATA(3)<S$SCR> = CCTR.DESC
      SCREEN DISPLAY;;3
      IF EQP.TYPE = "1" OR EQP.TYPE = "40" THEN
        FOR REF = 1 TO 3
          ON REF GOSUB 1100,1300,1400
          IF S$VALUE = "END" THEN GOTO 100
        NEXT REF
        TBL.KEY = EQP.KEY:"*":PAPER.WEIGHT:"*":INK.COVER
      END ELSE
        TBL.KEY = EQP.KEY
      END
      NEW.REC = 0
      * T26126 v
      MATREADU PST.REC FROM ESTIMATE.RES.PRESS.SPOIL, CONO:TBL.KEY LOCKED
        ERRMSG = 'PRESS SPOILAGE TABLE record is locked by user - ':GETUSERNAME(STATUS())
        GOSUB 90000;GOTO 100 
      END ELSE
      * T26126 ^
        MAT PST.REC = ""
        NEW.REC = 1
      END
  END CASE
  IF NEW.REC THEN
    DONE = 0
    FOR XX = 1 TO 10 UNTIL DONE
      ON XX GOSUB 1600,1700,1750,1800,1900,2000,2100,2200,2300,2400
      IF S$VALUE = "END" THEN DONE = 1
    NEXT XX
    IF DONE THEN
      GOTO 100
    END ELSE
      CNT = 0
      REF.NO = 0
      CURR.REF.NO = ""
      OPT = "A"
      GOTO 510
    END
  END ELSE
    CNT = DCOUNT(PST.QTY,VM)
    S$DATA(9)<S$SCR> = PST.EXTR
    SCREEN DISPLAY;;9
    S$DATA(12)<S$SCR> = PST.QTY
    S$DATA(13)<S$SCR> = PST.PCT
    REF.NO = 1
    CURR.REF.NO = ""
    GOSUB 40000
    GOSUB 50000
    IF DUP.FLG THEN
      DONE = 0
      FOR XX =1 TO 3 UNTIL DONE
        ON XX GOSUB 1100,1300,1400
        IF S$VALUE = 'END' THEN DONE = 1
      NEXT XX
      IF DONE THEN GO 100
      FND = 1
      READU DUMMY FROM ESTIMATE.RES.PRESS.SPOIL,CONO:DTBL:"*":PAPER.WEIGHT:"*":INK.COVER ELSE FND=0
      IF FND THEN
        ERRMSG = "Run efficiency table already present. Try again! "
        GOSUB 90000
        SCREEN CLEAR
        GOTO 100
      END
      TBL.KEY = DTBL:"*":PAPER.WEIGHT:"*":INK.COVER
      DUP.FLG = 0
    END
  END
*
*---- GET OPERATOR REPLY
*
500 *
  SCREEN FIELD;;21
  SCREEN INPUT;;21
  OPT = S$VALUE
510 *
  BEGIN CASE
    CASE OPT = "E" OR OPT = "END"
      GOTO 100
    CASE OPT = "A" AND CNT < 99
      DONE = 0
      FOR REF.NO = CNT + 1 TO 99 UNTIL DONE
        GOSUB 50000
        GOSUB 10100
        IF S$VALUE = "END" THEN
          DONE = 1
          GOSUB 600
        END ELSE
          CNT = CNT + 1
        END
      NEXT REF.NO
      REF.NO = CNT
      CURR.REF.NO = ""
      GOSUB 50000
    CASE OPT = "C" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 10100
      END
    CASE OPT = "I" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 700
        CNT = CNT + 1
        CURR.REF.NO = ""
        GOSUB 50000
        GOSUB 10100
        IF S$VALUE = "END" THEN
          GOSUB 600
          CNT = CNT - 1
        END
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "D" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO = S$VALUE
        GOSUB 600
        CNT = CNT - 1
        IF REF.NO > CNT THEN REF.NO = REF.NO - 1
        CURR.REF.NO = ""
        GOSUB 50000
      END
    CASE OPT = "S"
      REF.NO = CURR.REF.NO + LINE.COUNT
      IF REF.NO > CNT THEN REF.NO = 1
      GOSUB 50000
    * T25978 v
    CASE OPT = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 50000
    CASE OPT = 'ST'
      REF.NO = 1
      GOSUB 50000
    CASE OPT = 'SB'
      REF.NO = CNT
      GOSUB 50000
    * T25978 ^
    CASE NUM(OPT)
      ON OPT GOSUB 1600,1700,1750,1800,1900,2000,2100,2200,2300,2400
      GOTO 500
    CASE OPT='F'
      BEGIN CASE
        CASE PST.EXTR = '' 
          ERRMSG = "Field 1 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.INIT.QTY = '' 
          ERRMSG = "Field 2 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.COLOUR = '' 
          ERRMSG = "Field 3 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.PLATE = '' 
          ERRMSG = "Field 4 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.COL.CHNGE = '' 
          ERRMSG = "Field 5 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.FOIL = '' 
          ERRMSG = "Field 6 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.SSCREEN = '' 
          ERRMSG = "Field 7 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.GRAVURE = '' 
          ERRMSG = "Field 8 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.EMBOSS = '' 
          ERRMSG = "Field 9 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.SPROCKET = '' 
          ERRMSG = "Field 10 is a required field"
          GOSUB 90000
          GOTO 500
        CASE PST.QTY = ''
          ERRMSG = "Must define at least one quantity factor"
          GOSUB 90000
          GOTO 500
      END CASE
      TBL.KEY=EQP.KEY:"*":PAPER.WEIGHT:"*":INK.COVER
      MATWRITE PST.REC ON ESTIMATE.RES.PRESS.SPOIL,CONO:TBL.KEY
      GOTO 100
  END CASE
  GOTO 500
*
*---- DELETE MULTI-LINE DATA
*
600 *
  S$DATA(12) = DELETE(S$DATA(12),S$SCR,REF.NO,0)
  S$DATA(13) = DELETE(S$DATA(13),S$SCR,REF.NO,0)
  PST.QTY = DELETE(PST.QTY,1,REF.NO,0)
  PST.PCT = DELETE(PST.PCT,1,REF.NO,0)
  RETURN
*
*---- INSERT MULTI-LINE DATA
*
700 *
  S$DATA(12) = INSERT(S$DATA(12),S$SCR,REF.NO,0,"")
  S$DATA(13) = INSERT(S$DATA(13),S$SCR,REF.NO,0,"")
  PST.QTY = INSERT(PST.QTY,1,REF.NO,0,"")
  PST.PCT = INSERT(PST.PCT,1,REF.NO,0,"")
  RETURN
*
*---- DIE TYPE (R or F)
*
1100 *
  RETURN
*
*---- NUMBER OF COLOURS
*
1300 *
  SCREEN FIELD;;6
  SCREEN INPUT;;6;RETURN
  PAPER.WEIGHT = S$VALUE
  RETURN
*
*---- FOIL STAMPING (Y or N)
*
1400 *
  SCREEN FIELD;;25
  SCREEN INPUT;;25;RETURN
  INK.COVER = S$VALUE
  RETURN
*
*---- GET EXTRAPOLATE FLAG
*
1600 *
  SCREEN FIELD;;9
  SCREEN INPUT;;9;RETURN
  PST.EXTR = S$VALUE
  RETURN
*
* Base Spoilage
*
1700 *
  SCREEN FIELD;;26
  SCREEN INPUT;;26;RETURN
  PST.INIT.QTY=S$VALUE
  RETURN
*
* Per Colour
*
1750 *
  SCREEN FIELD;;31
  SCREEN INPUT;;31;RETURN
  PST.COLOUR=S$VALUE
  RETURN
*
* Plate Change Spoils
*
1800 *
  SCREEN FIELD;;27
  SCREEN INPUT;;27;RETURN
  PST.PLATE=S$VALUE
  RETURN
*
* Colour Change Spoils
*
1900 *
  SCREEN FIELD;;28
  SCREEN INPUT;;28;RETURN
  PST.COL.CHNGE=S$VALUE
  RETURN
*
* Foil Stamping Spoils
*
2000 *
  SCREEN FIELD;;29
  SCREEN INPUT;;29;RETURN
  PST.FOIL=S$VALUE
  RETURN
*
* Silkscreen Additional Spoilage %
*
2100 *
  SCREEN FIELD;;30
  SCREEN INPUT;;30;RETURN
  PST.SSCREEN=S$VALUE
  RETURN
*
* Gravure Additional %
*
2200 *
  SCREEN FIELD;;32
  SCREEN INPUT;;32;RETURN
  PST.GRAVURE=S$VALUE
  RETURN
*
* Emboss Additional %
*
2300 *
  SCREEN FIELD;;33
  SCREEN INPUT;;33;RETURN
  PST.EMBOSS=S$VALUE
  RETURN
*
* COL & PLATE CHANGE
*
2400 *
  SCREEN FIELD;;34
  SCREEN INPUT;;34;RETURN
  PST.SPROCKET=S$VALUE
  RETURN
*
*
*
*
*---- GET MULTI-LINE DATA
*
10100 *
  S$VAL = REF.NO
  SCREEN DISPLAY;;11
  S$VAL = REF.NO
  SCREEN FIELD;;12
  IF REF.NO = 1 THEN S$MINV = 1 ELSE S$MINV = PST.QTY<1,REF.NO-1>+1
*T26138 v
*  IF REF.NO >= CNT THEN S$MAXV = 99999999 ELSE S$MAXV = PST.QTY<1,REF.NO+1>-1
  IF REF.NO >= CNT THEN S$MAXV = 999999999 ELSE S$MAXV = PST.QTY<1,REF.NO+1>-1
*T26138 ^
  SCREEN INPUT;;12;RETURN
  PST.QTY<1,REF.NO> = S$VALUE
  S$VAL = REF.NO
  SCREEN FIELD;;13
  SCREEN INPUT;;13;RETURN
  PST.PCT<1,REF.NO> = S$VALUE
  RETURN
*
*---- GET LINE REFERENCE NUMBER
*
30000 *
  SCREEN FIELD;;22
  S$MINV = CURR.REF.NO
  S$MAXV = S$MINV + LINE.COUNT - 1
  IF S$MAXV > CNT THEN S$MAXV = CNT
  SCREEN INPUT;;22
  RETURN
*
* Display Data Fields
*
40000 *
  S$DATA(26)<S$SCR>=PST.INIT.QTY
  S$DATA(27)<S$SCR>=PST.PLATE
  S$DATA(28)<S$SCR>=PST.COL.CHNGE
  S$DATA(29)<S$SCR>=PST.FOIL
  S$DATA(30)<S$SCR>=PST.SSCREEN
  S$DATA(31)<S$SCR>=PST.COLOUR
  S$DATA(32)<S$SCR>=PST.GRAVURE
  S$DATA(33)<S$SCR>=PST.EMBOSS
  S$DATA(34)<S$SCR>=PST.SPROCKET
  SCREEN DISPLAY;;ALL
*
*---- DISPLAY MULTI-LINE DATA
*
50000 *
  START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
  IF START.REF.NO = CURR.REF.NO THEN RETURN
  CURR.REF.NO = START.REF.NO
  S$VAL = START.REF.NO
  S$CNT = COUNT(S$DATA(12)<S$SCR>,VM) + (S$DATA(12)<S$SCR> # "")
  SCREEN MULTI;;C;11;12;13
  RETURN
*
*---- ERROR ROUTINE
*
90000 *
  ERR.TYPE = 1; CALL SI_SYSCOM(MAT SYSCOM.REC)
  RETURN
*
*---- END OF PROGRAM
*
99999 *
END
