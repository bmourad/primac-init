   SUBROUTINE EST.FORMULA.Q065 (CONO, ACTION, EQTY, DEPT, COMP)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.FORMULA.Q065
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/08/86
*
* DESCRIPTION
*
* This formula is used to calculate the number of sheets or yards of spine
* cloth required for casebound book.
*
*T27924 cmykleb 03/12/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.BIND.SPOIL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>EDIT.COM
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
   TEMP.QTY = ""
   TYP = 0
   LOCATE EQTY IN EST.QTY<1>,1 SETTING QPTR ELSE RETURN
*T27924 v
   CQTY = EST.COMP.QTY<1,COMP,QPTR>
   BEGIN CASE
      CASE CQTY=""
         COMPQTY = EQTY
      CASE (CQTY[1,1]="+" OR CQTY[1,1]="-") AND CQTY[LEN(CQTY),1]="%"
         COMPQTY = INT(EQTY * (1 + (CQTY[1,LEN(CQTY)-1]/100)) + 0.5)
      CASE CQTY[1,1]="+" OR CQTY[1,1]="-"
         COMPQTY = EQTY + CQTY
      CASE 1
         COMPQTY = CQTY
   END CASE
   IF COMPQTY < 0 THEN COMPQTY = 0
*T27924 ^
   IF TEMP.TYPE[1,1] = "M" THEN
      LOCATE TEMP.OPV IN ESTM.PROD<1>,1 SETTING MP ELSE RETURN
   END
   CLOTH.WIDTH = EST.CASE.SPINE.CL.CUT.SIZE<1,1> / 10000
   CLOTH.LENGTH = EST.CASE.SPINE.CL.CUT.SIZE<1,2> / 10000
   NO.OUT = INT(EST.CASE.SPINE.CL.CUT.OUT / 100)
*
*---- MAIN PROCESSING
*
   BEGIN CASE
      CASE NUM(EST.CASE.SPOIL<1,3>)
         SPOIL = INT(COMPQTY * (EST.CASE.SPOIL<1,3> / 100) + 0.99) ;*T27924
      CASE 1
         MATREAD BST.REC FROM ESTIMATE.BIND.SPOIL, CONO:EST.CASE.SPOIL<1,3> ELSE
            MAT BST.REC = ""
         END
*T27924 v
         LOCATE COMPQTY IN BST.QTY<1>,1 BY "AR" SETTING BP ELSE NULL
*T27924 ^
         BEGIN CASE
            CASE BP = 1
               BSPCT = BST.PCT<1,BP>
            CASE BP > COUNT(BST.QTY,VM) + 1
               BSPCT = BST.PCT<1,BP-1>
            CASE BST.EXTR = "Y"
               BSPCT = INT(BST.PCT<1,BP>-(BST.QTY<1,BP>-COMPQTY)/(BST.QTY<1,BP>-BST.QTY<1,BP-1>)*(BST.PCT<1,BP>-BST.PCT<1,BP-1>)+0.5) ;*T27924
            CASE 1
               BSPCT = BST.PCT<1,BP>
         END CASE
         SPOIL = INT(COMPQTY * (BSPCT / 10000) + 0.99) ;*T27924
   END CASE
   PROD.QTY = COMPQTY + SPOIL ;*T27924
   BEGIN CASE
*T27924 v
*     CASE EST.BIND.COVER.STYLE # "C"
      CASE EST.BIND.COVER.STYLE<1,COMP> # "C"
*T27924 ^
         TEMP.QTY = 0
      CASE NO.OUT = 0
         TEMP.QTY = 0
      CASE EST.CASE.PIECES # 3
         TEMP.QTY = 0
      CASE EST.CASE.SPINE.CL.SRC = "S"
         TEMP.QTY = INT(PROD.QTY / NO.OUT + 0.99)
      CASE EST.CASE.PIECES = 3
         TEMP.QTY = INT(PROD.QTY * CLOTH.LENGTH / 36 / NO.OUT + 0.99)
      CASE 1
         TEMP.QTY = 0
   END CASE
   IF EST.CASE.SPINE.CL.SRC = "S" THEN
*T27924 v
*     EST.CASE.SPINE.CL.QTY<1,1,QPTR> = TEMP.QTY
*     EST.CASE.SPINE.CL.QTY<1,2> = "SHT"
      EST.CASE.SPINE.CL.QTY<1,COMP,QPTR> = TEMP.QTY
      EST.CASE.SPINE.CL.UOM<1,COMP,3> = "SHT"
   END ELSE
*T27924 v
*     EST.CASE.SPINE.CL.QTY<1,1,QPTR> = TEMP.QTY
*     EST.CASE.SPINE.CL.QTY<1,2> = "YDS"
      EST.CASE.SPINE.CL.QTY<1,COMP,QPTR> = TEMP.QTY
      EST.CASE.SPINE.CL.UOM<1,COMP,3> = "YDS"
*T27924 ^
   END
   IF TEMP.TYPE[1,1] = "M" THEN
      TEMP.FCTR = 1000
*T27924 v
*     TEMP.STD = EST.CASE.SPINE.CL.PRICE<1,1,QPTR> / 100
*     TEMP.STD.TYPE = "$/":EST.CASE.SPINE.CL.PRICE<1,2>
*     TEMP.UM = EST.CASE.SPINE.CL.PRICE<1,3>
      TEMP.STD = EST.CASE.SPINE.CL.PRICE<1,COMP,QPTR> / 100
      TEMP.STD.TYPE = "$/":EST.CASE.SPINE.CL.UOM<1,COMP,1>
      TEMP.UM = EST.CASE.SPINE.CL.UOM<1,COMP,2>
*T27924 ^
      TEMP.IND.1 = ESTM.FOH.PCT<1,MP>
      TEMP.MARKUP = ESTM.MARKUP<1,MP>
      TEMP.DESC = EST.CASE.SPINE.CL<1,2>
   END
   TEMP.VSALE = "Y"
   RETURN
END
