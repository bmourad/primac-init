      SUBROUTINE EST.MATL.XREF (CONO, ACTION, XTYPE, SELECTION)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.MATL.XREF
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/25/89
*
* DESCRIPTION
*
* This subroutine displays selective materials for selection
* purposes.
*
*********************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
      SELECTION = ""
      SCREEN DEFINE;EST.MATL.XREF
      SCREEN CLEAR;;D
      SCREEN FORMAT
      SCREEN COUNT;;5
      LINE.COUNT = S$LCNT
      LINE.SPACE = S$LSPC
*
*---- MAIN PROCESSING
*
100*
      CURR.REF.NO = ""
      LINE.CNT = DCOUNT(ESTM.PROD,VM)
      S$DATA(1)<S$SCR> = DATE()
      BEGIN CASE
      CASE XTYPE = "C"
         S$DATA(2)<S$SCR> = "CASEMAKING"
      CASE XTYPE = "I"
         S$DATA(2)<S$SCR> = "INK"
      CASE XTYPE = "F"
         S$DATA(2)<S$SCR> = "FILM"
      CASE XTYPE = "P"
         S$DATA(2)<S$SCR> = "PLATES"
      CASE XTYPE = "B"
         S$DATA(2)<S$SCR> = "BLANKETS"
      CASE XTYPE = "X"
         S$DATA(2)<S$SCR> = "STRIPPING"
      CASE XTYPE = "L"
         S$DATA(2)<S$SCR> = "LONG RUN PLATES"
      CASE XTYPE = "O"
         S$DATA(2)<S$SCR> = "OTHER"
      END CASE
      S$DATA(6)<S$SCR> = ESTM.PROD
      S$DATA(7)<S$SCR> = ESTM.FULL.DESC
      SCREEN DISPLAY;;ALL
      REF.NO = 1
      GOSUB 50000
*
*---- GET OPERATOR REPLY
*
500*
      SCREEN FIELD;;11
      SCREEN INPUT;;11
      OPT = S$VALUE
      BEGIN CASE
         CASE OPT # "" AND NUM(OPT)
            IF OPT < CURR.REF.NO OR OPT > LINE.CNT OR OPT > (CURR.REF.NO + LINE.COUNT - 1) THEN
               ERRMSG = "** OUT OF RANGE **"
               GOSUB 90000
            END ELSE
               SELECTION = ESTM.PROD<1,OPT>
               GOTO 99999
            END
         CASE OPT = "E" OR OPT = "END"
            GOTO 99999
         CASE OPT = "S" OR OPT = "SF"
            REF.NO = CURR.REF.NO + LINE.COUNT
            IF REF.NO > LINE.CNT THEN REF.NO = 1
            GOSUB 50000
         CASE OPT = "ST"
            REF.NO = 1
            GOSUB 50000
         CASE OPT = "SB"
            REF.NO = LINE.CNT
            GOSUB 50000
         CASE OPT = "SR"
            REF.NO = REF.NO - LINE.COUNT
            IF REF.NO < 1 THEN REF.NO = 1
            GOSUB 50000
      END CASE
      GOTO 500
*
*---- SCROLL MULTI-LINE DATA
*
50000*
      START.REF.NO = 1 + INT((REF.NO - 1) / LINE.COUNT) * LINE.COUNT
      IF START.REF.NO = CURR.REF.NO THEN RETURN
      CURR.REF.NO = START.REF.NO
      S$VAL = START.REF.NO
      S$CNT = DCOUNT(S$DATA(6)<S$SCR>,VM)
      SCREEN MULTI;;C;5;6;7
      RETURN
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
      SCREEN CLEAR;;D
      RETURN
   END
