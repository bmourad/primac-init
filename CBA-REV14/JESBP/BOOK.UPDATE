      SUBROUTINE BOOK.UPDATE (CONO, ACTION, EST.NUM, JOB.NUM, COMP.NUM)
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>COM.BOOKING
***************************************************************************
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* PROGRAM  - BOOK.UPDATE
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
* DATE     - 05/04/86
* DESCRIPTION
* This program will create ESTIMATE.JOB record when estimate is booked to a job
***************************************************************************
*
*---- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.JOB
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>ICS.CPYLIB>CATEGORY
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- MAIN PROCESSING
*
   MAT ESTJ.REC = ""
   ESTJ.EST.ID = EST.NUM
   ESTJ.COMP = COMP.NUM
   ESTJ.QTY = EST.BOOK.QTY
   ESTJ.EST.TYPE = EST.TYPE
   ESTJ.DIV = FIELD(EST.DIV,"-",1)
   DC.CNT = COUNT(EST.DEPT.COMP,VM) + (EST.DEPT.COMP # "")
   FOR LN = 1 TO DC.CNT
      IF FIELD(EST.DEPT.COMP<1,LN>,"!",3) = EST.BOOK.QTY THEN
         IF COMP.NUM = "ALL" THEN
            GOSUB 1000
         END ELSE
            IF FIELD(EST.DEPT.COMP<1,LN>,"!",2) = COMP.NUM THEN GOSUB 1000
         END
      END
   NEXT LN
   MATWRITE ESTJ.REC ON ESTIMATE.JOB, CONO:JOB.NUM
   GOTO 999999
*-----------------------*
*---   SUBROUTINES   ---*
*-----------------------*
*
*--- LOAD ESTJ.REC FROM ESTD.REC
*
1000*
   MATREAD ESTD.REC FROM ESTIMATE.DEPT, CONO:EST.NUM:"!":EST.DEPT.COMP<1,LN> ELSE MAT ESTD.REC = ""
   TYPE.CNT = COUNT(ESTD.TYPE,VM) + (ESTD.TYPE # "")
   FOR T = 1 TO TYPE.CNT
      BEGIN CASE
      CASE ESTD.TYPE<1,T> = "G"
      CASE ESTD.TYPE<1,T> = "L"
         GOSUB 10100
      CASE ESTD.TYPE<1,T>[1,1] = "M"
         MT.TYPE = ESTD.TYPE<1,T>[2,1]
         GOSUB 10200
      CASE ESTD.TYPE<1,T> = "P"
         GOSUB 10300
      CASE ESTD.TYPE<1,T> = "S"
         GOSUB 10400
      CASE 1
         GOSUB 10500
      END CASE
   NEXT T
1999*
   RETURN
*
*--- LABOR
*
10100*
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.LB.DEPT,VM) + (ESTJ.LB.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
      CASE ESTD.DEPT = ESTJ.LB.DEPT<1,D>
         BEGIN CASE
         CASE ESTD.CCTR<1,T> = ESTJ.LB.CCTR<1,D>
            BEGIN CASE
            CASE ESTD.OPV<1,T> = ESTJ.LB.OPER<1,D>
               DFND = D
               MATCHED = 1
            CASE ESTD.OPV<1,T> > ESTJ.LB.OPER<1,D>
               DFND = D + 1
            END CASE
         CASE ESTD.CCTR<1,T> > ESTJ.LB.CCTR<1,D>
            DFND = D + 1
         END CASE
      CASE ESTD.DEPT > ESTJ.LB.DEPT<1,D>
         DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.LB.DEPT = INSERT(ESTJ.LB.DEPT,1,DFND,0,"")
         ESTJ.LB.CCTR = INSERT(ESTJ.LB.CCTR,1,DFND,0,"")
         ESTJ.LB.OPER = INSERT(ESTJ.LB.OPER,1,DFND,0,"")
         ESTJ.LB.HRS = INSERT(ESTJ.LB.HRS,1,DFND,0,"")
         ESTJ.LB.QTY = INSERT(ESTJ.LB.QTY,1,DFND,0,"")
         ESTJ.LB.DCOST = INSERT(ESTJ.LB.DCOST,1,DFND,0,"")
         ESTJ.LB.COST = INSERT(ESTJ.LB.COST,1,DFND,0,"")
         ESTJ.LB.SALE = INSERT(ESTJ.LB.SALE,1,DFND,0,"")
      END
      ESTJ.LB.DEPT<1,DFND> = ESTD.DEPT
      ESTJ.LB.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.LB.OPER<1,DFND> = ESTD.OPV<1,T>
   END
   ESTJ.LB.HRS<1,DFND> = ESTJ.LB.HRS<1,DFND> + ESTD.HRS<1,T>
   IF ESTD.OPV.TYPE<1,T> = "F" THEN
      ESTJ.LB.QTY<1,DFND> = ESTJ.LB.QTY<1,DFND> + 0
   END ELSE
      ESTJ.LB.QTY<1,DFND> = ESTJ.LB.QTY<1,DFND> + ESTD.QTY<1,T>
   END
   ESTJ.LB.DCOST<1,DFND> = ESTJ.LB.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.LB.COST<1,DFND> = ESTJ.LB.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.LB.SALE<1,DFND> = ESTJ.LB.SALE<1,DFND> + ESTD.SALE<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
   RETURN
*
*--- MATERIAL
*
10200*
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.MT.DEPT,VM) + (ESTJ.MT.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
      CASE ESTD.DEPT = ESTJ.MT.DEPT<1,D>
         BEGIN CASE
         CASE ESTD.CCTR<1,T> = ESTJ.MT.CCTR<1,D>
            BEGIN CASE
            CASE ESTD.OPV<1,T> = ESTJ.MT.PROD<1,D>
               BEGIN CASE
               CASE ESTD.CODE<1,T> = ESTJ.MT.PLINE<1,D>
                  IF ESTD.UM<1,T> = ESTJ.MT.TYPE<1,D> THEN
                     DFND = D
                     MATCHED = 1
                  END ELSE
                     DFND = D + 1
                  END
               CASE ESTD.CODE<1,T> > ESTJ.MT.PLINE<1,D>
                  DFND = D + 1
               END CASE
            CASE ESTD.OPV<1,T> > ESTJ.MT.PROD<1,D>
               DFND = D + 1
            END CASE
         CASE ESTD.CCTR<1,T> > ESTJ.MT.CCTR<1,D>
            DFND = D + 1
         END CASE
      CASE ESTD.DEPT > ESTJ.MT.DEPT<1,D>
         DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.MT.DEPT = INSERT(ESTJ.MT.DEPT,1,DFND,0,"")
         ESTJ.MT.CCTR = INSERT(ESTJ.MT.CCTR,1,DFND,0,"")
         ESTJ.MT.PROD = INSERT(ESTJ.MT.PROD,1,DFND,0,"")
         ESTJ.MT.DESC = INSERT(ESTJ.MT.DESC,1,DFND,0,"")
         ESTJ.MT.PLINE = INSERT(ESTJ.MT.PLINE,1,DFND,0,"")
         ESTJ.MT.MLINE = INSERT(ESTJ.MT.MLINE,1,DFND,0,"")
         ESTJ.MT.QTY = INSERT(ESTJ.MT.QTY,1,DFND,0,"")
         ESTJ.MT.TYPE = INSERT(ESTJ.MT.TYPE,1,DFND,0,"")
         ESTJ.MT.DCOST = INSERT(ESTJ.MT.DCOST,1,DFND,0,"")
         ESTJ.MT.COST = INSERT(ESTJ.MT.COST,1,DFND,0,"")
         ESTJ.MT.SALE = INSERT(ESTJ.MT.SALE,1,DFND,0,"")
      END
      ESTJ.MT.DEPT<1,DFND> = ESTD.DEPT
      ESTJ.MT.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.MT.PROD<1,DFND> = ESTD.OPV<1,T>
      ESTJ.MT.DESC<1,DFND> = ESTD.DESC<1,T>
      ESTJ.MT.PLINE<1,DFND> = ESTD.CODE<1,T>
      MATREAD CATG.REC FROM CATEGORY, CONO:ESTD.CODE<1,T> ELSE MAT CATG.REC = ""
      ESTJ.MT.MLINE<1,DFND> = CATG.MAJ.LINE
      MATREAD ESTM.REC FROM ESTIMATE.MATL, CONO:MT.TYPE ELSE MAT ESTM.REC = ""
      LOCATE ESTD.OPV<1,T> IN ESTM.PROD<1>,1 SETTING PF ELSE PF = 0
      IF PF THEN
         ESTJ.MT.TYPE<1,DFND> = ESTM.UM<1,PF>
      END ELSE
         BEGIN CASE
         CASE CATG.TYPE = "S" OR MT.TYPE = "S"
            ESTJ.MT.TYPE<1,DFND> = "SHT"
         CASE CATG.TYPE = "P" OR CATG.TYPE = "F" OR CATG.TYPE = "O"
            ESTJ.MT.TYPE<1,DFND> = "EA"
         CASE MT.TYPE = "P" OR MT.TYPE = "F" OR MT.TYPE = "O"
            ESTJ.MT.TYPE<1,DFND> = "EA"
         CASE MT.TYPE = "B" OR MT.TYPE = "X"
            ESTJ.MT.TYPE<1,DFND> = "EA"
         CASE CATG.TYPE = "I" OR MT.TYPE = "I"
            ESTJ.MT.TYPE<1,DFND> = "LBS"
         CASE CATG.TYPE = "L" OR MT.TYPE = "R"
            ESTJ.MT.TYPE<1,DFND> = "LBS"
         CASE CATG.TYPE = "RL"
            ESTJ.MT.TYPE<1,DFND> = "MSI"
         CASE CATG.TYPE = "PC"
            ESTJ.MT.TYPE<1,DFND> = "MSI"
         END CASE
      END
   END
   ESTJ.MT.QTY<1,DFND> = ESTJ.MT.QTY<1,DFND> + ESTD.QTY<1,T>
   ESTJ.MT.DCOST<1,DFND> = ESTJ.MT.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.MT.COST<1,DFND> = ESTJ.MT.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.MT.SALE<1,DFND> = ESTJ.MT.SALE<1,DFND> + ESTD.SALE<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
   RETURN
*
*--- OUTSIDE PURCHASE
*
10300*
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.OS.DEPT,VM) + (ESTJ.OS.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
      CASE ESTD.DEPT = ESTJ.OS.DEPT<1,D>
         BEGIN CASE
         CASE ESTD.CCTR<1,T> = ESTJ.OS.CCTR<1,D>
            BEGIN CASE
            CASE ESTD.OPV<1,T> = ESTJ.OS.OPER<1,D>
               BEGIN CASE
               CASE ESTD.CODE<1,T> = ESTJ.OS.PLINE<1,D>
                  BEGIN CASE
                  CASE ESTD.VEND<1,T> = ESTJ.OS.VEND<1,D>
                     DFND = D
                     MATCHED = 1
                  CASE ESTD.VEND<1,T> > ESTJ.OS.VEND<1,D>
                     DFND = D + 1
                  END CASE
               CASE ESTD.CODE<1,T> > ESTJ.OS.PLINE<1,D>
                  DFND = D + 1
               END CASE
            CASE ESTD.OPV<1,T> > ESTJ.OS.OPER<1,D>
               DFND = D + 1
            END CASE
         CASE ESTD.CCTR<1,T> > ESTJ.OS.CCTR<1,D>
            DFND = D + 1
         END CASE
      CASE ESTD.DEPT > ESTJ.OS.DEPT<1,D>
         DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.OS.DEPT = INSERT(ESTJ.OS.DEPT,1,DFND,0,"")
         ESTJ.OS.CCTR = INSERT(ESTJ.OS.CCTR,1,DFND,0,"")
         ESTJ.OS.OPER = INSERT(ESTJ.OS.OPER,1,DFND,0,"")
         ESTJ.OS.DESC = INSERT(ESTJ.OS.DESC,1,DFND,0,"")
         ESTJ.OS.PLINE = INSERT(ESTJ.OS.PLINE,1,DFND,0,"")
         ESTJ.OS.VEND = INSERT(ESTJ.OS.VEND,1,DFND,0,"")
         ESTJ.OS.QTY = INSERT(ESTJ.OS.QTY,1,DFND,0,"")
         ESTJ.OS.DCOST = INSERT(ESTJ.OS.DCOST,1,DFND,0,"")
         ESTJ.OS.COST = INSERT(ESTJ.OS.COST,1,DFND,0,"")
         ESTJ.OS.SALE = INSERT(ESTJ.OS.SALE,1,DFND,0,"")
      END
      ESTJ.OS.DEPT<1,DFND> = ESTD.DEPT
      ESTJ.OS.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.OS.OPER<1,DFND> = ESTD.OPV<1,T>
      ESTJ.OS.DESC<1,DFND> = ESTD.DESC<1,T>
      ESTJ.OS.PLINE<1,DFND> = ESTD.CODE<1,T>
      ESTJ.OS.VEND<1,DFND> = ESTD.VEND<1,T>
   END
   ESTJ.OS.QTY<1,DFND> = ESTJ.OS.QTY<1,DFND> + ESTD.QTY<1,T>
   ESTJ.OS.DCOST<1,DFND> = ESTJ.OS.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.OS.COST<1,DFND> = ESTJ.OS.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.OS.SALE<1,DFND> = ESTJ.OS.SALE<1,DFND> + ESTD.SALE<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
   RETURN
*
*--- SHIPPING
*
10400*
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.SP.DEPT,VM) + (ESTJ.SP.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
      CASE ESTD.DEPT = ESTJ.SP.DEPT<1,D>
         BEGIN CASE
         CASE ESTD.CCTR<1,T> = ESTJ.SP.CCTR<1,D>
            BEGIN CASE
            CASE ESTD.OPV<1,T> = ESTJ.SP.OPER<1,D>
               BEGIN CASE
               CASE ESTD.CODE<1,T> = ESTJ.SP.VIA<1,D>
                  DFND = D
                  MATCHED = 1
               CASE ESTD.CODE<1,T> > ESTJ.SP.VIA<1,D>
                  DFND = D + 1
               END CASE
            CASE ESTD.OPV<1,T> > ESTJ.SP.OPER<1,D>
               DFND = D + 1
            END CASE
         CASE ESTD.CCTR<1,T> > ESTJ.SP.CCTR<1,D>
            DFND = D + 1
         END CASE
      CASE ESTD.DEPT > ESTJ.SP.DEPT<1,D>
         DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.SP.DEPT = INSERT(ESTJ.SP.DEPT,1,DFND,0,"")
         ESTJ.SP.CCTR = INSERT(ESTJ.SP.CCTR,1,DFND,0,"")
         ESTJ.SP.OPER = INSERT(ESTJ.SP.OPER,1,DFND,0,"")
         ESTJ.SP.DESC = INSERT(ESTJ.SP.DESC,1,DFND,0,"")
         ESTJ.SP.VIA = INSERT(ESTJ.SP.VIA,1,DFND,0,"")
         ESTJ.SP.DCOST = INSERT(ESTJ.SP.DCOST,1,DFND,0,"")
         ESTJ.SP.COST = INSERT(ESTJ.SP.COST,1,DFND,0,"")
         ESTJ.SP.SALE = INSERT(ESTJ.SP.SALE,1,DFND,0,"")
      END
      ESTJ.SP.DEPT<1,DFND> = ESTD.DEPT
      ESTJ.SP.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.SP.OPER<1,DFND> = ESTD.OPV<1,T>
      ESTJ.SP.DESC<1,DFND> = ESTD.DESC<1,T>
      ESTJ.SP.VIA<1,DFND> = ESTD.CODE<1,T>
   END
   ESTJ.SP.DCOST<1,DFND> = ESTJ.SP.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.SP.COST<1,DFND> = ESTJ.SP.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.SP.SALE<1,DFND> = ESTJ.SP.SALE<1,DFND> + ESTD.SALE<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
   RETURN
*
*--- MISCELLANEOUS
*
10500*
   DFND = 1
   MATCHED = 0
   DCNT = COUNT(ESTJ.MS.DEPT,VM) + (ESTJ.MS.DEPT # "")
   FOR D = 1 TO DCNT UNTIL MATCHED
      BEGIN CASE
      CASE ESTD.DEPT = ESTJ.MS.DEPT<1,D>
         BEGIN CASE
         CASE ESTD.CCTR<1,T> = ESTJ.MS.CCTR<1,D>
            BEGIN CASE
            CASE ESTD.OPV<1,T> = ESTJ.MS.OPER<1,D>
               IF ESTD.DESC<1,T> = ESTJ.MS.DESC<1,D> THEN
                  DFND = D
                  MATCHED = 1
               END ELSE
                  DFND = D + 1
               END
            CASE ESTD.OPV<1,T> > ESTJ.MS.OPER<1,D>
               DFND = D + 1
            END CASE
         CASE ESTD.CCTR<1,T> > ESTJ.MS.CCTR<1,D>
            DFND = D + 1
         END CASE
      CASE ESTD.DEPT > ESTJ.MS.DEPT<1,D>
         DFND = D + 1
      END CASE
   NEXT D
   IF MATCHED = 0 THEN
      IF DFND <= DCNT THEN
         ESTJ.MS.DEPT = INSERT(ESTJ.MS.DEPT,1,DFND,0,"")
         ESTJ.MS.CCTR = INSERT(ESTJ.MS.CCTR,1,DFND,0,"")
         ESTJ.MS.OPER = INSERT(ESTJ.MS.OPER,1,DFND,0,"")
         ESTJ.MS.DESC = INSERT(ESTJ.MS.DESC,1,DFND,0,"")
         ESTJ.MS.DCOST = INSERT(ESTJ.MS.DCOST,1,DFND,0,"")
         ESTJ.MS.COST = INSERT(ESTJ.MS.COST,1,DFND,0,"")
         ESTJ.MS.SALE = INSERT(ESTJ.MS.SALE,1,DFND,0,"")
      END
      ESTJ.MS.DEPT<1,DFND> = ESTD.DEPT
      ESTJ.MS.CCTR<1,DFND> = ESTD.CCTR<1,T>
      ESTJ.MS.OPER<1,DFND> = ESTD.OPV<1,T>
      ESTJ.MS.DESC<1,DFND> = ESTD.DESC<1,T>
   END
   ESTJ.MS.DCOST<1,DFND> = ESTJ.MS.DCOST<1,DFND> + ESTD.DCOST<1,T>
   ESTJ.MS.COST<1,DFND> = ESTJ.MS.COST<1,DFND> + ESTD.COST<1,T>
   ESTJ.MS.SALE<1,DFND> = ESTJ.MS.SALE<1,DFND> + ESTD.SALE<1,T>
   ESTJ.TOT.COST = ESTJ.TOT.COST + ESTD.COST<1,T>
   ESTJ.TOT.SALE = ESTJ.TOT.SALE + ESTD.SALE<1,T>
   RETURN
999999*
   RETURN
END
