   SUBROUTINE EST.PROD.WEB.MAINT(CONO,ACTION,EST.KEY,COMP,STATUS)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.PROD.WEB.MAINT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 01/06/86
*
* DESCRIPTION
*
* This subroutine captures multi-web specification data for use in
* subsequent calculations.
* 09/10/91 DLG TASK 16090 ADDED WEB PRESS CUTOFF LENGTH FIELD
*
* MOD TASK 17811 DMT 4/19/94 ALLOW 99 QUANTITIES
* MOD TASK 19347 DMT 6/26/95 FIX PROD PS/OS LENGTH FOR ROLL TYPE
*T21090 llh 10/11/1996 * NEED TO CHANGE /REDISPLAY PRODUCT DESCRIPTION
*T21528 julie 01/28/1997 * ALLOW USER TO CHANGE FLAT AND FINAL SIZE,
*                          BIND AND PRESS SPOIL AND M/R IMP ON
*                          SUBSEQUENT WEBS OF MULTI-WEB ESTIMATE
*T21738 rik 04/22/1997 * DISPLAY PAPER GROUP DESCRIPTION
*T22310 rick 01/13/1998 * Modify to use number up and number of forms
*T23155 markt 08/10/1998 * Add NumUp and #Form fields to screen.
*T23781 markt 04/16/1999 * Does not allow change to price table on 
*                          multi-web components when no price table setup
*                          for product id.
*                          (when web number is > 1)
*T25628 lanny 02/12/2001 * Cutoff Len not changing when Press changed on
*                          Web 1 (Webs 2-N).
*T25628 edwin 10/01/2001 * Fix problem with OS and PS width and length
*                          for 2nd web.
*T27924 cmykleb 03/17/2004 * Move estimate qty to the component level.
*T29032 cmyklebu 01/02/2007 * Move pgm from rev12 to rev14.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>PMC.CPYLIB>COMPANY
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>EQUIPMENT
*COPY>JES.CPYLIB>ESTIMATE.MATL
*COPY>JES.CPYLIB>ESTIMATE.PRESS.SPOIL
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>ICS.CPYLIB>INVENTORY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>EDIT.COM    ; * T27924
*COPY>JES.CPYLIB>ESTIMATE.PAPER.GROUP
   OPEN "ESTIMATE.PAPER.GROUP" TO ESTIMATE.PAPER.GROUP ELSE
      ERRMSG = "UNABLE TO OPEN ESTIMATE.PAPER.GROUP"
      GOSUB 90000
      RETURN
   END
*T29032 v
*  OPEN 'REV12A.SCREENS' TO REV12A.SCREENS ELSE
*     ERRMSG='NO REV12A.SCREENS FILE'
*     GOSUB 90000
*     RETURN
*  END
   OPEN 'JES.SCREENS' TO JES.SCREENS ELSE
      ERRMSG='NO JES.SCREENS FILE'
      GOSUB 90000
      RETURN
   END
*T29032 ^
   MAT SYSCOM.REC =  ""; SYS.TYPE = 2
*
*---- INITIALIZATION
*
   MPTR=2
   WEB.CNT = EST.PROD.WEB.CNT<1,COMP>+0
   FOR W = 2 TO WEB.CNT
      IF EST.PROD.OS.PROD<1,COMP,W> = EST.PROD.OS.PROD<1,COMP,W-1> THEN
         IF EST.PROD.OS.DESC<1,COMP,W> = "" THEN
            EST.PROD.OS.DESC<1,COMP,W> = EST.PROD.OS.DESC<1,COMP,W-1>
         END
      END
   NEXT W
*T29032 v
*  SCREEN DEFINE;EST.PROD.WEB.MAINT;REV12A.SCREENS
   SCREEN DEFINE;EST.PROD.WEB.MAINT;JES.SCREENS
*T29032 ^
   SCREEN FORMAT
*T27924 v
   MAT EDIT.COM = ''
   TYP=0 ; CALL EDIT.SUB1( MAT EDIT.COM )
*T27924 ^
   SCREEN COUNT;;42
   PRICE.LINE.COUNT=S$LCNT
   PRICE.LINE.SPACE=S$LSPC
   PRICE.CURR.REF.NO=""
   SCREEN COUNT;;36
   COLOR.LINE.COUNT=S$LCNT
   COLOR.LINE.SPACE=S$LSPC
   COLOR.CURR.REF.NO=""
*T27924 v
   SCREEN COUNT;;51
   CQTY.LINE.COUNT=S$LCNT
   CQTY.LINE.SPACE=S$LSPC
   CQTY.CURR.REF.NO=""
   CQTY.REF.NO = 1
   CQTY.CNT=0
   CQTY.LINE.CNT=0
   OPTQ = ''
   MORE = 0
*T27924 ^
   PRICE.LINE.CNT=0
   COLOR.LINE.CNT=0
   PRICE.REF.NO=1
   COLOR.REF.NO=1
   SCREEN FIELD;;17
   IF S$TYP="3" THEN CAL.PPI="P" ELSE CAL.PPI="C"
   S$TYP=0; CALL SCREEN.INPUT; S$MASK="#"
   PRICE.FLAG=CO.JES.PARAM<1,11>
   IF PRICE.FLAG="" THEN PRICE.FLAG="X"
   GOTO 110
*
*---- MAIN PROCESSING
*
100*
   SCREEN CLEAR
110*
   IF EST.PROD.OS.USAGE<1,COMP,MPTR>="" THEN
      NEW.REC=1
      COLOR.LINE.CNT=COUNT(EST.PROD.INK.ID<1,COMP,1>,"!")+(EST.PROD.INK.ID<1,COMP,1> # "")
   END ELSE
      NEW.REC=0
      COLOR.LINE.CNT=COUNT(EST.PROD.INK.ID<1,COMP,MPTR>,"!")+(EST.PROD.INK.ID<1,COMP,MPTR> # "")
   END
   PRICE.LINE.CNT=COUNT(EST.QTY,VM)+1
   CQTY.LINE.CNT=DCOUNT(EST.QTY<1>,VM) ; * T27924
   GOSUB 80000
*T27924 v
   CQTY.CURR.REF.NO=""
   CQTY.REF.NO = 1
   GOSUB 11690; GOSUB 85200
*T27924 ^
   PRICE.CURR.REF.NO=""
   PRICE.REF.NO = 1
   GOSUB 85100
   COLOR.CURR.REF.NO=""
   COLOR.REF.NO=1
   GOSUB 50000
   IF COUNT(EST.QTY,VM)+1 # COUNT(EST.PROD.PQTY<1,COMP,MPTR>,"!")+(EST.PROD.PQTY<1,COMP,MPTR> # "") THEN
      GOSUB 85000
   END
   MODE="A"
   GOSUB 40100
*
*---- GET OPERATOR REQUEST
*
500*
   IF ACTION="M" THEN
      SCREEN FIELD;;39
      SCREEN INPUT;;39
   END ELSE
*T27924 v
*     SCREEN FIELD;;51
*     SCREEN INPUT;;51
      X=0; Y=22; TYP=11; MAXL=3; VALDAT="W":VM:"I":VM:"E":VM:"PS":VM:"CQ"
      PMSG="Enter (W)eb, (PS)Price Scroll, (I)nk, (CQ) Qty Scroll, (E)nd"
      HMSG = "(W)eb, (PS) paper price scroll, (I)nk, (CQ) Component quantity scroll, (E)nd"
      CALL EDIT.SUB1(MAT EDIT.COM)
      VALDAT = ""
      S$VALUE = VALUE
*T27924 ^
   END
   OPTION=S$VALUE
   BEGIN CASE
      CASE OPTION="" OR OPTION="E" OR OPTION="END"
         GOTO 99999
      CASE NUM(OPTION)
         MODE="C"
         FLD=OPTION
         BEGIN CASE
            CASE FLD=11
               GOSUB 1110
               GOSUB 85000
               MODE="A"
               GOSUB 40000
            CASE FLD=21 AND EST.PROD.OS.TYPE<1,COMP> # ""
               GOSUB 1210
               GOSUB 85000
               MODE="A"
               GOSUB 40000
         END CASE
      CASE OPTION="W"
         MPTR=MPTR+1
         IF MPTR > WEB.CNT THEN
            MPTR = 2
         END
         GOTO 100
      CASE OPTION="P"
         MODE="C"
***** T23781 v
*           MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,1> ELSE
         MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,MPTR> ELSE
***** T23781 ^
            MAT ESTM.REC=""
         END
***** T23781 v
*           LOCATE EST.PROD.OS.PROD<1,COMP,1> IN ESTM.PROD<1>,1 SETTING MP ELSE
         LOCATE EST.PROD.OS.PROD<1,COMP,MPTR> IN ESTM.PROD<1>,1 SETTING MP ELSE
**** T23781 ^
            MP=0
         END
         IF MP > 0 THEN
            BEGIN CASE
***** T23781 v
*                 CASE (PRICE.FLAG="X" AND ESTM.QCOST<1,MP,1>="")
*                    GOSUB 40000
*                 CASE (PRICE.FLAG#"X" AND EST.PROD.INV.ID<1,COMP,1>="")
*                    IF ESTM.QCOST<1,MP,1>="" THEN GOSUB 40000
               CASE (PRICE.FLAG="X" AND ESTM.QCOST<1,MP,MPTR>="")
                  GOSUB 40000
               CASE (PRICE.FLAG#"X" AND EST.PROD.INV.ID<1,COMP,MPTR>="")
                  IF ESTM.QCOST<1,MP,MPTR>="" THEN GOSUB 40000
***** T23781 ^
            END CASE
         END
      CASE OPTION = 'PS'
         PRICE.REF.NO=PRICE.CURR.REF.NO+PRICE.LINE.COUNT
         IF PRICE.REF.NO > PRICE.LINE.CNT THEN PRICE.REF.NO=1
         GOSUB 85100
      CASE OPTION="I"
         MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:"I" ELSE
            MAT ESTM.REC=""
         END
         CALL EST.PROD.INK.MAINT(CONO,ACTION,"",COMP,MPTR,COLOR.REF.NO,COLOR.CURR.REF.NO)
*T27924 v
      CASE OPTION="CQ"
         CQTY.REF.NO=CQTY.CURR.REF.NO + CQTY.LINE.COUNT
         IF CQTY.REF.NO > CQTY.LINE.CNT THEN CQTY.REF.NO = 1
         GOSUB 85200
*T27924 ^
   END CASE
   GOTO 500
*
*---- GET LINE NUMBER
*
650*
   SCREEN FIELD;;45
   S$MINV=1
   S$MAXV=PRICE.LINE.CNT
   SCREEN INPUT;;45
   RETURN
*
*---- GET PAPER TYPE
*
1110*
   NEW.PROD=0
   PREV.TYPE=EST.PROD.OS.TYPE<1,COMP>
   PREV.USAGE=EST.PROD.OS.USAGE<1,COMP,MPTR>
   PREV.PROD=EST.PROD.OS.PROD<1,COMP,MPTR>
   PREV.INV.ID=EST.PROD.INV.ID<1,COMP,MPTR>
   S$DATA(4)<S$SCR>=EST.PROD.OS.TYPE<1,COMP>
   PAP.TYPE=EST.PROD.OS.TYPE<1,COMP>
   SCREEN FIELD;;47
   SCREEN INPUT;;47;RETURN
   USE.CODE=S$VALUE
   MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:PAP.TYPE:USE.CODE ELSE
      ERRMSG="Cannot locate on material file"
      GOSUB 90000
      GOTO 1110
   END
   EST.PROD.OS.USAGE<1,COMP,MPTR>=USE.CODE
*T21738 v
   MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP,CONO:USE.CODE ELSE
      MAT ESTPG.REC = ''
   END
   S$DATA(48)<S$SCR> = ESTPG.DESC
   SCREEN DISPLAY;;48
*T21738 ^
*
*---- GET PRODUCT ID
*
1120*
   S$DATA(6)<S$SCR>=EST.PROD.OS.PROD<1,COMP,MPTR>
   SCREEN FIELD;;6
   SCREEN INPUT;;6;RETURN
   IF S$VALUE="???" THEN
      S$SCR=S$SCR+1
      CALL EST.PAPER.XREF(CONO,"S",EST.PROD.OS.TYPE<1,COMP>,EST.PROD.OS.USAGE<1,COMP,MPTR>,SELECTION)
      SCREEN CLOSE
      S$SCR=S$SCR-1
      SCREEN FORMAT
      GOSUB 80900
*T27924 v
      CQTY.REF.NO=1
      CQTY.CURR.REF.NO = ''
      GOSUB 85200
*T27924 ^
      GOSUB 85100
      COLOR.CURR.REF.NO=""
      GOSUB 50000
      IF SELECTION="" THEN GOTO 1120
      S$VALUE=SELECTION
      S$DATA(6)<S$SCR>=SELECTION
      SCREEN DISPLAY;;6
   END
   LOCATE S$VALUE IN ESTM.PROD<1>,1 SETTING MP ELSE
      ERRMSG="Invalid product ID. Try again! "
      GOSUB 90000
      GOTO 1120
   END
   EST.PROD.OS.PROD<1,COMP,MPTR>=S$VALUE
   IF EST.PROD.OS.DESC<1,COMP,MPTR>="" THEN
      EST.PROD.OS.DESC<1,COMP,MPTR>=ESTM.DESC<1,MP>
   END
   IF S$VALUE#PREV.PROD THEN S$DATA(7)<S$SCR>=""
   SCREEN FIELD;;7
   S$O.R="O"
   S$DEFAULT=ESTM.DESC<1,MP>
   SCREEN INPUT;;7;RETURN
   EST.PROD.OS.DESC<1,COMP,MPTR>=S$VALUE
*
*---- GET INVENTORY ID
*
1125*
   S$DATA(20)<S$SCR>=EST.PROD.INV.ID<1,COMP,MPTR>
   SCREEN FIELD;;20
   SCREEN INPUT;;20;RETURN
   BEGIN CASE
      CASE S$VALUE=""
         EST.PROD.INV.ID<1,COMP,MPTR>=S$VALUE
      CASE 1
         INV.ID=S$VALUE
         IF INV.ID="???" THEN
            S$SCR=S$SCR+1
            PLINE=EST.PROD.PLINE<1,COMP,MPTR>
            BASIS=EST.PROD.BASIS.WT<1,COMP,MPTR>
            FIN=EST.PROD.FINISH<1,COMP,MPTR>
            CLR=EST.PROD.COLOR<1,COMP,MPTR>
            WIDTH=""
            XXLENGTH=""
            CALL EST.INV.XREF(CONO,"S",PLINE,BASIS,FIN,CLR,WIDTH,XXLENGTH,SELECTION)
            SCREEN CLOSE
            S$SCR=S$SCR-1
            SCREEN FORMAT
            IF SELECTION="" THEN
               GOSUB 80900
*T27924 v
               CQTY.REF.NO=1
               CQTY.CURR.REF.NO = ''
               GOSUB 85200
*T27924 ^
               GOSUB 85100
               COLOR.CURR.REF.NO=""
               GOSUB 50000
               GOTO 1125
            END ELSE
               INV.ID=SELECTION
            END
         END
         MATREAD INV.REC FROM INVENTORY,CONO:INV.ID ELSE
            ERRMSG="Invalid inventory item. Try again! "
            GOSUB 90000
            GOTO 1125
         END
         EST.PROD.INV.ID<1,COMP,MPTR>=INV.ID
   END CASE
   BEGIN CASE
      CASE EST.PROD.OS.USAGE<1,COMP,MPTR> # PREV.USAGE
         MODE="A"
      CASE EST.PROD.OS.PROD<1,COMP,MPTR> # PREV.PROD
         MODE="A"
      CASE EST.PROD.INV.ID<1,COMP,MPTR> # PREV.INV.ID
         MODE="A"
   END CASE
   IF MODE="A" THEN
      PRICE.CURR.REF.NO = ''
      GOSUB 1180
      EST.PROD.PCST<1,COMP,MPTR>=""
   END
*
*---- GET COATING CODE
*
   IF MODE="C" OR EST.PROD.COATING<1,COMP,MPTR>="" THEN
      SCREEN FIELD;;15
      SCREEN INPUT;;15;RETURN
      EST.PROD.COATING<1,COMP,MPTR>=S$VALUE
   END
*
*---- GET ORDER PAPER SIZE, GRAIN
*
1130*
   IF EST.PROD.INV.ID<1,COMP,MPTR>="" THEN
      IF MODE="C" OR EST.PROD.OS.WIDTH<1,COMP,MPTR>="" THEN
         SCREEN FIELD;;8
         SCREEN INPUT;;8;RETURN
         EST.PROD.OS.WIDTH<1,COMP,MPTR>=S$VALUE
      END
   END
   IF MODE="C" OR EST.PROD.OS.GRAIN<1,COMP,MPTR>="" THEN
      SCREEN FIELD;;10
      SCREEN INPUT;;10;RETURN
      EST.PROD.OS.GRAIN<1,COMP,MPTR>=S$VALUE
   END
*
*---- GET PRESS SHEET SIZE, GRAIN
*
1140*
   EST.PROD.PS.WIDTH<1,COMP,MPTR>=EST.PROD.OS.WIDTH<1,COMP,MPTR>
   S$DATA(11)<S$SCR>=EST.PROD.PS.WIDTH<1,COMP,MPTR>
   SCREEN DISPLAY;;11
   EST.PROD.PS.GRAIN<1,COMP,MPTR>=EST.PROD.OS.GRAIN<1,COMP,MPTR>
*
*---- GET BASIS WEIGHT
*
1150*
   IF EST.PROD.INV.ID<1,COMP,MPTR>="" THEN
      IF MODE="C" OR EST.PROD.BASIS.WT<1,COMP,MPTR>="" THEN
         SCREEN FIELD;;14
         SCREEN INPUT;;14;RETURN
         EST.PROD.BASIS.WT<1,COMP,MPTR>=S$VALUE
      END
   END
*
*---- GET PAPER CALIPER OR PPI
*
1155*
   IF MODE="C" OR (CAL.PPI="P" AND EST.PROD.PPI<1,COMP,MPTR>="") OR (CAL.PPI="C" AND EST.PROD.CALIPER<1,COMP,MPTR>="") THEN
      SCREEN FIELD;;17
      SCREEN INPUT;;17;RETURN
      IF CAL.PPI="P" THEN
         EST.PROD.PPI<1,COMP,MPTR>=S$VALUE
         EST.PROD.CALIPER<1,COMP,MPTR>=""
      END ELSE
         EST.PROD.CALIPER<1,COMP,MPTR>=S$VALUE
         EST.PROD.PPI<1,COMP,MPTR>=""
      END
   END
*
*---- GET PRODUCT LINE
*
1160*
   IF EST.PROD.INV.ID<1,COMP,MPTR>="" THEN
      IF MODE="C" OR EST.PROD.PLINE<1,COMP,MPTR>="" THEN
         SCREEN FIELD;;16
         SCREEN INPUT;;16;RETURN
         EST.PROD.PLINE<1,COMP,MPTR>=S$VALUE
      END
   END
*
*---- GET PSS/OSS
*
*     IF EST.PROD.OS.TYPE<1,COMP>="S" THEN
*        IF MODE="C" THEN
*           SCREEN FIELD;;21
*           SCREEN INPUT;;21;RETURN
*           EST.PROD.PSS.OSS<1,COMP,MPTR>=S$VALUE
*        END
*     END
*
*---- GET COLOR
*
1164*
   IF EST.PROD.INV.ID<1,COMP,MPTR>="" THEN
      IF MODE="C" OR EST.PROD.COLOR<1,COMP,MPTR>="" THEN
         S$DATA(18)<S$SCR>=EST.PROD.COLOR<1,COMP,MPTR>
         SCREEN FIELD;;18
         SCREEN INPUT;;18;RETURN
         IF S$VALUE # "" THEN
            READ REC FROM COLOR,CONO:S$VALUE ELSE
               ERRMSG="Invalid color code. Try again! "
               GOSUB 90000
               GOTO 1164
            END
         END
         EST.PROD.COLOR<1,COMP,MPTR>=S$VALUE
      END
   END
*
*---- GET FINISH
*
1166*
   IF EST.PROD.INV.ID<1,COMP,MPTR>="" THEN
      IF MODE="C" OR EST.PROD.FINISH<1,COMP,MPTR>="" THEN
         S$DATA(19)<S$SCR>=EST.PROD.FINISH<1,COMP,MPTR>
         SCREEN FIELD;;19
         SCREEN INPUT;;19;RETURN
         IF S$VALUE # "" THEN
            READ REC FROM FINISH,CONO:S$VALUE ELSE
               ERRMSG="Invalid finish code. Try again! "
               GOSUB 90000
               GOTO 1166
            END
         END
         EST.PROD.FINISH<1,COMP,MPTR>=S$VALUE
      END
   END
   RETURN
*
*---- DISPLAY PAPER DATA
*
1180*
   IF EST.PROD.INV.ID<1,COMP,MPTR>="" THEN
      IF EST.PROD.OS.DESC<1,COMP,MPTR> # "" THEN
         NULL
      END ELSE
         EST.PROD.OS.DESC<1,COMP,MPTR>=ESTM.DESC<1,MP>
      END
      EST.PROD.OS.WIDTH<1,COMP,MPTR>=ESTM.WIDTH<1,MP>
      EST.PROD.PLINE<1,COMP,MPTR>=ESTM.PLINE<1,MP>
      EST.PROD.BASIS.WT<1,COMP,MPTR>=ESTM.BASIS.WT<1,MP>
      EST.PROD.FINISH<1,COMP,MPTR>=ESTM.FINISH.CD<1,MP>
      EST.PROD.COLOR<1,COMP,MPTR>=ESTM.CLR.CD<1,MP>
      EST.PROD.CALIPER<1,COMP,MPTR>=ESTM.CALIPER<1,MP>
      EST.PROD.PPI<1,COMP,MPTR>=ESTM.PPI<1,MP>
   END ELSE
      EST.PROD.OS.DESC<1,COMP,MPTR>=INV.DESC
      EST.PROD.OS.WIDTH<1,COMP,MPTR>=INV.PAP.WIDTH        ;* REL 08.0
      EST.PROD.PLINE<1,COMP,MPTR>=INV.LINE
      IF NUM(INV.BAS.WT) THEN
         EST.PROD.BASIS.WT<1,COMP,MPTR>=INV.BAS.WT/100
      END ELSE
         EST.PROD.BASIS.WT<1,COMP,MPTR>=INV.BAS.WT
      END
      EST.PROD.FINISH<1,COMP,MPTR>=INV.PAP.FINSH
      EST.PROD.COLOR<1,COMP,MPTR>=INV.COLOR
      IF INV.PPI+0 > 0 THEN
         EST.PROD.PPI<1,COMP,MPTR>=INV.PPI
         EST.PROD.CALIPER<1,COMP,MPTR>=""
      END ELSE
         EST.PROD.CALIPER<1,COMP,MPTR>=ESTM.CALIPER<1,MP>
         EST.PROD.PPI<1,COMP,MPTR>=ESTM.PPI<1,MP>
      END
   END
   EST.PROD.OS.GRAIN<1,COMP,MPTR>=ESTM.GRAIN<1,MP>
   EST.PROD.PS.WIDTH<1,COMP,MPTR>=EST.PROD.OS.WIDTH<1,COMP,MPTR>
   EST.PROD.PS.GRAIN<1,COMP,MPTR>=ESTM.GRAIN<1,MP>
   EST.PROD.COATING<1,COMP,MPTR>=ESTM.COAT.CD<1,MP>
   EST.PROD.M.WT<1,COMP,MPTR>=ESTM.M.WEIGHT<1,MP>
   PRICE.LINE.CNT=COUNT(EST.PROD.PQTY<1,COMP,MPTR>,"!")+(EST.PROD.PQTY<1,COMP,MPTR> # "")
   COLOR.LINE.CNT=COUNT(EST.PROD.INK.ID<1,COMP,MPTR>,"!")+(EST.PROD.INK.ID<1,COMP,MPTR> # "")
   GOSUB 80000
*T27924 v
   CQTY.REF.NO=1
   CQTY.CURR.REF.NO = ''
   GOSUB 85200
*T27924 ^
   PRICE.REF.NO = 1
   GOSUB 85100
   COLOR.CURR.REF.NO=""
   COLOR.REF.NO=1
   GOSUB 50000
   RETURN
*
*---- GET PRESS ID
*
1210*
   PRS=FIELD(EST.PROD.PRESS.ID<1,COMP>,"-",1)
   MATREAD CCTR.REC FROM COST.CNTR,CONO:PRS ELSE
      ERRMSG="Invalid press ID. Try again! "
      GOSUB 90000
      GOTO 1210
   END
   MATREAD EQUIPMENT.REC FROM EQUIPMENT,CONO:PRS ELSE
      ERRMSG="Cannot locate press on equipment file. Try again! "
      GOSUB 90000
      GOTO 1210
   END
*
*---- GET PRINT MODE
*
   SCREEN FIELD;;26
   SCREEN INPUT;;26;RETURN
   EST.PROD.MODE<1,COMP,MPTR>=S$VALUE
*
*---- GET PAGE COUNT
*
1220*
*     PG.CNT=EST.PROD.PG.CNT<1,COMP,1>
*     SCREEN FIELD;;28
*     SCREEN INPUT;;28;RETURN
*     PG.CNT=S$VALUE
*
*---- GET PAGES PER FORM
*
1222*
   SCREEN FIELD;;29
   SCREEN INPUT;;29;RETURN
   PGS.PER.FORM=S$VALUE
   EST.PROD.PGS.PER.FORM<1,COMP,MPTR>=PGS.PER.FORM
*
*---- GET COLORS ON SIDE 1
*
   SCREEN FIELD;;30
   SCREEN INPUT;;30;RETURN
   EST.PROD.COLORS.1<1,COMP,MPTR>=S$VALUE
*
*---- GET COLORS ON SIDE 2
*
   IF EST.PROD.MODE<1,COMP,MPTR>="SS" THEN
      EST.PROD.COLORS.2<1,COMP,MPTR>=""
      S$DATA(31)<S$SCR>=""
      SCREEN DISPLAY;;31
   END ELSE
      SCREEN FIELD;;31
      SCREEN INPUT;;31;RETURN
      EST.PROD.COLORS.2<1,COMP,MPTR>=S$VALUE
   END
*
*---- GET NUMBER OF PLATES
*
*     IF EST.PROD.PLATES<1,COMP,MPTR>="" THEN GOSUB 84000
   GOSUB 84000
   SCREEN FIELD;;32
   SCREEN INPUT;;32;RETURN
   EST.PROD.PLATES<1,COMP,MPTR>=S$VALUE
*
*---- COMPUTE NUMBER OF PASSES PER FORM
*
   BEGIN CASE
      CASE EST.PROD.MODE<1,COMP,MPTR>="SS" OR EST.PROD.MODE<1,COMP,MPTR>="P"
         EST.PROD.PASSES<1,COMP,MPTR>=1
      CASE 1
         EST.PROD.PASSES<1,COMP,MPTR>=2
   END CASE
*
*---- GET UNTRIMMED SIG SIZE
*
* T21528 I TOOK OUT COMMENTS FROM FOLLOWING LINES
   SCREEN FIELD;;57
   SCREEN INPUT;;57;RETURN
   EST.PROD.US.WIDTH<1,COMP,MPTR>=S$VALUE
   SCREEN FIELD;;58
   SCREEN INPUT;;58;RETURN
   EST.PROD.US.LENGTH<1,COMP,MPTR>=S$VALUE
*
*---- GET TRIMMED SIG SIZE
*
   SCREEN FIELD;;59
   SCREEN INPUT;;59;RETURN
   EST.PROD.TS.WIDTH<1,COMP,MPTR>=S$VALUE
   SCREEN FIELD;;60
   SCREEN INPUT;;60;RETURN
   EST.PROD.TS.LENGTH<1,COMP,MPTR>=S$VALUE
*
*---- GET BINDERY SPOILAGE TABLE OR PERCENTAGE
*
1250*
   SCREEN FIELD;;34
   SCREEN INPUT;;34;RETURN
   IF NUM(S$VALUE) THEN
      EST.PROD.BIND.SPOIL<1,COMP,MPTR>=S$VALUE
   END ELSE
      READV REC FROM ESTIMATE.BIND.SPOIL,CONO:S$VALUE,1 ELSE
         ERRMSG="Invalid bindery spoilage table. Try again! "
         GOSUB 90000
         GOTO 1250
      END
      EST.PROD.BIND.SPOIL<1,COMP,MPTR>=S$VALUE
   END
*
*---- GET PRESS SPOILAGE TABLE OR PERCENTAGE
*
1260*
   SCREEN FIELD;;53
   SCREEN INPUT;;53;RETURN
   IF NUM(S$VALUE) THEN
      EST.PROD.PRESS.SPOIL<1,COMP,MPTR>=S$VALUE
   END ELSE
      MATREAD PST.REC FROM ESTIMATE.PRESS.SPOIL,CONO:S$VALUE ELSE
         ERRMSG="Invalid press spoilage table. Try again! "
         GOSUB 90000
         GOTO 1260
      END
      EST.PROD.PRESS.SPOIL<1,COMP,MPTR>=S$VALUE
      EST.PROD.INIT.MR.IMP<1,COMP,MPTR>=PST.INIT.QTY
      EST.PROD.SUBS.MR.IMP<1,COMP,MPTR>=PST.SUB.QTY
      S$DATA(33)<S$SCR>=PST.INIT.QTY
      SCREEN DISPLAY;;33
      S$DATA(54)<S$SCR>=PST.SUB.QTY
      SCREEN DISPLAY;;54
   END
*
*---- GET INITIAL M/R COUNT
*
   SCREEN FIELD;;55
   SCREEN INPUT;;55;RETURN
   EST.PROD.INIT.MR.CNT<1,COMP,MPTR>=S$VALUE
*
*---- GET SUBSEQUENT M/R COUNT
*
   S$DATA(56)<S$SCR>=EST.PROD.PASSES<1,COMP,MPTR>*EST.PROD.FORMS<1,COMP,1>-EST.PROD.INIT.MR.CNT<1,COMP,MPTR>
   IF S$DATA(56)<S$SCR> < 0 THEN S$DATA(56)<S$SCR>=0
   SCREEN FIELD;;56
   SCREEN INPUT;;56;RETURN
   EST.PROD.SUBS.MR.CNT<1,COMP,MPTR>=S$VALUE
*
*---- GET INITIAL M/R IMPRESSIONS
*
   SCREEN FIELD;;33
   SCREEN INPUT;;33;RETURN
   EST.PROD.INIT.MR.IMP<1,COMP,MPTR>=S$VALUE
*
*---- GET SUBSEQUENT M/R IMPRESSIONS
*
   SCREEN FIELD;;54
   SCREEN INPUT;;54;RETURN
   EST.PROD.SUBS.MR.IMP<1,COMP,MPTR>=S$VALUE
   RETURN
* T21528 TOOK OUT ABOVE COMMENT LINES
*
*---- GET PAPER PRICE DATA
*
40000*
   PRICE.CURR.REF.NO = ''
   PRICE.REF.NO = 1
   GOSUB 85100
40100*
   FOR PRICE.REF.NO=1 TO PRICE.LINE.CNT
      GOSUB 40200
   NEXT PRICE.REF.NO
   RETURN
40200*
   IF MODE="C" OR FIELD(EST.PROD.PCST<1,COMP,MPTR>,"!",PRICE.REF.NO)="" THEN
      IF MOD(PRICE.REF.NO,8) AND PRICE.REF.NO # PRICE.LINE.CNT THEN GOSUB 85100
      S$VAL=PRICE.REF.NO
      SCREEN FIELD;;43
      IF PRICE.REF.NO > 1 THEN
         S$O.R="O";S$DEFAULT=FIELD(EST.PROD.PCST<1,COMP,MPTR>,"!",PRICE.REF.NO-1)
      END
      SCREEN INPUT;;43
      IF S$VALUE # "END" THEN
         MACRO REPVAL;EST.PROD.PCST<1,COMP,MPTR>;"!";PRICE.REF.NO;S$VALUE
      END
   END
   RETURN
*
*---- DISPLAY COLOR DATA
*
50000*
   START.REF.NO=1+INT((COLOR.REF.NO-1)/COLOR.LINE.COUNT)*COLOR.LINE.COUNT
   IF START.REF.NO=COLOR.CURR.REF.NO THEN RETURN
   COLOR.CURR.REF.NO=START.REF.NO
   S$VAL=START.REF.NO
   S$CNT=COLOR.LINE.CNT
   SCREEN MULTI;;C;35;36;37;38
   RETURN
*
*---- LOAD SCREEN DATA
*
80000*
   IF NEW.REC THEN
      AC=COUNT(EST.A9C,",")+1
      FOR AP=1 TO AC
         AA=FIELD(EST.A9C,",",AP)
         AA1=FIELD(AA,"-",1)
         AA2=FIELD(AA,"-",2)
         IF AA2="" THEN AA2=AA1
         FOR AP1=AA1 TO AA2
            EST.REC(AP1)<1,COMP,MPTR>=EST.REC(AP1)<1,COMP,1>
         NEXT AP1
      NEXT AP
      NEW.REC = 0
   END ELSE
      EST.PROD.NO.UP<1,COMP,MPTR> = EST.PROD.NO.UP<1,COMP,1>
      EST.PROD.FORMS<1,COMP,MPTR> = EST.PROD.FORMS<1,COMP,1>
      EST.PROD.BIND.SPOIL<1,COMP,MPTR> = EST.PROD.BIND.SPOIL<1,COMP,1>
      EST.PROD.PG.CNT<1,COMP,MPTR> = EST.PROD.PG.CNT<1,COMP,1>
      EST.PROD.PRESS.SPOIL<1,COMP,MPTR> = EST.PROD.PRESS.SPOIL<1,COMP,1>
      EST.PROD.INIT.MR.CNT<1,COMP,MPTR> = EST.PROD.INIT.MR.CNT<1,COMP,1>
      EST.PROD.INIT.MR.IMP<1,COMP,MPTR> = EST.PROD.INIT.MR.IMP<1,COMP,1>
      EST.PROD.SUBS.MR.CNT<1,COMP,MPTR> = EST.PROD.SUBS.MR.CNT<1,COMP,1>
      EST.PROD.SUBS.MR.IMP<1,COMP,MPTR> = EST.PROD.SUBS.MR.IMP<1,COMP,1>
      EST.PROD.US.WIDTH<1,COMP,MPTR> = EST.PROD.US.WIDTH<1,COMP,1>
      EST.PROD.US.LENGTH<1,COMP,MPTR> = EST.PROD.US.LENGTH<1,COMP,1>
      EST.PROD.TS.WIDTH<1,COMP,MPTR> = EST.PROD.TS.WIDTH<1,COMP,1>
      EST.PROD.TS.LENGTH<1,COMP,MPTR> = EST.PROD.TS.LENGTH<1,COMP,1>
   END
   CURR.WEB.CNT = COUNT(EST.PROD.OS.PROD<1,COMP>,SM)+1
   S$DATA(1)<S$SCR>=DATE()
   S$DATA(2)<S$SCR>=COMP
   S$DATA(3)<S$SCR>=EST.PROD.DESC<1,COMP>
   S$DATA(23)<S$SCR>=MPTR;* T27924
   S$DATA(4)<S$SCR>=EST.PROD.OS.TYPE<1,COMP>
   TYPE=EST.PROD.OS.TYPE<1,COMP>
   BEGIN CASE
      CASE TYPE="S"
         S$DATA(5)<S$SCR>="SHEET"
      CASE TYPE="R"
         S$DATA(5)<S$SCR>="ROLL"
      CASE TYPE="L"
         S$DATA(5)<S$SCR>="LABEL"
      CASE TYPE=""
         S$DATA(5)<S$SCR>=""
      CASE 1
         S$DATA(5)<S$SCR>="??????"
   END CASE
   S$DATA(47)<S$SCR>=EST.PROD.OS.USAGE<1,COMP,MPTR>
*T21738 v
   MATREAD ESTPG.REC FROM ESTIMATE.PAPER.GROUP,CONO:EST.PROD.OS.USAGE<1,COMP,MPTR> ELSE
      MAT ESTPG.REC = ''
   END
   S$DATA(48)<S$SCR>=ESTPG.DESC
*T21738 ^
   S$DATA(6)<S$SCR>=EST.PROD.OS.PROD<1,COMP,MPTR>
   IF EST.PROD.OS.TYPE<1,COMP>="" THEN
      MAT ESTM.REC=""
      MP=1
   END ELSE
      MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,MPTR> ELSE
         MAT ESTM.REC=""
      END
      LOCATE EST.PROD.OS.PROD<1,COMP,MPTR> IN ESTM.PROD<1>,1 SETTING MP ELSE
         MP=999
      END
   END
   IF EST.PROD.OS.DESC<1,COMP,MPTR> # "" THEN
      S$DATA(7)<S$SCR>=EST.PROD.OS.DESC<1,COMP,MPTR>
   END ELSE
      S$DATA(7)<S$SCR>=ESTM.DESC<1,MP>
   END
*T25628 v
   IF MPTR # '' THEN
      S$DATA(8)<S$SCR>=EST.PROD.OS.WIDTH<1,COMP,MPTR>
      S$DATA(9)<S$SCR>=EST.PROD.OS.LENGTH<1,COMP,MPTR>
      S$DATA(11)<S$SCR>=EST.PROD.PS.WIDTH<1,COMP,MPTR>
      S$DATA(12)<S$SCR>=EST.PROD.PS.LENGTH<1,COMP,MPTR>
   END ELSE
      S$DATA(8)<S$SCR>=EST.PROD.OS.WIDTH<1,COMP,1>
      S$DATA(9)<S$SCR>=EST.PROD.OS.LENGTH<1,COMP,1>
      S$DATA(11)<S$SCR>=EST.PROD.PS.WIDTH<1,COMP,1>
      S$DATA(12)<S$SCR>=EST.PROD.PS.LENGTH<1,COMP,1>
   END
*T25628 ^
   S$DATA(10)<S$SCR>=EST.PROD.OS.GRAIN<1,COMP,MPTR>
   S$DATA(14)<S$SCR>=EST.PROD.BASIS.WT<1,COMP,MPTR>
   S$DATA(15)<S$SCR>=EST.PROD.COATING<1,COMP,MPTR>
   S$DATA(16)<S$SCR>=EST.PROD.PLINE<1,COMP,MPTR>
   IF CAL.PPI="P" THEN
      S$DATA(17)<S$SCR>=EST.PROD.PPI<1,COMP,MPTR>
   END ELSE
      S$DATA(17)<S$SCR>=EST.PROD.CALIPER<1,COMP,MPTR>
   END
   S$DATA(18)<S$SCR>=EST.PROD.COLOR<1,COMP,MPTR>
   S$DATA(19)<S$SCR>=EST.PROD.FINISH<1,COMP,MPTR>
   S$DATA(20)<S$SCR>=EST.PROD.INV.ID<1,COMP,MPTR>
   S$DATA(21)<S$SCR>=EST.PROD.PSS.OSS<1,COMP,MPTR>
   IF EST.PROD.DC.CODE<1,COMP>="" THEN
      S$DATA(24)<S$SCR>=EST.PROD.PRESS.ID<1,COMP>
   END ELSE
      S$DATA(24)<S$SCR>=EST.PROD.PRESS.ID<1,COMP>:"-":EST.PROD.DC.CODE<1,COMP>
   END
   IF EST.PROD.PRESS.ID<1,COMP>="" THEN
      MAT CCTR.REC=""
      MAT EQUIPMENT.REC=""
   END ELSE
      MATREAD CCTR.REC FROM COST.CNTR,CONO:EST.PROD.PRESS.ID<1,COMP> ELSE
         MAT CCTR.REC=""
         CCTR.DESC="???????????????"
      END
      MATREAD EQUIPMENT.REC FROM EQUIPMENT,CONO:EST.PROD.PRESS.ID<1,COMP> ELSE
         MAT EQUIPMENT.REC=""
      END
   END
   S$DATA(25)<S$SCR>=CCTR.DESC
   S$DATA(26)<S$SCR>=EST.PROD.MODE<1,COMP,MPTR>
   S$DATA(22)<S$SCR>=EST.PROD.WEB.CNT<1,COMP>
   S$DATA(28)<S$SCR>=EST.PROD.PG.CNT<1,COMP,1>
   S$DATA(27)<S$SCR>=EST.PROD.PGS.ALL.WEBS<1,COMP>
   IF MPTR=WEB.CNT THEN
      PGS.PER.FORM=EST.PROD.PGS.ALL.WEBS<1,COMP>
      FOR N=1 TO WEB.CNT-1
         PGS.PER.FORM=PGS.PER.FORM-EST.PROD.PGS.PER.FORM<1,COMP,N>
      NEXT N
      IF PGS.PER.FORM > 0 THEN
         EST.PROD.PGS.PER.FORM<1,COMP,MPTR>=PGS.PER.FORM
      END
   END
   S$DATA(29)<S$SCR>=EST.PROD.PGS.PER.FORM<1,COMP,MPTR>
   S$DATA(30)<S$SCR>=EST.PROD.COLORS.1<1,COMP,MPTR>
   S$DATA(31)<S$SCR>=EST.PROD.COLORS.2<1,COMP,MPTR>
   S$DATA(32)<S$SCR>=EST.PROD.PLATES<1,COMP,MPTR>
   S$DATA(34)<S$SCR>=EST.PROD.BIND.SPOIL<1,COMP,MPTR>
   S$DATA(53)<S$SCR>=EST.PROD.PRESS.SPOIL<1,COMP,MPTR>
   S$DATA(55)<S$SCR>=EST.PROD.INIT.MR.CNT<1,COMP,MPTR>
   S$DATA(56)<S$SCR>=EST.PROD.SUBS.MR.CNT<1,COMP,MPTR>
   S$DATA(33)<S$SCR>=EST.PROD.INIT.MR.IMP<1,COMP,MPTR>
   S$DATA(54)<S$SCR>=EST.PROD.SUBS.MR.IMP<1,COMP,MPTR>
   S$DATA(13)<S$SCR>=EST.PROD.NO.UP<1,COMP,MPTR>;* T27924
   S$DATA(49)<S$SCR>=EST.PROD.FORMS<1,COMP,MPTR>
   S$DATA(57)<S$SCR>=EST.PROD.US.WIDTH<1,COMP,MPTR>
   S$DATA(58)<S$SCR>=EST.PROD.US.LENGTH<1,COMP,MPTR>
   S$DATA(59)<S$SCR>=EST.PROD.TS.WIDTH<1,COMP,MPTR>
   S$DATA(60)<S$SCR>=EST.PROD.TS.LENGTH<1,COMP,MPTR>
      * TASK 16090 *
   IF EQP.PRESS.TYPE = "W" THEN
      S$DATA(50)<S$SCR>=EST.PROD.OS.LENGTH<1,COMP,1>
   END ELSE
      S$DATA(50)<S$SCR>=''
   END
   IF PRICE.LINE.CNT=0 THEN
      S$DATA(42)<S$SCR>=""
      S$DATA(43)<S$SCR>=""
   END ELSE
      FOR QPTR=1 TO PRICE.LINE.CNT
         S$DATA(42)<S$SCR,QPTR>=FIELD(EST.PROD.PQTY<1,COMP,MPTR>,"!",QPTR)
         S$DATA(43)<S$SCR,QPTR>=FIELD(EST.PROD.PCST<1,COMP,MPTR>,"!",QPTR)
      NEXT QPTR
   END
   IF COLOR.LINE.CNT=0 THEN
      S$DATA(36)<S$SCR>=""
      S$DATA(37)<S$SCR>=""
      S$DATA(38)<S$SCR>=""
   END ELSE
      FOR IPTR=1 TO COLOR.LINE.CNT
         S$DATA(36)<S$SCR,IPTR>=FIELD(EST.PROD.INK.ID<1,COMP,MPTR>,"!",IPTR)
         S$DATA(37)<S$SCR,IPTR>=FIELD(EST.PROD.INK.SIDES<1,COMP,MPTR>,"!",IPTR)
         S$DATA(38)<S$SCR,IPTR>=FIELD(EST.PROD.INK.COVER<1,COMP,MPTR>,"!",IPTR)
      NEXT IPTR
   END
80900*
   SCREEN DISPLAY;;ALL
   RETURN
*
*---- CALCULATE PLATE REQUIREMENTS
*
84000*
   PMODE=EST.PROD.MODE<1,COMP,MPTR>
   BEGIN CASE
      CASE PMODE="WT" OR PMODE="WR"
         IF EST.PROD.COLORS.1<1,COMP,1> > EST.PROD.COLORS.2<1,COMP,1> THEN
            EST.PROD.PLATES<1,COMP,MPTR>=EST.PROD.COLORS.1<1,COMP,MPTR>
         END ELSE
            EST.PROD.PLATES<1,COMP,MPTR>=EST.PROD.COLORS.2<1,COMP,MPTR>
         END
      CASE 1
         EST.PROD.PLATES<1,COMP,MPTR>=EST.PROD.COLORS.1<1,COMP,MPTR>+EST.PROD.COLORS.2<1,COMP,MPTR>
         EST.PROD.PLATES<1,COMP,MPTR>=EST.PROD.PLATES<1,COMP,MPTR>*EST.PROD.FORMS<1,COMP,1>
   END CASE
   S$DATA(32)<S$SCR>=EST.PROD.PLATES<1,COMP,MPTR>
   SCREEN DISPLAY;;32
   RETURN
*
*---- CALCULATE PAPER REQUIREMENTS
*
85000*
   IF EST.PROD.OS.TYPE<1,COMP>="" OR EST.PROD.PRESS.ID<1,COMP>="" THEN
      EST.PROD.PQTY<1,COMP,MPTR>=""
      EST.PROD.PCST<1,COMP,MPTR>=""
      S$DATA(42)<S$SCR>=""
      S$DATA(43)<S$SCR>=""
      PRICE.LINE.CNT=0
      RETURN
   END ELSE
      MATREAD ESTM.REC FROM ESTIMATE.MATL,CONO:EST.PROD.OS.TYPE<1,COMP>:EST.PROD.OS.USAGE<1,COMP,MPTR> ELSE
         MAT ESTM.REC=""
      END
   END
   PRICE.LINE.CNT=COUNT(EST.QTY,VM)+(EST.QTY # "")
   CALL EST.PROD.SUB.1B(CONO,"",COMP,MPTR)
   CALL EST.CALC.STK.PRICE(CONO,"")
   S$DATA(21)<S$SCR>=EST.PROD.PSS.OSS<1,COMP,MPTR>
   SCREEN DISPLAY;;21
   FOR QPTR=1 TO PRICE.LINE.CNT
      S$DATA(42)<S$SCR,QPTR>=FIELD(EST.PROD.PQTY<1,COMP,MPTR>,"!",QPTR)
      S$DATA(43)<S$SCR,QPTR>=FIELD(EST.PROD.PCST<1,COMP,MPTR>,"!",QPTR)
   NEXT QPTR
   RETURN
*
85100*
   START.REF.NO=1+INT((PRICE.REF.NO-1)/PRICE.LINE.COUNT)*PRICE.LINE.COUNT
   IF START.REF.NO=PRICE.CURR.REF.NO THEN RETURN
   PRICE.CURR.REF.NO=START.REF.NO
   S$VAL=START.REF.NO
   S$CNT=PRICE.LINE.CNT
   SCREEN MULTI;;C;41;42;43
   RETURN
*T27924 v
*
*-SCROLL COMPONENT QTY DATA
*
85200*
   START.REF.NO=1+INT((CQTY.REF.NO-1)/CQTY.LINE.COUNT)*CQTY.LINE.COUNT
   IF START.REF.NO=CQTY.CURR.REF.NO THEN RETURN
   CQTY.CURR.REF.NO=START.REF.NO
   S$VAL=START.REF.NO
   S$CNT=CQTY.LINE.CNT
   SCREEN MULTI;;C;1;51
   RETURN
*
*
*---- Load Screen Data for Comp Qty
*
11690 *
   IF CQTY.LINE.CNT=0 THEN
      S$DATA(51)<S$SCR>=""
   END ELSE
      FOR QPTR=1 TO CQTY.LINE.CNT
         COMPQTY = EST.COMP.QTY<1,COMP,QPTR>
         IF COMPQTY # "" THEN
            S$DATA(51)<S$SCR,QPTR>=COMPQTY
         END ELSE
            S$DATA(51)<S$SCR,QPTR>=EST.QTY<1,QPTR>
         END
      NEXT QPTR
   END
   RETURN
*
*T27924 ^
*
*---- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
*
*---- END OF PROGRAM
*
99999*
   SCREEN CLEAR;;D
   RETURN
END
