   SUBROUTINE EST.RL.RECAP.RPT (CONO, CO.NAME, EST.KEY, PAGE.NO,PRT.QTY)
*********************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.RL.RECAP.RPT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 05/12/86
*
* DESCRIPTION
*
* This subroutine prints the estimate summary by type 
* MOD 6/10/94 TASK 18171 (LMR)
* MOD TASK 17918 DMT 8/19/94 ADD BACKMARGIN CALC
* MOD 18484 RKB 10/19/94 ADD SUPPORT FOR 99 QTYS
*T26138 cm 09/14/2001 * Expand estimate qty from 8 to 9 digits.
*********************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.RL
*COPY>JES.CPYLIB>EST.RL.COST.TYPE.SUM
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>CUSTOMER
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- DEFINE DIMENSIONED VARIABLES
*
   DIM PHEAD(10)
   MAT PHEAD = ""
   PHEAD(1) = "TOTAL STOCK"
   PHEAD(2) = "TOTAL MATERIAL - INK"
   PHEAD(3) = "TOTAL MATERIAL - CASE"
   PHEAD(4) = "TOTAL MATERIAL - REGULAR"
   PHEAD(5) = "TOTAL OUTSIDE PURCHASES"
   PHEAD(6) = "TOTAL SHIPPING"
   PHEAD(7) = "TOTAL MISCELLANEOUS"
   PHEAD(8) = "TOTAL LABOR"
   DIM PTABLE(20)
   MAT PTABLE = ""
   EQU PAPER.SALE TO PTABLE(1)
   EQU INK.SALE   TO PTABLE(2)
   EQU CASE.SALE  TO PTABLE(3)
   EQU OTHER.SALE TO PTABLE(4)
   EQU OS.SALE    TO PTABLE(5)
   EQU SP.SALE    TO PTABLE(6)
   EQU MS.SALE    TO PTABLE(7)
   EQU LB.SALE    TO PTABLE(8)
   EQU TOTAL.SALE TO PTABLE(10)
   EQU PAPER.SALE.PCT TO PTABLE(11)
   EQU INK.SALE.PCT   TO PTABLE(12)
   EQU CASE.SALE.PCT  TO PTABLE(13)
   EQU OTHER.SALE.PCT TO PTABLE(14)
   EQU OS.SALE.PCT    TO PTABLE(15)
   EQU SP.SALE.PCT    TO PTABLE(16)
   EQU MS.SALE.PCT    TO PTABLE(17)
   EQU LB.SALE.PCT    TO PTABLE(18)
   EQU TOTAL.SALE.PCT TO PTABLE(19)
   DIM TOT.REC(10)
   MAT TOT.REC = ""
   EQU TOTAL.FCOST           TO TOT.REC(1)
   EQU TOTAL.VCOST           TO TOT.REC(2)
   EQU TOTAL.TCOST           TO TOT.REC(3)
   EQU TOTAL.FSALE           TO TOT.REC(6)
   EQU TOTAL.VSALE           TO TOT.REC(7)
   EQU TOTAL.TSALE           TO TOT.REC(8)
   LB.COPY = ""; LB.COLOR = ""; LB.VERSION = ""
   MT.COPY = ""; MT.COLOR = ""; MT.VERSION = ""
*
*---- INITIALIZATION
*
   HD1A = OCONV(DATE(),"D2/")
   HD1B = SPACE((40-LEN(CO.NAME))/2):CO.NAME
   HD1 = HD1A"L#46":HD1B"L#40":SPACE(34):"PAGE: "
   HD2 = SPACE(45):"E S T I M A T E   R E C A P   R E P O R T"
   MAT CUST.REC = ""
   IF EST.CUST = "P" THEN
      CUST.NAME = EST.CUST.NAME
   END ELSE
      MATREAD CUST.REC FROM CUSTOMER, CONO:EST.CUST ELSE
         MAT CUST.REC = ""
         CUST.NAME = EST.CUST.NAME
      END
   END
   HD4A = "ESTIMATE : ":EST.KEY
   HD4B = CUST.NAME
   HD4C = EST.COMMENTS<1,1>
   HD4 = HD4A"L#27":HD4B"L#44":HD4C
*
*---- MAIN PROCESSING
*
   PRINTER ON
   GOSUB 10000
   MAT CSUM.REC = ""
* 18484 v
   EC = DCOUNT(PRT.QTY,SVM)
   QPTR = ''
   FOR Q = 1 TO EC
      LOCATE PRT.QTY<1,1,Q> IN EST.QTY<1>,1 SETTING Q1 THEN
         QPTR<1,Q> = Q1
      END
   NEXT Q
*18484 ^
   IF EC > 3 THEN EM = 3 ELSE EM = EC
   FOR EP = 1 TO EC
      QSEL = EST.QTY<1,QPTR<1,EP>>  ;* 18484
      CALL EST.RL.COST.TYPE.SUB (CONO, EST.KEY, "ALL", "ALL", QSEL, MAT CSUM.REC)
      PAPER.SALE<EP> = CSUM.MT.SALE.DET<1,1>
      INK.SALE<EP> = CSUM.MT.SALE.DET<1,2>
      CASE.SALE<EP> = CSUM.MT.SALE.DET<1,5>
      OTHER.SALE<EP> = CSUM.MT.SALE - PAPER.SALE<EP> - INK.SALE<EP> - CASE.SALE<EP>
      OS.SALE<EP> = CSUM.OS.SALE
      SP.SALE<EP> = CSUM.SP.SALE
      MS.SALE<EP> = CSUM.MS.SALE
      LB.SALE<EP> = CSUM.LB.SALE
      PAPER.SALE.PCT<EP>  = PAPER.SALE<EP> / CSUM.TOT.SALE
      INK.SALE.PCT<EP>    =  INK.SALE<EP> / CSUM.TOT.SALE
      CASE.SALE.PCT<EP>   = CASE.SALE<EP> / CSUM.TOT.SALE
      OTHER.SALE.PCT<EP>  = OTHER.SALE<EP> / CSUM.TOT.SALE
      OS.SALE.PCT<EP>     = OS.SALE<EP> / CSUM.TOT.SALE
      SP.SALE.PCT<EP>     = SP.SALE<EP> / CSUM.TOT.SALE
      MS.SALE.PCT<EP>     = MS.SALE<EP> / CSUM.TOT.SALE
      LB.SALE.PCT<EP>     = LB.SALE<EP> / CSUM.TOT.SALE
      TOTAL.SALE<EP> = CSUM.TOT.SALE
      TOTAL.SALE.PCT<EP>  = PAPER.SALE.PCT<EP> + INK.SALE.PCT<EP> 
      TOTAL.SALE.PCT<EP>  = TOTAL.SALE.PCT<EP> + CASE.SALE.PCT<EP> + OTHER.SALE.PCT<EP> + OS.SALE.PCT<EP> + SP.SALE.PCT<EP> + MS.SALE.PCT<EP> + LB.SALE.PCT<EP>
      LB.COPY<EP> = CSUM.LB.COPY
      LB.COLOR<EP> = CSUM.LB.COLOR
      LB.VERSION<EP> = CSUM.LB.VERSION
      MT.COPY<EP> = CSUM.MT.COPY
      MT.COLOR<EP> = CSUM.MT.COLOR
      MT.VERSION<EP> = CSUM.MT.VERSION
      IF TOTAL.SALE.PCT<EP> > 1.0 THEN TOTAL.SALE.PCT<EP> = 10000
   NEXT EP
   PLINE="""L#30"
   FOR EP = 1 TO EM
      QQ = OCONV(EST.QTY<1,QPTR<1,EP>>,"MD0,")
*T26138 v
*     QQ = QQ:SPACE((10-LEN(QQ))/2)
*     PLINE=PLINE:SPACE(1):"<---------- ":QQ"R#10":" --------->"
      QQ = QQ:SPACE((11-LEN(QQ))/2)
      PLINE=PLINE:SPACE(1):"<--------- ":QQ"R#11":" --------->"
*T26138 ^
   NEXT EP
   PRINT PLINE
   PLINE="""L#30"
   FOR EP = 1 TO EM
      PLINE=PLINE:SPACE(1):" RECOMMENDED":SPACE(1):"PERCENT ":"  CUSTOMER  "
   NEXT EP
   PRINT PLINE
   PLINE="         DESCRIPTION          ""L#30"
   FOR EP = 1 TO EM
      PLINE=PLINE:SPACE(1):"    SELL    ":SPACE(1):"OF SALE ":"   QUOTE    "
   NEXT EP
   PRINT PLINE
   PLINE="------------------------------""L#30"
   FOR EP = 1 TO EM
      PLINE=PLINE:SPACE(1):"------------":SPACE(1):"------- ":"------------"
   NEXT EP
   PRINT PLINE
   FOR PP = 1 TO 10
      IF PHEAD(PP) # "" AND PTABLE(PP) # STR(AM,LEN(PTABLE(PP))) THEN
         PRINT
         PLINE=PHEAD(PP)"L#30"
         FOR EP = 1 TO EM
            PTABLE(PP+10)<EP> = ICONV(PTABLE(PP+10)<EP>,"MD4")
            PLINE=PLINE:SPACE(1):OCONV(PTABLE(PP)<EP>,"MD2Z,")"R#12":SPACE(1):OCONV(PTABLE(PP+10)<EP>,"MD2")"R#6":"%"
            PLINE=PLINE:SPACE(1):"____________"
         NEXT EP
         PRINT PLINE
      END
   NEXT PP
   PRINT
   PRINT
   PRINT
   PLINE="**** ESTIMATE TOTAL ****""L#30"
   FOR EP = 1 TO EM
      TOTAL.SALE.PCT<EP> = ICONV(TOTAL.SALE.PCT<EP>,"MD4")
      IF TOTAL.SALE.PCT<EP> > 10000 THEN TOTAL.SALE.PCT<EP> = 10000
      PLINE=PLINE:SPACE(1):OCONV(TOTAL.SALE<EP>,"MD2Z,")"R#12":SPACE(1):OCONV(TOTAL.SALE.PCT<EP>,"MD2")"R#6":"%"
      PLINE=PLINE:SPACE(1):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PRINT
   QC = DCOUNT(EST.QTY,VM)
   DC = DCOUNT(EST.DEPT.COMP,VM)
   FOR DP = 1 TO DC
      EQTY = FIELD(EST.DEPT.COMP<1,DP>,"!",3)
      LOCATE EQTY IN PRT.QTY<1,1>,1 SETTING EP THEN
         TOTAL.TCOST<1,EP>=TOTAL.TCOST<1,EP>+EST.DEPT.COMP.COST<1,DP>
         TOTAL.VCOST<1,EP>=TOTAL.VCOST<1,EP>+EST.DEPT.COMP.VCOST<1,DP>
         TOTAL.FCOST<1,EP>=TOTAL.TCOST<1,EP>-TOTAL.VCOST<1,EP>
         TOTAL.TSALE<1,EP>=TOTAL.TSALE<1,EP>+EST.DEPT.COMP.TSALE<1,DP>
         TOTAL.VSALE<1,EP>=TOTAL.VSALE<1,EP>+EST.DEPT.COMP.VSALE<1,DP>
         TOTAL.FSALE<1,EP>=TOTAL.TSALE<1,EP>-TOTAL.VSALE<1,EP>
      END
   NEXT DP
   PLINE="FIXED AMOUNT""L#30"
   FOR EP = 1 TO EM
      PLINE=PLINE:SPACE(1):OCONV(TOTAL.FSALE<1,EP>,"MD2Z,")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE="VARIABLE AMOUNT""L#30"
   FOR EP = 1 TO EM
      PLINE=PLINE:SPACE(1):OCONV(TOTAL.VSALE<1,EP>,"MD2Z,")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE = "MARK-UP PERCENTAGE:" "L#30"
   FOR EP = 1 TO EM
      MARKUP = OCONV(EST.OM.PCT<1,QPTR<1,EP>>,"MD4")
      PLINE = PLINE:SPACE(1):MARKUP "R#12":"%":SPACE(8):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE = "FINAL PRICE:" "L#30"
   FOR EP = 1 TO EM
      FP = EST.FINAL.PRICE<1,QPTR<1,EP>>
      PLINE = PLINE:SPACE(1):OCONV(FP,"MD2,Z")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE = "PRICE PER ":EST.PRT.PRICE.UOM 
   PLINE = PLINE "L#30"
   FOR EP = 1 TO EM
      IF EST.PRT.PRICE.UOM = 'EA' THEN
         PRICE.PER.UOM = OCONV(EST.PRICE.UOM<1,QPTR<1,EP>>,"MD4,Z")
      END ELSE
         PRICE.PER.UOM = OCONV(EST.PRICE.UOM<1,QPTR<1,EP>>,"MD2,Z")
      END
      PLINE = PLINE:SPACE(1):PRICE.PER.UOM "R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE="PRICE PER ADD'L 1,000""L#30"
   FOR EP = 1 TO EM
      PLINE=PLINE:SPACE(1):OCONV(EST.PRICE.THOU<1,QPTR<1,EP>>,"MD2Z,")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PRINT
   IF EST.RL.APPLY = "H" OR EST.RL.APPLY = "M" OR EST.RL.APPLY = "P" THEN
   END
   PLINE = "PRICE PER COPY CHANGE" "L#30"
   CHG.PC = EST.PROD.SUBS.MR.IMP<1,1,1> * EST.RL.COPY.CHANGES
   FOR EP = 1 TO EM
      LB.COST = 0; MT.COST = 0
      CCNT = DCOUNT(MT.COPY<EP>,VM)
      FOR I = 1 TO CCNT
         FND = 1
         LOCATE MT.COPY<EP,I> IN EST.PROD.OS.PROD<1,1>,1 SETTING POS ELSE FND = 0
         IF FND THEN
            CHG.MSI = INT(EST.PROD.OS.WIDTH<1,1,POS>/10000*CHG.PC*10/1000+.9)
            PRICE = FIELD(EST.RL.PAP.PRICE<1,1,POS>,"!",QPTR<1,EP>)
            MT.COST = MT.COST + INT(CHG.MSI * PRICE / 100 + 0.5)
         END
      NEXT I
      LB.COST = INT(EST.RL.MR.COPIES / 100 * LB.COPY<EP> + .5)
      PLINE=PLINE:SPACE(1):OCONV(MT.COST+LB.COST,"MD2Z,")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE = "PRICE PER COLOR CHANGE" "L#30"
   CHG.PC = EST.PROD.SUBS.MR.IMP<1,1,1> * EST.RL.COLOR.CHANGES
   FOR EP = 1 TO EM
      LB.COST = 0; MT.COST = 0
      CCNT = DCOUNT(MT.COLOR<EP>,VM)
      FOR I = 1 TO CCNT
         FND = 1
         LOCATE MT.COLOR<EP,I> IN EST.PROD.OS.PROD<1,1>,1 SETTING POS ELSE FND = 0
         IF FND THEN
            CHG.MSI = INT(EST.PROD.OS.WIDTH<1,1,POS>/10000*CHG.PC*10/1000+.9)
            PRICE = FIELD(EST.RL.PAP.PRICE<1,1,POS>,"!",QPTR<1,EP>)
            MT.COST = MT.COST + INT(CHG.MSI * PRICE / 100 + 0.5)
         END
      NEXT I
      LB.COST = INT(EST.RL.MR.COLORS / 100 * LB.COLOR<EP> + .5)
      PLINE=PLINE:SPACE(1):OCONV(MT.COST+LB.COST,"MD2Z,")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINT
   PLINE = "PRICE PER VERSION CHANGE" "L#30"
   CHG.PC = EST.PROD.INIT.MR.IMP<1,1,1> * EST.RL.VERSION.CHANGES
   FOR EP = 1 TO EM
      LB.COST = 0; MT.COST = 0
      CCNT = DCOUNT(MT.VERSION<EP>,VM)
      FOR I = 1 TO CCNT
         FND = 1
         LOCATE MT.VERSION<EP,I> IN EST.PROD.OS.PROD<1,1>,1 SETTING POS ELSE FND = 0
         IF FND THEN
            CHG.MSI = INT(EST.PROD.OS.WIDTH<1,1,POS>/10000*CHG.PC*10/1000+.9)
            PRICE = FIELD(EST.RL.PAP.PRICE<1,1,POS>,"!",QPTR<1,EP>)
            MT.COST = MT.COST + INT(CHG.MSI * PRICE / 100 + 0.5)
         END
      NEXT I
      LB.COST = INT(EST.RL.MR.VERSIONS / 100 * LB.VERSION<EP> + .5)
      PLINE=PLINE:SPACE(1):OCONV(MT.COST+LB.COST,"MD2Z,")"R#12":SPACE(9):"____________"
   NEXT EP
   PRINT PLINE
   PRINTER OFF
   RETURN
*
*---- PRINT HEADINGS
*
10000 *
   PRINT CHAR(12)
   PAGE.NO = PAGE.NO + 1
   PRINT HD1 : PAGE.NO
   PRINT HD2
   PRINT
   PRINT HD4
   PRINT
   PRINT
   RETURN
END
