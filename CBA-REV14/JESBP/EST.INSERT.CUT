   SUBROUTINE EST.INSERT.CUT (CONO, EQTY, DEPT, COMP, REF.NO, MAT ESTD.TEMP2)
***************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - EST.INSERT.CUT
*
* AUTHOR   - NICK AMENDOLA, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 12/01/86
*
* DESCRIPTION
*
* This subroutine updates the ESTIMATE.DEPT file with cutter specification
* data.
*
*T20853 doug 08/14/1996 * Only pull cost center from the defined
*                         component
*T27716 cmykleb 10/09/2003 * Add Pre-pres inclusive functionality.
*T27924 cmykleb 03/17/2004 * Move estimate qty to the component level.
***************************************************************************
*
*---- COPY STATEMENTS
*
*COPY>JES.CPYLIB>SCOMMON.ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>ESTIMATE.DEPT
*COPY>JES.CPYLIB>ESTIMATE.TEMP
*COPY>JES.CPYLIB>ESTIMATE.TEMP2
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>PMC.CPYLIB>COST.CNTR
*COPY>JCS.CPYLIB>OPERATION
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*
*---- INITIALIZATION
*
*T27924 v
*  BCNT = COUNT(EST.BIND.CUT.CCTR,VM) + (EST.BIND.CUT.CCTR # "")
   BCNT = DCOUNT(EST.BIND.CUT.CCTR<1,COMP>,SM)
*T27924 ^
   TCNT = COUNT(TEMP2.TYPE,VM) + (TEMP2.TYPE # "")
*
*---- MAIN PROCESSING
*
   BPTR = COMP;* T20853
   CCTR = EST.BIND.CUT.CCTR<1,BPTR,1>
   MATREAD CCTR.REC FROM COST.CNTR, CONO:CCTR ELSE GOTO 390
   FOR TDP = 1 TO TCNT
      MAT ESTD.TEMP = ""
      FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
         ESTD.TEMP(A) = ESTD.TEMP2(A)<1,TDP>
      NEXT A
      TEMP.CCTR = CCTR
      TEMP.CCTR.TYPE = CCTR.TYPE
      LOCATE TEMP.OPV IN CCTR.OPER<1>,1 SETTING OP.PTR ELSE GOTO 380
      TEMP.OPV.TYPE = CCTR.OPER.TYPE<1,OP.PTR>
      IF CCTR.OPER.STD.IMP<1,OP.PTR>+0=0 THEN TEMP.OPV.TYPE="F"
      MATREAD OPER.REC FROM OPERATION, CONO:TEMP.OPV ELSE
         MAT OPER.REC = ""
         OPER.DESC = "???????????????"
      END
      TEMP.DESC = OPER.DESC
      TEMP.GRP.CCTR = "CUT"
      IF TEMP.OPV.TYPE = "F" THEN
         TEMP.STD = CCTR.OPER.STD.HRS<1,OP.PTR> / 100
         TEMP.STD.TYPE = "HR/OP"
      END ELSE
         TEMP.STD = CCTR.OPER.STD.IMP<1,OP.PTR>
         TEMP.STD.TYPE = "OP/HR"
      END
      TEMP.HRS = ""
      TEMP.RATE = CCTR.OPER.HR.RATE<1,OP.PTR>
      TEMP.IND.1 = CCTR.FOH.TYPE<1,1>:CCTR.FOH.PCT<1,1>
      TEMP.IND.2 = CCTR.FOH.TYPE<1,2>:CCTR.FOH.PCT<1,2>
      TEMP.IND.3 = CCTR.FOH.TYPE<1,3>:CCTR.FOH.PCT<1,3>
      TEMP.IND.4 = CCTR.FOH.TYPE<1,4>:CCTR.FOH.PCT<1,4>
      TEMP.MARKUP = CCTR.OPER.MARKUP<1,OP.PTR>
      BEGIN CASE
         CASE TEMP.GRP.QCALC = ""
            TEMP.QTY = ""
         CASE TEMP.GRP.QCALC = "C"
            TEMP.QTY = TEMP.GRP.QPARAM
         CASE TEMP.GRP.QCALC = "F"
            TEMP.MPTR = BPTR
            TEMP.FPTR = REF.NO + 1
            SUB.NAME = "EST.FORMULA.Q":TEMP.GRP.QPARAM
            CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
*              IF TEMP.QTY = 0 THEN GOTO 380
            IF TEMP.QTY = "" THEN
               TEMP.GRP.QOR = ""
            END
      END CASE
      BEGIN CASE
         CASE TEMP.GRP.FCALC = ""
            TEMP.FCTR = 1000
         CASE TEMP.GRP.FCALC = "C"
            TEMP.FCTR = INT(TEMP.GRP.FPARAM*1000+0.5)
         CASE TEMP.GRP.FCALC = "M"
            TEMP.FCTR = INT(TEMP.GRP.FCTR*TEMP.GRP.FPARAM+0.5)
         CASE TEMP.GRP.FCALC = "F"
            TEMP.MPTR = BPTR
            TEMP.FPTR = REF.NO + 1
            SUB.NAME = "EST.FORMULA.F":TEMP.GRP.FPARAM
            CALL @SUB.NAME (CONO, "", EQTY, DEPT, COMP)
*              IF TEMP.FCTR = 0 THEN GOTO 380
            IF TEMP.FCTR = "" THEN TEMP.GRP.FOR = ""
         CASE TEMP.GRP.FCALC = "T"
            TBL.ID = TEMP.GRP.FPARAM
            CALL EST.TABLE.FCTR (CONO, TBL.ID, TEMP.GRP.FCTR)
            IF TEMP.FCTR+0 = 0 THEN TEMP.FCTR = 1000
      END CASE
*
*T27716 v
*     CALL EST.CALC.COST (CONO)
      CALL EST.CALC.COST (CONO,DEPT)
*T27716 ^
*
      REF.NO = REF.NO + 1
      FOR A = ESTD.FIRST.MV TO ESTD.LAST.MV
         ESTD.REC(A) = INSERT(ESTD.REC(A),1,REF.NO,0,ESTD.TEMP(A))
      NEXT A
      ESTD.QTY<1,REF.NO> = TEMP.QTY * 100
      ESTD.STD<1,REF.NO> = TEMP.STD * 100
      ESTD.GRP.QTY<1,REF.NO> = TEMP.GRP.QTY * 100
380 NEXT TDP
390 *
*   NEXT BPTR
   RETURN
END
