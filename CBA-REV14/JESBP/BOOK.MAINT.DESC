  SUBROUTINE BOOK.MAINT.DESC(ACTION,DESC.FLAG,LINE.COUNT,REF.NO,CURR.REF.NO,MAT MAST.REC)
*COPY>CPYLIB>SCOMMON1
*COPY>JES.CPYLIB>COM.BOOKING
**************************************************************************
*
* REVISION - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
*
* PROGRAM  - BOOK.MAINT.DESC
*
* AUTHOR   - WALID YAMOUT, COMPUTER BUSINESS ASSOCIATES
*
* DATE     - 04/23/86
*
* DESCRIPTION
*
* This program is a subroutine to maintain description
*
*T25978 adelgado 02/19/2002 * Add the use of prompts (S,SR,SB,ST).
**************************************************************************
*
*--- FILE COPY STATEMENTS
*
*COPY>JES.CPYLIB>ESTIMATE
*COPY>JES.CPYLIB>JES.FILE.VARS
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>CHAR
*COPY>CPYLIB>SYSCOM
  MAT SYSCOM.REC =  ""; SYS.TYPE = 2
  DIM MAST.REC(35)
  EQU MAST.EST        TO MAST.REC(1)
  EQU MAST.STATUS     TO MAST.REC(2)
  EQU MAST.EST.SUBS   TO MAST.REC(3)
  EQU MAST.JOB        TO MAST.REC(4)
  EQU MAST.JOB.SUBS   TO MAST.REC(5)
  EQU MAST.ESTIMATOR  TO MAST.REC(6)
  EQU MAST.CUST       TO MAST.REC(7)
  EQU MAST.SLSMN      TO MAST.REC(8)
  EQU MAST.DIV        TO MAST.REC(9)
  EQU MAST.ENTER.DATE TO MAST.REC(10)
  EQU MAST.DUE.DATE   TO MAST.REC(11)
  EQU MAST.MARKUP     TO MAST.REC(12)
  EQU MAST.QUOTED.AMT TO MAST.REC(13)
  EQU MAST.PRICE.THOU TO MAST.REC(14)
  EQU MAST.SALES.CODE TO MAST.REC(15)
  EQU MAST.TYPE       TO MAST.REC(16)
  EQU MAST.CATG       TO MAST.REC(17)
  EQU MAST.SHPV       TO MAST.REC(18)
  EQU MAST.CUST.PO    TO MAST.REC(19)
  EQU MAST.CREDIT     TO MAST.REC(20)
  EQU MAST.PRIOR.JOB  TO MAST.REC(21)
  EQU MAST.DESC       TO MAST.REC(22)
  EQU MAST.COMMENTS   TO MAST.REC(23)
  EQU MAST.BOOK.JOB   TO MAST.REC(24)
  EQU MAST.BOOK.QTY   TO MAST.REC(25)
  EQU MAST.BOOK.COMP  TO MAST.REC(26)
  EQU MAST.COLORS     TO MAST.REC(27)
  EQU MAST.EST.COST   TO MAST.REC(28)
  EQU MAST.EST.SALE   TO MAST.REC(29)
  EQU MAST.JOB.FLAG   TO MAST.REC(30)
*
*--- INITIALIZATION
*
  ERRMSG=""
  UNKNOWN=STR("?",30)
  TRUE=1 ; FALSE=0
  TODAY = DATE()
*
*--- MAIN PROCESSING
*
100*
  IF ACTION = "C" THEN
    IF DESC.FLAG # "C" THEN GOSUB 52000
    GOSUB 11700
    GOTO 99999
  END
  IF DESC.FLAG # "D" THEN GOSUB 51000
  CNT = COUNT(MAST.DESC,VM) + (MAST.DESC # "")
*
*--- GET OPERATOR REPLY
*
500*
  S$DATA(32)<S$SCR>=""
  SCREEN FIELD;BOOK.MAINT;32
  SCREEN INPUT;BOOK.MAINT;32
  OPT=S$VALUE
  BEGIN CASE
    CASE OPT="A" AND CNT < 999
      DONE=0
      FOR REF.NO=CNT+1 TO 999 UNTIL DONE
        GOSUB 81000
        GOSUB 11600
        IF S$VALUE="END" THEN
          DONE=1
          S$DATA(29)=DELETE(S$DATA(29),S$SCR,REF.NO,0)
          MAST.DESC=DELETE(MAST.DESC,1,REF.NO,0)
        END ELSE
          CNT=CNT+1
        END
      NEXT REF.NO
      REF.NO=CNT
      CURR.REF.NO=""
      GOSUB 81000
    CASE OPT="C" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO=S$VALUE
        GOSUB 11600
      END
    CASE OPT="D" AND CNT > 0
      GOSUB 30000
      IF S$VALUE # "" AND S$VALUE # "END" THEN
        REF.NO=S$VALUE
        S$DATA(29)=DELETE(S$DATA(29),S$SCR,REF.NO,0)
        MAST.DESC=DELETE(MAST.DESC,1,REF.NO,0)
        CNT=CNT-1
        IF REF.NO > CNT THEN REF.NO=REF.NO-1
        CURR.REF.NO=""
        GOSUB 81000
      END
    CASE OPT="S"
      REF.NO=CURR.REF.NO+LINE.COUNT
      IF REF.NO > CNT THEN REF.NO=1
      GOSUB 81000
    * T25978 v
    CASE OPT = 'SR'
      REF.NO = CURR.REF.NO - LINE.COUNT
      IF REF.NO < 1 THEN REF.NO = 1
      GOSUB 81000
    CASE OPT = 'ST'
      REF.NO = 1
      GOSUB 81000
    CASE OPT = 'SB'
      REF.NO = CNT
      GOSUB 81000
    * T25978 ^
    CASE OPT="" OR OPT="END" OR OPT="E"
      GOTO 99999
  END CASE
  GOTO 500
*
*-----------------------*
*---  SUBROUTINES   ---*
*-----------------------*
*
*--- DESCRIPTION
*
11600*
  S$VAL=REF.NO
  SCREEN DISPLAY;BOOK.MAINT;28
11610*
  S$VAL=REF.NO
  SCREEN FIELD;BOOK.MAINT;29
  SCREEN INPUT;BOOK.MAINT;29;RETURN
  BEGIN CASE
    CASE S$VALUE="" AND REF.NO > COUNT(MAST.DESC,VM)+(MAST.DESC # "")
      S$VALUE="END"
    CASE 1
      MAST.DESC<1,REF.NO>=S$VALUE
  END CASE
  RETURN
*
*--- INTERNAL COMMENTS
*
11700*
  REF.NO=1
  S$VAL=1
  SCREEN DISPLAY;BOOK.MAINT;28
  S$VAL=1
  SCREEN FIELD;BOOK.MAINT;29
  S$MAXL=63
  SCREEN INPUT;BOOK.MAINT;29;RETURN
  BEGIN CASE
    CASE S$VALUE = "" AND MAST.COMMENTS<1,2> = ""
      MAST.COMMENTS = ""
      GOTO 11799
    CASE S$VALUE = ""
      MAST.COMMENTS = DELETE(MAST.COMMENTS,1,1,0)
      S$DATA(29)<S$SCR> = MAST.COMMENTS
      CURR.REF.NO = ""
      GOSUB 81000
      GOTO 11700
  END CASE
  MAST.COMMENTS<1,1> = S$VALUE
  S$VAL=2
  SCREEN DISPLAY;BOOK.MAINT;28
  S$VAL=2
  SCREEN FIELD;BOOK.MAINT;29
  S$MAXL=63
  SCREEN INPUT;BOOK.MAINT;29;RETURN
  IF S$VALUE = "" THEN
    MAST.COMMENTS = DELETE(MAST.COMMENTS,1,2,0)
  END ELSE
    MAST.COMMENTS<1,2> = S$VALUE
  END
11799*
  CURR.REF.NO=""
  S$DATA(29)<S$SCR>=MAST.COMMENTS
  GOSUB 81000
  RETURN
*
*--- DUMMY
*
19990*
  RETURN
*
*--- GET LINE REFERENCE NUMBER
*
30000*
  S$DATA(37)<S$SCR>=""
  SCREEN FIELD;BOOK.MAINT;37
  S$MINV=CURR.REF.NO
  S$MAXV=S$MINV+LINE.COUNT-1
  IF S$MAXV > CNT THEN S$MAXV=CNT
  SCREEN INPUT;BOOK.MAINT;37
  RETURN
51000*
  S$DATA(27)="   Description"
  SCREEN DISPLAY;BOOK.MAINT;27
  S$DATA(29)<S$SCR>=MAST.DESC
  CURR.REF.NO=""
  REF.NO = 1
  GOSUB 81000
  DESC.FLAG = "D"
  RETURN
52000*
  S$DATA(27)<S$SCR>="Internal Comments"
  SCREEN DISPLAY;BOOK.MAINT;27
  S$DATA(29)<S$SCR>=MAST.COMMENTS
  CURR.REF.NO=""
  REF.NO = 1
  GOSUB 81000
  DESC.FLAG = "C"
  RETURN
*
*--- SCROLL INTERNAL COMMENTS
*
81000*
  START.REF.NO=1+INT((REF.NO-1)/LINE.COUNT)*LINE.COUNT
  IF START.REF.NO=CURR.REF.NO THEN RETURN
  CURR.REF.NO=START.REF.NO
  S$VAL=START.REF.NO
  S$CNT=COUNT(S$DATA(29)<S$SCR>,VM)+(S$DATA(29)<S$SCR> # "")
  SCREEN MULTI;BOOK.MAINT;C;28;29
  RETURN
*
*--- ERROR ROUTINE
*
90000 ERR.TYPE=1;CALL SI_SYSCOM(MAT SYSCOM.REC);RETURN
* 90000*
*    PRINT @(0,23):CL:ERRMSG:
*    INPUT REPLY:
*    PRINT @(0,23):CL:
*    RETURN
*
*--- END OF PROGRAM
*
99999*
  RETURN
END
