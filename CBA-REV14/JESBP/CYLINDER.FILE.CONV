*COPY>CPYLIB>SCOMMON1
*********************************************************************
* REVISION    - [08.0]
* Copyright 1982 by Computer Business Associates (Vercom Software, Inc.)
* SYSTEM      - PRIMAC
* SOURCE      - JESBP
* PROGRAM     - CYLINDER.FILE.CONV
* DESCRIPTION - Used to convert cylinder records from the old roll label
*               format to the new roll label format 
*               ESTIMATE.RL.CYLINDER ----> ESTIMATE.RES.CYLINDER
*
*********************************************************************
*COPY>JES.CPYLIB>ESTIMATE.RL.CYLINDER
*COPY>JES.CPYLIB>ESTIMATE.RL.DIE
*COPY>JES.CPYLIB>ESTIMATE.RES.GEARSET
*COPY>PMC.CPYLIB>COMPANY
*COPY>CPYLIB>SCREEN.COM
*COPY>CPYLIB>FILE.VARS
*COPY>CPYLIB>SYSCOM
*COPY>CPYLIB>CHAR
*
*---- SET SYSCOM
*
SYS.TYPE=1
CALL SI_SYSCOM(MAT SYSCOM.REC)
*
*---- OPEN FILES
*
OPEN 'ESTIMATE.RL.CYLINDER' TO ESTIMATE.RL.CYLINDER ELSE
  ERRMSG = 'CANNOT OPEN ESTIMATE.RL.CYLINDER FILE'
  GOTO 93000
END
OPEN 'ESTIMATE.RL.DIE' TO ESTIMATE.RL.DIE ELSE
  ERRMSG = 'CANNOT OPEN ESTIMATE.RL.DIE FILE'
  GOTO 93000
END
OPEN 'DICT','ESTIMATE.RL.DIE' TO D.EST.RL.DIE ELSE
  ERRMSG = 'CANNOT OPEN DICT ESTIMATE.RL.DIE FILE'
  GOTO 93000
END
OPEN 'ESTIMATE.RES.CYLINDER' TO ESTIMATE.RES.CYLINDER ELSE
  ERRMSG = 'CANNOT OPEN ESTIMATE.RES.CYLINDER FILE'
  GOTO 93000
END
OPEN 'ESTIMATE.RES.GEARSET' TO ESTIMATE.RES.GEARSET ELSE
  ERRMSG = 'CANNOT OPEN ESTIMATE.RES.GEARSET FILE'
  GOTO 93000
END
OPEN 'OLD.CYL.XREF' TO OLD.CYL.XREF ELSE
  ERRMSG = 'CANNOT OPEN OLD.CYL.XREF FILE'
  GOTO 93000
END
OPEN 'COMPANY' TO COMPANY ELSE
  ERRMSG = 'CANNOT OPEN COMPANY FILE'
  GOTO 93000
END
OPEN 'CONTROL' TO CONTROL ELSE
  ERRMSG = 'CANNOT OPEN CONTROL FILE'
  GOTO 93000
END
OPEN 'JES.SCREENS' TO JES.SCREENS ELSE
  ERRMSG = 'CANNOT OPEN JES.SCREENS FILE'
  GOTO 93000
END
READ REC FROM D.EST.RL.DIE, "CONO" ELSE
  ERRMSG = "Dictionary item CONO is missing from the ESTIMATE.RL.DIE file"
  GOTO 93000
END
READ REC FROM D.EST.RL.DIE, "RLDIE_CYLINDER" ELSE
  ERRMSG = "Dictionary item RLDIE_CYLINDER is missing from the ESTIMATE.RL.DIE file"
  GOTO 93000
END
*
*---- INITIALIZATION
*
CONO = ""
CALL GET.CONO1(CONO,MAT COMP.REC,COMPANY,CONTROL)
SCREEN INIT;#
S$SCR = 1
SCREEN DEFINE;CYLINDER.FILE.CONV;JES.SCREENS
SCREEN FORMAT
*
*---- MAIN PROCESSING
*
100 *
SCREEN FIELD;;1
SCREEN INPUT;;1
IF S$VALUE = 'END' THEN GOTO 99999
RL.ID = S$VALUE
MATREADU RLCYN.REC FROM ESTIMATE.RL.CYLINDER, CONO:RL.ID ELSE
  ERRMSG = "Cylinder does not exist"; GOSUB 91000
  RELEASE
  GOTO 100
END
GOSUB 80000
GOSUB 10100
IF S$VALUE = "END" THEN
  RELEASE
  GOTO 100
END
*
*---- ENTER PROMPT
*
500 *
SCREEN FIELD;;21
SCREEN INPUT;;21
OPTION = S$VALUE
BEGIN CASE
CASE NUM(OPTION)
  ON OPTION GOSUB 10100
CASE OPTION = 'E' OR OPTION = 'END'
  SCREEN CLEAR
  RELEASE
  GOTO 100
CASE OPTION = 'F'
  WRITE RL.ID ON OLD.CYL.XREF, CONO:RES.ID
  SENTENCE = 'SELECT ESTIMATE.RL.DIE WITH CONO = "':CONO:'" AND WITH RLDIE_CYLINDER = "':RL.ID:'"'
  PERFORM SENTENCE CAPTURING MSG
  DATA = 1
  LOOP
    READNEXT ID ELSE DATA = 0
  WHILE DATA DO
    MATREADU RLDIE.REC FROM ESTIMATE.RL.DIE, ID THEN
      RLDIE.CYLINDER = RES.ID
      MATWRITE RLDIE.REC ON ESTIMATE.RL.DIE, ID
    END ELSE
      RELEASE ESTIMATE.RL.DIE, ID
    END
  REPEAT
  MATREADU ESTGS.REC FROM ESTIMATE.RES.GEARSET, CONO:RES.ID[1,2] THEN
    LOCATE RES.ID IN ESTGS.CYLIDS<1>,1 SETTING FND ELSE
      ESTGS.CYLIDS<1,FND> = RES.ID
    END
    MATWRITE ESTGS.REC ON ESTIMATE.RES.GEARSET, CONO:RES.ID[1,2]
  END ELSE
    RELEASE ESTIMATE.RES.GEARSET, CONO:RES.ID[1,2]
  END
  RLCYN.DESC = ""
  RLCYN.CCTR = ""
  MATWRITE RLCYN.REC ON ESTIMATE.RES.CYLINDER, CONO:RES.ID
  DELETE ESTIMATE.RL.CYLINDER, CONO:RL.ID
  SCREEN CLEAR
  RELEASE
  GOTO 100
END CASE
GOTO 500
*
*---- CYLINDER
*
10100 *
SCREEN FIELD;;3
SCREEN INPUT;;3;RETURN
MATREAD ESTGS.REC FROM ESTIMATE.RES.GEARSET, CONO:S$VALUE ELSE
  ERRMSG = "Invaild gearset code"; GOSUB 91000
  GOTO 10100
END
S$DATA(2)<S$SCR> = S$VALUE:RLCYN.TEETH
SCREEN DISPLAY;;2
READ REC FROM ESTIMATE.RES.CYLINDER, CONO:S$VALUE:RLCYN.TEETH THEN
  ERRMSG = "New Cylinder ID is already on file"; GOSUB 91000
  GOTO 10100
END
RLCYN.REPEAT=RLCYN.TEETH*ESTGS.PITCH
S$DATA(5)<S$SCR> = RLCYN.REPEAT
SCREEN DISPLAY;;5
RES.ID = S$VALUE:RLCYN.TEETH
RETURN
*
*---- LOAD SCREEN
*
80000 *
S$DATA(4)<S$SCR> = RLCYN.TEETH
S$DATA(5)<S$SCR> = RLCYN.REPEAT
S$DATA(6)<S$SCR> = RLCYN.QUANTITY
S$DATA(8)<S$SCR> = RLCYN.LOC
S$DATA(9)<S$SCR> = RLCYN.TYPE
S$DATA(10)<S$SCR> = RLCYN.PERF.INCH
SCREEN DISPLAY;;ALL
RETURN
*
*---- SYSCOM MESSAGES
*
91000 *
ERR.TYPE=1
CALL SI_SYSCOM(MAT SYSCOM.REC)
RETURN
92000 *
ERR.TYPE=2
CALL SI_SYSCOM(MAT SYSCOM.REC)
RETURN
93000 *
ERR.TYPE=3
CALL SI_SYSCOM(MAT SYSCOM.REC)
99999 *
* PRINT @(-1)
END
